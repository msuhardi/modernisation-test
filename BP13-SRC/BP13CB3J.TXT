       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB3J.                                                 
       AUTHOR.        SANDEEP REDDY.                                            
      *DATE-WRITTEN.  26/11/2019.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13) TO GET THE GRANT USE AMOUNT     *           
      * =========================================================== *           
      *   OBJECTIVE   : CREATE FILE WITH BELOW INFORMATION FROM     *           
      *                 GRANT_ASMNT_PERSON TABLE.                   *           
      *                 * NRIC                                      *           
      *                 * GRANT TYPE                                *           
      *                 * GRANT AMOUNT                              *           
      *                 * APPROVED DATE                             *           
      *                                                             *           
      *   OUTPUT FILE :                                             *           
      *   1. BP13GRNT - GRANT DETAILS.                              *           
      *   2. BP13GRNR - GRANT DETAILS WITH REGNO.                   *           
      *   3. BP13GERR - ERROR RECORDS                                           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP138048 27/11/2019 SRG1 NEW PROGRAM                        *           
      * BP138238 16/03/2020 PP11 CATER TO CHANGE THE FILE LAYOUT AND*           
      *                          TO GET THE HOUSING GRANT DETAILS   *           
      *                          < '31-12-2019'                     *           
      * BP138252 11/05/2020 PP11 CATER TO CHANGE THE FILE LAYOUT AND*           
      *                          TO GET THE HOUSING GRANT DETAILS   *           
      *                          TILL PREVIOUS MONTH END DATA       *           
      * BP138252 20/05/2020 PP11 CATER TO OMIT RESALE CASES IN THE  *           
      *                          REPORT                             *           
      * BP138358 13/07/2020 PP11 CATER TO INCLUDE RESALE CASES IN   *           
      *                          THE REPORT                         *           
      * =========================================================== *           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
           SELECT BP13GRNT ASSIGN           TO BP13GRNT.                        
           SELECT BP13GRNR ASSIGN           TO BP13GRNR.                        
           SELECT BP13GERR ASSIGN           TO BP13GERR.                        
           SELECT BP13K800  ASSIGN         TO  BP13K800                         
                            ORGANIZATION   IS  INDEXED                          
                            ACCESS MODE    IS  RANDOM                           
                            RECORD KEY     IS  K800-NUM-REGN                    
                            FILE STATUS    IS  WS-K800-STATUS.                  
           SELECT BP13K893  ASSIGN         TO  BP13K893                         
                            ORGANIZATION   IS  INDEXED                          
                            ACCESS MODE    IS  DYNAMIC                          
                            RECORD KEY     IS  K893-KEY-FLD                     
                            FILE STATUS    IS  WS-K893-STATUS.                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
       FD  BP13GRNT                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13GRNT-REC.                                                        
           05 WS-NUM-NRIC                   PIC X(09).                          
           05 WS-NUM-GRANT-TYPE             PIC X(06).                          
           05 WS-AMT-GRANT                  PIC 9(6).9(2).                      
           05 WS-DTE-APPROVAL               PIC X(10).                          
           05 FILLER                        PIC X(66).                          
                                                                                
       FD  BP13GRNR                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13GRNR-REC.                                                        
           05 WS-NUM-REGNOR                 PIC X(09).                          
           05 FILLER                        PIC X(01).                          
           05 WS-NUM-NRICR                  PIC X(09).                          
           05 FILLER                        PIC X(01).                          
           05 WS-NUM-GRANT-TYPER            PIC X(06).                          
           05 FILLER                        PIC X(01).                          
           05 WS-AMT-GRANTR                 PIC 9(5).9(2).                      
           05 FILLER                        PIC X(01).                          
           05 WS-DTE-APPROVALR              PIC X(10).                          
           05 FILLER                        PIC X(54).                          
                                                                                
       FD  BP13GERR                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13GERR-REC                     PIC X(100).                         
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
                                                                                
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K893                                                             
           RECORD CONTAINS 2050 CHARACTERS.                                     
                                                                                
       COPY BP13K893.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-WORK-VARIABLES.                                                   
           05 WS-DATE                       PIC X(08) VALUE SPACES.             
           05 CNT-READ-PERSN                PIC 9(08) VALUE ZEROES.             
           05 CNT-WRITE-GRNT                PIC 9(08) VALUE ZEROES.             
           05 CNT-WRITE-GRNR                PIC 9(08) VALUE ZEROES.             
           05 CNT-WRITE-GERR                PIC 9(08) VALUE ZEROES.             
           05 CNT-REC-SKIP                  PIC 9(08) VALUE ZEROES.             
           05 CNT-REC-NFND                  PIC 9(08) VALUE ZEROES.             
           05 CNT-NTFND-K800                PIC 9(08) VALUE ZEROES.             
           05 WS-CURSOR-END                 PIC X     VALUE 'N'.                
           05 WS-REGNO                      PIC X(08) VALUE SPACES.             
           05 WS-NUM-REGN                   PIC X(08) VALUE SPACES.             
                                                                                
           05  WS-K800-STATUS         PIC  9(02).                               
               88 WS-K800-OK                     VALUE 00.                      
               88 WS-K800-EOF                    VALUE 10.                      
               88 WS-K800-INVALID-KEY            VALUE 23.                      
           05  WS-K893-STATUS         PIC  9(02).                               
               88 WS-K893-OK                     VALUE 00.                      
               88 WS-K893-EOF                    VALUE 10.                      
               88 WS-K893-INVALID-KEY            VALUE 23.                      
           05  WS-K800-KEY-STATUS     PIC  X(01) VALUE 'N'.                     
               88 WS-K800-FND                    VALUE 'Y'.                     
               88 WS-K800-NOT-FND                VALUE 'N'.                     
           05  WS-K893-KEY-STATUS     PIC  X(01) VALUE 'N'.                     
               88 WS-K893-FND                    VALUE 'Y'.                     
               88 WS-K893-NOT-FND                VALUE 'N'.                     
           05  WS-PRCS-REPORT         PIC  X(1)  VALUE 'N'.                     
               88 WS-NTPRCS-RPT                  VALUE 'N'.                     
               88 WS-PRCS-RPT                    VALUE 'Y'.                     
                                                                                
       01  WS-TIMESTAMP                     PIC X(26) VALUE SPACES.             
       01  WS-DTE-CCYYMMDD.                                                     
           05 WS-DTE-CCYY                   PIC X(04) VALUE SPACES.             
           05 WS-DTE-MM                     PIC X(02) VALUE SPACES.             
           05 WS-DTE-DD                     PIC X(02) VALUE SPACES.             
                                                                                
       01  WS-FMT-CCYYMM.                                                       
           05 WS-FMT-CCYY                   PIC X(04) VALUE SPACES.             
           05 FILLER                        PIC X(01) VALUE '-'.                
           05 WS-FMT-MM                     PIC X(02) VALUE SPACES.             
                                                                                
       01  WS-TIME-HHMMSS.                                                      
           05 WS-TIME-HH                    PIC X(02) VALUE SPACES.             
           05 WS-TIME-MM                    PIC X(02) VALUE SPACES.             
           05 WS-TIME-SS                    PIC X(02) VALUE SPACES.             
           05 WS-TIME-MS                    PIC X(02) VALUE SPACES.             
                                                                                
       01  WS-SQLCODE                       PIC ---9.                           
       01  WS-SQL-CODES.                                                        
           05  WS-SQL-OK                    PIC S9(4) COMP VALUE +000.          
           05  WS-SQL-NFND                  PIC S9(4) COMP VALUE +100.          
           05  WS-SQL-MORE-ROWS             PIC S9(4) COMP VALUE -811.          
                                                                                
             EXEC SQL INCLUDE  SQLCA    END-EXEC.                               
             EXEC SQL INCLUDE  P13VGAPN END-EXEC.                               
             EXEC SQL INCLUDE  P13VGASM END-EXEC.                               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *------------*                                                            
       0000-CONTROL.                                                            
      *------------*                                                            
           MOVE FUNCTION CURRENT-DATE       TO WS-DATE.                         
           ACCEPT WS-DTE-CCYYMMDD           FROM DATE YYYYMMDD.                 
           ACCEPT WS-TIME-HHMMSS            FROM TIME.                          
           MOVE WS-DTE-CCYY                 TO WS-FMT-CCYY                      
           MOVE WS-DTE-MM                   TO WS-FMT-MM                        
                                                                                
           OPEN OUTPUT BP13GRNT                                                 
                       BP13GERR                                                 
                       BP13GRNR                                                 
                INPUT  BP13K800                                                 
                       BP13K893.                                                
                                                                                
            IF WS-K800-STATUS  = 00                                             
               CONTINUE                                                         
            ELSE                                                                
               DISPLAY 'FILE OPEN ERROR BP13K800'                               
               MOVE WS-K800-STATUS      TO  RETURN-CODE                         
               DISPLAY 'RETURN CODE: '      RETURN-CODE                         
               PERFORM 9999-SQL-ERROR       THRU 9999-EXIT                      
            END-IF.                                                             
                                                                                
            IF WS-K893-STATUS = 00                                              
               CONTINUE                                                         
            ELSE                                                                
               DISPLAY 'FILE OPEN ERROR BP13K893'                               
               MOVE WS-K893-STATUS      TO  RETURN-CODE                         
               DISPLAY 'RETURN CODE: '      RETURN-CODE                         
               PERFORM 9999-SQL-ERROR       THRU 9999-EXIT                      
            END-IF.                                                             
                                                                                
           PERFORM 1000-PROCESS-GRNT-PERSON THRU 1000-EXIT.                     
           PERFORM 9000-WRAP-UP             THRU 9000-EXIT.                     
                                                                                
           STOP RUN.                                                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------*                                                
       1000-PROCESS-GRNT-PERSON.                                                
      *------------------------*                                                
           PERFORM 1100-OPEN-CURSOR         THRU 1100-EXIT.                     
           PERFORM 1200-FETCH-CURSOR        THRU 1200-EXIT                      
             UNTIL WS-CURSOR-END = 'Y'                                          
           PERFORM 1300-CLOSE-CURSOR        THRU 1300-EXIT.                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1100-OPEN-CURSOR.                                                        
      *----------------*                                                        
           INITIALIZE SQLCODE.                                                  
           MOVE 'N'                         TO WS-CURSOR-END.                   
      *--------------------------------------------*                            
      *    DECLARE CURSOR - GRANT ASSESSMENT PERSON                             
      *--------------------------------------------*                            
           EXEC SQL                                                             
                DECLARE CURSOR CURSOR FOR                                       
                 SELECT NUM_RGSTRN,                                             
                        NUM_IDNTY_NO,                                           
                        NUM_GRANT_TYPE,                                         
                        AMT_GRANT                                               
                   FROM GRANT_ASMNT_PERSON                                      
                  WHERE NUM_GRANT_TYPE IN ('AHG','EHG','FSG','HGT',             
                        'PHG','REG','SHG','SRG','SUG','TUG')                    
                    AND AMT_GRANT > 0                                           
                 ORDER BY NUM_GRANT_TYPE, DTE_UPDATE, NUM_IDNTY_NO              
           END-EXEC.                                                            
                                                                                
      *-----------------------------------------*                               
      *    OPEN CURSOR - GRANT ASSESSMENT PERSON                                
      *-----------------------------------------*                               
           EXEC SQL                                                             
                OPEN CURSOR                                                     
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE SQLCODE            TO WS-SQLCODE                       
                    DISPLAY 'OPEN CURSOR ERROR'                                 
                    GO TO 9999-SQL-ERROR                                        
           END-EVALUATE.                                                        
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------*                                                       
       1200-FETCH-CURSOR.                                                       
      *-----------------*                                                       
           EXEC SQL                                                             
                FETCH CURSOR                                                    
                 INTO :DCLGRANT-ASMNT-PERSON.NUM-RGSTRN,                        
                      :DCLGRANT-ASMNT-PERSON.NUM-IDNTY-NO,                      
                      :DCLGRANT-ASMNT-PERSON.NUM-GRANT-TYPE,                    
                      :DCLGRANT-ASMNT-PERSON.AMT-GRANT                          
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN WS-SQL-OK                                                   
                 MOVE NUM-RGSTRN OF DCLGRANT-ASMNT-PERSON                       
                                   TO WS-NUM-REGN                               
                 ADD 1                   TO CNT-READ-PERSN                      
                 PERFORM  2000-READ-K800   THRU 2000-EXIT                       
                 IF WS-PRCS-REPORT = 'Y'                                        
                    PERFORM 4000-PROCESS-REPORT  THRU 4000-EXIT                 
                 END-IF                                                         
                                                                                
               WHEN WS-SQL-NFND                                                 
                    MOVE 'Y'                TO WS-CURSOR-END                    
               WHEN OTHER                                                       
                    MOVE SQLCODE            TO WS-SQLCODE                       
                    DISPLAY 'FETCH CURSOR ERROR'                                
                    GO TO 9999-SQL-ERROR                                        
           END-EVALUATE.                                                        
                                                                                
       1200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------*                                                       
       1300-CLOSE-CURSOR.                                                       
      *-----------------*                                                       
           INITIALIZE SQLCODE.                                                  
      *-----------------------------------*                                     
      *    CLOSE CURSOR - GRANT ASSESSMENT                                      
      *-----------------------------------*                                     
           EXEC SQL                                                             
                CLOSE CURSOR                                                    
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    MOVE SQLCODE            TO WS-SQLCODE                       
                    DISPLAY 'CLOSE CURSOR ERROR'                                
                    GO TO 9999-SQL-ERROR                                        
           END-EVALUATE.                                                        
                                                                                
       1300-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-K800.                                                          
      *----------------------------------------------------------------*        
      * READ BP13K800                                                           
      *----------------------------------------------------------------*        
                                                                                
           MOVE SPACES             TO BP13K800-MASTER.                          
           INITIALIZE                 BP13K800-MASTER.                          
           MOVE NUM-RGSTRN OF DCLGRANT-ASMNT-PERSON                             
                                   TO K800-NUM-REGN.                            
                                                                                
           READ BP13K800 KEY IS K800-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    IF K800-NUM-STATUS = 'C' AND                                
                       K800-DTE-CANCEL NOT = ZEROES AND SPACES                  
                                                    AND LOW-VALUES              
                        SET WS-NTPRCS-RPT  TO TRUE                              
                        ADD  1           TO CNT-REC-SKIP                        
                    ELSE                                                        
                        SET WS-PRCS-RPT  TO TRUE                                
                    END-IF                                                      
                                                                                
               WHEN 23                                                          
                    ADD  1               TO CNT-NTFND-K800                      
                    PERFORM 3000-START-BP13K893 THRU 3000-EXIT                  
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K800 : ' WS-K800-STATUS          
                            ' REGN = ' K800-NUM-REGN                            
                    MOVE     WS-K800-STATUS    TO    RETURN-CODE                
                    PERFORM  9999-SQL-ERROR         THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------------*        
       3000-START-BP13K893.                                                     
      *----------------------------------------------------------------*        
                                                                                
           MOVE SPACES                           TO BP13K893-MASTER.            
           INITIALIZE                               BP13K893-MASTER.            
                                                                                
           MOVE SPACES                           TO K893-KEY-FLD.               
           MOVE NUM-RGSTRN OF DCLGRANT-ASMNT-PERSON                             
                                                 TO K893-NUM-REGN-HIST.         
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K893-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3150-READ-BP13K893    THRU 3150-EXIT                
               WHEN 23                                                          
                    SET WS-PRCS-RPT  TO TRUE                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K893: ' WS-K893-STATUS          
                            ' REGN = ' K893-NUM-REGN-HIST                       
                    MOVE     WS-K893-STATUS          TO RETURN-CODE             
                    PERFORM  9999-SQL-ERROR         THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
      *----------------------------------------------------------------*        
       3150-READ-BP13K893.                                                      
      *----------------------------------------------------------------*        
                                                                                
            READ  BP13K893 NEXT AT END                                          
                  MOVE SPACES               TO K893-KEY-FLD.                    
                                                                                
            EVALUATE WS-K893-STATUS                                             
                WHEN 00                                                         
                WHEN 02                                                         
                     IF NUM-RGSTRN OF DCLGRANT-ASMNT-PERSON =                   
                        K893-NUM-REGN-HIST                                      
                      IF K893-NUM-STATUS = 'C' AND                              
                         K893-DTE-CANCEL NOT = ZEROES AND SPACES                
                                                      AND LOW-VALUES            
                          SET WS-NTPRCS-RPT  TO TRUE                            
                          ADD  1           TO CNT-REC-SKIP                      
                      ELSE                                                      
                          SET WS-PRCS-RPT  TO TRUE                              
                      END-IF                                                    
                     ELSE                                                       
                      SET WS-PRCS-RPT  TO TRUE                                  
                     END-IF                                                     
                WHEN 23                                                         
                     SET WS-PRCS-RPT  TO TRUE                                   
                     ADD  1           TO CNT-REC-NFND                           
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K893 : ' WS-K893-STATUS         
                             ' REGN = ' K893-NUM-REGN-HIST                      
                     MOVE     WS-K893-STATUS    TO    RETURN-CODE               
                     PERFORM  9999-SQL-ERROR         THRU 9999-EXIT             
            END-EVALUATE.                                                       
                                                                                
       3150-EXIT.                                                               
           EXIT.                                                                
      *----------------------------------------------------------------*        
       4000-PROCESS-REPORT.                                                     
      *----------------------------------------------------------------*        
                                                                                
            INITIALIZE BP13GRNT-REC.                                            
            MOVE NUM-RGSTRN OF DCLGRANT-ASMNT-PERSON TO WS-REGNO                
                                                        WS-NUM-REGNOR.          
            MOVE NUM-IDNTY-NO OF DCLGRANT-ASMNT-PERSON                          
                                               TO WS-NUM-NRIC                   
                                                  WS-NUM-NRICR.                 
            MOVE NUM-GRANT-TYPE OF DCLGRANT-ASMNT-PERSON                        
                                               TO WS-NUM-GRANT-TYPE             
                                                  WS-NUM-GRANT-TYPER.           
            MOVE AMT-GRANT                     TO WS-AMT-GRANT                  
                                                  WS-AMT-GRANTR.                
            IF WS-REGNO(1:1) NOT = '6'                                          
                                                                                
              PERFORM 4500-FETCH-GRASMNT  THRU 4500-EXIT                        
                                                                                
              IF WS-NUM-NRIC NOT = SPACES AND LOW-VALUES                        
              AND WS-NUM-GRANT-TYPE NOT = SPACES AND LOW-VALUES                 
              AND WS-DTE-APPROVAL NOT = SPACES AND LOW-VALUES                   
              AND WS-DTE-APPROVAL(1:7) < WS-FMT-CCYYMM                          
              AND WS-DTE-APPROVAL >  '0001-01-01'                               
                  WRITE BP13GRNT-REC                                            
                  ADD 1               TO CNT-WRITE-GRNT                         
                  WRITE BP13GRNR-REC                                            
                  ADD 1               TO CNT-WRITE-GRNR                         
              ELSE                                                              
                  WRITE BP13GERR-REC FROM BP13GRNT-REC                          
                  ADD 1               TO CNT-WRITE-GERR                         
              END-IF                                                            
            END-IF                                                              
           .                                                                    
       4000-EXIT.                                                               
           EXIT.                                                                
      *----------------------------------------------------------------*        
       4500-FETCH-GRASMNT.                                                      
      *----------------------------------------------------------------*        
                                                                                
           EXEC SQL                                                             
              SELECT DTE_APRV                                                   
              INTO   :DCLGRANT-ASMNT.DTE-APRV                                   
              FROM                                                              
                GRANT_ASMNT                                                     
              WHERE                                                             
                    NUM_RGSTRN        = :DCLGRANT-ASMNT-PERSON.                 
                                                  NUM-RGSTRN                    
                AND NUM_GRANT_TYPE    = :DCLGRANT-ASMNT-PERSON.                 
                                                NUM-GRANT-TYPE                  
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN WS-SQL-OK                                                       
           WHEN WS-SQL-MORE-ROWS                                                
                MOVE  DTE-APRV   TO WS-DTE-APPROVAL                             
                                    WS-DTE-APPROVALR                            
           WHEN WS-SQL-NFND                                                     
                DISPLAY 'REGNO NOT FOUND IN GRASMNT:' NUM-RGSTRN OF             
                                                  DCLGRANT-ASMNT-PERSON         
           WHEN OTHER                                                           
                MOVE SQLCODE   TO WS-SQLCODE                                    
                DISPLAY 'ERROR READING GRANT_ASMNT'                             
                DISPLAY 'SQLCODE IS : ' WS-SQLCODE                              
                DISPLAY SPACES                                                  
                PERFORM 9999-SQL-ERROR      THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
      *------------*                                                            
       9000-WRAP-UP.                                                            
      *------------*                                                            
           CLOSE BP13GRNT                                                       
                 BP13GERR                                                       
                 BP13GRNR                                                       
                 BP13K800                                                       
                 BP13K893.                                                      
                                                                                
           DISPLAY '***********************************************'            
           DISPLAY '***      END OF THE PROGRAM BP13CB3J        ***'.           
           DISPLAY SPACES.                                                      
           DISPLAY 'RECORDS FETCH FROM GRANT ASMT PERSON : '                    
                   CNT-READ-PERSN.                                              
           DISPLAY 'RECORDS WRITE INTO OUTPUT FILE       : '                    
                   CNT-WRITE-GRNT.                                              
           DISPLAY 'RECORDS WRITE INTO OUTPUT FILE REFR  : '                    
                   CNT-WRITE-GRNR.                                              
           DISPLAY 'RECORDS WITH INVALID DATA            : '                    
                   CNT-WRITE-GERR.                                              
           DISPLAY 'NO OF RECORD SKIPPED                 : '                    
                   CNT-REC-SKIP.                                                
           DISPLAY 'NO OF RECORD NOT FOUND IN K893       : '                    
                   CNT-REC-NFND.                                                
           DISPLAY 'NO OF RECORD NOT FOUND IN K800       : '                    
                   CNT-NTFND-K800.                                              
           DISPLAY SPACES.                                                      
           DISPLAY '***********************************************'.           
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------*                                                          
       9999-SQL-ERROR.                                                          
      *--------------*                                                          
           CALL 'DBATIAR' USING SQLCA.                                          
           PERFORM 9000-WRAP-UP   THRU 9000-EXIT                                
           MOVE 12 TO RETURN-CODE                                               
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
