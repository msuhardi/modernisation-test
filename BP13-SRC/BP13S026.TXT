  /*-----------------------------------------------------------------*/         
  /* SYSTEM OF COMMITMENT - (BP13)                                   */         
  /*-----------------------------------------------------------------*/         
  /*-----------------------------------------------------------------*/         
  /* DATE        : SEP 26 2007                                       */         
  /* AUTHOR      : BALA AMUTHAN                                      */         
  /* PROGRAM TO PRINT ALLOCATION MODE OF TAKEN POSSESSION CASES      */         
  /*-----------------------------------------------------------------*/         
  /*LOCAL FILE VARIABLE                                              */         
  /*      TIMER_MCPS = "FMCPS"  - MCPS FIRST-TIMER                   */         
  /*      TIMER_MCPS = "SMCPS"  - MCPS SECOND-TIMER                  */         
  /*      TIMER_MCPS = "FMCPSN" - FIRST-TIMER                        */         
  /*      TIMER_MCPS = "SMCPSN" - SECOND-TIMER                       */         
  /*-----------------------------------------------------------------*/         
  /* CHG-NO   BY  ON           DESCRIPTION                           */         
  /* -------- --  --           ------------                          */         
  /* BP133142 BA2 26/09/2007   NEW PRG.                              */         
  /* BP133292 IL5 25/01/2008   TO SORT REPORT BY NT AND FT           */         
  /* BP133362 IL5 29/06/2008   TO REVISE CATEGORISATION FOR ATMP     */         
  /* BP133501 IL5 15/01/2009   TO REPORT ON BALLOT HOUSEHOLD INSTEAD */         
  /*                           OF NUM-HOUSEHOLD                                 
  /* BP133511 IL5 18/02/2009   TO CHANGE GROUPING FOR UNSUCC ATTEMPTS*/         
  /* BP133604                                                         /         
  /* BP133636 ZDD 23/07/2009   ADD P13L026B REPLACE WAIT_TIME  TO     /         
  /*                           WAIT_TIME1 (WAIT_TIME - 1)            /          
  /* BP133913 ZDD 03/06/2010   ADD >300% WHEN REQUEST STAT = 'U'      /         
  /* BP136096 SMR 15/01/2016   CATER FOR BHH = F/N                    /         
  /*-----------------------------------------------------------------*/         
                                                                                
  %GLOBAL DATE;                                                                 
  %GLOBAL TT_HEAD;                                                              
                                                                                
   PROC FORMAT;                                                                 
        VALUE  $CAT_FMT "1"="CAT1"                                              
                        "2"="CAT2"                                              
                        "3"="CAT 3 & 4";                                        
                                                                                
        VALUE  $MCPS_FMT "FMCPS "="MCPS FIRST-TIMER"                            
                         "SMCPS "="MCPS SECOND-TIMER"                           
                         "FMCPSN"="FIRST-TIMER"                                 
                         "SMCPSN"="SECOND-TIMER";                               
                                                                                
        VALUE   TIME_FMT  0="0"                                                 
                          1="1"                                                 
                          2="2"                                                 
                          3="3"                                                 
                          4="4"                                                 
                          5="5"                                                 
                          6="6"                                                 
                          7="7"                                                 
                          8=">7";                                               
                                                                                
                                                                                
   DATA BP13F730;                                                               
        LENGTH WAIT_TIME1 2.;                                                   
        LENGTH TIMER_MCPS $6.;                                                  
        INFILE BP13F730;                                                        
        INPUT @80   CDE_COHORT               2.                                 
              @114  WAIT_TIME                2.                                 
              @461  TIMER_TAG                $CHAR1.                            
              @118  MCPS_TAG                 $CHAR1.                            
              @187  NEWTOWN                  $CHAR3.                            
              @211  FT                       $CHAR2.                            
              @225  CATEGORY                 $CHAR1.                            
              @253  RESIDENT_HA1             $CHAR1.                            
              @260  REQUEST_STATUS           $CHAR1.;                           
                                                                                
  /*-------------CHECKING FOR %--------------------------------------*/         
              IF REQUEST_STATUS = 'S' THEN                                      
                 NUM_PER = "100% ";                                             
              ELSE                                                              
              IF REQUEST_STATUS = 'R' THEN                                      
                 NUM_PER = "200% ";                                             
              ELSE                                                              
              IF REQUEST_STATUS = 'W' THEN                                      
                 NUM_PER = "300% ";                                             
              ELSE                                                              
              IF REQUEST_STATUS = 'U' THEN                                      
                 NUM_PER = ">300%";                                             
                                                                                
                                                                                
  /*-------------CHECKING FOR MCPS TIMERS----------------------------*/         
              IF (MCPS_TAG ^= ' ') AND (TIMER_TAG = 'H' OR                      
                  TIMER_TAG = 'T' OR TIMER_TAG = 'F') AND                       
                 (RESIDENT_HA1 ^= 'H' AND                                       
                  RESIDENT_HA1 ^= 'P' AND RESIDENT_HA1 ^= 'M') THEN             
                 TIMER_MCPS = 'FMCPS ';                                         
              ELSE                                                              
              IF (MCPS_TAG ^= ' ') AND (TIMER_TAG = 'G' OR                      
                  TIMER_TAG = 'N') AND                                          
                 (RESIDENT_HA1 ^= 'H' AND RESIDENT_HA1 ^= 'P' AND               
                  RESIDENT_HA1 ^= 'M') THEN                                     
                 TIMER_MCPS = 'SMCPS ';                                         
              ELSE                                                              
              IF TIMER_TAG = 'H' OR TIMER_TAG = 'T' OR                          
                 TIMER_TAG = 'F' THEN                                           
                 TIMER_MCPS = 'FMCPSN';                                         
              ELSE                                                              
              IF TIMER_TAG = 'G' OR TIMER_TAG = 'N' THEN                        
                 TIMER_MCPS = 'SMCPSN';                                         
                                                                                
  /*-------------CHECKING FOR CATEGORY-------------------------------*/         
              IF CATEGORY = '4' THEN                                            
                DO;                                                             
                   CATEGORY = '3';                                              
                END;                                                            
                                                                                
  /*-------------CHECKING FOR WAITING TIME---------------------------*/         
              IF WAIT_TIME > 7 THEN                                             
                DO;                                                             
                  WAIT_TIME = 8;                                                
                END;                                                            
                                                                                
          IF WAIT_TIME >=2 THEN                                                 
          DO;                                                                   
             WAIT_TIME1 = WAIT_TIME - 1;                                        
          END;                                                                  
                                                                                
        IF _N_ =1 THEN                                                          
          DO;                                                                   
             LENGTH WS_TT_HEAD $20.;                                            
                                                                                
             DATE = PUT(TODAY(),DDMMYY8.);                                      
             CALL SYMPUT('DATE',DATE);                                          
                                                                                
                                                                                
  /*-------------CHECKING FOR SUB HEADING TOTAL,SUCCESS & UNSUCCESS--*/         
             IF REQUEST_STATUS ^= ' ' THEN                                      
                WS_TT_HEAD='SUCCESSFUL CASES  ';                                
             ELSE                                                               
                WS_TT_HEAD='TOTAL REQUEST     ';                                
                                                                                
             CALL SYMPUT('TT_HEAD',WS_TT_HEAD);                                 
                                                                                
         END;                                                                   
                                                                                
   DATA BP13FOUT;                                                               
        SET BP13F730;                                                           
                     WHERE TIMER_MCPS = "FMCPS " OR                             
                           TIMER_MCPS = "SMCPS " OR                             
                           TIMER_MCPS = "FMCPSN" OR                             
                           TIMER_MCPS = "SMCPSN";                               
                                                                                
   PROC SORT;                                                                   
        BY NEWTOWN FT NUM_PER WAIT_TIME TIMER_MCPS CATEGORY;                    
                                                                                
  /*-------------PRINTING TABULATE REPORT FOR L026A------------------*/         
     OPTIONS MISSING=0 PAGENO=1 PAGESIZE=55 LS=132 NODATE;                      
     PROC PRINTTO FILE=P13L026A NEW;                                            
                                                                                
     PROC TABULATE DATA=BP13FOUT MISSING FORMAT=5.;                             
                                                                                
 TITLE1 "P13L026A  HDB3               S Y S T E M    O F   C O M M I T M E N T  
                   DATE : &DATE       PAGE :    ";                              
 TITLE2 "                        ATTEMPT BY MCPS / HOUSEHOLD / CATEGORY &TT_HEAD
                                                ";                              
 TITLE3 "        ";                                                             
                                                                                
  CLASS NEWTOWN FT NUM_PER WAIT_TIME TIMER_MCPS CATEGORY;                       
                                                                                
  FORMAT CATEGORY   $CAT_FMT.                                                   
         TIMER_MCPS $MCPS_FMT.                                                  
         WAIT_TIME   TIME_FMT.;                                                 
                                                                                
  KEYLABEL N=" " ALL="TOTAL";                                                   
                                                                                
  TABLE ((NEWTOWN="")*(FT="")*(NUM_PER="")*(WAIT_TIME=' ' ALL='TOTAL')          
         ALL='GRAND TOTAL'),                                                    
        (TIMER_MCPS ='')*(CATEGORY=' ' ALL='SUB-TOTAL')                         
        N='GRAND TOTAL'                                                         
        /RTS=26  BOX='NT    FT    %     ATMP';                                  
                                                                                
                                                                                
  RUN;                                                                          
                                                                                
                                                                                
   DATA P13FOUTA;                                                               
        SET BP13F730;                                                           
                     WHERE TIMER_MCPS = "FMCPS " OR                             
                           TIMER_MCPS = "SMCPS " OR                             
                           TIMER_MCPS = "FMCPSN" OR                             
                           TIMER_MCPS = "SMCPSN" AND                            
                            WAIT_TIME1 >= 1;                                    
   PROC SORT;                                                                   
        BY NEWTOWN FT NUM_PER WAIT_TIME1 TIMER_MCPS CATEGORY;                   
                                                                                
  /*-------------PRINTING TABULATE REPORT FOR L026B------------------*/         
     OPTIONS MISSING=0 PAGENO=1 PAGESIZE=55 LS=132 NODATE;                      
     PROC PRINTTO FILE=P13L026B NEW;                                            
                                                                                
     PROC TABULATE DATA=P13FOUTA MISSING FORMAT=5.;                             
                                                                                
 TITLE1 "P13L026B  HDB3               S Y S T E M    O F   C O M M I T M E N T  
                   DATE : &DATE       PAGE :    ";                              
 TITLE2 "                        ADDITIONAL CHANCES ACCORDED TO APPLICATION     
                                                ";                              
 TITLE3 "        ";                                                             
                                                                                
  CLASS NEWTOWN FT NUM_PER WAIT_TIME1 TIMER_MCPS CATEGORY;                      
                                                                                
  FORMAT CATEGORY   $CAT_FMT.                                                   
         TIMER_MCPS $MCPS_FMT.                                                  
         WAIT_TIME1  TIME_FMT.;                                                 
                                                                                
  KEYLABEL N=" " ALL="TOTAL";                                                   
                                                                                
  TABLE ((NEWTOWN="")*(FT="")*(NUM_PER="")*(WAIT_TIME1=' ' ALL='TOTAL')         
         ALL='GRAND TOTAL'),                                                    
        (TIMER_MCPS ='')*(CATEGORY=' ' ALL='SUB-TOTAL')                         
        N='GRAND TOTAL'                                                         
        /RTS=26  BOX='NT    FT    %    CHANCES';                                
                                                                                
  RUN;                                                                          
