       IDENTIFICATION DIVISION.                                         00010000
       PROGRAM-ID.    BP13CB3I.                                         00020000
       AUTHOR.        SANDEEP REDDY.                                    00030000
      *DATE-WRITTEN.  07/11/2019.                                       00040000
      * =========================================================== *   00050000
      * SYSTEM OF COMMITMENT (BP13)                                 *   00060000
      * =========================================================== *   00070000
      *   OBJECTIVE   : LOAD THE DATA INTO GRANT_ASMNT AND          *   00080000
      *                 GRANT_ASMNT_PERSON TABLES.                  *   00090000
      *                                                             *   00100000
      *   INPUT FILES :                                             *   00110000
      *   1. BP13GASM - GRANT ASMNT LOAD FILE.                      *   00120000
      *   2. BP13GAPN - GRANT ASMNT PERSON LOAD FILE.               *   00130000
      *                                                             *   00140000
      *   OUTPUT FILES :                                            *   00150000
      *   1. BP13DASM - GRANT ASMNT DUPLICATE RECORDS.              *   00160000
      *   2. BP13DAPN - GRANT ASMNT PERSON DUPLICATE RECORDS.       *   00170000
      *==>ERROR RECORDS : EMPTY REGN/NRIC/GRANT TYPE.               *   00180000
      *   3. BP13EASM - GRANT ASMNT ERROR RECORDS.                  *   00190000
      *   4. BP13EAPN - GRANT ASMNT PERSON ERROR RECORDS.           *   00200000
      *                                                             *   00210000
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *   00220000
      * -------- ---------- ---- ---------------------------------- *   00230000
      * BP138048 07/11/2019 SRG1 NEW PROGRAM                        *   00240000
      * BP138149 04/02/2020 PP11 CATER TO VALIDATE THE INPUT FIELDS *   00250000
      * BP138252 11/05/2020 PP11 CATER TO VALIDATE THE INPUT FIELDS *   00251003
      * =========================================================== *   00260000
       ENVIRONMENT DIVISION.                                            00270000
       CONFIGURATION SECTION.                                           00280000
       SOURCE-COMPUTER. IBM-3090.                                       00290000
       OBJECT-COMPUTER. IBM-3090.                                       00300000
                                                                        00310000
       INPUT-OUTPUT SECTION.                                            00320000
                                                                        00330000
       FILE-CONTROL.                                                    00340000
           SELECT BP13GASM ASSIGN           TO BP13GASM.                00350000
           SELECT BP13GAPN ASSIGN           TO BP13GAPN.                00360000
                                                                        00370000
           SELECT BP13DASM ASSIGN           TO BP13DASM.                00380000
           SELECT BP13DAPN ASSIGN           TO BP13DAPN.                00390000
           SELECT BP13EASM ASSIGN           TO BP13EASM.                00400000
           SELECT BP13EAPN ASSIGN           TO BP13EAPN.                00410000
                                                                        00420000
       DATA DIVISION.                                                   00430000
                                                                        00440000
       FILE SECTION.                                                    00450000
       FD  BP13GASM                                                     00460000
           RECORDING MODE  IS  F                                        00470000
           RECORD CONTAINS 435 CHARACTERS                               00480000
           LABEL RECORDS ARE STANDARD.                                  00490000
       COPY BP13GASM.                                                   00500000
                                                                        00510000
       FD  BP13GAPN                                                     00520000
           RECORDING MODE  IS  F                                        00530000
           RECORD CONTAINS 181 CHARACTERS                               00540000
           LABEL RECORDS ARE STANDARD.                                  00550000
       COPY BP13GAPN.                                                   00560000
                                                                        00570000
       FD  BP13DASM                                                     00580000
           RECORDING MODE  IS  F                                        00590000
           RECORD CONTAINS 435 CHARACTERS                               00600000
           LABEL RECORDS ARE STANDARD.                                  00610000
       01  BP13DASM-REC                     PIC X(435).                 00620000
                                                                        00630000
       FD  BP13DAPN                                                     00640000
           RECORDING MODE  IS  F                                        00650000
           RECORD CONTAINS 181 CHARACTERS                               00660000
           LABEL RECORDS ARE STANDARD.                                  00670000
       01  BP13DAPN-REC                     PIC X(181).                 00680000
                                                                        00690000
       FD  BP13EASM                                                     00700000
           RECORDING MODE  IS  F                                        00710000
           RECORD CONTAINS 435 CHARACTERS                               00720000
           LABEL RECORDS ARE STANDARD.                                  00730000
       01  BP13EASM-REC                     PIC X(435).                 00740000
                                                                        00750000
       FD  BP13EAPN                                                     00760000
           RECORDING MODE  IS  F                                        00770000
           RECORD CONTAINS 181 CHARACTERS                               00780000
           LABEL RECORDS ARE STANDARD.                                  00790000
       01  BP13EAPN-REC                     PIC X(181).                 00800000
                                                                        00810000
       WORKING-STORAGE SECTION.                                         00820000
                                                                        00830000
       01  WS-WORK-VARIABLES.                                           00840000
           05 WS-DATE                       PIC X(08) VALUE SPACES.     00850000
           05 CNT-READ-ASMNT                PIC 9(05) VALUE ZEROES.     00860000
           05 CNT-READ-PERSN                PIC 9(05) VALUE ZEROES.     00870000
           05 CNT-INSERT-ASMNT              PIC 9(05) VALUE ZEROES.     00880000
           05 CNT-INSERT-PERSN              PIC 9(05) VALUE ZEROES.     00890000
           05 CNT-EXISTS-ASMNT              PIC 9(05) VALUE ZEROES.     00900000
           05 CNT-EXISTS-PERSN              PIC 9(05) VALUE ZEROES.     00910000
           05 CNT-ERR-INST-ASMNT            PIC 9(05) VALUE ZEROES.     00920000
           05 CNT-ERR-INST-PERSN            PIC 9(05) VALUE ZEROES.     00930000
           05 WS-GRNT-UPD-LOG               PIC 9(05) VALUE ZEROES.     00940000
           05 WS-GASM-EOF                   PIC X     VALUE 'N'.        00950000
           05 WS-GAPN-EOF                   PIC X     VALUE 'N'.        00960000
                                                                        00970000
       01  WS-TIMESTAMP                     PIC X(26) VALUE SPACES.     00980000
       01  WS-DTE-CCYYMMDD.                                             00990000
           05 WS-DTE-CCYY                   PIC X(04) VALUE SPACES.     01000000
           05 WS-DTE-MM                     PIC X(02) VALUE SPACES.     01010000
           05 WS-DTE-DD                     PIC X(02) VALUE SPACES.     01020000
       01  WS-TIME-HHMMSS.                                              01030000
           05 WS-TIME-HH                    PIC X(02) VALUE SPACES.     01040000
           05 WS-TIME-MM                    PIC X(02) VALUE SPACES.     01050000
           05 WS-TIME-SS                    PIC X(02) VALUE SPACES.     01060000
           05 WS-TIME-MS                    PIC X(02) VALUE SPACES.     01070000
                                                                        01080000
       01  WS-FRMT-DATE.                                                01090000
           05 WS-FMT-CCYY                   PIC X(04) VALUE SPACES.     01100000
           05 WS-FMT-MM                     PIC X(02) VALUE SPACES.     01110000
           05 WS-FMT-DD                     PIC X(02) VALUE SPACES.     01120000
       01  WS-TMP-DTE-UPDATE                PIC X(10) VALUE SPACES.     01130000
       01  WS-TMP-TME-UPDATE                PIC X(08) VALUE SPACES.     01140000
       01  WS-TMP-AMT-INCOME                PIC 9(09) VALUE ZEROES.     01150000
       01  WS-REGNO                         PIC X(08) VALUE SPACES.     01151000
       01  WS-GRANT-TYPE                    PIC X(06) VALUE SPACES.     01152000
       01  WS-NRIC                          PIC X(20) VALUE SPACES.     01153000
                                                                        01160000
       01  WS-SQLCODE                       PIC ---9.                   01170000
       01  WS-SQL-CODES.                                                01180000
           05  WS-SQL-OK                    PIC S9(4) COMP VALUE +000.  01190000
           05  WS-SQL-NFND                  PIC S9(4) COMP VALUE +100.  01200000
           05  WS-SQL-MORE-ROWS             PIC S9(4) COMP VALUE -811.  01210000
                                                                        01220000
             EXEC SQL INCLUDE  SQLCA    END-EXEC.                       01230000
             EXEC SQL INCLUDE  P13VGASM END-EXEC.                       01240000
             EXEC SQL INCLUDE  P13VGAPN END-EXEC.                       01250000
                                                                        01260000
       PROCEDURE DIVISION.                                              01270000
                                                                        01280000
      *------------*                                                    01290000
       0000-CONTROL.                                                    01300000
      *------------*                                                    01310000
           MOVE FUNCTION CURRENT-DATE       TO WS-DATE.                 01320000
           ACCEPT WS-DTE-CCYYMMDD         FROM DATE YYYYMMDD.           01330000
           ACCEPT WS-TIME-HHMMSS          FROM TIME.                    01340000
                                                                        01350000
           EXEC SQL                                                     01360000
                SET :WS-TIMESTAMP = CURRENT TIMESTAMP                   01370000
           END-EXEC.                                                    01380000
                                                                        01390000
           IF SQLCODE NOT = 000                                         01400000
              MOVE SQLCODE                  TO WS-SQLCODE               01410000
              DISPLAY 'SQL ERROR SET DB2 TIMESTAMP'                     01420000
              DISPLAY 'SQL ERROR CODE  : ' WS-SQLCODE                   01430000
              PERFORM 9999-SQL-ERROR        THRU 9999-EXIT              01440000
           END-IF.                                                      01450000
                                                                        01460000
           OPEN INPUT BP13GASM                                          01470000
                      BP13GAPN                                          01480000
               OUTPUT BP13DASM                                          01490000
                      BP13DAPN                                          01500000
                      BP13EASM                                          01510000
                      BP13EAPN.                                         01520000
                                                                        01530000
           PERFORM 1000-PROCESS-GRANT-ASMNT THRU 1000-EXIT              01540000
             UNTIL WS-GASM-EOF = 'Y'.                                   01550000
           PERFORM 2000-PROCESS-GRANT-PERSON THRU 2000-EXIT             01560000
             UNTIL WS-GAPN-EOF = 'Y'.                                   01570000
           PERFORM 9000-WRAP-UP             THRU 9000-EXIT.             01580000
           STOP RUN.                                                    01590000
                                                                        01600000
       0000-EXIT.                                                       01610000
           EXIT.                                                        01620000
                                                                        01630000
      *------------------------*                                        01640000
       1000-PROCESS-GRANT-ASMNT.                                        01650000
      *------------------------*                                        01660000
           READ BP13GASM                                                01670000
             AT END                                                     01680000
                MOVE 'Y'                    TO WS-GASM-EOF              01690000
             NOT AT END                                                 01700000
                ADD 1 TO CNT-READ-ASMNT                                 01710000
                PERFORM 1050-MOVE-GRANT-ASMNT-INFO  THRU 1050-EXIT      01720000
                IF (NUM-RGSTRN OF DCLGRANT-ASMNT NOT = SPACES AND       01730000
                                                      LOW-VALUES) AND   01740000
                   (NUM-GRANT-TYPE OF DCLGRANT-ASMNT NOT = SPACES AND   01750000
                                                      LOW-VALUES)       01760000
                   PERFORM 1100-INSERT-GASM    THRU 1100-EXIT           01770000
                ELSE                                                    01780000
                   WRITE BP13EASM-REC    FROM WS-GRANT-ASMNT            01790000
                   ADD 1 TO CNT-ERR-INST-ASMNT                          01800000
                END-IF                                                  01810000
           END-READ.                                                    01820000
                                                                        01830000
       1000-EXIT.                                                       01840000
           EXIT.                                                        01850000
                                                                        01860000
      *----------------------------*                                    01870000
       1050-MOVE-GRANT-ASMNT-INFO.                                      01880000
      *----------------------------*                                    01890000
           PERFORM 1060-VALIDATE-FIELDS     THRU 1060-EXIT.             01900000
           MOVE WS-NUM-RGSTRN OF WS-GRANT-ASMNT                         01910000
                                   TO NUM-RGSTRN    OF DCLGRANT-ASMNT.  01920000
           MOVE WS-NUM-GRANT-TYPE OF WS-GRANT-ASMNT                     01930000
                                   TO NUM-GRANT-TYPE OF DCLGRANT-ASMNT. 01940000
           MOVE WS-NUM-ELGBL-GRANT TO NUM-ELGBL-GRANT.                  01950000
           MOVE WS-NUM-GRANT-AMOUNT-ELGBL                               01960000
                                   TO NUM-GRANT-AMOUNT-ELGBL.           01970000
           MOVE WS-NUM-GRANT-STATUS                                     01980000
                                   TO NUM-GRANT-STATUS.                 01990000
           MOVE WS-DTE-APRV        TO DTE-APRV.                         02000000
           MOVE WS-DTE-CANCEL      TO DTE-CANCEL.                       02010000
           MOVE WS-DTE-REJECT      TO DTE-REJECT.                       02020000
           MOVE WS-DTE-SUBMIT      TO DTE-SUBMIT.                       02030000
           MOVE WS-DTE-WTHDRW      TO DTE-WTHDRW.                       02040000
           MOVE WS-NUM-RJCTN-REASON                                     02050000
                                   TO NUM-RJCTN-REASON.                 02060000
           MOVE WS-NUM-WTDRWL-REASON                                    02070000
                                   TO NUM-WTDRWL-REASON.                02080000
           MOVE WS-TXT-CNCLTN-REASON                                    02090000
                                   TO TXT-CNCLTN-REASON.                02100000
           MOVE WS-DTE-GRANT-RQST  TO DTE-GRANT-RQST.                   02110000
           MOVE WS-NUM-GRANT-DOC-RCV                                    02120000
                                   TO NUM-GRANT-DOC-RCV.                02130000
           MOVE WS-DTE-ACK         TO DTE-ACK.                          02140000
                                                                        02150000
           MOVE WS-DTE-GRANT-FORM-ISSUE                                 02160000
                                   TO DTE-GRANT-FORM-ISSUE.             02170000
           MOVE WS-DTE-GRNT-OPT-OUT-FRM-ISSUE                           02180000
                                   TO DTE-GRANT-OPT-OUT-FORM-ISSUE.     02190000
           MOVE WS-NUM-GRANT-OPT-OUT-REASON                             02200000
                                   TO NUM-GRANT-OPT-OUT-REASON.         02210000
           MOVE WS-DTE-GRANT-OPT-OUT-REASON                             02220000
                                   TO DTE-GRANT-OPT-OUT-REASON.         02230000
           MOVE WS-NUM-ALCTN-SCHEME                                     02240000
                                   TO NUM-ALCTN-SCHEME.                 02250000
           MOVE WS-NUM-HSHLD-CTZNSP                                     02260000
                                   TO NUM-HSHLD-CTZNSP.                 02270000
           MOVE WS-NUM-WRKNG-CHILD-ASMNT                                02280000
                                   TO NUM-WRKNG-CHILD-ASMNT.            02290000
           MOVE WS-QTY-MONTH-WORK  TO QTY-MONTH-WORK.                   02300000
           MOVE WS-NUM-APPEAL-ATCHMT                                    02310000
                                   TO NUM-APPEAL-ATCHMT.                02320000
           MOVE WS-NUM-PARENT-ADRS-BLDNG                                02330000
                                   TO NUM-PARENT-ADRS-BLDNG.            02340000
           MOVE WS-NUM-PARENT-ADRS-LEVEL                                02350000
                                   TO NUM-PARENT-ADRS-LEVEL.            02360000
           MOVE WS-NUM-PARENT-ADRS-UNIT                                 02370000
                                   TO NUM-PARENT-ADRS-UNIT.             02380000
           MOVE WS-TXT-PARENT-ADRS1                                     02390000
                                   TO TXT-PARENT-ADRS1.                 02400000
           MOVE WS-TXT-PARENT-ADRS2                                     02410000
                                   TO TXT-PARENT-ADRS2.                 02420000
           MOVE WS-NUM-PARENT-ADRS-POSTAL                               02430000
                                   TO NUM-PARENT-ADRS-POSTAL.           02440000
           MOVE WS-DTE-PYMNT       TO DTE-PYMNT.                        02450000
           MOVE WS-DTE-CPF-ACK-GRANT-RCV                                02460000
                                   TO DTE-CPF-ACK-GRANT-RCV.            02470000
           MOVE WS-NUM-GRANT-CREDIT                                     02480000
                                   TO NUM-GRANT-CREDIT.                 02490000
           MOVE WS-TME-APNTMT      TO TME-APNTMT.                       02500000
           MOVE WS-DTE-APNTMT      TO DTE-APNTMT.                       02510000
           MOVE WS-TME-CREATE-ASMNT                                     02520000
                                   TO TME-CREATE-ASMNT.                 02530000
           MOVE WS-NUM-UPDATE-USERID OF WS-GRANT-ASMNT                  02540000
                                   TO NUM-UPDATE-USERID OF              02550000
                                      DCLGRANT-ASMNT.                   02560000
           MOVE WS-TME-UPDATE OF WS-GRANT-ASMNT                         02570000
                                   TO TME-UPDATE OF DCLGRANT-ASMNT.     02580000
           MOVE WS-DTE-UPDATE OF WS-GRANT-ASMNT                         02590000
                                   TO DTE-UPDATE OF DCLGRANT-ASMNT.     02600000
           MOVE WS-NUM-SOURCE-SYSTEM                                    02610000
                                   TO NUM-SOURCE-SYSTEM.                02620000
           MOVE WS-NUM-SCHEME      TO NUM-SCHEME.                       02630000
           MOVE WS-NUM-ACNT        TO NUM-ACNT.                         02640000
           MOVE WS-NUM-TNCY        TO NUM-TNCY.                         02650000
           MOVE WS-NUM-HALF-GRANT  TO NUM-HALF-GRANT.                   02660000
                                                                        02670000
       1050-EXIT.                                                       02680000
           EXIT.                                                        02690000
                                                                        02700000
      *----------------------------*                                    02710000
       1060-VALIDATE-FIELDS.                                            02720000
      *----------------------------*                                    02730000
           MOVE SPACES            TO  WS-TMP-TME-UPDATE                 02740000
                                      WS-TMP-DTE-UPDATE.                02750000
                                                                        02760000
           IF  WS-DTE-APRV   = SPACES OR LOW-VALUES OR ZEROES           02770000
               MOVE '0001-01-01'  TO WS-DTE-APRV                        02780000
           ELSE                                                         02790000
              IF ((WS-DTE-APRV NOT = SPACES AND LOW-VALUES AND ZEROES)  02800000
                   AND (WS-DTE-APRV(9:2) = SPACES OR LOW-VALUES))       02810000
                 MOVE WS-DTE-APRV  TO  WS-FRMT-DATE                     02820000
                                                                        02830000
                 STRING WS-FMT-CCYY '-'                                 02840000
                        WS-FMT-MM   '-'                                 02850000
                        WS-FMT-DD                                       02860000
                           DELIMITED BY SIZE                            02870000
                           INTO WS-DTE-APRV                             02880000
              ELSE                                                      02890000
               IF ((WS-DTE-APRV NOT = SPACES AND LOW-VALUES AND ZEROES) 02900000
                   AND (WS-DTE-APRV(5:1) = '-'))                        02910000
                   CONTINUE                                             02920000
                END-IF                                                  02930000
              END-IF                                                    02940000
           END-IF.                                                      02950000
                                                                        02960000
           IF  WS-DTE-CANCEL = SPACES OR LOW-VALUES OR ZEROES           02970000
               MOVE '0001-01-01'  TO WS-DTE-CANCEL                      02980000
           END-IF.                                                      02990000
                                                                        03000000
           IF  WS-DTE-REJECT = SPACES OR LOW-VALUES OR ZEROES           03010000
               MOVE '0001-01-01'  TO WS-DTE-REJECT                      03020000
           END-IF.                                                      03030000
                                                                        03040000
           IF  WS-DTE-SUBMIT = SPACES OR LOW-VALUES OR ZEROES           03050000
               MOVE '0001-01-01'  TO WS-DTE-SUBMIT                      03060000
           END-IF.                                                      03070000
                                                                        03080000
           IF  WS-DTE-WTHDRW = SPACES OR LOW-VALUES OR ZEROES           03090000
               MOVE '0001-01-01'  TO WS-DTE-WTHDRW                      03100000
           END-IF.                                                      03110000
                                                                        03120000
           IF  WS-DTE-GRANT-RQST = SPACES OR LOW-VALUES OR ZEROES       03130000
               MOVE '0001-01-01'  TO WS-DTE-GRANT-RQST                  03140000
           END-IF.                                                      03150000
                                                                        03160000
           IF  WS-DTE-ACK        = SPACES OR LOW-VALUES OR ZEROES       03170000
               MOVE '0001-01-01'  TO WS-DTE-ACK                         03180000
           END-IF.                                                      03190000
                                                                        03200000
           IF WS-DTE-GRANT-FORM-ISSUE = SPACES OR LOW-VALUES OR ZEROES  03210000
               MOVE '0001-01-01'  TO WS-DTE-GRANT-FORM-ISSUE            03220000
           END-IF.                                                      03230000
                                                                        03240000
           IF WS-DTE-GRNT-OPT-OUT-FRM-ISSUE = SPACES OR LOW-VALUES      03250000
                                                     OR ZEROES          03260000
               MOVE '0001-01-01'  TO WS-DTE-GRNT-OPT-OUT-FRM-ISSUE      03270000
           END-IF.                                                      03280000
                                                                        03290000
           IF WS-DTE-GRANT-OPT-OUT-REASON   = SPACES OR LOW-VALUES      03300000
                                                     OR ZEROES          03310000
               MOVE '0001-01-01'  TO WS-DTE-GRANT-OPT-OUT-REASON        03320000
           END-IF.                                                      03330000
                                                                        03340000
           IF WS-DTE-PYMNT  = SPACES OR LOW-VALUES OR ZEROES            03350000
               MOVE '0001-01-01'  TO WS-DTE-PYMNT                       03360000
           END-IF.                                                      03370000
                                                                        03380000
           IF WS-DTE-CPF-ACK-GRANT-RCV = SPACES OR LOW-VALUES OR ZEROES 03390000
                                                                        03400000
               MOVE '0001-01-01'  TO WS-DTE-CPF-ACK-GRANT-RCV           03410000
           END-IF.                                                      03420000
                                                                        03430000
           IF WS-DTE-APNTMT = SPACES OR LOW-VALUES OR ZEROES            03440000
               MOVE '0001-01-01'  TO WS-DTE-APNTMT                      03450000
           END-IF.                                                      03460000
                                                                        03470000
           IF WS-TME-APNTMT = SPACES OR LOW-VALUES OR ZEROES            03480000
               MOVE '00.00.00'  TO WS-TME-APNTMT                        03490000
           END-IF.                                                      03500000
                                                                        03510000
           IF WS-TME-CREATE-ASMNT = SPACES OR LOW-VALUES OR ZEROES      03520000
               MOVE WS-TIMESTAMP  TO WS-TME-CREATE-ASMNT                03530000
           END-IF.                                                      03540000
                                                                        03550000
           MOVE 'BP13CB3I'    TO WS-NUM-UPDATE-USERID OF WS-GRANT-ASMNT.03560000
                                                                        03570000
           MOVE WS-TME-UPDATE OF WS-GRANT-ASMNT                         03580000
                                   TO  WS-TMP-TME-UPDATE.               03590000
           IF WS-TMP-TME-UPDATE  = SPACES OR LOW-VALUES OR ZEROES       03600000
               STRING WS-TIME-HH '.'                                    03610000
                      WS-TIME-MM '.'                                    03620000
                      WS-TIME-SS                                        03630000
                         DELIMITED BY SIZE                              03640000
                         INTO WS-TME-UPDATE OF WS-GRANT-ASMNT           03650000
           END-IF.                                                      03660000
                                                                        03670000
           MOVE WS-DTE-UPDATE OF WS-GRANT-ASMNT                         03680000
                                   TO  WS-TMP-DTE-UPDATE.               03690000
           IF WS-TMP-DTE-UPDATE = SPACES OR LOW-VALUES OR ZEROES        03700000
               STRING WS-DTE-CCYY '-'                                   03710000
                      WS-DTE-MM   '-'                                   03720000
                      WS-DTE-DD                                         03730000
                         DELIMITED BY SIZE                              03740000
                         INTO WS-DTE-UPDATE  OF WS-GRANT-ASMNT          03750000
           END-IF.                                                      03760000
                                                                        03770000
       1060-EXIT.                                                       03780000
           EXIT.                                                        03790000
                                                                        03800000
      *----------------*                                                03810000
       1100-INSERT-GASM.                                                03820000
      *----------------*                                                03830000
           EXEC SQL                                                     03840000
                INSERT INTO GRANT_ASMNT                                 03850000
                       ( NUM_RGSTRN,                                    03860000
                         NUM_GRANT_TYPE,                                03870000
                         NUM_ELGBL_GRANT,                               03880000
                         NUM_GRANT_AMOUNT_ELGBL,                        03890000
                         NUM_GRANT_STATUS,                              03900000
                         DTE_APRV,                                      03910000
                         DTE_CANCEL,                                    03920000
                         DTE_REJECT,                                    03930000
                         DTE_SUBMIT,                                    03940000
                         DTE_WTHDRW,                                    03950000
                         NUM_RJCTN_REASON,                              03960000
                         NUM_WTDRWL_REASON,                             03970000
                         TXT_CNCLTN_REASON,                             03980000
                         DTE_GRANT_RQST,                                03990000
                         NUM_GRANT_DOC_RCV,                             04000000
                         DTE_ACK,                                       04010000
                         DTE_GRANT_FORM_ISSUE,                          04020000
                         DTE_GRANT_OPT_OUT_FORM_ISSUE,                  04030000
                         NUM_GRANT_OPT_OUT_REASON,                      04040000
                         DTE_GRANT_OPT_OUT_REASON,                      04050000
                         NUM_ALCTN_SCHEME,                              04060000
                         NUM_HSHLD_CTZNSP,                              04070000
                         NUM_WRKNG_CHILD_ASMNT,                         04080000
                         QTY_MONTH_WORK,                                04090000
                         NUM_APPEAL_ATCHMT,                             04100000
                         NUM_PARENT_ADRS_BLDNG,                         04110000
                         NUM_PARENT_ADRS_LEVEL,                         04120000
                         NUM_PARENT_ADRS_UNIT,                          04130000
                         TXT_PARENT_ADRS1,                              04140000
                         TXT_PARENT_ADRS2,                              04150000
                         NUM_PARENT_ADRS_POSTAL,                        04160000
                         DTE_PYMNT,                                     04170000
                         DTE_CPF_ACK_GRANT_RCV,                         04180000
                         NUM_GRANT_CREDIT,                              04190000
                         TME_APNTMT,                                    04200000
                         DTE_APNTMT,                                    04210000
                         TME_CREATE_ASMNT,                              04220000
                         NUM_UPDATE_USERID,                             04230000
                         TME_UPDATE,                                    04240000
                         DTE_UPDATE,                                    04250000
                         NUM_SOURCE_SYSTEM,                             04260000
                         NUM_ACNT,                                      04270000
                         NUM_SCHEME,                                    04280000
                         NUM_TNCY,                                      04290000
                         NUM_HALF_GRANT )                               04300000
                VALUES ( :DCLGRANT-ASMNT.NUM-RGSTRN,                    04310000
                         :DCLGRANT-ASMNT.NUM-GRANT-TYPE,                04320000
                         :DCLGRANT-ASMNT.NUM-ELGBL-GRANT,               04330000
                         :DCLGRANT-ASMNT.NUM-GRANT-AMOUNT-ELGBL,        04340000
                         :DCLGRANT-ASMNT.NUM-GRANT-STATUS,              04350000
                         :DCLGRANT-ASMNT.DTE-APRV,                      04360000
                         :DCLGRANT-ASMNT.DTE-CANCEL,                    04370000
                         :DCLGRANT-ASMNT.DTE-REJECT,                    04380000
                         :DCLGRANT-ASMNT.DTE-SUBMIT,                    04390000
                         :DCLGRANT-ASMNT.DTE-WTHDRW,                    04400000
                         :DCLGRANT-ASMNT.NUM-RJCTN-REASON,              04410000
                         :DCLGRANT-ASMNT.NUM-WTDRWL-REASON,             04420000
                         :DCLGRANT-ASMNT.TXT-CNCLTN-REASON,             04430000
                         :DCLGRANT-ASMNT.DTE-GRANT-RQST,                04440000
                         :DCLGRANT-ASMNT.NUM-GRANT-DOC-RCV,             04450000
                         :DCLGRANT-ASMNT.DTE-ACK,                       04460000
                         :DCLGRANT-ASMNT.DTE-GRANT-FORM-ISSUE,          04470000
                         :DCLGRANT-ASMNT.DTE-GRANT-OPT-OUT-FORM-ISSUE,  04480000
                         :DCLGRANT-ASMNT.NUM-GRANT-OPT-OUT-REASON,      04490000
                         :DCLGRANT-ASMNT.DTE-GRANT-OPT-OUT-REASON,      04500000
                         :DCLGRANT-ASMNT.NUM-ALCTN-SCHEME,              04510000
                         :DCLGRANT-ASMNT.NUM-HSHLD-CTZNSP,              04520000
                         :DCLGRANT-ASMNT.NUM-WRKNG-CHILD-ASMNT,         04530000
                         :DCLGRANT-ASMNT.QTY-MONTH-WORK,                04540000
                         :DCLGRANT-ASMNT.NUM-APPEAL-ATCHMT,             04550000
                         :DCLGRANT-ASMNT.NUM-PARENT-ADRS-BLDNG,         04560000
                         :DCLGRANT-ASMNT.NUM-PARENT-ADRS-LEVEL,         04570000
                         :DCLGRANT-ASMNT.NUM-PARENT-ADRS-UNIT,          04580000
                         :DCLGRANT-ASMNT.TXT-PARENT-ADRS1,              04590000
                         :DCLGRANT-ASMNT.TXT-PARENT-ADRS2,              04600000
                         :DCLGRANT-ASMNT.NUM-PARENT-ADRS-POSTAL,        04610000
                         :DCLGRANT-ASMNT.DTE-PYMNT,                     04620000
                         :DCLGRANT-ASMNT.DTE-CPF-ACK-GRANT-RCV,         04630000
                         :DCLGRANT-ASMNT.NUM-GRANT-CREDIT,              04640000
                         :DCLGRANT-ASMNT.TME-APNTMT,                    04650000
                         :DCLGRANT-ASMNT.DTE-APNTMT,                    04660000
                         :DCLGRANT-ASMNT.TME-CREATE-ASMNT,              04670000
                         :DCLGRANT-ASMNT.NUM-UPDATE-USERID,             04680000
                         :DCLGRANT-ASMNT.TME-UPDATE,                    04690000
                         :DCLGRANT-ASMNT.DTE-UPDATE,                    04700000
                         :DCLGRANT-ASMNT.NUM-SOURCE-SYSTEM,             04710000
                         :DCLGRANT-ASMNT.NUM-ACNT,                      04720000
                         :DCLGRANT-ASMNT.NUM-SCHEME,                    04730000
                         :DCLGRANT-ASMNT.NUM-TNCY,                      04740000
                         :DCLGRANT-ASMNT.NUM-HALF-GRANT )               04750000
           END-EXEC.                                                    04760000
                                                                        04770000
           EVALUATE SQLCODE                                             04780000
               WHEN 000                                                 04790000
                    ADD 1                   TO CNT-INSERT-ASMNT         04800000
               WHEN -803                                                04810000
                    WRITE BP13DASM-REC    FROM WS-GRANT-ASMNT           04820000
                    ADD 1                   TO CNT-EXISTS-ASMNT         04830000
               WHEN OTHER                                               04840000
                    MOVE SQLCODE            TO WS-SQLCODE               04850000
                    DISPLAY 'SQL ERROR (INSERT) IN GRANT_ASMNT'         04860000
                    DISPLAY 'SQL ERROR CODE  : '  WS-SQLCODE            04870000
                    DISPLAY DCLGRANT-ASMNT                              04880000
                    PERFORM 9999-SQL-ERROR  THRU 9999-EXIT              04890000
           END-EVALUATE.                                                04900000
                                                                        04910000
       1100-EXIT.                                                       04920000
           EXIT.                                                        04930000
                                                                        04940000
      *-------------------------*                                       04950000
       2000-PROCESS-GRANT-PERSON.                                       04960000
      *-------------------------*                                       04970000
           READ BP13GAPN                                                04980000
             AT END                                                     04990000
                MOVE 'Y'                    TO WS-GAPN-EOF              05000000
             NOT AT END                                                 05010000
                ADD 1 TO CNT-READ-PERSN                                 05020000
                PERFORM 2050-VALIDATE-PERSN-FLD  THRU 2050-EXIT         05030000
                MOVE WS-GRANT-ASMNT-PERSON  TO DCLGRANT-ASMNT-PERSON    05040000
                IF (NUM-RGSTRN OF DCLGRANT-ASMNT-PERSON NOT = SPACES AND05050000
                                                      LOW-VALUES) AND   05060000
                   (NUM-GRANT-TYPE OF DCLGRANT-ASMNT-PERSON NOT = SPACES05070000
                                                  AND LOW-VALUES) AND   05080000
                   (NUM-IDNTY-NO OF DCLGRANT-ASMNT-PERSON NOT = SPACES  05090000
                                                  AND LOW-VALUES)       05100000
                   PERFORM 2100-INSERT-GAPN    THRU 2100-EXIT           05110000
                ELSE                                                    05120000
                   WRITE BP13EAPN-REC    FROM WS-GRANT-ASMNT-PERSON     05130000
                   ADD 1 TO CNT-ERR-INST-PERSN                          05140000
                END-IF                                                  05150000
           END-READ.                                                    05160000
                                                                        05170000
       2000-EXIT.                                                       05180000
           EXIT.                                                        05190000
                                                                        05200000
      *------------------------*                                        05210000
       2050-VALIDATE-PERSN-FLD.                                         05220000
      *------------------------*                                        05230000
           MOVE SPACES            TO  WS-TMP-TME-UPDATE                 05240000
                                      WS-TMP-DTE-UPDATE.                05250000
           MOVE ZEROES            TO  WS-TMP-AMT-INCOME.                05260000
           IF  WS-AMT-GRANT IS NUMERIC                                  05270000
               CONTINUE                                                 05280000
           ELSE                                                         05290000
               MOVE ZEROES         TO   WS-AMT-GRANT                    05300000
           END-IF.                                                      05310000
                                                                        05320000
           IF  WS-AMT-GRANT-USE IS NUMERIC                              05330000
               CONTINUE                                                 05340000
           ELSE                                                         05350000
               MOVE ZEROES         TO   WS-AMT-GRANT-USE                05360000
           END-IF.                                                      05370000
                                                                        05380000
           MOVE WS-AMT-INCOME      TO   WS-TMP-AMT-INCOME               05390000
           IF  WS-TMP-AMT-INCOME IS NUMERIC                             05400000
               CONTINUE                                                 05410000
           ELSE                                                         05420000
               MOVE ZEROES         TO   WS-AMT-INCOME                   05430000
           END-IF.                                                      05440000
                                                                        05450000
           MOVE 'BP13CB3I'         TO WS-NUM-UPDATE-USERID OF           05460000
                                      WS-GRANT-ASMNT-PERSON             05470000
                                                                        05480000
           MOVE WS-TME-UPDATE OF WS-GRANT-ASMNT-PERSON                  05490000
                                   TO  WS-TMP-TME-UPDATE.               05500000
           IF WS-TMP-TME-UPDATE  = SPACES OR LOW-VALUES OR ZEROES       05510000
               STRING WS-TIME-HH '.'                                    05520000
                      WS-TIME-MM '.'                                    05530000
                      WS-TIME-SS                                        05540000
                         DELIMITED BY SIZE                              05550000
                         INTO WS-TME-UPDATE  OF WS-GRANT-ASMNT-PERSON   05560000
           END-IF.                                                      05570000
                                                                        05580000
           MOVE WS-DTE-UPDATE OF WS-GRANT-ASMNT-PERSON                  05590000
                                   TO  WS-TMP-DTE-UPDATE                05600000
                                                                        05610000
           IF WS-TMP-DTE-UPDATE  = SPACES OR LOW-VALUES OR ZEROES       05620000
               STRING WS-DTE-CCYY '-'                                   05630000
                      WS-DTE-MM   '-'                                   05640000
                      WS-DTE-DD                                         05650000
                         DELIMITED BY SIZE                              05660000
                         INTO WS-DTE-UPDATE  OF WS-GRANT-ASMNT-PERSON   05670000
           END-IF.                                                      05680000
                                                                        05690000
           IF  WS-AMT-GRANT-THRSHD IS NUMERIC                           05700000
               CONTINUE                                                 05710000
           ELSE                                                         05720000
               MOVE ZEROES     TO  WS-AMT-GRANT-THRSHD                  05730000
           END-IF                                                       05740000
           IF  WS-AMT-GRANT-EXCESS  IS NUMERIC                          05750000
               CONTINUE                                                 05760000
           ELSE                                                         05770000
               MOVE ZEROES     TO  WS-AMT-GRANT-EXCESS                  05780000
           END-IF.                                                      05790000
                                                                        05800000
       2050-EXIT.                                                       05810000
           EXIT.                                                        05820000
                                                                        05830000
      *----------------*                                                05840000
       2100-INSERT-GAPN.                                                05850000
      *----------------*                                                05860000
           EXEC SQL                                                     05870000
                INSERT INTO GRANT_ASMNT_PERSON                          05880000
                       ( NUM_RGSTRN,                                    05890000
                         NUM_GRANT_TYPE,                                05900000
                         NUM_IDNTY_NO,                                  05910000
                         NUM_IDNTY_NO_TYPE,                             05920000
                         NUM_CSTMR_SOURCE,                              05930000
                         NUM_CPF_ACNT,                                  05940000
                         AMT_GRANT,                                     05950000
                         AMT_GRANT_USE,                                 05960000
                         NUM_ROLE_TAG,                                  05970000
                         NUM_RLTNSP,                                    05980000
                         NME_PERSON,                                    05990000
                         NUM_GENDER,                                    06000000
                         NUM_CTZNSP,                                    06010000
                         NUM_MRTL_STATUS,                               06020000
                         AMT_INCOME,                                    06030000
                         NUM_1YR_CNTNS_EMPLY,                           06040000
                         NUM_DEBAR,                                     06050000
                         NUM_DEBAR_REASON,                              06060000
                         NUM_RCVRY,                                     06070000
                         NUM_UPDATE_USERID,                             06080000
                         DTE_UPDATE,                                    06090000
                         TME_UPDATE,                                    06100000
                         AMT_GRANT_THRSHD )                             06110000
                VALUES ( :DCLGRANT-ASMNT-PERSON.NUM-RGSTRN,             06120000
                         :DCLGRANT-ASMNT-PERSON.NUM-GRANT-TYPE,         06130000
                         :DCLGRANT-ASMNT-PERSON.NUM-IDNTY-NO,           06140000
                         :DCLGRANT-ASMNT-PERSON.NUM-IDNTY-NO-TYPE,      06150000
                         :DCLGRANT-ASMNT-PERSON.NUM-CSTMR-SOURCE,       06160000
                         :DCLGRANT-ASMNT-PERSON.NUM-CPF-ACNT,           06170000
                         :DCLGRANT-ASMNT-PERSON.AMT-GRANT,              06180000
                         :DCLGRANT-ASMNT-PERSON.AMT-GRANT-USE,          06190000
                         :DCLGRANT-ASMNT-PERSON.NUM-ROLE-TAG,           06200000
                         :DCLGRANT-ASMNT-PERSON.NUM-RLTNSP,             06210000
                         :DCLGRANT-ASMNT-PERSON.NME-PERSON,             06220000
                         :DCLGRANT-ASMNT-PERSON.NUM-GENDER,             06230000
                         :DCLGRANT-ASMNT-PERSON.NUM-CTZNSP,             06240000
                         :DCLGRANT-ASMNT-PERSON.NUM-MRTL-STATUS,        06250000
                         :DCLGRANT-ASMNT-PERSON.AMT-INCOME,             06260000
                         :DCLGRANT-ASMNT-PERSON.NUM-1YR-CNTNS-EMPLY,    06270000
                         :DCLGRANT-ASMNT-PERSON.NUM-DEBAR,              06280000
                         :DCLGRANT-ASMNT-PERSON.NUM-DEBAR-REASON,       06290000
                         :DCLGRANT-ASMNT-PERSON.NUM-RCVRY,              06300000
                         :DCLGRANT-ASMNT-PERSON.NUM-UPDATE-USERID,      06310000
                         :DCLGRANT-ASMNT-PERSON.DTE-UPDATE,             06320000
                         :DCLGRANT-ASMNT-PERSON.TME-UPDATE,             06330000
                         :DCLGRANT-ASMNT-PERSON.AMT-GRANT-THRSHD )      06340000
           END-EXEC                                                     06350000
                                                                        06360000
           EVALUATE SQLCODE                                             06370000
               WHEN 000                                                 06380000
                    ADD 1                   TO CNT-INSERT-PERSN         06390000
               WHEN -803                                                06400000
                    PERFORM 3000-UPD-GRANT-PERSON THRU 3000-EXIT        06401000
               WHEN OTHER                                               06430000
                    MOVE SQLCODE            TO WS-SQLCODE               06440000
                    DISPLAY 'SQL ERROR (INSERT) IN GRNT_ASMT_PERSN'     06450000
                    DISPLAY 'SQL ERROR CODE  : '  WS-SQLCODE            06460000
                    DISPLAY DCLGRANT-ASMNT-PERSON                       06470000
                    PERFORM 9999-SQL-ERROR  THRU 9999-EXIT              06480000
           END-EVALUATE.                                                06490000
                                                                        06500000
       2100-EXIT.                                                       06510000
           EXIT.                                                        06520000
                                                                        06530000
      ***************************************************************   06531000
       3000-UPD-GRANT-PERSON.                                           06532000
      ***************************************************************   06533000
                                                                        06534000
           MOVE WS-NUM-RGSTRN  OF WS-GRANT-ASMNT-PERSON TO WS-REGNO     06534100
           MOVE WS-NUM-GRANT-TYPE OF WS-GRANT-ASMNT-PERSON              06534200
                                  TO WS-GRANT-TYPE                      06534300
           MOVE WS-NUM-IDNTY-NO OF WS-GRANT-ASMNT-PERSON                06534400
                                  TO WS-NRIC                            06534500
           MOVE WS-AMT-GRANT TO AMT-GRANT OF DCLGRANT-ASMNT-PERSON.     06535000
           MOVE 'BP13CB4B ' TO NUM-UPDATE-USERID OF                     06536000
                               DCLGRANT-ASMNT-PERSON.                   06537000
           MOVE WS-TIMESTAMP(1:10) TO DTE-UPDATE OF                     06538000
                                      DCLGRANT-ASMNT-PERSON.            06539000
           STRING WS-TIME-HHMMSS(1:2) '.'                               06539100
                  WS-TIME-HHMMSS(3:2) '.'                               06539200
                  WS-TIME-HHMMSS(5:2)                                   06539300
                  DELIMITED BY SIZE INTO TME-UPDATE OF                  06539400
                                         DCLGRANT-ASMNT-PERSON.         06539500
           EXEC SQL                                                     06539600
              UPDATE GRANT_ASMNT_PERSON                                 06539700
              SET AMT_GRANT = :DCLGRANT-ASMNT-PERSON.AMT-GRANT,         06539800
                  NUM_UPDATE_USERID                                     06539900
                            = :DCLGRANT-ASMNT-PERSON.NUM-UPDATE-USERID, 06540000
                  TME_UPDATE = :DCLGRANT-ASMNT-PERSON.TME-UPDATE,       06540100
                  DTE_UPDATE = :DCLGRANT-ASMNT-PERSON.DTE-UPDATE        06540200
              WHERE NUM_RGSTRN = :WS-REGNO                              06540300
              AND NUM_GRANT_TYPE = :WS-GRANT-TYPE                       06540400
              AND NUM_IDNTY_NO   = :WS-NRIC                             06540500
           END-EXEC                                                     06540600
                                                                        06540700
           EVALUATE SQLCODE                                             06540800
           WHEN WS-SQL-OK                                               06540900
            WRITE BP13DAPN-REC    FROM WS-GRANT-ASMNT-PERSON            06541000
            ADD 1                   TO CNT-EXISTS-PERSN                 06541100
           WHEN WS-SQL-NFND                                             06541700
                MOVE SQLCODE   TO WS-SQLCODE                            06541800
                DISPLAY 'CASE NOTFND IN GRANT_ASMNT_PERSON'             06541900
                DISPLAY 'SQLCODE IS : ' WS-SQLCODE                      06542000
                DISPLAY 'REGNO : ' NUM-RGSTRN OF DCLGRANT-ASMNT-PERSON  06542100
                DISPLAY 'GRANT TYPE: ' NUM-GRANT-TYPE                   06542200
                                            OF DCLGRANT-ASMNT-PERSON    06542300
                DISPLAY 'IDNTY NO  :' NUM-IDNTY-NO OF                   06542400
                                      DCLGRANT-ASMNT-PERSON             06542500
                                                                        06542600
                DISPLAY SPACES                                          06542700
           WHEN OTHER                                                   06542800
                MOVE SQLCODE   TO WS-SQLCODE                            06542900
                DISPLAY 'ERROR UPDATING GRANT_ASMNT_PERSON'             06543000
                DISPLAY 'SQLCODE IS : ' WS-SQLCODE                      06543100
                DISPLAY SPACES                                          06543200
                PERFORM 9999-SQL-ERROR  THRU 9999-EXIT                  06543300
                                                                        06543400
           END-EVALUATE.                                                06543500
                                                                        06543600
       3000-EXIT.                                                       06543700
           EXIT.                                                        06543800
      ******************************************************************06544000
       9000-WRAP-UP.                                                    06550000
      ******************************************************************06560000
           CLOSE BP13GASM                                               06570000
                 BP13GAPN                                               06580000
                 BP13DASM                                               06590000
                 BP13DAPN                                               06600000
                 BP13EASM                                               06610000
                 BP13EAPN.                                              06620000
                                                                        06630000
           DISPLAY '***********************************************'    06640000
           DISPLAY '***      END OF THE PROGRAM BP13CB3I        ***'.   06650000
           DISPLAY SPACES.                                              06660000
           DISPLAY 'RECORDS READ FROM BP13GASM               : '        06670000
                    CNT-READ-ASMNT.                                     06680000
           DISPLAY 'RECORDS READ FROM BP13GAPN               : '        06690000
                    CNT-READ-PERSN.                                     06700000
           DISPLAY 'RECORDS INSERTED IN GRANT_ASMNT          : '        06710000
                    CNT-INSERT-ASMNT.                                   06720000
           DISPLAY 'RECORDS INSERTED IN GRANT_ASMNT_PERSON   : '        06730000
                    CNT-INSERT-PERSN.                                   06740000
           DISPLAY 'RECORDS ALRDY EXIST IN GRANT_ASMNT       : '        06750000
                    CNT-EXISTS-ASMNT.                                   06760000
           DISPLAY 'RECORDS ALRDY EXIST IN GRANT_ASMNT_PERSON: '        06770000
                    CNT-EXISTS-PERSN.                                   06780000
           DISPLAY 'RECORDS NOT INSRTD IN GRANT_ASMNT        : '        06790000
                    CNT-ERR-INST-ASMNT                                  06800000
           DISPLAY 'RECORDS NOT INSRTD IN GRANT_ASMNT_PERSON : '        06810000
                    CNT-ERR-INST-PERSN.                                 06820000
           DISPLAY SPACES.                                              06830000
           DISPLAY '***********************************************'.   06840000
                                                                        06850000
       9000-EXIT.                                                       06860000
           EXIT.                                                        06870000
                                                                        06880000
      ***************************************************************** 06890000
       9999-SQL-ERROR.                                                  06900000
      ***************************************************************** 06910000
           CALL 'DBATIAR' USING SQLCA.                                  06920000
           PERFORM 9000-WRAP-UP   THRU 9000-EXIT                        06930000
           MOVE 12 TO RETURN-CODE                                       06940000
           STOP RUN.                                                    06950000
                                                                        06960000
       9999-EXIT.                                                       06970000
           EXIT.                                                        06980000
