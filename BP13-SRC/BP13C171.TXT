       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C171.                                                 
      *AUTHOR.        MCP1.                                                     
      *DATE-WRITTEN.  04/08/03.                                                 
      * ============================================================== *        
      *  AUTHOR.        MCP1                                           *        
      *  DATE-WRITTEN.  04/08/03.                                      *        
      *                                                                *        
      *  INPUT FILES  - BP13F310                                       *        
      *               - BM06K110                                       *        
      *               - BP13K022                                       *        
      *  OUTPUT FILES - BM06F120                                       *        
      *                                                                *        
      *  OBJECTIVES : TO CREATE THE EMAIL FOR KEY AVAILABLE EMAIL ALERT*        
      *                                                                *        
      * CHG REQ# DATE     BY   REMARKS                                 *        
      * -------- ----     --   -------                                 *        
      * BP132495 04/08/03 MCP1 NEW PROGRAM                             *        
      * BP132551 24/08/04 CKK7 OUTPUT RECS TO BM06F120                 *        
      * ============================================================== *        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3083.                                               
       OBJECT-COMPUTER. IBM-3083.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310 ASSIGN       TO BP13F310.                            
                                                                                
           SELECT BP13K022 ASSIGN       TO BP13K022                             
                  ACCESS MODE           IS RANDOM                               
                  ORGANIZATION          IS INDEXED                              
                  RECORD KEY            IS K022-KEY-FLD                         
                  ALTERNATE RECORD KEY  IS K022-NUM-REGN                        
                  FILE STATUS           IS BP13K022-STATUS.                     
                                                                                
           SELECT BM06K110  ASSIGN       TO BM06K110                            
                  ACCESS  MODE           IS RANDOM                              
                  ORGANIZATION           IS INDEXED                             
                  RECORD   KEY           IS K110-KEY-FLD                        
                  ALTERNATE RECORD KEY   IS K110-REGN-NO                        
                  FILE STATUS            IS BM06K110-STATUS.                    
                                                                                
           SELECT BM06F120 ASSIGN       TO BM06F120.                            
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      ******************************************************************        
                                                                                
       FD  BP13F310                                                             
           RECORDING MODE  IS F                                                 
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13F310.                                                           
                                                                                
       FD  BP13K022                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K022.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BM06F120                                                             
           RECORDING MODE  IS F                                                 
           RECORD CONTAINS 700 CHARACTERS.                                      
       COPY BM06F120.                                                           
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  BP13K022-STATUS         PIC 9(2)    VALUE 0.                     
           05  BM06K110-STATUS         PIC 9(2)    VALUE 0.                     
           05  WS-INPUT-EOF            PIC X       VALUE 'N'.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  BP13F310-READ           PIC 9(8)    VALUE 0.                     
           05  F120-WRITTEN            PIC 9(8)    VALUE 0.                     
           05  WS-K110-FND             PIC 9(03)   VALUE 0.                     
           05  WS-K110-NOTFND          PIC 9(03)   VALUE 0.                     
           05  WS-K022-NOTFND          PIC 9(03)   VALUE 0.                     
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *----------------------------------------------------------------*        
       0000-CONTROL.                                                            
      *----------------------------------------------------------------*        
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-READ-BP13F310     THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS           THRU 3000-EXIT                        
                   UNTIL WS-INPUT-EOF = 'Y'                                     
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
      *----------------------------------------------------------------*        
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------*        
           OPEN INPUT  BP13F310                                                 
                       BP13K022                                                 
                       BM06K110                                                 
                OUTPUT BM06F120.                                                
                                                                                
           IF BP13K022-STATUS NOT = ZEROES AND 97                               
              MOVE BP13K022-STATUS TO RETURN-CODE                               
              DISPLAY 'OPEN BP13K022 FILE ERROR ' BP13K022-STATUS               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                          
                                                                                
           IF BM06K110-STATUS NOT = ZEROES AND 97                               
              MOVE BM06K110-STATUS TO RETURN-CODE                               
              DISPLAY 'OPEN BM06K110 FILE ERROR ' BM06K110-STATUS               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-READ-BP13F310.                                                      
      *----------------------------------------------------------------*        
           READ BP13F310 AT END                                                 
           MOVE 'Y'                      TO WS-INPUT-EOF                        
           IF BP13F310-READ = ZERO                                              
              MOVE '99'                     TO RETURN-CODE                      
           END-IF                                                               
           GO TO 2000-EXIT.                                                     
                                                                                
           ADD 1                         TO BP13F310-READ.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3000-PROCESS.                                                            
      *----------------------------------------------------------------*        
           MOVE SPACES           TO K110-REC.                                   
           MOVE F310-REGN-NO     TO K110-REGN-NO.                               
                                                                                
           READ BM06K110 KEY IS K110-REGN-NO.                                   
                                                                                
           EVALUATE BM06K110-STATUS                                             
             WHEN 00                                                            
                  ADD 1               TO WS-K110-FND                            
                  PERFORM 3500-READ-BP13K022 THRU 3500-EXIT                     
             WHEN 23                                                            
                  ADD 1               TO WS-K110-NOTFND                         
                  DISPLAY 'REC NOTFD BM06K110, KEY IS ' K110-REGN-NO            
             WHEN OTHER                                                         
                 DISPLAY 'READ  BM06K110 ERROR ' BM06K110-STATUS                
                 MOVE BM06K110-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2000-READ-BP13F310     THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3500-READ-BP13K022.                                                      
      *----------------------------------------------------------------*        
           MOVE SPACES       TO BP13K022-REC.                                   
           MOVE K110-REGN-NO TO K022-NUM-REGN.                                  
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE BP13K022-STATUS                                             
             WHEN 00                                                            
                  IF K022-CDE-ALERT = 'Y' AND                                   
                     K022-CDE-ALERT-TYPE = 'KA'                                 
                     PERFORM 4000-WRITE-BM06F120 THRU 4000-EXIT                 
                  END-IF                                                        
             WHEN 23                                                            
                  ADD 1               TO WS-K022-NOTFND                         
                  DISPLAY 'REC NOTFD BP13K022, KEY IS ' K022-NUM-REGN           
             WHEN OTHER                                                         
                 DISPLAY 'READ  BP13K022 ERROR ' BP13K022-STATUS                
                 MOVE BP13K022-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4000-WRITE-BM06F120.                                                     
      *----------------------------------------------------------------*        
           MOVE SPACES             TO F120-REC.                                 
           INITIALIZE              F120-REC.                                    
                                                                                
           MOVE K110-REC           TO F120-REC(1:500).                          
                                                                                
           MOVE 'KA'               TO F120-REC(688:2).                          
           MOVE K022-NUM-OIC       TO F120-REC(690:10).                         
                                                                                
           MOVE SPACES             TO F120-BO-CODE.                             
           MOVE K110-REGN-NO       TO F120-REGN-NO.                             
           MOVE K110-SCH-ACC-NO    TO F120-SCH-ACC-NO.                          
           MOVE F310-NUM-BLK       TO F120-BLK-NO.                              
           MOVE F310-NME-STREET    TO F120-STREET-NAME.                         
           MOVE F310-NUM-LEVEL     TO F120-LEVEL-NO.                            
           MOVE F310-NUM-MAIN(1:4) TO F120-MAIN-UNIT.                           
           MOVE F310-NUM-POSTAL    TO F120-POSTAL-CODE.                         
                                                                                
           WRITE F120-REC.                                                      
           ADD 1                   TO F120-WRITTEN.                             
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       9000-CLOSE-FILES.                                                        
      *----------------------------------------------------------------*        
           DISPLAY 'NO. OF REC BP13F310          = ' BP13F310-READ.             
           DISPLAY 'NO. OF REC BM06K110 NOTFND   = ' WS-K110-NOTFND.            
           DISPLAY 'NO. OF REC BP13K022 NOTFND   = ' WS-K022-NOTFND.            
           DISPLAY 'NO. OF REC BM06F120 WRITTEN  = ' F120-WRITTEN.              
                                                                                
           CLOSE  BP13F310                                                      
                  BP13K022                                                      
                  BM06K110                                                      
                  BM06F120.                                                     
                                                                                
           IF BP13K022-STATUS NOT = ZEROES                                      
              MOVE BP13K022-STATUS TO RETURN-CODE                               
              DISPLAY 'CLOSE BP13K022 FILE ERROR ' BP13K022-STATUS.             
                                                                                
           IF BM06K110-STATUS NOT = ZEROES                                      
              MOVE BM06K110-STATUS TO RETURN-CODE                               
              DISPLAY 'CLOSE BM06K110 FILE ERROR ' BM06K110-STATUS.             
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
