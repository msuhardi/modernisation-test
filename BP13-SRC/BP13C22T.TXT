       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C22T.                                                 
      *AUTHOR.        LIAW MEI SIEN.                                            
      *DATE-WRITTEN.  05/04/2005.                                               
      * ========================================================== *            
      * SYSTEM NAME : SYSTEM OF COMMITMENT                         *            
      * ========================================================== *            
      * OBJECTIVE :                                                *            
      *     TO PASS DEBARMENT RECORDS FOR CASES UNDER DEBARMENT    *            
      *     "MA" TO DEBARMENT SYSTEM FOR DELETION.                 *            
      *                                                            *            
      * INPUT FILES  :  BP13F310  -  SEQUENTIAL DATASET            *            
      *                 BP13K800  -  VSAM DATASET                  *            
      *                 BP13K853  -  VSAM DATASET                  *            
      *                 P04K010   -  VSAM DATASET                  *            
      * OUTPUT FILES :  P04F010   -  SEQUENTIAL DATASET            *            
      *                 BP13FOUT  -  SEQUENTIAL DATASET            *            
      *----------------------------------------------------------*              
      *  MODIFICATIONS:                                          *              
      *                                                          *              
      *  CHGE-NO  DATE       OIC  DESCRIPTION                    *              
      *  ======== ========== ==== ============================== *              
      *  BP132681 05/04/2005 LMS1 NEW PROGRAM.                   *              
      *  BP135599 11/12/2014 PCL4 TO UPDATE K010-INTERFACE-SYS   *              
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310  ASSIGN       TO BP13F310.                           
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13K853  ASSIGN       TO BP13K853                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K853-KEY-FLD                        
                            FILE STATUS  IS WS-K853-STATUS.                     
                                                                                
           SELECT P04K010   ASSIGN       TO P04K010                             
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K010-PRIME-KEY                      
                            FILE STATUS  IS WS-K010-STATUS.                     
                                                                                
           SELECT P04F010   ASSIGN       TO P04F010.                            
                                                                                
           SELECT BP13FOUT  ASSIGN       TO BP13FOUT.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD  BP13F310                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13F310.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K853                                                             
           RECORD CONTAINS 250 CHARACTERS.                                      
       COPY BP13K853.                                                           
                                                                                
       FD  P04K010                                                              
           RECORD CONTAINS 300 CHARACTERS.                                      
       COPY P04K010.                                                            
                                                                                
       FD  P04F010                                                              
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 300 CHARACTERS.                                      
       01  P04F010-REC                 PIC X(300).                              
                                                                                
       FD  BP13FOUT                                                             
           RECORDING MODE  F                                                    
           LABEL RECORDS   ARE STANDARD                                         
           BLOCK CONTAINS  0   RECORDS                                          
           RECORD CONTAINS 200 CHARACTERS.                                      
       01  BP13FOUT-REC.                                                        
           05  FOUT-REGN        PIC X(08).                                      
           05  FOUT-ZONE        PIC X(02).                                      
           05  FOUT-NT          PIC X(03).                                      
           05  FOUT-NAME        PIC X(66).                                      
           05  FOUT-NRIC        PIC X(09).                                      
           05  FOUT-SCH-ACC     PIC X(11).                                      
           05  FOUT-BLK         PIC X(05).                                      
           05  FOUT-STREET      PIC X(20).                                      
           05  FOUT-LEVEL       PIC X(02).                                      
           05  FOUT-MAIN        PIC X(05).                                      
           05  FOUT-DEBAR-CDE   PIC X(02).                                      
           05  FOUT-DTE-BALLOT  PIC X(06).                                      
           05  FOUT-FLAT-TYPE   PIC X(02).                                      
           05  FOUT-MCPS-POSTAL PIC X(06).                                      
           05  FOUT-DTE-EFFECT  PIC X(08).                                      
           05  FILLER           PIC X(45).                                      
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-STATUS.                                                           
           05  WS-K800-STATUS           PIC 9(2)  VALUE 00.                     
           05  WS-K853-STATUS           PIC 9(2)  VALUE 00.                     
           05  WS-K010-STATUS           PIC 9(2)  VALUE 00.                     
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-EOF-F310-SW           PIC X     VALUE 'N'.                    
           05  WS-END-K853-SW           PIC X     VALUE 'N'.                    
           05  WS-END-K010-SW           PIC X     VALUE 'N'.                    
                                                                                
       01  WS-ACCUMULATORS.                                                     
           05  WS-F310-READ             PIC 9(5)  VALUE ZEROES.                 
           05  WS-K853-NRIC-FND         PIC 9(5)  VALUE ZEROES.                 
           05  WS-K800-REGN-FND         PIC 9(5)  VALUE ZEROES.                 
           05  WS-K800-REGN-NOTFND      PIC 9(5)  VALUE ZEROES.                 
           05  WS-K010-REF-FND          PIC 9(5)  VALUE ZEROES.                 
           05  WS-K010-REASON           PIC 9(5)  VALUE ZEROES.                 
           05  WS-K010-BYPASSED         PIC 9(5)  VALUE ZEROES.                 
           05  WS-F010-WRITTEN          PIC 9(5)  VALUE ZEROES.                 
           05  WS-FOUT-WRITTEN          PIC 9(5)  VALUE ZEROES.                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       MAIN.                                                                    
      *-------------------------------------------------------------            
           PERFORM 0000-OPEN-ROUTINE    THRU 0000-EXIT.                         
           PERFORM 1000-READ-F310       THRU 1000-EXIT.                         
           PERFORM 2000-PROCESS-DATA    THRU 2000-EXIT                          
                       UNTIL WS-EOF-F310-SW = 'Y'.                              
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
                                                                                
       0000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F310                                                 
                       BP13K800                                                 
                       BP13K853                                                 
                       P04K010                                                  
                OUTPUT P04F010                                                  
                       BP13FOUT.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN BP13K800 FAIL ERROR ' WS-K800-STATUS                
              MOVE     WS-K800-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K853-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN BP13K853 FAIL ERROR ' WS-K853-STATUS                
              MOVE     WS-K853-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN BP13K010 FAIL ERROR ' WS-K010-STATUS                
              MOVE     WS-K010-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       1000-READ-F310.                                                          
      *-------------------------------------------------------------            
           READ BP13F310 AT END                                                 
                MOVE 'Y' TO WS-EOF-F310-SW                                      
                GO TO 1000-EXIT.                                                
                                                                                
           ADD  1  TO WS-F310-READ.                                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
           PERFORM 2100-READ-BP13K800 THRU 2100-EXIT.                           
           PERFORM 3000-START-K853    THRU 3000-EXIT.                           
           IF WS-K853-STATUS  =  00                                             
              PERFORM 3100-READ-K853        THRU 3100-EXIT                      
              PERFORM 3200-PROCESS-K853     THRU 3200-EXIT                      
                      UNTIL WS-END-K853-SW = 'Y'                                
                      OR (K853-NUM-REGN NOT = F310-REGN-NO)                     
           END-IF.                                                              
           PERFORM 1000-READ-F310     THRU 1000-EXIT.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2100-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                   TO BP13K800-MASTER.                    
           INITIALIZE                       BP13K800-MASTER.                    
                                                                                
           MOVE F310-REGN-NO             TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    ADD 1                TO WS-K800-REGN-FND                    
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1                TO WS-K800-REGN-NOTFND                 
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR : ' WS-K800-STATUS             
                            ' K800-KEY-FLD : ' K800-NUM-REGN                    
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3000-START-K853.                                                         
      *-------------------------------------------------------------            
           MOVE  SPACES       TO K853-KEY-FLD.                                  
           MOVE  F310-REGN-NO TO K853-NUM-REGN.                                 
           START BP13K853 KEY >= K853-KEY-FLD                                   
           IF WS-K853-STATUS  =  00                                             
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-K853-STATUS = 23                                            
                 DISPLAY 'READ BP13K853 NOTFD : ' WS-K853-STATUS                
                         ' K853-KEY-FLD : ' K853-NUM-REGN                       
              ELSE                                                              
                 DISPLAY 'READ BP13K853 FAIL ERROR : ' WS-K853-STATUS           
                         ' K853-KEY-FLD : ' K853-NUM-REGN                       
                 MOVE     WS-K853-STATUS      TO RETURN-CODE                    
                 PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3100-READ-K853.                                                          
      *-------------------------------------------------------------            
           READ BP13K853 NEXT RECORD AT END                                     
                MOVE 'Y' TO WS-END-K853-SW.                                     
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3200-PROCESS-K853.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF K853-NUM-REGN = F310-REGN-NO                                      
              ADD  1  TO WS-K853-NRIC-FND                                       
              PERFORM 4000-START-K010   THRU 4000-EXIT                          
              IF WS-K010-STATUS = 00                                            
                 PERFORM 4100-READ-K010        THRU 4100-EXIT                   
                 PERFORM 4200-PROCESS-K010     THRU 4200-EXIT                   
                         UNTIL WS-END-K010-SW = 'Y'                             
                         OR (K010-NUM-UINFIN(1:9) NOT = K853-NUM-NRIC)          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3100-READ-K853    THRU 3100-EXIT.                            
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4000-START-K010.                                                         
      *-------------------------------------------------------------            
           MOVE  SPACES        TO K010-PRIME-KEY.                               
           MOVE  K853-NUM-NRIC TO K010-NUM-UINFIN.                              
           START P04K010 KEY   >= K010-PRIME-KEY                                
           IF WS-K010-STATUS   =  00                                            
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-K010-STATUS = 23                                            
                 DISPLAY 'READ P04K010 FAIL ERROR : ' WS-K010-STATUS            
                         ' K010-KEY-FLD : ' K010-NUM-UINFIN                     
              ELSE                                                              
                 DISPLAY 'READ P04K010 FAIL ERROR : ' WS-K010-STATUS            
                         ' K010-KEY-FLD : ' K010-NUM-UINFIN                     
                 MOVE     WS-K010-STATUS      TO RETURN-CODE                    
                 PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4100-READ-K010.                                                          
      *-------------------------------------------------------------            
           READ P04K010 NEXT RECORD AT END                                      
                MOVE 'Y' TO WS-END-K010-SW.                                     
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4200-PROCESS-K010.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF K010-NUM-UINFIN(1:9) = K853-NUM-NRIC                              
              ADD 1 TO WS-K010-REF-FND                                          
              EVALUATE K010-REASON-CODE                                         
              WHEN 'MA'                                                         
                 ADD 1 TO WS-K010-REASON                                        
                 PERFORM 5000-WRITE-F010 THRU 5000-EXIT                         
                 PERFORM 6000-WRITE-BP13FOUT THRU 6000-EXIT                     
              WHEN OTHER                                                        
                 ADD 1 TO WS-K010-BYPASSED                                      
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
           PERFORM 4100-READ-K010 THRU 4100-EXIT.                               
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       5000-WRITE-F010.                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE 'BP13'         TO K010-INTERFACE-SYS.                           
                                                                                
           WRITE P04F010-REC FROM P04K010-REC.                                  
                                                                                
           ADD  1  TO WS-F010-WRITTEN.                                          
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       6000-WRITE-BP13FOUT.                                                     
      *-------------------------------------------------------------            
           MOVE F310-REGN-NO             TO FOUT-REGN.                          
           MOVE F310-NUM-ZONE            TO FOUT-ZONE.                          
           MOVE F310-CDE-NT              TO FOUT-NT.                            
           MOVE K853-NME-OCCP            TO FOUT-NAME.                          
           MOVE K853-NUM-NRIC            TO FOUT-NRIC.                          
           MOVE F310-SCH-ACC             TO FOUT-SCH-ACC.                       
           MOVE F310-NUM-BLK             TO FOUT-BLK.                           
           MOVE F310-NME-STREET          TO FOUT-STREET.                        
           MOVE F310-NUM-LEVEL           TO FOUT-LEVEL.                         
           MOVE F310-NUM-MAIN            TO FOUT-MAIN.                          
           MOVE K010-REASON-CODE         TO FOUT-DEBAR-CDE.                     
           MOVE K800-DTE-BALLOT          TO FOUT-DTE-BALLOT.                    
           MOVE F310-CDE-FLAT-TYPE       TO FOUT-FLAT-TYPE.                     
           MOVE K800-NUM-MCPS-POSTAL     TO FOUT-MCPS-POSTAL.                   
           MOVE K010-EFF-DATE            TO FOUT-DTE-EFFECT.                    
                                                                                
           WRITE BP13FOUT-REC.                                                  
                                                                                
           ADD 1                         TO WS-FOUT-WRITTEN.                    
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C22T CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY '01. NO BP13F310 REGN READ  : ' WS-F310-READ.                
           DISPLAY '02. NO BP13K800 REGN FD    : ' WS-K800-REGN-FND.            
           DISPLAY '03. NO BP13K800 REGN NOTFD : ' WS-K800-REGN-NOTFND.         
           DISPLAY '04. NO BP13K853 NRIC FD    : ' WS-K853-NRIC-FND.            
           DISPLAY '05. NO P04K010  REFNO FD   : ' WS-K010-REF-FND.             
           DISPLAY '06. NO P04K010  REASON MA  : ' WS-K010-REASON.              
           DISPLAY '07. NO P04K010  BYPASSED   : ' WS-K010-BYPASSED.            
           DISPLAY '08. NO P04F010  WRITTEN    : ' WS-F010-WRITTEN.             
           DISPLAY '09. NO BP13FOUT WRITTEN    : ' WS-FOUT-WRITTEN.             
           DISPLAY '  '.                                                        
           DISPLAY ' CHECK '.                                                   
           DISPLAY '      05.  =  06.  +  07.'.                                 
           DISPLAY '      08.  =  06.'.                                         
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13F310                                                       
                 BP13K853                                                       
                 P04K010                                                        
                 P04F010                                                        
                 BP13FOUT.                                                      
                                                                                
           IF WS-K853-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE BP13K853 FAIL ERROR ' WS-K853-STATUS               
              MOVE     WS-K853-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE P04K010 FAIL ERROR ' WS-K010-STATUS                
              MOVE     WS-K010-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
