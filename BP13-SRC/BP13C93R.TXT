       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C93R.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  22 JUN 2017.                                              
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *                                                             *           
      *   OBJECTIVE   : UPDATE/WRITE BP13KW3C BASED ON COMPLETE-TAG *           
      *                 TAG = 'M' UPDATE NON-MGPS FIELDS            *           
      *                 TAG = 'B' NO UPDATE                         *           
      *                 TAG = ' ' REPLACE RECORD                    *           
      *   INPUT FILES : BP13FW3C - BOOKING SUMMARY                  *           
      *   I-O    FILE : BP13KW3C - VSAM BOOKING SUMMARY FILE        *           
      *                                                             *           
      * =========================================================== *           
      * CHQ REQ  DATE       BY   DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136856 22/06/2017 FNP1 NEW PROGRAM                        *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FW3C ASSIGN       TO BP13FW3C.                            
                                                                                
           SELECT BP13KW3C ASSIGN       TO BP13KW3C                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KW3C-KEY-FLD                         
                           FILE STATUS  IS BP13KW3C-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13FW3C                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD     IS STANDARD                                        
            RECORDING MODE   IS F.                                              
       COPY BP13FW3C.                                                           
                                                                                
       FD   BP13KW3C                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BP13KW3C.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FW3C-READ                 PIC 9(06)   VALUE ZEROES.           
           05  WS-KW3C-FOUND                PIC 9(06)   VALUE ZEROES.           
           05  WS-KW3C-UPDATED              PIC 9(06)   VALUE ZEROES.           
           05  WS-KW3C-WRITTEN              PIC 9(06)   VALUE ZEROES.           
           05  WS-KW3C-BK-COMP              PIC 9(06)   VALUE ZEROES.           
                                                                                
       01  WS-FW3C-EOF                      PIC X       VALUE SPACES.           
       01  BP13KW3C-STATUS                  PIC 9(02)   VALUE ZEROES.           
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13FW3C       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECS        THRU 3000-EXIT                      
              UNTIL WS-FW3C-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13FW3C                                                 
                I-O    BP13KW3C.                                                
                                                                                
           IF BP13KW3C-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPEN ERROR BP13KW3C. STATUS : ' BP13KW3C-STATUS          
              MOVE BP13KW3C-STATUS       TO   RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2000-READ-BP13FW3C.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13FW3C AT END                                                 
                MOVE HIGH-VALUES TO BP13FW3C-REC                                
                MOVE 'Y' TO WS-FW3C-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-FW3C-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-RECS.                                                       
      *-----------------*                                                       
                                                                                
           PERFORM 3500-READ-BP13KW3C    THRU 3500-EXIT.                        
                                                                                
           PERFORM 2000-READ-BP13FW3C    THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3500-READ-BP13KW3C.                                                      
      *-----------------*                                                       
           MOVE SPACES                 TO BP13KW3C-REC.                         
           INITIALIZE                     BP13KW3C-REC                          
                                                                                
           MOVE FW3C-KEY-FLD           TO KW3C-KEY-FLD.                         
                                                                                
           READ BP13KW3C.                                                       
                                                                                
           EVALUATE BP13KW3C-STATUS                                             
           WHEN 00                                                              
              ADD 1 TO WS-KW3C-FOUND                                            
              IF KW3C-CDE-COMPLETE-TAG = 'B'                                    
                 ADD 1 TO WS-KW3C-BK-COMP                                       
              ELSE                                                              
                 PERFORM 4000-UPDATE-BP13KW3C  THRU 4000-EXIT                   
              END-IF                                                            
           WHEN 23                                                              
              PERFORM 5000-WRITE-BP13KW3C   THRU 5000-EXIT                      
           WHEN OTHER                                                           
                DISPLAY 'READ ERROR BP13KW3C, STATUS  '                         
                        BP13KW3C-STATUS                                         
                        ',  KEY    ' KW3C-KEY-FLD                               
                MOVE BP13KW3C-STATUS         TO RETURN-CODE                     
                PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4000-UPDATE-BP13KW3C.                                                    
      *------------------*                                                      
                                                                                
           MOVE FW3C-NUM-TOTAL-FLAT     TO KW3C-NUM-TOTAL-FLAT                  
           MOVE FW3C-NUM-AVAIL-CAT1     TO KW3C-NUM-AVAIL-CAT1                  
           MOVE FW3C-NUM-AVAIL-CAT2     TO KW3C-NUM-AVAIL-CAT2                  
           MOVE FW3C-NUM-AVAIL-CAT3     TO KW3C-NUM-AVAIL-CAT3                  
           MOVE FW3C-NUM-AVAIL-TOTAL    TO KW3C-NUM-AVAIL-TOTAL                 
           MOVE FW3C-NUM-LIMIT          TO KW3C-NUM-LIMIT                       
           MOVE FW3C-NUM-ZONE           TO KW3C-NUM-ZONE                        
           MOVE FW3C-NUM-AVAIL-SPR      TO KW3C-NUM-AVAIL-SPR                   
           MOVE FW3C-SPR-AVAIL          TO KW3C-SPR-AVAIL                       
           MOVE FW3C-NUM-UNIT-SPR       TO KW3C-NUM-UNIT-SPR                    
           MOVE FW3C-NUM-BTO-ZONE       TO KW3C-NUM-BTO-ZONE                    
           MOVE FW3C-NUM-TOTAL-GEN3     TO KW3C-NUM-TOTAL-GEN3                  
           MOVE FW3C-NUM-UNIT-CAT1-NOGEN3 TO KW3C-NUM-UNIT-CAT1-NOGEN3          
           MOVE FW3C-NUM-UNIT-CAT2-NOGEN3 TO KW3C-NUM-UNIT-CAT2-NOGEN3          
           MOVE FW3C-NUM-UNIT-CAT3-NOGEN3 TO KW3C-NUM-UNIT-CAT3-NOGEN3          
           MOVE FW3C-NME-PROJECT        TO KW3C-NME-PROJECT                     
           MOVE FW3C-NUM-NEW-TOWN       TO KW3C-NUM-NEW-TOWN                    
           MOVE FW3C-NUM-FLAT-TYPE      TO KW3C-NUM-FLAT-TYPE                   
           MOVE FW3C-NUM-NT-CHAR3       TO KW3C-NUM-NT-CHAR3                    
           MOVE FW3C-NUM-FT-CHAR2       TO KW3C-NUM-FT-CHAR2                    
           MOVE FW3C-NUM-TOTAL-BOOK     TO KW3C-NUM-TOTAL-BOOK                  
           MOVE FW3C-DTE-BOOKING-START  TO KW3C-DTE-BOOKING-START               
           MOVE FW3C-DTE-BOOKING-END    TO KW3C-DTE-BOOKING-END                 
           MOVE FW3C-DTE-EST-TOP        TO KW3C-DTE-EST-TOP                     
           MOVE FW3C-DTE-ACTUAL-TOP     TO KW3C-DTE-ACTUAL-TOP                  
           MOVE FW3C-NUM-TOT-APPL       TO KW3C-NUM-TOT-APPL                    
           MOVE FW3C-NUM-TOT-BALLOT     TO KW3C-NUM-TOT-BALLOT                  
           MOVE FW3C-NUM-TOT-CANCEL     TO KW3C-NUM-TOT-CANCEL                  
           MOVE FW3C-NUM-TOT-INVITED    TO KW3C-NUM-TOT-INVITED                 
           MOVE FW3C-NUM-LAST-QUEUE     TO KW3C-NUM-LAST-QUEUE                  
           MOVE FW3C-NUM-FLAT-PROJ      TO KW3C-NUM-FLAT-PROJ                   
           MOVE FW3C-NUM-BOOK-PROJ      TO KW3C-NUM-BOOK-PROJ                   
                                                                                
           IF KW3C-CDE-COMPLETE-TAG = ' '                                       
              MOVE FW3C-NUM-APPL-MGPS-22    TO KW3C-NUM-APPL-MGPS-22            
              MOVE FW3C-NUM-APPL-MGPS-23    TO KW3C-NUM-APPL-MGPS-23            
              MOVE FW3C-NUM-APPL-MGPS-23P   TO KW3C-NUM-APPL-MGPS-23P           
              MOVE FW3C-NUM-APPL-MGPS-24    TO KW3C-NUM-APPL-MGPS-24            
              MOVE FW3C-NUM-APPL-MGPS-25    TO KW3C-NUM-APPL-MGPS-25            
              MOVE FW3C-NUM-APPL-MGPS-32    TO KW3C-NUM-APPL-MGPS-32            
              MOVE FW3C-NUM-APPL-MGPS-33    TO KW3C-NUM-APPL-MGPS-33            
              MOVE FW3C-NUM-APPL-MGPS-33P   TO KW3C-NUM-APPL-MGPS-33P           
              MOVE FW3C-NUM-APPL-MGPS-34    TO KW3C-NUM-APPL-MGPS-34            
              MOVE FW3C-NUM-APPL-MGPS-35    TO KW3C-NUM-APPL-MGPS-35            
              MOVE FW3C-NUM-APPL-MGPS-3P2   TO KW3C-NUM-APPL-MGPS-3P2           
              MOVE FW3C-NUM-APPL-MGPS-3P3   TO KW3C-NUM-APPL-MGPS-3P3           
              MOVE FW3C-NUM-APPL-MGPS-3P3P  TO KW3C-NUM-APPL-MGPS-3P3P          
              MOVE FW3C-NUM-APPL-MGPS-3P4   TO KW3C-NUM-APPL-MGPS-3P4           
              MOVE FW3C-NUM-APPL-MGPS-3P5   TO KW3C-NUM-APPL-MGPS-3P5           
              MOVE FW3C-NUM-BOOK-MGPS-22    TO KW3C-NUM-BOOK-MGPS-22            
              MOVE FW3C-NUM-BOOK-MGPS-23    TO KW3C-NUM-BOOK-MGPS-23            
              MOVE FW3C-NUM-BOOK-MGPS-23P   TO KW3C-NUM-BOOK-MGPS-23P           
              MOVE FW3C-NUM-BOOK-MGPS-24    TO KW3C-NUM-BOOK-MGPS-24            
              MOVE FW3C-NUM-BOOK-MGPS-25    TO KW3C-NUM-BOOK-MGPS-25            
              MOVE FW3C-NUM-BOOK-MGPS-32    TO KW3C-NUM-BOOK-MGPS-32            
              MOVE FW3C-NUM-BOOK-MGPS-33    TO KW3C-NUM-BOOK-MGPS-33            
              MOVE FW3C-NUM-BOOK-MGPS-33P   TO KW3C-NUM-BOOK-MGPS-33P           
              MOVE FW3C-NUM-BOOK-MGPS-34    TO KW3C-NUM-BOOK-MGPS-34            
              MOVE FW3C-NUM-BOOK-MGPS-35    TO KW3C-NUM-BOOK-MGPS-35            
              MOVE FW3C-NUM-BOOK-MGPS-3P2   TO KW3C-NUM-BOOK-MGPS-3P2           
              MOVE FW3C-NUM-BOOK-MGPS-3P3   TO KW3C-NUM-BOOK-MGPS-3P3           
              MOVE FW3C-NUM-BOOK-MGPS-3P3P  TO KW3C-NUM-BOOK-MGPS-3P3P          
              MOVE FW3C-NUM-BOOK-MGPS-3P4   TO KW3C-NUM-BOOK-MGPS-3P4           
              MOVE FW3C-NUM-BOOK-MGPS-3P5   TO KW3C-NUM-BOOK-MGPS-3P5           
           END-IF.                                                              
                                                                                
           REWRITE BP13KW3C-REC.                                                
                                                                                
           EVALUATE BP13KW3C-STATUS                                             
           WHEN 00                                                              
                ADD 1 TO WS-KW3C-UPDATED                                        
           WHEN OTHER                                                           
                DISPLAY 'ERROR REWRITING BP13KW3C, STATUS  '                    
                        BP13KW3C-STATUS                                         
                        ',  KEY    ' KW3C-KEY-FLD                               
                MOVE BP13KW3C-STATUS         TO RETURN-CODE                     
                PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       5000-WRITE-BP13KW3C.                                                     
      *------------------*                                                      
           MOVE BP13FW3C-REC       TO BP13KW3C-REC.                             
                                                                                
           WRITE BP13KW3C-REC.                                                  
                                                                                
           EVALUATE BP13KW3C-STATUS                                             
           WHEN 00                                                              
                ADD 1 TO WS-KW3C-WRITTEN                                        
           WHEN OTHER                                                           
                DISPLAY 'ERROR WRITING BP13KW3C, STATUS  '                      
                        BP13KW3C-STATUS                                         
                        ',  KEY    ' KW3C-KEY-FLD                               
                MOVE BP13KW3C-STATUS         TO RETURN-CODE                     
                PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '*==========BP13C93R=========================*'.             
           DISPLAY '*-------------------------------------------*'.             
           DISPLAY '  RECS READ FROM BP13FW3C    : ' WS-FW3C-READ.              
           DISPLAY '  RECS FOUND IN  BP13KW3C    : ' WS-KW3C-FOUND.             
           DISPLAY '  RECS BOOKING EX COMPLETED  : ' WS-KW3C-BK-COMP.           
           DISPLAY '  RECS UPDATED IN BP13KW3C   : ' WS-KW3C-UPDATED.           
           DISPLAY '  RECS WRITTEN IN BP13KW3C   : ' WS-KW3C-WRITTEN.           
           DISPLAY '*===========================================*'.             
                                                                                
           CLOSE BP13FW3C                                                       
                 BP13KW3C.                                                      
                                                                                
           IF BP13KW3C-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR BP13KW3C. STATUS : ' BP13KW3C-STATUS         
              MOVE BP13KW3C-STATUS       TO   RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
