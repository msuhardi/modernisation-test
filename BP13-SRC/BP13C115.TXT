       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C115.                                                    
       AUTHOR.     M.PRINCE.                                                    
       DATE-WRITTEN. 28/08/1998.                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : EC SYSTEM                             *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE : PROGRAM TO PRINT ESSENTIAL FAMILY WITH  *                
      *                RDB HOUSEHOLD INFOR NAD DEBARMENT INFOR *                
      *                FOR CREDIT ASSESSMENT CHECK             *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY   DATE   DESCRIPTION                        *                
      * ======  ===  ====   ============                       *                
      * BP130542 MPA 980828 - NEW PGM                          *                
      * BP130630 NOR 990116 - CHGE F715 TO F716 EXPAND TO 350  *                
      * BP130897 AKT 000731 - F558 EXPAND TO X(600)            *                
      **********************************************************                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F558   ASSIGN TO BP13F558.                                
                                                                                
           SELECT BP13F716   ASSIGN TO BP13F716.                                
                                                                                
           SELECT BP13K060   ASSIGN TO BP13K060                                 
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K060-KEY-FLD                                  
                  FILE STATUS  IS BP13K060-STATUS.                              
                                                                                
           SELECT BP13L115   ASSIGN TO BP13L115.                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F558                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
                                                                                
       COPY BP13F558.                                                           
                                                                                
       FD   BP13F716                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 350 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
                                                                                
       COPY BP13F716.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS 25   CHARACTERS.                                    
                                                                                
       COPY BP13K060.                                                           
                                                                                
       FD   BP13L115                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
                                                                                
       01   PRINT-REC              PIC X(132).                                  
                                                                                
      ***************************                                               
       WORKING-STORAGE SECTION.                                                 
      ***************************                                               
       01  BP13K060-STATUS         PIC 9(2)    VALUE ZEROES.                    
                                                                                
       01  WS-K060-READ            PIC 9(8)    VALUE 0.                         
       01  WS-F558-READ            PIC 9(8)    VALUE 0.                         
       01  WS-F716-READ            PIC 9(8)    VALUE 0.                         
                                                                                
       01  WS-CURRENT-REGN         PIC X(8)    VALUE SPACES.                    
       01  WS-CC                   PIC X(2)    VALUE SPACES.                    
       01  WS-PREV-F558-REGN       PIC X(8)    VALUE SPACES.                    
       01  WS-PREV-F716-REGN       PIC X(8)    VALUE SPACES.                    
       01  WS-F716-NRIC            PIC X(9)    VALUE SPACES.                    
       01  WS-F558-NRIC            PIC X(9)    VALUE SPACES.                    
       01  WS-WRITE-F716           PIC X       VALUE 'N'.                       
       01  WS-FIRST                PIC X       VALUE 'N'.                       
       01  WS-REGN-CHGD            PIC X       VALUE 'N'.                       
                                                                                
       01  EOF-BP13F558            PIC X       VALUE 'N'.                       
       01  EOF-BP13F716            PIC X       VALUE 'N'.                       
                                                                                
       01  L115-COUNTERS.                                                       
           05 WS-SNO               PIC 9(4)     VALUE 0.                        
           05 WS-LINE-CTR          PIC 9(2)     VALUE 66.                       
           05 WS-MR-REGN-CNT       PIC 9(8)     VALUE 0.                        
           05 WS-MR-NRIC-CNT       PIC 9(8)     VALUE 0.                        
           05 WS-PAGENO            PIC 9(4)     VALUE 0.                        
                                                                                
       01  L115-PR-HEAD-01.                                                     
           05  FILLER              PIC X(9)      VALUE ' BP13L115'.             
           05  FILLER              PIC X(4)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(21)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(23)     VALUE                          
                                    'C O M M I T M E N T '.                     
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  L115-DATE           PIC X(10).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(6)      VALUE 'PAGE :'.                
           05  L115-PAGENO         PIC ZZZ9 .                                   
                                                                                
       01  L115-PR-HEAD-02.                                                     
           05  FILLER              PIC X(132) VALUE                             
           '        REPORT ON ESSENTIAL FAMILY WITH RDB AND DEBARMENT IN        
      -    'FORMATION FOR CREDIT ASSESSMENT CHECK'.                             
                                                                                
       01  L115-PR-HEAD-03.                                                     
           05  FILLER              PIC X(91) VALUE                              
           '  S/NO REGN NO. F558      SOC             DEBARMENT   DEBARM        
      -    'ENT  RDB       RDB       LESSEE'.                                   
           05  FILLER              PIC X(41) VALUE                              
           ' RDB               DATE       DATE'.                                
                                                                                
       01  L115-PR-HEAD-04.                                                     
           05  FILLER              PIC X(91) VALUE                              
           '                REG-NRIC  RELATION        SCH-ACCNT   EFF DA        
      -    'TE   SCH-ACCNT NRIC      STATUS'.                                   
           05  FILLER              PIC X(41) VALUE                              
           ' RELATIONSHIP      MOVE-IN    MOVE-OUT'.                            
                                                                                
       01  L115-PR-DETAILS.                                                     
           05  L115-SNO            PIC ZZZZZZ.                                  
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L115-REG-NO         PIC X(08).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L115-REG-NRIC       PIC X(09).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L115-SOC-RELN       PIC X(15).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L115-DEB-SCHACC     PIC X(11).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L115-DEB-EFF-DTE    PIC X(10).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L115-RDB-SCHACC     PIC X(09).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L115-RDB-NRIC       PIC X(09).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L115-LESSEE-STAT    PIC X(06).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L115-RDB-RELN       PIC X(17).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L115-MOVEIN         PIC X(10).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L115-MOVEOUT        PIC X(10).                                   
                                                                                
      ***************************                                               
       PROCEDURE DIVISION.                                                      
      ***************************                                               
       000-MAIN.                                                                
      *----------------------------------------------------------------*        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F558      THRU 200-EXIT.                            
           PERFORM 300-READ-F716      THRU 300-EXIT.                            
           PERFORM 400-MATCH-REGN     THRU 400-EXIT                             
             UNTIL F558-NUM-REGN = HIGH-VALUE                                   
             AND   F716-NUM-REGN = HIGH-VALUE.                                  
           PERFORM 900-CLOSE-FILES    THRU 900-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F558                                                 
                       BP13F716                                                 
                       BP13K060                                                 
                OUTPUT BP13L115.                                                
                                                                                
           IF BP13K060-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'FILE BP13K060 OPEN ERROR'                                
              DISPLAY 'ERROR STATUS = ' BP13K060-STATUS                         
              MOVE BP13K060-STATUS     TO RETURN-CODE                           
              PERFORM 900-CLOSE-FILES  THRU 900-EXIT.                           
                                                                                
           MOVE 'Y'                     TO WS-FIRST.                            
           PERFORM 150-GET-CURRENT-DATE THRU 150-EXIT.                          
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       150-GET-CURRENT-DATE.                                                    
      *----------------------------------------------------------------*        
           STRING FUNCTION CURRENT-DATE(7:2) '/'                                
                  FUNCTION CURRENT-DATE(5:2) '/'                                
                  FUNCTION CURRENT-DATE(1:4)  DELIMITED BY SIZE                 
                  INTO L115-DATE.                                               
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-F558.                                                           
      *-------------------------------------------------------------            
           READ BP13F558 AT END                                                 
                MOVE HIGH-VALUE       TO F558-NUM-REGN                          
                                         F558-NUM-NRIC                          
                MOVE 'Y'              TO EOF-BP13F558                           
           GO TO 200-EXIT.                                                      
                                                                                
           ADD  1                     TO WS-F558-READ.                          
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-READ-F716.                                                           
      *----------------------------------------------------------------*        
           READ BP13F716 AT END                                                 
                MOVE 'Y'              TO EOF-BP13F716                           
                MOVE HIGH-VALUE       TO F716-NUM-REGN                          
                                         F716-NUM-NRIC                          
                GO TO 300-EXIT.                                                 
                                                                                
           ADD 1                      TO WS-F716-READ.                          
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-MATCH-REGN.                                                          
      *-------------------------------------------------------------            
           IF F558-NUM-REGN = F716-NUM-REGN                                     
              ADD     1                TO   WS-MR-REGN-CNT                      
              PERFORM 500-MATCH-NRIC   THRU 500-EXIT                            
               UNTIL  (F558-NUM-NRIC = HIGH-VALUES                              
                AND    F716-NUM-NRIC = HIGH-VALUES)                             
              MOVE WS-F558-NRIC        TO F558-NUM-NRIC                         
              MOVE WS-F716-NRIC        TO F716-NUM-NRIC                         
           ELSE                                                                 
              IF F558-NUM-REGN < F716-NUM-REGN                                  
                 PERFORM 200-READ-F558 THRU 200-EXIT                            
              ELSE                                                              
                 PERFORM 300-READ-F716 THRU 300-EXIT.                           
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-MATCH-NRIC.                                                          
      *----------------------------------------------------------------*        
           INITIALIZE L115-PR-DETAILS.                                          
           MOVE F716-NUM-REGN          TO WS-CURRENT-REGN                       
                                          L115-REG-NO.                          
                                                                                
           IF F558-NUM-NRIC = F716-NUM-NRIC                                     
              ADD  1                   TO WS-MR-NRIC-CNT                        
              MOVE F716-NUM-NRIC       TO L115-REG-NRIC                         
              MOVE F558-NUM-NRIC       TO L115-RDB-NRIC                         
              MOVE 'Y'                 TO WS-WRITE-F716                         
              MOVE F558-NUM-REGN       TO WS-PREV-F558-REGN                     
              MOVE F716-NUM-REGN       TO WS-PREV-F716-REGN                     
              PERFORM 600-OUTPUT-F716  THRU 600-EXIT                            
              PERFORM 200-READ-F558    THRU 200-EXIT                            
              PERFORM 300-READ-F716    THRU 300-EXIT                            
           ELSE                                                                 
             IF F558-NUM-NRIC < F716-NUM-NRIC                                   
                MOVE F558-NUM-NRIC         TO L115-REG-NRIC                     
                MOVE SPACES                TO L115-RDB-NRIC                     
                MOVE 'N'                   TO WS-WRITE-F716                     
                PERFORM 600-OUTPUT-F716    THRU 600-EXIT                        
                MOVE F558-NUM-REGN         TO WS-PREV-F558-REGN                 
                MOVE F716-NUM-REGN         TO WS-PREV-F716-REGN                 
                PERFORM 200-READ-F558      THRU 200-EXIT                        
             ELSE                                                               
               IF F558-NUM-NRIC > F716-NUM-NRIC                                 
                  MOVE F716-NUM-NRIC       TO L115-RDB-NRIC                     
                  MOVE SPACES              TO L115-REG-NRIC                     
                  MOVE 'Y'                 TO WS-WRITE-F716                     
                  PERFORM 600-OUTPUT-F716  THRU 600-EXIT                        
                  MOVE F716-NUM-REGN       TO WS-PREV-F716-REGN                 
                  MOVE F558-NUM-REGN       TO WS-PREV-F558-REGN                 
                  PERFORM 300-READ-F716    THRU 300-EXIT.                       
                                                                                
           IF  F558-NUM-REGN  NOT =  WS-PREV-F558-REGN                          
               MOVE F558-NUM-NRIC          TO WS-F558-NRIC                      
               MOVE HIGH-VALUES            TO F558-NUM-NRIC.                    
                                                                                
           IF  F716-NUM-REGN  NOT =  WS-PREV-F716-REGN                          
               MOVE F716-NUM-NRIC          TO WS-F716-NRIC                      
               MOVE HIGH-VALUES            TO F716-NUM-NRIC.                    
                                                                                
           IF (F558-NUM-NRIC  = HIGH-VALUES)  AND                               
              (F716-NUM-NRIC  = HIGH-VALUES)                                    
               MOVE 'Y'                    TO WS-REGN-CHGD.                     
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-OUTPUT-F716.                                                         
      *----------------------------------------------------------------*        
           IF WS-LINE-CTR >= 55                                                 
              PERFORM 700-PRINT-HEADER THRU 700-EXIT                            
              MOVE 6                   TO WS-LINE-CTR                           
           END-IF.                                                              
                                                                                
           IF (WS-REGN-CHGD = 'Y') OR                                           
              (WS-FIRST = 'Y')                                                  
                MOVE SPACES            TO PRINT-REC                             
                WRITE PRINT-REC                                                 
                ADD  1                 TO WS-LINE-CTR                           
                ADD  1                 TO WS-SNO                                
                MOVE WS-SNO            TO L115-SNO                              
                MOVE F716-NUM-REGN     TO WS-CURRENT-REGN                       
                MOVE F716-NUM-REGN     TO L115-REG-NO                           
           ELSE                                                                 
             IF WS-LINE-CTR > 6                                                 
                MOVE SPACES TO L115-REG-NO.                                     
                                                                                
           STRING  F558-DTE-DEBAR-EFF(7:2) '/'                                  
                   F558-DTE-DEBAR-EFF(5:2) '/'                                  
                   F558-DTE-DEBAR-EFF(1:4)                                      
           DELIMITED BY SIZE INTO L115-DEB-EFF-DTE.                             
                                                                                
           IF WS-REGN-CHGD = 'Y' OR                                             
              (WS-FIRST = 'Y')                                                  
              MOVE F716-NUM-SCH-ACCNT TO L115-RDB-SCHACC                        
           ELSE                                                                 
              MOVE SPACES             TO L115-RDB-SCHACC                        
           END-IF.                                                              
                                                                                
           IF WS-REGN-CHGD = 'Y' OR                                             
              (WS-FIRST = 'Y')                                                  
              MOVE F558-NUM-HDB-REF-DEBAR TO L115-DEB-SCHACC                    
           ELSE                                                                 
              MOVE SPACES                 TO L115-DEB-SCHACC                    
           END-IF.                                                              
                                                                                
           IF L115-REG-NRIC NOT = SPACES AND LOW-VALUES                         
             IF F558-CDE-RELATIONSHIP = '00'                                    
                MOVE 'APPLICANT'        TO L115-SOC-RELN                        
             ELSE                                                               
                MOVE SPACES             TO K060-KEY-FLD                         
                MOVE '35'               TO K060-SERIAL-NO                       
                MOVE F558-CDE-RELATIONSHIP TO K060-CODE                         
                READ BP13K060                                                   
               IF BP13K060-STATUS = 0                                           
                  MOVE K060-DESC(1:17) TO L115-SOC-RELN                         
               ELSE                                                             
                  MOVE SPACES          TO L115-SOC-RELN                         
               END-IF                                                           
             END-IF                                                             
           END-IF.                                                              
                                                                                
           IF WS-WRITE-F716 = 'N'                                               
              MOVE SPACES             TO PRINT-REC                              
              WRITE PRINT-REC         FROM L115-PR-DETAILS                      
              MOVE SPACES             TO L115-PR-DETAILS                        
              MOVE 'N'                TO WS-FIRST                               
              MOVE 'N'                TO WS-REGN-CHGD                           
              ADD 1                   TO WS-LINE-CTR                            
           GO TO  600-EXIT.                                                     
                                                                                
           IF F716-DTE-MOVING-IN NOT = SPACES AND LOW-VALUES                    
                                                                                
              STRING F716-DTE-MOVING-IN(7:2) '/'                                
                     F716-DTE-MOVING-IN(5:2) '/'                                
                     F716-DTE-MOVING-IN(1:4)                                    
                     DELIMITED BY SIZE                                          
           INTO L115-MOVEIN.                                                    
                                                                                
           IF F716-DTE-MOVING-OUT NOT = SPACES AND LOW-VALUES                   
                                                                                
              STRING F716-DTE-MOVING-OUT(7:2) '/'                               
                     F716-DTE-MOVING-OUT(5:2) '/'                               
                     F716-DTE-MOVING-OUT(1:4)                                   
                     DELIMITED BY SIZE                                          
           INTO L115-MOVEOUT.                                                   
                                                                                
           IF F716-CDE-OCCUPIER NOT = SPACES AND LOW-VALUES                     
              IF F716-CDE-OCCUPIER = 0                                          
                   MOVE 'LESSEE'      TO L115-LESSEE-STAT                       
              ELSE                                                              
              IF F716-CDE-OCCUPIER = 1                                          
                   MOVE 'CO-LES'      TO L115-LESSEE-STAT                       
              ELSE                                                              
              IF F716-CDE-OCCUPIER = 2                                          
                   MOVE 'OCCUP '      TO L115-LESSEE-STAT                       
              ELSE                                                              
                   MOVE 'OTHER '      TO L115-LESSEE-STAT                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F716-CDE-RELATIONSHIP = '00'                                      
              MOVE 'LESSEE'           TO L115-RDB-RELN                          
           ELSE                                                                 
              MOVE SPACES             TO K060-KEY-FLD                           
              MOVE '35'               TO K060-SERIAL-NO                         
              MOVE F716-CDE-RELATIONSHIP TO K060-CODE                           
              READ BP13K060                                                     
              IF BP13K060-STATUS = 0                                            
                 MOVE K060-DESC(1:17) TO L115-RDB-RELN                          
              ELSE                                                              
                 MOVE SPACES          TO L115-RDB-RELN                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE SPACES                TO PRINT-REC.                             
           WRITE PRINT-REC            FROM L115-PR-DETAILS.                     
                                                                                
           MOVE 'N'                   TO WS-FIRST.                              
           MOVE 'N'                   TO WS-REGN-CHGD.                          
                                                                                
           MOVE SPACES                TO L115-PR-DETAILS.                       
           ADD 1                      TO WS-LINE-CTR.                           
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       700-PRINT-HEADER.                                                        
      *----------------------------------------------------------------*        
                                                                                
           ADD 1 TO WS-PAGENO.                                                  
           MOVE  SPACES               TO PRINT-REC.                             
           MOVE  WS-PAGENO            TO L115-PAGENO.                           
                                                                                
           WRITE PRINT-REC FROM L115-PR-HEAD-01 AFTER PAGE.                     
           WRITE PRINT-REC FROM L115-PR-HEAD-02 AFTER 2.                        
           WRITE PRINT-REC FROM L115-PR-HEAD-03 AFTER 2.                        
           WRITE PRINT-REC FROM L115-PR-HEAD-04.                                
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-FILES.                                                         
      *----------------------------------------------------------------*        
                                                                                
                                                                                
           DISPLAY 'PROGRAM-ID : BP13C115 '.                                    
           DISPLAY 'CURR-DATE  : ' L115-DATE.                                   
           DISPLAY 'BP13F558-RECORDS READ    : ' WS-F558-READ.                  
           DISPLAY 'BP13F716-RECORDS READ    : ' WS-F716-READ.                  
           DISPLAY 'NO OF REGN NOS MATCH     : ' WS-MR-REGN-CNT.                
           DISPLAY 'NO OF NRICS MATCHED      : ' WS-MR-NRIC-CNT.                
                                                                                
           CLOSE BP13F558                                                       
                 BP13F716                                                       
                 BP13K060                                                       
                 BP13L115.                                                      
                                                                                
           IF  BP13K060-STATUS NOT = ZEROS                                      
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'BP13K060-STATUS ' BP13K060-STATUS.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
