       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C624.                                                 
      *AUTHOR.        IT1.                                                      
      *DATE-WRITTEN.  18/08/1997.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *  THIS PROGRAM WAS MODIFIED FROM BP13C514.  THE CHANGES     *            
      *  IS TO ISSUE SINGLE QUEUE NUMBER TO 4RM AND 5RM CASES,     *            
      *  CONTROL BREAK BY NEW TOWN ONLY (WITHOUT THE FLAT TYPE)    *            
      * ---------------------------------------------------------- *            
      *                                                            *            
      *  OBJECTIVE  :                                              *            
      *    1.  PRINT SUCCESSFUL REQUEST ASSIGNED REGISTRATION NO.  *            
      *    2.  ASSIGN QUEUE NO BASED ON NEW TOWN, FLAT TYPE AND    *            
      *        REGISTRATION NO.                                    *            
      *                                                            *            
      *  I-O      : 1. BP13K740 - BALLOT FILE                      *            
      *                                                            *            
      *  INPUT    : 1. BP13F740 - BALLOT FILE (SEQ)                *            
      *             2. BP13K825 - SERS OCCUPIER FILE               *            
      *                                                            *            
      *  LISTING  : 1. BP13L624 - SUCCESSFUL REQUEST REPORT        *            
      * ---------------------------------------------------------- *            
      * BP130364 LSB 19970818  ISSUE SINGLE QUEUE NO FOR 4R & 5R   *            
      * BP130652 NOR 19990222  EXPAND F740 & K740 RECL 300 TO 500  *            
      * BP130857 SSS 20000707  REMOVE 'TEAM'                       *            
      * BP132162 ANC 20020711  ADD 'T' TAG FOR RESIDENT-HA         *            
      * BP132344 YCH2 13052003 CHGE ZE TO LFP                      *            
      * BP132463 SK  09122003  CATER FOR HA1,HA2,HA3 & HA4         *            
      * BP132500 YCH2 03042004 CATER FOR DIFFERENT BAND            *            
      * BP135944 SMR2 15092015 REMOVE REFERENCE TO CDE-NT6/7/8     *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3900.                                               
       OBJECT-COMPUTER. IBM-3900.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F740 ASSIGN  TO BP13F740.                                 
                                                                                
           SELECT BP13K740  ASSIGN TO BP13K740                                  
                  ACCESS MODE      IS DYNAMIC                                   
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K740-KEY-FLD                              
                  FILE STATUS      IS WS-K740-STATUS.                           
                                                                                
           SELECT BP13K825  ASSIGN TO BP13K825                                  
                  ACCESS MODE      IS DYNAMIC                                   
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K825-KEY-FLD                              
                  FILE STATUS      IS WS-K825-STATUS.                           
                                                                                
           SELECT BP13L624  ASSIGN TO BP13L624.                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F740                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 500 CHARACTERS               
                                   RECORDING MODE IS F                          
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F740.                                                           
                                                                                
       FD  BP13K740                                                             
           BLOCK  CONTAINS   0 RECORDS                                          
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL  RECORDS  ARE STANDARD.                                        
       COPY BP13K740.                                                           
                                                                                
       FD  BP13K825                                                             
           BLOCK  CONTAINS   0 RECORDS                                          
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS   ARE STANDARD.                                        
       COPY BP13K825.                                                           
                                                                                
       FD  BP13L624                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE F                                                     
           LABEL RECORDS ARE OMITTED.                                           
       01  L624-PRT-REC                PIC X(132).                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-K740-STATUS              PIC X(2)    VALUE SPACES.                
       01  WS-K825-STATUS              PIC X(2)    VALUE SPACES.                
       01  WS-NUM-QUEUE                PIC 9(5)    VALUE ZEROES.                
       01  WS-F740-READ                PIC 9(8)    VALUE ZEROES.                
       01  WS-REWRITE                  PIC 9(8)    VALUE ZEROES.                
       01  WS-BYPASS                   PIC 9(8)    VALUE ZEROES.                
                                                                                
       01  WS-NAME                     PIC X(66)   VALUE SPACES.                
       01  WS-DATE                     PIC X(8)    VALUE SPACES.                
                                                                                
       01  WS-KEY.                                                              
           03  WS-CDE-SUCC-NT-ZONE     PIC X(3)    VALUE SPACES.                
                                                                                
       01  WS-PREV-KEY.                                                         
           03  WS-PREV-CDE-SUCC-NT     PIC X(3)    VALUE SPACES.                
                                                                                
       01  WS-REPORT-CNT.                                                       
           03  WS-LINE-CNT             PIC 9(2)    VALUE 65.                    
           03  WS-PAGE-CNT             PIC 9(5)    VALUE ZEROES.                
                                                                                
       01  WS-L624-HDG1.                                                        
           03  FILLER              PIC X(14)   VALUE 'BP13L624'.                
           03  FILLER              PIC X(8)    VALUE 'HDB3'.                    
           03  FILLER              PIC X(18)   VALUE SPACES.                    
           03  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           03  FILLER              PIC X(21)   VALUE SPACES.                    
           03  FILLER              PIC X(7)    VALUE 'DATE: '.                  
           03  L624-DATE           PIC X(10)   VALUE SPACES.                    
           03  FILLER              PIC X(3)    VALUE SPACES.                    
           03  FILLER              PIC X(7)    VALUE 'PAGE: '.                  
           03  L624-PAGENO         PIC ZZZZ9   VALUE ZEROS.                     
                                                                                
       01  WS-L624-HDG3.                                                        
           03  FILLER              PIC X(30) VALUE SPACES.                      
           03  FILLER              PIC X(60) VALUE                              
               ' LIST OF SUCCESSFUL REQUEST ASSIGNED REGISTRATION NO '.         
                                                                                
       01  WS-L624-HDG4.                                                        
           03  FILLER              PIC X(12) VALUE 'NEW TOWN : '.               
           03  L624-NEW-TOWN       PIC X(3)  VALUE SPACES.                      
           03  FILLER              PIC X(5)  VALUE SPACES.                      
           03  FILLER              PIC X(15) VALUE SPACES.                      
           03  FILLER              PIC X(93) VALUE SPACES.                      
                                                                                
       01  WS-L624-HDG5.                                                        
           03  FILLER              PIC X     VALUE SPACES.                      
           03  FILLER              PIC X(4)  VALUE 'S/N'.                       
           03  FILLER              PIC X(13) VALUE SPACES.                      
           03  FILLER              PIC X(9)  VALUE 'RANDOM NO'.                 
           03  FILLER              PIC X(13) VALUE SPACES.                      
           03  FILLER              PIC X(6)  VALUE 'REF NO'.                    
           03  FILLER              PIC X(4)  VALUE SPACES.                      
           03  FILLER              PIC X(7)  VALUE 'REGN NO'.                   
           03  FILLER              PIC X(8)  VALUE SPACES.                      
           03  FILLER              PIC X(15) VALUE 'APPLICANT NAMES'.           
           03  FILLER              PIC X(7)  VALUE SPACES.                      
           03  FILLER              PIC X(4)  VALUE 'NRIC'.                      
           03  FILLER              PIC X(5)  VALUE SPACES.                      
           03  FILLER              PIC X(2)  VALUE 'FT'.                        
           03  FILLER              PIC X(2)  VALUE SPACES.                      
           03  FILLER              PIC X(3)  VALUE 'CAT'.                       
           03  FILLER              PIC X(5)  VALUE SPACES.                      
           03  FILLER              PIC X(2)  VALUE 'JB'.                        
           03  FILLER              PIC X(6)  VALUE SPACES.                      
           03  FILLER              PIC X(5)  VALUE 'ALLOC'.                     
           03  FILLER              PIC X(3)  VALUE SPACES.                      
           03  FILLER              PIC X(3)  VALUE 'LFP'.                       
                                                                                
       01  WS-L624-HDG6.                                                        
           03  FILLER              PIC X(87) VALUE SPACES.                      
           03  FILLER              PIC X(4)  VALUE ' NO '.                      
           03  FILLER              PIC X(15) VALUE SPACES.                      
           03  FILLER              PIC X(4)  VALUE 'CODE'.                      
           03  FILLER              PIC X(7)  VALUE SPACES.                      
           03  FILLER              PIC X(4)  VALUE 'SCH '.                      
                                                                                
       01  WS-L624-DTL.                                                         
           03  L624-SNO            PIC ZZZZ9.                                   
           03  FILLER              PIC X(05) VALUE SPACES.                      
           03  L624-RANDOM-NO      PIC X(26) VALUE SPACES.                      
           03  FILLER              PIC X(04) VALUE SPACES.                      
           03  L624-REFNO          PIC X(06) VALUE SPACES.                      
           03  FILLER              PIC X(02) VALUE SPACES.                      
           03  L624-TAG-RES        PIC X(01) VALUE SPACES.                      
           03  FILLER              PIC X(01) VALUE SPACES.                      
           03  L624-REGNO          PIC X(08) VALUE SPACES.                      
           03  FILLER              PIC X(04) VALUE SPACES.                      
           03  L624-NAME           PIC X(22) VALUE SPACES.                      
           03  FILLER              PIC X(01) VALUE SPACES.                      
           03  L624-NRICNO         PIC X(09) VALUE SPACES.                      
           03  FILLER              PIC X(02) VALUE SPACES.                      
           03  L624-FT             PIC X(02) VALUE SPACES.                      
           03  FILLER              PIC X(03) VALUE SPACES.                      
           03  L624-CAT            PIC X(01) VALUE SPACES.                      
           03  FILLER              PIC X(06) VALUE SPACES.                      
           03  L624-JB-CDE         PIC X(01) VALUE SPACES.                      
           03  FILLER              PIC X(08) VALUE SPACES.                      
           03  L624-ALLOC-SCH      PIC X(03) VALUE SPACES.                      
           03  FILLER              PIC X(04) VALUE SPACES.                      
           03  L624-LFP            PIC X(03) VALUE SPACES.                      
                                                                                
       01  WS-L624-TOT1.                                                        
           03  FILLER              PIC X(05) VALUE SPACES.                      
           03  FILLER              PIC X(35) VALUE                              
               'NO OF RECORDS READ FROM BP13F740 '.                             
           03  L624-READ           PIC ZZZZZZZ9.                                
           03  FILLER              PIC X(04) VALUE SPACES.                      
                                                                                
       01  WS-L624-TOT2.                                                        
           03  FILLER              PIC X(05) VALUE SPACES.                      
           03  FILLER              PIC X(35) VALUE                              
               'NO OF RECORDS UPDATED TO P13K740 '.                             
           03  L624-REWRITE        PIC ZZZZZZZ9.                                
           03  FILLER              PIC X(04) VALUE SPACES.                      
                                                                                
       01  WS-L624-TOT3.                                                        
           03  FILLER              PIC X(05) VALUE SPACES.                      
           03  FILLER              PIC X(35) VALUE                              
               'NO OF RECORDS WITHOUT SUCC NT    '.                             
           03  L624-BYPASS         PIC ZZZZZZZ9.                                
           03  FILLER              PIC X(04) VALUE SPACES.                      
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       010-OPEN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F740                                                 
                       BP13K825                                                 
                I-O    BP13K740                                                 
                OUTPUT BP13L624.                                                
                                                                                
           IF WS-K740-STATUS NOT = ZEROES AND                                   
              WS-K740-STATUS NOT = '97'                                         
              DISPLAY ' OPENING BP13K740 ERROR ' WS-K740-STATUS                 
              MOVE WS-K740-STATUS TO RETURN-CODE                                
              PERFORM 030-CLOSE-ROUTINE.                                        
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND                                   
              WS-K825-STATUS NOT = '97'                                         
              DISPLAY ' OPENING BP13K825 ERROR ' WS-K825-STATUS                 
              MOVE WS-K825-STATUS TO RETURN-CODE                                
              PERFORM 030-CLOSE-ROUTINE.                                        
                                                                                
      *-------------------------------------------------------------            
       020-PROCESS-ROUTINE.                                                     
      *-------------------------------------------------------------            
           PERFORM 100-READ-F740      THRU 100-EXIT.                            
           PERFORM 200-PROCESS-INDATA THRU 200-EXIT                             
                       UNTIL WS-KEY = HIGH-VALUE.                               
                                                                                
      *-------------------------------------------------------------            
       030-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 600-PRINT-TOTAL THRU 600-EXIT.                               
           CLOSE BP13F740                                                       
                 BP13K740                                                       
                 BP13K825                                                       
                 BP13L624.                                                      
                                                                                
           IF WS-K740-STATUS NOT = ZEROES AND                                   
              WS-K740-STATUS NOT = '97'                                         
              DISPLAY ' CLOSING BP13K740 ERROR ' WS-K740-STATUS.                
                                                                                
           IF WS-K825-STATUS NOT = ZEROES AND                                   
              WS-K825-STATUS NOT = '97'                                         
              DISPLAY ' CLOSING BP13K825 ERROR ' WS-K825-STATUS.                
                                                                                
           STOP RUN.                                                            
                                                                                
       030-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       100-READ-F740.                                                           
      *-------------------------------------------------------------            
           READ BP13F740 AT END MOVE HIGH-VALUE TO WS-KEY                       
                GO TO 100-EXIT.                                                 
                                                                                
           ADD 1 TO WS-F740-READ.                                               
                                                                                
           IF F740-CDE-SUCC-NT-ZONE = SPACES                                    
              ADD 1 TO WS-BYPASS                                                
              GO TO 100-READ-F740                                               
           ELSE                                                                 
              MOVE F740-CDE-SUCC-NT-ZONE TO WS-CDE-SUCC-NT-ZONE.                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       200-PROCESS-INDATA.                                                      
      *-------------------------------------------------------------            
           IF WS-KEY NOT = WS-PREV-KEY                                          
              MOVE WS-CDE-SUCC-NT-ZONE TO L624-NEW-TOWN                         
              PERFORM 2000-REPORT-HDG THRU 2000-EXIT                            
              MOVE 1 TO WS-NUM-QUEUE                                            
              MOVE WS-KEY TO WS-PREV-KEY                                        
           ELSE                                                                 
              ADD 1 TO WS-NUM-QUEUE.                                            
                                                                                
           MOVE F740-KEY-FLD  TO K740-KEY-FLD.                                  
           READ BP13K740                                                        
           IF WS-K740-STATUS = ZEROES OR '02'                                   
              MOVE WS-NUM-QUEUE TO K740-NUM-NT-FT-QUEUE                         
              REWRITE BP13K740-REC                                              
              IF WS-K740-STATUS = ZEROES OR '02'                                
                 ADD 1 TO WS-REWRITE                                            
              ELSE                                                              
                 DISPLAY 'REWRITE BP13K740 ERROR ' WS-K740-STATUS               
                 DISPLAY 'K740-NUM-REGN          ' F740-NUM-REGN                
           ELSE                                                                 
              NEXT SENTENCE.                                                    
                                                                                
           PERFORM 2100-REPORT-DTL THRU 2100-EXIT.                              
           PERFORM 100-READ-F740 THRU 100-EXIT.                                 
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-PRINT-TOTAL.                                                         
      *-------------------------------------------------------------            
           MOVE  SPACES         TO L624-PRT-REC.                                
           MOVE WS-F740-READ    TO L624-READ.                                   
           MOVE WS-REWRITE      TO L624-REWRITE.                                
           MOVE WS-BYPASS       TO L624-BYPASS.                                 
                                                                                
           WRITE L624-PRT-REC FROM    WS-L624-HDG1  AFTER PAGE.                 
           WRITE L624-PRT-REC FROM    WS-L624-HDG3  AFTER 1.                    
           MOVE  SPACES               TO L624-PRT-REC.                          
           WRITE L624-PRT-REC AFTER 1.                                          
           WRITE L624-PRT-REC FROM    WS-L624-TOT1  AFTER 2.                    
           WRITE L624-PRT-REC FROM    WS-L624-TOT2  AFTER 2.                    
           WRITE L624-PRT-REC FROM    WS-L624-TOT3  AFTER 2.                    
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ****************************************************************          
      *            PRINT HEADING FOR DETAIL LIST                     *          
      ****************************************************************          
       2000-REPORT-HDG.                                                         
      *-------------------------------------------------------------            
           MOVE SPACES                TO L624-PRT-REC.                          
           MOVE 1                     TO WS-LINE-CNT.                           
           ADD  1                     TO WS-PAGE-CNT.                           
           MOVE WS-PAGE-CNT           TO L624-PAGENO.                           
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO L624-DATE.                             
                                                                                
           WRITE L624-PRT-REC FROM    WS-L624-HDG1  AFTER PAGE.                 
           WRITE L624-PRT-REC FROM    WS-L624-HDG3  AFTER 1.                    
           WRITE L624-PRT-REC FROM    WS-L624-HDG4  AFTER 2.                    
           WRITE L624-PRT-REC FROM    WS-L624-HDG5  AFTER 2.                    
           MOVE  SPACES               TO L624-PRT-REC.                          
           WRITE L624-PRT-REC AFTER 1.                                          
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2100-REPORT-DTL.                                                         
      *-------------------------------------------------------------            
           IF WS-LINE-CNT > 52                                                  
              PERFORM 2000-REPORT-HDG THRU 2000-EXIT.                           
                                                                                
           MOVE WS-NUM-QUEUE       TO L624-SNO.                                 
           MOVE F740-NUM-RANDOM    TO L624-RANDOM-NO.                           
           MOVE F740-NUM-REF       TO L624-REFNO.                               
           MOVE F740-NUM-REGN      TO L624-REGNO.                               
           MOVE SPACES             TO K825-KEY-FLD.                             
           MOVE F740-NUM-REGN      TO K825-NUM-REGN.                            
           MOVE F740-NUM-NRIC1     TO K825-NUM-NRIC.                            
           PERFORM 2110-READ-BP13K825 THRU 2110-EXIT.                           
           MOVE WS-NAME            TO L624-NAME.                                
           MOVE F740-NUM-NRIC1     TO L624-NRICNO.                              
                                                                                
           IF  F740-CDE-FLAT-TYPE = '3 ' AND                                    
               F740-NUM-BUYBACK = 'Y'                                           
               MOVE '3B'       TO L624-FT                                       
           ELSE                                                                 
               MOVE F740-CDE-FLAT-TYPE TO L624-FT                               
           END-IF.                                                              
                                                                                
           MOVE F740-CDE-CATEGORY  TO L624-CAT.                                 
           MOVE F740-CDE-JB        TO L624-JB-CDE.                              
           MOVE F740-CDE-ALLOC-SCH TO L624-ALLOC-SCH.                           
           MOVE F740-NUM-LOW-FLOOR TO L624-LFP.                                 
           MOVE SPACES             TO L624-TAG-RES.                             
                                                                                
           IF (F740-CDE-NT-WITH-LAND = 'Y')                                     
               IF F740-CDE-HOUSEHOLD  = 'T'                                     
                   MOVE 'T'         TO L624-TAG-RES                             
               ELSE                                                             
                   MOVE SPACES       TO L624-TAG-RES                            
           ELSE                                                                 
              IF (F740-CDE-RESIDENT-HA1  = SPACES OR                            
                                           'H' OR 'M' OR 'P') AND               
                 (F740-CDE-RESIDENT-HA2  = SPACES OR                            
                                           'H' OR 'M' OR 'P') AND               
                 (F740-CDE-RESIDENT-HA3  = SPACES OR                            
                                           'H' OR 'M' OR 'P') AND               
                 (F740-CDE-RESIDENT-HA4  = SPACES OR                            
                                           'H' OR 'M' OR 'P')                   
                    MOVE SPACES       TO L624-TAG-RES                           
              ELSE                                                              
                    PERFORM 2105-CHECK-RESIDENT THRU 2105-EXIT.                 
                                                                                
           WRITE L624-PRT-REC FROM WS-L624-DTL AFTER 1.                         
           ADD 1 TO WS-LINE-CNT.                                                
           IF F740-NUM-NRIC2(2:1)  = '#' OR SPACES                              
              MOVE SPACES TO L624-PRT-REC                                       
              WRITE L624-PRT-REC AFTER 1                                        
              ADD 1 TO WS-LINE-CNT                                              
           ELSE                                                                 
              MOVE SPACES TO WS-L624-DTL                                        
              MOVE SPACES                TO K825-KEY-FLD                        
              MOVE F740-NUM-REGN      TO K825-NUM-REGN                          
              MOVE F740-NUM-NRIC2     TO K825-NUM-NRIC                          
              PERFORM 2110-READ-BP13K825 THRU 2110-EXIT                         
              MOVE WS-NAME            TO L624-NAME                              
              MOVE F740-NUM-NRIC2     TO L624-NRICNO                            
              WRITE L624-PRT-REC FROM WS-L624-DTL AFTER 1                       
              ADD 2 TO WS-LINE-CNT                                              
              MOVE  SPACES            TO L624-PRT-REC                           
              WRITE L624-PRT-REC AFTER 1.                                       
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2105-CHECK-RESIDENT.                                                     
      *-------------------------------------------------------------            
              IF ((F740-CDE-RESIDENT-HA1 = '1' OR 'A')  OR                      
                  (F740-CDE-RESIDENT-HA2 = '1' OR 'A')  OR                      
                  (F740-CDE-RESIDENT-HA3 = '1' OR 'A')  OR                      
                  (F740-CDE-RESIDENT-HA4 = '1' OR 'A')) AND                     
                 (F740-CDE-SUCC-NT-ZONE = F740-CDE-NT1)                         
                 MOVE 'R'          TO L624-TAG-RES                              
              ELSE                                                              
              IF ((F740-CDE-RESIDENT-HA1 = '2' OR 'B')  OR                      
                  (F740-CDE-RESIDENT-HA2 = '2' OR 'B')  OR                      
                  (F740-CDE-RESIDENT-HA3 = '2' OR 'B')  OR                      
                  (F740-CDE-RESIDENT-HA4 = '2' OR 'B')) AND                     
                 (F740-CDE-SUCC-NT-ZONE = F740-CDE-NT2)                         
                 MOVE 'R'          TO L624-TAG-RES                              
              ELSE                                                              
              IF ((F740-CDE-RESIDENT-HA1 = '3' OR 'C')  OR                      
                  (F740-CDE-RESIDENT-HA2 = '3' OR 'C')  OR                      
                  (F740-CDE-RESIDENT-HA3 = '3' OR 'C')  OR                      
                  (F740-CDE-RESIDENT-HA4 = '3' OR 'C')) AND                     
                 (F740-CDE-SUCC-NT-ZONE = F740-CDE-NT3)                         
                 MOVE 'R'          TO L624-TAG-RES                              
              ELSE                                                              
              IF ((F740-CDE-RESIDENT-HA1 = '4' OR 'D')  OR                      
                  (F740-CDE-RESIDENT-HA2 = '4' OR 'D')  OR                      
                  (F740-CDE-RESIDENT-HA3 = '4' OR 'D')  OR                      
                  (F740-CDE-RESIDENT-HA4 = '4' OR 'D')) AND                     
                 (F740-CDE-SUCC-NT-ZONE = F740-CDE-NT4)                         
                 MOVE 'R'          TO L624-TAG-RES                              
              ELSE                                                              
              IF ((F740-CDE-RESIDENT-HA1 = '5' OR 'E')  OR                      
                  (F740-CDE-RESIDENT-HA2 = '5' OR 'E')  OR                      
                  (F740-CDE-RESIDENT-HA3 = '5' OR 'E')  OR                      
                  (F740-CDE-RESIDENT-HA4 = '5' OR 'E')) AND                     
                 (F740-CDE-SUCC-NT-ZONE = F740-CDE-NT5)                         
                 MOVE 'R'          TO L624-TAG-RES                              
              ELSE                                                              
              IF ((F740-CDE-RESIDENT-HA1 = 'T')  OR                             
                  (F740-CDE-RESIDENT-HA2 = 'T')  OR                             
                  (F740-CDE-RESIDENT-HA3 = 'T')  OR                             
                  (F740-CDE-RESIDENT-HA4 = 'T')) AND                            
                 (F740-CDE-SUCC-NT-ZONE = F740-CDE-NT1)                         
                 MOVE 'T'          TO L624-TAG-RES.                             
                                                                                
       2105-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2110-READ-BP13K825.                                                      
      *-------------------------------------------------------------            
           READ BP13K825.                                                       
                                                                                
           IF WS-K825-STATUS = 00                                               
              MOVE K825-NME-OCCP     TO WS-NAME                                 
              MOVE SPACES TO L624-PRT-REC.                                      
                                                                                
           IF WS-K825-STATUS = 23                                               
              MOVE SPACES  TO WS-NAME                                           
              DISPLAY 'BP13K825 RECORD NOT FOUND   '   WS-K825-STATUS           
              DISPLAY 'K825-KEY-FLD   '   K825-KEY-FLD.                         
                                                                                
           IF WS-K825-STATUS NOT = 00 AND 23                                    
              DISPLAY 'ERROR READING BP13K825'  WS-K825-STATUS                  
              GO TO 030-CLOSE-ROUTINE.                                          
                                                                                
       2110-EXIT.                                                               
            EXIT.                                                               
                                                                                
