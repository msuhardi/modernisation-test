      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C606.                                                 
       AUTHOR.        ANNALYN C BANTA.                                          
       DATE-WRITTEN.  27/07/94.                                                 
                                                                                
      * ========================================================== *            
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                      *            
      *    SYSTEM ID   : BP13                                      *            
      *    OBJECTIVE   : TO PRINT CPF STATEMENT OF ACCOUNT.        *            
      *------------------------------------------------------------*            
      * CHG-NO   BY   DATE    DETAILS                              *            
      *--------- ---  ------  -------------------------------------*            
      * BP130522 SGK  270798  TO CHANGE FILE SIZE OF AB02F875 FROM *            
      *                       1000 BYTES TO 1500 BYTES.            *            
      * BP130418 CLT  121098  CHG Y2K CHANGES                      *            
      * BP130929 ESC  281200  ADD LOAN-SCHEME, PREM-PYMT-TERM      *            
      * BP132587 JF4  010904  CHANGE AMT-CPF-NET TO AMT-AVAIL-HSE  *            
      * ========================================================== *            
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
                                                                                
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AB02F875 ASSIGN TO AB02F875.                                  
                                                                                
           SELECT BP13L606 ASSIGN TO BP13L606.                                  
                                                                                
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
                                                                                
       FD  AB02F875                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 1500 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD.                                          
       COPY AB02F875.                                                           
                                                                                
       FD  BP13L606                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  BP13L606-REC                   PIC X(132).                           
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F875-READ                PIC 9(5)  VALUE 0.                    
                                                                                
       01  WS-EOF-F875                    PIC 9     VALUE 0.                    
           88 EOF-F875                    VALUE 1.                              
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05 WS-CURR-CCYY                PIC 9(4).                             
           05 WS-CURR-MM                  PIC 99.                               
           05 WS-CURR-DD                  PIC 99.                               
                                                                                
       01  WS-GTOT-AMT-AVAIL              PIC 9(10)V99.                         
       01  WS-CONTR-AMT                   PIC 9(7).                             
       01  WS-CONTR-DTE.                                                        
           05 WS-YEAR                     PIC 9(4).                             
           05 WS-MONTH                    PIC 99.                               
                                                                                
       01  WS-MONTH-TABLE.                                                      
           05 FILLER                      PIC X(3)    VALUE 'JAN'.              
           05 FILLER                      PIC X(3)    VALUE 'FEB'.              
           05 FILLER                      PIC X(3)    VALUE 'MAR'.              
           05 FILLER                      PIC X(3)    VALUE 'APR'.              
           05 FILLER                      PIC X(3)    VALUE 'MAY'.              
           05 FILLER                      PIC X(3)    VALUE 'JUN'.              
           05 FILLER                      PIC X(3)    VALUE 'JUL'.              
           05 FILLER                      PIC X(3)    VALUE 'AUG'.              
           05 FILLER                      PIC X(3)    VALUE 'SEP'.              
           05 FILLER                      PIC X(3)    VALUE 'OCT'.              
           05 FILLER                      PIC X(3)    VALUE 'NOV'.              
           05 FILLER                      PIC X(3)    VALUE 'DEC'.              
                                                                                
       01  WS-MONTH-TAB REDEFINES WS-MONTH-TABLE.                               
           05 WS-MTH-DESC OCCURS 12       PIC X(3).                             
                                                                                
       01  WS-PRINT-VARIABLES.                                                  
           05 L606-HDG1.                                                        
              10 FILLER                   PIC X(55)   VALUE SPACES.             
              10 FILLER                   PIC X(22)   VALUE                     
                'SYSTEM  OF  COMMITMENT'.                                       
              10 FILLER                   PIC X(55)   VALUE SPACES.             
                                                                                
           05 L606-HDG2.                                                        
              10 FILLER                   PIC X(54)   VALUE 'BP13L606'.         
              10 FILLER                   PIC X(24)   VALUE                     
                'CPF STATEMENT OF ACCOUNT'.                                     
              10 FILLER                   PIC X(33)   VALUE SPACES.             
              10 FILLER                   PIC X(07)   VALUE 'DATE : '.          
              10 L606-SYS-DATE            PIC X(10).                            
              10 FILLER                   PIC X(06)   VALUE SPACES.             
                                                                                
           05 L606-HDG3.                                                        
              10 FILLER                   PIC X(50)   VALUE SPACES.             
              10 FILLER                   PIC X(32)   VALUE                     
                'FOR APPLICATION OF HDB/HUDC FLAT'.                             
              10 FILLER                   PIC X(29)   VALUE SPACES.             
              10 FILLER                   PIC X(21)   VALUE                     
                'USER : AGREEMENT TEAM'.                                        
                                                                                
           05 L606-HDG4.                                                        
              10 FILLER                   PIC X(20)   VALUE                     
                'REGISTRATION NO. :  '.                                         
              10 L606-REGN                PIC X(08)   VALUE SPACES.             
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(14)   VALUE                     
                 'LOAN SCHEME : '.                                              
              10 L606-LOAN-SCH            PIC X(01)   VALUE SPACES.             
              10 FILLER                   PIC X(22)   VALUE SPACES.             
                                                                                
           05 L606-HDG5.                                                        
              10 FILLER                   PIC X(09)   VALUE 'UIN NO'.           
              10 FILLER                   PIC X(04)   VALUE SPACES.             
              10 FILLER                   PIC X(08)   VALUE 'CPF NO'.           
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(22)   VALUE 'NAME'.             
              10 FILLER                   PIC X(07)   VALUE SPACES.             
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(14)   VALUE                     
                '  CPF BLOCK   '.                                               
              10 FILLER                   PIC X(04)   VALUE SPACES.             
              10 FILLER                   PIC X(17)   VALUE                     
                'BAL IN ORDRY ACCT'.                                            
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(13)   VALUE                     
                'HPI PREM/TERM'.                                                
              10 FILLER                   PIC X(03)   VALUE SPACES.             
              10 FILLER                   PIC X(16)   VALUE                     
                'AMOUNT AVAILABLE'.                                             
                                                                                
           05 L606-DTL1.                                                        
              10 L606-UIN                 PIC X(09).                            
              10 FILLER                   PIC X(04)   VALUE SPACES.             
              10 L606-CPF                 PIC X(09).                            
              10 FILLER                   PIC X(03)   VALUE SPACES.             
              10 L606-LESSEE              PIC X(22).                            
              10 FILLER                   PIC X(07)   VALUE SPACES.             
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 L606-CPF-BLOCK           PIC ZZZ,ZZZ,ZZ9.99.                   
              10 FILLER                   PIC X(06)   VALUE SPACES.             
              10 L606-CPF-GROSS           PIC ZZZ,ZZZ,ZZ9.99.                   
              10 FILLER                   PIC X(08)   VALUE SPACES.             
              10 L606-HPI-PREM            PIC ZZ,ZZ9.99.                        
              10 FILLER                   PIC X(01)   VALUE '/'.                
              10 L606-PYMT-TERM           PIC 99.                               
              10 FILLER                   PIC X(04)   VALUE SPACES.             
              10 L606-AMT-AVAIL           PIC ZZZ,ZZZ,ZZ9.99.                   
              10 FILLER                   PIC X       VALUE SPACES.             
           05 L606-DTL-BLNK1 REDEFINES L606-DTL1 PIC X(132).                    
                                                                                
           05 L606-CONTRI-LINE.                                                 
              10 FILLER                   PIC X(132)  VALUE                     
                'CONTRIBUTION RECEIVED FROM'.                                   
                                                                                
           05 L606-DTL2.                                                        
              10 FILLER                   PIC X(03)   VALUE SPACES.             
              10 L606-COMPANY             PIC X(30).                            
              10 FILLER                   PIC X(03)   VALUE SPACES.             
              10 L606-MONTH               PIC X(03).                            
              10 FILLER                   PIC X       VALUE SPACES.             
              10 L606-YEAR                PIC X(4).                             
              10 FILLER                   PIC X(05)   VALUE '   $ '.            
              10 L606-CONTR               PIC Z,ZZZ,ZZ9.99.                     
              10 FILLER                   PIC X(73)   VALUE SPACES.             
           05 L606-DTL-BLNK2 REDEFINES L606-DTL2 PIC X(132).                    
                                                                                
           05 L606-GTOTAL.                                                      
              10 FILLER                   PIC X(90)   VALUE SPACES.             
              10 FILLER                   PIC X(25)   VALUE                     
                'GRAND TOTAL AMT AVAIL :  '.                                    
              10 L606-G-TOTAL             PIC Z,ZZZ,ZZZ,ZZ9.99.                 
                                                                                
           05 L606-BLANK                  PIC X(132)  VALUE SPACES.             
           05 L606-DASH                   PIC X(132)  VALUE ALL '-'.            
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
       000-MAIN-ROUTINE.                                                        
      ******************************************************************        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-AB02F875  THRU 200-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL EOF-F875.                                                    
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       100-INITIALIZATION.                                                      
      ******************************************************************        
           OPEN INPUT  AB02F875                                                 
                OUTPUT BP13L606.                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       200-READ-AB02F875.                                                       
      ******************************************************************        
           READ AB02F875 AT END                                                 
                MOVE 1   TO WS-EOF-F875                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD  1  TO WS-F875-READ.                                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-RTN.                                                         
      ******************************************************************        
           MOVE FUNCTION CURRENT-DATE TO  WS-CURRENT-DATE.                      
           STRING WS-CURR-DD '/'                                                
                  WS-CURR-MM '/'                                                
                  WS-CURR-CCYY DELIMITED BY SIZE                                
                             INTO L606-SYS-DATE.                                
                                                                                
           MOVE ZEROS                   TO WS-GTOT-AMT-AVAIL.                   
           MOVE F875-NUM-REGN           TO L606-REGN.                           
           MOVE F875-NUM-LOAN-SCHEME    TO L606-LOAN-SCH.                       
                                                                                
           WRITE BP13L606-REC FROM L606-BLANK AFTER PAGE.                       
           WRITE BP13L606-REC FROM L606-HDG1  AFTER 9.                          
           WRITE BP13L606-REC FROM L606-HDG2  AFTER 2.                          
           WRITE BP13L606-REC FROM L606-HDG3  AFTER 1.                          
           WRITE BP13L606-REC FROM L606-HDG4  AFTER 4.                          
           WRITE BP13L606-REC FROM L606-HDG5  AFTER 3.                          
           WRITE BP13L606-REC FROM L606-DASH  AFTER 1.                          
                                                                                
           IF  F875-AMT-AVAIL-HSE-LSE1 IS NOT NUMERIC                           
               MOVE ZEROS TO F875-AMT-AVAIL-HSE-LSE1                            
           END-IF.                                                              
                                                                                
           IF  F875-AMT-AVAIL-HSE-LSE2 IS NOT NUMERIC                           
               MOVE ZEROS TO F875-AMT-AVAIL-HSE-LSE2                            
           END-IF.                                                              
                                                                                
           IF  F875-AMT-AVAIL-HSE-LSE3 IS NOT NUMERIC                           
               MOVE ZEROS TO F875-AMT-AVAIL-HSE-LSE3                            
           END-IF.                                                              
                                                                                
           IF  F875-AMT-AVAIL-HSE-LSE4 IS NOT NUMERIC                           
               MOVE ZEROS TO F875-AMT-AVAIL-HSE-LSE4                            
           END-IF.                                                              
                                                                                
           MOVE F875-NUM-CPF1                    TO L606-CPF.                   
           MOVE F875-NUM-UIN-LSE1                TO L606-UIN.                   
           MOVE F875-NME-LSE1                    TO L606-LESSEE.                
           MOVE F875-AMT-CPF-BLOCK-LSE1          TO L606-CPF-BLOCK.             
           MOVE F875-AMT-CPF-GROSS-LSE1          TO L606-CPF-GROSS.             
           MOVE F875-AMT-HPI-PREMIUM-LSE1        TO L606-HPI-PREM.              
           MOVE F875-NUM-PREM-PYMT-TERM-LSE1     TO L606-PYMT-TERM.             
           MOVE F875-AMT-AVAIL-HSE-LSE1          TO L606-AMT-AVAIL              
           ADD  F875-AMT-AVAIL-HSE-LSE1          TO WS-GTOT-AMT-AVAIL.          
           WRITE BP13L606-REC FROM L606-DTL1     AFTER 3.                       
                                                                                
           IF F875-NME-COMPANY-LSE1(1) NOT = SPACES AND LOW-VALUES              
              MOVE  F875-NME-COMPANY-LSE1(1)     TO L606-COMPANY                
              MOVE  F875-NUM-LAST-MTH-LSE1(1)    TO WS-CONTR-DTE                
              MOVE  WS-MTH-DESC(WS-MONTH)        TO L606-MONTH                  
              MOVE  WS-YEAR                      TO L606-YEAR                   
              MOVE  F875-AMT-LAST-LSE1(1)        TO WS-CONTR-AMT                
              MOVE  WS-CONTR-AMT                 TO L606-CONTR                  
              WRITE BP13L606-REC FROM L606-CONTRI-LINE AFTER 1                  
              WRITE BP13L606-REC FROM L606-DTL2  AFTER 1.                       
                                                                                
           IF F875-NME-COMPANY-LSE1(2) NOT = SPACES AND LOW-VALUES              
              MOVE  F875-NME-COMPANY-LSE1(2)     TO L606-COMPANY                
              MOVE  F875-NUM-LAST-MTH-LSE1(2)    TO WS-CONTR-DTE                
              MOVE  WS-MTH-DESC(WS-MONTH)        TO L606-MONTH                  
              MOVE  WS-YEAR                      TO L606-YEAR                   
              MOVE  F875-AMT-LAST-LSE1(2)        TO WS-CONTR-AMT                
              MOVE  WS-CONTR-AMT                 TO L606-CONTR                  
              WRITE BP13L606-REC FROM L606-DTL2  AFTER 1.                       
                                                                                
           IF F875-NUM-CPF2 NOT = SPACES                                        
              MOVE F875-NUM-CPF2                 TO L606-CPF                    
              MOVE F875-NUM-UIN-LSE2             TO L606-UIN                    
              MOVE F875-NME-LSE2                 TO L606-LESSEE                 
              MOVE F875-AMT-CPF-BLOCK-LSE2       TO L606-CPF-BLOCK              
              MOVE F875-AMT-CPF-GROSS-LSE2       TO L606-CPF-GROSS              
              MOVE F875-AMT-HPI-PREMIUM-LSE2     TO L606-HPI-PREM               
              MOVE F875-NUM-PREM-PYMT-TERM-LSE2  TO L606-PYMT-TERM              
              MOVE F875-AMT-AVAIL-HSE-LSE2       TO L606-AMT-AVAIL              
              ADD  F875-AMT-AVAIL-HSE-LSE2       TO WS-GTOT-AMT-AVAIL           
              WRITE BP13L606-REC FROM L606-DTL1  AFTER 3                        
                                                                                
              IF F875-NME-COMPANY-LSE2(1) NOT = SPACES AND LOW-VALUES           
                 MOVE  F875-NME-COMPANY-LSE2(1)     TO L606-COMPANY             
                 MOVE  F875-NUM-LAST-MTH-LSE2(1)    TO WS-CONTR-DTE             
                 MOVE  WS-MTH-DESC(WS-MONTH)        TO L606-MONTH               
                 MOVE  WS-YEAR                      TO L606-YEAR                
                 MOVE  F875-AMT-LAST-LSE2(1)        TO WS-CONTR-AMT             
                 MOVE  WS-CONTR-AMT                 TO L606-CONTR               
                 WRITE BP13L606-REC FROM L606-CONTRI-LINE AFTER 1               
                 WRITE BP13L606-REC FROM L606-DTL2  AFTER 1                     
              END-IF                                                            
                                                                                
              IF F875-NME-COMPANY-LSE2(2) NOT = SPACES AND LOW-VALUES           
                 MOVE  F875-NME-COMPANY-LSE2(2)     TO L606-COMPANY             
                 MOVE  F875-NUM-LAST-MTH-LSE2(2)    TO WS-CONTR-DTE             
                 MOVE  WS-MTH-DESC(WS-MONTH)        TO L606-MONTH               
                 MOVE  WS-YEAR                      TO L606-YEAR                
                 MOVE  F875-AMT-LAST-LSE2(2)        TO WS-CONTR-AMT             
                 MOVE  WS-CONTR-AMT                 TO L606-CONTR               
                 WRITE BP13L606-REC FROM L606-DTL2  AFTER 1                     
              END-IF.                                                           
                                                                                
           IF F875-NUM-CPF3 NOT = SPACES                                        
              MOVE F875-NUM-CPF3                 TO L606-CPF                    
              MOVE F875-NUM-UIN-LSE3             TO L606-UIN                    
              MOVE F875-NME-LSE3                 TO L606-LESSEE                 
              MOVE F875-AMT-CPF-BLOCK-LSE3       TO L606-CPF-BLOCK              
              MOVE F875-AMT-CPF-GROSS-LSE3       TO L606-CPF-GROSS              
              MOVE F875-AMT-HPI-PREMIUM-LSE3     TO L606-HPI-PREM               
              MOVE F875-NUM-PREM-PYMT-TERM-LSE3  TO L606-PYMT-TERM              
              MOVE F875-AMT-AVAIL-HSE-LSE3       TO L606-AMT-AVAIL              
              ADD  F875-AMT-AVAIL-HSE-LSE3       TO WS-GTOT-AMT-AVAIL           
              WRITE BP13L606-REC FROM L606-DTL1  AFTER 3                        
                                                                                
              IF F875-NME-COMPANY-LSE3(1) NOT = SPACES AND LOW-VALUES           
                 MOVE  F875-NME-COMPANY-LSE3(1)     TO L606-COMPANY             
                 MOVE  F875-NUM-LAST-MTH-LSE3(1)    TO WS-CONTR-DTE             
                 MOVE  WS-MTH-DESC(WS-MONTH)        TO L606-MONTH               
                 MOVE  WS-YEAR                      TO L606-YEAR                
                 MOVE  F875-AMT-LAST-LSE3(1)        TO WS-CONTR-AMT             
                 MOVE  WS-CONTR-AMT                 TO L606-CONTR               
                 WRITE BP13L606-REC FROM L606-CONTRI-LINE AFTER 1               
                 WRITE BP13L606-REC FROM L606-DTL2        AFTER 1               
              END-IF                                                            
                                                                                
              IF F875-NME-COMPANY-LSE3(2) NOT = SPACES AND LOW-VALUES           
                 MOVE  F875-NME-COMPANY-LSE3(2)     TO L606-COMPANY             
                 MOVE  F875-NUM-LAST-MTH-LSE3(2)    TO WS-CONTR-DTE             
                 MOVE  WS-MTH-DESC(WS-MONTH)        TO L606-MONTH               
                 MOVE  WS-YEAR                      TO L606-YEAR                
                 MOVE  F875-AMT-LAST-LSE3(2)        TO WS-CONTR-AMT             
                 MOVE  WS-CONTR-AMT                 TO L606-CONTR               
                 WRITE BP13L606-REC FROM L606-DTL2  AFTER 1                     
           END-IF.                                                              
                                                                                
           IF F875-NUM-CPF4 NOT = SPACES                                        
              MOVE F875-NUM-CPF4                 TO L606-CPF                    
              MOVE F875-NUM-UIN-LSE4             TO L606-UIN                    
              MOVE F875-NME-LSE4                 TO L606-LESSEE                 
              MOVE F875-AMT-CPF-BLOCK-LSE4       TO L606-CPF-BLOCK              
              MOVE F875-AMT-CPF-GROSS-LSE4       TO L606-CPF-GROSS              
              MOVE F875-AMT-HPI-PREMIUM-LSE4     TO L606-HPI-PREM               
              MOVE F875-NUM-PREM-PYMT-TERM-LSE4  TO L606-PYMT-TERM              
              MOVE F875-AMT-AVAIL-HSE-LSE4       TO L606-AMT-AVAIL              
              ADD  F875-AMT-AVAIL-HSE-LSE4       TO WS-GTOT-AMT-AVAIL           
              WRITE BP13L606-REC FROM L606-DTL1  AFTER 3                        
                                                                                
              IF F875-NME-COMPANY-LSE4(1) NOT = SPACES AND LOW-VALUES           
                 MOVE  F875-NME-COMPANY-LSE4(1)     TO L606-COMPANY             
                 MOVE  F875-NUM-LAST-MTH-LSE4(1)    TO WS-CONTR-DTE             
                 MOVE  WS-MTH-DESC(WS-MONTH)        TO L606-MONTH               
                 MOVE  WS-YEAR                      TO L606-YEAR                
                 MOVE  F875-AMT-LAST-LSE4(1)        TO WS-CONTR-AMT             
                 MOVE  WS-CONTR-AMT                 TO L606-CONTR               
                 WRITE BP13L606-REC FROM L606-CONTRI-LINE AFTER 1               
                 WRITE BP13L606-REC FROM L606-DTL2  AFTER 1                     
              END-IF                                                            
                                                                                
              IF F875-NME-COMPANY-LSE4(2) NOT = SPACES AND LOW-VALUES           
                 MOVE  F875-NME-COMPANY-LSE4(2)     TO L606-COMPANY             
                 MOVE  F875-NUM-LAST-MTH-LSE4(2)    TO WS-CONTR-DTE             
                 MOVE  WS-MTH-DESC(WS-MONTH)        TO L606-MONTH               
                 MOVE  WS-YEAR                      TO L606-YEAR                
                 MOVE  F875-AMT-LAST-LSE4(2)        TO WS-CONTR-AMT             
                 MOVE  WS-CONTR-AMT                 TO L606-CONTR               
                 WRITE BP13L606-REC FROM L606-DTL2  AFTER 1                     
                                                                                
           END-IF.                                                              
                                                                                
           MOVE WS-GTOT-AMT-AVAIL       TO L606-G-TOTAL.                        
           WRITE BP13L606-REC FROM L606-GTOTAL AFTER 4.                         
                                                                                
           PERFORM 200-READ-AB02F875  THRU 200-EXIT.                            
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       999-CLOSE-FILES.                                                         
      ******************************************************************        
           CLOSE AB02F875                                                       
                 BP13L606.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C606 ================*'.              
           DISPLAY '    NO. OF F875 RECORDS READ     = ' WS-F875-READ.          
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
