      *-------------------------*                                               
       IDENTIFICATION DIVISION.                                                 
      *-------------------------*                                               
       PROGRAM-ID.    BP13C39A.                                                 
      *AUTHOR.        JIJO FRANCIS.                                             
      *DATE-WRITTEN.  29/09/2003.                                               
      *===============================================================*         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      * OBJECTIVE :                                                   *         
      *  1. TO RETRIEVE DATA FOR PRINTING OF UNDERTAKING LETTER.      *         
      *                                                               *         
      * FILES USED :                                                  *         
      *  1. BP13F520                                                  *         
      *  2. BP13K200                                                  *         
      *  3. BP13K800                                                  *         
      *  4. BP13K820                                                  *         
      *  5. BP13F398                                                  *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * CHG REF  BY    DATE     DESCRIPTION                           *         
      * -------- ---  --------  -----------                           *         
      * BP132684 CKK  24/04/05  NEW PROGRAM TO REPLACE BP13C398       *         
      *===============================================================*         
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F520  ASSIGN        TO BP13F520.                          
                                                                                
           SELECT BP13F398  ASSIGN        TO BP13F398.                          
                                                                                
           SELECT BP13K200  ASSIGN        TO BP13K200                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K200-KEY-FLD                       
                            ALTERNATE KEY IS K200-NUM-REGN                      
                            FILE STATUS   IS WS-K800-STATUS.                    
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS WS-K800-STATUS.                    
                                                                                
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K820-KEY-FLD                       
                            FILE STATUS   IS WS-K820-STATUS.                    
                                                                                
      *---------------*                                                         
       DATA DIVISION.                                                           
      *---------------*                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F520                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS  500  CHARACTERS.                                   
       COPY BP13F520.                                                           
                                                                                
       FD   BP13F398                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS  2500 CHARACTERS.                                   
       COPY BP13F398.                                                           
                                                                                
       FD   BP13K200                                                            
            RECORD CONTAINS   300 CHARACTERS.                                   
       COPY BP13K200.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS  2000 CHARACTERS.                                   
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS   400 CHARACTERS.                                   
       COPY BP13K820.                                                           
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
      *------------------------------*                                          
      *   WORKING STORAGE VARIABLES  *                                          
      *------------------------------*                                          
       01 WS-F520-EOF             PIC X(01) VALUE 'N'.                          
          88 F520-END-OF-FILE   VALUE 'Y'.                                      
       01 WS-K820-EOF             PIC X(01) VALUE 'N'.                          
          88 K820-END-OF-FILE   VALUE 'Y'.                                      
       01 WS-K200-STATUS          PIC 9(02) VALUE ZEROES.                       
       01 WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                       
       01 WS-K820-STATUS          PIC 9(02) VALUE ZEROES.                       
                                                                                
       01 WS-F520-READ-CNT        PIC 9(08) VALUE ZEROES.                       
       01 WS-K800-READ-CNT        PIC 9(08) VALUE ZEROES.                       
       01 WS-K820-READ-CNT        PIC 9(08) VALUE ZEROES.                       
       01 WS-F398-WRITE-CNT       PIC 9(08) VALUE ZEROES.                       
       01 WS-PRINT-CASE-CNT       PIC 9(08) VALUE ZEROES.                       
                                                                                
       01 WS-K800-REGN-FND        PIC X(01) VALUE SPACES.                       
       01 WS-APPLCNT-CTR          PIC 9(02) VALUE ZEROES.                       
       01 WS-OCCP-CTR             PIC 9(02) VALUE ZEROES.                       
       01 WS-NRIC-CNT             PIC 9(02) VALUE ZEROES.                       
       01 WS-CTR1                 PIC 9(02) VALUE ZEROES.                       
                                                                                
       01 WS-CURR-DATE            PIC X(08) VALUE ZEROES.                       
       01 WS-NUM-CURR-DATE  REDEFINES WS-CURR-DATE.                             
          05 WS-DATE              PIC 9(08).                                    
       01 WS-BIRTH-DATE           PIC 9(08) VALUE ZEROES.                       
                                                                                
       01 WS-AGE                  PIC 9(08) VALUE ZEROES.                       
       01 WS-NUM-AGE  REDEFINES WS-AGE.                                         
          05 WS-AGE-CMPLTD        PIC 9(04).                                    
          05 WS-NUM-REMNG         PIC 9(04).                                    
                                                                                
       01 WS-DTE-LETTER           PIC 9(08) VALUE ZEROES.                       
       01 WS-NUM-DTE-LETTER       PIC 9(07) VALUE ZEROES.                       
       01 WS-EDT-DATE             PIC X(10) VALUE SPACES.                       
                                                                                
       01 WS-EDIT1                PIC Z(07)9.                                   
                                                                                
       01 WS-C398-PARAMETERS.                                                   
          02 WS-C398-LTR-VARS.                                                  
             05 WS-C398-NUM-REGN                   PIC X(08).                   
             05 WS-C398-PRT-DATE                   PIC X(10).                   
             05 WS-C398-ESS-MBRS                   PIC X(01).                   
             05 WS-C398-ORPHANS-DECL               PIC X(01).                   
             05 WS-C398-MCPS-FRM                   PIC X(01).                   
             05 WS-C398-APPLN-FRM                  PIC X(01).                   
             05 WS-C398-OCCUPIERS  OCCURS 12 TIMES.                             
                10 WS-C398-NRIC                    PIC X(09).                   
                10 WS-C398-CDE-OCCUPIER            PIC X(01).                   
                10 WS-C398-NME-OCCUPIER            PIC X(66).                   
                10 WS-C398-AGE-OCCUPIER            PIC 9(03).                   
                10 WS-C398-DOC-REQD.                                            
                   20 WS-C398-ID-CARD              PIC X(01).                   
                   20 WS-C398-PASS-PERMIT          PIC X(01).                   
                   20 WS-C398-BIRTH-CERTF          PIC X(01).                   
                   20 WS-C398-MARRIAGE-CERTF       PIC X(01).                   
                   20 WS-C398-PAY-SLIP             PIC X(01).                   
                   20 WS-C398-UNEMPLYMNT-DECL      PIC X(01).                   
                   20 WS-C398-STUDENT-PASS         PIC X(01).                   
                   20 WS-C398-DEATH-CERTF          PIC X(01).                   
                   20 WS-C398-NRIC-DTH-CERTF       PIC X(09).                   
                   20 WS-C398-NME-DTH-CERTF        PIC X(66).                   
                   20 WS-C398-DIVORCE-CERTF        PIC X(01).                   
                   20 WS-C398-PHOTOGRAPH           PIC X(01).                   
                   20 WS-C398-MARRIAGE-CONSENT     PIC X(01).                   
             05 FILLER                             PIC X(97).                   
          02 WS-C398-LETTER-CNT                    PIC 9(02).                   
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F520      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-DATA       THRU 3000-EXIT                       
                                           UNTIL F520-END-OF-FILE.              
           PERFORM 9000-CLOSE-PARA         THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN  INPUT  BP13F520                                                
                        BP13K200                                                
                        BP13K800                                                
                        BP13K820                                                
                 OUTPUT BP13F398.                                               
                                                                                
           IF WS-K200-STATUS  NOT =  ZEROS AND 97                               
              DISPLAY 'BP13K200 - OPEN ERROR, STATUS ' WS-K200-STATUS           
              MOVE    WS-K200-STATUS     TO RETURN-CODE                         
              PERFORM 9000-CLOSE-PARA                                           
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS  NOT =  ZEROS AND 97                               
              DISPLAY 'BP13K800 - OPEN ERROR, STATUS ' WS-K800-STATUS           
              MOVE    WS-K800-STATUS     TO RETURN-CODE                         
              PERFORM 9000-CLOSE-PARA                                           
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS  NOT =  ZEROS AND 97                               
              DISPLAY 'BP13K820 - OPEN ERROR, STATUS ' WS-K820-STATUS           
              MOVE    WS-K820-STATUS     TO RETURN-CODE                         
              PERFORM 9000-CLOSE-PARA                                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CURR-DATE.                     
           STRING WS-CURR-DATE(7:2), '/',                                       
                  WS-CURR-DATE(5:2), '/',                                       
                  WS-CURR-DATE(1:4)  DELIMITED BY SIZE                          
                  INTO WS-EDT-DATE.                                             
                                                                                
           MOVE  0     TO  WS-C398-LETTER-CNT.                                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F520.                                                      
      *---------------------------------------------------------------*         
           READ BP13F520 AT END                                                 
                          MOVE 'Y' TO WS-F520-EOF                               
                         NOT AT END                                             
                          ADD 1    TO WS-F520-READ-CNT                          
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-DATA.                                                       
      *---------------------------------------------------------------*         
      *---------------------------------------------------*                     
      *MOVE APPLICANT DETAILS TO ARRAY POSITIONS 1 TO 4   *                     
      *MOVE OCCUPIER DETAILS TO ARRAY POSITIONS 5 TO 12   *                     
      *---------------------------------------------------*                     
           MOVE  ZEROS      TO WS-APPLCNT-CTR.                                  
           MOVE   4         TO WS-OCCP-CTR.                                     
           MOVE   'N'       TO WS-K820-EOF.                                     
           MOVE  SPACES     TO WS-C398-LTR-VARS                                 
                               BP13F398-REC.                                    
           INITIALIZE          WS-C398-LTR-VARS                                 
                               BP13F398-REC.                                    
                                                                                
           PERFORM 3100-READ-BP13K800         THRU 3100-EXIT.                   
                                                                                
           IF WS-K800-REGN-FND  =  'Y'                                          
              PERFORM 3150-GET-LTR-DATE       THRU 3150-EXIT                    
              PERFORM 3200-START-BP13K820     THRU 3200-EXIT                    
              PERFORM 3300-READNXT-BP13K820   THRU 3300-EXIT                    
                      UNTIL (K820-NUM-REGN  NOT =  F520-NUM-REGN)  OR           
                            (K820-END-OF-FILE)                                  
                                                                                
              IF K800-NUM-ALLO-SCHEME =  'MCP'                                  
                 MOVE   'Y'     TO WS-C398-MCPS-FRM                             
                                   F398-MCPS-FRM                                
              END-IF                                                            
                                                                                
              MOVE   'Y'    TO  WS-C398-ESS-MBRS                                
                                F398-ESS-MBRS-FRM                               
                                                                                
              IF F520-CDE-ELIG-SCH = 'OR '                                      
                 MOVE  'Y'  TO  WS-C398-ORPHANS-DECL                            
                                F398-ORPHAN-SCH-FRM                             
              END-IF                                                            
                                                                                
              IF F520-NUM-APPLN-TYPE = 'I'                                      
                 MOVE  'Y'  TO  WS-C398-APPLN-FRM                               
                                F398-APPLN-FRM                                  
              END-IF                                                            
                                                                                
              IF WS-C398-LETTER-CNT < 2                                         
                ADD  1      TO  WS-C398-LETTER-CNT                              
              END-IF                                                            
                                                                                
              CALL 'BP13C399'   USING WS-C398-PARAMETERS                        
              ADD    1      TO WS-PRINT-CASE-CNT                                
                                                                                
              PERFORM 5000-WRITE-BP13F398   THRU 5000-EXIT                      
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F520         THRU 2000-EXIT.                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3100-READ-BP13K800.                                                      
      *---------------------------------------------------------------*         
           MOVE F520-NUM-REGN   TO K800-NUM-REGN.                               
           READ BP13K800.                                                       
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
              MOVE  'Y'         TO WS-K800-REGN-FND                             
              ADD   1           TO WS-K800-READ-CNT                             
           WHEN 23                                                              
              MOVE   SPACES     TO WS-K800-REGN-FND                             
              DISPLAY 'BP13K800 REG NO NOT FOUND : ' F520-NUM-REGN              
           WHEN OTHER                                                           
              DISPLAY 'BP13K800 - READ ERROR, STATUS ' WS-K800-STATUS           
              MOVE WS-K800-STATUS    TO RETURN-CODE                             
              PERFORM 9000-CLOSE-PARA                                           
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3150-GET-LTR-DATE.                                                       
      *---------------------------------------------------------------*         
           MOVE F520-NUM-REGN         TO WS-C398-NUM-REGN                       
                                         F398-NUM-REGN                          
                                         K200-NUM-REGN.                         
           MOVE F520-CDE-ELIG-SCH     TO F398-CDE-SCHEME.                       
                                                                                
           PERFORM 3170-READ-BP13K200 THRU 3170-EXIT.                           
                                                                                
           IF WS-DTE-LETTER = ZEROES                                            
              MOVE  SPACES          TO   WS-C398-PRT-DATE                       
           ELSE                                                                 
              COMPUTE WS-NUM-DTE-LETTER =                                       
                      FUNCTION INTEGER-OF-DATE(WS-DTE-LETTER)                   
              SUBTRACT    10        FROM WS-NUM-DTE-LETTER                      
              COMPUTE WS-DTE-LETTER =                                           
                      FUNCTION DATE-OF-INTEGER(WS-NUM-DTE-LETTER)               
              STRING WS-DTE-LETTER(7:2) '/'                                     
                     WS-DTE-LETTER(5:2) '/'                                     
                     WS-DTE-LETTER(1:4)                                         
                     DELIMITED BY SIZE INTO WS-C398-PRT-DATE                    
           END-IF.                                                              
                                                                                
       3150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3170-READ-BP13K200.                                                      
      *---------------------------------------------------------------*         
           READ BP13K200 KEY IS K200-NUM-REGN.                                  
           EVALUATE WS-K200-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              MOVE  K200-DTE-ALLOC    TO WS-DTE-LETTER                          
           WHEN 23                                                              
              MOVE  ZEROES            TO WS-DTE-LETTER                          
              DISPLAY 'BP13K200 REG NO NOT FOUND : ' K200-NUM-REGN              
           WHEN OTHER                                                           
              DISPLAY 'BP13K200 - READ ERROR, STATUS ' WS-K200-STATUS           
              MOVE WS-K200-STATUS    TO RETURN-CODE                             
              PERFORM 9000-CLOSE-PARA                                           
           END-EVALUATE.                                                        
                                                                                
       3170-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3200-START-BP13K820.                                                     
      *---------------------------------------------------------------*         
           MOVE  ZEROES            TO WS-NRIC-CNT.                              
           MOVE  LOW-VALUES        TO K820-KEY-FLD.                             
           MOVE  F520-NUM-REGN     TO K820-NUM-REGN.                            
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD.                                  
           EVALUATE WS-K820-STATUS                                              
           WHEN 00                                                              
              CONTINUE                                                          
           WHEN 23                                                              
              DISPLAY 'BP13K820 REG NO NOT FOUND : ' F520-NUM-REGN              
           WHEN 46                                                              
              MOVE    'Y'          TO WS-K820-EOF                               
           WHEN OTHER                                                           
              DISPLAY 'BP13K820 - START ERROR, STATUS ' WS-K820-STATUS          
              MOVE WS-K820-STATUS  TO   RETURN-CODE                             
              PERFORM 9000-CLOSE-PARA                                           
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3300-READNXT-BP13K820.                                                   
      *---------------------------------------------------------------*         
           READ BP13K820 NEXT                                                   
                         AT END MOVE 'Y' TO WS-K820-EOF                         
                         GO TO 3300-EXIT.                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
             WHEN 00                                                            
                IF K820-NUM-REGN = F520-NUM-REGN                                
                   ADD    1   TO WS-NRIC-CNT                                    
                   PERFORM 4000-PROCESS-K820     THRU 4000-EXIT                 
                   ADD    1   TO WS-K820-READ-CNT                               
                END-IF                                                          
             WHEN 46                                                            
                MOVE    'Y'   TO WS-K820-EOF                                    
             WHEN OTHER                                                         
                DISPLAY 'BP18K820 - READNXT ERROR, STATUS: '                    
                                                     WS-K820-STATUS             
                MOVE     WS-K820-STATUS    TO RETURN-CODE                       
                PERFORM  9000-CLOSE-PARA                                        
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-PROCESS-K820.                                                       
      *---------------------------------------------------------------*         
      *-----------------------------------------*                               
      * CALCULATE COMPLETED AGE FROM BIRTH DATE *                               
      *-----------------------------------------*                               
           MOVE K820-DTE-BIRTH        TO WS-BIRTH-DATE.                         
           COMPUTE WS-AGE  =  WS-DATE  -  WS-BIRTH-DATE.                        
                                                                                
      *---------------------------------------------------*                     
      * IF NRIC FOUND IN BP13K800, PERFORM APPLICANT PARA *                     
      * ELSE PERFORM OCCUPIER PARA                        *                     
      *---------------------------------------------------*                     
           IF  (K820-NUM-NRIC  =  K800-NUM-NRIC1)  OR                           
               (K820-NUM-NRIC  =  K800-NUM-NRIC2)  OR                           
               (K820-NUM-NRIC  =  K800-NUM-NRIC3)  OR                           
               (K820-NUM-NRIC  =  K800-NUM-NRIC4)                               
                                                                                
               PERFORM 4100-PROCESS-APPLICANT     THRU 4100-EXIT                
           ELSE                                                                 
               PERFORM 4500-PROCESS-OCCUPIER      THRU 4500-EXIT                
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4100-PROCESS-APPLICANT.                                                  
      *---------------------------------------------------------------*         
           ADD   1     TO WS-APPLCNT-CTR.                                       
      *------------------------------------*                                    
      * NRIC, NAME, AGE AND OCCUPIER CODE  *                                    
      *------------------------------------*                                    
           MOVE K820-NUM-NRIC     TO                                            
                             WS-C398-NRIC(WS-APPLCNT-CTR)                       
                             F398-NUM-NRIC(WS-APPLCNT-CTR).                     
           MOVE   'A'             TO                                            
                             WS-C398-CDE-OCCUPIER(WS-APPLCNT-CTR)               
                             F398-CDE-OCCP(WS-APPLCNT-CTR).                     
           MOVE   K820-NME-OCCP   TO                                            
                             WS-C398-NME-OCCUPIER(WS-APPLCNT-CTR)               
                             F398-NME-OCCP(WS-APPLCNT-CTR).                     
           MOVE   WS-AGE-CMPLTD   TO                                            
                             WS-C398-AGE-OCCUPIER(WS-APPLCNT-CTR)               
                             F398-NUM-AGE(WS-APPLCNT-CTR).                      
                                                                                
      *-------------------------------*                                         
      * IDENTITY CARD                 *                                         
      *-------------------------------*                                         
           MOVE   'Y'             TO                                            
                             WS-C398-ID-CARD(WS-APPLCNT-CTR)                    
                             F398-ID-CARD(WS-APPLCNT-CTR).                      
                                                                                
      *-------------------------------*                                         
      * BIRTH CERTIFICATE             *                                         
      *-------------------------------*                                         
           IF K820-NUM-RELATIONSHIP = '41' OR '39' OR '06'                      
               MOVE   'Y'         TO                                            
                             WS-C398-BIRTH-CERTF(WS-APPLCNT-CTR)                
                             F398-BIRTH-CERTF(WS-APPLCNT-CTR)                   
           END-IF.                                                              
                                                                                
           IF F520-CDE-ALLOC-SCH  =  'MTF'                                      
              IF K820-NUM-RELATIONSHIP = '02' OR '12' OR '01' OR                
                                         '43' OR '45'                           
                 MOVE   'Y'       TO                                            
                             WS-C398-BIRTH-CERTF(WS-APPLCNT-CTR)                
                             F398-BIRTH-CERTF(WS-APPLCNT-CTR)                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *-------------------------------*                                         
      * MARRIAGE CERTIFICATE          *                                         
      *-------------------------------*                                         
           IF K820-NUM-MARITAL-STATUS  =  '2' OR 'M'                            
               MOVE   'Y'         TO                                            
                             WS-C398-MARRIAGE-CERTF(WS-APPLCNT-CTR)             
                             F398-MARRIAGE-CERTF(WS-APPLCNT-CTR)                
               PERFORM VARYING WS-CTR1 FROM 1 BY 1 UNTIL                        
                                             WS-CTR1 = WS-NRIC-CNT              
                 IF K820-NUM-NRIC-SPOUSE = WS-C398-NRIC(WS-CTR1)                
                    MOVE   'N'    TO                                            
                             WS-C398-MARRIAGE-CERTF(WS-APPLCNT-CTR)             
                             F398-MARRIAGE-CERTF(WS-APPLCNT-CTR)                
                 END-IF                                                         
               END-PERFORM                                                      
           END-IF.                                                              
                                                                                
      *----------------------------------*                                      
      * PAYSLIP/UNEMPLOYMENT DECLARATION *                                      
      *----------------------------------*                                      
           IF K820-AMT-INCOME  >  0                                             
               MOVE   'Y'         TO                                            
                             WS-C398-PAY-SLIP(WS-APPLCNT-CTR)                   
                             F398-PAY-SLIP(WS-APPLCNT-CTR)                      
           ELSE                                                                 
               IF (WS-AGE-CMPLTD >= 31)  AND (WS-AGE-CMPLTD <= 55)              
                  MOVE   'Y'      TO                                            
                             WS-C398-UNEMPLYMNT-DECL(WS-APPLCNT-CTR)            
                             F398-UNEMPLY-DECL(WS-APPLCNT-CTR)                  
               ELSE                                                             
                  IF (WS-AGE-CMPLTD >= 18)  AND (WS-AGE-CMPLTD <= 30)           
                     MOVE   'Y'      TO                                         
                             WS-C398-UNEMPLYMNT-DECL(WS-APPLCNT-CTR)            
                             F398-UNEMPLY-DECL(WS-APPLCNT-CTR)                  
                             WS-C398-STUDENT-PASS(WS-APPLCNT-CTR)               
                             F398-STUDENT-PASS(WS-APPLCNT-CTR)                  
                  END-IF                                                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *-------------------------------*                                         
      * DEATH CERTIFICATE             *                                         
      *-------------------------------*                                         
           IF K820-NUM-MARITAL-STATUS  =  '3' OR 'W'                            
               MOVE   'Y'         TO                                            
                             WS-C398-DEATH-CERTF(WS-APPLCNT-CTR)                
                             F398-DEATH-CERTF(WS-APPLCNT-CTR)                   
               MOVE K820-NUM-NRIC-SPOUSE   TO                                   
                             WS-C398-NRIC-DTH-CERTF(WS-APPLCNT-CTR)             
                             F398-NRIC-DEATH-CERTF(WS-APPLCNT-CTR)              
               MOVE K820-NME-SPOUSE        TO                                   
                             WS-C398-NME-DTH-CERTF(WS-APPLCNT-CTR)              
                             F398-NME-DEATH-CERTF(WS-APPLCNT-CTR)               
           END-IF.                                                              
                                                                                
      *-------------------------------*                                         
      * DIVORCE CERTIFICATE           *                                         
      *-------------------------------*                                         
           IF K820-NUM-MARITAL-STATUS  =  '4' OR 'D'                            
               MOVE   'Y'         TO                                            
                             WS-C398-DIVORCE-CERTF(WS-APPLCNT-CTR)              
                             F398-DIVORCE-CERTF(WS-APPLCNT-CTR)                 
           END-IF.                                                              
                                                                                
      *-------------------------------*                                         
      * PHOTOGRAPH                    *                                         
      *-------------------------------*                                         
           MOVE   'Y'             TO                                            
                             WS-C398-PHOTOGRAPH(WS-APPLCNT-CTR)                 
                             F398-PHOTOGRAPH(WS-APPLCNT-CTR).                   
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4500-PROCESS-OCCUPIER.                                                   
      *---------------------------------------------------------------*         
           ADD   1     TO WS-OCCP-CTR.                                          
      *------------------------------------*                                    
      * NRIC, NAME, AGE AND OCCUPIER CODE  *                                    
      *------------------------------------*                                    
           MOVE K820-NUM-NRIC     TO                                            
                             WS-C398-NRIC(WS-OCCP-CTR)                          
                             F398-NUM-NRIC(WS-OCCP-CTR).                        
           MOVE   'O'             TO                                            
                             WS-C398-CDE-OCCUPIER(WS-OCCP-CTR)                  
                             F398-CDE-OCCP(WS-OCCP-CTR).                        
           MOVE   K820-NME-OCCP   TO                                            
                             WS-C398-NME-OCCUPIER(WS-OCCP-CTR)                  
                             F398-NME-OCCP(WS-OCCP-CTR).                        
           MOVE   WS-AGE-CMPLTD   TO                                            
                             WS-C398-AGE-OCCUPIER(WS-OCCP-CTR)                  
                             F398-NUM-AGE(WS-OCCP-CTR).                         
                                                                                
      *-------------------------------*                                         
      * IDENTITY CARD                 *                                         
      *-------------------------------*                                         
           IF (K820-NUM-CITIZENSHIP = '10' OR '20')  AND                        
              (WS-AGE-CMPLTD >= 15)                                             
              MOVE   'Y'          TO                                            
                             WS-C398-ID-CARD(WS-OCCP-CTR)                       
                             F398-ID-CARD(WS-OCCP-CTR)                          
           END-IF.                                                              
                                                                                
      *-------------------------------*                                         
      * EMPLOYMENT PASS/WORK PERMIT   *                                         
      *-------------------------------*                                         
           IF (K820-NUM-CITIZENSHIP  NOT =  '10')                               
              MOVE   'Y'          TO                                            
                             WS-C398-PASS-PERMIT(WS-OCCP-CTR)                   
                             F398-PASS-PERMIT(WS-OCCP-CTR)                      
           END-IF.                                                              
                                                                                
      *-------------------------------*                                         
      * BIRTH CERTIFICATE             *                                         
      *-------------------------------*                                         
           IF K820-NUM-RELATIONSHIP = '41' OR '39' OR '06'                      
               MOVE   'Y'         TO                                            
                             WS-C398-BIRTH-CERTF(WS-OCCP-CTR)                   
                             F398-BIRTH-CERTF(WS-OCCP-CTR)                      
           END-IF.                                                              
                                                                                
           IF F520-CDE-ALLOC-SCH  =  'MTF'                                      
              IF K820-NUM-RELATIONSHIP = '02' OR '12' OR '01' OR                
                                         '43' OR '45'                           
                 MOVE   'Y'       TO                                            
                             WS-C398-BIRTH-CERTF(WS-OCCP-CTR)                   
                             F398-BIRTH-CERTF(WS-OCCP-CTR)                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *-------------------------------*                                         
      * MARRIAGE CERTIFICATE          *                                         
      *-------------------------------*                                         
           IF K820-NUM-MARITAL-STATUS  =  '2' OR 'M'                            
              MOVE   'Y'          TO                                            
                             WS-C398-MARRIAGE-CERTF(WS-OCCP-CTR)                
                             F398-MARRIAGE-CERTF(WS-OCCP-CTR)                   
              PERFORM VARYING WS-CTR1 FROM 1 BY 1 UNTIL                         
                                             WS-CTR1 = WS-NRIC-CNT              
                 IF K820-NUM-NRIC-SPOUSE = WS-C398-NRIC(WS-CTR1)                
                    MOVE   'N'    TO                                            
                             WS-C398-MARRIAGE-CERTF(WS-OCCP-CTR)                
                             F398-MARRIAGE-CERTF(WS-OCCP-CTR)                   
                 END-IF                                                         
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
      *----------------------------------*                                      
      * PAYSLIP/UNEMPLOYMENT DECLARATION *                                      
      *----------------------------------*                                      
           IF K820-AMT-INCOME  >  0                                             
               MOVE   'Y'         TO                                            
                             WS-C398-PAY-SLIP(WS-OCCP-CTR)                      
                             F398-PAY-SLIP(WS-OCCP-CTR)                         
           ELSE                                                                 
               IF (WS-AGE-CMPLTD >= 31)  AND (WS-AGE-CMPLTD <= 55)              
                  MOVE   'Y'      TO                                            
                             WS-C398-UNEMPLYMNT-DECL(WS-OCCP-CTR)               
                             F398-UNEMPLY-DECL(WS-OCCP-CTR)                     
               ELSE                                                             
                  IF (WS-AGE-CMPLTD >= 18)  AND (WS-AGE-CMPLTD <= 30)           
                     MOVE   'Y'   TO                                            
                             WS-C398-UNEMPLYMNT-DECL(WS-OCCP-CTR)               
                             F398-UNEMPLY-DECL(WS-OCCP-CTR)                     
                             WS-C398-STUDENT-PASS(WS-OCCP-CTR)                  
                             F398-STUDENT-PASS(WS-OCCP-CTR)                     
                  END-IF                                                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
      *-------------------------------*                                         
      * DEATH CERTIFICATE             *                                         
      *-------------------------------*                                         
           IF K820-NUM-MARITAL-STATUS  =  '3' OR 'W'                            
               MOVE   'Y'         TO                                            
                             WS-C398-DEATH-CERTF(WS-OCCP-CTR)                   
                             F398-DEATH-CERTF(WS-OCCP-CTR)                      
               MOVE K820-NUM-NRIC-SPOUSE   TO                                   
                             WS-C398-NRIC-DTH-CERTF(WS-OCCP-CTR)                
                             F398-NRIC-DEATH-CERTF(WS-OCCP-CTR)                 
               MOVE K820-NME-SPOUSE        TO                                   
                             WS-C398-NME-DTH-CERTF(WS-OCCP-CTR)                 
                             F398-NME-DEATH-CERTF(WS-OCCP-CTR)                  
           END-IF.                                                              
                                                                                
      *-------------------------------*                                         
      * DIVORCE CERTIFICATE           *                                         
      *-------------------------------*                                         
           IF K820-NUM-MARITAL-STATUS  =  '4' OR 'D'                            
               MOVE   'Y'         TO                                            
                             WS-C398-DIVORCE-CERTF(WS-OCCP-CTR)                 
                             F398-DIVORCE-CERTF(WS-OCCP-CTR)                    
           END-IF.                                                              
                                                                                
      *-------------------------------*                                         
      * MARRIAGE CONSENT OF MINOR     *                                         
      *-------------------------------*                                         
           IF (K820-NUM-RELATIONSHIP  =  '09')  AND                             
              (WS-AGE-CMPLTD >= 18  AND  WS-AGE-CMPLTD < 21)                    
               MOVE   'Y'         TO                                            
                             WS-C398-MARRIAGE-CONSENT(WS-OCCP-CTR)              
                             F398-MARRIAGE-CONSNT(WS-OCCP-CTR)                  
           END-IF.                                                              
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5000-WRITE-BP13F398.                                                     
      *---------------------------------------------------------------*         
           MOVE WS-CURR-DATE    TO F398-DTE-UPDATE.                             
           MOVE 'BP13C398'      TO F398-NUM-USERID.                             
           WRITE BP13F398-REC.                                                  
           ADD   1   TO WS-F398-WRITE-CNT.                                      
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-PARA.                                                         
      *---------------------------------------------------------------*         
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13C39A       ' WS-EDT-DATE.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-F520-READ-CNT    TO WS-EDIT1.                                
           DISPLAY 'NO OF RECS READ FROM BP13F520  : ' WS-EDIT1.                
           MOVE WS-K820-READ-CNT    TO WS-EDIT1.                                
           DISPLAY 'NO OF RECS READ FROM BP13K820  : ' WS-EDIT1.                
           MOVE WS-K800-READ-CNT    TO WS-EDIT1.                                
           DISPLAY 'NO OF RECS READ FROM BP13K800  : ' WS-EDIT1.                
           MOVE WS-F398-WRITE-CNT    TO WS-EDIT1.                               
           DISPLAY 'NO OF RECS WRITTEN TO BP13F398 : ' WS-EDIT1.                
           MOVE WS-PRINT-CASE-CNT  TO WS-EDIT1.                                 
           DISPLAY 'NO OF CASES PRINTED            : ' WS-EDIT1.                
           DISPLAY '*---------------------------------------------*'.           
                                                                                
           CLOSE   BP13F520                                                     
                   BP13K200                                                     
                   BP13K800                                                     
                   BP13K820                                                     
                   BP13F398.                                                    
                                                                                
           IF WS-K200-STATUS  NOT =  ZEROS AND 97                               
              DISPLAY 'BP13K200 - CLOSE ERROR, STATUS ' WS-K200-STATUS          
              MOVE    WS-K200-STATUS     TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS  NOT =  ZEROS AND 97                               
              DISPLAY 'BP13K800 - CLOSE ERROR, STATUS ' WS-K800-STATUS          
              MOVE    WS-K800-STATUS     TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS  NOT =  ZEROS AND 97                               
              DISPLAY 'BP13K820 - CLOSE ERROR, STATUS ' WS-K820-STATUS          
              MOVE    WS-K820-STATUS     TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
