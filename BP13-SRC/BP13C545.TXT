       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C545.                                                 
       AUTHOR.        ANNALYN C BANTA.                                          
       DATE-WRITTEN.  15/09/94.                                                 
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO CALCULATE THE TOTAL NUMBER OF      *                
      *                  UNITS FOR THE DIFFERENT ALLOC SCH.    *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY   DATE     DETAILS                          *                
      *------------------------------                          *                
      *BP130341 LSB 970709 - COMPUTE BY ALLOTED % FOR PRIORITY *                
      *                      INSTEAD OF BY PROPORTION          *                
      *BP130350 LSB 970904 - COMPUTE CCC, COMPUTE UPG FIRST    *                
      *                      >.5, ROUND UP TO 1,BAL TO GIVE NHH*                
      *BP130445 LSB 980206 - READ BP13K010, UPDATE K712 INSTEAD*                
      *                      OF READ BP13K712 SEQUENTIALLY     *                
      *BP130695 NOR 990622 - MOVE ZEROS K712 NUMERIC VARIABLES *                
      *BP130720 SM  990805 - COMPUTE APA3 FLAT SUPPLY          *                
      *BP130652 YCH 991012 - CHANGE THE DATE TO DROP 19        *                
      *BP130836 LSB 004242 - IMPL ONLINE, BATCH NO CHG         *                
      *BP133504 IL5 200109 - ADD CHECK TO RECALCULATE FLAT     *                
      *                      SUPPLY FOR FT/UG WHEN REQ-FT      *                
      *                      < THAN FLAT-FT                    *                
      *BP133562 IL5 140409 - ADD CHECK TO RECALCULATE FLAT     *                
      *                      SUPPLY FOR FT/UG WHEN REQ-FT      *                
      *                      < THAN FLAT-FT                    *                
      *BP133763 IL5 041209 - NEW POLICY TO ALLOCATE 95% OF     *                
      *                      FLAT TO FT                        *                
      *BP134861 EAA1 220413  EXPAND BP13K010 TO 600            *                
      *BP135028 SMR2 220813  REMOVE REQ-NT, FLAT-NT & REG-NT   *                
      *BP135458 SMR2 040814  EXPAND BP13K712 TO 350            *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K010 ASSIGN TO BP13K010                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS SEQUENTIAL                                 
                  RECORD KEY      IS K010-KEY-FLD                               
                  FILE STATUS     IS WS-K010-STATUS.                            
                                                                                
           SELECT BP13F713 ASSIGN TO BP13F713.                                  
                                                                                
           SELECT BP13K712 ASSIGN TO BP13K712                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K712-KEY-FLD                               
                  FILE STATUS     IS WS-K712-STATUS.                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K010                                                             
           RECORD CONTAINS 600 CHARACTERS.                                      
       COPY BP13K010.                                                           
                                                                                
       FD  BP13F713                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 20 CHARACTERS                                        
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F713.                                                           
                                                                                
       FD  BP13K712                                                             
           RECORD CONTAINS 350 CHARACTERS.                                      
       COPY BP13K712.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       COPY PRIOSCH.                                                            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 WS-K712-STATUS              PIC 99    VALUE ZEROS.                
           05 WS-K010-STATUS              PIC 99    VALUE ZEROS.                
                                                                                
       01  WS-COUNTER.                                                          
           05 WS-K010-READ                PIC 9(5)  VALUE ZEROS.                
           05 WS-K712-WRTN                PIC 9(5)  VALUE ZEROS.                
           05 WS-K712-UPDATE              PIC 9(5)  VALUE ZEROS.                
                                                                                
       01 WS-TOT-REQUEST                  PIC 9(6)  VALUE ZEROS.                
      *01 WS-EXTRA-FLAT                   PIC 9(5)  VALUE ZEROS.                
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-CC                       PIC 99.                               
           05 WS-YY                       PIC 99.                               
           05 WS-MM                       PIC 99.                               
           05 WS-DD                       PIC 99.                               
                                                                                
       01  WS-SYS-TIME                    PIC 9(8).                             
       01  WS-NUM-FLAT-UG                 PIC S9(5).                            
       01  WS-TEMP                        PIC S9V9(10).                         
                                                                                
       01  WS-EOF-K712                    PIC 9     VALUE 0.                    
           88 EOF-K712                    VALUE 1.                              
       01  WS-EOF-K010                    PIC 9     VALUE 0.                    
           88 EOF-K010                    VALUE 1.                              
                                                                                
                                                                                
       01  WS-K712-KEY                    PIC X(10)  VALUE SPACES.              
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       000-MAIN-ROUTINE.                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 250-READ-F713      THRU 250-EXIT.                            
           PERFORM 200-READ-K010      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-K010   THRU 300-EXIT                             
             UNTIL EOF-K010.                                                    
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       100-INITIALIZATION.                                                      
      ******************************************************************        
           OPEN INPUT  BP13K010                                                 
                       BP13F713                                                 
                I-O    BP13K712.                                                
                                                                                
           IF WS-K712-STATUS NOT = 00 AND 97                                    
              MOVE WS-K712-STATUS            TO RETURN-CODE                     
              DISPLAY 'ERROR OPENING BP13K712'                                  
              GO TO 999-CLOSE-FILES.                                            
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              MOVE WS-K010-STATUS            TO RETURN-CODE                     
              DISPLAY 'ERROR OPENING BP13K010'                                  
              GO TO 999-CLOSE-FILES.                                            
                                                                                
           ACCEPT WS-SYS-DATE              FROM DATE YYYYMMDD.                  
           ACCEPT WS-SYS-TIME              FROM TIME.                           
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       200-READ-K010.                                                           
      ******************************************************************        
           READ BP13K010 AT END                                                 
                MOVE 1 TO WS-EOF-K010                                           
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1  TO WS-K010-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       250-READ-F713.                                                           
      ******************************************************************        
           READ BP13F713 AT END                                                 
                DISPLAY 'BP13F713 EMPTY FILE '                                  
                GO TO 999-CLOSE-FILES.                                          
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       260-READ-K712.                                                           
      ******************************************************************        
           READ  BP13K712                                                       
           IF WS-K712-STATUS = 00 OR 23                                         
              NEXT SENTENCE                                                     
           ELSE                                                                 
              DISPLAY 'ERRO READING BP13K712, STATUS = '                        
                      WS-K712-STATUS                                            
              GO TO 999-CLOSE-FILES.                                            
       260-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-K010.                                                        
      ******************************************************************        
                                                                                
           IF K010-KEY-FLD = SPACES OR LOW-VALUES                               
                 PERFORM 200-READ-K010           THRU 200-EXIT                  
                 GO TO 300-EXIT                                                 
           END-IF.                                                              
                                                                                
           IF F713-CDE-COHORT = 'MT'                                            
                                                                                
              IF K010-NUM-TOT-MTF > 0                                           
                  MOVE K010-CDE-NT-ZONE TO K712-CDE-NT-ZONE                     
                  MOVE K010-CDE-FLAT-TY TO K712-CDE-FLAT-TY                     
                  MOVE 'MT'             TO K712-CDE-COHORT                      
                  MOVE 'MTF'            TO K712-CDE-ALLOC-SCH                   
                  PERFORM 260-READ-K712 THRU 260-EXIT                           
                  PERFORM 310-UPDATE-BP13K712                                   
              END-IF                                                            
                                                                                
              IF K010-NUM-TOT-CCC > 0                                           
                  MOVE K010-CDE-NT-ZONE TO K712-CDE-NT-ZONE                     
                  MOVE K010-CDE-FLAT-TY TO K712-CDE-FLAT-TY                     
                  MOVE 'CC'             TO K712-CDE-COHORT                      
                  MOVE 'CCC'            TO K712-CDE-ALLOC-SCH                   
                  PERFORM 260-READ-K712 THRU 260-EXIT                           
                  PERFORM 310-UPDATE-BP13K712                                   
              END-IF                                                            
                                                                                
              IF K010-NUM-TOT-RH > 0                                            
                 MOVE K010-CDE-NT-ZONE TO K712-CDE-NT-ZONE                      
                 MOVE K010-CDE-FLAT-TY TO K712-CDE-FLAT-TY                      
                 MOVE 'MT'             TO K712-CDE-COHORT                       
                 MOVE 'RH '            TO K712-CDE-ALLOC-SCH                    
                 PERFORM 260-READ-K712 THRU 260-EXIT                            
                 PERFORM 310-UPDATE-BP13K712                                    
              END-IF                                                            
                                                                                
              IF K010-NUM-TOT-REN > 0                                           
                 MOVE K010-CDE-NT-ZONE TO K712-CDE-NT-ZONE                      
                 MOVE K010-CDE-FLAT-TY TO K712-CDE-FLAT-TY                      
                 MOVE 'MT'             TO K712-CDE-COHORT                       
                 MOVE 'REN'            TO K712-CDE-ALLOC-SCH                    
                 PERFORM 260-READ-K712 THRU 260-EXIT                            
                 PERFORM 310-UPDATE-BP13K712                                    
              END-IF                                                            
                                                                                
              IF K010-NUM-TOT-JB > 0                                            
                 MOVE K010-CDE-NT-ZONE TO K712-CDE-NT-ZONE                      
                 MOVE K010-CDE-FLAT-TY TO K712-CDE-FLAT-TY                      
                 MOVE 'MT'             TO K712-CDE-COHORT                       
                 MOVE 'JB '            TO K712-CDE-ALLOC-SCH                    
                 PERFORM 260-READ-K712 THRU 260-EXIT                            
                 PERFORM 310-UPDATE-BP13K712                                    
              END-IF                                                            
                                                                                
              IF K010-NUM-TOT-RDC > 0                                           
                 MOVE K010-CDE-NT-ZONE TO K712-CDE-NT-ZONE                      
                 MOVE K010-CDE-FLAT-TY TO K712-CDE-FLAT-TY                      
                 MOVE 'MT'             TO K712-CDE-COHORT                       
                 MOVE 'RDC'            TO K712-CDE-ALLOC-SCH                    
                 PERFORM 260-READ-K712 THRU 260-EXIT                            
                 PERFORM 310-UPDATE-BP13K712                                    
              END-IF                                                            
                                                                                
              IF K010-NUM-TOT-TCP > 0                                           
                 MOVE K010-CDE-NT-ZONE TO K712-CDE-NT-ZONE                      
                 MOVE K010-CDE-FLAT-TY TO K712-CDE-FLAT-TY                      
                 MOVE 'MT'             TO K712-CDE-COHORT                       
                 MOVE 'TCP'            TO K712-CDE-ALLOC-SCH                    
                 PERFORM 260-READ-K712 THRU 260-EXIT                            
                 PERFORM 310-UPDATE-BP13K712                                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
           IF F713-CDE-COHORT = 'PA'                                            
              IF K010-NUM-TOT-PA > 0                                            
                 MOVE K010-CDE-NT-ZONE TO K712-CDE-NT-ZONE                      
                 MOVE K010-CDE-FLAT-TY TO K712-CDE-FLAT-TY                      
                 MOVE 'PA'             TO K712-CDE-COHORT                       
                 MOVE 'PA'            TO K712-CDE-ALLOC-SCH                     
                 PERFORM 260-READ-K712 THRU 260-EXIT                            
                                                                                
                 IF K712-NUM-FLAT-UG-APA3 IS NOT NUMERIC                        
                    MOVE ZEROES TO K712-NUM-FLAT-UG-APA3                        
                 END-IF                                                         
                                                                                
                 IF K712-NUM-FLAT-UG-APA3 > ZEROES                              
                    CONTINUE                                                    
                 ELSE                                                           
                    MOVE K010-NUM-FLAT-UPG-APA3 TO                              
                                     K712-NUM-FLAT-UG-APA3                      
                 END-IF                                                         
                                                                                
                 PERFORM 310-UPDATE-BP13K712 THRU 310-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-K010           THRU 200-EXIT.                       
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       310-UPDATE-BP13K712.                                                     
      ******************************************************************        
                                                                                
           IF WS-K712-STATUS = 00                                               
              PERFORM 400-CAL-TOT-UNIT-AVAILABLE THRU 400-EXIT                  
              PERFORM 500-CAL-FLAT-ALLOCATION    THRU 500-EXIT                  
                                                                                
              IF K712-NUM-FLAT-U3 = SPACES OR LOW-VALUES                        
                 MOVE ZEROS      TO K712-NUM-FLAT-U3                            
              END-IF                                                            
              IF K712-NUM-REG-NH = SPACES OR LOW-VALUES                         
              MOVE ZEROS        TO K712-NUM-REG-NH                              
              END-IF                                                            
              IF K712-NUM-REG-UG = SPACES OR LOW-VALUES                         
              MOVE ZEROS        TO K712-NUM-REG-UG                              
              END-IF                                                            
              IF K712-NUM-REG-U3 = SPACES OR LOW-VALUES                         
                 MOVE ZEROS     TO K712-NUM-REG-U3                              
              END-IF                                                            
                                                                                
              REWRITE BP13K712-REC                                              
              IF WS-K712-STATUS = 00                                            
                 ADD 1                       TO WS-K712-UPDATE                  
              ELSE                                                              
                 MOVE WS-K712-STATUS         TO RETURN-CODE                     
                 DISPLAY 'ERROR REWRITING IN BP13K712, '                        
                          K712-KEY-FLD                                          
                          GO TO 999-CLOSE-FILES                                 
              END-IF                                                            
           ELSE                                                                 
           IF WS-K712-STATUS = 23                                               
              MOVE K712-KEY-FLD              TO WS-K712-KEY                     
              MOVE SPACES                    TO BP13K712-REC                    
              INITIALIZE                        BP13K712-REC                    
              MOVE ZEROS TO       K712-NUM-REQ-NH                               
                                  K712-NUM-REQ-UG                               
                                  K712-NUM-REQ-U3                               
                                  K712-NUM-FLAT-NH                              
                                  K712-NUM-FLAT-UG                              
                                  K712-NUM-FLAT-U3                              
                                  K712-NUM-REG-NH                               
                                  K712-NUM-REG-UG                               
                                  K712-NUM-REG-U3                               
              MOVE WS-K712-KEY TO K712-KEY-FLD                                  
              PERFORM 400-CAL-TOT-UNIT-AVAILABLE THRU 400-EXIT                  
              PERFORM 500-CAL-FLAT-ALLOCATION    THRU 500-EXIT                  
                                                                                
              WRITE BP13K712-REC                                                
              IF WS-K712-STATUS = 00                                            
                 ADD 1                       TO WS-K712-WRTN                    
              ELSE                                                              
                 MOVE WS-K712-STATUS         TO RETURN-CODE                     
                 DISPLAY 'ERROR WRITING IN BP13K712, '                          
                          K712-KEY-FLD                                          
                          GO TO 999-CLOSE-FILES                                 
              END-IF                                                            
           ELSE                                                                 
              MOVE WS-K712-STATUS            TO RETURN-CODE                     
              DISPLAY 'ERROR READING BP13K712, '                                
                       K712-KEY-FLD                                             
                       GO TO 999-CLOSE-FILES                                    
           END-IF.                                                              
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       400-CAL-TOT-UNIT-AVAILABLE.                                              
      ******************************************************************        
                                                                                
           EVALUATE K712-CDE-ALLOC-SCH                                          
           WHEN 'MTF'                                                           
              COMPUTE K712-NUM-TOT-UNIT = (K010-NUM-TOT-MTF -                   
                                           (K010-REG-MTF1   +                   
                                            K010-REG-MTF2   +                   
                                            K010-REG-MTF3))                     
                                                                                
             WHEN 'CCC'                                                         
              COMPUTE K712-NUM-TOT-UNIT = (K010-NUM-TOT-CCC -                   
                                           (K010-REG-CCC1   +                   
                                            K010-REG-CCC2   +                   
                                            K010-REG-CCC3))                     
                                                                                
             WHEN 'JB '                                                         
              COMPUTE K712-NUM-TOT-UNIT = (K010-NUM-TOT-JB  -                   
                                           (K010-REG-JB1    +                   
                                            K010-REG-JB2    +                   
                                            K010-REG-JB3))                      
                                                                                
             WHEN 'PA '                                                         
              COMPUTE K712-NUM-TOT-UNIT = (K010-NUM-TOT-PA  -                   
                                           (K010-REG-PA1    +                   
                                            K010-REG-PA2    +                   
                                            K010-REG-PA3))                      
                                                                                
             WHEN 'TCP'                                                         
              COMPUTE K712-NUM-TOT-UNIT = (K010-NUM-TOT-TCP -                   
                                           (K010-REG-TCP1   +                   
                                            K010-REG-TCP2   +                   
                                            K010-REG-TCP3))                     
                                                                                
             WHEN 'RH '                                                         
              COMPUTE K712-NUM-TOT-UNIT = (K010-NUM-TOT-RH  -                   
                                           (K010-REG-RH1    +                   
                                            K010-REG-RH2    +                   
                                            K010-REG-RH3))                      
                                                                                
             WHEN 'RDC'                                                         
              COMPUTE K712-NUM-TOT-UNIT = (K010-NUM-TOT-RDC -                   
                                           (K010-REG-RDC1   +                   
                                            K010-REG-RDC2   +                   
                                            K010-REG-RDC3))                     
                                                                                
             WHEN 'REN'                                                         
              COMPUTE K712-NUM-TOT-UNIT = (K010-NUM-TOT-REN -                   
                                           (K010-REG-REN1   +                   
                                            K010-REG-REN2   +                   
                                            K010-REG-REN3))                     
           END-EVALUATE.                                                        
                                                                                
                                                                                
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       500-CAL-FLAT-ALLOCATION.                                                 
      ******************************************************************        
                                                                                
           PERFORM 510-COMPUTE-FOR-UPG THRU 510-EXIT.                           
                                                                                
           IF WS-NUM-FLAT-UG > ZERO                                             
              MOVE WS-NUM-FLAT-UG TO K712-NUM-FLAT-UG.                          
                                                                                
           COMPUTE K712-NUM-FLAT-NH = K712-NUM-TOT-UNIT -                       
                                      K712-NUM-FLAT-UG.                         
                                                                                
           MOVE K010-NUM-NHH             TO K712-NUM-NHH.                       
           MOVE K010-NUM-UPG             TO K712-NUM-UPG.                       
           MOVE WS-SYS-DATE              TO K712-DTE-UPDATE.                    
           MOVE WS-SYS-TIME              TO K712-TME-SYSTEM.                    
           MOVE 'P13C545'                TO K712-USERID.                        
                                                                                
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       510-COMPUTE-FOR-UPG.                                                     
      ******************************************************************        
           COMPUTE WS-NUM-FLAT-UG         = (K712-NUM-TOT-UNIT *                
                                             K010-NUM-UPG / 100).               
                                                                                
      **BP133562                                                                
           IF (K712-NUM-REQ-NH <  K712-NUM-TOT-UNIT -                           
                                 WS-NUM-FLAT-UG) AND                            
               K712-NUM-REQ-NH > 0                                              
              COMPUTE WS-NUM-FLAT-UG      = ((K712-NUM-REQ-NH *                 
                                              K010-NUM-UPG)                     
                                             / K010-NUM-NHH)                    
           ELSE                                                                 
              IF K712-NUM-REQ-NH = 0 AND K712-NUM-REQ-UG > 0                    
                 MOVE K712-NUM-TOT-UNIT  TO  WS-NUM-FLAT-UG                     
              END-IF                                                            
           END-IF.                                                              
      **BP133562                                                                
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
      * THIS PARA WILL NOT BE UNSED WITH THE REVISED POLICY FROM                
      * 3RD QTR 97, FOR PRIORITY COMPUTE BY % ALLOTED TO NHH/UPG                
      ******************************************************************        
      *520-COMPUTE-FOR-PRIORITY.                                                
      ******************************************************************        
      *                                                                         
      *    COMPUTE WS-TOT-REQUEST     =  K712-NUM-REQ-NT +                      
      *                                  K712-NUM-REQ-NH +                      
      *                                  K712-NUM-REQ-UG.                       
      *                                                                         
      *    COMPUTE WS-TEMP = ((K712-NUM-REQ-NT + K712-NUM-REQ-NH) /             
      *                        WS-TOT-REQUEST).                                 
      *                                                                         
      *                                                                         
      *    COMPUTE WS-NUM-FLAT-NH ROUNDED =                                     
      *            (WS-TEMP * K712-NUM-TOT-UNIT) - 0.50.                        
      *                                                                         
      *                                                                         
      *520-EXIT.                                                                
      *    EXIT.                                                                
                                                                                
      ******************************************************************        
       999-CLOSE-FILES.                                                         
      ******************************************************************        
           CLOSE BP13K010                                                       
                 BP13F713                                                       
                 BP13K712.                                                      
                                                                                
           IF WS-K712-STATUS NOT = 00                                           
              MOVE WS-K712-STATUS TO RETURN-CODE                                
              DISPLAY 'ERROR CLOSING BP13K712, ' WS-K712-STATUS.                
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C545 ================*'.              
           DISPLAY '   NO. OF K010 RECORDS READ      = ' WS-K010-READ.          
           DISPLAY '   NO. OF K712 RECORDS WRITTEN   = ' WS-K712-WRTN.          
           DISPLAY '   NO. OF K712 RECORDS REWRITTEN = ' WS-K712-UPDATE.        
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
