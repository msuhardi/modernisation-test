      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13CB3H.                                                 
       AUTHOR.        SANDEEP REDDY.                                            
       DATE-WRITTEN.  01/11/2019.                                               
                                                                                
      ************************************************************              
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                      *              
      *                                                          *              
      *  SYSTEM ID   : BP13                                      *              
      *                                                          *              
      *  OBJECTIVE   : TO PROCESS COLLECTION SYSTEM CASH         *              
      *                INTERFACE AND UPDATE TO BP13KD05.         *              
      *                                                          *              
      *  INPUT FILE  : AC28F526 (COLL SYS INTERFACE FILE)        *              
      *                BP13K800                                  *              
      *  OUTPUT FILE : BP13KD05                                  *              
      *                                                          *              
      *  CHG-NO   BY   ON          DESCRIPTION                   *              
      *  -------- ---- ----------  ----------------------------  *              
      *  BP138045 SRG1 01/11/2019  NEW PROGRAM                   *              
      *  BP138095 KR13 27/11/2019  ADD EHG.                      *              
      ************************************************************              
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT AC28F526 ASSIGN       TO AC28F526.                            
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS WS-K800-STATUS.                    
                                                                                
           SELECT BP13KD05  ASSIGN        TO BP13KD05                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KD05-KEY-FLD                       
                            FILE STATUS   IS WS-KD05-STATUS.                    
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
                                                                                
       FD  AC28F526                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 130 CHARACTERS.                                      
       COPY AC28F526.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13KD05                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13KD05.                                                           
                                                                                
      *--------------------------------------------------------------*          
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------*          
       01  WS-FILE-STATUS.                                                      
           05  WS-KD05-STATUS               PIC 9(2) VALUE ZEROES.              
           05  WS-K800-STATUS               PIC 9(2) VALUE ZEROES.              
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-KD05-WRITE                PIC 9(5) VALUE ZEROES.              
           05  WS-K800-READ                 PIC 9(5) VALUE ZEROES.              
           05  WS-KD05-READ                 PIC 9(5) VALUE ZEROES.              
           05  WS-K800-NOTFD                PIC 9(5) VALUE ZEROES.              
           05  WS-F526-READ                 PIC 9(5) VALUE ZEROES.              
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F526-EOF                  PIC X VALUE 'N'.                    
           05  WS-KD05-EOF                  PIC X VALUE 'N'.                    
           05  WS-KD05-FND                  PIC X VALUE 'N'.                    
           05  WS-KD05-TAG                  PIC X VALUE 'N'.                    
           05  WS-K800-REC-FND              PIC X VALUE SPACES.                 
                                                                                
       01  WS-AMT-TXN                       PIC 9(8)V99 VALUE ZEROS.            
                                                                                
      *-----------------------*                                                 
       PROCEDURE DIVISION.                                                      
      *-----------------------*                                                 
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-AC28F526       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-AC28F526    THRU 3000-EXIT                      
             UNTIL WS-F526-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
           OPEN INPUT  AC28F526                                                 
                       BP13K800                                                 
                I-O    BP13KD05.                                                
                                                                                
           IF WS-KD05-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR BP13KD05. STATUS : ' WS-KD05-STATUS        
              MOVE WS-KD05-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-AC28F526.                                                      
      ******************************************************************        
           READ AC28F526                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-F526-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F526-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-AC28F526.                                                   
      ******************************************************************        
           MOVE ZEROS                       TO WS-AMT-TXN.                      
           MOVE 'N'                         TO WS-KD05-TAG.                     
                                                                                
           IF F526-NUM-RCPTCD = 'AHG  ' OR 'SHG  ' OR 'SRG  ' OR                
                                'SUG  ' OR 'EHG  '                              
              MOVE F526-NUM-REF             TO K800-NUM-REGN                    
              PERFORM 3100-READ-K800        THRU 3100-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-AC28F526       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-READ-K800.                                                          
      ******************************************************************        
           IF F526-AMT-TXN  IS NOT NUMERIC                                      
              MOVE ZEROS                    TO F526-AMT-TXN                     
           END-IF.                                                              
                                                                                
           READ BP13K800.                                                       
           EVALUATE WS-K800-STATUS                                              
               WHEN '00'                                                        
                    MOVE 'Y'                TO WS-K800-REC-FND                  
                    ADD  1                  TO WS-K800-READ                     
                    PERFORM 3110-READ-KD05  THRU 3110-EXIT                      
               WHEN '23'                                                        
                    MOVE 'N'                TO WS-K800-REC-FND                  
                    ADD 1                   TO WS-K800-NOTFD                    
                    DISPLAY 'BP13K800-RECORD NOT FOUND  '                       
                                    K800-NUM-REGN                               
                    PERFORM 3110-READ-KD05  THRU 3110-EXIT                      
               WHEN OTHER                                                       
                    MOVE 'N'                TO WS-K800-REC-FND                  
                    DISPLAY 'BP13K800-READ ERROR: '                             
                                                 WS-K800-STATUS                 
                    MOVE WS-K800-STATUS     TO RETURN-CODE                      
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
             END-EVALUATE .                                                     
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3110-READ-KD05.                                                          
      ******************************************************************        
           MOVE SPACES                      TO KD05-KEY-FLD.                    
           INITIALIZE                          KD05-KEY-FLD.                    
           MOVE F526-NUM-REF                TO KD05-NUM-REGN.                   
           MOVE F526-NUM-UIN                TO KD05-NUM-NRIC.                   
           MOVE 'N'                         TO WS-KD05-EOF                      
                                               WS-KD05-FND.                     
           READ BP13KD05 KEY IS KD05-KEY-FLD.                                   
                                                                                
           EVALUATE WS-KD05-STATUS                                              
               WHEN 00                                                          
                    PERFORM 4000-READ-UPD-KD05 THRU 4000-EXIT                   
               WHEN 10                                                          
               WHEN 23                                                          
                    DISPLAY 'RECORD NOT FOUND IN BP13KD05 ' KD05-KEY-FLD        
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13KD05, STATUS '                   
                    DISPLAY WS-KD05-STATUS                                      
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-READ-UPD-KD05.                                                      
      ******************************************************************        
           MOVE 'Y'                         TO WS-KD05-FND.                     
           ADD 1                            TO WS-KD05-READ.                    
                                                                                
           IF F526-AMT-TXN IS NOT NUMERIC                                       
              MOVE ZERO                     TO F526-AMT-TXN                     
           END-IF.                                                              
           IF KD05-AMT-AHG-GRANT IS NOT NUMERIC                                 
              MOVE ZERO                     TO KD05-AMT-AHG-GRANT               
           END-IF.                                                              
           IF KD05-AMT-HSGNT IS NOT NUMERIC                                     
              MOVE ZERO                     TO KD05-AMT-HSGNT                   
           END-IF.                                                              
           IF KD05-AMT-SHG-GRANT IS NOT NUMERIC                                 
              MOVE ZERO                     TO KD05-AMT-SHG-GRANT               
           END-IF.                                                              
           IF KD05-AMT-SUG-GRANT IS NOT NUMERIC                                 
              MOVE ZERO                     TO KD05-AMT-SUG-GRANT               
           END-IF.                                                              
                                                                                
           EVALUATE F526-NUM-RCPTCD                                             
             WHEN 'AHG  '                                                       
             WHEN 'EHG  '                                                       
                 PERFORM 4100-CHK-AHG-RECOVERY   THRU 4100-EXIT                 
             WHEN 'SHG  '                                                       
                 PERFORM 4200-CHK-SHG-RECOVERY   THRU 4200-EXIT                 
             WHEN 'SUG  '                                                       
                 PERFORM 4300-CHK-SUG-RECOVERY   THRU 4300-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4100-CHK-AHG-RECOVERY.                                                   
      ******************************************************************        
           MOVE ZEROS                       TO WS-AMT-TXN.                      
           IF KD05-AMT-AHG-GRANT > 0 AND F526-AMT-TXN > 0                       
              COMPUTE WS-AMT-TXN = KD05-AMT-AHG-GRANT-RECOVER +                 
                                   F526-AMT-TXN                                 
              IF (KD05-AMT-AHG-GRANT-RECOVER = KD05-AMT-AHG-GRANT)              
              OR (WS-AMT-TXN > KD05-AMT-AHG-GRANT)                              
                 DISPLAY 'GRANT RECOVERY > GRANT ' KD05-KEY-FLD                 
              ELSE                                                              
                 COMPUTE KD05-AMT-AHG-GRANT-RECOVER =                           
                         KD05-AMT-AHG-GRANT-RECOVER +                           
                         F526-AMT-TXN                                           
                 MOVE F526-DTE-TXN         TO KD05-DTE-AHG-GRANT-RECOVER        
                 PERFORM 4400-REWRITE-KD05  THRU 4400-EXIT                      
              END-IF                                                            
           ELSE                                                                 
              DISPLAY 'NO AHG GRANT > ' KD05-KEY-FLD                            
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4200-CHK-SHG-RECOVERY.                                                   
      ******************************************************************        
             MOVE ZEROS                     TO WS-AMT-TXN.                      
             IF KD05-AMT-SHG-GRANT > 0 AND F526-AMT-TXN > 0                     
                COMPUTE WS-AMT-TXN = KD05-AMT-SHG-GRANT-RECOVER +               
                                     F526-AMT-TXN                               
                IF (KD05-AMT-SHG-GRANT-RECOVER = KD05-AMT-SHG-GRANT )           
                OR (WS-AMT-TXN > KD05-AMT-SHG-GRANT)                            
                   DISPLAY 'GRANT RECOVERY > GRANT ' KD05-KEY-FLD               
                ELSE                                                            
                   COMPUTE KD05-AMT-SHG-GRANT-RECOVER =                         
                           KD05-AMT-SHG-GRANT-RECOVER +                         
                           F526-AMT-TXN                                         
                   MOVE F526-DTE-TXN       TO KD05-DTE-SHG-GRANT-RECOVER        
                   PERFORM 4400-REWRITE-KD05 THRU 4400-EXIT                     
                END-IF                                                          
             ELSE                                                               
                DISPLAY 'NO SHG GRANT > ' KD05-KEY-FLD                          
             END-IF.                                                            
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4300-CHK-SUG-RECOVERY.                                                   
      ******************************************************************        
             MOVE ZEROS                     TO  WS-AMT-TXN.                     
             IF KD05-AMT-SUG-GRANT > 0 AND F526-AMT-TXN > 0                     
                COMPUTE WS-AMT-TXN = KD05-AMT-SUG-GRANT-RECOVER +               
                                     F526-AMT-TXN                               
                IF (KD05-AMT-SUG-GRANT-RECOVER = KD05-AMT-SUG-GRANT)            
                OR (WS-AMT-TXN > KD05-AMT-SUG-GRANT)                            
                   DISPLAY 'GRANT RECOVERY > GRANT ' KD05-KEY-FLD               
                ELSE                                                            
                   COMPUTE KD05-AMT-SUG-GRANT-RECOVER =                         
                           KD05-AMT-SUG-GRANT-RECOVER +                         
                           F526-AMT-TXN                                         
                   MOVE F526-DTE-TXN       TO KD05-DTE-SUG-GRANT-RECOVER        
                   PERFORM 4400-REWRITE-KD05 THRU 4400-EXIT                     
                END-IF                                                          
             ELSE                                                               
                DISPLAY 'NO SUG GRANT > ' KD05-KEY-FLD                          
             END-IF.                                                            
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4400-REWRITE-KD05.                                                       
      ******************************************************************        
           MOVE FUNCTION CURRENT-DATE(1:8)  TO KD05-DTE-UPDATE.                 
           MOVE FUNCTION CURRENT-DATE(9:8)  TO KD05-TME-UPDATE.                 
           MOVE 'P13CB3H'                   TO KD05-NUM-USERID.                 
           REWRITE BP13KD05-REC.                                                
                                                                                
           IF WS-KD05-STATUS = 00                                               
              ADD             1             TO WS-KD05-WRITE                    
              MOVE 'Y'                      TO WS-KD05-TAG                      
              MOVE KD05-NUM-NRIC            TO K800-NUM-NRIC1                   
           ELSE                                                                 
              MOVE 'N'                      TO WS-KD05-TAG                      
              MOVE WS-KD05-STATUS           TO RETURN-CODE                      
              DISPLAY 'ERROR WRITING BP13KD05. STATUS : '                       
                                               WS-KD05-STATUS                   
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       9000-CLOSE-FILES.                                                        
      ***************************************************************           
           DISPLAY '********* BP13CB3H *********'.                              
           DISPLAY 'NO OF RECS PROCESSED (AC28F526) = ' WS-F526-READ.           
           DISPLAY 'NO OF RECS READ      (BP13K800) = ' WS-K800-READ.           
           DISPLAY 'NO OF RECS NOTFD     (BP13K800) = ' WS-K800-NOTFD.          
           DISPLAY 'NO OF RECS READ      (BP13KD05) = ' WS-KD05-READ.           
           DISPLAY 'NO OF RECS UPDATED   (BP13KD05) = ' WS-KD05-WRITE.          
                                                                                
           CLOSE AC28F526                                                       
                 BP13K800                                                       
                 BP13KD05.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING ERROR BP13K800. STATUS : ' WS-K800-STATUS        
              MOVE WS-K800-STATUS           TO RETURN-CODE.                     
                                                                                
           IF WS-KD05-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING ERROR BP13KD05. STATUS : ' WS-KD05-STATUS        
              MOVE WS-KD05-STATUS           TO RETURN-CODE.                     
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
