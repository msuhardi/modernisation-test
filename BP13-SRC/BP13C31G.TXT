      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C31G.                                                 
      *AUTHOR.        TAN M K.                                                  
      *DATE-WRITTEN.  01/09/89.                                                 
                                                                                
      * =========================================================== *           
      *                SYSTEM OF COMMITMENT (BP13)                  *           
      * =========================================================== *           
      *    OBJECTIVE :                                              *           
      *      1.  THIS IS A PROGRAM TO PRINT A LIST OF PAYMENT       *           
      *          FOR STAMPING AFTER SALES AGREEMENT                 *           
      *          (FINAL PINK VOUCHER)                               *           
      *                                                             *           
      *    INPUT FILES :                                            *           
      *      1.  BP13F315 - TRANSACTION FILE                        *           
      *      2.  BP13K893 - SOC MASTER APPLN HISTORY FILE           *           
      *      2.  BP13K410 -                                         *           
      *                                                             *           
      *    OUTPUT FILE :                                            *           
      *      1.  AG07F220 - VOUCHER STAMPING FILE                   *           
      *      2.  BP13K625 - STAMP FEE MASTER FILE                   *           
      *      3.  AG07F225 - VOUCHER FILE FOR STAMPFEE               *           
      *                                                             *           
      *    LISTING     :   1.  BP13L31G - CONTROL LISTING           *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  BY   DATE   DESCRIPTION                            *           
      * -------- ---- ------ -----------                            *           
      * BP134998 JGO1 060913 PROGRAM COPIED FROM BP13C314.          *           
      * BP135096 JGO1 161013 UPDATE VOUCHER FROM 'SU' TO 'SS'       *           
      * BP135017 JGO1 280414 PASS F315-USERID TO F225 STAFF-ID-PREP *           
      * BP135247 JGO1 300414 PASS F315-USERID IF ID NOT = BP13P001  *           
      * BP135832 JGO1 300414 UPDATE VALUES PASS FOR BANK ANG IBG    *           
      * BP137140 PCL4 191217 TO REDEFINE AG07 FILE ERROR CODE       *           
      * BP138339 KR13 070820 REMOVED P13L31GA - IOM8 CONVERSION.    *           
      * BP139164 040522 KR13 ENHANCE AGREEMENT STAMPING MODULE TO   *           
      *                      VALIDATE POSTAL CODE                   *           
      * =========================================================== *           
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F315  ASSIGN       TO BP13F315.                           
           SELECT BP13K893  ASSIGN       TO BP13K893                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K893-KEY-FLD                        
                            FILE STATUS  IS BP13K893-STATUS.                    
           SELECT BP13K410  ASSIGN       TO BP13K410                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K410-KEY-FLD                        
                            FILE STATUS  IS BP13K410-STATUS.                    
           SELECT BP13K625  ASSIGN       TO BP13K625                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K625-KEY-FLD                        
                            FILE STATUS  IS BP13K625-STATUS.                    
                                                                                
           SELECT AG07F225  ASSIGN       TO AG07F225.                           
           SELECT BP13L31G  ASSIGN       TO BP13L31G.                           
           SELECT P13L31GB  ASSIGN       TO P13L31GB.                           
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
       FILE SECTION.                                                            
      *-------------*                                                           
                                                                                
       FD   BP13F315                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       COPY BP13F315.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD   BP13K410                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K410.                                                           
                                                                                
       FD   BP13L31G                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       01   L31G-PRINT-REC                    PIC X(132).                       
                                                                                
       FD   P13L31GB                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       01   L31GB-PRINT-REC                   PIC X(132).                       
                                                                                
       FD   BP13K625                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K625.                                                           
                                                                                
       FD   AG07F225                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 600 CHARACTERS.                                     
       COPY AG07F225.                                                           
                                                                                
      *=======================*                                                 
       WORKING-STORAGE SECTION.                                                 
      *=======================*                                                 
       01  WS1-FILE-ACCESS-STATUS.                                              
           05  BP13K893-STATUS         PIC 9(2)      VALUE ZEROS.               
           05  BP13K625-STATUS         PIC 9(2)      VALUE ZEROS.               
           05  BP13K410-STATUS         PIC 9(2)      VALUE ZEROS.               
           05  WS-AG07-STATUS          PIC 9(2)      VALUE ZEROS.               
           05  WS-AG07-STAT-X REDEFINES WS-AG07-STATUS PIC X(2).                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F315-READ        PIC 9(3)      VALUE ZEROS.               
           05  WS-CNT-K893-READ        PIC 9(3)      VALUE ZEROS.               
           05  WS-CNT-K410-READ        PIC 9(3)      VALUE ZEROS.               
           05  WS-CNT-K625-WRITE       PIC 9(3)      VALUE ZEROS.               
           05  WS-CNT-F225-WRITE       PIC 9(3)      VALUE ZEROS.               
           05  WS-CNT-L31G-WRITE       PIC 9(3)      VALUE ZEROS.               
           05  WS-CNT-L31G-DETLS       PIC 9(4)      VALUE ZEROS.               
                                                                                
           05  WS-L31GB-PRINT-CNT      PIC 9(4)      VALUE ZEROS.               
           05  WS-L31GB-PAGE-CNT       PIC 9(5)      VALUE ZEROS.               
           05  WS-L31GB-REC-ON-PAGE-CNT  PIC 9(2)    VALUE 66.                  
                                                                                
       01  WS-ACCUM-VARIABLES.                                                  
           05  WS-NO-OF-PAGE           PIC 9(5)      VALUE ZEROS.               
           05  WS-TOTAL-ACTUAL-LEASE   PIC S9(6)V99  VALUE ZEROS.               
           05  WS-TOTAL-DUP-LEASE      PIC S9(3)V99  VALUE ZEROS.               
           05  WS-TOTAL-LEASE          PIC S9(7)V99  VALUE ZEROS.               
           05  WS-COMP-TOT-LEASE       PIC S9(7)V99  VALUE ZEROS.               
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F315-EOF-IND         PIC X(1)      VALUE 'N'.                 
               88 WS-F315-EOF                        VALUE 'Y'.                 
           05  WS-NEW-VOUCHER-NO       PIC X(1)      VALUE 'Y'.                 
           05  WS-NEW-VOUCHER-F225     PIC X(1)      VALUE 'Y'.                 
           05  WS-NEW-BATCH-NO         PIC X(1)      VALUE 'Y'.                 
           05  WS-NEW-HEADER           PIC X(1)      VALUE 'Y'.                 
           05  WS-K625-EOF             PIC X(1)      VALUE 'N'.                 
           05  WS-K893-FND             PIC X(01) VALUE SPACE.                   
               88 K893-FND                      VALUE 'Y'.                      
           05  WS-K893-EOF             PIC X(01) VALUE SPACE.                   
               88 K893-EOF                      VALUE 'Y'.                      
                                                                                
                                                                                
       01  WS-DATES-VARIABLES.                                                  
           05  WS-AO-DATE.                                                      
               10  WS-AO-YEAR          PIC 9(4)      VALUE 0.                   
               10  WS-AO-MM            PIC 99        VALUE 0.                   
               10  WS-AO-DD            PIC 99        VALUE 0.                   
           05  WS-DATE.                                                         
               10 WS-DD                PIC 99        VALUE 00.                  
               10 FILLER               PIC X         VALUE '/'.                 
               10 WS-MM                PIC 99        VALUE 00.                  
               10 FILLER               PIC X         VALUE '/'.                 
               10 WS-YEAR              PIC 9(4)      VALUE 00.                  
           05  WS-PR-CUR-DATE.                                                  
               10 WS-PC-DD             PIC 99        VALUE 00.                  
               10 FILLER               PIC X         VALUE '/'.                 
               10 WS-PC-MM             PIC 99        VALUE 00.                  
               10 FILLER               PIC X         VALUE '/'.                 
               10 WS-PC-CCYY           PIC 9999      VALUE ZERO.                
           05  WS-CURRENT-CCYYMMDD.                                             
                   15 WS-CUR-CCYY      PIC 9999      VALUE ZERO.                
                   15 WS-CUR-MM        PIC 99        VALUE 00.                  
                   15 WS-CUR-DD        PIC 99        VALUE 00.                  
                                                                                
       01  WS-DEFAULT-VALUES.                                                   
           05  WS-DEF-DUP-LEASE        PIC 9(2)V99   VALUE  0.                  
           05  WS-DUP-LEASE            PIC 9(2)V99   VALUE  0.                  
           05  WS-MAX-LINES            PIC 9(2)      VALUE  2.                  
                                                                                
       01  WS-SCH-ACC.                                                          
           05  WS-SCH-1-4              PIC X(4)      VALUE SPACES.              
           05  WS-SCH-5-8              PIC X(4)      VALUE SPACES.              
           05  WS-SCH-9-9              PIC X(1)      VALUE SPACE.               
           05  WS-SCH-10-11            PIC X(2)      VALUE SPACES.              
                                                                                
                                                                                
       01  WS-NUM-VOUCHER              PIC X(11)     VALUE SPACES.              
       01  WS-NUM-VOUCHER-F225         PIC X(11)     VALUE SPACES.              
       01  WS-NUM-GL-ACCT              PIC X(13)     VALUE SPACES.              
       01  WS-ANNUAL-RENT              PIC 9(7)V99   VALUE ZEROES.              
       01  WS-NUM-BATCH-TYPE           PIC X(4)      VALUE SPACES.              
       01  WS-NUM-FIN-YEAR             PIC 9(4)      VALUE ZEROES.              
       01  WS-NUM-BATCH-SERIAL         PIC 9(5)      VALUE ZEROES.              
       01  WS-SEQ-NO-F225              PIC 9(2)      VALUE ZEROES.              
       01  WS-NUM-HDB-REF              PIC X(11)     VALUE SPACES.              
       01  WS-NUM-DOC                  PIC X(3)      VALUE SPACES.              
       01  WS-NUM-DOC-TYP              PIC X(1)      VALUE SPACES.              
       01  WS-NUM-DOC-CNT              PIC 9(1)      VALUE ZEROES.              
       01  WS-NET-AMT-F225             PIC 9(7)V99   VALUE ZEROS.               
                                                                                
       01  WS-AMT-EARMARK              PIC 9(10)V99  VALUE ZEROES.              
       01  WS-OTHER-VARIABLES.                                                  
           05  WS-NUM-UNIT.                                                     
               10  WS-HSE-MAIN            PIC X(4)   VALUE SPACES.              
               10  WS-HSE-SUB             PIC X(3)   VALUE SPACES.              
           05  WS-NUM-BLK-HSE.                                                  
               10  WS-BLK-HSE-DESC        PIC X(4)   VALUE 'BLK '.              
               10  WS-BLK-HSE-NO          PIC X(6)   VALUE SPACES.              
           05  WS-STAFF-START             PIC X(9)   VALUE '29586H   '.         
                                                                                
      *----------------------------------------------------------*              
      *    WS LINKAGE VARIABLES PASSED TO "AG07C907"             *              
      *----------------------------------------------------------*              
       COPY AG7C907C.                                                           
      *----------------------------------------------------------*              
      *    WS LINKAGE VARIABLES PASSED TO "BP13C906"             *              
      *----------------------------------------------------------*              
       COPY P13C906C.                                                           
      *----------------------------------------------------------*              
      *    WS LINKAGE VARIABLES PASSED TO "AB05C902"             *              
      *----------------------------------------------------------*              
       COPY AB05902C.                                                           
                                                                                
      *----------------------------------------------------------*              
      *        WS FOR LIST OF PAYMENT  FOR STAMPING              *              
      *----------------------------------------------------------*              
       01  L31G-PR-HDG-01.                                                      
           05  FILLER              PIC X(8)    VALUE 'BP13L31G'.                
           05  FILLER              PIC X(10)   VALUE '      HDB3'.              
           05  FILLER              PIC X(28)   VALUE SPACES.                    
           05  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(12)   VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'DATE : '.                 
           05  L31G-CURRENT-DATE   PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(6)    VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'PAGE : '.                 
           05  L31G-PAGE           PIC ZZZZ9.                                   
                                                                                
       01  L31G-PR-HDG-02.                                                      
           05  FILLER              PIC X(30)   VALUE                            
               'HOUSING AND DEVELOPMENT BOARD'.                                 
           05  FILLER              PIC X(21)   VALUE SPACES.                    
           05  FILLER              PIC X(30)   VALUE                            
               'P A Y M E N T    V O U C H E R'.                                
           05  FILLER              PIC X(25)   VALUE SPACES.                    
           05  L31G-COPY-NAME      PIC X(20)   VALUE SPACES.                    
                                                                                
       01  L31G-PR-HDG-03.                                                      
           05  FILLER              PIC X(46)   VALUE SPACES.                    
           05  FILLER              PIC X(85)   VALUE ALL '-'.                   
                                                                                
       01  L31G-PR-HDG-04.                                                      
           05  FILLER              PIC X(46)   VALUE                            
               'SALES UNIT (AGREEMENT TEAM)'.                                   
           05  FILLER              PIC X(1)    VALUE '¦'.                       
           05  FILLER              PIC X(27)   VALUE                            
               ' FOR FINANCE DEPARTMENT USE'.                                   
           05  FILLER              PIC X(21)   VALUE SPACES.                    
           05  FILLER              PIC X(17)   VALUE                            
               '¦  BATCH NO    : '.                                             
           05  L31G-BATCH-NO       PIC X(14)   VALUE SPACES.                    
           05  FILLER              PIC X(4)    VALUE SPACES.                    
           05  FILLER              PIC X(1)    VALUE '¦'.                       
                                                                                
       01  L31G-PR-HDG-05.                                                      
           05  FILLER              PIC X(46)   VALUE SPACES.                    
           05  FILLER              PIC X(1)    VALUE '¦'.                       
           05  FILLER              PIC X(48)   VALUE SPACES.                    
           05  FILLER              PIC X(1)    VALUE '¦'.                       
           05  FILLER              PIC X(34)   VALUE SPACES.                    
           05  FILLER              PIC X(1)    VALUE '¦'.                       
                                                                                
       01  L31G-PR-HDG-06.                                                      
           05  FILLER              PIC X(46)   VALUE 'SYSTEM CODE : 04'.        
           05  FILLER              PIC X(14)   VALUE '¦ CHECKED BY :'.          
           05  FILLER              PIC X(35)   VALUE SPACES.                    
           05  FILLER              PIC X(17)   VALUE '¦  VOUCHER NO  :'.        
           05  L31G-VOUCHER-NO     PIC X(11)   VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE SPACES.                    
           05  FILLER              PIC X(1)    VALUE '¦'.                       
                                                                                
       01  L31G-PR-HDG-07.                                                      
           05  FILLER              PIC X(46)   VALUE                            
              'GL CODE     : 0000000104250'.                                    
           05  FILLER              PIC X(85)   VALUE ALL '-'.                   
                                                                                
       01  L31G-PR-HDG-08.                                                      
           05  FILLER              PIC X(46)   VALUE                            
              'DESCRIPTION : STAMP FEES'.                                       
                                                                                
       01  L31G-PR-HDG-09.                                                      
           05  FILLER              PIC X(53)   VALUE                            
              'PAYABLE TO  : COMMISSIONER OF STAMP DUTIES         '.            
           05  FILLER              PIC X(13)   VALUE  SPACES.                   
           05  FILLER              PIC X(20)   VALUE                            
              'BANK   : DBS        '.                                           
           05  FILLER              PIC X(11)   VALUE  SPACES.                   
           05  FILLER              PIC X(25)   VALUE                            
              'BANK CODE   : DBSSSGSGXXX'.                                      
                                                                                
       01  L31G-PR-HDG-09A.                                                     
           05  FILLER              PIC X(24)   VALUE                            
              'A/C NO      : 0010468600'.                                       
           05  FILLER              PIC X(42)   VALUE  SPACES.                   
                                                                                
       01  L31G-PR-HDG-10.                                                      
           05  FILLER              PIC X(11)   VALUE                            
               'REMITTANCE '.                                                   
           05  FILLER              PIC X(55)   VALUE                            
              'ADVICE  DESPATCH TO :  C/O SC(STAMPING) OF SALES UNIT'.          
                                                                                
       01  L31G-PR-HDG-11.                                                      
           05  FILLER              PIC X(3)    VALUE 'NO.'.                     
           05  FILLER              PIC X(4)    VALUE SPACES.                    
           05  FILLER              PIC X(11)   VALUE                            
               'SCH-ACCT-NO'.                                                   
           05  FILLER              PIC X(5)    VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'REGN NO'.                 
           05  FILLER              PIC X(3)    VALUE SPACES.                    
           05  FILLER              PIC X(3)    VALUE 'BLK'.                     
           05  FILLER              PIC X(4)    VALUE SPACES.                    
           05  FILLER              PIC X(24)   VALUE                            
               'FLAT ADDRESS/STREET NAME'.                                      
           05  FILLER              PIC X(3)    VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'FLAT NO'.                 
           05  FILLER              PIC X(5)    VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'DATE OF'.                 
           05  FILLER              PIC X(8)    VALUE SPACES.                    
           05  FILLER              PIC X(12)   VALUE 'ACTUAL LEASE'.            
           05  FILLER              PIC X(8)    VALUE SPACES.                    
           05  FILLER              PIC X(3)    VALUE 'DUP'.                     
           05  FILLER              PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(10)   VALUE 'TOTAL'.                   
                                                                                
       01  L31G-PR-HDG-12.                                                      
           05  FILLER              PIC X(79)   VALUE SPACES.                    
           05  FILLER              PIC X(9)    VALUE 'AGR ORDER'.               
           05  FILLER              PIC X(8)    VALUE SPACES.                    
           05  FILLER              PIC X(9)    VALUE 'STAMP FEE'.               
           05  FILLER              PIC X(8)    VALUE SPACES.                    
           05  FILLER              PIC X(5)    VALUE 'LEASE'.                   
                                                                                
       01  L31G-PR-DETAIL.                                                      
           05  L31G-NO             PIC ZZZ9.                                    
           05  FILLER              PIC X(2)    VALUE SPACES.                    
           05  L31G-SCH-ACC.                                                    
               10 L31G-SCH-1-4     PIC X(4)    VALUE SPACES.                    
               10 FILLER           PIC X(1)    VALUE '-'.                       
               10 L31G-SCH-5-8     PIC X(4)    VALUE SPACES.                    
               10 FILLER           PIC X(1)    VALUE '-'.                       
               10 L31G-SCH-9-9     PIC X(1)    VALUE SPACE.                     
               10 FILLER           PIC X(1)    VALUE '-'.                       
               10 L31G-SCH-10-11   PIC X(2)    VALUE SPACES.                    
           05  FILLER              PIC X(2)    VALUE SPACES.                    
           05  L31G-REGN-NO        PIC X(8)    VALUE SPACES.                    
           05  FILLER              PIC X(2)    VALUE SPACES.                    
           05  L31G-BLK-NO         PIC X(5)    VALUE SPACES.                    
           05  FILLER              PIC X(3)    VALUE SPACES.                    
           05  L31G-STREET         PIC X(23)   VALUE SPACES.                    
           05  FILLER              PIC X(4)    VALUE SPACES.                    
           05  L31G-FLAT-NO.                                                    
               10  FILLER          PIC X(1)    VALUE '#'.                       
               10  L31G-LEVEL-NO   PIC X(2)    VALUE SPACES.                    
               10  FILLER          PIC X(1)    VALUE '-'.                       
               10  L31G-UNIT-NO    PIC X(4)    VALUE SPACES.                    
           05  FILLER              PIC X(4)    VALUE SPACES.                    
           05  L31G-AGR-ORDER-DATE PIC X(12)   VALUE SPACES.                    
           05  FILLER              PIC X(4)    VALUE SPACES.                    
           05  L31G-LEASE          PIC -$$$,$$9.99.                             
           05  FILLER              PIC X(6)    VALUE SPACES.                    
           05  L31G-DUP-LEASE      PIC $$9.99.                                  
           05  FILLER              PIC X(2)    VALUE SPACES.                    
           05  L31G-DETL-TOTAL     PIC -$$$$,$$9.99.                            
                                                                                
       01  L31G-PR-TOTAL-LINE.                                                  
           05  FILLER              PIC X(39)   VALUE SPACES.                    
           05  FILLER              PIC X(13)   VALUE '(SERIAL NO : '.           
           05  L31G-VR-NO-LOW      PIC X(4)    VALUE '0001'.                    
           05  FILLER              PIC X(4)    VALUE ' TO '.                    
           05  L31G-VR-NO-HIGH     PIC X(4)    VALUE '0010'.                    
           05  FILLER              PIC X(1)    VALUE ')'.                       
           05  FILLER              PIC X(13)   VALUE SPACES.                    
           05  FILLER              PIC X(16)   VALUE 'TOTAL : '.                
           05  L31G-TOTAL-ACTUAL   PIC -$$$$,$$9.99.                            
           05  FILLER              PIC X(5)    VALUE SPACES.                    
           05  L31G-TOTAL-DUP      PIC $$$9.99.                                 
           05  L31G-TOTAL-LEASE    PIC -$$,$$$,$$9.99.                          
                                                                                
       01  L31G-PR-DASH-LINE.                                                   
           05  FILLER              PIC X(94)   VALUE SPACES.                    
           05  FILLER              PIC X(38)   VALUE ALL '-'.                   
                                                                                
       01  L31G-PR-FTG-01.                                                      
           05  FILLER              PIC X(15)   VALUE 'NET PAYABLE  : '.         
           05  L31G-AMT-WORDS.                                                  
               10  L31G-AMT-DESC1  PIC X(60)   VALUE SPACES.                    
               10  L31G-AMT-DESC2  PIC X(60)   VALUE SPACES.                    
                                                                                
       01  L31G-PR-FTG-02.                                                      
           05  FILLER              PIC X(39)   VALUE SPACES.                    
           05  FILLER              PIC X(14)   VALUE 'CHECKED BY : '.           
           05  FILLER              PIC X(21)   VALUE ALL '_'.                   
           05  FILLER              PIC X(4)    VALUE SPACES.                    
           05  FILLER              PIC X(16)   VALUE 'CERTIFIED BY : '.         
           05  FILLER              PIC X(21)   VALUE ALL '_'.                   
                                                                                
      *-----------------------------------------------------------*             
      *    DEFINE P13L31GB REPORT LINES                           *             
      *-----------------------------------------------------------*             
       01  L31GB-HEADER1.                                                       
           05  FILLER              PIC X(8)    VALUE 'P13L31GB'.                
           05  FILLER              PIC X(10)   VALUE '      HDB3'.              
           05  FILLER              PIC X(28)   VALUE SPACES.                    
           05  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(12)   VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'DATE : '.                 
           05  L31GB-DATE          PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(6)    VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'PAGE : '.                 
           05  L31GB-PAGE          PIC ZZZZ9.                                   
                                                                                
       01  L31GB-HEADER2.                                                       
           05  FILLER                    PIC X(47) VALUE SPACES.                
           05  FILLER                    PIC X(35)  VALUE                       
               'REPORT ON CASES WITH EARMARK ERROR'.                            
                                                                                
       01  L31GB-HEADER3.                                                       
           05  FILLER               PIC X(4)  VALUE 'S/NO'.                     
           05  FILLER               PIC X(3)  VALUE SPACE.                      
           05  FILLER               PIC X(10) VALUE 'SCH-ACC-NO'.               
           05  FILLER               PIC X(9)  VALUE SPACES.                     
           05  FILLER               PIC X(8)  VALUE 'REGN NO'.                  
           05  FILLER               PIC X(5)  VALUE SPACE.                      
           05  FILLER               PIC X(13) VALUE 'JR VOUCHER NO'.            
           05  FILLER               PIC X(5)  VALUE SPACES.                     
           05  FILLER               PIC X(12) VALUE 'TOTAL AMOUNT'.             
           05  FILLER               PIC X(4)  VALUE SPACES.                     
           05  FILLER               PIC X(17) VALUE 'ERROR DESCRIPTION'.        
                                                                                
       01  L31GB-REC-DETAIL.                                                    
           05  L31GB-SERIAL-NO              PIC ZZZ9.                           
           05  FILLER                       PIC X(3)  VALUE SPACE.              
           05  L31GB-SCH-ACC                PIC X(14) VALUE SPACES.             
           05  FILLER                       PIC X(5)  VALUE SPACE.              
           05  L31GB-REGN-NO                PIC X(8)  VALUE SPACES.             
           05  FILLER                       PIC X(5)  VALUE SPACE.              
           05  L31GB-JR-VCHR-NO             PIC X(13) VALUE SPACES.             
           05  FILLER                       PIC X(5)  VALUE SPACES.             
           05  L31GB-AMT-TOTAL              PIC $$$$,$$9.99.                    
           05  FILLER                       PIC X(5)  VALUE SPACES.             
           05  L31GB-ERROR-DESC             PIC X(30).                          
                                                                                
       01  L31GB-UNDERLINE                  PIC X(132) VALUE ALL '-'.           
                                                                                
      *----------------------------------------------------------*              
      *    WS VARIABLES FOR CONVERTING AMOUNT TO WORDS           *              
      *----------------------------------------------------------*              
       01  WS-SUBSCRIPTS.                                                       
           05  WA-SUB              PIC 999   VALUE 0.                           
           05  WF-SUB              PIC 999   VALUE 0.                           
           05  WL-SUB              PIC 999   VALUE 0.                           
           05  WS-SUB              PIC 999   VALUE 0.                           
           05  WS-FIRST-SUB        PIC 999   VALUE 0.                           
                                                                                
       01  WA-NAMES.                                                            
           05  FILLER              PIC X(10) VALUE '3ONE      '.                
           05  FILLER              PIC X(10) VALUE '3TWO      '.                
           05  FILLER              PIC X(10) VALUE '5THREE    '.                
           05  FILLER              PIC X(10) VALUE '4FOUR     '.                
           05  FILLER              PIC X(10) VALUE '4FIVE     '.                
           05  FILLER              PIC X(10) VALUE '3SIX      '.                
           05  FILLER              PIC X(10) VALUE '5SEVEN    '.                
           05  FILLER              PIC X(10) VALUE '5EIGHT    '.                
           05  FILLER              PIC X(10) VALUE '4NINE     '.                
           05  FILLER              PIC X(10) VALUE '3TEN      '.                
           05  FILLER              PIC X(10) VALUE '6ELEVEN   '.                
           05  FILLER              PIC X(10) VALUE '6TWELVE   '.                
           05  FILLER              PIC X(10) VALUE '8THIRTEEN '.                
           05  FILLER              PIC X(10) VALUE '8FOURTEEN '.                
           05  FILLER              PIC X(10) VALUE '7FIFTEEN  '.                
           05  FILLER              PIC X(10) VALUE '7SIXTEEN  '.                
           05  FILLER              PIC X(10) VALUE '9SEVENTEEN'.                
           05  FILLER              PIC X(10) VALUE '8EIGHTEEN '.                
           05  FILLER              PIC X(10) VALUE '8NINETEEN '.                
           05  FILLER              PIC X(10) VALUE '6TWENTY   '.                
           05  FILLER              PIC X(10) VALUE '6THIRTY   '.                
           05  FILLER              PIC X(10) VALUE '5FORTY    '.                
           05  FILLER              PIC X(10) VALUE '5FIFTY    '.                
           05  FILLER              PIC X(10) VALUE '5SIXTY    '.                
           05  FILLER              PIC X(10) VALUE '7SEVENTY  '.                
           05  FILLER              PIC X(10) VALUE '6EIGHTY   '.                
           05  FILLER              PIC X(10) VALUE '6NINETY   '.                
       01  WA-NAMER REDEFINES WA-NAMES.                                         
           05  WA-NAME             PIC X(10) OCCURS 27 TIMES.                   
                                                                                
       01  WS-LITERALS.                                                         
           05  L-CENT              PIC X(5)  VALUE '4CENT'.                     
           05  L-AND               PIC X(4)  VALUE '3AND'.                      
           05  L-X                 PIC X     VALUE '%'.                         
           05  L-MILLION           PIC X(8)  VALUE '7MILLION'.                  
           05  L-THOUSAND          PIC X(9)  VALUE '8THOUSAND'.                 
           05  L-DOLLARS           PIC X(8)  VALUE '7DOLLARS'.                  
           05  L-CENTS             PIC X(6)  VALUE '6CENTS'.                    
           05  L-ONLY              PIC X(6)  VALUE '5ONLY '.                    
           05  L-HUNDRED           PIC X(8)  VALUE '7HUNDRED'.                  
           05  L-DOLLAR            PIC X(7)  VALUE '6DOLLAR'.                   
                                                                                
       01  SWITCHES.                                                            
           05  WS-SWITCH           PIC 9     VALUE 0.                           
           05  WS-THO-SW           PIC 9     VALUE 0.                           
           05  WS-CNT-SW           PIC 9     VALUE 0.                           
           05  WS-LIT-SW           PIC 9     VALUE 0.                           
           05  WS-TEN-SW           PIC 9     VALUE 0.                           
           05  WS-AND-SW           PIC 9     VALUE 0.                           
                                                                                
       01  WS-PLACE-COUNTER        PIC 999   VALUE 0.                           
       01  WL-AREA-SAVE            PIC X(12) VALUE SPACES.                      
       01  WS-RESULT               PIC 999   VALUE 0.                           
       01  WL-AREA.                                                             
           05  WL-ITEM1            PIC 9     VALUE 0.                           
           05  FILLER              PIC X(11) VALUE SPACE.                       
       01  WL-ITM REDEFINES WL-AREA.                                            
           05  WL-ITEM             PIC X     OCCURS 12 TIMES.                   
                                                                                
       01  WS-CHEQUE-DETAILS.                                                   
           05  WS-AMT              PIC 9(7)V99 VALUE 0.                         
           05  WS-DIGIT REDEFINES  WS-AMT OCCURS 9 TIMES PIC 9.                 
           05  WF-DETAILS.                                                      
               10  WF-LINE-1       PIC X(60).                                   
               10  WF-LINE-2       PIC X(60).                                   
           05  WF-ITEM REDEFINES WF-DETAILS OCCURS 120 PIC X.                   
           05  WP-ITEM REDEFINES WF-DETAILS OCCURS 2 PIC X(60).                 
                                                                                
      *----------------------------------------------------------*              
      *      CONTROL FEILDS FOR HDB ASSESSMENT DATA FILE         *              
      *----------------------------------------------------------*              
       77  WS-LEASE                   PIC 9(13)V99 VALUE ZEROES.                
                                                                                
                                                                                
      *------------------*                                                      
       PROCEDURE DIVISION.                                                      
      *------------------*                                                      
                                                                                
      ******************************************************************        
       000-MAIN-ROUTINE.                                                        
      ******************************************************************        
                                                                                
           PERFORM  100-OPEN-FILES     THRU  100-EXIT.                          
           PERFORM  200-READ-BP13F315  THRU  200-EXIT.                          
           PERFORM  300-MAIN-ROUTINE   THRU  300-EXIT UNTIL WS-F315-EOF.        
           IF WS-SEQ-NO-F225 NOT = ZEROES                                       
              PERFORM  770-WRITE-TRAILER-F225 THRU 770-EXIT                     
           END-IF.                                                              
           PERFORM  900-CLOSE-FILES    THRU  900-EXIT.                          
                                                                                
       000-MAIN-EXIT.                                                           
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       100-OPEN-FILES.                                                          
      ******************************************************************        
           OPEN INPUT  BP13F315                                                 
                       BP13K893                                                 
                       BP13K410                                                 
                I-O    BP13K625                                                 
                OUTPUT AG07F225                                                 
                       BP13L31G                                                 
                       P13L31GB.                                                
                                                                                
           IF BP13K893-STATUS NOT = 00 AND NOT = 97                             
              DISPLAY 'OPENING ERROR, BP13K893-STATUS ' BP13K893-STATUS         
              MOVE     BP13K893-STATUS    TO   RETURN-CODE                      
              PERFORM  900-CLOSE-FILES    THRU 900-EXIT                         
           END-IF.                                                              
                                                                                
           IF BP13K410-STATUS NOT = 00 AND NOT = 97                             
              DISPLAY 'OPENING ERROR, BP13K410-STATUS ' BP13K410-STATUS         
              MOVE     BP13K410-STATUS    TO   RETURN-CODE                      
              PERFORM  900-CLOSE-FILES    THRU 900-EXIT                         
           END-IF.                                                              
                                                                                
           IF BP13K625-STATUS NOT = 00 AND NOT = 97                             
              DISPLAY 'OPENING ERROR, BP13K625-STATUS ' BP13K625-STATUS         
              MOVE     BP13K625-STATUS    TO   RETURN-CODE                      
              PERFORM  900-CLOSE-FILES    THRU 900-EXIT                         
           END-IF.                                                              
                                                                                
      * VOUCHER NUMBER                                                          
           MOVE       'AG07K960'          TO   C907-NUM-FILENME.                
                                                                                
           CALL 'K960OPEN' USING AG07C907-AREA.                                 
                                                                                
           IF C907-NUM-FILE-STATUS NOT = ('00' AND '97')                        
              DISPLAY 'ERROR OPENING AG07K960 = ' C907-NUM-FILE-STATUS          
              MOVE     C907-NUM-FILE-STATUS  TO WS-AG07-STAT-X                  
              MOVE     WS-AG07-STATUS        TO   RETURN-CODE                   
              PERFORM  900-CLOSE-FILES       THRU 900-EXIT                      
           END-IF.                                                              
                                                                                
      * BATCH NUMBER (14/07/1999-ROMAR)                                         
           MOVE       'BP13K620'          TO   C906-NUM-FILENME.                
                                                                                
           CALL 'K620OPEN' USING BP13C906-AREA.                                 
                                                                                
           IF C906-NUM-FILE-STATUS NOT = ('00' AND '97')                        
              DISPLAY 'ERROR OPENING BP13K620 = ' C906-NUM-FILE-STATUS          
              MOVE     C906-NUM-FILE-STATUS  TO   RETURN-CODE                   
              PERFORM  900-CLOSE-FILES       THRU 900-EXIT                      
           END-IF.                                                              
                                                                                
      **CALL AB05C902 TO OPEN B05K006 AND AB05K009 FILES(CODE='O')              
           MOVE SPACES              TO   WS-B902-AREA.                          
           INITIALIZE                    WS-B902-AREA.                          
           MOVE 'O'                 TO   B902-CDE-TYPE.                         
           CALL 'AB05C902'  USING WS-B902-AREA.                                 
                                                                                
           IF B902-CDE-ERROR NOT = ZEROES                                       
              MOVE B902-CDE-SYSERR  TO   RETURN-CODE                            
              DISPLAY 'ERROR CALLING AB05C902 SUB-ROUTINE!'                     
              PERFORM  900-CLOSE-FILES  THRU 900-EXIT                           
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       200-READ-BP13F315.                                                       
      ******************************************************************        
                                                                                
           READ BP13F315                                                        
                AT END MOVE 'Y' TO WS-F315-EOF-IND                              
                GO TO  200-EXIT.                                                
                                                                                
           ADD 1 TO WS-CNT-F315-READ.                                           
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-MAIN-ROUTINE.                                                        
      ******************************************************************        
      *------------------------------------------------------------*            
      *    1. READ FILE BP13F315.                                  *            
      *    2. RETRIEVE FLAT'S DETAIL FROM BP13F315.                *            
      *    3. READ K893 SOC MASTER TO RETRIEVE K893-AMT-LEASE-FEE. *            
      *    4. PRINT LIST OF PAYMENT FOR STAMPING.                  *            
      *------------------------------------------------------------*            
                                                                                
           PERFORM 400-READ-BP13K893  THRU  400-EXIT.                           
           MOVE    F315-SCH-ACC       TO    WS-SCH-ACC.                         
           MOVE    F315-NUM-BLK       TO    L31G-BLK-NO.                        
           MOVE    F315-NME-STREET    TO    L31G-STREET.                        
           MOVE    F315-NUM-MAIN      TO    L31G-UNIT-NO.                       
           MOVE    F315-NUM-LEVEL     TO    L31G-LEVEL-NO.                      
           MOVE    F315-REGN-NO       TO    L31G-REGN-NO.                       
                                                                                
           MOVE    WS-SCH-1-4         TO    L31G-SCH-1-4.                       
           MOVE    WS-SCH-5-8         TO    L31G-SCH-5-8.                       
           MOVE    WS-SCH-9-9         TO    L31G-SCH-9-9.                       
           MOVE    WS-SCH-10-11       TO    L31G-SCH-10-11.                     
                                                                                
      ******** 14/07/1999 - ROMAR                                               
                                                                                
           IF WS-NEW-BATCH-NO = 'Y'                                             
              MOVE  'SA  '      TO C906-NUM-BATCH-TYPE                          
              MOVE  'BP13K620'  TO C906-NUM-FILENME                             
                                                                                
              CALL  'BP13C906'  USING BP13C906-AREA                             
                                                                                
              IF C906-NUM-FILE-STATUS =  '00'                                   
                 MOVE C906-NUM-BATCH-TYPE      TO  WS-NUM-BATCH-TYPE            
                 MOVE C906-NUM-FIN-YEAR        TO  WS-NUM-FIN-YEAR              
                 MOVE C906-NUM-BATCH-SERIAL    TO  WS-NUM-BATCH-SERIAL          
                 MOVE       'N'                TO  WS-NEW-BATCH-NO              
              ELSE                                                              
                 DISPLAY 'ERROR CALLING BP13C906, '                             
                         'FILE STATUS = '    C906-NUM-FILE-STATUS               
                 MOVE     C906-NUM-FILE-STATUS TO RETURN-CODE                   
                 PERFORM  900-CLOSE-FILES                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-NEW-VOUCHER-F225 = 'Y'                                         
      *          MOVE  'SU '      TO C907-NUM-DDD                               
                 MOVE  'SS '      TO C907-NUM-DDD                               
                 MOVE  'AG07K960' TO C907-NUM-FILENME                           
                                                                                
                 CALL 'AG07C907'  USING AG07C907-AREA                           
                                                                                
                 IF C907-NUM-FILE-STATUS =  '00'                                
                    PERFORM  750-WRITE-HEADER-F225 THRU 750-EXIT                
                    MOVE 'N'           TO   WS-NEW-VOUCHER-F225                 
                 ELSE                                                           
                    DISPLAY 'ERROR CALLING AG07C907, '                          
                            'FILE STATUS = '    C907-NUM-FILE-STATUS            
                    MOVE     C907-NUM-FILE-STATUS TO WS-AG07-STAT-X             
                    MOVE     WS-AG07-STATUS       TO   RETURN-CODE              
                    PERFORM  900-CLOSE-FILES                                    
                 END-IF                                                         
           END-IF.                                                              
                                                                                
           PERFORM  450-EARMARK-ACCOUNT      THRU 450-EXIT.                     
           IF B902-CDE-SYSERR NOT = ZEROES                                      
              DISPLAY 'ERROR CALLING AB05C902, STATUS = '                       
                       B902-CDE-SYSERR                                          
              MOVE     '99'                 TO RETURN-CODE                      
              PERFORM  900-CLOSE-FILES                                          
           END-IF.                                                              
                                                                                
           IF B902-CDE-ERROR-ACCT(1) = SPACES                                   
                                                                                
              PERFORM 650-WRITE-STAMPING-FILE  THRU  650-EXIT                   
              PERFORM 760-WRITE-F225-DETAIL THRU 760-EXIT                       
                                                                                
              IF WS-SEQ-NO-F225 = 25                                            
                PERFORM 770-WRITE-TRAILER-F225 THRU 770-EXIT                    
                MOVE   'Y'                  TO    WS-NEW-VOUCHER-F225           
                MOVE    ZEROES              TO    WS-SEQ-NO-F225                
                MOVE    ZEROES              TO    WS-NET-AMT-F225               
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM  200-READ-BP13F315        THRU  200-EXIT.                    
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       400-READ-BP13K893.                                                       
      ******************************************************************        
      *------------------------------------------------------------*            
      *    READ APPLICATION MASTER TO GET ACTUAL LEASE STAMP FEE   *            
      *------------------------------------------------------------*            
           MOVE SPACES                           TO BP13K893-MASTER.            
           INITIALIZE                               BP13K893-MASTER.            
                                                                                
           MOVE    F315-REGN-NO       TO    K893-NUM-REGN-HIST.                 
                                                                                
           MOVE 'N'                              TO WS-K893-EOF                 
                                                    WS-K893-FND                 
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           EVALUATE BP13K893-STATUS                                             
              WHEN 00                                                           
                 PERFORM 425-READNEXT-BP13K893 THRU 425-EXIT                    
                   UNTIL K893-NUM-REGN-HIST NOT = F315-REGN-NO                  
                      OR WS-K893-EOF = 'Y'                                      
              WHEN 10                                                           
              WHEN 23                                                           
                 DISPLAY 'K893 RECORD NOT FOUND. REGN NO = '                    
                          F315-REGN-NO                                          
                 MOVE ZEROES    TO  L31G-LEASE                                  
                                    L31G-DUP-LEASE                              
                 MOVE     BP13K893-STATUS    TO     RETURN-CODE                 
                 PERFORM  900-CLOSE-FILES    THRU   900-EXIT                    
              WHEN OTHER                                                        
                 DISPLAY 'ERROR START BP13K893    ' BP13K893-STATUS             
                 MOVE     BP13K893-STATUS    TO     RETURN-CODE                 
                 PERFORM  900-CLOSE-FILES    THRU   900-EXIT                    
           END-EVALUATE.                                                        
                                                                                
                                                                                
           IF K893-FND                                                          
      * TESTING ONLY                                                            
      *       MOVE 'N' TO K893-NUM-RELEASE-PAYMENT                              
              IF   K893-NUM-RELEASE-PAYMENT NOT = 'Y'                           
                   PERFORM 660-READ-BP13K410  THRU  660-EXIT                    
              ADD  1                     TO   WS-CNT-K893-READ                  
              MOVE K893-DTE-RAISE-AO     TO   WS-AO-DATE                        
              MOVE WS-AO-DD              TO   WS-DD                             
              MOVE WS-AO-MM              TO   WS-MM                             
              MOVE WS-AO-YEAR            TO   WS-YEAR                           
              MOVE WS-DATE               TO   L31G-AGR-ORDER-DATE               
              MOVE 0.00                  TO   WS-DEF-DUP-LEASE                  
                                              WS-DUP-LEASE                      
                                                                                
              IF F315-AMT-LEASE-FEE NOT NUMERIC                                 
                 MOVE ZEROES TO F315-AMT-LEASE-FEE                              
              END-IF                                                            
                                                                                
              IF F315-AMT-LEASE-FEE-DUP NOT NUMERIC                             
                 MOVE ZEROES TO F315-AMT-LEASE-FEE-DUP                          
              END-IF                                                            
                                                                                
              IF F315-NUM-ADHOC-SF = SPACES                                     
                 MOVE ZEROES            TO   WS-DEF-DUP-LEASE                   
                 COMPUTE WS-COMP-TOT-LEASE = K893-AMT-LEASE-FEE                 
                 MOVE WS-COMP-TOT-LEASE     TO   L31G-LEASE                     
                 ADD  WS-COMP-TOT-LEASE     TO   WS-TOTAL-ACTUAL-LEASE          
                 MOVE WS-DEF-DUP-LEASE      TO   L31G-DUP-LEASE                 
                 ADD  WS-DEF-DUP-LEASE      TO   WS-TOTAL-DUP-LEASE             
                 MOVE K893-AMT-LEASE-FEE    TO   L31G-DETL-TOTAL                
                 ADD  K893-AMT-LEASE-FEE    TO   WS-TOTAL-LEASE                 
              ELSE                                                              
                 COMPUTE WS-COMP-TOT-LEASE = F315-AMT-LEASE-FEE                 
                 MOVE WS-COMP-TOT-LEASE      TO   L31G-LEASE                    
                 ADD  WS-COMP-TOT-LEASE      TO   WS-TOTAL-ACTUAL-LEASE         
                 COMPUTE WS-DUP-LEASE  = F315-AMT-LEASE-FEE-DUP                 
                 MOVE WS-DUP-LEASE           TO   L31G-DUP-LEASE                
                 ADD  WS-DUP-LEASE           TO   WS-TOTAL-DUP-LEASE            
                 COMPUTE L31G-DETL-TOTAL = F315-AMT-LEASE-FEE +                 
                                           F315-AMT-LEASE-FEE-DUP               
                 ADD  F315-AMT-LEASE-FEE     TO   WS-TOTAL-LEASE                
                 ADD  F315-AMT-LEASE-FEE-DUP TO   WS-TOTAL-LEASE                
              END-IF                                                            
                                                                                
              ELSE                                                              
                 DISPLAY 'CASE ALREADY RELEASED, REGN NO = '                    
                          K893-NUM-REGN                                         
                 MOVE     '99'               TO     RETURN-CODE                 
                 PERFORM  900-CLOSE-FILES    THRU   900-EXIT                    
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       425-READNEXT-BP13K893.                                                   
      *-------------------------------------------------------------*           
                                                                                
           READ BP13K893 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                         TO WS-K893-EOF                 
           END-READ.                                                            
                                                                                
           EVALUATE BP13K893-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                 IF K893-NUM-REGN-HIST = F315-REGN-NO                           
                    MOVE 'Y'            TO  WS-K893-FND                         
                    MOVE 'Y'            TO  WS-K893-EOF                         
                    ADD  1              TO  WS-CNT-K893-READ                    
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
                 DISPLAY 'K893 RECORD NOT FOUND. REGN NO = '                    
                          F315-REGN-NO                                          
                 MOVE ZEROES    TO  L31G-LEASE                                  
                                    L31G-DUP-LEASE                              
                 MOVE     BP13K893-STATUS    TO     RETURN-CODE                 
                 PERFORM  900-CLOSE-FILES    THRU   900-EXIT                    
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READ NEXT BP13K893' BP13K893-STATUS             
                 MOVE     BP13K893-STATUS    TO     RETURN-CODE                 
                 PERFORM  900-CLOSE-FILES    THRU   900-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       425-EXIT.                                                                
            EXIT.                                                               
                                                                                
      ******************************************************************        
       450-EARMARK-ACCOUNT.                                                     
      ******************************************************************        
      **CALL AB05C902 TO EARMARK ACCOUNT(CODE='E')                              
           MOVE SPACES              TO   WS-B902-AREA.                          
           INITIALIZE                    WS-B902-AREA.                          
                                                                                
           STRING C907-NUM-DDD  C907-NUM-FIN-YR                                 
                  C907-NUM-VR-SERIAL                                            
                  DELIMITED BY SIZE INTO B902-VR-NO                             
           END-STRING.                                                          
                                                                                
           MOVE 'E'                     TO   B902-CDE-TYPE.                     
           MOVE 'BP13'                  TO   B902-CDE-SYSTEMID.                 
           MOVE FUNCTION CURRENT-DATE   TO   B902-DTE-TRANS.                    
           MOVE 'P13C31G'               TO   B902-NUM-USERID-UPDATE.            
           MOVE 'CV'                    TO   B902-CDE-EARMARK-REASON.           
           MOVE 01                      TO   B902-NUM-HDB-REF-CNT.              
           MOVE F315-SCH-ACC            TO   B902-NUM-HDB-REF(1).               
           MOVE ZEROES                  TO   WS-AMT-EARMARK.                    
                                                                                
           IF K893-AMT-LEASE-FEE NUMERIC                                        
              IF F315-NUM-ADHOC-SF = SPACES                                     
                 MOVE K893-AMT-LEASE-FEE TO  WS-AMT-EARMARK                     
              ELSE                                                              
                 COMPUTE WS-AMT-EARMARK = F315-AMT-LEASE-FEE +                  
                                          F315-AMT-LEASE-FEE-DUP                
              END-IF                                                            
           ELSE                                                                 
              MOVE ZEROES             TO  WS-AMT-EARMARK                        
           END-IF.                                                              
                                                                                
           MOVE WS-AMT-EARMARK          TO   B902-AMT-EARMARK(1).               
                                                                                
           DISPLAY '----------------'.                                          
           DISPLAY 'B902-VR-NO       = ' B902-VR-NO.                            
           DISPLAY 'B902-CDE-TYPE    = ' B902-CDE-TYPE.                         
           DISPLAY 'CDE-SYSTEMID     = ' B902-CDE-SYSTEMID.                     
           DISPLAY 'B902-DTE-TRANS   = ' B902-DTE-TRANS.                        
           DISPLAY 'NUM-USERID       = ' B902-NUM-USERID-UPDATE.                
           DISPLAY 'HDB-REF-CNT      = ' B902-NUM-HDB-REF-CNT.                  
           DISPLAY 'NUM-HDB-REF      = ' B902-NUM-HDB-REF(1).                   
           DISPLAY 'B902-AMT-EARMARK = ' B902-AMT-EARMARK(1).                   
                                                                                
           CALL 'AB05C902'  USING WS-B902-AREA.                                 
                                                                                
           DISPLAY 'CDE-ERROR-ACCT   = ' B902-CDE-ERROR-ACCT(1).                
                                                                                
           EVALUATE B902-CDE-ERROR-ACCT(1)                                      
              WHEN SPACES                                                       
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 COMPUTE WS-TOTAL-LEASE = WS-TOTAL-LEASE -                      
                                          WS-AMT-EARMARK                        
                 COMPUTE WS-TOTAL-ACTUAL-LEASE =                                
                         WS-TOTAL-ACTUAL-LEASE - WS-AMT-EARMARK                 
                 PERFORM 850-PRINT-L31GB-REPORT     THRU 850-EXIT               
           END-EVALUATE.                                                        
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       520-PRINT-HEADING.                                                       
      ******************************************************************        
      *------------------------------------------------------------*            
      *    PRINT HEADING LINE                                      *            
      *------------------------------------------------------------*            
           MOVE   SPACES            TO    L31G-PRINT-REC.                       
           ADD    1                 TO    WS-NO-OF-PAGE.                        
           MOVE   WS-NO-OF-PAGE     TO    L31G-PAGE.                            
           MOVE   FUNCTION CURRENT-DATE(1:8)                                    
                                    TO    WS-CURRENT-CCYYMMDD.                  
           MOVE   WS-CUR-DD         TO    WS-PC-DD.                             
           MOVE   WS-CUR-MM         TO    WS-PC-MM.                             
           MOVE   WS-CUR-CCYY       TO    WS-PC-CCYY.                           
           MOVE   WS-PR-CUR-DATE    TO    L31G-CURRENT-DATE.                    
                                                                                
           STRING WS-NUM-BATCH-TYPE   DELIMITED BY SPACE                        
                  WS-NUM-FIN-YEAR     DELIMITED BY SPACE                        
                  '-'                 DELIMITED BY SIZE                         
                  WS-NUM-BATCH-SERIAL DELIMITED BY SPACE                        
                  INTO L31G-BATCH-NO                                            
           END-STRING.                                                          
                                                                                
           MOVE   WS-NUM-VOUCHER-F225    TO    L31G-VOUCHER-NO.                 
                                                                                
           WRITE  L31G-PRINT-REC  FROM L31G-PR-HDG-01 AFTER PAGE.               
                                                                                
           MOVE "FINANCE'S COPY" TO L31G-COPY-NAME                              
           WRITE  L31G-PRINT-REC  FROM L31G-PR-HDG-02 AFTER 2.                  
           WRITE  L31G-PRINT-REC  FROM L31G-PR-HDG-03 AFTER 2.                  
           WRITE  L31G-PRINT-REC  FROM L31G-PR-HDG-04 AFTER 1.                  
           WRITE  L31G-PRINT-REC  FROM L31G-PR-HDG-05 AFTER 1.                  
           WRITE  L31G-PRINT-REC  FROM L31G-PR-HDG-05 AFTER 1.                  
           WRITE  L31G-PRINT-REC  FROM L31G-PR-HDG-06 AFTER 1.                  
           WRITE  L31G-PRINT-REC  FROM L31G-PR-HDG-07 AFTER 1.                  
           WRITE  L31G-PRINT-REC  FROM L31G-PR-HDG-08 AFTER 1.                  
           WRITE  L31G-PRINT-REC  FROM L31G-PR-HDG-09 AFTER 1.                  
           WRITE  L31G-PRINT-REC  FROM L31G-PR-HDG-09A AFTER 1.                 
           WRITE  L31G-PRINT-REC  FROM L31G-PR-HDG-10 AFTER 1.                  
           WRITE  L31G-PRINT-REC  FROM L31G-PR-HDG-11 AFTER 2.                  
           WRITE  L31G-PRINT-REC  FROM L31G-PR-HDG-12 AFTER 1.                  
                                                                                
           MOVE   SPACES TO  L31G-PRINT-REC.                                    
                                                                                
           WRITE L31G-PRINT-REC  AFTER 1.                                       
                                                                                
       520-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       620-PRINT-DETAIL.                                                        
      ******************************************************************        
      *------------------------------------------------------------*            
      *    PRINT LIST OF PAYMENT FOR STAMPING                      *            
      *------------------------------------------------------------*            
                                                                                
           ADD   1                   TO    WS-CNT-L31G-WRITE.                   
           MOVE  WS-CNT-L31G-DETLS   TO    L31G-NO.                             
           WRITE L31G-PRINT-REC      FROM  L31G-PR-DETAIL AFTER 1.              
                                                                                
           MOVE  SPACES              TO    L31G-PRINT-REC.                      
                                                                                
       620-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      ******************************************************************        
       650-WRITE-STAMPING-FILE.                                                 
      ******************************************************************        
            DISPLAY '650-WRITE-STAMPING-FILE' F315-SCH-ACC.                     
            INITIALIZE BP13K625-REC.                                            
            MOVE SPACES                 TO BP13K625-REC.                        
                                                                                
            MOVE F315-SCH-ACC           TO WS-NUM-HDB-REF.                      
            MOVE 'ALE'                  TO WS-NUM-DOC.                          
                                                                                
      ****  WRITE DETAIL PART FOR ORIGINAL AGREEMENT LEASE*******               
            MOVE 'O'                    TO WS-NUM-DOC-TYP.                      
                                                                                
            DISPLAY 'DOC-TYPE = ' WS-NUM-DOC-TYP.                               
            PERFORM 670-CHK-EXSTNG-BP13K625  THRU 670-EXIT.                     
            PERFORM 690-MAIN-DETAILS-TO-K625 THRU 690-EXIT.                     
                                                                                
            IF F315-NUM-ADHOC-SF = SPACES                                       
               COMPUTE WS-LEASE          = K893-AMT-LEASE-FEE -                 
                                           WS-DEF-DUP-LEASE                     
            ELSE                                                                
               COMPUTE WS-LEASE          = F315-AMT-LEASE-FEE                   
            END-IF.                                                             
                                                                                
            MOVE WS-LEASE               TO K625-AMT-STAMP-DUTY.                 
            MOVE ZEROES                 TO K625-AMT-STAMP-DUTY-DUP.             
            DISPLAY 'DOC-TYPE = ' K625-NUM-DOC-TYP.                             
            DISPLAY 'AMT      = ' K625-AMT-STAMP-DUTY.                          
            ADD 1                       TO WS-CNT-K625-WRITE.                   
            WRITE BP13K625-REC.                                                 
                                                                                
      ****  WRITE DETAIL PART FOR DUPLICATE AGREEMENT LEASE*******              
            MOVE 'D'                    TO WS-NUM-DOC-TYP.                      
                                                                                
            DISPLAY 'DOC-TYPE = ' WS-NUM-DOC-TYP.                               
            PERFORM 670-CHK-EXSTNG-BP13K625  THRU 670-EXIT.                     
            PERFORM 690-MAIN-DETAILS-TO-K625 THRU 690-EXIT.                     
            IF F315-NUM-ADHOC-SF = SPACES                                       
               MOVE WS-DEF-DUP-LEASE  TO K625-AMT-STAMP-DUTY-DUP                
            ELSE                                                                
               COMPUTE K625-AMT-STAMP-DUTY-DUP = F315-AMT-LEASE-FEE-DUP         
            END-IF.                                                             
                                                                                
            MOVE ZEROES                 TO K625-AMT-STAMP-DUTY.                 
                                                                                
            DISPLAY 'DOC-TYPE = ' K625-NUM-DOC-TYP.                             
            DISPLAY 'AMT      = ' K625-AMT-STAMP-DUTY.                          
            ADD 1                       TO WS-CNT-K625-WRITE.                   
            WRITE BP13K625-REC.                                                 
                                                                                
       650-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       660-READ-BP13K410.                                                       
      ******************************************************************        
      *------------------------------------------------------------*            
      *    READ ACCUM SO MASTER TO GET ANNUAL RENT, PURCHASE PRICE,*            
      *    AMT LOAN.                                               *            
      *------------------------------------------------------------*            
           MOVE K893-NUM-REGN          TO K410-NUM-REGN.                        
           READ BP13K410.                                                       
                                                                                
           IF BP13K410-STATUS = 00                                              
              ADD  1           TO  WS-CNT-K410-READ                             
              MOVE ZEROES TO WS-ANNUAL-RENT                                     
           ELSE                                                                 
              IF BP13K410-STATUS = 23                                           
                 DISPLAY 'K410 RECORD NOT FOUND. REGN NO = '                    
                          K410-NUM-REGN                                         
                 MOVE     BP13K410-STATUS    TO     RETURN-CODE                 
                 PERFORM  900-CLOSE-FILES    THRU   900-EXIT                    
              ELSE                                                              
                 DISPLAY 'ERROR READING, BP13K410 ' BP13K410-STATUS             
                 MOVE     BP13K410-STATUS    TO     RETURN-CODE                 
                 PERFORM  900-CLOSE-FILES    THRU   900-EXIT.                   
                                                                                
       660-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       670-CHK-EXSTNG-BP13K625.                                                 
      ******************************************************************        
           MOVE WS-NUM-HDB-REF          TO K625-NUM-HDB-REF.                    
           MOVE WS-NUM-DOC              TO K625-NUM-DOC.                        
           MOVE WS-NUM-DOC-TYP          TO K625-NUM-DOC-TYP.                    
           MOVE 1                       TO K625-NUM-DOC-CNT.                    
           MOVE 'N'                     TO WS-K625-EOF.                         
                                                                                
           START BP13K625 KEY = K625-KEY-FLD.                                   
                                                                                
           EVALUATE BP13K625-STATUS                                             
              WHEN '00'                                                         
                    PERFORM 680-READNEXT-BP13K625 THRU 680-EXIT                 
                            UNTIL WS-K625-EOF = 'Y'                             
                    ADD  1              TO WS-NUM-DOC-CNT                       
              WHEN '10'                                                         
              WHEN '23'                                                         
                    MOVE 1              TO WS-NUM-DOC-CNT                       
              WHEN OTHER                                                        
                    DISPLAY 'ERROR WHILE STARTING BROWSE-BP13K625 '             
                    MOVE BP13K625-STATUS TO RETURN-CODE                         
                    PERFORM 900-CLOSE-FILES THRU 900-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       670-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       680-READNEXT-BP13K625.                                                   
      ******************************************************************        
           READ BP13K625 NEXT AT END MOVE 'Y' TO WS-K625-EOF                    
              GO TO 680-EXIT.                                                   
                                                                                
           IF (K625-NUM-HDB-REF NOT = WS-NUM-HDB-REF) OR                        
              (K625-NUM-DOC     NOT = WS-NUM-DOC)     OR                        
              (K625-NUM-DOC-TYP NOT = WS-NUM-DOC-TYP)                           
              MOVE 'Y'                   TO WS-K625-EOF                         
              GO TO 680-EXIT                                                    
           END-IF.                                                              
                                                                                
           MOVE K625-NUM-DOC-CNT         TO WS-NUM-DOC-CNT.                     
                                                                                
       680-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       690-MAIN-DETAILS-TO-K625.                                                
      ******************************************************************        
            MOVE  SPACES                TO BP13K625-REC.                        
            INITIALIZE                     BP13K625-REC.                        
                                                                                
            MOVE  F315-SCH-ACC          TO K625-NUM-HDB-REF.                    
            MOVE  'ALE'                 TO K625-NUM-DOC                         
            MOVE  WS-NUM-DOC-TYP        TO K625-NUM-DOC-TYP.                    
            MOVE  WS-NUM-DOC-CNT        TO K625-NUM-DOC-CNT.                    
            MOVE  C907-NUM-DDD          TO K625-NUM-DDD.                        
            MOVE  C907-NUM-FIN-YR       TO K625-NUM-FIN-YEAR.                   
            MOVE  C907-NUM-VR-SERIAL    TO K625-NUM-VR-SERIAL.                  
            MOVE  C906-NUM-BATCH-TYPE   TO K625-NUM-BATCH-TYPE.                 
            MOVE  C906-NUM-FIN-YEAR     TO K625-NUM-CAL-YEAR.                   
            MOVE  C906-NUM-BATCH-SERIAL TO K625-NUM-BATCH-SERIAL.               
            MOVE  K893-NUM-REGN         TO K625-NUM-REGN.                       
            MOVE  K893-DTE-RAISE-AO     TO K625-DTE-DOC-PREPARE.                
            MOVE  K410-AMT-SELLING-PRICE TO K625-AMT-PURCHASE-PRICE.            
            MOVE  WS-ANNUAL-RENT        TO K625-AMT-ANNUAL-RENT.                
            MOVE  SPACES                TO K625-NUM-INCREASE-SCCC.              
            MOVE  ZEROES                TO K625-AMT-LOAN.                       
            MOVE  'BP13C31G'            TO K625-NUM-SYSTEM.                     
            MOVE  WS-CURRENT-CCYYMMDD   TO K625-DTE-UPDATE.                     
                                                                                
            IF K893-NUM-FLAT-TYPE = SPACES OR LOW-VALUES                        
               MOVE  ZEROES             TO K625-NUM-LEASE-TERM                  
            ELSE                                                                
               IF K893-NUM-FLAT-TYPE = '1A' OR '2A'                             
                  COMPUTE K625-NUM-LEASE-TERM = 30 * 12                         
               ELSE                                                             
                  COMPUTE K625-NUM-LEASE-TERM = 99 * 12                         
               END-IF                                                           
            END-IF.                                                             
                                                                                
       690-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       720-PRINT-TOTAL-LINE.                                                    
      ******************************************************************        
      *------------------------------------------------------------*            
      *    PRINT TOTAL LEASE LINE                                  *            
      *------------------------------------------------------------*            
           IF WS-CNT-F315-READ = 0                                              
              GO TO 720-EXIT                                                    
           END-IF.                                                              
                                                                                
           WRITE L31G-PRINT-REC        FROM  L31G-PR-DASH-LINE AFTER 3.         
                                                                                
           MOVE  SPACES                TO    L31G-PRINT-REC.                    
           MOVE  WS-CNT-L31G-DETLS     TO    L31G-VR-NO-HIGH.                   
           MOVE  WS-TOTAL-ACTUAL-LEASE TO    L31G-TOTAL-ACTUAL.                 
           MOVE  WS-TOTAL-DUP-LEASE    TO    L31G-TOTAL-DUP.                    
           MOVE  WS-TOTAL-LEASE        TO    L31G-TOTAL-LEASE.                  
                                                                                
           WRITE L31G-PRINT-REC       FROM   L31G-PR-TOTAL-LINE AFTER 1.        
                                                                                
           MOVE  SPACES                TO    L31G-PRINT-REC.                    
           WRITE L31G-PRINT-REC       FROM   L31G-PR-DASH-LINE AFTER 1.         
                                                                                
       720-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       750-WRITE-HEADER-F225.                                                   
      ******************************************************************        
           INITIALIZE    AG07F225-HEADER.                                       
           MOVE SPACES                    TO AG07F225-HEADER.                   
           MOVE C907-NUM-DDD              TO F225-NUM-DDD                       
           MOVE C907-NUM-FIN-YR           TO F225-NUM-FIN-YEAR                  
           MOVE C907-NUM-VR-SERIAL        TO F225-NUM-VR-SERIAL                 
           MOVE F225-NUM-VR               TO WS-NUM-VOUCHER-F225                
           MOVE 00                        TO F225-NUM-REC-TYP.                  
           MOVE 'PV'                      TO F225-NUM-VR-TYP.                   
           MOVE 'TL'                      TO F225-NUM-VR-SUB-TYP.               
           MOVE 'SOC     '                TO F225-NUM-TRANS-TYP.                
           MOVE SPACES                    TO F225-NUM-ROOT-ACCT.                
           MOVE  FUNCTION CURRENT-DATE(1:8) TO                                  
                                             WS-CURRENT-CCYYMMDD.               
           MOVE WS-CURRENT-CCYYMMDD       TO F225-DTE-VR-CREATE                 
                                             F225-DTE-VR-PRT-FINAL              
           MOVE 'BP13C31G'                TO F225-NUM-VR-CREATE-USERID          
                                          F225-NUM-VR-PRT-FINAL-USERID          
           MOVE 'DBSSSGSGXXX'             TO F225-NUM-PAYEE-BANK                
           MOVE SPACES                    TO F225-NUM-PAYEE-BANK-BRNCH          
           MOVE '0010468600'              TO F225-NUM-PAYEE-IBG-ACCT.           
           MOVE 'STPFEE'                  TO F225-NUM-DDD-FILE-REF.             
           MOVE 'COMMISSIONER OF STAMP DUTIES'                                  
                                          TO F225-NME-PAYEE1.                   
           MOVE SPACES                    TO F225-NUM-PAYEE-UIN1.               
           MOVE SPACES                    TO F225-NME-PAYEE2.                   
           MOVE SPACES                    TO F225-NUM-PAYEE-UIN2.               
           MOVE SPACES                    TO F225-NUM-PAYEE-TAX-REGN.           
           MOVE   '55'                            TO  F225-NUM-BLK-HSE.         
           MOVE   'NEWTON ROAD'                   TO  F225-NME-STREET.          
           MOVE   'REVENUE HOUSE'                 TO  F225-NME-BLDNG.           
           MOVE   '307987'                        TO  F225-NUM-POSTAL.          
      *    MOVE 'C/O SC(STAMPING) OF SALES UNIT'                                
      *                                   TO F225-NME-STREET                    
           MOVE FUNCTION CURRENT-DATE(1:8) TO                                   
                                             WS-CURRENT-CCYYMMDD.               
           MOVE 'SALES AGREEMENT TEAM'    TO F225-NME-COLL-OFFCR.               
           MOVE '64903783'                TO F225-NUM-COLL-OFFCR-PHONE.         
           MOVE 'SALES AGREEMENT TEAM'    TO F225-NME-CONTACT-OFFCR.            
           MOVE '64903783'                TO F225-NUM-CONTACT-PHONE.            
           MOVE SPACES                    TO F225-TXT-REJECT-HDR.               
           MOVE 'STPFEE  '                TO F225-NUM-INTRFC-FR-HDR.            
           MOVE 'CV'                      TO F225-NUM-ACTION-TYP.               
           MOVE SPACES                    TO F225-NUM-PRCS-TERM.                
           MOVE SPACES                    TO F225-NUM-ROUTE-FLAG.               
           MOVE 'Y'                   TO F225-CDE-EARMARK-STAMP-FEE.            
           IF F315-USERID  = 'BP13P001'                                         
              MOVE WS-STAFF-START         TO F225-NUM-STAFF-ID-PREP             
                                             F225-NUM-USERID-START              
           ELSE                                                                 
              MOVE F315-USERID            TO F225-NUM-STAFF-ID-PREP             
                                             F225-NUM-USERID-START              
           END-IF.                                                              
           MOVE 'PREP'                    TO F225-NUM-VCHR-STATE-START.         
           MOVE SPACES                    TO F225-DTE-PYMNT-DUE.                
                                                                                
           WRITE AG07F225-HEADER.                                               
           ADD      1                     TO WS-CNT-F225-WRITE.                 
           PERFORM  520-PRINT-HEADING   THRU   520-EXIT.                        
                                                                                
       750-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       760-WRITE-F225-DETAIL.                                                   
      ******************************************************************        
           INITIALIZE  AG07F225-TRANS.                                          
           MOVE        SPACES               TO AG07F225-TRANS.                  
           ADD         1                    TO WS-SEQ-NO-F225.                  
           ADD         1                    TO WS-CNT-L31G-DETLS.               
           MOVE        WS-NUM-VOUCHER-F225  TO F225-NUM-VR-T.                   
           MOVE        WS-SEQ-NO-F225       TO F225-NUM-TRANS-SERIAL.           
           MOVE        '04'                 TO F225-NUM-SYS.                    
           MOVE        F315-SCH-ACC         TO F225-NUM-HDB-REF                 
           MOVE '0000000104250'             TO F225-NUM-GL-ACCT.                
           MOVE SPACES                      TO F225-NUM-TAX-CDE.                
                                                                                
           IF F315-NUM-ADHOC-SF = SPACES                                        
                 MOVE K893-AMT-LEASE-FEE    TO F225-AMT-TRANS                   
              ELSE                                                              
                COMPUTE     F225-AMT-TRANS = F315-AMT-LEASE-FEE +               
                                           F315-AMT-LEASE-FEE-DUP               
           END-IF                                                               
           IF F225-AMT-TRANS NOT NUMERIC                                        
              DISPLAY 'F225-AMT-TRANS NOT NUMERIC:' F225-AMT-TRANS              
              MOVE ZEROES TO F225-AMT-TRANS                                     
           END-IF.                                                              
                                                                                
           COMPUTE WS-NET-AMT-F225 = F225-AMT-TRANS + WS-NET-AMT-F225.          
           MOVE        'D'                 TO  F225-NUM-TRANS-DR-CR.            
           MOVE        ZEROS               TO  F225-AMT-TRANS-GST.              
           MOVE        F315-REGN-NO        TO  F225-TXT-INFO.                   
           MOVE        SPACES              TO  F225-NUM-CPF.                    
           MOVE        SPACES              TO  F225-TXT-REJECT-DTL.             
           MOVE        'STPFEE  '          TO  F225-NUM-INTRFC-FR-T.            
           WRITE       AG07F225-TRANS.                                          
                                                                                
           ADD         1                   TO WS-CNT-F225-WRITE.                
           PERFORM 620-PRINT-DETAIL   THRU  620-EXIT.                           
                                                                                
       760-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       770-WRITE-TRAILER-F225.                                                  
      ******************************************************************        
                                                                                
           IF WS-SEQ-NO-F225  = ZEROES                                          
              GO TO 770-EXIT                                                    
           END-IF.                                                              
                                                                                
           INITIALIZE AG07F225-TRAILER.                                         
           MOVE       SPACES              TO  AG07F225-TRAILER.                 
                                                                                
           MOVE  WS-NUM-VOUCHER-F225      TO  F225-NUM-VR-R.                    
           MOVE       99                  TO  F225-NUM-TRAILER-SERIAL.          
           MOVE 'PAYMENT FOR LEASE/MORTGAGE STAMP FEES'                         
                                    TO  F225-TXT-VOUCHER-NARRATION              
           MOVE       WS-SEQ-NO-F225      TO  F225-QTY-VR-TRANS.                
           MOVE       WS-NET-AMT-F225     TO  F225-AMT-VR-DR-TOTAL.             
           MOVE       ZEROES              TO  F225-AMT-VR-CR-TOTAL.             
           MOVE       SPACES              TO  F225-TXT-REJECT-TLR.              
           MOVE       'STPFEE  '          TO  F225-NUM-INTRFC-FR.               
                                                                                
           WRITE      AG07F225-TRAILER.                                         
                                                                                
           ADD        1                   TO       WS-CNT-F225-WRITE.           
           MOVE      'Y'                  TO       WS-NEW-VOUCHER-F225.         
           PERFORM  720-PRINT-TOTAL-LINE  THRU   720-EXIT.                      
           PERFORM  800-PRINT-FOOTING     THRU   800-EXIT.                      
           MOVE     0   TO WS-CNT-L31G-DETLS                                    
                           WS-TOTAL-ACTUAL-LEASE                                
                           WS-TOTAL-DUP-LEASE                                   
                           WS-TOTAL-LEASE.                                      
           MOVE    'Y'  TO WS-NEW-VOUCHER-F225                                  
                           WS-NEW-BATCH-NO                                      
                           WS-NEW-HEADER.                                       
                                                                                
       770-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       800-PRINT-FOOTING.                                                       
      ******************************************************************        
           MOVE    WS-TOTAL-LEASE       TO    WS-AMT.                           
           PERFORM 9000-CONVT-AMT-WORD  THRU  9000-EXIT.                        
           MOVE    WP-ITEM (1)          TO    L31G-AMT-DESC1.                   
           MOVE    WP-ITEM (2)          TO    L31G-AMT-DESC2.                   
                                                                                
           WRITE  L31G-PRINT-REC        FROM  L31G-PR-FTG-01   AFTER 2.         
           WRITE  L31G-PRINT-REC        FROM  L31G-PR-FTG-02   AFTER 5.         
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       850-PRINT-L31GB-REPORT.                                                  
      ******************************************************************        
           IF WS-L31GB-REC-ON-PAGE-CNT > 65                                     
              PERFORM 851-PRINT-L416-HEADER     THRU 851-EXIT                   
           END-IF.                                                              
                                                                                
           MOVE SPACES                TO L31GB-REC-DETAIL.                      
           INITIALIZE                    L31GB-REC-DETAIL.                      
           ADD  1                     TO WS-L31GB-PRINT-CNT.                    
           MOVE WS-L31GB-PRINT-CNT    TO L31GB-SERIAL-NO.                       
                                                                                
           STRING F315-SCH-ACC(1:4) '-'                                         
                  F315-SCH-ACC(5:4) '-'                                         
                  F315-SCH-ACC(9:1) '-'                                         
                  F315-SCH-ACC(10:2)                                            
                  DELIMITED BY SIZE INTO L31GB-SCH-ACC                          
           END-STRING.                                                          
                                                                                
           MOVE F315-REGN-NO      TO L31GB-REGN-NO.                             
           STRING C907-NUM-DDD    '-'                                           
                  C907-NUM-FIN-YR   '-'                                         
                  C907-NUM-VR-SERIAL                                            
                  DELIMITED BY SIZE INTO L31GB-JR-VCHR-NO                       
           END-STRING.                                                          
           MOVE WS-AMT-EARMARK    TO L31GB-AMT-TOTAL.                           
                                                                                
           EVALUATE B902-CDE-ERROR-ACCT(1)                                      
              WHEN 'S0045'                                                      
                 MOVE 'B05K006 FILE ERROR'         TO L31GB-ERROR-DESC          
              WHEN 'S0052'                                                      
                 MOVE 'INVALID INPUT      '        TO L31GB-ERROR-DESC          
              WHEN 'S0053'                                                      
                 MOVE 'AB05K009 FILE ERROR'        TO L31GB-ERROR-DESC          
              WHEN 'S0054'                                                      
                 MOVE 'B05K006 REC NOT FND'        TO L31GB-ERROR-DESC          
              WHEN 'S0055'                                                      
                 MOVE 'DOESN''T HAVE ENOUGH BALANCE'                            
                                                   TO L31GB-ERROR-DESC          
              WHEN OTHER                                                        
                 MOVE 'AB05C902 SUBROUTINE ERROR'  TO L31GB-ERROR-DESC          
           END-EVALUATE.                                                        
                                                                                
           WRITE L31GB-PRINT-REC  FROM L31GB-REC-DETAIL  AFTER 1.               
                                                                                
           ADD   1     TO  WS-L31GB-REC-ON-PAGE-CNT.                            
                                                                                
       850-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       851-PRINT-L416-HEADER.                                                   
      ******************************************************************        
           MOVE   FUNCTION CURRENT-DATE(1:8)                                    
                                    TO    WS-CURRENT-CCYYMMDD.                  
           MOVE   WS-CUR-DD         TO    WS-PC-DD.                             
           MOVE   WS-CUR-MM         TO    WS-PC-MM.                             
           MOVE   WS-CUR-CCYY       TO    WS-PC-CCYY.                           
           MOVE   WS-PR-CUR-DATE    TO    L31GB-DATE.                           
                                                                                
           ADD  1      TO WS-L31GB-PAGE-CNT.                                    
           MOVE WS-L31GB-PAGE-CNT    TO  L31GB-PAGE.                            
           WRITE L31GB-PRINT-REC  FROM L31GB-HEADER1     AFTER PAGE.            
           WRITE L31GB-PRINT-REC  FROM L31GB-HEADER2     AFTER 2.               
           WRITE L31GB-PRINT-REC  FROM L31GB-HEADER3     AFTER 2.               
           WRITE L31GB-PRINT-REC  FROM L31GB-UNDERLINE   AFTER 1.               
           MOVE  6     TO  WS-L31GB-REC-ON-PAGE-CNT.                            
                                                                                
       851-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       900-CLOSE-FILES.                                                         
      ******************************************************************        
      *------------------------------------------------------------*            
      *    CLOSE FILES AND END PROCESSING                          *            
      *------------------------------------------------------------*            
           CLOSE BP13F315                                                       
                 BP13K893                                                       
                 BP13K410                                                       
                 BP13K625                                                       
                 AG07F225                                                       
                 BP13L31G                                                       
                 P13L31GB.                                                      
                                                                                
           IF BP13K893-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BP13K893-STATUS ' BP13K893-STATUS         
              MOVE     BP13K893-STATUS TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF BP13K410-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BP13K410-STATUS ' BP13K410-STATUS         
              MOVE     BP13K410-STATUS TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF BP13K625-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BP13K625-STATUS ' BP13K625-STATUS         
              MOVE     BP13K625-STATUS TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           MOVE 'AG07K960' TO C907-NUM-FILENME.                                 
                                                                                
           CALL 'K960CLSE' USING AG07C907-AREA.                                 
                                                                                
           IF C907-NUM-FILE-STATUS NOT = ('00' AND '97')                        
              DISPLAY 'ERROR CLOSING AG07K960 = ' C907-NUM-FILE-STATUS          
              MOVE     C907-NUM-FILE-STATUS  TO WS-AG07-STAT-X                  
              MOVE     WS-AG07-STATUS        TO   RETURN-CODE                   
           END-IF.                                                              
                                                                                
           MOVE 'BP13K620' TO C906-NUM-FILENME.                                 
                                                                                
           CALL 'K620MCLS' USING BP13C906-AREA.                                 
                                                                                
           IF C906-NUM-FILE-STATUS NOT = ('00' AND '97')                        
              DISPLAY 'ERROR CLOSING BP13K620 = ' C906-NUM-FILE-STATUS          
              MOVE     C906-NUM-FILE-STATUS TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
      **CALL AB05C902 TO CLOSE B05K006 AND AB05K009 FILES(CODE='C')             
           MOVE SPACES              TO   WS-B902-AREA.                          
           INITIALIZE                    WS-B902-AREA.                          
           MOVE 'C'                 TO   B902-CDE-TYPE.                         
           CALL 'AB05C902'  USING WS-B902-AREA.                                 
                                                                                
           IF B902-CDE-ERROR NOT = ZEROES                                       
              MOVE B902-CDE-SYSERR  TO   RETURN-CODE                            
              DISPLAY 'ERROR CALLING AB05C902 SUB-ROUTINE!'                     
           END-IF.                                                              
                                                                                
                                                                                
           DISPLAY '                                   '.                       
           DISPLAY '******   BP13C31G CONTROL TOTALS   ******'.                 
           DISPLAY '                                   '.                       
           DISPLAY 'NO OF RECORDS READ FROM DAILY TRANS (BP13F315)  = '         
                    WS-CNT-F315-READ.                                           
           DISPLAY 'NO OF RECORDS READ FROM RFF MASTER  (BP13K893)  = '         
                    WS-CNT-K893-READ.                                           
           DISPLAY 'NO OF RECORDS READ FROM ACCUMSO FILE(BP13K410)  = '         
                    WS-CNT-K410-READ.                                           
           DISPLAY 'NO OF RECORDS WRITTEN  (BP13K625)               = '         
                    WS-CNT-K625-WRITE.                                          
           DISPLAY 'NO OF PAYMENTS WRITTEN (BP13L31G)               = '         
                    WS-CNT-L31G-WRITE.                                          
           DISPLAY 'NO OF PAYMENTS WRITTEN (AG07F225)               = '         
                    WS-CNT-F225-WRITE.                                          
           DISPLAY 'NO OF RECS WITH ERROR IN EARMARK                = '         
                    WS-L31GB-PRINT-CNT.                                         
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9000-CONVT-AMT-WORD.                                                     
      ******************************************************************        
      *------------------------------------------------------------*            
      *     OBJECTIVE  :  CONVERSION FROM AMOUNT IN DIGITS         *            
      *                              INTO AMOUNT IN WORDS          *            
      *------------------------------------------------------------*            
            MOVE SPACES TO WF-DETAILS.                                          
            MOVE ZERO   TO WS-SWITCH                                            
                           WS-THO-SW                                            
                           WS-CNT-SW                                            
                           WS-LIT-SW                                            
                           WS-TEN-SW                                            
                           WS-AND-SW.                                           
            MOVE 60     TO WS-PLACE-COUNTER.                                    
            MOVE 1      TO WF-SUB.                                              
                                                                                
            PERFORM B000-ROUTINE THRU B990-EXIT                                 
                         VARYING WS-SUB FROM 1 BY 1 UNTIL WS-SUB = 10.          
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       B000-ROUTINE SECTION.                                                    
      ******************************************************************        
       B000-GO.                                                                 
            IF WS-DIGIT (WS-SUB) = ZERO                                         
                 IF WS-TEN-SW = ZERO                                            
                    GO TO B800-MOVE-TYPE                                        
                 ELSE                                                           
                    GO TO B010-PROCESS.                                         
                                                                                
            IF WS-FIRST-SUB = ZERO                                              
               MOVE WS-SUB TO WS-FIRST-SUB.                                     
                                                                                
            IF WS-SWITCH = ZERO                                                 
               MOVE 1 TO WS-SWITCH.                                             
                                                                                
       B010-PROCESS.                                                            
            IF WS-SUB = 8                                                       
               MOVE 9 TO WS-CNT-SW.                                             
                                                                                
            IF WS-SUB = 9                                                       
               IF WS-CNT-SW = ZERO                                              
                  MOVE WS-DIGIT (WS-SUB) TO WS-CNT-SW.                          
                                                                                
            IF WS-SUB = 2 OR 3 OR 4                                             
               MOVE 1 TO WS-THO-SW.                                             
                                                                                
            MOVE WS-DIGIT (WS-SUB) TO WA-SUB.                                   
            IF WS-SUB = 3 OR 6 OR 8                                             
               IF WS-DIGIT (WS-SUB) = 1                                         
                  MOVE 1 TO WS-TEN-SW                                           
                  GO TO B990-EXIT                                               
               ELSE                                                             
                  ADD 18 TO WA-SUB                                              
            ELSE                                                                
            IF WS-SUB = 1 OR 4 OR 7 OR 9                                        
               IF WS-TEN-SW = 1                                                 
                  MOVE 0 TO WS-TEN-SW                                           
                  ADD 10 TO WA-SUB.                                             
            MOVE WA-NAME (WA-SUB) TO WL-AREA.                                   
            PERFORM R000-LOAD-IN.                                               
                                                                                
       B300-TENS.                                                               
            IF WS-SUB = 3 OR 6 OR 8                                             
               GO TO B990-EXIT.                                                 
                                                                                
       B400-MOVE-HUNDRED.                                                       
            IF WS-SUB = 2 OR 5                                                  
               MOVE L-HUNDRED TO WL-AREA                                        
               MOVE 1 TO WS-LIT-SW                                              
               PERFORM R000-LOAD-IN                                             
               MOVE 1 TO WS-AND-SW                                              
               GO TO B990-EXIT.                                                 
                                                                                
       B800-MOVE-TYPE.                                                          
            IF WS-SWITCH = ZERO                                                 
               GO TO B990-EXIT.                                                 
                                                                                
            IF WS-SUB = 1                                                       
               MOVE  L-MILLION TO WL-AREA                                       
               MOVE  1         TO WS-LIT-SW                                     
               MOVE  1         TO WS-AND-SW                                     
               GO TO B880-LOAD-IN.                                              
                                                                                
            IF WS-SUB = 7                                                       
               MOVE 1 TO WS-LIT-SW WS-AND-SW                                    
               IF WS-FIRST-SUB = 7  AND                                         
                  WS-DIGIT (WS-SUB) = 1                                         
                  MOVE L-DOLLAR TO WL-AREA                                      
                  GO TO B880-LOAD-IN                                            
               ELSE                                                             
                  MOVE L-DOLLARS TO WL-AREA                                     
                  GO TO B880-LOAD-IN.                                           
                                                                                
            IF WS-SUB = 4 AND WS-THO-SW = 1                                     
               MOVE 1 TO WS-AND-SW WS-LIT-SW                                    
               MOVE L-THOUSAND TO WL-AREA                                       
               GO TO B880-LOAD-IN.                                              
                                                                                
            IF WS-SUB = 9                                                       
               MOVE 1 TO WS-LIT-SW                                              
               IF WS-CNT-SW = ZERO                                              
                  MOVE L-ONLY TO WL-AREA                                        
                  GO TO B880-LOAD-IN                                            
               ELSE                                                             
               IF WS-CNT-SW = 1                                                 
                  MOVE L-CENT TO WL-AREA                                        
                  PERFORM R000-LOAD-IN                                          
                  MOVE L-ONLY TO WL-AREA                                        
                  GO TO B880-LOAD-IN                                            
               ELSE                                                             
                  MOVE L-CENTS TO WL-AREA                                       
                  PERFORM R000-LOAD-IN                                          
                  MOVE L-ONLY TO WL-AREA                                        
                  GO TO B880-LOAD-IN.                                           
            GO TO B990-EXIT.                                                    
                                                                                
       B880-LOAD-IN.                                                            
            PERFORM R000-LOAD-IN.                                               
                                                                                
       B990-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       R000-LOAD-IN SECTION.                                                    
      ******************************************************************        
      *------------------------------------------------------------*            
      *     THIS SECTION LOADS THE WORDS INTO THE THREE LINES      *            
      *     AND JUST KEEPS INCREMENTING A SUBSCRIPT.               *            
      *------------------------------------------------------------*            
                                                                                
       R001-GO.                                                                 
            IF WS-LIT-SW = 1                                                    
               MOVE ZERO TO WS-LIT-SW                                           
               GO TO R050-CONT.                                                 
                                                                                
            IF WS-SUB = 2 OR 5                                                  
               GO TO R050-CONT.                                                 
                                                                                
       R025-ADD-TEST.                                                           
            IF WS-AND-SW NOT = 1                                                
               GO TO R050-CONT.                                                 
                                                                                
            MOVE ZERO TO WS-AND-SW.                                             
            MOVE WL-AREA TO WL-AREA-SAVE.                                       
            MOVE L-AND TO WL-AREA.                                              
            PERFORM R200-LOADUP.                                                
            MOVE WL-AREA-SAVE TO WL-AREA.                                       
                                                                                
       R050-CONT.                                                               
            PERFORM R200-LOADUP.                                                
                                                                                
       R099-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       R200-LOADUP SECTION.                                                     
      ******************************************************************        
       R201-GO.                                                                 
            ADD WL-ITEM1 WF-SUB GIVING WS-RESULT.                               
            IF  WS-RESULT > WS-PLACE-COUNTER                                    
                ADD 1 WS-PLACE-COUNTER GIVING WF-SUB                            
                ADD 60 TO WS-PLACE-COUNTER.                                     
                                                                                
            PERFORM R500-LOAD VARYING WL-SUB FROM 2 BY 1 UNTIL                  
                    WL-ITEM (WL-SUB) = L-X.                                     
                                                                                
       R490-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       R500-LOAD SECTION.                                                       
      ******************************************************************        
       R501-GO.                                                                 
                                                                                
            MOVE WL-ITEM (WL-SUB) TO WF-ITEM (WF-SUB).                          
            ADD  1                TO WF-SUB.                                    
            IF WL-ITEM (WL-SUB) = SPACE                                         
               ADD   1     TO     WL-SUB                                        
               MOVE  L-X   TO     WL-ITEM (WL-SUB)                              
               SUBTRACT 1  FROM   WL-SUB.                                       
                                                                                
       R990-EXIT.                                                               
            EXIT.                                                               
