       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CL05.                                                 
      *AUTHOR.        BALA AMUTHAN.                                             
      *DATE-WRITTEN.  30/11/2006.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT  (SOC)                                 *           
      * =========================================================== *           
      *                                                             *           
      *  OBJECTIVE  :                                               *           
      *    1. PRINT SUMMARY REPORTS OF LETTER SENT & ACCEPTED.      *           
      *                                                             *           
      *  INPUT FILES:  1. BP13LC01 (INFO FOR OFFER LETTERS)         *           
      *                                                             *           
      *     REPORT  :  1. BP13LL05 (DBSS LETTER OF OFFER)           *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  BY  ON      DESCRIPTION                            *           
      * -------- --  --      -----------                            *           
      * BP132873 BA2 301106  NEW PROGRAM                            *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13LC01 ASSIGN TO BP13LC01.                                  
                                                                                
           SELECT BP13LL05 ASSIGN TO BP13LL05.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13LC01                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13LC01.                                                           
                                                                                
       FD  BP13LL05                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  PRINT-REC           PIC X(132).                                      
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
      *---------------------------------------------------------------*         
      *       SWITCHES    VARIABLE.                                   *         
      *---------------------------------------------------------------*         
       01  WS-SWITCH.                                                           
           05  WS-LC01-END           PIC X(1)       VALUE 'N'.                  
               88 CS-LC01-END                       VALUE 'Y'.                  
                                                                                
      *---------------------------------------------------------------*         
      *       RECORD-COUNT VARIABLE.                                  *         
      *---------------------------------------------------------------*         
       01  WS-COUNT.                                                            
           05  WS-LC01-READ          PIC 9(5) COMP  VALUE ZEROS.                
           05  WS-L05A-WRITE         PIC 9(5) COMP  VALUE ZEROS.                
                                                                                
      *---------------------------------------------------------------*         
      *       OTHER VARIABLES.                                        *         
      *---------------------------------------------------------------*         
       01  WS-DATE.                                                             
           05 WS-DATE-CHAR           PIC X(8).                                  
           05 WS-DATE-NUM REDEFINES WS-DATE-CHAR                                
                                     PIC 9(8).                                  
                                                                                
       01  WS-PREV-KEYS.                                                        
           05 WS-PREV-PRJCT          PIC X(03) VALUE SPACES.                    
           05 WS-PREV-FLAT           PIC X(02) VALUE SPACES.                    
                                                                                
       01  WS-PREVE-NME-PRJCT        PIC X(30).                         00181603
                                                                                
       77  WS-LOF-SENT               PIC 9(5) COMP VALUE ZEROES.                
       77  WS-LOF-ACCPT              PIC 9(5) COMP VALUE ZEROES.                
       77  WS-PAGE-NO                PIC 9(4) COMP VALUE ZEROS.                 
                                                                                
      *---------------------------------------------------------------*         
      *       HEADINGS FOR REPORT                                     *         
      *---------------------------------------------------------------*         
       01  CL05-HEAD-01.                                                        
           05  FILLER                PIC X(8)  VALUE 'BP13LL05'.                
           05  FILLER                PIC X(4)  VALUE SPACES.                    
           05  FILLER                PIC X(8)  VALUE 'HDB3    '.                
           05  FILLER                PIC X(24) VALUE SPACES.                    
           05  FILLER                PIC X(20) VALUE                            
                                      'S Y S T E M   O F   '.                   
           05  FILLER                PIC X(19) VALUE                            
                                       'C O M M I T M E N T'.                   
           05  FILLER                PIC X(15) VALUE SPACES.                    
           05  FILLER                PIC X(7)  VALUE 'DATE :'.                  
           05  CL05-DATE             PIC X(10) VALUE SPACES.                    
           05  FILLER                PIC X(4)  VALUE SPACES.                    
           05  FILLER                PIC X(7)  VALUE 'PAGE : '.                 
           05  CL05-PAGENO           PIC ZZZ9 .                                 
                                                                                
       01  CL05-HEAD-02.                                                        
           05  FILLER                PIC X(44)   VALUE SPACES.                  
           05  FILLER                PIC X(38)   VALUE                          
               '           SUMMARY REPORTS          '.                          
           05  FILLER                PIC X(50)   VALUE SPACES.                  
                                                                                
       01  CL05-HEAD-03.                                                        
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(15)   VALUE                          
               'PROJECT      : '.                                               
           05  CL05-PROJECT          PIC X(30)   VALUE SPACES.                  
           05  FILLER                PIC X(85)   VALUE SPACES.                  
                                                                                
       01  CL05-HEAD-04.                                                        
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(15)   VALUE                          
               'FLAT TYPE    : '.                                               
           05  CL05-FLT-TYP          PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(113)  VALUE SPACES.                  
                                                                                
       01  CL05-HEAD-05.                                                        
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(27)   VALUE                          
               'NO .OF LOF SENT          : '.                                   
           05  CL05-LOF-SENT         PIC Z(04)9  VALUE ZEROES.                  
           05  FILLER                PIC X(98)   VALUE SPACES.                  
                                                                                
       01  CL05-HEAD-06.                                                        
           05  FILLER                PIC X(02)   VALUE SPACES.                  
           05  FILLER                PIC X(27)   VALUE                          
               'NO .OF LOF ACCEPTED      : '.                                   
           05  CL05-LOF-ACCPT        PIC Z(04)9  VALUE ZEROES.                  
           05  FILLER                PIC X(98)   VALUE SPACES.                  
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-OPENING-ROUTINE THRU 1000-EXIT.                         
           PERFORM 2000-READ-F120       THRU 2000-EXIT.                         
           PERFORM 3000-MAIN-PROCESS    THRU 3000-EXIT                          
                   UNTIL CS-LC01-END.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT BP13LC01                                                  
               OUTPUT BP13LL05.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE-CHAR(7:2) '/' WS-DATE-CHAR(5:2) '/'                   
                  WS-DATE(1:4) DELIMITED BY SIZE INTO CL05-DATE.                
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-F120.                                                          
      *-------------------------------------------------------------            
           READ BP13LC01 AT END MOVE 'Y' TO WS-LC01-END                         
                                GO TO 2000-EXIT.                                
           ADD 1 TO WS-LC01-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-MAIN-PROCESS.                                                       
      *-------------------------------------------------------------            
           IF LC01-KEY-FLD NOT = WS-PREV-KEYS AND                               
              (WS-LOF-ACCPT > 0 OR WS-LOF-SENT > 0)                             
              PERFORM 5000-PRINT-REPORT        THRU 5000-EXIT                   
              MOVE ZEROES TO WS-LOF-SENT WS-LOF-ACCPT                           
           END-IF.                                                              
                                                                                
           IF LC01-DTE-OFFER-LTR-SENT NOT = SPACES AND ZEROES AND               
                                                   LOW-VALUES                   
              ADD +1            TO   WS-LOF-SENT                                
           END-IF.                                                              
                                                                                
           IF LC01-DTE-OFFER-LTR-ACCEPT NOT = SPACES AND ZEROES AND             
                                                   LOW-VALUES                   
              ADD +1            TO   WS-LOF-ACCPT                               
           END-IF.                                                              
                                                                                
           MOVE LC01-KEY-FLD    TO   WS-PREV-KEYS.                              
           MOVE LC01-NME-PRJCT  TO   WS-PREVE-NME-PRJCT.                        
                                                                                
           PERFORM 2000-READ-F120           THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-PRINT-REPORT.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-PREVE-NME-PRJCT       TO CL05-PROJECT.                       
           MOVE WS-PREV-FLAT             TO CL05-FLT-TYP.                       
                                                                                
           MOVE WS-LOF-SENT              TO CL05-LOF-SENT.                      
           MOVE WS-LOF-ACCPT             TO CL05-LOF-ACCPT.                     
                                                                                
           ADD +1                        TO WS-PAGE-NO.                         
           MOVE WS-PAGE-NO               TO CL05-PAGENO.                        
                                                                                
           MOVE CL05-HEAD-01          TO PRINT-REC.                             
           WRITE   PRINT-REC AFTER PAGE.                                        
                                                                                
           MOVE CL05-HEAD-02          TO PRINT-REC.                             
           WRITE   PRINT-REC.                                                   
                                                                                
           MOVE SPACES                TO PRINT-REC.                             
           WRITE   PRINT-REC.                                                   
                                                                                
           MOVE CL05-HEAD-03          TO PRINT-REC.                             
           WRITE   PRINT-REC.                                                   
                                                                                
           MOVE CL05-HEAD-04          TO PRINT-REC.                             
           WRITE   PRINT-REC.                                                   
                                                                                
           MOVE SPACES                TO PRINT-REC.                             
           WRITE   PRINT-REC.                                                   
                                                                                
           MOVE CL05-HEAD-05          TO PRINT-REC.                             
           WRITE   PRINT-REC.                                                   
                                                                                
           MOVE CL05-HEAD-06          TO PRINT-REC.                             
           WRITE   PRINT-REC.                                                   
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           IF WS-LOF-ACCPT > 0 OR WS-LOF-SENT > 0                               
              PERFORM 5000-PRINT-REPORT        THRU 5000-EXIT                   
              MOVE ZEROES TO WS-LOF-SENT WS-LOF-ACCPT                           
           END-IF.                                                              
                                                                                
           DISPLAY '------- BP13CL05 -------------'.                            
           DISPLAY 'NO OF LC01 READ    (BP13LC01) : '                           
                                               WS-LC01-READ.                    
           DISPLAY 'NO OF L05A WRITTEN (BP13LL05) : '                           
                                               WS-L05A-WRITE.                   
                                                                                
           CLOSE BP13LC01                                                       
                 BP13LL05.                                                      
                                                                                
       9999-EXIT.                                                               
           STOP RUN.                                                            
