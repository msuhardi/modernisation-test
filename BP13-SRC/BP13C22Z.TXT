       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C22Z.                                                 
      *AUTHOR.        SURESH NARAYANAN.                                         
      *DATE-WRITTEN.  30/07/2002.                                               
      *COPY OF.       BP13C220.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *  OBJECTIVE  :                                              *            
      *        TO COMPUTE CAT1 CAT2 CAT3 AND UPDATE INTO BP13F999. *            
      *                                                            *            
      *  INPUT FILES:                                              *            
      *       BP13K240                                             *            
      *  I-O :                                                     *            
      *       BP13F999                                             *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY   DESCRIPTION                         *            
      * -------- -------- ---  -----------                         *            
      * BP132562 20051007 GAN  ADD NEW OUTPUTS F230 AND F240       *            
      *                        ORIGINAL COPY IN BP13C220           *            
      * BP135572 20141121 ESA1 TO CATER FOR NEIGH&CNTRCT IN        *            
      *                        BP13K240 KEY-FLD                    *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F999 ASSIGN TO BP13F999.                                  
                                                                                
           SELECT BP13K240 ASSIGN TO BP13K240                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K240-KEY-FLD                                  
                  FILE STATUS  IS BP13K240-STATUS.                              
                                                                                
           SELECT BP13K230 ASSIGN TO BP13K230                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K230-KEY-FLD                                  
                  FILE STATUS  IS BP13K230-STATUS.                              
                                                                                
           SELECT BP13F240 ASSIGN TO BP13F240.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F999                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F999.                                                           
                                                                                
                                                                                
       FD   BP13K240                 RECORD CONTAINS 450 CHARACTERS.            
       COPY BP13K240.                                                           
                                                                                
       FD   BP13K230                 RECORD CONTAINS 450 CHARACTERS.            
       COPY BP13K230.                                                           
                                                                                
       FD  BP13F240                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 450 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  F240-REC        PIC X(450).                                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-VARIABLES.                                                        
           05 WS-F999-READ                  PIC 9(08)  VALUE ZEROES.            
           05 WS-PRT-LINE-CNT               PIC 9(08)  VALUE ZEROES.            
           05 WS-K240-NFND                  PIC 9(08)  VALUE ZEROES.            
           05 WS-K230-NFND                  PIC 9(08)  VALUE ZEROES.            
           05 WS-DETAIL-WRITTEN             PIC 9(08)  VALUE ZEROES.            
           05 WS-HEADER-WRITTEN             PIC 9(08)  VALUE ZEROES.            
           05 BP13K240-STATUS               PIC 9(02)  VALUE ZEROES.            
           05 BP13K230-STATUS               PIC 9(02)  VALUE ZEROES.            
           05 WS-K240-FND                   PIC X(01)  VALUE SPACES.            
           05 WS-K230-FND                   PIC X(01)  VALUE SPACES.            
           05 WS-F999-EOF                   PIC X(01)  VALUE 'N'.               
           05 WS-EDT-TMP                    PIC Z(7)9.                          
           05 WS-EDT-TMP1                   PIC Z(7)9.                          
           05 WS-REWRITE-F999               PIC 9(08)  VALUE ZEROES.            
           05 WS-TOTAL                      PIC 9(03)  VALUE ZEROES.            
           05 WS-BALANCE                    PIC 9(03)  VALUE ZEROES.            
           05 WS-ADJ-CAT1                   PIC 9(03)  VALUE ZEROES.            
           05 WS-ADJ-CAT2                   PIC 9(03)  VALUE ZEROES.            
           05 WS-ADJ-CAT3                   PIC 9(03)  VALUE ZEROES.            
                                                                                
                                                                                
       01  WS-EDT-DATE.                                                         
           05  WS-EDT-DD                    PIC 9(02).                          
           05  FILLER                       PIC X      VALUE '/'.               
           05  WS-EDT-MM                    PIC 9(02).                          
           05  FILLER                       PIC X      VALUE '/'.               
           05  WS-EDT-CCYY                  PIC 9(04).                          
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY                  PIC 9(04).                          
           05  WS-CUR-MM                    PIC 9(02).                          
           05  WS-CUR-DD                    PIC 9(02).                          
                                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-----------------------------------------------------------              
       000-MAIN-ROUTINE.                                                        
      *-----------------------------------------------------------              
           PERFORM 100-OPEN-FILES  THRU 100-EXIT.                               
           PERFORM 200-F999-READ   THRU 200-EXIT.                               
           PERFORM 300-PROCESS-REC THRU 300-EXIT                                
                   UNTIL WS-F999-EOF = 'Y'.                                     
                                                                                
           PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-----------------------------------------------------------              
           OPEN INPUT  BP13K240                                                 
                       BP13K230                                                 
                I-O    BP13F999                                                 
                OUTPUT BP13F240.                                                
                                                                                
           IF BP13K240-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BP13K240 STATUS ' BP13K240-STATUS            
              MOVE BP13K240-STATUS TO RETURN-CODE                               
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K230-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BP13K230 STATUS ' BP13K230-STATUS            
              MOVE BP13K230-STATUS TO RETURN-CODE                               
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
           MOVE WS-CUR-CCYY           TO WS-EDT-CCYY.                           
           MOVE WS-CUR-MM             TO WS-EDT-MM.                             
           MOVE WS-CUR-DD             TO WS-EDT-DD.                             
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-F999-READ.                                                           
      *-----------------------------------------------------------              
           READ BP13F999 AT END                                                 
                MOVE 'Y' TO WS-F999-EOF                                         
                GO TO 200-EXIT.                                                 
                                                                                
                ADD 1 TO WS-F999-READ.                                          
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-REC.                                                         
      *-----------------------------------------------------------              
            PERFORM 400-BP13K230-READ  THRU 400-EXIT.                           
                                                                                
            IF WS-K230-FND = 'Y'                                                
               PERFORM 500-BP13K240-READ  THRU 500-EXIT                         
            END-IF.                                                             
                                                                                
            IF WS-K240-FND = 'Y'                                                
               COMPUTE WS-TOTAL =  K240-NUM-TOT-CAT1 +                          
                                   K240-NUM-TOT-CAT2 +                          
                                   K240-NUM-TOT-CAT3                            
                                                                                
               COMPUTE WS-BALANCE = K240-NUM-TOTAL - WS-TOTAL                   
                                                                                
               COMPUTE WS-ADJ-CAT1 = F999-NUM-MAX-CAT1 -                        
                                     K240-NUM-TOT-CAT1                          
               COMPUTE WS-ADJ-CAT2 = F999-NUM-MAX-CAT2 -                        
                                     K240-NUM-TOT-CAT2                          
               COMPUTE WS-ADJ-CAT3 = F999-NUM-MAX-CAT3 -                        
                                     K240-NUM-TOT-CAT3                          
                                                                                
               IF WS-ADJ-CAT1 > WS-BALANCE                                      
                  MOVE WS-BALANCE     TO F999-NUM-ADJ-CAT1                      
               ELSE                                                             
                  MOVE WS-ADJ-CAT1    TO F999-NUM-ADJ-CAT1                      
               END-IF                                                           
                                                                                
               IF WS-ADJ-CAT2 > WS-BALANCE                                      
                  MOVE WS-BALANCE     TO F999-NUM-ADJ-CAT2                      
               ELSE                                                             
                  MOVE WS-ADJ-CAT2    TO F999-NUM-ADJ-CAT2                      
               END-IF                                                           
                                                                                
               IF WS-ADJ-CAT3 > WS-BALANCE                                      
                  MOVE WS-BALANCE     TO F999-NUM-ADJ-CAT3                      
               ELSE                                                             
                  MOVE WS-ADJ-CAT3    TO F999-NUM-ADJ-CAT3                      
               END-IF                                                           
                                                                                
               IF K240-NUM-CAT1-FULL = 'F'                                      
                  MOVE SPACES              TO F999-NUM-CAT1-FULL                
                  MOVE ZEROES              TO F999-NUM-MAX-CAT1                 
               ELSE                                                             
                  MOVE K240-NUM-CAT1-FULL  TO F999-NUM-CAT1-FULL                
               END-IF                                                           
                                                                                
               IF K240-NUM-CAT2-FULL = 'F'                                      
                  MOVE SPACES              TO F999-NUM-CAT2-FULL                
                  MOVE ZEROES              TO F999-NUM-MAX-CAT2                 
               ELSE                                                             
                  MOVE K240-NUM-CAT2-FULL  TO F999-NUM-CAT2-FULL                
               END-IF                                                           
                                                                                
               IF K240-NUM-CAT3-FULL = 'F'                                      
                  MOVE SPACES              TO F999-NUM-CAT3-FULL                
                  MOVE ZEROES              TO F999-NUM-MAX-CAT3                 
               ELSE                                                             
                  MOVE K240-NUM-CAT3-FULL  TO F999-NUM-CAT3-FULL                
               END-IF                                                           
                                                                                
               PERFORM 700-BP13F240-HEADER THRU 700-EXIT                        
                                                                                
               REWRITE BP13F999-RECORD                                          
               ADD 1 TO WS-REWRITE-F999                                         
            END-IF.                                                             
                                                                                
            PERFORM 200-F999-READ   THRU 200-EXIT.                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-BP13K230-READ.                                                       
      *-----------------------------------------------------------              
           MOVE  SPACES                  TO  K230-KEY-FLD.                      
           MOVE  F999-NUM-HDB-REF        TO  K230-NUM-SCH-ACC.                  
                                                                                
           READ BP13K230.                                                       
           EVALUATE BP13K230-STATUS                                             
           WHEN 00                                                              
              MOVE 'Y'       TO WS-K230-FND                                     
           WHEN 23                                                              
              MOVE 'N'       TO WS-K230-FND                                     
              DISPLAY 'RECORD NOT FOUND BP13K230 : ' K230-KEY-FLD               
               ADD  1        TO WS-K230-NFND                                    
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K230 : ' BP13K230-STATUS               
              MOVE BP13K230-STATUS            TO  RETURN-CODE                   
              PERFORM 999-CLOSE-ROUTINE       THRU 999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-BP13K240-READ.                                                       
      *-----------------------------------------------------------              
           MOVE  SPACES                  TO  K240-KEY-FLD                       
           MOVE  '01'                    TO  K240-CDE-HEADER                    
           MOVE  K230-NUM-BLK            TO  K240-NUM-BLK                       
           MOVE  K230-NUM-NEW-TOWN       TO  K240-NUM-NEW-TOWN                  
           MOVE  K230-DTE-ALLOC          TO  K240-DTE-ALLOC                     
           MOVE  F999-NEIGHBOURHOOD      TO  K240-NUM-NEIGH.                    
           MOVE  F999-CONTRACT-NO        TO  K240-NUM-CNTRCT.                   
           MOVE  K230-NUM-FLAT-TYPE      TO  K240-NUM-FLAT-TYPE                 
                                                                                
           READ BP13K240.                                                       
           EVALUATE BP13K240-STATUS                                             
           WHEN 00                                                              
              MOVE 'Y'       TO WS-K240-FND                                     
           WHEN 23                                                              
              MOVE 'N'       TO WS-K240-FND                                     
              DISPLAY 'RECORD NOT FOUND BP13K240 : ' K240-KEY-FLD               
               ADD  1        TO WS-K240-NFND                                    
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K240 : ' BP13K240-STATUS               
              MOVE BP13K240-STATUS            TO  RETURN-CODE                   
              PERFORM 999-CLOSE-ROUTINE       THRU 999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       700-BP13F240-HEADER.                                                     
      *-----------------------------------------------------------              
           MOVE WS-BALANCE          TO K240-NUM-TOTAL.                          
           MOVE F999-NUM-CAT1-FULL  TO K240-NUM-CAT1-FULL.                      
           MOVE F999-NUM-CAT2-FULL  TO K240-NUM-CAT2-FULL.                      
           MOVE F999-NUM-CAT3-FULL  TO K240-NUM-CAT3-FULL.                      
           MOVE F999-NUM-ADJ-CAT1   TO K240-NUM-MAX-CAT1.                       
           MOVE F999-NUM-ADJ-CAT2   TO K240-NUM-MAX-CAT2.                       
           MOVE F999-NUM-ADJ-CAT3   TO K240-NUM-MAX-CAT3.                       
           MOVE ZEROES              TO K240-NUM-TOT-CAT1                        
                                       K240-NUM-TOT-CAT2                        
                                       K240-NUM-TOT-CAT3.                       
                                                                                
           WRITE F240-REC FROM K240-HEADER-REC.                                 
                                                                                
           ADD 1 TO WS-HEADER-WRITTEN.                                          
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       999-CLOSE-ROUTINE.                                                       
      *-----------------------------------------------------------              
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' BP13C22Z CONTROL TOTAL        ' WS-EDT-DATE.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-F999-READ    TO WS-EDT-TMP.                                  
           DISPLAY 'NO OF RECS READ FROM BP13F999  : ' WS-EDT-TMP.              
           MOVE WS-REWRITE-F999 TO WS-EDT-TMP1.                                 
           DISPLAY 'NO OF RECS REWRITTEN IN F999   : ' WS-EDT-TMP1.             
           MOVE WS-HEADER-WRITTEN TO WS-EDT-TMP1.                               
           DISPLAY 'NO OF HDR RECS WRITTEN TO BP13F240 : ' WS-EDT-TMP1.         
           DISPLAY '*---------------------------------------------*'.           
                                                                                
                                                                                
           CLOSE BP13F999                                                       
                 BP13K230                                                       
                 BP13K240                                                       
                 BP13F240.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
