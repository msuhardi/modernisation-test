      *------------*                                                            
       IDENTIFICATION DIVISION.                                                 
      *------------*                                                            
       PROGRAM-ID.    BP13CC4A.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  09 MAR 2009.                                              
      * =========================================================== *           
      * SYSTEM OF COMMITMENT  (BP13)                                *           
      * =========================================================== *           
      *  OBJECTIVES  :   TO READ BP13FD04 AND PRINT REPORTS         *           
      *                                                             *           
      *  INPUT-OUTPUT FILES :                                       *           
      *  ====================                                       *           
      *  TYPE  DD NAME   ACCESS   KEYLENGTH                         *           
      *  --    ----      ---      -----                             *           
      *  I     BP13FD04   SEQ.       08                             *           
      *  I     BP13KA10  RANDOM      03                             *           
      *  I     SY02F001   SEQ.                                      *           
      *  O     BP13LC4A   SEQ.       132                            *           
      * ------------------------------ *                                        
      * CHG-NO   DATE   BY   DESCRIPTION                            *           
      * ---- --- -           ------                                 *           
      * BP13XXXX 090309 JB8  NEW PGM                                *           
      * BP135161 070514 IMC  CATER FOR X ACCOUNTS                   *           
      * BP135452 060814 IMC  CATER FOR CONTROL RECORDS              *           
      * BP135903 210416 FNP1 INITIALIZE INPUT AMOUNT FIELDS         *           
      * BP136098 210416 FNP1 DO NOT COUNT IF OS LOAN AMT < 0        *           
      * =========================================================== *           
                                                                                
      *------------*                                                            
       ENVIRONMENT DIVISION.                                                    
      *------------*                                                            
       CONFIGURATION SECTION.                                                   
      *------------*                                                            
                                                                                
         SOURCE-COMPUTER.   IBM-3090.                                           
         OBJECT-COMPUTER.   IBM-3090.                                           
                                                                                
      *------------*                                                            
       INPUT-OUTPUT SECTION.                                                    
      *------------*                                                            
       FILE-CONTROL.                                                            
           SELECT BP13FD04 ASSIGN       TO BP13FD04.                            
           SELECT SY02F001 ASSIGN       TO SY02F001.                            
                                                                                
           SELECT BP13KA10 ASSIGN       TO BP13KA10                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS KA10-KEY-FLD                         
                           FILE STATUS  IS WS-KA10-STATUS.                      
                                                                                
           SELECT BP13LC4A ASSIGN       TO BP13LC4A.                            
                                                                                
      *------------*                                                            
       DATA DIVISION.                                                           
      *------------*                                                            
      *------------*                                                            
       FILE SECTION.                                                            
      *------------*                                                            
                                                                                
       FD   BP13FD04                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 1700 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13FD04.                                                           
                                                                                
       FD   BP13KA10                                                            
            RECORD CONTAINS 800 CHARACTERS.                                     
       COPY BP13KA10.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13LC4A    BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 132 CHARACTERS                          
                        LABEL RECORD IS OMITTED                                 
                        RECORDING MODE IS F.                                    
       01   BP13LC4A-REC PIC X(132).                                            
                                                                                
      *-------------------------------                                          
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------                                          
       01  WS-COUNTERS.                                                         
           05  WS-FD04-READ        PIC 9(7)      VALUE ZEROES.                  
           05  WS-LC4A-WRITE       PIC 9(7)      VALUE ZEROES.                  
           05  WS-SNO              PIC 9(5)      VALUE ZEROES.                  
           05  WS-PAGE-CTR         PIC 9(4)      VALUE ZEROES.                  
           05  WS-LINE-CTR         PIC 9(2)      VALUE 61.                      
           05  WS-CNT-3MTH         PIC 9(05)     VALUE 0.                       
           05  WS-CNT-6MTH         PIC 9(05)     VALUE 0.                       
           05  WS-CNT-1YEAR        PIC 9(05)     VALUE 0.                       
           05  WS-CNT-2YEAR        PIC 9(05)     VALUE 0.                       
           05  WS-CNT-3YEAR        PIC 9(05)     VALUE 0.                       
           05  WS-CNT-6YEAR        PIC 9(05)     VALUE 0.                       
           05  WS-CNT-7YEAR        PIC 9(05)     VALUE 0.                       
           05  WS-AMT-3MTH-OS      PIC S9(09)V99 VALUE 0.                       
           05  WS-AMT-6MTH-OS      PIC S9(09)V99 VALUE 0.                       
           05  WS-AMT-1YR-OS       PIC S9(09)V99 VALUE 0.                       
           05  WS-AMT-2YR-OS       PIC S9(09)V99 VALUE 0.                       
           05  WS-AMT-3YR-OS       PIC S9(09)V99 VALUE 0.                       
           05  WS-AMT-6YR-OS       PIC S9(09)V99 VALUE 0.                       
           05  WS-AMT-7YR-OS       PIC S9(09)V99 VALUE 0.                       
           05  WS-AMT-3MTH-WTG     PIC S9(09)V99 VALUE 0.                       
           05  WS-AMT-6MTH-WTG     PIC S9(09)V99 VALUE 0.                       
           05  WS-AMT-1YR-WTG      PIC S9(09)V99 VALUE 0.                       
           05  WS-AMT-2YR-WTG      PIC S9(09)V99 VALUE 0.                       
           05  WS-AMT-3YR-WTG      PIC S9(09)V99 VALUE 0.                       
           05  WS-AMT-6YR-WTG      PIC S9(09)V99 VALUE 0.                       
           05  WS-AMT-7YR-WTG      PIC S9(09)V99 VALUE 0.                       
           05  WS-AMT-TOT-OS       PIC S9(09)V99 VALUE 0.                       
           05  WS-AMT-TOT-WTG      PIC S9(09)V99 VALUE 0.                       
           05  WS-CNT-TOT          PIC S9(09)V99 VALUE 0.                       
           05  WS-REPUD-MONTH      PIC 9(09)     VALUE 0.                       
           05  WS-CURR-MONTH       PIC 9(09)     VALUE 0.                       
                                                                                
       01  WS-DTE-REPUDIATE.                                                    
           05  WS-DTE-REPUD-YYYY   PIC 9(4)      VALUE ZEROES.                  
           05  WS-DTE-REPUD-MM     PIC 9(2)      VALUE ZEROES.                  
                                                                                
       01  WS-DTE-CURRENT.                                                      
           05  WS-DTE-CURR-YYYY    PIC 9(4)      VALUE ZEROES.                  
           05  WS-DTE-CURR-MM      PIC 9(2)      VALUE ZEROES.                  
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-KA10-STATUS      PIC 9(2)      VALUE ZEROES.                  
           05  WS-FD04-EOF         PIC X(01)     VALUE SPACE.                   
                                                                                
       01  WS-AMOUTS.                                                           
           05  WS-SUB-LOAN-BAL     PIC S9(09)V99 VALUE ZEROES.                  
           05  WS-SUB-WTG-LOAN     PIC S9(09)V99 VALUE ZEROES.                  
           05  WS-SUB-ARREARS      PIC S9(08)V99 VALUE ZEROES.                  
           05  WS-GRT-LOAN-BAL     PIC S9(09)V99 VALUE ZEROES.                  
           05  WS-GRT-WTG-LOAN     PIC S9(09)V99 VALUE ZEROES.                  
           05  WS-GRT-ARREARS      PIC S9(08)V99 VALUE ZEROES.                  
           05  WS-AMT-WTG-LOAN     PIC S9(07)V99 VALUE ZEROES.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-AGE          PIC 9(02)     VALUE ZEROES.                  
           05  WS-AGE-TYPE         PIC X(05)     VALUE SPACES.                  
           05  WS-PRE-TYPE         PIC X(05)     VALUE SPACES.                  
           05  WS-AGE-AREA         PIC X(50)     VALUE SPACES.                  
           05  WS-NO-REC           PIC X(01)     VALUE SPACE.                   
                                                                                
       01  WS-AGE-AREA-DETAIL.                                                  
           05  WS-AGE-AREA1        PIC X(50)     VALUE                          
               'PAST DUE < 3 MTHS                 '.                            
           05  WS-AGE-AREA2        PIC X(50)     VALUE                          
               'PAST DUE >= 3 MTHS BUT < 6 MTHS   '.                            
           05  WS-AGE-AREA3        PIC X(50)     VALUE                          
               'PAST DUE >= 6 MTHS BUT < 12 MTHS  '.                            
           05  WS-AGE-AREA4        PIC X(50)     VALUE                          
               'PAST DUE >= 1 YEAR BUT < 2 YEARS  '.                            
           05  WS-AGE-AREA5        PIC X(50)     VALUE                          
               'PAST DUE >= 2 YEARS BUT < 3 YEARS '.                            
           05  WS-AGE-AREA6        PIC X(50)     VALUE                          
               'PAST DUE >= 3 YEARS BUT < 6 YEARS '.                            
           05  WS-AGE-AREA7        PIC X(50)     VALUE                          
               'PAST DUE >= 6 YEARS               '.                            
                                                                                
       01  WS-MONTH-NAME.                                                       
           05  WS-STRING           PIC X(36)     VALUE                          
               'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'.                          
           05  WS-MONTH  REDEFINES WS-STRING OCCURS 12 TIMES PIC XXX.           
                                                                        01010000
       01  HEADING-1.                                                           
           05  LC4A-HDR            PIC X(08)     VALUE 'BP13LC4A'.              
           05  FILLER              PIC X(12)     VALUE '     HDBCAT3'.          
           05  FILLER              PIC X(22)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LC4A-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LC4A-PAGE-HDR       PIC ZZZZ      VALUE ZEROES.                  
                                                                                
       01  HEADING-2.                                                           
           05  FILLER              PIC X(28)     VALUE SPACES.                  
           05  FILLER              PIC X(38)     VALUE                          
               'WEIGHTED AMOUNT OF DBSS LOAN PAST DUE '.                        
           05  FILLER              PIC X(23)     VALUE                          
               'BUT NOT IMPAIRED AS AT '.                                       
           05  LC4A-DTE            PIC X(11)     VALUE SPACES.                  
                                                                                
       01  HEADING-2X.                                                          
           05  FILLER              PIC X(28)     VALUE SPACES.                  
           05  FILLER              PIC X(38)     VALUE                          
               'WEIGHTED AMOUNT OF DBSS LOAN PAST DUE '.                        
           05  FILLER              PIC X(23)     VALUE                          
               'BUT NOT IMPAIRED AS AT '.                                       
           05  LC4A-DTEX           PIC X(11)     VALUE SPACES.                  
           05  FILLER              PIC X(14)     VALUE                          
               ' (EX-ACCOUNTS)'.                                                
                                                                                
       01  BP13LC4A-AGE.                                                        
           05  FILLER              PIC X(11)     VALUE 'CATEGORY : '.           
           05  LC4A-AGE-AREA       PIC X(50)     VALUE SPACES.                  
                                                                                
       01  BP13LC4A-NO-REC.                                                     
           05  FILLER              PIC X(132)    VALUE                          
               '  ** NO RECORDS ** '.                                           
                                                                                
       01  BP13LC4A-HEADER-1.                                                   
           05  FILLER               PIC X(72)    VALUE SPACES.                  
           05  FILLER               PIC X(32)    VALUE                          
               'O/S LOAN     AGE  WTG LOAN AMT  '.                              
           05  FILLER               PIC X(45)     VALUE                         
               'ARREARS AMT  CURRENT INST'.                                     
                                                                                
       01  BP13LC4A-HEADER-2.                                                   
           05  FILLER               PIC X(40)    VALUE                          
               '  S/N  REGN NO.  HDB REF         PROJECT'.                      
           05  FILLER               PIC X(49)     VALUE                         
               '                         A/C       ($)      (MTH)'.             
           05  FILLER               PIC X(40)     VALUE                         
               '      ($)          ($)           ($)    '.                      
                                                                                
       01  BP13LC4A-HEADER2.                                                    
           05  FILLER               PIC X(51)     VALUE                         
               '-----  --------  --------------  ------------------'.           
           05  FILLER               PIC X(45)     VALUE                         
               '------------  ---  ------------   ---  ------'.                 
           05  FILLER               PIC X(33)     VALUE                         
               '------  -----------  ------------'.                             
                                                                                
       01  BP13LC4A-DETAILS.                                                    
           05  LC4A-DTL-SERIAL      PIC ZZZZ9     VALUE ZEROES.                 
           05  FILLER               PIC X(02)     VALUE SPACES.                 
           05  LC4A-REGNO           PIC X(08)     VALUE SPACES.                 
           05  FILLER               PIC X(02)     VALUE SPACES.                 
           05  LC4A-SCH-ACC         PIC X(14)     VALUE SPACES.                 
           05  FILLER               PIC X(02)     VALUE SPACES.                 
           05  LC4A-NME-PRJCT       PIC X(30)     VALUE SPACES.                 
           05  FILLER               PIC X(03)     VALUE SPACES.                 
           05  LC4A-ACCT-STAT       PIC X(01)     VALUE SPACES.                 
           05  FILLER               PIC X(03)     VALUE SPACES.                 
           05  LC4A-AMT-LOAN-BAL    PIC Z,ZZZ,ZZ9.99 VALUE ZEROES.              
           05  FILLER               PIC X(04)     VALUE SPACES.                 
           05  LC4A-MO-ARREAR       PIC Z9        VALUE ZEROES.                 
           05  FILLER               PIC X(02)     VALUE SPACES.                 
           05  LC4A-AMT-WTG-LOAN    PIC Z,ZZZ,ZZ9.99 VALUE ZEROES.              
           05  FILLER               PIC X(03)     VALUE SPACES.                 
           05  LC4A-AMT-ARREAR      PIC ZZZ,ZZ9.99 VALUE ZEROES.                
           05  FILLER               PIC X(04)     VALUE SPACES.                 
           05  LC4A-AMT-INSTAL      PIC ZZZ,ZZ9.99 VALUE ZEROES.                
                                                                                
       01  BP13LC4A-TOTAL.                                                      
           05  FILLER               PIC X(02)     VALUE SPACES.                 
           05  LC4A-TOT-TYPE        PIC X(20)     VALUE SPACES.                 
           05  FILLER               PIC X(46)     VALUE SPACES.                 
           05  LC4A-TOT-LOAN-BAL    PIC ZZZ,ZZZ,ZZ9.99 VALUE ZEROES.            
           05  FILLER               PIC X(06)     VALUE SPACES.                 
           05  LC4A-TOT-WTG-LOAN    PIC ZZZ,ZZZ,ZZ9.99 VALUE ZEROES.            
           05  FILLER               PIC X(01)     VALUE SPACES.                 
           05  LC4A-TOT-ARREAR      PIC Z,ZZZ,ZZ9.99 VALUE ZEROES.              
                                                                                
       01  BP13LC4A-TOT-DASH.                                                   
           05  FILLER               PIC X(02)     VALUE SPACES.                 
           05  FILLER               PIC X(20)     VALUE SPACES.                 
           05  FILLER               PIC X(46)     VALUE SPACES.                 
           05  FILLER               PIC X(14)     VALUE ALL '-'.                
           05  FILLER               PIC X(06)     VALUE SPACES.                 
           05  FILLER               PIC X(14)     VALUE ALL '-'.                
           05  FILLER               PIC X(01)     VALUE SPACES.                 
           05  FILLER               PIC X(12)     VALUE ALL '-'.                
                                                                                
       01  P13LC4AB-FIL1.                                                       
           05  FILLER               PIC X(22)    VALUE SPACES.                  
           05  FILLER               PIC X(89)    VALUE ALL '-'.                 
                                                                                
       01  P13LC4AB-FIL2.                                                       
           05  FILLER               PIC X(22)    VALUE SPACES.                  
           05  FILLER               PIC X(35)    VALUE                          
               '|   LIVE ACCOUNTS/DEBIT BALANCE   |'.                           
           05  FILLER               PIC X(54)    VALUE                          
               '            LOAN PAST DUE BUT NOT IMPAIRED           |'.        
                                                                                
       01  P13LC4AB-FIL3.                                                       
           05  FILLER               PIC X(22)    VALUE SPACES.                  
           05  FILLER               PIC X(35)    VALUE                          
               '|                                 |'.                           
           05  FILLER               PIC X(54)    VALUE                          
               '                        LOAN AMT                     |'.        
                                                                                
       01  P13LC4AB-FIL4.                                                       
           05  FILLER               PIC X(22)    VALUE SPACES.                  
           05  FILLER               PIC X(35)    VALUE                          
               '|                                 |'.                           
           05  FILLER               PIC X(54)    VALUE                          
               ' O/S LOAN AMT($) | WEIGHTED LOAN AMT($)| NO OF CASES |'.        
                                                                                
       01  P13LC4AB-FIL5.                                                       
           05  FILLER               PIC X(22)    VALUE SPACES.                  
           05  FILLER               PIC X(36)    VALUE                          
               '|PAST DUE < 3 MTHS                | '.                          
           05  FIL5-OS-LOAN         PIC ZZZ,ZZZ,ZZ9.99 VALUE ZEROES.            
           05  FILLER               PIC X(08)    VALUE '  |     '.              
           05  FIL5-WTG-LOAN        PIC ZZZ,ZZZ,ZZ9.99 VALUE ZEROES.            
           05  FILLER               PIC X(08)    VALUE '  |     '.              
           05  FIL5-CNT-CASES       PIC ZZZZ9    VALUE ZEROES.                  
           05  FILLER               PIC X(04)    VALUE '   |'.                  
                                                                                
       01  P13LC4AB-FIL6.                                                       
           05  FILLER               PIC X(22)    VALUE SPACES.                  
           05  FILLER               PIC X(36)    VALUE                          
               '|PAST DUE >= 3 MTHS BUT < 6 MTHS  | '.                          
           05  FIL6-OS-LOAN         PIC ZZZ,ZZZ,ZZ9.99 VALUE ZEROES.            
           05  FILLER               PIC X(08)    VALUE '  |     '.              
           05  FIL6-WTG-LOAN        PIC ZZZ,ZZZ,ZZ9.99 VALUE ZEROES.            
           05  FILLER               PIC X(08)    VALUE '  |     '.              
           05  FIL6-CNT-CASES       PIC ZZZZ9    VALUE ZEROES.                  
           05  FILLER               PIC X(04)    VALUE '   |'.                  
                                                                                
       01  P13LC4AB-FIL7.                                                       
           05  FILLER               PIC X(22)    VALUE SPACES.                  
           05  FILLER               PIC X(36)    VALUE                          
               '|PAST DUE >= 6 MTHS BUT < 12 MTHS | '.                          
           05  FIL7-OS-LOAN         PIC ZZZ,ZZZ,ZZ9.99 VALUE ZEROES.            
           05  FILLER               PIC X(08)    VALUE '  |     '.              
           05  FIL7-WTG-LOAN        PIC ZZZ,ZZZ,ZZ9.99 VALUE ZEROES.            
           05  FILLER               PIC X(08)    VALUE '  |     '.              
           05  FIL7-CNT-CASES       PIC ZZZZ9    VALUE ZEROES.                  
           05  FILLER               PIC X(04)    VALUE '   |'.                  
                                                                                
       01  P13LC4AB-FIL8.                                                       
           05  FILLER               PIC X(22)    VALUE SPACES.                  
           05  FILLER               PIC X(36)    VALUE                          
               '|PAST DUE >= 1 YEAR BUT < 2 YEARS | '.                          
           05  FIL8-OS-LOAN         PIC ZZZ,ZZZ,ZZ9.99 VALUE ZEROES.            
           05  FILLER               PIC X(08)    VALUE '  |     '.              
           05  FIL8-WTG-LOAN        PIC ZZZ,ZZZ,ZZ9.99 VALUE ZEROES.            
           05  FILLER               PIC X(08)    VALUE '  |     '.              
           05  FIL8-CNT-CASES       PIC ZZZZ9    VALUE ZEROES.                  
           05  FILLER               PIC X(04)    VALUE '   |'.                  
                                                                                
       01  P13LC4AB-FIL9.                                                       
           05  FILLER               PIC X(22)    VALUE SPACES.                  
           05  FILLER               PIC X(36)    VALUE                          
               '|PAST DUE >= 2 YEARS BUT < 3 YEARS| '.                          
           05  FIL9-OS-LOAN         PIC ZZZ,ZZZ,ZZ9.99 VALUE ZEROES.            
           05  FILLER               PIC X(08)    VALUE '  |     '.              
           05  FIL9-WTG-LOAN        PIC ZZZ,ZZZ,ZZ9.99 VALUE ZEROES.            
           05  FILLER               PIC X(08)    VALUE '  |     '.              
           05  FIL9-CNT-CASES       PIC ZZZZ9    VALUE ZEROES.                  
           05  FILLER               PIC X(04)    VALUE '   |'.                  
                                                                                
       01  P13LC4AB-FIL10.                                                      
           05  FILLER               PIC X(22)    VALUE SPACES.                  
           05  FILLER               PIC X(36)    VALUE                          
               '|PAST DUE >= 3 YEARS BUT < 6 YEARS| '.                          
           05  FIL10-OS-LOAN        PIC ZZZ,ZZZ,ZZ9.99 VALUE ZEROES.            
           05  FILLER               PIC X(08)    VALUE '  |     '.              
           05  FIL10-WTG-LOAN       PIC ZZZ,ZZZ,ZZ9.99 VALUE ZEROES.            
           05  FILLER               PIC X(08)    VALUE '  |     '.              
           05  FIL10-CNT-CASES      PIC ZZZZ9    VALUE ZEROES.                  
           05  FILLER               PIC X(04)    VALUE '   |'.                  
                                                                                
       01  P13LC4AB-FIL11.                                                      
           05  FILLER               PIC X(22)    VALUE SPACES.                  
           05  FILLER               PIC X(36)    VALUE                          
               '|PAST DUE >= 6 YEARS              | '.                          
           05  FIL11-OS-LOAN        PIC ZZZ,ZZZ,ZZ9.99 VALUE ZEROES.            
           05  FILLER               PIC X(08)    VALUE '  |     '.              
           05  FIL11-WTG-LOAN       PIC ZZZ,ZZZ,ZZ9.99 VALUE ZEROES.            
           05  FILLER               PIC X(08)    VALUE '  |     '.              
           05  FIL11-CNT-CASES      PIC ZZZZ9    VALUE ZEROES.                  
           05  FILLER               PIC X(04)    VALUE '   |'.                  
                                                                                
       01  P13LC4AB-FIL12.                                                      
           05  FILLER               PIC X(22)    VALUE SPACES.                  
           05  FILLER               PIC X(36)    VALUE                          
               '|TOTAL                            | '.                          
           05  FIL12-OS-LOAN        PIC ZZZ,ZZZ,ZZ9.99 VALUE ZEROES.            
           05  FILLER               PIC X(08)    VALUE '  |     '.              
           05  FIL12-WTG-LOAN       PIC ZZZ,ZZZ,ZZ9.99 VALUE ZEROES.            
           05  FILLER               PIC X(08)    VALUE '  |     '.              
           05  FIL12-CNT-CASES      PIC ZZZZ9    VALUE ZEROES.                  
           05  FILLER               PIC X(04)    VALUE '   |'.                  
                                                                                
      *-------------------------------                                          
       PROCEDURE DIVISION.                                                      
      *-------------------------------                                          
                                                                                
      *-------------------------------                                          
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------                                          
                                                                                
           PERFORM 1000-START-UP           THRU 1000-EXIT.                      
      * 1ST READ IS FOR THE CONTROL RECORD TO DETERMINE HEADER                  
           PERFORM 2000-FD04-READ          THRU 2000-EXIT.                      
           PERFORM 5000-PRINT-HEADER THRU 5000-EXIT                             
           ADD 1                      TO WS-LINE-CTR                            
      *                                                                         
           PERFORM 2000-FD04-READ          THRU 2000-EXIT.                      
           PERFORM 3100-INITIALIZE-AMOUNTS THRU 3100-EXIT.                      
           PERFORM 2100-PROC-FD04          THRU 2100-EXIT.                      
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
                   UNTIL WS-FD04-EOF = 'Y'.                                     
           PERFORM 4200-PRINT-SUBTOTAL     THRU 4200-EXIT.                      
                                                                                
           MOVE 'TYPE8'                      TO WS-AGE-TYPE.                    
           PERFORM 4500-DETERMINE-AGE-TYPE THRU 4500-EXIT.                      
                                                                                
           PERFORM 4300-PRINT-GRANT-TOTAL  THRU 4300-EXIT.                      
           PERFORM 6000-PRINT-SUMMARY      THRU 6000-EXIT.                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------                                          
       1000-START-UP.                                                           
      *-------------------------------                                          
                                                                                
           OPEN INPUT  BP13FD04                                                 
                       BP13KA10                                                 
                       SY02F001                                                 
               OUTPUT  BP13LC4A.                                                
                                                                                
           IF WS-KA10-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KA10 - FILE OPEN ERROR'                              
              MOVE WS-KA10-STATUS                TO RETURN-CODE                 
              PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------                                          
       2000-FD04-READ.                                                          
      *-------------------------------                                          
                                                                                
           READ BP13FD04     AT  END                                            
                MOVE 'Y'     TO  WS-FD04-EOF                                    
                GO           TO  2000-EXIT.                                     
                                                                                
           ADD 1             TO WS-FD04-READ.                                   
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------                                          
       2100-PROC-FD04.                                                          
      *-------------------------------                                          
                                                                                
           MOVE F001-DTE-LAST-DAY-PREV TO WS-DTE-CURRENT                        
                                                                                
           IF FD04-CDE-ACCT-STAT = 'X'                                          
              MOVE BP13FD04-REC(1401:8)   TO WS-DTE-REPUDIATE                   
                                                                                
              IF WS-DTE-REPUDIATE NOT NUMERIC                                   
                 MOVE ZEROES              TO WS-DTE-REPUDIATE                   
              END-IF                                                            
                                                                                
              COMPUTE WS-REPUD-MONTH = WS-DTE-REPUD-YYYY * 12 +                 
                                       WS-DTE-REPUD-MM                          
                                                                                
              COMPUTE WS-CURR-MONTH  = WS-DTE-CURR-YYYY * 12 +                  
                                       WS-DTE-CURR-MM                           
                                                                                
              COMPUTE FD04-NUM-MTH-INSTAL = WS-REPUD-MONTH -                    
                                            WS-CURR-MONTH                       
           END-IF.                                                              
                                                                                
           COMPUTE WS-NUM-AGE = FD04-NUM-MTH-INSTAL.                            
                                                                                
           IF WS-NUM-AGE = FD04-NUM-MTH-INSTAL                                  
              CONTINUE                                                          
           ELSE                                                                 
              ADD 1             TO WS-NUM-AGE                                   
           END-IF.                                                              
                                                                                
           COMPUTE WS-AMT-WTG-LOAN ROUNDED =                                    
                   FD04-AMT-LOAN-BAL * WS-NUM-AGE.                              
                                                                                
           IF WS-NUM-AGE < 3                                                    
              MOVE 'TYPE1'                             TO WS-AGE-TYPE           
              MOVE WS-AGE-AREA1                        TO WS-AGE-AREA           
           ELSE                                                                 
           IF WS-NUM-AGE < 6 AND WS-NUM-AGE >= 3                                
              MOVE 'TYPE2'                             TO WS-AGE-TYPE           
              MOVE WS-AGE-AREA2                        TO WS-AGE-AREA           
           ELSE                                                                 
           IF WS-NUM-AGE < 12 AND WS-NUM-AGE >= 6                               
              MOVE 'TYPE3'                             TO WS-AGE-TYPE           
              MOVE WS-AGE-AREA3                        TO WS-AGE-AREA           
           ELSE                                                                 
           IF WS-NUM-AGE < 24 AND WS-NUM-AGE >= 12                              
              MOVE 'TYPE4'                             TO WS-AGE-TYPE           
              MOVE WS-AGE-AREA4                        TO WS-AGE-AREA           
           ELSE                                                                 
           IF WS-NUM-AGE < 36 AND WS-NUM-AGE >= 24                              
              MOVE 'TYPE5'                             TO WS-AGE-TYPE           
              MOVE WS-AGE-AREA5                        TO WS-AGE-AREA           
           ELSE                                                                 
           IF WS-NUM-AGE < 72 AND WS-NUM-AGE >= 36                              
              MOVE 'TYPE6'                             TO WS-AGE-TYPE           
              MOVE WS-AGE-AREA6                        TO WS-AGE-AREA           
           ELSE                                                                 
              MOVE 'TYPE7'                             TO WS-AGE-TYPE           
              MOVE WS-AGE-AREA7                        TO WS-AGE-AREA           
           END-IF.                                                              
                                                                                
           COMPUTE WS-AMT-TOT-OS  = WS-AMT-TOT-OS + FD04-AMT-LOAN-BAL           
           COMPUTE WS-AMT-TOT-WTG = WS-AMT-TOT-WTG + WS-AMT-WTG-LOAN            
           IF WS-AMT-TOT-OS > 0                                                 
             ADD 1 TO WS-CNT-TOT.                                               
                                                                                
           IF WS-NUM-AGE < 3                                                    
             COMPUTE WS-AMT-3MTH-OS = WS-AMT-3MTH-OS + FD04-AMT-LOAN-BAL        
             COMPUTE WS-AMT-3MTH-WTG = WS-AMT-3MTH-WTG + WS-AMT-WTG-LOAN        
             IF WS-AMT-3MTH-OS > 0                                              
                ADD 1 TO WS-CNT-3MTH                                            
             END-IF                                                             
           ELSE                                                                 
           IF WS-NUM-AGE < 6 AND WS-NUM-AGE >= 3                                
             COMPUTE WS-AMT-6MTH-OS = WS-AMT-6MTH-OS + FD04-AMT-LOAN-BAL        
             COMPUTE WS-AMT-6MTH-WTG = WS-AMT-6MTH-WTG + WS-AMT-WTG-LOAN        
             IF WS-AMT-6MTH-OS > 0                                              
                ADD 1 TO WS-CNT-6MTH                                            
             END-IF                                                             
           ELSE                                                                 
           IF WS-NUM-AGE < 12 AND WS-NUM-AGE >= 6                               
             COMPUTE WS-AMT-1YR-OS = WS-AMT-1YR-OS + FD04-AMT-LOAN-BAL          
             COMPUTE WS-AMT-1YR-WTG = WS-AMT-1YR-WTG + WS-AMT-WTG-LOAN          
             IF WS-AMT-1YR-OS > 0                                               
                ADD 1 TO WS-CNT-1YEAR                                           
             END-IF                                                             
           ELSE                                                                 
           IF WS-NUM-AGE < 24 AND WS-NUM-AGE >= 12                              
             COMPUTE WS-AMT-2YR-OS = WS-AMT-2YR-OS + FD04-AMT-LOAN-BAL          
             COMPUTE WS-AMT-2YR-WTG = WS-AMT-2YR-WTG + WS-AMT-WTG-LOAN          
             IF WS-AMT-2YR-OS > 0                                               
                ADD 1 TO WS-CNT-2YEAR                                           
             END-IF                                                             
           ELSE                                                                 
           IF WS-NUM-AGE < 36 AND WS-NUM-AGE >= 24                              
             COMPUTE WS-AMT-3YR-OS = WS-AMT-3YR-OS + FD04-AMT-LOAN-BAL          
             COMPUTE WS-AMT-3YR-WTG = WS-AMT-3YR-WTG + WS-AMT-WTG-LOAN          
             IF WS-AMT-3YR-OS > 0                                               
                ADD 1 TO WS-CNT-3YEAR                                           
             END-IF                                                             
           ELSE                                                                 
           IF WS-NUM-AGE < 72 AND WS-NUM-AGE >= 36                              
             COMPUTE WS-AMT-6YR-OS = WS-AMT-6YR-OS + FD04-AMT-LOAN-BAL          
             COMPUTE WS-AMT-6YR-WTG = WS-AMT-6YR-WTG + WS-AMT-WTG-LOAN          
             IF WS-AMT-6YR-OS > 0                                               
                ADD 1 TO WS-CNT-6YEAR                                           
             END-IF                                                             
           ELSE                                                                 
             COMPUTE WS-AMT-7YR-OS = WS-AMT-7YR-OS + FD04-AMT-LOAN-BAL          
             COMPUTE WS-AMT-7YR-WTG = WS-AMT-7YR-WTG + WS-AMT-WTG-LOAN          
             IF WS-AMT-7YR-OS > 0                                               
                ADD 1 TO WS-CNT-7YEAR                                           
             END-IF                                                             
           END-IF.                                                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------                                          
       3000-PROCESS-RECORDS.                                                    
      *-------------------------------                                          
                                                                                
           PERFORM 3100-INITIALIZE-AMOUNTS     THRU 3100-EXIT.                  
                                                                                
           IF WS-AGE-TYPE = WS-PRE-TYPE                                         
              PERFORM 4000-PRINT-DETAILS       THRU 4000-EXIT                   
           ELSE                                                                 
              IF WS-PRE-TYPE NOT = SPACES                                       
                 PERFORM 4200-PRINT-SUBTOTAL   THRU 4200-EXIT                   
              END-IF                                                            
              PERFORM 4500-DETERMINE-AGE-TYPE  THRU 4500-EXIT                   
              MOVE WS-AGE-TYPE                   TO WS-PRE-TYPE                 
              MOVE WS-AGE-AREA                   TO LC4A-AGE-AREA               
              PERFORM 4100-PRINT-AGE-AREA      THRU 4100-EXIT                   
              PERFORM 4000-PRINT-DETAILS       THRU 4000-EXIT                   
           END-IF.                                                              
                                                                                
           PERFORM 2000-FD04-READ              THRU 2000-EXIT.                  
           IF WS-FD04-EOF NOT = 'Y'                                             
              PERFORM 2100-PROC-FD04           THRU 2100-EXIT                   
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------                                          
       3100-INITIALIZE-AMOUNTS.                                                 
      *-------------------------------                                          
                                                                                
           IF FD04-AMT-ARR-INSTAL NOT NUMERIC                                   
              MOVE 0 TO FD04-AMT-ARR-INSTAL                                     
           END-IF.                                                              
                                                                                
           IF FD04-AMT-INSTAL NOT NUMERIC                                       
              MOVE 0 TO FD04-AMT-INSTAL                                         
           END-IF.                                                              
                                                                                
           IF FD04-AMT-LOAN-BAL NOT NUMERIC                                     
              MOVE 0 TO FD04-AMT-LOAN-BAL                                       
           END-IF.                                                              
                                                                                
           IF FD04-NUM-MTH-INSTAL NOT NUMERIC                                   
              MOVE 0 TO FD04-NUM-MTH-INSTAL                                     
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------                                          
       4000-PRINT-DETAILS.                                                      
      *-------------------------------                                          
                                                                                
           ADD 1                             TO WS-LINE-CTR                     
           IF WS-LINE-CTR > 60                                                  
              PERFORM 5000-PRINT-HEADER THRU 5000-EXIT                          
              ADD 1                      TO WS-LINE-CTR                         
           END-IF.                                                              
                                                                                
           ADD 1                             TO WS-SNO                          
           MOVE WS-SNO                       TO LC4A-DTL-SERIAL.                
           MOVE FD04-NUM-REGN                TO LC4A-REGNO.                     
           MOVE FD04-CDE-ACCT-STAT           TO LC4A-ACCT-STAT.                 
                                                                                
           PERFORM 4400-RETRIEVE-PRJCT     THRU 4400-EXIT                       
                                                                                
           MOVE FD04-AMT-LOAN-BAL            TO LC4A-AMT-LOAN-BAL.              
           MOVE WS-NUM-AGE                   TO LC4A-MO-ARREAR.                 
           MOVE FD04-AMT-ARR-INSTAL          TO LC4A-AMT-ARREAR.                
           MOVE FD04-AMT-INSTAL              TO LC4A-AMT-INSTAL.                
           MOVE WS-AMT-WTG-LOAN              TO LC4A-AMT-WTG-LOAN.              
                                                                                
           STRING FD04-NUM-SCH-ACCNT(01:4) '-'                                  
                  FD04-NUM-SCH-ACCNT(05:4) '-'                                  
                  FD04-NUM-SCH-ACCNT(09:1) '-'                                  
                  FD04-NUM-SCH-ACCNT(10:2)                                      
                  DELIMITED BY SIZE        INTO LC4A-SCH-ACC                    
           END-STRING.                                                          
                                                                                
           COMPUTE WS-SUB-LOAN-BAL =                                            
                   WS-SUB-LOAN-BAL + FD04-AMT-LOAN-BAL                          
           COMPUTE WS-SUB-WTG-LOAN =                                            
                   WS-SUB-WTG-LOAN + WS-AMT-WTG-LOAN                            
           COMPUTE WS-SUB-ARREARS  =                                            
                   WS-SUB-ARREARS  + FD04-AMT-ARR-INSTAL                        
           COMPUTE WS-GRT-LOAN-BAL =                                            
                   WS-GRT-LOAN-BAL + FD04-AMT-LOAN-BAL                          
           COMPUTE WS-GRT-WTG-LOAN =                                            
                   WS-GRT-WTG-LOAN + WS-AMT-WTG-LOAN.                           
           COMPUTE WS-GRT-ARREARS  =                                            
                   WS-GRT-ARREARS  + FD04-AMT-ARR-INSTAL                        
                                                                                
           WRITE BP13LC4A-REC FROM BP13LC4A-DETAILS.                            
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       4100-PRINT-AGE-AREA.                                                     
      *--------------------------------------------------------------           
                                                                                
           IF WS-NO-REC = 'Y'                                                   
              ADD 5                      TO WS-LINE-CTR                         
              IF WS-LINE-CTR > 60                                               
                 PERFORM 5000-PRINT-HEADER THRU 5000-EXIT                       
                 ADD 5                   TO WS-LINE-CTR                         
              END-IF                                                            
           ELSE                                                                 
              ADD 3                      TO WS-LINE-CTR                         
              IF WS-LINE-CTR > 60                                               
                 PERFORM 5000-PRINT-HEADER THRU 5000-EXIT                       
                 ADD 3                   TO WS-LINE-CTR                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE ZEROES                   TO WS-SUB-LOAN-BAL                     
                                            WS-SUB-WTG-LOAN                     
                                            WS-SUB-ARREARS.                     
                                                                                
           MOVE SPACES                   TO BP13LC4A-REC.                       
           WRITE BP13LC4A-REC.                                                  
                                                                                
           WRITE BP13LC4A-REC FROM BP13LC4A-AGE.                                
                                                                                
           MOVE SPACES                   TO BP13LC4A-REC.                       
           WRITE BP13LC4A-REC.                                                  
                                                                                
           IF WS-NO-REC = 'Y'                                                   
              WRITE BP13LC4A-REC FROM BP13LC4A-NO-REC                           
              MOVE SPACES                TO BP13LC4A-REC                        
              WRITE BP13LC4A-REC                                                
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       4200-PRINT-SUBTOTAL.                                                     
      *--------------------------------------------------------------           
                                                                                
           IF WS-SUB-LOAN-BAL > 0 OR                                            
              WS-SUB-WTG-LOAN > 0 OR                                            
              WS-SUB-ARREARS  > 0                                               
              CONTINUE                                                          
           ELSE                                                                 
              GO TO 4200-EXIT                                                   
           END-IF.                                                              
                                                                                
           ADD 4                         TO WS-LINE-CTR.                        
           IF WS-LINE-CTR > 60                                                  
              PERFORM 5000-PRINT-HEADER THRU 5000-EXIT                          
              ADD 4                      TO WS-LINE-CTR                         
           END-IF.                                                              
                                                                                
           WRITE BP13LC4A-REC FROM BP13LC4A-TOT-DASH AFTER 2.                   
                                                                                
           MOVE 'SUB-TOTAL :       '     TO LC4A-TOT-TYPE.                      
           MOVE WS-SUB-LOAN-BAL          TO LC4A-TOT-LOAN-BAL                   
           MOVE WS-SUB-WTG-LOAN          TO LC4A-TOT-WTG-LOAN                   
           MOVE WS-SUB-ARREARS           TO LC4A-TOT-ARREAR                     
           WRITE BP13LC4A-REC FROM BP13LC4A-TOTAL.                              
                                                                                
           WRITE BP13LC4A-REC FROM BP13LC4A-TOT-DASH.                           
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       4300-PRINT-GRANT-TOTAL.                                                  
      *--------------------------------------------------------------           
                                                                                
           IF WS-GRT-LOAN-BAL > 0 OR                                            
              WS-GRT-WTG-LOAN > 0 OR                                            
              WS-GRT-ARREARS  > 0                                               
              CONTINUE                                                          
           ELSE                                                                 
              GO TO 4300-EXIT                                                   
           END-IF.                                                              
                                                                                
           ADD 3                         TO WS-LINE-CTR.                        
           IF WS-LINE-CTR > 60                                                  
              PERFORM 5000-PRINT-HEADER THRU 5000-EXIT                          
              ADD 3                      TO WS-LINE-CTR                         
           END-IF.                                                              
                                                                                
           WRITE BP13LC4A-REC FROM BP13LC4A-TOT-DASH.                           
                                                                                
           MOVE 'GRANT TOTAL :     '     TO LC4A-TOT-TYPE.                      
           MOVE WS-GRT-LOAN-BAL          TO LC4A-TOT-LOAN-BAL                   
           MOVE WS-GRT-WTG-LOAN          TO LC4A-TOT-WTG-LOAN                   
           MOVE WS-GRT-ARREARS           TO LC4A-TOT-ARREAR                     
           WRITE BP13LC4A-REC FROM BP13LC4A-TOTAL.                              
                                                                                
           WRITE BP13LC4A-REC FROM BP13LC4A-TOT-DASH.                           
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------           
       4400-RETRIEVE-PRJCT.                                                     
      *--------------------------------------------------------------           
                                                                                
           MOVE SPACES                   TO LC4A-NME-PRJCT.                     
           MOVE FD04-NUM-PROJECT         TO KA10-KEY-FLD.                       
                                                                                
           READ BP13KA10.                                                       
                                                                                
           IF WS-KA10-STATUS = 00                                               
              MOVE KA10-NME-PRJCT        TO LC4A-NME-PRJCT                      
           ELSE                                                                 
              MOVE 'PROJECT NOT FOUND'   TO LC4A-NME-PRJCT                      
              IF WS-KA10-STATUS = 23                                            
                 DISPLAY 'BP13KA10 - REC NOT FND '                              
                         ',KEY IS '  KA10-KEY-FLD                               
              ELSE                                                              
                 DISPLAY 'BP13KA10 - ERROR ON READ ' WS-KA10-STATUS             
                         ',KEY IS '  KA10-KEY-FLD                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------                                          
       4500-DETERMINE-AGE-TYPE.                                                 
      *-------------------------------                                          
                                                                                
           EVALUATE WS-PRE-TYPE ALSO WS-AGE-TYPE                                
              WHEN  '     ' ALSO 'TYPE2'                                        
                    PERFORM 4510-PRINT-AGE-TYPE1   THRU 4510-EXIT               
              WHEN  '     ' ALSO 'TYPE3'                                        
                    PERFORM 4510-PRINT-AGE-TYPE1   THRU 4510-EXIT               
                    PERFORM 4520-PRINT-AGE-TYPE2   THRU 4520-EXIT               
              WHEN  '     ' ALSO 'TYPE4'                                        
                    PERFORM 4510-PRINT-AGE-TYPE1   THRU 4510-EXIT               
                    PERFORM 4520-PRINT-AGE-TYPE2   THRU 4520-EXIT               
                    PERFORM 4530-PRINT-AGE-TYPE3   THRU 4530-EXIT               
              WHEN  '     ' ALSO 'TYPE5'                                        
                    PERFORM 4510-PRINT-AGE-TYPE1   THRU 4510-EXIT               
                    PERFORM 4520-PRINT-AGE-TYPE2   THRU 4520-EXIT               
                    PERFORM 4530-PRINT-AGE-TYPE3   THRU 4530-EXIT               
                    PERFORM 4540-PRINT-AGE-TYPE4   THRU 4540-EXIT               
              WHEN  '     ' ALSO 'TYPE6'                                        
                    PERFORM 4510-PRINT-AGE-TYPE1   THRU 4510-EXIT               
                    PERFORM 4520-PRINT-AGE-TYPE2   THRU 4520-EXIT               
                    PERFORM 4530-PRINT-AGE-TYPE3   THRU 4530-EXIT               
                    PERFORM 4540-PRINT-AGE-TYPE4   THRU 4540-EXIT               
                    PERFORM 4550-PRINT-AGE-TYPE5   THRU 4550-EXIT               
              WHEN  '     ' ALSO 'TYPE7'                                        
                    PERFORM 4510-PRINT-AGE-TYPE1   THRU 4510-EXIT               
                    PERFORM 4520-PRINT-AGE-TYPE2   THRU 4520-EXIT               
                    PERFORM 4530-PRINT-AGE-TYPE3   THRU 4530-EXIT               
                    PERFORM 4540-PRINT-AGE-TYPE4   THRU 4540-EXIT               
                    PERFORM 4550-PRINT-AGE-TYPE5   THRU 4550-EXIT               
                    PERFORM 4560-PRINT-AGE-TYPE6   THRU 4560-EXIT               
              WHEN  '     ' ALSO 'TYPE8'                                        
                    PERFORM 4510-PRINT-AGE-TYPE1   THRU 4510-EXIT               
                    PERFORM 4520-PRINT-AGE-TYPE2   THRU 4520-EXIT               
                    PERFORM 4530-PRINT-AGE-TYPE3   THRU 4530-EXIT               
                    PERFORM 4540-PRINT-AGE-TYPE4   THRU 4540-EXIT               
                    PERFORM 4550-PRINT-AGE-TYPE5   THRU 4550-EXIT               
                    PERFORM 4560-PRINT-AGE-TYPE6   THRU 4560-EXIT               
                    PERFORM 4570-PRINT-AGE-TYPE7   THRU 4570-EXIT               
              WHEN  'TYPE1' ALSO 'TYPE3'                                        
                    PERFORM 4520-PRINT-AGE-TYPE2   THRU 4520-EXIT               
              WHEN  'TYPE1' ALSO 'TYPE4'                                        
                    PERFORM 4520-PRINT-AGE-TYPE2   THRU 4520-EXIT               
                    PERFORM 4530-PRINT-AGE-TYPE3   THRU 4530-EXIT               
              WHEN  'TYPE1' ALSO 'TYPE5'                                        
                    PERFORM 4520-PRINT-AGE-TYPE2   THRU 4520-EXIT               
                    PERFORM 4530-PRINT-AGE-TYPE3   THRU 4530-EXIT               
                    PERFORM 4540-PRINT-AGE-TYPE4   THRU 4540-EXIT               
              WHEN  'TYPE1' ALSO 'TYPE6'                                        
                    PERFORM 4520-PRINT-AGE-TYPE2   THRU 4520-EXIT               
                    PERFORM 4530-PRINT-AGE-TYPE3   THRU 4530-EXIT               
                    PERFORM 4540-PRINT-AGE-TYPE4   THRU 4540-EXIT               
                    PERFORM 4550-PRINT-AGE-TYPE5   THRU 4550-EXIT               
              WHEN  'TYPE1' ALSO 'TYPE7'                                        
                    PERFORM 4520-PRINT-AGE-TYPE2   THRU 4520-EXIT               
                    PERFORM 4530-PRINT-AGE-TYPE3   THRU 4530-EXIT               
                    PERFORM 4540-PRINT-AGE-TYPE4   THRU 4540-EXIT               
                    PERFORM 4550-PRINT-AGE-TYPE5   THRU 4550-EXIT               
                    PERFORM 4560-PRINT-AGE-TYPE6   THRU 4560-EXIT               
              WHEN  'TYPE1' ALSO 'TYPE8'                                        
                    PERFORM 4520-PRINT-AGE-TYPE2   THRU 4520-EXIT               
                    PERFORM 4530-PRINT-AGE-TYPE3   THRU 4530-EXIT               
                    PERFORM 4540-PRINT-AGE-TYPE4   THRU 4540-EXIT               
                    PERFORM 4550-PRINT-AGE-TYPE5   THRU 4550-EXIT               
                    PERFORM 4560-PRINT-AGE-TYPE6   THRU 4560-EXIT               
                    PERFORM 4570-PRINT-AGE-TYPE7   THRU 4570-EXIT               
              WHEN  'TYPE2' ALSO 'TYPE4'                                        
                    PERFORM 4530-PRINT-AGE-TYPE3   THRU 4530-EXIT               
              WHEN  'TYPE2' ALSO 'TYPE5'                                        
                    PERFORM 4530-PRINT-AGE-TYPE3   THRU 4530-EXIT               
                    PERFORM 4540-PRINT-AGE-TYPE4   THRU 4540-EXIT               
              WHEN  'TYPE2' ALSO 'TYPE6'                                        
                    PERFORM 4530-PRINT-AGE-TYPE3   THRU 4530-EXIT               
                    PERFORM 4540-PRINT-AGE-TYPE4   THRU 4540-EXIT               
                    PERFORM 4550-PRINT-AGE-TYPE5   THRU 4550-EXIT               
              WHEN  'TYPE2' ALSO 'TYPE7'                                        
                    PERFORM 4530-PRINT-AGE-TYPE3   THRU 4530-EXIT               
                    PERFORM 4540-PRINT-AGE-TYPE4   THRU 4540-EXIT               
                    PERFORM 4550-PRINT-AGE-TYPE5   THRU 4550-EXIT               
                    PERFORM 4560-PRINT-AGE-TYPE6   THRU 4560-EXIT               
              WHEN  'TYPE2' ALSO 'TYPE8'                                        
                    PERFORM 4530-PRINT-AGE-TYPE3   THRU 4530-EXIT               
                    PERFORM 4540-PRINT-AGE-TYPE4   THRU 4540-EXIT               
                    PERFORM 4550-PRINT-AGE-TYPE5   THRU 4550-EXIT               
                    PERFORM 4560-PRINT-AGE-TYPE6   THRU 4560-EXIT               
                    PERFORM 4570-PRINT-AGE-TYPE7   THRU 4570-EXIT               
              WHEN  'TYPE3' ALSO 'TYPE5'                                        
                    PERFORM 4540-PRINT-AGE-TYPE4   THRU 4540-EXIT               
              WHEN  'TYPE3' ALSO 'TYPE6'                                        
                    PERFORM 4540-PRINT-AGE-TYPE4   THRU 4540-EXIT               
                    PERFORM 4550-PRINT-AGE-TYPE5   THRU 4550-EXIT               
              WHEN  'TYPE3' ALSO 'TYPE7'                                        
                    PERFORM 4540-PRINT-AGE-TYPE4   THRU 4540-EXIT               
                    PERFORM 4550-PRINT-AGE-TYPE5   THRU 4550-EXIT               
                    PERFORM 4560-PRINT-AGE-TYPE6   THRU 4560-EXIT               
              WHEN  'TYPE3' ALSO 'TYPE8'                                        
                    PERFORM 4540-PRINT-AGE-TYPE4   THRU 4540-EXIT               
                    PERFORM 4550-PRINT-AGE-TYPE5   THRU 4550-EXIT               
                    PERFORM 4560-PRINT-AGE-TYPE6   THRU 4560-EXIT               
                    PERFORM 4570-PRINT-AGE-TYPE7   THRU 4570-EXIT               
              WHEN  'TYPE4' ALSO 'TYPE6'                                        
                    PERFORM 4550-PRINT-AGE-TYPE5   THRU 4550-EXIT               
              WHEN  'TYPE4' ALSO 'TYPE7'                                        
                    PERFORM 4550-PRINT-AGE-TYPE5   THRU 4550-EXIT               
                    PERFORM 4560-PRINT-AGE-TYPE6   THRU 4560-EXIT               
              WHEN  'TYPE4' ALSO 'TYPE8'                                        
                    PERFORM 4550-PRINT-AGE-TYPE5   THRU 4550-EXIT               
                    PERFORM 4560-PRINT-AGE-TYPE6   THRU 4560-EXIT               
                    PERFORM 4570-PRINT-AGE-TYPE7   THRU 4570-EXIT               
              WHEN  'TYPE5' ALSO 'TYPE7'                                        
                    PERFORM 4560-PRINT-AGE-TYPE6   THRU 4560-EXIT               
              WHEN  'TYPE5' ALSO 'TYPE8'                                        
                    PERFORM 4560-PRINT-AGE-TYPE6   THRU 4560-EXIT               
                    PERFORM 4570-PRINT-AGE-TYPE7   THRU 4570-EXIT               
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------                                          
       4510-PRINT-AGE-TYPE1.                                                    
      *-------------------------------                                          
                                                                                
           MOVE WS-AGE-AREA1               TO LC4A-AGE-AREA                     
           MOVE 'Y'                        TO WS-NO-REC.                        
           PERFORM 4100-PRINT-AGE-AREA   THRU 4100-EXIT.                        
           MOVE 'N'                        TO WS-NO-REC.                        
                                                                                
       4510-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------                                          
       4520-PRINT-AGE-TYPE2.                                                    
      *-------------------------------                                          
                                                                                
           MOVE WS-AGE-AREA2               TO LC4A-AGE-AREA                     
           MOVE 'Y'                        TO WS-NO-REC.                        
           PERFORM 4100-PRINT-AGE-AREA   THRU 4100-EXIT.                        
           MOVE 'N'                        TO WS-NO-REC.                        
                                                                                
       4520-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------                                          
       4530-PRINT-AGE-TYPE3.                                                    
      *-------------------------------                                          
                                                                                
           MOVE WS-AGE-AREA3               TO LC4A-AGE-AREA                     
           MOVE 'Y'                        TO WS-NO-REC.                        
           PERFORM 4100-PRINT-AGE-AREA   THRU 4100-EXIT.                        
           MOVE 'N'                        TO WS-NO-REC.                        
                                                                                
       4530-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------                                          
       4540-PRINT-AGE-TYPE4.                                                    
      *-------------------------------                                          
                                                                                
           MOVE WS-AGE-AREA4               TO LC4A-AGE-AREA                     
           MOVE 'Y'                        TO WS-NO-REC.                        
           PERFORM 4100-PRINT-AGE-AREA   THRU 4100-EXIT.                        
           MOVE 'N'                        TO WS-NO-REC.                        
                                                                                
       4540-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------                                          
       4550-PRINT-AGE-TYPE5.                                                    
      *-------------------------------                                          
                                                                                
           MOVE WS-AGE-AREA5               TO LC4A-AGE-AREA                     
           MOVE 'Y'                        TO WS-NO-REC.                        
           PERFORM 4100-PRINT-AGE-AREA   THRU 4100-EXIT.                        
           MOVE 'N'                        TO WS-NO-REC.                        
                                                                                
       4550-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------                                          
       4560-PRINT-AGE-TYPE6.                                                    
      *-------------------------------                                          
                                                                                
           MOVE WS-AGE-AREA6               TO LC4A-AGE-AREA                     
           MOVE 'Y'                        TO WS-NO-REC.                        
           PERFORM 4100-PRINT-AGE-AREA   THRU 4100-EXIT.                        
           MOVE 'N'                        TO WS-NO-REC.                        
                                                                                
       4560-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------                                          
       4570-PRINT-AGE-TYPE7.                                                    
      *-------------------------------                                          
                                                                                
           MOVE WS-AGE-AREA7               TO LC4A-AGE-AREA                     
           MOVE 'Y'                        TO WS-NO-REC.                        
           PERFORM 4100-PRINT-AGE-AREA   THRU 4100-EXIT.                        
           MOVE 'N'                        TO WS-NO-REC.                        
                                                                                
       4570-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------                                          
       5000-PRINT-HEADER.                                                       
      *-------------------------------                                          
                                                                                
           ADD 1                           TO WS-PAGE-CTR.                      
           MOVE WS-PAGE-CTR                TO LC4A-PAGE-HDR.                    
                                                                                
           STRING F001-DTE-CURRENT(7:2) '/'                                     
                  F001-DTE-CURRENT(5:2) '/'                                     
                  F001-DTE-CURRENT(1:4) '/'                                     
                  DELIMITED BY SIZE      INTO LC4A-DATE-HDR                     
           END-STRING.                                                          
                                                                                
           STRING F001-LST-PREV-DD ' '                                          
                  WS-MONTH(F001-LST-PREV-MM) ' '                                
                  F001-LST-PREV-CC                                              
                  F001-LST-PREV-YY                                              
                  DELIMITED BY SIZE      INTO LC4A-DTE                          
           END-STRING.                                                          
           MOVE LC4A-DTE                   TO LC4A-DTEX                         
                                                                                
           MOVE SPACES                     TO BP13LC4A-REC                      
           WRITE BP13LC4A-REC AFTER PAGE.                                       
           WRITE BP13LC4A-REC FROM HEADING-1 AFTER 3.                           
           IF FD04-CDE-ACCT-STAT = 'X'                                          
              WRITE BP13LC4A-REC FROM HEADING-2X                                
           ELSE                                                                 
              WRITE BP13LC4A-REC FROM HEADING-2                                 
           END-IF.                                                              
           WRITE BP13LC4A-REC FROM BP13LC4A-HEADER-1 AFTER 2.                   
           WRITE BP13LC4A-REC FROM BP13LC4A-HEADER-2.                           
           WRITE BP13LC4A-REC FROM BP13LC4A-HEADER2.                            
                                                                                
           MOVE 9                          TO WS-LINE-CTR.                      
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------                                          
       6000-PRINT-SUMMARY.                                                      
      *-------------------------------                                          
                                                                                
           MOVE WS-CNT-3MTH            TO FIL5-CNT-CASES                        
           MOVE WS-CNT-6MTH            TO FIL6-CNT-CASES                        
           MOVE WS-CNT-1YEAR           TO FIL7-CNT-CASES                        
           MOVE WS-CNT-2YEAR           TO FIL8-CNT-CASES                        
           MOVE WS-CNT-3YEAR           TO FIL9-CNT-CASES                        
           MOVE WS-CNT-6YEAR           TO FIL10-CNT-CASES                       
           MOVE WS-CNT-7YEAR           TO FIL11-CNT-CASES                       
           MOVE WS-CNT-TOT             TO FIL12-CNT-CASES                       
           MOVE WS-AMT-3MTH-OS         TO FIL5-OS-LOAN                          
           MOVE WS-AMT-6MTH-OS         TO FIL6-OS-LOAN                          
           MOVE WS-AMT-1YR-OS          TO FIL7-OS-LOAN                          
           MOVE WS-AMT-2YR-OS          TO FIL8-OS-LOAN                          
           MOVE WS-AMT-3YR-OS          TO FIL9-OS-LOAN                          
           MOVE WS-AMT-6YR-OS          TO FIL10-OS-LOAN                         
           MOVE WS-AMT-7YR-OS          TO FIL11-OS-LOAN                         
           MOVE WS-AMT-TOT-OS          TO FIL12-OS-LOAN                         
           MOVE WS-AMT-3MTH-WTG        TO FIL5-WTG-LOAN                         
           MOVE WS-AMT-6MTH-WTG        TO FIL6-WTG-LOAN                         
           MOVE WS-AMT-1YR-WTG         TO FIL7-WTG-LOAN                         
           MOVE WS-AMT-2YR-WTG         TO FIL8-WTG-LOAN                         
           MOVE WS-AMT-3YR-WTG         TO FIL9-WTG-LOAN                         
           MOVE WS-AMT-6YR-WTG         TO FIL10-WTG-LOAN                        
           MOVE WS-AMT-7YR-WTG         TO FIL11-WTG-LOAN                        
           MOVE WS-AMT-TOT-WTG         TO FIL12-WTG-LOAN                        
                                                                                
           ADD 1                       TO WS-PAGE-CTR                           
           MOVE  WS-PAGE-CTR           TO LC4A-PAGE-HDR                         
                                                                                
           MOVE SPACES                 TO BP13LC4A-REC                          
           WRITE BP13LC4A-REC AFTER PAGE.                                       
           WRITE BP13LC4A-REC FROM HEADING-1 AFTER 3.                           
           IF FD04-CDE-ACCT-STAT = 'X'                                          
              WRITE BP13LC4A-REC FROM HEADING-2X                                
           ELSE                                                                 
              WRITE BP13LC4A-REC FROM HEADING-2                                 
           END-IF.                                                              
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL1 AFTER 2.                       
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL2.                               
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL1.                               
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL3.                               
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL1.                               
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL4.                               
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL1.                               
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL5.                               
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL1.                               
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL6.                               
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL1.                               
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL7.                               
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL1.                               
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL8.                               
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL1.                               
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL9.                               
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL1.                               
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL10.                              
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL1.                               
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL11.                              
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL1.                               
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL12.                              
           WRITE BP13LC4A-REC FROM P13LC4AB-FIL1.                               
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------                                          
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------                                          
                                                                                
           CLOSE BP13FD04                                                       
                 BP13KA10                                                       
                 SY02F001                                                       
                 BP13LC4A.                                                      
                                                                                
           DISPLAY ' *------ BP13CC4A AUDIT TRAIL ------*'.                     
           DISPLAY '   RUN DATE : ' F001-DTE-CURRENT(7:2) '/'                   
                                    F001-DTE-CURRENT(5:2) '/'                   
                                    F001-DTE-CURRENT(1:4).                      
           DISPLAY '    '.                                                      
           DISPLAY 'NO. OF RECORDS READ FROM DAILY TRANS (BP13FD04) = '         
                   WS-FD04-READ.                                                
           DISPLAY 'NO. OF RECORDS WRITTEN TO            (BP13LC4A) = '         
                   WS-LC4A-WRITE.                                               
           DISPLAY ' *----------------------------------*'.                     
                                                                                
           IF WS-KA10-STATUS NOT = 00                                           
              DISPLAY 'BP13KA10 - ERROR CLOSING : ' WS-KA10-STATUS              
              MOVE WS-KA10-STATUS                TO RETURN-CODE                 
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
