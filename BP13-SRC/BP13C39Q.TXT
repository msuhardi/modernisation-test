       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C39Q.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  20/10/2016.                                               
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SOC                                   *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO UPDATE BP13K640                    *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO    BY     DATE      DESCRIPTION                 *                
      * ------    --     ----      -----------                 *                
      * BP136488  PCL4  20161020   NEW PROGRAM                 *                
      * BP136488  PCL4  20161028   TO UPDATE AMT-REPAY-PERIOD, *                
      *                                      AMT-INTEREST-RATE *                
      * ====================================================== *                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F313 ASSIGN       TO BP13F313.                            
                                                                                
           SELECT BP13K640 ASSIGN       TO BP13K640                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K640-KEY-FLD                         
                           FILE STATUS  IS K640-STATUS.                         
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD  BP13K640                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K640.                                                           
                                                                                
       FD  BP13F313                                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       COPY BP13F313.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-CURR-DATE                   PIC X(8)  VALUE SPACES.               
                                                                                
       01  K640-STATUS                    PIC 9(2)  VALUE ZERO.                 
       01  WS-F313-READ                   PIC 9(7)  VALUE ZERO.                 
       01  WS-K640-UPD                    PIC 9(7)  VALUE ZERO.                 
       01  WS-K640-NFND                   PIC 9(7)  VALUE ZERO.                 
                                                                                
       01  WS-EOF-F313                    PIC X     VALUE SPACE.                
           88 EOF-F313                    VALUE 'Y'.                            
                                                                                
       PROCEDURE DIVISION.                                                      
      *---------------------------------------------------------------*         
       0000-CONTROL.                                                            
      *---------------------------------------------------------------*         
                                                                                
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-READ-BP13F313     THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS-BP13F313  THRU 3000-EXIT                        
             UNTIL EOF-F313.                                                    
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
                                                                                
           OPEN INPUT  BP13F313                                                 
                I-O    BP13K640.                                                
                                                                                
           IF K640-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPEN ERROR, K640-STATUS ' K640-STATUS                    
              MOVE K640-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F313.                                                      
      *---------------------------------------------------------------*         
                                                                                
           READ BP13F313 AT END                                                 
                MOVE 'Y' TO WS-EOF-F313                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F313-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2500-READ-BP13K640.                                                      
      *---------------------------------------------------------------*         
                                                                                
           MOVE SPACES                  TO BP13K640-TRANS-REC.                  
           INITIALIZE                      BP13K640-TRANS-REC.                  
                                                                                
                                                                                
           MOVE F313-NUM-REGN           TO K640-REGN-NO.                        
                                                                                
           READ BP13K640.                                                       
                                                                                
           EVALUATE K640-STATUS                                                 
               WHEN 00                                                          
                    PERFORM 3100-REWRITE-BP13K640     THRU 3100-EXIT            
                                                                                
               WHEN 23                                                          
                    MOVE SPACES         TO BP13K640-TRANS-REC                   
                    INITIALIZE             BP13K640-TRANS-REC                   
                                                                                
                    ADD 1               TO WS-K640-NFND                         
                                                                                
               WHEN OTHER                                                       
                  DISPLAY 'READ ERROR BP13K640 : ' K640-STATUS                  
                          ' KEY : ' K640-REGN-NO                                
                  PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT               
           END-EVALUATE.                                                        
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-BP13F313.                                                   
      *---------------------------------------------------------------*         
                                                                                
           IF (F313-NUM-LOAN-TAG  = 'N')                                        
               PERFORM 2500-READ-BP13K640        THRU 2500-EXIT                 
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F313            THRU 2000-EXIT.                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3100-REWRITE-BP13K640.                                                   
      *---------------------------------------------------------------*         
                                                                                
           IF K640-AMT-ELIG-INSTAL NOT NUMERIC                                  
              MOVE ZEROES                  TO K640-AMT-ELIG-INSTAL              
           END-IF.                                                              
                                                                                
           IF K640-AMT-MAX-LOAN NOT NUMERIC                                     
              MOVE ZEROES                  TO K640-AMT-MAX-LOAN                 
           END-IF.                                                              
                                                                                
           IF K640-AMT-INTEREST-RATE NOT NUMERIC                                
              MOVE ZEROES                  TO K640-AMT-INTEREST-RATE            
           END-IF.                                                              
                                                                                
           IF K640-AMT-REPAY-PERIOD  NOT NUMERIC                                
              MOVE ZEROES                  TO K640-AMT-REPAY-PERIOD             
           END-IF.                                                              
                                                                                
           MOVE K640-AMT-ELIG-INSTAL     TO K640-AMT-ELIG-INSTAL-OLD.           
           MOVE K640-AMT-MAX-LOAN        TO K640-AMT-MAX-LOAN-OLD.              
           MOVE K640-AMT-REPAY-PERIOD    TO K640-AMT-REPAY-PERIOD-OLD.          
           MOVE K640-AMT-INTEREST-RATE   TO K640-AMT-INTEREST-RATE-OLD.         
                                                                                
           MOVE ZEROES                   TO K640-AMT-ELIG-INSTAL                
                                            K640-AMT-MAX-LOAN                   
                                            K640-AMT-REPAY-PERIOD               
                                            K640-AMT-INTEREST-RATE.             
                                                                                
           REWRITE BP13K640-TRANS-REC.                                          
                                                                                
           EVALUATE K640-STATUS                                                 
               WHEN 00                                                          
                    ADD  1                 TO WS-K640-UPD                       
                                                                                
               WHEN OTHER                                                       
                  DISPLAY 'REWRITE ERROR BP13K640 : ' K640-STATUS               
                          ' KEY : ' F313-KEY-FLD                                
                  PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT               
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-FILES.                                                        
      *---------------------------------------------------------------*         
                                                                                
           CLOSE BP13F313                                                       
                 BP13K640.                                                      
                                                                                
           IF K640-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, K640-STATUS ' K640-STATUS                   
              MOVE K640-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C39Q ================*'.              
           DISPLAY '   NO. OF F313  RECORDS READ      = ' WS-F313-READ.         
           DISPLAY '   NO. OF K640  RECORDS NOT FOUND = ' WS-K640-NFND          
           DISPLAY '   NO. OF K640  RECORDS UPDATED   = ' WS-K640-UPD.          
           DISPLAY '*==========================================*'.              
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
