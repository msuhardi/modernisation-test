      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.   BP13C617.                                                  
      *AUTHOR.       RAYMOND H. BUNADO                                          
      *DATE-WRITTEN. 24/04/97.                                                  
                                                                                
      ***********************************************************               
      *  INPUT FILE :- BP13F410                                 *               
      *                BP13K800                                 *               
      *                BP13K820                                 *               
      *  I-O           BP13K665                                 *               
      *  OUTPUT FILE:- BP04K010                                 *               
      ***********************************************************               
      *                                                         *               
      * CHG NO   BY  DATE        REMARKS                        *               
      * ------   --- ----------  -----------------------------  *               
      * BP130287 RHB 24/04/1997  NEW PROGRAM.                   *               
      * BP130287 RHB 02/05/1997  TO CATER FOR OUTRIGHT PURCHASE.*               
      * BP130402 SIA 14/01/1998  TO CATER FOR EXPANSION OF      *               
      *                          LAST-DEBREF.                   *               
      * BP130418 RHB 06/06/1998  Y2K                            *               
      * BP130883 RAS 08/08/2000  TO CHANGE BP13C904 TO BP13C913 *               
      * BP135599 PCL 11/12/2014  TO UPDATE K010-INTERFACE-SYS   *               
      * BP138123 KR13 11/12/2019 TAG DEBAR Z1/Z2 IF NOT TAGGED  *               
      * BP138123 KR13 27/02/2020 CHK DEBAR HIST TO WRITE K010.  *               
      * BP138223 KR13 05/08/2020 RMVE CHK DBAR HIST TO WRIT K010.               
      * BP138223 KTM3 26/08/2020 ENHANCE Z1/Z2 CONDITIONS       .               
      ***********************************************************               
                                                                                
      *************************                                                 
       ENVIRONMENT DIVISION.                                                    
      *************************                                                 
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *************************                                                 
       INPUT-OUTPUT SECTION.                                                    
      *************************                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410 ASSIGN TO BP13F410.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
           SELECT BP13K820  ASSIGN TO BP13K820                                  
                  ORGANIZATION     IS INDEXED                                   
                  ACCESS MODE      IS DYNAMIC                                   
                  RECORD KEY       IS K820-KEY-FLD                              
                  FILE STATUS      IS WS-K820-STATUS.                           
                                                                                
           SELECT BP13K640  ASSIGN TO BP13K640                                  
                  ACCESS MODE      IS RANDOM                                    
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K640-KEY-FLD                              
                  FILE STATUS      IS K640-STATUS.                              
                                                                                
           SELECT BP13K665 ASSIGN TO BP13K665                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS WS-K665-KEY-FLD                            
                  FILE STATUS     IS WS-K665-STATUS.                            
                                                                                
           SELECT BP13K893  ASSIGN TO BP13K893                                  
                  ACCESS MODE      IS DYNAMIC                                   
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K893-KEY-FLD                              
                  FILE STATUS      IS WS-K893-STATUS.                           
                                                                                
           SELECT BP13K895  ASSIGN TO BP13K895                                  
                  ACCESS MODE      IS DYNAMIC                                   
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K895-KEY-FLD                              
                  ALTERNATE RECORD KEY IS K895-NUM-NRIC-HIST                    
                                          WITH DUPLICATES                       
                  FILE STATUS  IS WS-K895-STATUS.                               
                                                                                
           SELECT BP04K010 ASSIGN TO BP04K010                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K010-PRIME-KEY                             
                  FILE STATUS     IS WS-K010-STATUS.                            
                                                                                
           SELECT P04K010  ASSIGN TO P04K010                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS F010-PRIME-KEY                             
                  FILE STATUS     IS K010-STATUS.                               
                                                                                
           SELECT BP13L617 ASSIGN TO BP13L617.                                  
           SELECT P13L617B ASSIGN TO P13L617B.                                  
                                                                                
                                                                                
      *************************                                                 
       DATA DIVISION.                                                           
      *************************                                                 
       FILE SECTION.                                                            
                                                                                
       FD  BP13F410   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 2000 CHARACTERS                           
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13F410.                                                           
                                                                                
       FD  BP13K640                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K640.                                                           
                                                                                
       FD  BP13K665   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 50 CHARACTERS                             
                      LABEL RECORDS ARE STANDARD.                               
       01  WS-BP13K665-REC.                                                     
           05  WS-K665-KEY-FLD         PIC X(08).                               
           05  FILLER                  PIC X(42).                               
                                                                                
       FD  BP13K800   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 2000 CHARACTERS                           
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13K800.                                                           
                                                                                
       FD BP13K820    BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 400 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD   BP13K895                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K895.                                                           
                                                                                
       FD BP04K010    BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 300 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       COPY P04K010.                                                            
                                                                                
       FD P04K010    BLOCK CONTAINS 0 RECORDS                                   
                      RECORD CONTAINS 300 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       COPY P04F010.                                                            
                                                                                
       FD BP13L617    RECORD CONTAINS 133 CHARACTERS                            
                      RECORDING MODE IS F.                                      
       01 BP13L617-REC                   PIC X(133).                            
                                                                                
       FD P13L617B    RECORD CONTAINS 133 CHARACTERS                            
                      RECORDING MODE IS F.                                      
       01 P13L617B-REC                   PIC X(133).                            
                                                                                
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
      *----------------------*                                                  
      * LINKAGE FOR BP13C913 *                                                  
      *----------------------*                                                  
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH          PIC X(4).                           
               10  WS-LINK-NUM-ACC          PIC X(5).                           
                                                                                
        COPY P13COMM8.                                                          
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F410-EOF               PIC X    VALUE 'N'.                    
               88 F410-EOF                        VALUE 'Y'.                    
           05  WS-EOF-K010               PIC X    VALUE 'N'.                    
           05  WS-FOUND                  PIC X    VALUE SPACES.                 
           05  WS-K820-EOF               PIC X    VALUE SPACES.                 
           05  WS-K893-EOF               PIC X    VALUE SPACES.                 
           05  WS-K895-EOF               PIC X    VALUE SPACES.                 
           05  WS-K640-FND               PIC X    VALUE SPACES.                 
           05  WS-K800-FND               PIC X    VALUE SPACES.                 
           05  WS-K895-FND               PIC X    VALUE SPACES.                 
           05  WS-SPO-IC                 PIC X    VALUE SPACES.                 
           05  WS-K893-FND               PIC X    VALUE SPACES.                 
           05  WS-DEBARMENT-TAG          PIC X    VALUE SPACES.                 
           05  WS-DEBAR-TAG              PIC X    VALUE SPACES.                 
           05  WS-DEBAR-FOUND            PIC X    VALUE SPACES.                 
           05  WS-DEBAR-HIST             PIC X    VALUE SPACES.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-CNT-F410          PIC 9(6) VALUE ZEROS.                  
           05  WS-K665-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K665-WRITE             PIC 9(6) VALUE ZEROS.                  
           05  WS-K665-REWRITE           PIC 9(6) VALUE ZEROS.                  
           05  WS-K640-CNT               PIC 9(6) VALUE ZEROS.                  
           05  WS-K640-LOAN-P            PIC 9(6) VALUE ZEROS.                  
           05  WS-K800-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K800-NFND              PIC 9(6) VALUE ZEROS.                  
           05  WS-K820-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K820-NFND              PIC 9(6) VALUE ZEROS.                  
           05  WS-K893-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K893-NFND              PIC 9(6) VALUE ZEROS.                  
           05  WS-K895-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K895-NFND              PIC 9(6) VALUE ZEROS.                  
           05  K010-READ                 PIC 9(6) VALUE ZEROS.                  
           05  WS-K010-WRITE             PIC 9(6) VALUE ZEROS.                  
           05  WS-LINE-CTR               PIC 9(6) VALUE 60.                     
           05  WS-LINE-CTR1              PIC 9(6) VALUE 60.                     
           05  WS-PAGE-CTR               PIC 9(6) VALUE ZEROS.                  
           05  WS-PAGE-CTR1              PIC 9(6) VALUE ZEROS.                  
           05  WS-SEQ-NO                 PIC 9(6) VALUE ZEROS.                  
           05  WS-SEQ-NO1                PIC 9(6) VALUE ZEROS.                  
           05  WS-NO-LOAN                PIC 9(6) VALUE ZEROS.                  
           05  WS-WITH-LOAN              PIC 9(6) VALUE ZEROS.                  
           05  WS-Z1-CNT                 PIC 9(6) VALUE ZEROS.                  
           05  WS-Z2-CNT                 PIC 9(6) VALUE ZEROS.                  
           05  WS-Z1-O-CNT               PIC 9(6) VALUE ZEROS.                  
           05  WS-Z2-O-CNT               PIC 9(6) VALUE ZEROS.                  
           05  WS-SPACE-O-CNT            PIC 9(6) VALUE ZEROS.                  
           05  WS-OTHER-O-CNT            PIC 9(6) VALUE ZEROS.                  
           05  WS-DEB-FND-CNT            PIC 9(6) VALUE ZEROS.                  
           05  WS-DEB-NFND-CNT           PIC 9(6) VALUE ZEROS.                  
           05  WS-Z2SP-CNT               PIC 9(6) VALUE ZEROS.                  
           05  WS-IC-TOT                 PIC 9(6) VALUE ZEROS.                  
           05  K820-CNT                  PIC 9(2) VALUE ZEROS.                  
           05  UNTAG-CNT                 PIC 9(6) VALUE ZEROS.                  
           05  SP-TAG-CNT                PIC 9(6) VALUE ZEROS.                  
           05  WS-DEBAR-EQ-0             PIC 9(6) VALUE ZEROS.                  
                                                                                
       01  WS-TEMPORARY-VARS.                                                   
           05  WS-LOAN-TAG               PIC X(2) VALUE SPACES.                 
           05  WS-PREV-IC                PIC X(9) VALUE ZEROS.                  
           05  K895-REC-PREV             PIC X(450) VALUE SPACES.               
           05  K895-REC-PREV1            PIC X(450) VALUE SPACES.               
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K665-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K800-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K820-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K893-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K895-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K010-STATUS            PIC 99   VALUE ZEROS.                  
           05  K010-STATUS               PIC 99   VALUE ZEROS.                  
           05  K640-STATUS               PIC 99   VALUE ZEROS.                  
                                                                                
       01  WS-K820-K895.                                                        
           05  WS-K820-K895-LOAN         PIC X(02)  VALUE SPACES.               
           05  WS-NUM-NRIC               PIC X(09)  VALUE SPACES.               
           05  WS-NME-OCCP               PIC X(66)  VALUE SPACES.               
           05  WS-NUM-RELATIONSHIP       PIC X(02)  VALUE SPACES.               
                                                                                
       01  WS-K800-K893.                                                        
           05  WS-NUM-NRIC1              PIC X(09)  VALUE SPACES.               
           05  WS-NUM-NRIC2              PIC X(09)  VALUE SPACES.               
           05  WS-NUM-NRIC3              PIC X(09)  VALUE SPACES.               
           05  WS-NUM-NRIC4              PIC X(09)  VALUE SPACES.               
                                                                                
       01  WS-CURR-DATE.                                                        
           05 WS-CURR-CC           PIC 9(02).                                   
           05 WS-YYMMDD.                                                        
              10 WS-CURR-YY        PIC 9(02).                                   
              10 WS-CURR-MM        PIC 9(02).                                   
              10 WS-CURR-DD        PIC 9(02).                                   
                                                                                
       COPY BP13K665.                                                           
                                                                                
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01  L617-PR-HEAD-01.                                                     
           05  FILLER              PIC X(8)      VALUE 'BP13L617'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(19)     VALUE                          
                                    'C O M M I T M E N T'.                      
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  L617-DATE           PIC X(10).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  L617-PAGENO         PIC ZZZZ9 .                                  
                                                                                
       01  L617-PR-HEAD-02.                                                     
           05  FILLER              PIC X(45)     VALUE SPACES .                 
           05  FILLER              PIC X(46)     VALUE                          
               'REPORT ON BATCH UPDATE OF HDB CON LOAN TAGGING'.                
           05  FILLER              PIC X(46)     VALUE SPACES.                  
                                                                                
       01  L617-PR-HEAD-04.                                                     
           05  FILLER              PIC X(31)     VALUE                          
               'SEQ.    REGN   SCH & A/C   NAME'.                               
           05  FILLER              PIC X(15)     VALUE SPACES .                 
           05  FILLER              PIC X(05)     VALUE 'RLTN ' .                
           05  FILLER              PIC X(32)     VALUE                          
               'I/C NO.   LOAN AMT   INT ADDRESS'.                              
           05  FILLER              PIC X(22)     VALUE SPACES.                  
           05  FILLER              PIC X(25)     VALUE                          
               'DEB-REF     CODE EFF-DATE'.                                     
                                                                                
       01  L617-PR-DTL-01.                                                      
           05  L617-SEQ-NO         PIC ZZZ9.                                    
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L617-NUM-REGN       PIC X(08).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L617-NUM-SCHEME     PIC X(11).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L617-NME-OCCP       PIC X(20).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L617-RLTN           PIC X(02).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L617-NUM-NRIC       PIC X(09).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L617-LOAN-AMT       PIC ZZZZZZ9.99 VALUE ZEROES.                 
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L617-LOAN-OPT       PIC X.                                       
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L617-ADDRESS        PIC X(28).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L617-DEB-REF-NO     PIC X(13).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L617-DEB-CODE       PIC X(02).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L617-EFF-DATE       PIC X(10).                                   
                                                                                
       01  L617-PR-LINE            PIC X(132)    VALUE ALL '-'.                 
                                                                                
       01  L617B-PR-HEAD-01.                                                    
           05  FILLER              PIC X(8)      VALUE 'P13L617B'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(19)     VALUE                          
                                    'C O M M I T M E N T'.                      
           05  FILLER              PIC X(17)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  L617B-DATE          PIC X(10).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  L617B-PAGENO         PIC ZZZZ9 .                                 
                                                                                
       01  L617B-PR-HEAD-02.                                                    
           05  FILLER              PIC X(52)     VALUE SPACES .                 
           05  FILLER              PIC X(28)     VALUE                          
               'OUTRIGHT PURCHASE CASES     '.                                  
           05  FILLER              PIC X(52)     VALUE SPACES.                  
                                                                                
       01  L617B-PR-HEAD-03.                                                    
           05  FILLER              PIC X(52)     VALUE SPACES .                 
           05  FILLER              PIC X(28)     VALUE                          
               'SALES UNIT - AGREEMENT TEAM '.                                  
           05  FILLER              PIC X(52)     VALUE SPACES.                  
                                                                                
       01  L617B-PR-HEAD-04.                                                    
           05  FILLER              PIC X(31)     VALUE                          
               'SEQ.    REGN   1ST APPLN   NAME'.                               
           05  FILLER              PIC X(31)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
               'LOAN-AMT    LOAN-TAG'.                                          
                                                                                
       01  L617B-PR-DTL-01.                                                     
           05  L617B-SEQ-NO        PIC ZZZ9.                                    
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L617B-NUM-REGN      PIC X(08).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L617B-NUM-NRIC      PIC X(11).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L617B-NME-OCCP      PIC X(30).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  L617B-LOAN-AMT      PIC ZZZZZZ9.99 VALUE ZEROES.                 
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  L617B-LOAN-TAG      PIC X(01).                                   
                                                                                
      *---------------------------------------------------------------*         
      *        MAIL HEADER                                            *         
      *---------------------------------------------------------------*         
       01  MAIL-HDR-HELO.                                                       
           05  FILLER PIC X(132) VALUE 'HELO SGPHDB1'.                          
                                                                                
       01  MAIL-HDR-MAIL-FROM.                                                  
           05  FILLER PIC X(132) VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.              
                                                                                
       01  MAIL-HDR-MAIL-RCPT.                                                  
           05  FILLER PIC X(132) VALUE 'RCPT TO:<LOW_SOO_CHIEW@HDB.GOV.S        
      -      'G>'.                                                              
                                                                                
       01  MAIL-HDR-DATA.                                                       
           05  FILLER PIC X(132) VALUE 'DATA'.                                  
                                                                                
       01  MAIL-HDR-FROM.                                                       
           05  FILLER PIC X(132) VALUE 'FROM:Soc System - Email Alert'.         
                                                                                
       01  MAIL-HDR-MAIL-TO.                                                    
            05  FILLER PIC X(132) VALUE 'TO:<LOW_SOO_CHIEW@HDB.GOV.SG>'.        
                                                                                
       01  MAIL-SUBJECT-L617.                                                   
           05  FILLER PIC X(132) VALUE 'SUBJECT : Report on Batch Update        
      -        ' of HDB Con Loan Tagging'.                                      
                                                                                
       01  MAIL-SUBJECT-L617B.                                                  
           05  FILLER PIC X(132) VALUE 'SUBJECT : Outright  Purchase Cas        
      -        'es'.                                                            
                                                                                
       01  MAIL-BODY-L617              PIC X(132) VALUE SPACES.                 
                                                                                
       01  MAIL-END-L617               PIC X(132) VALUE SPACES.                 
                                                                                
       01  MAIL-HDR-DATE.                                                       
           05  FILLER                  PIC X(6)   VALUE 'DATE: '.               
           05  MAIL-DATE               PIC X(10)  VALUE SPACES.                 
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                   PIC X      VALUE '.'.                    
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(132) VALUE SPACES.                 
                                                                                
       01  MIME-HEADER.                                                         
           05  WS-MIME-HDR1             PIC X(41)                               
               VALUE 'Mime-Version: 1.0                        '.               
           05  WS-MIME-HDR2             PIC X(80)                               
               VALUE 'Content-type: multipart/mixed; boundary="Mime.Part        
      -         '.Boundary"'.                                                   
           05  WS-MIME-HDR3             PIC X(80)                               
               VALUE 'This is a multi-part message in MIME format.'.            
           05  WS-MIME-HDR4             PIC X(80)                               
               VALUE '--Mime.Part.Boundary                        '.            
           05  WS-MIME-HDR5             PIC X(80)                               
               VALUE 'Content-Type: text/plain; charset="iso-8859-1"'.          
           05  WS-MIME-HDR6             PIC X(80)                               
               VALUE 'Content-Transfer-Encoding: 7bit               '.          
           05  WS-MIME-HDR7             PIC X(80)                               
               VALUE 'Content-disposition: attachment;              '.          
           05  WS-MAIL-FNAMEA           PIC X(80)                               
               VALUE '           FILENAME="BP13L617.TXT"            '.          
           05  WS-MAIL-FNAMEB           PIC X(80)                               
               VALUE '           FILENAME="P13L617B.TXT"            '.          
                                                                                
      *************************                                                 
       PROCEDURE DIVISION.                                                      
      *************************                                                 
                                                                                
      *-----------------                                                        
       000-MAIN-LOGIC.                                                          
      *-----------------                                                        
            PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                    
                                                                                
            PERFORM 2000-READ-F410           THRU 2000-EXIT.                    
                                                                                
            PERFORM 3000-PROCESS-F410        THRU 3000-EXIT                     
                    UNTIL F410-EOF.                                             
            PERFORM 8000-PRINT-CONTROL-TOTAL THRU 8000-EXIT.                    
                                                                                
            PERFORM 9999-CLOSE-FILES         THRU 9999-EXIT.                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------                                                        
       1000-OPEN-FILES.                                                         
      *-----------------                                                        
           OPEN INPUT  BP13F410                                                 
                       BP13K640                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13K893                                                 
                       BP13K895                                                 
                       P04K010                                                  
                I-O    BP13K665                                                 
                       BP04K010                                                 
                OUTPUT BP13L617                                                 
                       P13L617B.                                                
                                                                                
           IF WS-K665-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K665-STATUS ' WS-K665-STATUS              
              MOVE WS-K665-STATUS       TO RETURN-CODE                          
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT.                          
                                                                                
           IF K640-STATUS NOT = ZEROS AND 97                                    
              DISPLAY 'OPENING ERROR, K640-STATUS ' K640-STATUS                 
              MOVE K640-STATUS       TO RETURN-CODE                             
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT.                          
                                                                                
           IF WS-K800-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K800-STATUS ' WS-K800-STATUS              
              MOVE WS-K800-STATUS       TO RETURN-CODE                          
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT.                          
                                                                                
           IF WS-K820-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K820-STATUS ' WS-K820-STATUS              
              MOVE WS-K820-STATUS       TO RETURN-CODE                          
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT.                          
                                                                                
           IF WS-K893-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K893-STATUS ' WS-K893-STATUS              
              MOVE WS-K893-STATUS       TO RETURN-CODE                          
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT.                          
                                                                                
           IF WS-K895-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K895-STATUS ' WS-K895-STATUS              
              MOVE WS-K895-STATUS       TO RETURN-CODE                          
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT.                          
                                                                                
           IF WS-K010-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K010-STATUS ' WS-K010-STATUS              
              MOVE WS-K010-STATUS       TO RETURN-CODE                          
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT.                          
                                                                                
           IF K010-STATUS NOT = ZEROS AND 97                                    
              DISPLAY 'OPENING ERROR, P04K010-STATUS ' K010-STATUS              
              MOVE K010-STATUS          TO RETURN-CODE                          
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT.                          
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
                                                                                
           STRING WS-CURR-DD '/'                                                
                  WS-CURR-MM '/'                                                
                  WS-CURR-CC                                                    
                  WS-CURR-YY DELIMITED BY SIZE                                  
                             INTO L617-DATE.                                    
                                                                                
           MOVE L617-DATE TO L617B-DATE.                                        
                                                                                
           PERFORM 1500-MAIL-HEADER          THRU 1500-EXIT.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------                                                        
       1500-MAIL-HEADER.                                                        
      *-----------------                                                        
                                                                                
           MOVE L617-DATE                   TO MAIL-DATE.                       
                                                                                
           WRITE BP13L617-REC               FROM MAIL-HDR-HELO.                 
           WRITE BP13L617-REC               FROM MAIL-HDR-MAIL-FROM.            
           WRITE BP13L617-REC               FROM MAIL-HDR-MAIL-RCPT.            
           WRITE BP13L617-REC               FROM MAIL-HDR-DATA.                 
           WRITE BP13L617-REC               FROM MAIL-HDR-FROM.                 
           WRITE BP13L617-REC               FROM MAIL-HDR-MAIL-TO.              
           WRITE BP13L617-REC               FROM MAIL-SUBJECT-L617.             
           WRITE BP13L617-REC               FROM MAIL-HDR-DATE.                 
                                                                                
           WRITE BP13L617-REC               FROM WS-MIME-HDR1                   
           WRITE BP13L617-REC               FROM WS-MIME-HDR2                   
           WRITE BP13L617-REC               FROM MAIL-SPACES.                   
           WRITE BP13L617-REC               FROM WS-MIME-HDR3                   
           WRITE BP13L617-REC               FROM WS-MIME-HDR4                   
           WRITE BP13L617-REC               FROM WS-MIME-HDR5                   
           WRITE BP13L617-REC               FROM WS-MIME-HDR6                   
           WRITE BP13L617-REC               FROM WS-MIME-HDR7                   
           WRITE BP13L617-REC               FROM WS-MAIL-FNAMEA                 
                                                                                
           WRITE BP13L617-REC               FROM MAIL-SPACES.                   
                                                                                
           MOVE L617-DATE                   TO MAIL-DATE.                       
                                                                                
           WRITE P13L617B-REC               FROM MAIL-HDR-HELO.                 
           WRITE P13L617B-REC               FROM MAIL-HDR-MAIL-FROM.            
           WRITE P13L617B-REC               FROM MAIL-HDR-MAIL-RCPT.            
           WRITE P13L617B-REC               FROM MAIL-HDR-DATA.                 
           WRITE P13L617B-REC               FROM MAIL-HDR-FROM.                 
           WRITE P13L617B-REC               FROM MAIL-HDR-MAIL-TO.              
           WRITE P13L617B-REC               FROM MAIL-SUBJECT-L617.             
           WRITE P13L617B-REC               FROM MAIL-HDR-DATE.                 
                                                                                
           WRITE P13L617B-REC               FROM WS-MIME-HDR1                   
           WRITE P13L617B-REC               FROM WS-MIME-HDR2                   
           WRITE P13L617B-REC               FROM MAIL-SPACES.                   
           WRITE P13L617B-REC               FROM WS-MIME-HDR3                   
           WRITE P13L617B-REC               FROM WS-MIME-HDR4                   
           WRITE P13L617B-REC               FROM WS-MIME-HDR5                   
           WRITE P13L617B-REC               FROM WS-MIME-HDR6                   
           WRITE P13L617B-REC               FROM WS-MIME-HDR7                   
           WRITE P13L617B-REC               FROM WS-MAIL-FNAMEB                 
                                                                                
           WRITE P13L617B-REC               FROM MAIL-SPACES.                   
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------                                                        
       2000-READ-F410.                                                          
      *-----------------                                                        
           READ BP13F410 AT END                                                 
                MOVE 'Y' TO WS-F410-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD   1     TO WS-READ-CNT-F410.                                     
           MOVE SPACES TO WS-FOUND.                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------                                                        
       3000-PROCESS-F410.                                                       
      *-----------------                                                        
                                                                                
           IF (F410-AMT-LOAN > 0 AND F410-NUM-LOAN-TAG  = 'H') OR               
              (F410-NUM-LOAN-TAG  = 'N' AND F410-NUM-CONTRA-TYPE = 'TM')        
              ADD 1 TO WS-WITH-LOAN                                             
                                                                                
              PERFORM 3050-READ-BP13K640           THRU 3050-EXIT               
                                                                                
              IF (K640-CDE-LOAN-OPTION = 'P' OR WS-K640-FND = 'N')              
                 AND F410-CDE-LOAN-SCH NOT = '4'                                
                 PERFORM 3100-READ-BP13K800        THRU 3100-EXIT               
                                                                                
                 IF WS-K800-FND = 'Y'                                           
                    PERFORM 3200-START-BP13K820    THRU 3200-EXIT               
                 ELSE                                                           
                    PERFORM 3300-READ-BP13K893     THRU 3300-EXIT               
                                                                                
                    IF WS-K893-FND = 'Y'                                        
                       PERFORM 3400-START-BP13K895 THRU 3400-EXIT               
                    ELSE                                                        
                       ADD 1 TO WS-K893-NFND                                    
                    END-IF                                                      
                                                                                
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              ADD 1 TO WS-NO-LOAN                                               
              PERFORM 6000-PRINT-L617B             THRU 6000-EXIT               
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F410                  THRU 2000-EXIT.              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3050-READ-BP13K640.                                                      
      ****************************                                              
                                                                                
           MOVE SPACES                      TO BP13K640-TRANS-REC.              
           MOVE F410-NUM-REGN               TO K640-REGN-NO.                    
                                                                                
           READ BP13K640.                                                       
                                                                                
           EVALUATE K640-STATUS                                                 
               WHEN 00                                                          
                    ADD 1                   TO WS-K640-CNT                      
                    MOVE 'Y'                TO WS-K640-FND                      
                    IF K640-CDE-LOAN-OPTION = 'P'                               
                       ADD 1 TO WS-K640-LOAN-P                                  
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES             TO K640-CDE-LOAN-OPTION             
                    MOVE 'N'                TO WS-K640-FND                      
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K640!'                           
                             ' / KEY : ' K640-REGN-NO                           
                             ' / FILE STATUS :' K640-STATUS                     
                    PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3100-READ-BP13K800.                                                      
      ****************************                                              
                                                                                
           MOVE SPACES                      TO WS-K800-K893.                    
           MOVE F410-NUM-REGN               TO K800-NUM-REGN.                   
           MOVE 'N'                         TO WS-K800-FND WS-SPO-IC            
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    ADD 1                   TO WS-K800-READ                     
                    MOVE 'Y'                TO WS-K800-FND                      
                    MOVE K800-NUM-NRIC1     TO WS-NUM-NRIC1                     
                    IF K800-NUM-NRIC2(1:1) NOT = SPACES AND '#'                 
                       MOVE K800-NUM-NRIC2  TO WS-NUM-NRIC2                     
                    END-IF                                                      
                    IF K800-NUM-NRIC3(1:1) NOT = SPACES AND '#'                 
                       MOVE K800-NUM-NRIC3  TO WS-NUM-NRIC3                     
                    END-IF                                                      
                    IF K800-NUM-NRIC4(1:1) NOT = SPACES AND '#'                 
                       MOVE K800-NUM-NRIC4  TO WS-NUM-NRIC4                     
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1 TO WS-K800-NFND                                       
                    MOVE 'N'               TO WS-K800-FND                       
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K800!'                           
                             ' / KEY : ' K800-NUM-REGN                          
                             ' / FILE STATUS :' WS-K800-STATUS                  
                    PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       3200-START-BP13K820.                                                     
      *---------------*                                                         
                                                                                
           MOVE SPACES                     TO K820-KEY-FLD .                    
           MOVE ZEROES                     TO K820-CNT.                         
           MOVE F410-NUM-REGN              TO K820-NUM-REGN.                    
                                                                                
           MOVE 'N'                        TO WS-DEBAR-TAG WS-K820-EOF.         
                                                                                
           START BP13K820 KEY IS >= K820-KEY-FLD                                
                                                                                
           EVALUATE WS-K820-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3250-PROCESS-K820  THRU 3250-EXIT                   
                      UNTIL WS-K820-EOF = 'Y'                                   
               WHEN 23                                                          
                    DISPLAY 'KEY NOT FOUND IN BP13K820!'                        
                            ' / KEY : ' K820-KEY-FLD                            
               WHEN OTHER                                                       
                    DISPLAY 'ERROR LOCATING KEY IN BP13K820!'                   
                    DISPLAY 'KEY : ' F410-NUM-REGN                              
                    DISPLAY 'FILE STATUS :' WS-K820-STATUS                      
                    PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
           IF K820-CNT = ZEROES                                                 
              ADD  1 TO WS-K820-NFND.                                           
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------                                                     
       3250-PROCESS-K820.                                                       
      *--------------------                                                     
            READ BP13K820 NEXT                                                  
                                                                                
            EVALUATE WS-K820-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K820-NUM-REGN  = F410-NUM-REGN                           
                       PERFORM 3251-MOVE-K820-DETLS THRU 3251-EXIT              
                       PERFORM 4000-PROCESS-DEBAR   THRU 4000-EXIT              
                    ELSE                                                        
                       MOVE 'Y'                     TO WS-K820-EOF              
                    END-IF                                                      
               WHEN 23                                                          
                    MOVE 'Y'                        TO WS-K820-EOF              
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K820!'                           
                            ' / K895-NUM-REGN :' K820-NUM-REGN                  
                            ' / FILE STATUS : ' WS-K820-STATUS                  
                    PERFORM 9999-CLOSE-FILES        THRU 9999-EXIT              
            END-EVALUATE.                                                       
                                                                                
       3250-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ****************************                                              
       3251-MOVE-K820-DETLS.                                                    
      ****************************                                              
                                                                                
           ADD 1 TO WS-K820-READ K820-CNT                                       
           MOVE SPACES                      TO WS-K820-K895                     
           MOVE K820-NUM-LOAN-TAG           TO WS-K820-K895-LOAN                
           MOVE K820-NUM-NRIC               TO WS-NUM-NRIC                      
           MOVE K820-NME-OCCP               TO WS-NME-OCCP                      
           MOVE K820-NUM-RELATIONSHIP       TO WS-NUM-RELATIONSHIP.             
                                                                                
       3251-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ****************************                                              
       3300-READ-BP13K893.                                                      
      ****************************                                              
                                                                                
           MOVE SPACES                       TO K893-KEY-FLD.                   
           MOVE 'N'                          TO WS-K893-EOF WS-K893-FND.        
           MOVE F410-NUM-REGN                TO K893-NUM-REGN-HIST              
                                                                                
           START BP13K893 KEY >= K893-NUM-REGN-HIST                             
                                                                                
           EVALUATE WS-K893-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM UNTIL WS-K893-EOF = 'Y'                             
                       READ BP13K893 NEXT                                       
                                                                                
                       IF WS-K893-STATUS = 00                                   
                          IF F410-NUM-REGN = K893-NUM-REGN-HIST                 
                            ADD 1               TO WS-K893-READ                 
                            MOVE SPACES         TO WS-K800-K893                 
                            MOVE K893-NUM-NRIC1 TO WS-NUM-NRIC1                 
                            IF K893-NUM-NRIC2(1:1) NOT = SPACES AND '#'         
                               MOVE K893-NUM-NRIC2 TO WS-NUM-NRIC2              
                            END-IF                                              
                            IF K893-NUM-NRIC3(1:1) NOT = SPACES AND '#'         
                               MOVE K893-NUM-NRIC3 TO WS-NUM-NRIC3              
                            END-IF                                              
                            IF K893-NUM-NRIC4(1:1) NOT = SPACES AND '#'         
                               MOVE K893-NUM-NRIC4 TO WS-NUM-NRIC4              
                            END-IF                                              
                            MOVE 'Y'          TO WS-K893-FND WS-K893-EOF        
                          ELSE                                                  
                            MOVE 'Y'            TO WS-K893-EOF                  
                          END-IF                                                
                       ELSE                                                     
                          MOVE 'Y'              TO WS-K893-EOF                  
                       END-IF                                                   
                    END-PERFORM                                                 
               WHEN 23                                                          
                    DISPLAY 'KEY NOT FOUND IN BP13K893!' K893-KEY-FLD           
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K893!'                          
                             ' / KEY : ' K893-KEY-FLD                           
                             ' / FILE STATUS :' WS-K893-STATUS                  
                    PERFORM 9999-CLOSE-FILES    THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3400-START-BP13K895.                                                     
      *--------------------*                                                    
                                                                                
           MOVE SPACES                      TO BP13K895-REC WS-PREV-IC          
                                           K895-REC-PREV K895-REC-PREV1.        
           MOVE ZEROES TO K820-CNT.                                             
           INITIALIZE     BP13K895-REC.                                         
                                                                                
           MOVE 'N'                        TO WS-K895-FND WS-K895-EOF           
                                              WS-DEBAR-TAG.                     
                                                                                
           MOVE SPACES                     TO K895-KEY-FLD.                     
           MOVE F410-NUM-REGN              TO K895-NUM-REGN-HIST.               
                                                                                
           START BP13K895 KEY >= K895-NUM-REGN-HIST                             
                                                                                
           EVALUATE WS-K895-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3450-PROCESS-K895   THRU 3450-EXIT                  
                      UNTIL WS-K895-EOF = 'Y'                                   
               WHEN 23                                                          
                    DISPLAY 'KEY NOT FOUND IN BP13K895!'                        
                            ' / KEY : ' K895-KEY-FLD                            
               WHEN OTHER                                                       
                    DISPLAY 'ERROR LOCATING KEY IN BP13K895!'                   
                    DISPLAY 'KEY : ' K895-KEY-FLD                               
                    DISPLAY 'FILE STATUS :' WS-K895-STATUS                      
                    PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
           IF K820-CNT = ZEROES                                                 
              ADD  1 TO WS-K895-NFND.                                           
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       3450-PROCESS-K895.                                                       
      *------------------*                                                      
                                                                                
           READ BP13K895 NEXT                                                   
                                                                                
           EVALUATE WS-K895-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K895-NUM-REGN-HIST = F410-NUM-REGN                       
                       IF K895-NUM-NRIC-HIST = WS-PREV-IC                       
                       OR WS-PREV-IC         = SPACES                           
                          MOVE BP13K895-REC          TO K895-REC-PREV           
                          MOVE K895-NUM-NRIC-HIST    TO WS-PREV-IC              
                       ELSE                                                     
                          MOVE K895-NUM-NRIC-HIST    TO WS-PREV-IC              
                          MOVE BP13K895-REC          TO K895-REC-PREV1          
                          MOVE K895-REC-PREV         TO BP13K895-REC            
                          PERFORM 3455-MOVE-K895-DETLS THRU 3455-EXIT           
                          PERFORM 4000-PROCESS-DEBAR THRU 4000-EXIT             
                          MOVE K895-REC-PREV1        TO K895-REC-PREV           
                       END-IF                                                   
                    ELSE                                                        
                       IF K895-REC-PREV NOT = SPACES                            
                          MOVE K895-REC-PREV           TO BP13K895-REC          
                          PERFORM 3455-MOVE-K895-DETLS THRU 3455-EXIT           
                          PERFORM 4000-PROCESS-DEBAR   THRU 4000-EXIT           
                       END-IF                                                   
                       MOVE 'Y'                      TO WS-K895-EOF             
                    END-IF                                                      
               WHEN 23                                                          
                    MOVE 'Y'                         TO WS-K895-EOF             
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K895!'                           
                            ' / K895-NUM-REGN :' K895-NUM-REGN-HIST             
                            ' / FILE STATUS : ' WS-K895-STATUS                  
                    PERFORM 9999-CLOSE-FILES         THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       3450-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3455-MOVE-K895-DETLS.                                                    
      ****************************                                              
                                                                                
           ADD 1                            TO WS-K895-READ K820-CNT            
           MOVE SPACES                      TO WS-K820-K895                     
           MOVE K895-NUM-NRIC-HIST          TO WS-NUM-NRIC                      
           MOVE K895-NUM-LOAN-TAG           TO WS-K820-K895-LOAN                
           MOVE K895-NME-OCCP               TO WS-NME-OCCP                      
           MOVE K895-NUM-RELATIONSHIP       TO WS-NUM-RELATIONSHIP.             
                                                                                
       3455-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------                                                   
       4000-PROCESS-DEBAR.                                                      
      *---------------------                                                    
                                                                                
            MOVE 'N'                              TO WS-DEBAR-FOUND             
            ADD 1 TO WS-IC-TOT                                                  
                                                                                
            PERFORM 4050-DETERMIN-LOANTAG         THRU 4050-EXIT                
                                                                                
            IF WS-LOAN-TAG       NOT = SPACES                                   
            OR WS-K820-K895-LOAN NOT = SPACES                                   
               PERFORM 4100-START-P04K010         THRU 4100-EXIT                
                                                                                
               IF WS-DEBAR-FOUND = 'N'                                          
                  IF WS-DEBAR-TAG = 'N'                                         
                     PERFORM 4200-GET-DEBARMENT-NO   THRU 4200-EXIT             
                     MOVE 'Y' TO WS-DEBAR-TAG                                   
                  END-IF                                                        
                  ADD 1 TO WS-DEB-NFND-CNT                                      
                  PERFORM 5000-PREPARE-DEBARMENT-REC THRU 5000-EXIT             
               END-IF                                                           
            ELSE                                                                
               ADD  1  TO UNTAG-CNT                                             
            END-IF.                                                             
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------                                                    
       4050-DETERMIN-LOANTAG.                                                   
      *---------------------                                                    
                                                                                
           IF WS-K820-K895-LOAN = 'XX'                                          
              MOVE SPACES                      TO WS-K820-K895-LOAN.            
                                                                                
           IF WS-K820-K895-LOAN = SPACES                                        
              ADD 1 TO WS-SPACE-O-CNT                                           
              MOVE SPACES                      TO WS-LOAN-TAG                   
                                                                                
              IF WS-NUM-NRIC = WS-NUM-NRIC1 OR                                  
                 WS-NUM-NRIC = WS-NUM-NRIC2 OR                                  
                 WS-NUM-NRIC = WS-NUM-NRIC3 OR                                  
                 WS-NUM-NRIC = WS-NUM-NRIC4                                     
                 MOVE 'Z1'                     TO WS-LOAN-TAG                   
                 ADD 1                         TO WS-Z1-CNT                     
              ELSE                                                              
                 IF WS-NUM-RELATIONSHIP = '20' OR '09' OR '10'                  
                    MOVE 'Z2'                  TO WS-LOAN-TAG                   
                    ADD 1                      TO WS-Z2-CNT                     
      *          ELSE                                                           
      *             IF WS-SPO-IC = 'Y'                                          
      *                 IF (WS-NUM-NRIC = WS-SP-NRIC(1) OR                      
      *                     WS-NUM-NRIC = WS-SP-NRIC(2) OR                      
      *                     WS-NUM-NRIC = WS-SP-NRIC(3) OR                      
      *                     WS-NUM-NRIC = WS-SP-NRIC(4))                        
      *                    ADD 1               TO WS-Z2SP-CNT                   
      *                    MOVE 'Z2'           TO WS-LOAN-TAG                   
      *                 END-IF                                                  
      *             END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              MOVE WS-K820-K895-LOAN           TO WS-LOAN-TAG                   
              IF WS-K820-K895-LOAN = 'Z1'                                       
                 ADD 1       TO WS-Z1-O-CNT                                     
              ELSE                                                              
                 IF WS-K820-K895-LOAN = 'Z2'                                    
                    ADD 1    TO WS-Z2-O-CNT                                     
                 ELSE                                                           
                    ADD 1    TO WS-OTHER-O-CNT                                  
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4100-START-P04K010.                                                      
      ******************************************************************        
           MOVE 'N'                           TO WS-DEBAR-FOUND                 
                                          WS-EOF-K010 WS-DEBAR-HIST.            
           MOVE SPACES                        TO P04F010-REC.                   
           INITIALIZE                            P04F010-REC.                   
           MOVE WS-NUM-NRIC                   TO F010-NUM-UINFIN.               
           ADD 1 TO K010-READ                                                   
                                                                                
           START P04K010  KEY = F010-NUM-UINFIN.                                
                                                                                
           EVALUATE K010-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM UNTIL WS-EOF-K010 = 'Y'                             
                       READ P04K010 NEXT                                        
                                                                                
                       IF K010-STATUS = 00                                      
                         IF F010-NUM-UINFIN      = WS-NUM-NRIC                  
                           IF F010-EX-SCH-ACCTNO = F410-NUM-SCH-ACC(1:9)        
                           AND F010-REASON-CODE  = WS-LOAN-TAG                  
                              MOVE 'Y'     TO WS-DEBAR-FOUND WS-EOF-K010        
                              ADD 1 TO WS-DEB-FND-CNT                           
                           END-IF                                               
                           MOVE 'Y'        TO WS-DEBAR-HIST                     
                         ELSE                                                   
                           MOVE 'Y'        TO WS-EOF-K010                       
                         END-IF                                                 
                       ELSE                                                     
                          MOVE 'Y'         TO WS-EOF-K010                       
                       END-IF                                                   
                    END-PERFORM                                                 
               WHEN 20                                                          
               WHEN 23                                                          
                    MOVE SPACES            TO F010-REASON-CODE                  
               WHEN OTHER                                                       
                    DISPLAY 'START ERROR P04K010 : ' K010-STATUS                
                            ' NRIC : ' F010-NUM-UINFIN                          
                    PERFORM 9999-CLOSE-FILES        THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
           IF WS-DEBAR-HIST = 'N'                                               
              ADD 1 TO WS-DEBAR-EQ-0.                                           
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------                                                     
       4200-GET-DEBARMENT-NO.                                                   
      *--------------------                                                     
           MOVE  LOW-VALUES    TO WS-K665-KEY-FLD.                              
           MOVE '2019'         TO WS-K665-KEY-FLD(1:4).                         
           START BP13K665 KEY IS >= WS-K665-KEY-FLD                             
                 INVALID KEY                                                    
                 DISPLAY 'READING ERROR, K665-STATUS ' WS-K665-STATUS           
                 ' \ KEY: ' K665-KEY-FLD                                        
                 MOVE WS-K665-STATUS       TO RETURN-CODE                       
                 PERFORM 9999-CLOSE-FILES THRU 9999-EXIT.                       
                                                                                
           PERFORM 4210-READ-BP13K665 THRU 4210-EXIT                            
             UNTIL WS-K665-KEY-FLD = HIGH-VALUES.                               
                                                                                
           IF K665-KEY-FLD = SPACES OR LOW-VALUES                               
              MOVE 0                TO  K665-NUM-LAST-DEBREF-ID                 
           END-IF.                                                              
                                                                                
           ADD   1                  TO  K665-NUM-LAST-DEBREF-ID.                
           MOVE  'SL'               TO  K665-NUM-DEBREF-AO                      
                                        K665-NUM-LAST-DEBREF-AO.                
           MOVE  WS-CURR-DATE       TO  K665-DTE-ISSUE-DEBARMENT.               
           MOVE  WS-CURR-DATE(1:4)  TO  K665-NUM-LAST-DEBREF-YR.                
           MOVE  'P13C617'          TO  K665-NUM-USERID.                        
                                                                                
           IF WS-DEBARMENT-TAG = 'Y'                                            
              ADD 1 TO WS-K665-REWRITE                                          
              REWRITE WS-BP13K665-REC FROM BP13K665-REC                         
           ELSE                                                                 
              ADD 1 TO  WS-K665-WRITE                                           
              WRITE   WS-BP13K665-REC FROM BP13K665-REC.                        
                                                                                
           EVALUATE WS-K665-STATUS                                              
           WHEN 00                                                              
              MOVE  'Y'               TO  WS-DEBARMENT-TAG                      
                                                                                
           WHEN 22                                                              
              REWRITE WS-BP13K665-REC FROM BP13K665-REC                         
                      INVALID KEY                                               
                      DISPLAY 'REWRITING ERROR, K665-STATUS '                   
                      WS-K665-STATUS ' \ KEY: ' K665-KEY-FLD                    
                      MOVE WS-K665-STATUS       TO RETURN-CODE                  
                      PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                   
              END-REWRITE                                                       
              ADD   1                 TO  WS-K665-REWRITE                       
              MOVE  'Y'               TO   WS-DEBARMENT-TAG                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'WRITING ERROR, K665-STATUS ' WS-K665-STATUS              
                      ' \ KEY: ' K665-KEY-FLD                                   
              MOVE WS-K665-STATUS     TO   RETURN-CODE                          
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------                                                     
       4210-READ-BP13K665.                                                      
      *--------------------                                                     
           READ BP13K665 NEXT                                                   
                AT END MOVE HIGH-VALUES TO WS-K665-KEY-FLD                      
                GO TO 4210-EXIT.                                                
                                                                                
           ADD 1 TO WS-K665-READ.                                               
                                                                                
           IF WS-K665-KEY-FLD NOT = SPACES AND LOW-VALUES                       
              MOVE WS-BP13K665-REC TO BP13K665-REC.                             
                                                                                
       4210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------                                                     
       5000-PREPARE-DEBARMENT-REC.                                              
      *--------------------                                                     
           MOVE SPACES TO P04K010-REC.                                          
                                                                                
           PERFORM 5100-WRITE-BP04K010    THRU 5100-EXIT.                       
                                                                                
           PERFORM 5200-PRINT-L617        THRU 5200-EXIT.                       
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------                                                     
       5100-WRITE-BP04K010.                                                     
      *--------------------                                                     
           MOVE SPACES                    TO BP13COMM8-REC.                     
           INITIALIZE                        BP13COMM8-REC.                     
                                                                                
           MOVE F410-NUM-SCH-ACC         TO WS-LINK-REC.                        
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           IF (COMM8-CDE-SYSERR = 0) AND                                        
              (COMM8-NUM-MGMT   NOT = SPACES)                                   
              STRING 'BLK' COMM8-NUM-BLK                                        
                     '#'   COMM8-NUM-LEVEL                                      
                     '-'   COMM8-NUM-UNIT-MAIN COMM8-NUM-UNIT-SUB               
                     ' '   COMM8-NME-STREET(1:20)                               
                     DELIMITED BY SIZE INTO L617-ADDRESS                        
           ELSE                                                                 
              DISPLAY 'SQL CODE = ' COMM8-CDE-SYSERR                            
              MOVE SPACES TO L617-ADDRESS.                                      
                                                                                
           MOVE 'BP13'                    TO K010-INTERFACE-SYS.                
           MOVE WS-LOAN-TAG               TO K010-REASON-CODE.                  
           MOVE WS-NUM-NRIC               TO K010-NUM-UINFIN.                   
           MOVE WS-NME-OCCP               TO K010-NAME.                         
           MOVE K665-NUM-LAST-DEBARMENT   TO K010-DEB-REF-NO.                   
           MOVE F410-DTE-SO-EFFECTIVE     TO K010-EFF-DATE.                     
           MOVE F410-NUM-SCH-ACC(1:9)     TO K010-EX-SCH-ACCTNO.                
           MOVE F410-NUM-SCH-ACC(10:2)    TO K010-LSE-TEN-CODE.                 
           MOVE '1'                       TO K010-PROP-TYPE.                    
           MOVE '999'                     TO K010-PERIOD-DEBAR.                 
           MOVE ZEROS                     TO K010-AMT-OWING.                    
                                                                                
           MOVE COMM8-NUM-BLK             TO K010-BLOCK.                        
           MOVE COMM8-NME-STREET          TO K010-STREETNAME.                   
           MOVE COMM8-NUM-POSTAL-CODE     TO K010-POSTCODE.                     
                                                                                
           STRING    COMM8-NUM-LEVEL                                            
                     COMM8-NUM-UNIT-MAIN COMM8-NUM-UNIT-SUB                     
                     DELIMITED BY SIZE INTO K010-HOUSE-NO.                      
                                                                                
           MOVE WS-CURR-DATE              TO K010-INCLUSION-DATE                
                                             K010-LUPDATE.                      
                                                                                
           WRITE P04K010-REC.                                                   
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN 00                                                              
                ADD 1 TO WS-K010-WRITE                                          
           WHEN 22                                                              
           WHEN 02                                                              
                DISPLAY 'DUPLICATE RECORD BP04K010: ' K010-PRIME-KEY            
           WHEN OTHER                                                           
                MOVE WS-K010-STATUS       TO RETURN-CODE                        
                DISPLAY 'ERROR IN WRITING BP04K010: ' K010-PRIME-KEY            
                        ' / STAT: ' WS-K010-STATUS                              
                PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------                                                     
       5200-PRINT-L617.                                                         
      *--------------------                                                     
           ADD  1                        TO WS-SEQ-NO.                          
           MOVE WS-SEQ-NO                TO L617-SEQ-NO.                        
           MOVE F410-NUM-REGN            TO L617-NUM-REGN.                      
           MOVE K665-NUM-LAST-DEBARMENT  TO L617-DEB-REF-NO.                    
           MOVE K010-REASON-CODE         TO L617-DEB-CODE.                      
           MOVE WS-NUM-NRIC              TO L617-NUM-NRIC.                      
           MOVE WS-NME-OCCP              TO L617-NME-OCCP.                      
           MOVE WS-NUM-RELATIONSHIP      TO L617-RLTN.                          
           MOVE F410-NUM-SCH-ACC         TO L617-NUM-SCHEME.                    
           MOVE F410-AMT-LOAN            TO L617-LOAN-AMT.                      
           MOVE K640-CDE-LOAN-OPTION     TO L617-LOAN-OPT.                      
                                                                                
           STRING K010-EFF-DATE(7:2) '-'                                        
                  K010-EFF-DATE(5:2) '-'                                        
                  K010-EFF-DATE(1:4) DELIMITED BY SIZE                          
                  INTO L617-EFF-DATE.                                           
                                                                                
           IF WS-LINE-CTR > 55                                                  
              ADD  1              TO   WS-PAGE-CTR                              
              MOVE 6              TO   WS-LINE-CTR                              
              MOVE WS-PAGE-CTR    TO   L617-PAGENO                              
              WRITE BP13L617-REC  FROM L617-PR-HEAD-01                          
              WRITE BP13L617-REC  FROM L617-PR-HEAD-02                          
              WRITE BP13L617-REC  FROM L617-PR-LINE                             
              WRITE BP13L617-REC  FROM L617-PR-HEAD-04                          
              WRITE BP13L617-REC  FROM L617-PR-LINE.                            
                                                                                
           WRITE BP13L617-REC  FROM L617-PR-DTL-01.                             
           ADD   1             TO   WS-LINE-CTR .                               
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------                                                     
       6000-PRINT-L617B.                                                        
      *--------------------                                                     
           ADD  1                         TO WS-SEQ-NO1.                        
           MOVE WS-SEQ-NO1                TO L617B-SEQ-NO.                      
           MOVE F410-NUM-REGN             TO L617B-NUM-REGN.                    
           MOVE F410-NRIC1                TO L617B-NUM-NRIC.                    
           MOVE F410-NAME1                TO L617B-NME-OCCP.                    
           MOVE F410-AMT-LOAN             TO L617B-LOAN-AMT.                    
           MOVE F410-NUM-LOAN-TAG         TO L617B-LOAN-TAG.                    
                                                                                
           IF WS-LINE-CTR1 > 55                                                 
              ADD  1              TO   WS-PAGE-CTR1                             
              MOVE 6              TO   WS-LINE-CTR1                             
              MOVE WS-PAGE-CTR1   TO   L617B-PAGENO                             
              WRITE P13L617B-REC  FROM L617B-PR-HEAD-01                         
              WRITE P13L617B-REC  FROM L617B-PR-HEAD-02                         
              WRITE P13L617B-REC  FROM L617B-PR-HEAD-03                         
              WRITE P13L617B-REC  FROM L617-PR-LINE                             
              WRITE P13L617B-REC  FROM L617B-PR-HEAD-04                         
              WRITE P13L617B-REC  FROM L617-PR-LINE.                            
                                                                                
           WRITE P13L617B-REC  FROM L617B-PR-DTL-01.                            
           ADD   1             TO   WS-LINE-CTR1 .                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       8000-PRINT-CONTROL-TOTAL.                                                
      *----------------------------------------------------------------*        
           IF WS-LINE-CTR > 55                                                  
              ADD  1              TO   WS-PAGE-CTR                              
              MOVE 6              TO   WS-LINE-CTR                              
              MOVE WS-PAGE-CTR    TO   L617-PAGENO                              
              WRITE BP13L617-REC  FROM L617-PR-HEAD-01                          
              WRITE BP13L617-REC  FROM L617-PR-HEAD-02                          
              WRITE BP13L617-REC  FROM L617-PR-LINE                             
              WRITE BP13L617-REC  FROM L617-PR-HEAD-04                          
              WRITE BP13L617-REC  FROM L617-PR-LINE.                            
                                                                                
           MOVE    '*-------BP13C617 CONTROL TOTALS ------------*'              
                   TO L617-PR-LINE.                                             
           WRITE BP13L617-REC  FROM L617-PR-LINE.                               
           STRING  '1. NO OF REC READ        BP13F410   = '                     
                    WS-READ-CNT-F410 DELIMITED BY SIZE                          
                    INTO L617-PR-LINE.                                          
           WRITE BP13L617-REC  FROM L617-PR-LINE.                               
           STRING  '2. NO OF CASES TAGGED    BP04K010   = '                     
                    WS-K010-WRITE DELIMITED BY SIZE                             
                    INTO L617-PR-LINE.                                          
           WRITE BP13L617-REC  FROM L617-PR-LINE.                               
           STRING  '3. NO OF OUTRIGHT PURCHASE          = '                     
                    WS-NO-LOAN DELIMITED BY SIZE                                
                    INTO L617-PR-LINE.                                          
           WRITE BP13L617-REC  FROM L617-PR-LINE.                               
                                                                                
      *write MIME end Boundary                                                  
           WRITE BP13L617-REC               FROM WS-MIME-HDR4                   
           WRITE P13L617B-REC               FROM WS-MIME-HDR4.                  
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       9999-CLOSE-FILES.                                                        
      *----------------------------------------------------------------*        
           CLOSE BP13F410                                                       
                 BP13K640                                                       
                 BP13K665                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K893                                                       
                 BP13K895                                                       
                 BP04K010                                                       
                 P04K010                                                        
                 BP13L617                                                       
                 P13L617B.                                                      
                                                                                
           IF WS-K665-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K665, STATUS ' WS-K665-STATUS          
              MOVE WS-K665-STATUS        TO RETURN-CODE.                        
                                                                                
           IF K640-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BP13K640, STATUS ' K640-STATUS             
              MOVE K640-STATUS        TO RETURN-CODE.                           
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K800, STATUS ' WS-K800-STATUS          
              MOVE WS-K800-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K820, STATUS ' WS-K820-STATUS          
              MOVE WS-K820-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K893, STATUS ' WS-K893-STATUS          
              MOVE WS-K893-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K895-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K895, STATUS ' WS-K895-STATUS          
              MOVE WS-K895-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP04K010, STATUS ' WS-K010-STATUS          
              MOVE WS-K010-STATUS        TO RETURN-CODE.                        
                                                                                
           IF K010-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING P04K010, STATUS ' K010-STATUS              
              MOVE K010-STATUS           TO RETURN-CODE.                        
                                                                                
           DISPLAY '*------------------ BP13C617 -------------------'.          
           DISPLAY '                  ' WS-CURR-DATE                            
           DISPLAY ' 1.) NO OF REC READ   BP13F410 = ' WS-READ-CNT-F410         
           DISPLAY ' 2.) NO OF CASE WITH LOAN      = ' WS-WITH-LOAN             
           DISPLAY ' 3.) NO OF NO LOAN CASE, L617B = ' WS-NO-LOAN               
           DISPLAY '     1 = 2 + 3'.                                            
           DISPLAY '  '.                                                        
           DISPLAY '     NO OF REC FOUND  BP13K640 = ' WS-K640-CNT              
           DISPLAY '     NO OF K640 WITH LOAN AS P = ' WS-K640-LOAN-P           
           DISPLAY ' 4.) NO OF REC READ   BP13K800 = ' WS-K800-READ             
           DISPLAY ' 5.) NO OF REC N-FND  BP13K800 = ' WS-K800-NFND             
           DISPLAY ' 6.) NO OF REC READ   BP13K820 = ' WS-K820-READ             
           DISPLAY ' 7.) NO OF REC N-FND  BP13K820 = ' WS-K820-NFND             
           DISPLAY '  '.                                                        
           DISPLAY ' 8.) NO OF REC READ   BP13K893 = ' WS-K893-READ             
           DISPLAY ' 9.) NO OF REC N-FND  BP13K893 = ' WS-K893-NFND             
           DISPLAY '10.) NO OF REC READ   BP13K895 = ' WS-K895-READ             
           DISPLAY '11.) NO OF REC N-FND  BP13K895 = ' WS-K895-NFND             
           DISPLAY '     2 = 4 + 8    (total regn K800+K893)'.                  
           DISPLAY '  '.                                                        
           DISPLAY '12.) NO OF TOTAL IC            = ' WS-IC-TOT                
           DISPLAY '  '.                                                        
           DISPLAY '14.) NO OF REC READ   BP13K665 = ' WS-K665-READ             
           DISPLAY '15.) NO OF REC WRITE  BP13K665 = ' WS-K665-WRITE            
           DISPLAY '   NO OF REC RE-WRITE BP13K665 = ' WS-K665-REWRITE          
           DISPLAY '  '.                                                        
           DISPLAY '16.) NO OF  Z1 ORIG TAGGED     = ' WS-Z1-O-CNT              
           DISPLAY '17.) NO OF  Z2 ORIG TAGGED     = ' WS-Z2-O-CNT              
           DISPLAY '18.) NO OF SPA ORIG TAGGED     = ' WS-SPACE-O-CNT           
           DISPLAY '19.) NO OF OTH ORIG TAGGED     = ' WS-OTHER-O-CNT           
           DISPLAY '     16 + 17 + 18 + 19 = 12 (total IC K820+K895)'.          
           DISPLAY '  '.                                                        
           DISPLAY '20.) NO OF  Z1  NEW TAGGED     = ' WS-Z1-CNT                
           DISPLAY '21.) NO OF  Z2  NEW TAGGED     = ' WS-Z2-CNT                
           DISPLAY '               NO OF  UNTAG    = ' UNTAG-CNT                
           DISPLAY '     16 + 17  +  20 + 21 + 22 = 23 (K010 READ) '.           
           DISPLAY '     ORIG TAG +  NEW TAG'.                                  
           DISPLAY '23.) NO OF REC READ    P04K010 = ' K010-READ                
           DISPLAY '24.) NO OF REC MATCH   P04K010 = ' WS-DEB-FND-CNT           
           DISPLAY '25.) NO OF REC N-MATCH P04K010 = ' WS-DEB-NFND-CNT          
           DISPLAY '26.) NO OF REC WRITE  BP04K010 = ' WS-K010-WRITE.           
           DISPLAY '27.) NO OF RECS WITH NO DEBAR  = ' WS-DEBAR-EQ-0.           
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
