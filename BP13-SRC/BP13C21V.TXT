       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C21V.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  19/08/2010.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES: TO CREATE BP13F800 AND BP13FH17                *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP134006  ESA1  18082010  NEW PROGRAM                      *            
      * BP134226  ESA1  22072011  CATER FOR 2-BYTES QUEUE PREFIX   *            
      * BP134331  ESA1  18082011  REPLACE BP13K816 WITH BP13K813   *            
      * BP134135  ESA1  26082011  CORRECT READING OF BP13K813      *            
      * BP134681  ESA1  02102012  CATER FOR 3-BYTES ZONE           *            
      * BP135190  SMR2  04122013  EXPAND BP13K813 TO 1000          *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
      *-------------------------------------------------------------            
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13F800  ASSIGN        TO BP13F800.                          
                                                                                
           SELECT BP13FH17  ASSIGN        TO BP13FH17.                          
                                                                                
           SELECT P13F800A  ASSIGN        TO P13F800A.                          
                                                                                
           SELECT BP13K813 ASSIGN TO BP13K813                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K813-KEY-FLD                               
                  FILE STATUS     IS BP13K813-STATUS.                           
                                                                                
           SELECT BP13KH40 ASSIGN TO BP13KH40                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KH40-KEY-FLD                               
                  FILE STATUS     IS BP13KH40-STATUS.                           
                                                                                
      *-------------------------------------------------------------            
       DATA DIVISION.                                                           
      *-------------------------------------------------------------            
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13FH17                                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13FH17.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13KH40                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13KH40.                                                           
                                                                                
       FD   P13F800A                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       01   P13F800A-MASTER      PIC X(2000).                                   
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01 WS-VARIABLES.                                                         
          05 WS-QUEUE-START       PIC 9(05) VALUE ZEROES.                       
          05 WS-QUEUE-END         PIC 9(05) VALUE ZEROES.                       
          05 WS-MAX-QUEUE         PIC 9(05) VALUE ZEROES.                       
          05 WS-DATE              PIC X(08) VALUE SPACES.                       
          05 WS-CURRENT-TIME      PIC 9(08) VALUE ZEROES.                       
                                                                                
       01 WS-COUNTERS.                                                          
          05 WS-F800-READ         PIC 9(08) VALUE ZEROES.                       
          05 WS-FH17-WRITE        PIC 9(08) VALUE ZEROES.                       
          05 WS-KH40-ERR-CNT      PIC 9(08) VALUE ZEROES.                       
          05 WS-KH40-FND-SW       PIC X     VALUE SPACES.                       
          05 WS-KH40-READ-ERR     PIC 9(08) VALUE ZEROES.                       
          05 WS-KH40-READ-CNT     PIC 9(08) VALUE ZEROES.                       
          05 WS-K813-READ-ERR     PIC 9(08) VALUE ZEROES.                       
          05 WS-K813-READ-CNT     PIC 9(08) VALUE ZEROES.                       
          05 WS-F800A-WRITE-CNT   PIC 9(08) VALUE ZEROES.                       
          05 WS-FLAG              PIC X     VALUE SPACES.                       
          05 WS-PAIR-FLAG         PIC X     VALUE SPACES.                       
          05 WS-CTR               PIC 9(02) VALUE ZEROES.                       
                                                                                
       01 WS-FILE-STATUS.                                                       
          05 BP13KH40-STATUS      PIC 9(02) VALUE ZEROES.                       
          05 BP13K813-STATUS      PIC 9(02) VALUE ZEROES.                       
                                                                                
       01 WS-SWITCHES.                                                          
          05 WS-F800-EOF                 PIC X(1) VALUE 'N'.                    
          05 WS-K813-EOF                 PIC X(1) VALUE 'N'.                    
          05 WS-ZONE-FND-FLAG            PIC X(1) VALUE 'N'.                    
                                                                                
       01 WS-F800-KEY.                                                          
          10 WS-F800-MAIN-KEY.                                                  
             15 WS-F800-NUM-ALLO-CAT                PIC X(3).                   
             15 WS-F800-DTE-BALLOT                  PIC X(6).                   
             15 WS-F800-NUM-BTO-ZONE                PIC X(3).                   
                                                                                
       01 WS-PREV-KEY.                                                          
          10 WS-PREV-MAIN-KEY.                                                  
             15 WS-PREV-NUM-ALLO-CAT                PIC X(3).                   
             15 WS-PREV-DTE-BALLOT                  PIC X(6).                   
             15 WS-PREV-NUM-BTO-ZONE                PIC X(3).                   
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F800  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-DATA   THRU 3000-EXIT                           
              UNTIL WS-F800-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13K813                                                 
                I-O    BP13KH40                                                 
               OUTPUT  BP13FH17                                                 
                       P13F800A.                                                
                                                                                
           IF BP13K813-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K813 : ' BP13K813-STATUS             
              MOVE BP13K813-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF BP13KH40-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KH40 : ' BP13KH40-STATUS             
              MOVE BP13KH40-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           ACCEPT WS-CURRENT-TIME   FROM TIME.                                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F800 AT END                                                 
             MOVE 'Y'         TO WS-F800-EOF                                    
             MOVE HIGH-VALUES TO WS-F800-KEY                                    
             GO TO 2000-EXIT.                                                   
                                                                                
           ADD 1                         TO WS-F800-READ.                       
                                                                                
           MOVE F800-NUM-ALLO-CAT        TO WS-F800-NUM-ALLO-CAT.               
           MOVE F800-DTE-BALLOT          TO WS-F800-DTE-BALLOT.                 
           MOVE F800-NUM-BTO-ZONE        TO WS-F800-NUM-BTO-ZONE.               
           MOVE ZERO                     TO F800-NUM-NT-FT-QUEUE(1:1).          
           IF F800-NUM-NT-FT-QUEUE(2:1) NOT NUMERIC                             
              MOVE ZERO                  TO F800-NUM-NT-FT-QUEUE(2:1)           
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE ZEROES                   TO WS-QUEUE-START                      
                                            WS-QUEUE-END                        
                                            WS-MAX-QUEUE.                       
                                                                                
           MOVE WS-F800-KEY              TO WS-PREV-KEY.                        
                                                                                
           PERFORM 4000-GET-QUEUE-START-END THRU 4000-EXIT                      
             UNTIL WS-F800-EOF = 'Y' OR                                         
                   WS-F800-KEY NOT = WS-PREV-KEY.                               
                                                                                
           IF WS-QUEUE-START > 0 AND WS-QUEUE-END  > 0                          
              PERFORM 5000-WRITE-BP13FH17   THRU 5000-EXIT                      
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-GET-QUEUE-START-END.                                                
      *-------------------------------------------------------------            
                                                                                
            IF F800-NUM-NT-FT-QUEUE NOT = SPACES AND LOW-VALUES AND ZEROES      
               IF WS-QUEUE-START      = ZEROES OR                               
                  WS-QUEUE-END        = ZEROES                                  
                  MOVE F800-NUM-NT-FT-QUEUE        TO WS-QUEUE-START            
                                                      WS-QUEUE-END              
               END-IF                                                           
                                                                                
               IF F800-NUM-NT-FT-QUEUE < WS-QUEUE-START                         
                  MOVE F800-NUM-NT-FT-QUEUE        TO WS-QUEUE-START            
               ELSE                                                             
                  MOVE F800-NUM-NT-FT-QUEUE        TO WS-MAX-QUEUE              
               END-IF                                                           
                                                                                
               IF WS-MAX-QUEUE > WS-QUEUE-END                                   
                  MOVE WS-MAX-QUEUE                TO WS-QUEUE-END              
               END-IF                                                           
           END-IF.                                                              
                                                                                
           PERFORM 6000-PROCESS-BP13KH40         THRU 6000-EXIT.                
           PERFORM 2000-READ-BP13F800            THRU 2000-EXIT.                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-WRITE-BP13FH17.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                      TO BP13FH17-REC.                    
           INITIALIZE                          BP13FH17-REC.                    
                                                                                
           MOVE WS-PREV-NUM-ALLO-CAT        TO FH17-NUM-SELECTION.              
           MOVE WS-PREV-DTE-BALLOT          TO FH17-DTE-BALLOT.                 
           MOVE WS-PREV-NUM-BTO-ZONE        TO FH17-NUM-NEW-TOWN.               
           MOVE K813-NUM-FLAT-TYPE          TO FH17-NUM-FT.                     
           MOVE SPACES                      TO FH17-NUM-ETHNIC.                 
                                                                                
           MOVE WS-QUEUE-START              TO FH17-NUM-QUEUE-START.            
           MOVE WS-QUEUE-END                TO FH17-NUM-QUEUE-END.              
                                                                                
           MOVE WS-DATE                     TO FH17-DTE-UPDATE.                 
           MOVE WS-CURRENT-TIME             TO FH17-TME-UPDATE.                 
           MOVE 'BP13C21V'                  TO FH17-NUM-USERID.                 
                                                                                
           WRITE BP13FH17-REC.                                                  
                                                                                
           ADD 1                            TO WS-FH17-WRITE.                   
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-PROCESS-BP13KH40.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE F800-NUM-ALLO-CAT        TO   KH40-NUM-SELECTION-TYPE.          
           MOVE F800-DTE-BALLOT          TO   KH40-DTE-BALLOT.                  
           PERFORM 6100-DECODE-NEW-TOWN  THRU 6100-EXIT.                        
           PERFORM 6200-DECODE-FLAT-TYPE THRU 6200-EXIT.                        
           PERFORM 6300-READ-BP13KH40    THRU 6300-EXIT.                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6100-DECODE-NEW-TOWN.                                                    
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'                       TO WS-K813-EOF                        
                                             WS-ZONE-FND-FLAG.                  
           MOVE SPACES                    TO K813-KEY-FLD.                      
           MOVE F800-NUM-BTO-ZONE         TO K813-NUM-ZONE.                     
                                                                                
           START BP13K813 KEY >= K813-NUM-ZONE.                                 
                                                                                
           IF BP13K813-STATUS = 00 OR 02                                        
              PERFORM 6150-READNEXT-BP13K813 THRU 6150-EXIT                     
              UNTIL WS-K813-EOF = 'Y' OR WS-ZONE-FND-FLAG = 'Y'                 
           ELSE                                                                 
              IF BP13K813-STATUS = 10 OR 23                                     
                 MOVE SPACES                 TO KH40-NUM-NEW-TOWN               
                 DISPLAY 'ZONE NOT FOUND IN BP13K813:' K813-KEY-FLD             
                 ADD   1     TO WS-K813-READ-ERR                                
                 GO          TO 6100-EXIT                                       
              ELSE                                                              
                DISPLAY 'START ERROR BP13K825 : ' BP13K813-STATUS               
                        ' ZONE : ' K813-NUM-ZONE                                
                PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------------------------------------------*             
       6150-READNEXT-BP13K813.                                                  
      *-----------------------------------------------------------*             
                                                                                
           READ BP13K813 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-K813-EOF.                                 
                                                                                
           IF BP13K813-STATUS = 0                                               
              IF K813-NUM-ZONE   = F800-NUM-BTO-ZONE                            
                 MOVE K813-NME-SITE          TO KH40-NUM-NEW-TOWN               
                 ADD   1  TO WS-K813-READ-CNT                                   
                 MOVE 'Y'        TO  WS-ZONE-FND-FLAG                           
              END-IF                                                            
           ELSE                                                                 
              IF BP13K813-STATUS = 10 OR 23                                     
                 MOVE SPACES        TO KH40-NUM-NEW-TOWN                        
                 DISPLAY 'ZONE NOT FOUND IN BP13K813:' K813-KEY-FLD             
                 ADD   1            TO WS-K813-READ-ERR                         
                 MOVE 'Y'           TO  WS-K813-EOF                             
              ELSE                                                              
                 IF BP13K813-STATUS NOT = 0                                     
                    DISPLAY  'READ ERROR IN BP13K813 ' K813-KEY-FLD             
                    DISPLAY  'FILE STATUS: ' BP13K813-STATUS                    
                    ADD   1   TO WS-K813-READ-ERR                               
                    GO TO 9000-CLOSE-ROUTINE                                    
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       6150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6200-DECODE-FLAT-TYPE.                                                   
      *-------------------------------------------------------------            
      ******************************************                                
      * HARDCODE C3 & C4 TO CATER FOR PAIR UNITS                                
      ******************************************                                
           EVALUATE K813-NUM-FLAT-TYPE(1:1)                                     
              WHEN '1'                                                          
                 IF K813-NUM-FLAT-TYPE(2:1) = 'A'                               
                    MOVE 'SA       ' TO KH40-NUM-FLAT-TYPE                      
                 ELSE                                                           
                    MOVE '1-ROOM   ' TO KH40-NUM-FLAT-TYPE                      
                 END-IF                                                         
              WHEN '2'                                                          
                 MOVE '2-ROOM   ' TO KH40-NUM-FLAT-TYPE                         
              WHEN '3'                                                          
                 MOVE '3-ROOM   '    TO KH40-NUM-FLAT-TYPE                      
              WHEN '4'                                                          
                 IF K813-NUM-ZONE = 'C3'                                        
                    IF F800-NUM-FLAT-TYPE(1:1) = '4'                            
                       MOVE '4-ROOM-SA' TO KH40-NUM-FLAT-TYPE                   
                    ELSE                                                        
                       MOVE '4-ROOM-SP' TO KH40-NUM-FLAT-TYPE                   
                    END-IF                                                      
                 ELSE                                                           
                    MOVE '4-ROOM   '    TO KH40-NUM-FLAT-TYPE                   
                 END-IF                                                         
              WHEN '5'                                                          
                 IF K813-NUM-ZONE = 'C4'                                        
                    IF F800-NUM-FLAT-TYPE(1:1) = '5'                            
                       MOVE '5-ROOM-SA' TO KH40-NUM-FLAT-TYPE                   
                    ELSE                                                        
                       MOVE '5-ROOM-SP' TO KH40-NUM-FLAT-TYPE                   
                    END-IF                                                      
                 ELSE                                                           
                    MOVE '5-ROOM   '    TO KH40-NUM-FLAT-TYPE                   
                 END-IF                                                         
              WHEN '6'                                                          
              WHEN 'E'                                                          
                 MOVE 'EXECUTIVE'    TO KH40-NUM-FLAT-TYPE                      
              WHEN OTHER                                                        
                 MOVE SPACES         TO KH40-NUM-FLAT-TYPE                      
                 DISPLAY 'ERROR - FLAT TYPE NOT SUPPORTED: '                    
                                        K813-NUM-FLAT-TYPE                      
                 GO TO 6200-EXIT                                                
           END-EVALUATE.                                                        
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6300-READ-BP13KH40.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13KH40 KEY IS KH40-KEY-FLD.                                   
                                                                                
           IF BP13KH40-STATUS = 0                                               
              ADD   1  TO WS-KH40-READ-CNT                                      
              WRITE P13F800A-MASTER FROM BP13F800-MASTER                        
              ADD   1  TO WS-F800A-WRITE-CNT                                    
           ELSE                                                                 
           IF BP13KH40-STATUS = 23                                              
              DISPLAY 'RECORD NOT FOUND IN BP13KH40:' KH40-KEY-FLD              
              ADD   1     TO WS-KH40-READ-ERR                                   
              GO          TO 6300-EXIT                                          
           ELSE                                                                 
           IF BP13KH40-STATUS NOT = 0                                           
              MOVE '4'  TO RETURN-CODE                                          
              DISPLAY  'READ ERROR IN BP13KH40 ' KH40-KEY-FLD                   
              ADD   1   TO WS-KH40-READ-ERR                                     
              GO        TO 9000-CLOSE-ROUTINE.                                  
                                                                                
       6300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13C21V CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'RECS READ    FROM BP13F800  : ' WS-F800-READ.               
           DISPLAY 'RECS WRITTEN   IN P13F800A  : ' WS-F800A-WRITE-CNT.         
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN   IN BP13FH17  : ' WS-FH17-WRITE.              
           DISPLAY '                              '.                            
           DISPLAY 'RECS READ    FROM BP13K813  : ' WS-K813-READ-CNT.           
           DISPLAY 'RECS NOTFND    IN BP13K813  : ' WS-K813-READ-ERR.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS READ    FROM BP13KH40  : ' WS-KH40-READ-CNT.           
           DISPLAY 'RECS NOTFND    IN BP13KH40  : ' WS-KH40-READ-ERR.           
                                                                                
           CLOSE BP13F800                                                       
                 BP13K813                                                       
                 BP13KH40                                                       
                 BP13FH17.                                                      
                                                                                
           IF BP13K813-STATUS NOT = 0 AND 97                                    
              DISPLAY ' ERROR CLOSING - BP13K813 : ' BP13K813-STATUS            
              MOVE BP13K813-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           IF BP13KH40-STATUS NOT = 0 AND 97                                    
              DISPLAY ' ERROR CLOSING - BP13KH40 : ' BP13KH40-STATUS            
              MOVE BP13KH40-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
