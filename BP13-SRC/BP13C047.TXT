       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C047.                                                 
      *AUTHOR.        SK.                                                       
      *DATE-WRITTEN.  14/08/2003.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE   :  MATCHING PROGRAM                           *            
      *                                                            *            
      *  INPUT FILES :  BP13F800                                   *            
      *                 BP13F595                                   *            
      *                                                            *            
      *  OUT FILE    :  BP13OUT                                    *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP132391 20030814 SK    NEW PGM                            *            
      * BP132558 20040709 SK    CATER FOR MULTIPLE NRIC            *            
      * BP132691 20050428 SK    CATER MUTIPLE ATTEMP OF BE/BTO     *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F800   ASSIGN TO BP13F800.                                
           SELECT BP13F595   ASSIGN TO BP13F595.                                
           SELECT BP13OUT    ASSIGN TO BP13OUT.                                 
           SELECT BP13OUTA   ASSIGN TO BP13OUTA.                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BP13F800                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 30 CHARACTERS.                                      
                                                                                
       01   BP13F800-REC.                                                       
            05 F800-NUM-REGN     PIC X(08).                                     
            05 F800-NUM-NRIC     PIC X(09).                                     
            05 F800-NUM-FT       PIC X(02).                                     
            05 F800-NUM-HH       PIC X(01).                                     
            05 FILLER            PIC X(10).                                     
                                                                                
       FD   BP13F595                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 30 CHARACTERS.                                      
                                                                                
       01   BP13F595-REC.                                                       
            05 F595-NUM-REGN         PIC X(8).                                  
            05 F595-NUM-NRIC         PIC X(9).                                  
            05 F595-DTE-BALLOT       PIC X(6).                                  
            05 F595-NUM-ALLO-CAT     PIC X(3).                                  
            05 F595-CDE-FLAT-TYPE    PIC X(2).                                  
            05 FILLER                PIC X(2).                                  
                                                                                
       FD   BP13OUT                                                             
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 100 CHARACTERS.                                     
                                                                                
       01   BP13OUT-REC.                                                        
            05 OUT-CAN-NUM-REGN      PIC X(08).                                 
            05 OUT-CAN-NUM-NRIC      PIC X(09).                                 
            05 OUT-CAN-DTE-BALLOT    PIC X(06).                                 
            05 OUT-CAN-NUM-ALLOCAT   PIC X(03).                                 
            05 OUT-CAN-NUM-FTYPE     PIC X(02).                                 
            05 OUT-CAN-NUMBER        PIC 9(02).                                 
            05 OUT-BTO-NUMBER        PIC 9(02).                                 
            05 OUT-BE-NUMBER         PIC 9(02).                                 
            05 FILLER                PIC X(66).                                 
                                                                                
       FD   BP13OUTA                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 100 CHARACTERS.                                     
                                                                                
       01   BP13OUTA-REC.                                                       
            05 OUTA-CAN-NUM-REGN      PIC X(08).                                
            05 OUTA-CAN-NUM-NRIC      PIC X(09).                                
            05 OUTA-CAN-DTE-BALLOT    PIC X(06).                                
            05 OUTA-CAN-NUM-ALLOCAT   PIC X(03).                                
            05 OUTA-CAN-NUM-FTYPE     PIC X(02).                                
            05 OUTA-CAN-NUM-HH        PIC X(01).                                
            05 FILLER                 PIC X(71).                                
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WORK-AREAS.                                                          
           05  WS-MATCH            PIC 9(8)    VALUE 0.                         
           05  WS-NOTMATCH         PIC 9(8)    VALUE 0.                         
           05  WS-READ-F800        PIC 9(8)    VALUE 0.                         
           05  WS-READ-F595        PIC 9(8)    VALUE 0.                         
           05  WS-WRITE-OUT        PIC 9(8)    VALUE 0.                         
           05  WS-WRITE-OUTA       PIC 9(8)    VALUE 0.                         
           05  WS-CTR              PIC 9(2)    VALUE 0.                         
           05  WS-CTR-BTO          PIC 9(2)    VALUE 0.                         
           05  WS-CTR-BE           PIC 9(2)    VALUE 0.                         
                                                                                
       01   BP13F595-TREC.                                                      
            05 F595-NUM-REGN-T       PIC X(8).                                  
            05 F595-NUM-NRIC-T       PIC X(9).                                  
            05 F595-DTE-BALLOT-T     PIC X(6).                                  
            05 F595-NUM-ALLO-CAT-T   PIC X(3).                                  
            05 F595-CDE-FLAT-TYPE-T  PIC X(2).                                  
            05 FILLER                PIC X(2).                                  
                                                                                
       01  WS-DATE                 PIC X(8)    VALUE SPACES.                    
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT                            
           PERFORM 2000-READ-F800     THRU 2000-EXIT                            
           PERFORM 2100-READ-F595     THRU 2100-EXIT                            
                                                                                
           PERFORM 3000-COMPARE       THRU 3000-EXIT                            
                   UNTIL F800-NUM-NRIC = HIGH-VALUES                            
                     AND F595-NUM-NRIC = HIGH-VALUES                            
                                                                                
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13F595                                                 
                OUTPUT BP13OUT                                                  
                       BP13OUTA.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-DATE.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-F800.                                                          
      *-------------------------------------------------------------            
                                                                                
           READ BP13F800                                                        
              AT END MOVE HIGH-VALUES TO F800-NUM-NRIC                          
              GO TO 2000-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-READ-F800.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-F595.                                                          
      *-------------------------------------------------------------            
                                                                                
           READ BP13F595                                                        
              AT END MOVE HIGH-VALUES TO F595-NUM-NRIC                          
              GO TO 2100-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-READ-F595.                                               
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-COMPARE.                                                            
      *-------------------------------------------------------------            
                                                                                
           MOVE 0 TO WS-CTR WS-CTR-BTO WS-CTR-BE.                               
           IF F800-NUM-NRIC = F595-NUM-NRIC                                     
              PERFORM 3500-PROCESS           THRU 3500-EXIT                     
                      UNTIL F595-NUM-NRIC NOT = F800-NUM-NRIC                   
                      OR F595-NUM-NRIC = HIGH-VALUES                            
              PERFORM 4000-WRITE-RECORDS     THRU 4000-EXIT                     
              PERFORM 2000-READ-F800         THRU 2000-EXIT                     
           ELSE                                                                 
              IF F800-NUM-NRIC > F595-NUM-NRIC                                  
                 PERFORM 2100-READ-F595      THRU 2100-EXIT                     
              ELSE                                                              
                 ADD 1 TO WS-NOTMATCH                                           
                 PERFORM 2000-READ-F800      THRU 2000-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3500-PROCESS.                                                            
      *-------------------------------------------------------------            
           ADD 1 TO WS-MATCH WS-CTR.                                            
                                                                                
           EVALUATE F595-NUM-ALLO-CAT                                           
              WHEN 'BTO'                                                        
                 ADD 1  TO WS-CTR-BTO                                           
              WHEN 'BE '                                                        
                 ADD 1  TO WS-CTR-BE                                            
           END-EVALUATE.                                                        
                                                                                
           MOVE BP13F595-REC TO BP13F595-TREC.                                  
                                                                                
           PERFORM 4500-WRITE-OUTA  THRU 4500-EXIT.                             
           PERFORM 2100-READ-F595   THRU 2100-EXIT.                             
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-WRITE-RECORDS.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE  SPACES              TO BP13OUT-REC.                            
           INITIALIZE                   BP13OUT-REC.                            
                                                                                
           MOVE F800-NUM-REGN        TO OUT-CAN-NUM-REGN.                       
           MOVE F800-NUM-NRIC        TO OUT-CAN-NUM-NRIC.                       
           MOVE F595-DTE-BALLOT-T    TO OUT-CAN-DTE-BALLOT.                     
           MOVE F595-NUM-ALLO-CAT-T  TO OUT-CAN-NUM-ALLOCAT.                    
           MOVE F595-CDE-FLAT-TYPE-T TO OUT-CAN-NUM-FTYPE.                      
           MOVE WS-CTR               TO OUT-CAN-NUMBER.                         
           MOVE WS-CTR-BTO           TO OUT-BTO-NUMBER.                         
           MOVE WS-CTR-BE            TO OUT-BE-NUMBER.                          
                                                                                
           WRITE BP13OUT-REC.                                                   
           ADD 1 TO WS-WRITE-OUT.                                               
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4500-WRITE-OUTA.                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE  SPACES              TO BP13OUTA-REC.                           
           INITIALIZE                   BP13OUTA-REC.                           
                                                                                
           MOVE F800-NUM-REGN        TO OUTA-CAN-NUM-REGN.                      
           MOVE F800-NUM-NRIC        TO OUTA-CAN-NUM-NRIC.                      
           MOVE F800-NUM-FT          TO OUTA-CAN-NUM-FTYPE.                     
           MOVE F800-NUM-HH          TO OUTA-CAN-NUM-HH.                        
           MOVE F595-DTE-BALLOT      TO OUTA-CAN-DTE-BALLOT.                    
           MOVE F595-NUM-ALLO-CAT    TO OUTA-CAN-NUM-ALLOCAT.                   
                                                                                
           WRITE BP13OUTA-REC.                                                  
           ADD 1 TO WS-WRITE-OUTA.                                              
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '--- BP13C047 CONTROL TOTAL --------------'.                 
           DISPLAY '                    RUN DATE  : ' WS-DATE.                  
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF REC READ FROM BP13F800  : ' WS-READ-F800.             
           DISPLAY 'NO OF REC READ FROM BP13F595  : ' WS-READ-F595.             
           DISPLAY 'NO OF REC WRITTEN TO BP13OUT  : ' WS-WRITE-OUT.             
           DISPLAY 'NO OF REC WRITTEN TO BP13OUTA : ' WS-WRITE-OUTA.            
           DISPLAY 'NO OF MATCH                   : ' WS-MATCH.                 
           DISPLAY 'NO OF NOT MATCH               : ' WS-NOTMATCH.              
           DISPLAY '-----------------------------------------'.                 
                                                                                
           CLOSE BP13F800                                                       
                 BP13F595                                                       
                 BP13OUT                                                        
                 BP13OUTA.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
