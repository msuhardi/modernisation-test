       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C296.                                                  
       AUTHOR.       ANNALYN BANTA.                                             
      *DATE-WRITTEN. 18.03.94.                                                  
      ************************************************************              
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                      *              
      *  SYSTEM ID   : BP13                                      *              
      *  OBJECTIVE   : TO CREATE ALLOCATION SUMMARY FILE BP13F222*              
      *                                                          *              
      *  BP13F205    : CONTROL REC                               *              
      *  BP13F222    : ALLOCATION SUMMARY RESULT FILE            *              
      *  BP13K270    : BOOKING PROGRAMME FILE                    *              
      *  BP13K210    : BOOKING WORKPLAN                          *              
      *                                                          *              
      * CHG-NO  BY   DATE    DESCRIPTION                         *              
      * ------  ---  ------  -----------                         *              
      * N940009 LMS  080694  TO INCLUDE K222-DTE-ALLOC.          *              
      * N940009 LMS  210694  TO CATER FOR EOF-K100.              *              
      * JAR8488 LMS  160994  TO CATER FOR NEXT BP13F205 REC.     *              
      * SOC-PH9 GD   051194  TO RENAME NUM-NT VARIABLES TO       *              
      *                         NUM-NT-ZONE.                     *              
      *                      TO CATER FOR GROUPING BY FLAT TYPE. *              
      *BP130067 LMS  010795  PGM RECOMPILED DUE TO CHG IN        *              
      *                      FILE BP13K222.                      *              
      *BP130336 LMS  120897  TO CATER FOR JB IN SERS.            *              
      *(BP130250 IN LCS)                                         *              
      *BP130488 LMS  080598  TO WRITE BLOCKS TO BP13K278 FOR     *              
      *                      OCS BLOCKS.                         *              
      *BP130418 LMS  020998  TO CATER FOR Y2K CHANGES.           *              
      *BP130778 GBR  141299  TO INCLUDE 3 ROOM FLAT UNDER FLAT   *              
      *                      TYPE 'SE'                           *              
      *BP130883 CLT  041100  TO READ STREET NAME FROM PIDB       *              
      *                      INTERFACE TABLE                     *              
      *BP130949 LMS  221100  TO CATER FOR 2 AND 3 ROOM OCS.      *              
      *BP131017 LMS  110501  TO WRITE TO F222 INSTEAD OF K222.   *              
      *BP131059 LMS  200801  TO CATER FOR BTO.                   *              
      *BP132075 JAM1 170102  TO USE K270-NUM-OFFER INSTEAD OF    *              
      *                      K270-NUM-ALLOC IN ACCUMULATING TOTAL*              
      *                      NUMBER OF UNITS OFFERED.            *              
      *BP132306 JF4  180203  TO CHANGE WS-START-DATE-DD TO 20.   *              
      *BP133610 ESA1 030609  DELETE USING OF BP13K767            *              
      *BP13XXXX KAM4 040113  CATER TO MOVE WS-FLAT-OFFER TO      *              
      *                      F222-NUM-FLAT-OFFER TO FIX THE ZERO *              
      *                      (ALWAYS HAS A ZERO) VALUE ON THIS   *              
      *                      FIELD WHEN USING THIS PROG IN A JOB *              
      *BP135198 ESA1 030315  ADD READING OF BP13K813 TO CORRECT  *              
      *                      FLAT-OFFER FOR BTO CONTRACTS        *              
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
                                                                                
           SELECT BP13F205 ASSIGN       TO BP13F205.                            
                                                                                
           SELECT BP13F222 ASSIGN       TO BP13F222.                            
                                                                                
           SELECT BP13K270 ASSIGN       TO BP13K270                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K270-KEY-FLD                         
                           FILE STATUS  IS K270-STATUS.                         
                                                                                
           SELECT BP13K278 ASSIGN       TO BP13K278                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K278-KEY-FLD                         
                           FILE STATUS  IS K278-STATUS.                         
                                                                                
           SELECT BM06K100 ASSIGN       TO BM06K100                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K100-KEY-FLD                         
                           FILE STATUS  IS K100-STATUS.                         
                                                                                
           SELECT BP13K813 ASSIGN       TO BP13K813                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K813-KEY-FLD                         
                           FILE STATUS  IS K813-STATUS.                         
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F205                                                             
           BLOCK  CONTAINS 0   RECORDS                                          
           LABEL  RECORDS  ARE STANDARD                                         
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 80  CHARACTERS.                                      
       COPY BP13F205.                                                           
                                                                                
       FD  BP13F222                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13F222.                                                           
                                                                                
       FD  BP13K278                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K278.                                                           
                                                                                
       FD  BM06K100                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K100.                                                           
                                                                                
       FD  BP13K270                                                             
           RECORD CONTAINS 250 CHARACTERS.                                      
       COPY BP13K270.                                                           
                                                                                
       FD  BP13K813                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K813.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       COPY AV02COMM.                                                           
       01  WS-FILE-STATUS.                                                      
           05  F222-STATUS         PIC 99      VALUE ZERO.                      
           05  K278-STATUS         PIC 99      VALUE ZERO.                      
           05  K270-STATUS         PIC 99      VALUE ZERO.                      
           05  K100-STATUS         PIC 99      VALUE ZERO.                      
           05  K813-STATUS         PIC 99      VALUE ZERO.                      
                                                                                
       01  WS-K210-FT              PIC X(2).                                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F205-READ        PIC 9(8)    VALUE ZERO.                      
           05  WS-F222-WRITE       PIC 9(8)    VALUE ZERO.                      
           05  WS-K278-WRITE       PIC 9(8)    VALUE ZERO.                      
           05  WS-F222-UPDATE      PIC 9(8)    VALUE ZERO.                      
           05  WS-K278-UPDATE      PIC 9(8)    VALUE ZERO.                      
           05  WS-K100-READ        PIC 9(8)    VALUE ZERO.                      
                                                                                
       01  WS-SWITCHES.                                                         
           05  F205-EOF            PIC X       VALUE 'N'.                       
               88  END-OF-F205                 VALUE 'Y'.                       
           05  K270-EOF            PIC X       VALUE 'N'.                       
               88  END-OF-K270                 VALUE 'Y'.                       
           05  K210-EOF            PIC X       VALUE 'N'.                       
               88  END-OF-K210                 VALUE 'Y'.                       
           05  WS-ZONE-FND-FLAG    PIC X       VALUE 'N'.                       
           05  WS-EOF-K813         PIC X       VALUE 'N'.                       
                                                                                
       01  WS-TEMP-VARIABLES.                                                   
           05  WS-CNT-BKAPPMT      PIC 9(4)    VALUE ZERO.                      
           05  WS-FLAT-OFFER       PIC 9(4)    VALUE ZERO.                      
           05  WS-ALLOC-TOTAL      PIC 9(4)    VALUE ZERO.                      
           05  WS-SERS-FLAT        PIC 9(4)    VALUE ZERO.                      
           05  WS-FT-NO            PIC 9(2)    VALUE ZERO.                      
           05  WS-START-DATE.                                                   
               10  WS-START-DATE-CCYY  PIC X(04)  VALUE SPACES.                 
               10  WS-START-DATE-MM    PIC X(02)  VALUE SPACES.                 
               10  WS-START-DATE-DD    PIC X(02)  VALUE SPACES.                 
           05  WS-END-DATE.                                                     
               10  WS-END-DATE-CCYY  PIC X(04)  VALUE SPACES.                   
               10  WS-END-DATE-MM    PIC X(02)  VALUE SPACES.                   
               10  WS-END-DATE-DD    PIC X(02)  VALUE SPACES.                   
           05  WS-WORK-END-DATE.                                                
               10  WS-WORK-END-DATE-CCYY  PIC X(04)  VALUE SPACES.              
               10  WS-WORK-END-DATE-MM    PIC X(02)  VALUE SPACES.              
               10  WS-WORK-END-DATE-DD    PIC X(02)  VALUE SPACES.              
           05  WS-DAY              PIC 9(2)    VALUE ZERO.                      
           05  WS-FT               PIC X(2).                                    
           05  WS-CDE-BLK          PIC X(5)    VALUE SPACES.                    
           05  WS-NME-STREET       PIC X(20)   VALUE SPACES.                    
           05  WS-VALID-NEXT-DAY   PIC X(1)    VALUE SPACES.                    
                                                                                
       01  WS-OCS-DUE-DATE         PIC 9(8).                                    
       01  WS-K278-DTE-END         PIC X(8).                                    
       01  WS-K278-DTE-END-9 REDEFINES WS-K278-DTE-END PIC 9(8).                
                                                                                
       01  WS-SQL-CODES.                                                        
           05  WS-SQL-FOUND                 PIC S9(4) COMP VALUE +000.          
           05  WS-SQL-NOT-FOUND             PIC S9(4) COMP VALUE +100.          
                                                                                
                                                                                
           EXEC SQL INCLUDE P13VINTF END-EXEC.                                  
           EXEC SQL INCLUDE SQLCA    END-EXEC.                                  
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN-LOGIC.                                                         
      *************************************************************             
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-F205       THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-RTN     THRU 3000-EXIT                          
             UNTIL END-OF-F205.                                                 
           PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT.                         
                                                                                
      *************************************************************             
       1000-OPEN-FILES.                                                         
      *************************************************************             
           OPEN  INPUT BP13F205                                                 
                       BP13K270                                                 
                       BM06K100                                                 
                       BP13K813                                                 
                I-O    BP13K278                                                 
                OUTPUT BP13F222.                                                
                                                                                
           IF K270-STATUS NOT = 00 AND 97                                       
              MOVE K270-STATUS            TO RETURN-CODE                        
              DISPLAY 'ERROR OPENING BP13K270 FILE, ' K270-STATUS               
              PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                         
                                                                                
           IF K278-STATUS NOT = 00 AND 97                                       
              MOVE K278-STATUS            TO RETURN-CODE                        
              DISPLAY 'ERROR OPENING BP13K278 FILE, ' K278-STATUS               
              PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                         
                                                                                
           IF K100-STATUS NOT = 00 AND 97                                       
              MOVE K100-STATUS            TO RETURN-CODE                        
              DISPLAY 'ERROR OPENING BM06K100 FILE, ' K100-STATUS               
              PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                         
                                                                                
           IF K813-STATUS NOT = 00 AND 97                                       
              MOVE K813-STATUS            TO RETURN-CODE                        
              DISPLAY 'ERROR OPENING BP13K813 FILE, ' K813-STATUS               
              PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                          
           END-IF.                                                              
                                                                                
           INITIALIZE      WS-SWITCHES                                          
                           WS-TEMP-VARIABLES                                    
                           WS-COUNTERS.                                         
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       2000-READ-F205.                                                          
      *************************************************************             
           READ BP13F205                                                        
             AT END                                                             
                MOVE 'Y' TO F205-EOF                                            
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                   TO WS-F205-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       3000-PROCESS-RTN.                                                        
      *************************************************************             
                                                                                
           INITIALIZE  WS-TEMP-VARIABLES                                        
                       WS-SWITCHES.                                             
                                                                                
           PERFORM 3100-GET-START-END-DATE       THRU  3100-EXIT.               
                                                                                
           PERFORM 3400-READ-K270 THRU 3400-EXIT.                               
                                                                                
           PERFORM 2000-READ-F205      THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       3100-GET-START-END-DATE.                                                 
      *************************************************************             
                                                                                
           MOVE SPACES     TO WS-START-DATE                                     
                              WS-END-DATE                                       
                              WS-WORK-END-DATE.                                 
                                                                                
           MOVE F205-DTE-ALLOCN (1:4)   TO WS-START-DATE-CCYY                   
                                           WS-END-DATE-CCYY                     
                                           WS-WORK-END-DATE-CCYY.               
           MOVE F205-DTE-ALLOCN (5:2)   TO WS-START-DATE-MM                     
                                           WS-END-DATE-MM                       
                                           WS-WORK-END-DATE-MM.                 
           MOVE F205-DTE-ALLOCN (7:2)   TO WS-START-DATE-DD.                    
                                                                                
           MOVE  WS-START-DATE         TO WS-IN-DATE.                           
           MOVE 'LM'                   TO WS-IN-OPTION.                         
           MOVE SPACES                 TO WS-RET-GEN.                           
                                                                                
           CALL 'AV02C001' USING LINK-REC.                                      
                                                                                
           IF WS-RETURN = 'EID'                                                 
              DISPLAY 'ERROR IN DATE : ' WS-IN-DATE                             
           END-IF.                                                              
                                                                                
           IF WS-RET-MM = SPACES                                                
              DISPLAY 'ERROR IN CALLING AV02C001'                               
           ELSE                                                                 
              MOVE WS-RET-MM     TO  WS-WORK-END-DATE-DD                        
                                     WS-END-DATE-DD                             
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3400-READ-K270.                                                          
      *-----------------------------------------------------------*             
                                                                                
           MOVE SPACES                    TO K270-KEY-FLD.                      
           MOVE F205-DTE-ALLOCN           TO K270-ALLOCN-DATE.                  
           MOVE ZEROS                     TO WS-FLAT-OFFER.                     
                                                                                
           START BP13K270 KEY NOT < K270-KEY-FLD.                               
           IF K270-STATUS = 00 OR 02                                            
              CONTINUE                                                          
           ELSE                                                                 
           IF K270-STATUS = 23                                                  
              DISPLAY 'RECORD NOT FOUND IN BP13K270'                            
              DISPLAY 'K270-KEY-FLD = ' K270-KEY-FLD                            
              GO TO 3400-EXIT                                                   
           ELSE                                                                 
              MOVE K270-STATUS           TO RETURN-CODE                         
              DISPLAY 'START ERROR READING BP13K270 FILE, ' K270-STATUS         
              DISPLAY 'K270-KEY-FLD = ' K270-KEY-FLD                            
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                          
                                                                                
      * TO GET THE SUBSCRIPT FOR K270-NUM-ALLOC BASED ON                        
      * K100-NUM-FLAT-TYPE                                                      
                                                                                
           IF F205-NUM-FLAT-TYPE = '1 '                                         
              MOVE 1 TO WS-FT-NO                                                
           ELSE                                                                 
           IF F205-NUM-FLAT-TYPE = '2 '                                         
              MOVE 2 TO WS-FT-NO                                                
           ELSE                                                                 
           IF F205-NUM-FLAT-TYPE = '3 '                                         
              MOVE 3 TO WS-FT-NO                                                
           ELSE                                                                 
           IF F205-NUM-FLAT-TYPE = '4 ' OR '4D'                                 
              MOVE 4 TO WS-FT-NO                                                
           ELSE                                                                 
           IF F205-NUM-FLAT-TYPE = '4S'                                         
              MOVE 5 TO WS-FT-NO                                                
           ELSE                                                                 
           IF F205-NUM-FLAT-TYPE = '5 ' OR '5D'                                 
              MOVE 6 TO WS-FT-NO                                                
           ELSE                                                                 
           IF F205-NUM-FLAT-TYPE = 'E ' OR 'ED'                                 
              MOVE 7 TO WS-FT-NO                                                
           ELSE                                                                 
           IF F205-NUM-FLAT-TYPE = 'H '                                         
              MOVE 8 TO WS-FT-NO                                                
           ELSE                                                                 
           IF F205-NUM-FLAT-TYPE = 'SE'                                         
              MOVE 9 TO WS-FT-NO.                                               
                                                                                
           IF K270-STATUS = 0                                                   
              MOVE 'N'       TO K270-EOF                                        
              PERFORM 3500-ACCUM-FLATOFFER THRU 3500-EXIT                       
                UNTIL END-OF-K270.                                              
                                                                                
           PERFORM 3600-WRITETO-F222       THRU 3600-EXIT.                      
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3500-ACCUM-FLATOFFER.                                                    
      *-----------------------------------------------------------*             
           READ BP13K270 NEXT                                                   
                AT END                                                          
                   MOVE 'Y' TO K270-EOF                                         
                   GO 3500-EXIT.                                                
                                                                                
           IF K270-ALLOCN-DATE NOT = F205-DTE-ALLOCN                            
              MOVE 'Y' TO K270-EOF                                              
              GO 3500-EXIT.                                                     
                                                                                
           EVALUATE F205-NUM-FLAT-TYPE                                          
             WHEN '00'                                                          
              IF F205-NUM-NT-ZONE(2:1) IS NUMERIC                               
                 PERFORM 3560-STARTBR-BP13K813  THRU 3560-EXIT                  
                 IF K813-CDE-NT = K270-CDE-NT                                   
                    IF WS-ZONE-FND-FLAG = 'Y'                                   
                       MOVE 0 TO WS-ALLOC-TOTAL                                 
                       IF F205-NUM-NT-ZONE (1:2) = '17'                         
                         PERFORM 3510-COMPUTE-FOR-DUXTON THRU 3510-EXIT         
                       ELSE                                                     
                         PERFORM 3520-COMPUTE-FOR-BTO THRU 3520-EXIT            
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              ELSE                                                              
                 IF (K270-CDE-NT = F205-NUM-NT-ZONE) OR                         
                    (K270-CDE-APPL-ZONE = F205-NUM-NT-ZONE)                     
                     MOVE 0 TO WS-ALLOC-TOTAL                                   
                     COMPUTE WS-ALLOC-TOTAL = K270-NUM-ALLOC(4) +               
                                              K270-NUM-ALLOC(5) +               
                                              K270-NUM-ALLOC(6) +               
                                              K270-NUM-ALLOC(7)                 
                     ADD WS-ALLOC-TOTAL TO WS-FLAT-OFFER                        
                     IF WS-ALLOC-TOTAL > 0                                      
                        PERFORM 3700-READ-BM06K100   THRU 3700-EXIT             
                        PERFORM 4000-WRITE-BP13K278  THRU 4000-EXIT             
                     END-IF                                                     
                 ELSE                                                           
                    IF F205-NUM-NT-ZONE = '00'                                  
                        MOVE 0 TO WS-ALLOC-TOTAL                                
                        COMPUTE WS-ALLOC-TOTAL = K270-NUM-OFFER(4)              
                        ADD WS-ALLOC-TOTAL TO WS-FLAT-OFFER                     
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
             WHEN OTHER                                                         
              IF (K270-CDE-NT = F205-NUM-NT-ZONE) OR                            
                 (K270-CDE-APPL-ZONE = F205-NUM-NT-ZONE)                        
                  IF WS-FT-NO = 9                                               
                     ADD K270-NUM-ALLOC(3) TO WS-FLAT-OFFER                     
                     ADD K270-NUM-ALLOC(4) TO WS-FLAT-OFFER                     
                     ADD K270-NUM-ALLOC(5) TO WS-FLAT-OFFER                     
                     ADD K270-NUM-ALLOC(6) TO WS-FLAT-OFFER                     
                  ELSE                                                          
                     IF K270-NUM-COMBINE-SA = 'Y'                               
                        PERFORM 3550-COMPUTE-FOR-COMBINE-SA                     
                              THRU 3550-EXIT                                    
                     ELSE                                                       
                        ADD K270-NUM-ALLOC(WS-FT-NO) TO WS-FLAT-OFFER           
                     END-IF                                                     
                  END-IF                                                        
                  IF F205-DTE-BALLOT >= '199804'                                
                     MOVE 0 TO WS-ALLOC-TOTAL                                   
                     EVALUATE F205-NUM-FLAT-TYPE                                
                       WHEN '2 '                                                
                          MOVE K270-NUM-ALLOC(2) TO WS-ALLOC-TOTAL              
                       WHEN '3 '                                                
                          MOVE K270-NUM-ALLOC(3) TO WS-ALLOC-TOTAL              
                       WHEN '4 '                                                
                          COMPUTE WS-ALLOC-TOTAL = K270-NUM-ALLOC(4) +          
                                                   K270-NUM-ALLOC(5)            
                       WHEN '5 '                                                
                          MOVE K270-NUM-ALLOC(6) TO WS-ALLOC-TOTAL              
                       WHEN 'E '                                                
                          COMPUTE WS-ALLOC-TOTAL = K270-NUM-ALLOC(7) +          
                                                   K270-NUM-ALLOC(9)            
                       WHEN 'SE'                                                
                          COMPUTE WS-ALLOC-TOTAL = K270-NUM-ALLOC(3) +          
                                                   K270-NUM-ALLOC(4) +          
                                                   K270-NUM-ALLOC(5) +          
                                                   K270-NUM-ALLOC(6)            
                     END-EVALUATE                                               
                                                                                
                     IF WS-ALLOC-TOTAL > 0                                      
                        PERFORM 3700-READ-BM06K100   THRU 3700-EXIT             
                        PERFORM 4000-WRITE-BP13K278  THRU 4000-EXIT             
                     END-IF                                                     
                  END-IF                                                        
              END-IF                                                            
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3510-COMPUTE-FOR-DUXTON.                                                 
      *-----------------------------------------------------------*             
           COMPUTE WS-ALLOC-TOTAL = K270-NUM-OFFER(4) +                         
                                    K270-NUM-OFFER(6).                          
                                                                                
           MOVE WS-ALLOC-TOTAL TO WS-FLAT-OFFER.                                
                                                                                
       3510-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3520-COMPUTE-FOR-BTO.                                                    
      *-----------------------------------------------------------*             
           EVALUATE K813-NUM-FLAT-TYPE(1:1)                                     
              WHEN '1'                                                          
                  COMPUTE WS-ALLOC-TOTAL = WS-FLAT-OFFER     +                  
                                           K270-NUM-OFFER(1) +                  
                                           K270-NUM-OFFER(2)                    
                  MOVE WS-ALLOC-TOTAL TO WS-FLAT-OFFER                          
              WHEN '2'                                                          
                  COMPUTE WS-ALLOC-TOTAL = WS-FLAT-OFFER  +                     
                                           K270-NUM-OFFER(2)                    
                  MOVE WS-ALLOC-TOTAL TO WS-FLAT-OFFER                          
              WHEN '3'                                                          
                  ADD  K270-NUM-OFFER(3) TO WS-FLAT-OFFER                       
                  MOVE K270-NUM-OFFER(3) TO WS-ALLOC-TOTAL                      
              WHEN '4'                                                          
                  ADD  K270-NUM-OFFER(4) TO WS-FLAT-OFFER                       
                  MOVE K270-NUM-OFFER(4) TO WS-ALLOC-TOTAL                      
              WHEN '5'                                                          
                  ADD  K270-NUM-OFFER(6) TO WS-FLAT-OFFER                       
                  MOVE K270-NUM-OFFER(6) TO WS-ALLOC-TOTAL                      
              WHEN OTHER                                                        
                  DISPLAY 'BP13K813 - INVALID FLAT TYPE: '                      
                           K813-NUM-FLAT-TYPE                                   
           END-EVALUATE.                                                        
                                                                                
       3520-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3550-COMPUTE-FOR-COMBINE-SA.                                             
      *-----------------------------------------------------------*             
           COMPUTE WS-ALLOC-TOTAL = K270-NUM-OFFER(1) +                         
                                    K270-NUM-OFFER(2).                          
                                                                                
           MOVE WS-ALLOC-TOTAL TO WS-FLAT-OFFER.                                
                                                                                
       3550-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3560-STARTBR-BP13K813.                                                   
      *-----------------------------------------------------------*             
                                                                                
           MOVE SPACES                     TO BP13K813-REC.                     
           INITIALIZE                         BP13K813-REC.                     
                                                                                
           MOVE 'N'                        TO WS-EOF-K813                       
                                              WS-ZONE-FND-FLAG.                 
                                                                                
           MOVE F205-NUM-NT-ZONE           TO K813-NUM-ZONE.                    
                                                                                
           START BP13K813 KEY >= K813-NUM-ZONE.                                 
                                                                                
           EVALUATE K813-STATUS                                                 
           WHEN 00                                                              
           WHEN 02                                                              
                PERFORM 3570-READNEXT-BP13K813 THRU 3570-EXIT                   
                 UNTIL WS-EOF-K813 = 'Y' OR WS-ZONE-FND-FLAG = 'Y'              
           WHEN 10                                                              
           WHEN 23                                                              
                CONTINUE                                                        
           WHEN OTHER                                                           
                DISPLAY 'START ERROR BP13K825 : ' K813-STATUS                   
                        ' ZONE : ' K813-NUM-ZONE                                
                PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       3560-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------*             
       3570-READNEXT-BP13K813.                                                  
      *-----------------------------------------------------------*             
                                                                                
           READ BP13K813 NEXT RECORD                                            
                AT END MOVE 'Y' TO WS-EOF-K813.                                 
                                                                                
           EVALUATE K813-STATUS                                                 
           WHEN 00                                                              
           WHEN 02                                                              
                IF K813-NUM-ZONE   = F205-NUM-NT-ZONE                           
                   MOVE 'Y'        TO  WS-ZONE-FND-FLAG                         
                END-IF                                                          
           WHEN 10                                                              
           WHEN 23                                                              
                MOVE 'Y'           TO  WS-EOF-K813                              
           WHEN OTHER                                                           
                DISPLAY 'READNEXT ERROR BP13K813 : ' K813-STATUS                
                        ' ZONE : ' K813-NUM-ZONE                                
                PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       3570-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------------------------------------------*             
       3600-WRITETO-F222.                                                       
      *-----------------------------------------------------------*             
           MOVE SPACES                    TO BP13F222-REC.                      
           MOVE ZEROS                     TO F222-NUM-BOOKED.                   
           MOVE ZEROS                     TO F222-NUM-BKAPPMT.                  
           MOVE WS-FLAT-OFFER             TO F222-NUM-FLAT-OFFER.               
           MOVE F205-NUM-NT-ZONE          TO F222-NUM-NT-ZONE.                  
           MOVE F205-DTE-ALLOCN           TO F222-DTE-ALLOC.                    
           MOVE F205-DTE-BALLOT           TO F222-DTE-BALLOT.                   
           MOVE F205-NUM-FLAT-TYPE        TO F222-NUM-FLAT-TYPE.                
           MOVE F205-DTE-ALLOCN           TO F222-DTE-START-BKAPPMT.            
           MOVE WS-END-DATE               TO F222-DTE-END-BKAPPMT.              
           MOVE 'P13C296'                 TO F222-NUM-USERID.                   
           MOVE FUNCTION CURRENT-DATE     TO F222-DTE-UPDATE.                   
                                                                                
           WRITE BP13F222-REC.                                                  
           ADD 1  TO WS-F222-WRITE.                                             
                                                                                
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       3700-READ-BM06K100.                                                      
      *************************************************************             
           INITIALIZE K100-REC.                                                 
           MOVE K270-ESTATE               TO K100-ESTATE.                       
           MOVE K270-NEIGHBOURHOOD        TO K100-NEIGHBOURHOOD.                
           MOVE K270-CONTRACT-NO          TO K100-CONTRACT-NO.                  
           MOVE K270-NUM-BLK              TO K100-BLK-NO.                       
                                                                                
           READ BM06K100                                                        
           EVALUATE K100-STATUS                                                 
                                                                                
           WHEN 00                                                              
              MOVE K100-CDE-BLK  TO WS-CDE-BLK                                  
              ADD 1 TO WS-K100-READ                                             
              IF K100-DTE-OCS-DUE = SPACES OR LOW-VALUES OR ZEROES              
                 MOVE ZEROES TO WS-OCS-DUE-DATE                                 
              ELSE                                                              
                 MOVE K100-DTE-OCS-DUE TO WS-OCS-DUE-DATE                       
              END-IF                                                            
                                                                                
           WHEN 23                                                              
              MOVE SPACES        TO WS-CDE-BLK                                  
              DISPLAY 'REC NOTFD IN BM06K100 = '                                
                       K100-KEY-FLD OF K100-REC                                 
                                                                                
           WHEN OTHER                                                           
              MOVE K100-STATUS            TO RETURN-CODE                        
              DISPLAY 'ERROR READING BM06K100 FILE, ' K100-STATUS               
              PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                         
                                                                                
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       4000-WRITE-BP13K278.                                                     
      *************************************************************             
                                                                                
           INITIALIZE BP13K278-REC.                                             
           MOVE SPACES TO BP13K278-REC.                                         
                                                                                
           PERFORM 4100-GET-STREET-NAME THRU 4100-EXIT.                         
                                                                                
           IF (WS-OCS-DUE-DATE >= WS-WORK-END-DATE)                             
               MOVE 'Y' TO K278-NUM-OCS                                         
           ELSE                                                                 
               MOVE 'N' TO K278-NUM-OCS                                         
           END-IF.                                                              
                                                                                
           MOVE F205-NUM-NT-ZONE          TO K278-NUM-NT-ZONE.                  
           MOVE F205-DTE-ALLOCN           TO K278-DTE-ALLOCN.                   
           MOVE F205-NUM-FLAT-TYPE        TO K278-NUM-FLAT-TYPE.                
           MOVE K100-CDE-BLK              TO K278-CDE-BLK.                      
           MOVE K100-ESTATE               TO K278-NUM-ESTATE.                   
           MOVE K100-NEIGHBOURHOOD        TO K278-NUM-NEIGHBOUR.                
           MOVE K100-CONTRACT-NO          TO K278-NUM-CONTR.                    
           MOVE K100-BLK-NO               TO K278-NUM-BLK.                      
           MOVE WS-END-DATE               TO K278-DTE-END-BKAPPMT.              
           MOVE 'P13C296'                 TO K278-NUM-USERID.                   
           MOVE FUNCTION CURRENT-DATE     TO K278-DTE-UPDATE.                   
           PERFORM 4050-WRITE-BP13K278  THRU 4050-EXIT.                         
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       4050-WRITE-BP13K278.                                                     
      *************************************************************             
                                                                                
           WRITE BP13K278-REC.                                                  
                                                                                
           IF K278-STATUS = 00                                                  
              ADD 1                       TO WS-K278-WRITE                      
           ELSE                                                                 
              IF K278-STATUS = 22                                               
                 MOVE 'UPDK278'           TO K278-NUM-USERID                    
                 REWRITE BP13K278-REC                                           
                 IF K278-STATUS NOT = 00                                        
                    MOVE K278-STATUS      TO RETURN-CODE                        
                    DISPLAY 'ERROR IN UPDATING BP13K278'                        
                    DISPLAY 'K278-KEY-FLD = ' K278-KEY-FLD                      
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
                 END-IF                                                         
                 ADD 1                    TO WS-K278-UPDATE                     
              ELSE                                                              
                 MOVE K278-STATUS         TO RETURN-CODE                        
                 DISPLAY 'ERROR WRITING BP13K278 FILE, ' K278-STATUS            
                 DISPLAY 'K278-KEY-FLD = ' K278-KEY-FLD                         
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       4100-GET-STREET-NAME.                                                    
      *************************************************************             
      *--> GET STREET NAME                                                      
      *--> AS PIDB INTERFACE TABLE IS BY UNIT BASIS, CATER FOR -811             
      *    TO AVOID ABEND.                                                      
                                                                                
           EXEC SQL SELECT NME_STREET                                           
                    INTO :DCLPIDB-INTERFACE.NME-STREET                          
                    FROM PIDB_INTERFACE                                         
                    WHERE NUM_BLDNG_GL = :WS-CDE-BLK                            
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN 000                                                             
           WHEN -811                                                            
              MOVE NME-STREET  TO K278-NME-STREET                               
           WHEN 100                                                             
              DISPLAY 'REC NOTFD IN PIDB INTERFACE TABLE = ' WS-CDE-BLK         
              MOVE SPACES      TO K278-NME-STREET                               
              GO TO 4100-EXIT                                                   
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN READING PIDB INTERFACE TABLE '                  
              GO TO 9900-ABORT-PROCESS                                          
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       9000-CLOSE-FILES.                                                        
      *************************************************************             
           DISPLAY '***************** BP13C296 *****************'.              
           DISPLAY 'TOTAL RECORDS READ    (BP13F205) = ' WS-F205-READ.          
           DISPLAY 'TOTAL RECORDS WRITTEN (BP13F222) = ' WS-F222-WRITE.         
           DISPLAY 'TOTAL RECORDS UPDATED (BP13F222) = ' WS-F222-UPDATE.        
           DISPLAY 'TOTAL RECORDS WRITTEN (BP13K278) = ' WS-K278-WRITE.         
           DISPLAY 'TOTAL RECORDS UPDATED (BP13K278) = ' WS-K278-UPDATE.        
                                                                                
           CLOSE BP13F205                                                       
                 BP13K270                                                       
                 BP13K278                                                       
                 BM06K100                                                       
                 BP13F222.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       9900-ABORT-PROCESS.                                                      
      *************************************************************             
           CALL 'DBATIAR' USING SQLCA.                                          
           MOVE SQLCODE       TO RETURN-CODE.                                   
           GOBACK.                                                              
                                                                                
