       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C622.                                                  
      *DATE-WRITTEN. 13 OCT 2016.                                               
      *AUTHOR.       KRISTINE LANTIN.                                           
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: 1) To update BP13K62A with IRAS Acknowledgment *            
      *                file.                                       *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP136262 20160823 KVL1  NEW PROGRAM                        *            
      * BP136262 20160823 KVL1  To process empty file first        *            
      * BP138916 20220926 KAC1  TO CATER FOR NEW 12 BYTES FIELD FOR*            
      *                            K62A-REF-CASE-REF-NO-NEW.       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *-------------------------------------------------------------            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F41D  ASSIGN        TO BP13F41D.                          
                                                                                
           SELECT BP13F41H  ASSIGN        TO BP13F41H.                          
                                                                                
           SELECT BP13F41T  ASSIGN        TO BP13F41T.                          
                                                                                
           SELECT BP13F41R  ASSIGN        TO BP13F41R.                          
                                                                                
           SELECT BP13K62A  ASSIGN        TO BP13K62A                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K62A-KEY-FLD                       
                            FILE STATUS   IS BP13K62A-STATUS.                   
                                                                                
           SELECT BP13F622  ASSIGN        TO BP13F622.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F41D                                                             
           RECORD CONTAINS 100  CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       COPY BP13F416.                                                           
                                                                                
       FD  BP13F41H                                                             
           RECORD CONTAINS 100  CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       01  BP13F41H-REC         PIC X(100).                                     
                                                                                
       FD  BP13F41T                                                             
           RECORD CONTAINS 100  CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       01  BP13F41T-REC         PIC X(100).                                     
                                                                                
       FD  BP13F41R                                                             
           RECORD CONTAINS 100  CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       01  BP13F41R-REC.                                                        
           05 F41R-LABEL        PIC X(07).                                      
           05 F41R-COUNT        PIC 9(10).                                      
           05 FILLER            PIC X(83).                                      
                                                                                
       FD  BP13K62A                                                             
           RECORD CONTAINS 1200 CHARACTERS.                                     
       COPY BP13K62A.                                                           
                                                                                
       FD  BP13F622                                                             
           RECORD CONTAINS 100  CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       01  BP13F622-REC         PIC X(100).                                     
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BP13K62A-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  WS-K62A-FND         PIC X(01)  VALUE SPACE.                          
       01  WS-BP13F41D-EOF     PIC X(01)  VALUE SPACE.                          
       01  WS-BP13F41H-EOF     PIC X(01)  VALUE SPACE.                          
       01  WS-BP13F41T-EOF     PIC X(01)  VALUE SPACE.                          
       01  WS-BP13F41R-EOF     PIC X(01)  VALUE SPACE.                          
       01  WS-READ-DETAIL-CNT  PIC 9(10)  VALUE ZEROES.                         
       01  WS-READ-HEAD-CNT    PIC 9(10)  VALUE ZEROES.                         
       01  WS-READ-TRAIL-CNT   PIC 9(10)  VALUE ZEROES.                         
       01  WS-WRITE-CNT        PIC 9(10)  VALUE ZEROES.                         
       01  WS-MISMATCH-CNT     PIC 9(10)  VALUE ZEROES.                         
       01  WS-HEAD-DTL.                                                         
           05 WS-TABLE-ID-HEADER                                                
                               PIC X(06)  VALUE SPACES.                         
           05 WS-NUM-DOCS      PIC 9(08)  VALUE ZEROES.                         
           05 WS-DTE-FILE-SENT PIC X(08)  VALUE SPACES.                         
           05 WS-DTE-HDB-SENT  PIC X(08)  VALUE SPACES.                         
           05 WS-DTE-PROCESS   PIC X(08)  VALUE SPACES.                         
           05 WS-FILE-LVL-REJ-CODE                                              
                               PIC X(03)  VALUE SPACES.                         
           05 WS-NUM-DOCS-SUCCESS                                               
                               PIC 9(08)  VALUE ZEROES.                         
           05 WS-NUM-DOCS-FAIL PIC 9(08)  VALUE ZEROES.                         
           05 FILLER           PIC X(43)  VALUE SPACES.                         
       01  WS-TRAIL-DTL.                                                        
           05 WS-TABLE-ID-TRAIL                                                 
                               PIC X(06)  VALUE SPACES.                         
           05 WS-NUM-TOTAL-ROWS                                                 
                               PIC 9(10)  VALUE ZEROES.                         
           05 FILLER           PIC X(84)  VALUE SPACES.                         
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F41D       THRU 2000-EXIT.                     
           IF WS-BP13F41D-EOF = 'Y'                                             
              DISPLAY 'FILE BP13F416 IS  EMPTY'                                 
              PERFORM 9000-CLOSE-ROUTINE                                        
                 THRU 9000-EXIT                                                 
           END-IF                                                               
           PERFORM 1100-READ-HEADER         THRU 1100-EXIT                      
             UNTIL WS-BP13F41H-EOF = 'Y'.                                       
           IF WS-READ-HEAD-CNT = 0                                              
              DISPLAY 'IRAS ACK FILE HAS NO HEADER'                             
              MOVE 94                       TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE                                        
                 THRU 9000-EXIT                                                 
           ELSE                                                                 
              IF WS-READ-HEAD-CNT > 1                                           
                 DISPLAY 'IRAS ACK FILE HAS MORE THAN 1 HEADER'                 
                 MOVE 95                    TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
              ELSE                                                              
                 MOVE BP13F41H-REC          TO WS-HEAD-DTL                      
                 IF WS-FILE-LVL-REJ-CODE   NOT = SPACES AND LOW-VALUES          
                    DISPLAY 'THIS BATCH SENTIRAS FILE NOT PROCESSED  DUE        
      -                ' TO A FILE REJ CODE OF ' F416-FILE-LVL-REJ-CODE         
                    MOVE 96                 TO RETURN-CODE                      
                    PERFORM 9000-CLOSE-ROUTINE                                  
                       THRU 9000-EXIT                                           
                 END-IF                                                         
                 PERFORM 1200-READ-TRAILER  THRU 1200-EXIT                      
                   UNTIL WS-BP13F41T-EOF = 'Y'                                  
                 IF WS-READ-TRAIL-CNT = 0                                       
                    DISPLAY 'IRAS ACK FILE HAS NO TRAILER'                      
                    MOVE 97                 TO RETURN-CODE                      
                    PERFORM 9000-CLOSE-ROUTINE                                  
                       THRU 9000-EXIT                                           
                 ELSE                                                           
                    IF WS-READ-TRAIL-CNT > 1                                    
                       DISPLAY 'IRAS ACK FILE HAS MORE THAN 1 TRAILER'          
                       MOVE 98              TO RETURN-CODE                      
                       PERFORM 9000-CLOSE-ROUTINE                               
                          THRU 9000-EXIT                                        
                    ELSE                                                        
                       MOVE BP13F41T-REC      TO WS-TRAIL-DTL                   
                       PERFORM 1300-READ-ROWS THRU 1300-EXIT                    
                         UNTIL WS-BP13F41R-EOF = 'Y'                            
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
           PERFORM 3000-PROCESS-DATA        THRU 3000-EXIT                      
             UNTIL WS-BP13F41D-EOF = 'Y'.                                       
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY '@ - 1000-OPEN-FILES'                                        
           OPEN INPUT  BP13F41D                                                 
                       BP13F41H                                                 
                       BP13F41T                                                 
                       BP13F41R                                                 
                I-O    BP13K62A                                                 
                OUTPUT BP13F622.                                                
                                                                                
           IF BP13K62A-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K62A OPEN ERROR, STATUS : ' BP13K62A-STATUS          
              MOVE     BP13K62A-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1100-READ-HEADER.                                                        
      *-------------------------------------------------------------            
           DISPLAY '@ - 1100-READ-HEADER'                                       
           READ BP13F41H AT END                                                 
                         MOVE 'Y'  TO WS-BP13F41H-EOF                           
                         NOT AT END                                             
                         ADD  1    TO WS-READ-HEAD-CNT                          
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1200-READ-TRAILER.                                                       
      *-------------------------------------------------------------            
           DISPLAY '@ - 1200-READ-TRAILER'                                      
           READ BP13F41T AT END                                                 
                         MOVE 'Y'  TO WS-BP13F41T-EOF                           
                         NOT AT END                                             
                         ADD  1    TO WS-READ-TRAIL-CNT                         
           END-READ.                                                            
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1300-READ-ROWS.                                                          
      *-------------------------------------------------------------            
           DISPLAY '@ - 1300-READ-ROWS'                                         
                                                                                
           READ BP13F41R AT END                                                 
                    MOVE 'Y'   TO WS-BP13F41R-EOF                               
                    IF F41R-COUNT NOT = WS-NUM-TOTAL-ROWS                       
                       DISPLAY 'IRAS ACK FILE NO. OF ROWS DO NOT TALLY'         
                       MOVE 99 TO RETURN-CODE                                   
                       PERFORM 9000-CLOSE-ROUTINE                               
                          THRU 9000-EXIT                                        
                    END-IF                                                      
           END-READ.                                                            
                                                                                
       1300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F41D.                                                      
      *-------------------------------------------------------------            
           DISPLAY '@ - 2000-READ-BP13F41D'                                     
                                                                                
           READ BP13F41D AT END                                                 
                         MOVE 'Y'  TO WS-BP13F41D-EOF                           
                         NOT AT END                                             
                         ADD  1    TO WS-READ-DETAIL-CNT                        
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-BP13K62A.                                                      
      *-------------------------------------------------------------            
           DISPLAY '@ - 2100-READ-BP13K62A'                                     
           DISPLAY ' '.                                                         
           READ BP13K62A.                                                       
                                                                                
           EVALUATE BP13K62A-STATUS                                             
              WHEN 00                                                           
                 DISPLAY 'IRAS-DOC-REF-K625=' K62A-NUM-IRAS-DOC-REF-K625        
                 DISPLAY 'F416-NUM-IRAS-DOC-REF=' F416-NUM-IRAS-DOC-REF         
                 IF K62A-NUM-IRAS-DOC-REF-K625 =                                
                    F416-NUM-IRAS-DOC-REF                                       
                    MOVE 'Y'             TO WS-K62A-FND                         
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
                 MOVE 'N'             TO   WS-K62A-FND                          
                 DISPLAY 'RECORD NOT FOUND IN BP13K62A. KEY IS '                
                          F416-NUM-HDB-REF ' ' F416-NUM-IRAS-DOC-REF            
                 MOVE  SPACES         TO   BP13F622-REC                         
                 WRITE BP13F622-REC   FROM BP13F416-DETAIL                      
                 ADD 1                TO   WS-MISMATCH-CNT                      
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K62A. STATUS IS '                   
                          BP13K62A-STATUS                                       
                 MOVE BP13K62A-STATUS TO   RETURN-CODE                          
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2200-REWRITE-BP13K62A.                                                   
      *-------------------------------------------------------------            
           DISPLAY '@ - 2200-REWRITE-BP13K62A'                                  
           DISPLAY ' '.                                                         
           REWRITE BP13K62A-REC.                                                
                                                                                
           EVALUATE BP13K62A-STATUS                                             
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR REWRITING BP13K62A FOR KEY '                    
                          F416-NUM-HDB-REF                                      
                 MOVE BP13K62A-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
           DISPLAY '@ - 3000-PROCESS-DATA'                                      
           DISPLAY ' '.                                                         
           MOVE SPACES                TO   BP13K62A-REC                         
           MOVE F416-NUM-HDB-REF      TO   K62A-NUM-HDB-REF                     
           MOVE 'ALEO1'               TO   K62A-STAMP-DOC-TYPE                  
           PERFORM 2100-READ-BP13K62A THRU 2100-EXIT                            
           IF WS-K62A-FND = 'Y'                                                 
              MOVE F416-REFUND-CASE-REF                                         
                TO K62A-REF-CASE-REF-NO                                         
                   K62A-REF-CASE-REF-NO-NEW                                     
              MOVE F416-CASE-LVL-REJ-CODE                                       
                TO K62A-CDE-REJECTN                                             
              MOVE WS-DTE-PROCESS                                               
                TO K62A-DTE-IRAS-PROCESS-REFUND                                 
              MOVE WS-DTE-FILE-SENT                                             
                TO K62A-DTE-IRAS-ACK-FILE-SENT                                  
              PERFORM 2200-REWRITE-BP13K62A THRU 2200-EXIT                      
              COMPUTE WS-WRITE-CNT = WS-WRITE-CNT + 1                           
           END-IF.                                                              
           PERFORM 2000-READ-BP13F41D       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '--------- BP13C622 CONTROL TOTAL ----------'.               
           DISPLAY ' NO OF DETAIL RECS READ FROM BP13F416   = '                 
                     WS-READ-DETAIL-CNT.                                        
           DISPLAY ' NO OF RECS UPDATED IN BP13K62A         = '                 
                     WS-WRITE-CNT.                                              
           DISPLAY ' NO OF MISMATCH RECS                    = '                 
                     WS-MISMATCH-CNT.                                           
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BP13F41D                                                 
                       BP13F41H                                                 
                       BP13F41T                                                 
                       BP13F41R                                                 
                       BP13K62A                                                 
                       BP13F622.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
