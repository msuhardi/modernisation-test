       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C709.                                                 
      *AUTHOR.        RAYATHATHA ASHOK.                                         
      *DATE-WRITTEN.  30/06/92.                                                 
      *============================================================*            
      *  SYSTEM OF COMMITMENT (BP13)                               *            
      *============================================================*            
      *  OBJECTIVE : PRINT REPORT TO SHOW THE JB LINKS ON THE      *            
      *              UPDATING OF LARGER COHORTS, LARGER WAITING    *            
      *              WAITING TIME, SMALLEST RANDOM NUMBER,         *            
      *              HOUSEHOLE TYPE.                               *            
      *                                                            *            
      *   INPUT FILES:  1.  BP13F520                               *            
      *                 2.  BP13K520                               *            
      *                                                            *            
      *   OUTPUT FILE : 1.  BP13L709                               *            
      *------------------------------------------------------------*            
      * BP130340 19971115 SSS  REMOVE F520-NUM-JB-REF              *            
      * BP130652 19990220 NOR  EXPAND F520 & K520 RECL 300 TO 500  *            
      * BP132317 20030310 ROM1 TO USE REGN-NO IN READING BP13K520  *            
      *                        INSTEAD OF ALTERNATE KEY (REF NO)   *            
      * BP132317 20030318 ROM1 TO PRINT S/N, CONTROL TOTALS, NT1   *            
      *                        AND FLAT TYPE                       *            
      *============================================================*            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13K520 ASSIGN TO P13K520                                    
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K520-KEY-NEW                               
                  FILE STATUS     IS BP13K520-STATUS.                           
                                                                                
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
                                                                                
           SELECT BP13L709 ASSIGN TO BP13L709.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F520                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F520.                                                           
                                                                                
       FD   BP13K520                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13K520.                                                           
                                                                                
       FD   BP13L709                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED.                                            
                                                                                
       01  BP13L709-REC                  PIC X(132).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-ACCESS-STATUS.                                               
           05  BP13K520-STATUS           PIC 9(2)  VALUE ZEROES.                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F520-EOF               PIC X     VALUE 'N'.                   
               88 F520-EOF                         VALUE 'Y'.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-LINE                   PIC 9(2)  VALUE 70.                    
           05  WS-PAGE                   PIC 9(4)  VALUE ZEROES.                
           05  WS-K520-READ              PIC 9(6)  VALUE ZEROES.                
           05  WS-F520-READ              PIC 9(6)  VALUE ZEROES.                
           05  WS-NUM-PRINT              PIC 9(6)  VALUE ZEROES.                
           05  WS-SERIAL-NO              PIC 9(5)  VALUE ZEROES.                
                                                                                
       01  WS-DATE                       PIC 9(8)  VALUE ZEROES.                
                                                                                
       01  L709-HEAD1.                                                          
           03  FILLER              PIC X(14)   VALUE 'BP13L709'.                
           03  FILLER              PIC X(8)    VALUE 'HDB3'.                    
           03  FILLER              PIC X(18)   VALUE SPACES.                    
           03  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           03  FILLER              PIC X(18)   VALUE SPACES.                    
           03  FILLER              PIC X(7)    VALUE 'DATE: '.                  
           03  HEAD1-DATE          PIC X(10)   VALUE SPACES.                    
           03  FILLER              PIC X(8)    VALUE ' PAGE: '.                 
           03  HEAD1-PAGE          PIC ZZZZ9.                                   
                                                                                
       01  L709-HEAD2.                                                          
           03  FILLER              PIC X(35) VALUE SPACES.                      
           03  FILLER              PIC X(70) VALUE                              
               'REPORT  ON  UPDATING  SMALLEST  JB  RANDOM  NO  FOR  THE        
      -        '  JB  LINKS'.                                                   
                                                                                
       01  L709-HEAD3.                                                          
           05  FILLER              PIC X(5)    VALUE ' S/NO'.                   
           05  FILLER              PIC X(4)    VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'REGN NO'.                 
           05  FILLER              PIC X(4)    VALUE SPACES.                    
           05  FILLER              PIC X(10)   VALUE 'JB REGN NO'.              
           05  FILLER              PIC X(3)    VALUE SPACES.                    
           05  FILLER              PIC X(13)   VALUE 'OLD RANDOM NO'.           
           05  FILLER              PIC X(18)   VALUE SPACES.                    
           05  FILLER              PIC X(13)   VALUE 'NEW RANDOM NO'.           
           05  FILLER              PIC X(19)   VALUE SPACES.                    
           05  FILLER              PIC X(3)    VALUE 'NT1'.                     
           05  FILLER              PIC X(4)    VALUE SPACES.                    
           05  FILLER              PIC X(9)    VALUE 'FLAT TYPE'.               
                                                                                
       01  L709-DETAIL.                                                         
           03  L709-SERIAL-NO      PIC ZZZZ9.                                   
           03  FILLER              PIC X(4)   VALUE SPACES.                     
           03  L709-REGN-NO        PIC X(8).                                    
           03  FILLER              PIC X(3)   VALUE SPACES.                     
           03  L709-JB-REGN-NO     PIC X(8).                                    
           03  FILLER              PIC X(5)   VALUE SPACES.                     
           03  L709-PRV-RANDOM     PIC X(28).                                   
           03  FILLER              PIC X(3)   VALUE SPACES.                     
           03  L709-UPD-RANDOM     PIC X(28).                                   
           03  FILLER              PIC X(4)   VALUE SPACES.                     
           03  L709-CDE-NT1        PIC X(3).                                    
           03  FILLER              PIC X(8)   VALUE SPACES.                     
           03  L709-CDE-FLAT-TYPE  PIC X(2).                                    
                                                                                
       01  L709-CONTROL-STATEMENT-1.                                            
           05 FILLER                     PIC X(30) VALUE SPACES.                
           05 FILLER                     PIC X(54) VALUE                        
              'TOTAL NUMBER OF RECORDS PROCESSED          :'.                   
           05 L709-TOT-REC               PIC ZZZZZ9.                            
                                                                                
       01  L709-CONTROL-STATEMENT-2.                                            
           05 FILLER                     PIC X(30) VALUE SPACES.                
           05 FILLER                     PIC X(54) VALUE                        
              'TOTAL NUMBER OF RECORDS READ (BP17F520)    :'.                   
           05 L709-F520-READ-CNT         PIC ZZZZZ9.                            
                                                                                
       01  L709-CONTROL-STATEMENT-3.                                            
           05 FILLER                     PIC X(30) VALUE SPACES.                
           05 FILLER                     PIC X(54) VALUE                        
              'TOTAL NUMBER OF RECORDS READ (BP17K520)    :'.                   
           05 L709-K520-READ-CNT         PIC ZZZZZ9.                            
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM 100-OPEN-FILES        THRU 100-EXIT.                         
           PERFORM 200-READ-BP13F520     THRU 200-EXIT.                         
           PERFORM 300-PROCESS-K520      THRU 300-EXIT                          
                   UNTIL F520-EOF.                                              
           PERFORM 900-CLOSE-ROUTINE     THRU 900-EXIT.                         
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN  INPUT BP13K520                                                 
                       BP13F520                                                 
                OUTPUT BP13L709.                                                
                                                                                
           IF BP13K520-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPEN ERROR FOR BP13K520, STATUS '                        
                                                BP13K520-STATUS                 
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                          
                                                                                
           ACCEPT WS-DATE                FROM DATE YYYYMMDD.                    
                                                                                
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/'  WS-DATE(1:4)               
              DELIMITED BY SIZE INTO HEAD1-DATE.                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-BP13F520.                                                       
      *-------------------------------------------------------------            
           READ BP13F520 AT END                                                 
              MOVE 'Y'                   TO WS-F520-EOF                         
              GO TO 200-EXIT.                                                   
           ADD   1                       TO WS-F520-READ.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-K520.                                                        
      *-------------------------------------------------------------            
           MOVE F520-NUM-REGN            TO K520-NUM-REGN.                      
           READ BP13K520 KEY IS K520-KEY-NEW.                                   
           IF BP13K520-STATUS = 00                                              
               ADD 1                     TO WS-K520-READ                        
               PERFORM 400-PRINT-REPORT  THRU 400-EXIT                          
           ELSE                                                                 
              IF BP13K520-STATUS = 23                                           
                 DISPLAY 'RECORD NOT FOUND IN BP13K520, STATUS '                
                         BP13K520-STATUS                                        
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K520, STATUS '                      
                         BP13K520-STATUS.                                       
                                                                                
           PERFORM 200-READ-BP13F520     THRU 200-EXIT.                         
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-PRINT-REPORT.                                                        
      *-------------------------------------------------------------            
           IF WS-LINE > 55                                                      
              PERFORM 500-PRINT-HEAD     THRU 500-EXIT.                         
                                                                                
           ADD 1                         TO WS-SERIAL-NO.                       
           MOVE WS-SERIAL-NO             TO L709-SERIAL-NO.                     
           MOVE F520-NUM-REGN            TO L709-REGN-NO.                       
           MOVE F520-NUM-RANDOM          TO L709-UPD-RANDOM.                    
           MOVE K520-NUM-RANDOM          TO L709-PRV-RANDOM.                    
           MOVE K520-NUM-JB-REGN         TO L709-JB-REGN-NO.                    
           MOVE K520-CDE-NT1             TO L709-CDE-NT1.                       
           MOVE K520-CDE-FLAT-TYPE       TO L709-CDE-FLAT-TYPE.                 
                                                                                
           WRITE BP13L709-REC            FROM L709-DETAIL AFTER 1.              
                                                                                
           ADD 1                         TO WS-LINE                             
                                            WS-NUM-PRINT.                       
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-PRINT-HEAD.                                                          
      *-------------------------------------------------------------            
           ADD  1                        TO WS-PAGE.                            
           MOVE WS-PAGE                  TO HEAD1-PAGE.                         
                                                                                
           WRITE BP13L709-REC     FROM L709-HEAD1 AFTER PAGE.                   
           WRITE BP13L709-REC     FROM L709-HEAD2 AFTER 1.                      
           WRITE BP13L709-REC     FROM L709-HEAD3 AFTER 2.                      
           MOVE  SPACES           TO   BP13L709-REC.                            
           WRITE BP13L709-REC.                                                  
           MOVE 7                 TO WS-LINE.                                   
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       800-PRINT-CONTROL-TOTAL.                                                 
      *-------------------------------------------------------------            
                                                                                
           MOVE  WS-F520-READ    TO   L709-TOT-REC                              
                                      L709-F520-READ-CNT.                       
           MOVE  WS-K520-READ    TO   L709-K520-READ-CNT                        
                                                                                
           WRITE BP13L709-REC   FROM  L709-CONTROL-STATEMENT-1 AFTER 2.         
           WRITE BP13L709-REC   FROM  L709-CONTROL-STATEMENT-2 AFTER 2.         
           WRITE BP13L709-REC   FROM  L709-CONTROL-STATEMENT-3 AFTER 2.         
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 800-PRINT-CONTROL-TOTAL THRU 800-EXIT.                       
                                                                                
           DISPLAY '*-------------- BP13C709 ---------------*'.                 
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM BP13F520 : ' WS-F520-READ.          
           DISPLAY 'NO OF RECORDS READ FROM BP13K520 : ' WS-K520-READ.          
           DISPLAY 'NO OF RECORDS PRINTED,  BP13L709 : ' WS-NUM-PRINT.          
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F520                                                       
                 BP13K520                                                       
                 BP13L709.                                                      
                                                                                
           IF BP13K520-STATUS NOT = 00 AND 97                                   
              DISPLAY '***  CLOSING ERROR, BP13K520-STATUS '                    
              BP13K520-STATUS.                                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
