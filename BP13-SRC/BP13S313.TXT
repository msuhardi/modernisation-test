 /*============================================================*/               
 /* SAS PROGRAM TO PRINT SUMMARY AGEING REPORT ON BOOKING FEE  */               
 /* CHG-NO   BY    ON     DESCRIPTION                          */               
 /* ---------------------------------                          */               
 /* BP130084 LSB 960228 - NEW PGM                              */               
 /* BP130130 EVM 960716 - ADD HEADING TITLE                    */               
 /* BP130130 LJL 960826 - TO REMOVE THE '<' AND '>' SIGNS.     */               
 /* BP130447 SRN 981229 - CHANGED THE INPUT FILE TO BP13F142.  */               
 /*============================================================*/               
  OPTIONS  NODATE NONUMBER;                                                     
                                                                                
    PROC FORMAT;                                                                
      PICTURE AMTEDT LOW-<0='000,000,000,000.00DR'                              
                          (FILL=' ' PREFIX='$' MULT=100.00000000001)            
                     0-HIGH='000,000,000,000.00CR'                              
                          (FILL=' ' PREFIX='$' MULT=100.00000000001);           
                                                                                
  DATA BP13F142 ;                                                               
     INFILE BP13F142;                                                           
     INPUT  @01  REGNO    $CHAR8.                                               
            @223 AMTBKPD  ZD9.2                                                 
            @249 MONTH    3.;                                                   
                                                                                
     FORMAT AMTBKPD AMTEDT.;                                                    
                                                                                
  PROC SORT; BY MONTH;                                                          
                                                                                
  DATA _NULLS_;                                                                 
    SET BP13F142 END=EOF;                                                       
    BY MONTH;                                                                   
                                                                                
    FILE BP13L313   NOTITLES HEADER=HH;                                         
                                                                                
    IF FIRST.MONTH THEN DO;                                                     
       CASENO=0; TOT_AMT=0;                                                     
       END;                                                                     
                                                                                
    CASENO+1; TOT_AMT+AMTBKPD;                                                  
                                                                                
    IF LAST.MONTH THEN                                                          
       DO;                                                                      
          MAX_MTH=MONTH+1;                                                      
          PUT @26  ' '                                                          
              @28  MONTH 3.                                                     
              @33  '  '                                                         
              @58  CASENO 6.                                                    
              @94  TOT_AMT AMTEDT.;                                             
       END;                                                                     
    CASETOT+1;                                                                  
    GRD_AMT+AMTBKPD;                                                            
    IF EOF THEN DO;                                                             
       PUT @54  '================='  @89  '=====================';              
       PUT @10  'GRAND TOTAL :'                                                 
           @56  CASETOT 8.  @94  GRD_AMT AMTEDT.;                               
       END;                                                                     
     RETURN;                                                                    
  HH:                                                                           
    PAGE+1;                                                                     
    CURRENT=DATE();                                                             
    PUT @1   'BP13L313      HDB3'                                               
        @41  'S Y S T E M   O F   C O M M I T M E N T'                          
        @90  'DATE : '      @98  CURRENT DDMMYY10.                              
        @120 'PAGE : '      @127 PAGE 3.;                                       
    PUT @40  'SUMMARY  AGEING  REPORT  ON  BOOKING  FEE';                       
    PUT @52  'FROM BOOKING DATE'                                                
        @90  'USER : FINANCE DEPT'//;                                           
    PUT @25  'BALANCE (MONTH)'                                                  
        @55  'NUMBER OF CASES'                                                  
        @92  'AMT-BOOKING-FEE';                                                 
    PUT @24  '-----------------'                                                
        @54  '-----------------'                                                
        @89  '---------------------';                                           
    RETURN;                                                                     
