       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C279.                                                 
      *AUTHOR.        MTD.                                                      
      *DATE-WRITTEN.  31/07/02                                                  
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE   :  TO EDIT RECORDS TO BE SENT TO INTV          *            
      *                                                            *            
      *  INPUT FILES :   BP13F230  -  FLATLIST FILE (RT 01,02,03)  *            
      *                               OR FLATLIST FILE (RT 05,06)  *            
      *                                                            *            
      *  OUTPUT FILE :   P13F230O  -  EDITED BP13F230              *            
      * ---------------------------------------------------------- *            
      * CHGE NO  OIC   DATE    DESCRIPTION                         *            
      * -------- ---- ------   ----------------------------------- *            
      * BP13XXXX MTD  31/07/02 NEW PROGRAM.                        *            
      * BP132243 MTD  14/11/02 EDIT RECORDS WITH NUM-CAT-FULL = 'F'*            
      * BP132283 MTD  11/01/03 EDIT RECORDS WITH SELTYPE = 'BE'   '*            
      * BP132278 JF4  28/03/03 CHGE BP13F230 LRECL TO 450.         *            
      * BP132379 MTD  14/08/03 COMPUTE FOR THE VALUE OF MAX CAT    *            
      * BP132426 MTD  11/10/03 EDIT RECORDS WITH SELTYPE = 'WIT'   *            
      * BP132426 MTD  18/10/03 CHANGE 'PUNGGOL 21-ON SITE' TO      *            
      *                        'PUNGGOL - ON SITE'                 *            
      * BP132785 LMS  22/12/05 TO CHANGE FLAT TYPE SA TO 7A        *            
      * BP132828 LMS  27/02/06 TO CHANGE 'WIS' TO 'WIR' FOR WIS    *            
      *                        3-ROOM.                             *            
      * BP133482 DW5  19/12/08 TO CHANGE FLAT TYPE S1 AND S2 TO    *            
      *                        1S AND 2S                           *            
      * BP133724 ESA1 12/10/09 CATER FOR BLOCK DETAILS(INTV06)     *            
      * BP133386 ESA1 28/04/10 MOVE '7A' TO 'SP' FLAT TYPE         *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F230  ASSIGN        TO  BP13F230.                         
                                                                                
           SELECT P13F230O  ASSIGN        TO  P13F230O.                         
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F230                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13F230.                                                           
                                                                                
       FD   P13F230O                                                            
            RECORDING MODE  IS  F                                               
            BLOCK  CONTAINS 0   RECORDS                                         
            RECORD CONTAINS 130 CHARACTERS                                      
            LABEL  RECORDS  ARE STANDARD.                                       
                                                                                
       01   P13F230O-REC        PIC X(130).                                     
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-DATE             PIC X(08) VALUE SPACES.                  
           05  FILLER                  PIC X(13) VALUE SPACES.                  
                                                                                
       01  WS-DISP-DATE.                                                        
           05  WS-DISP-DD              PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  WS-DISP-MM              PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(01) VALUE '/'.                     
           05  WS-DISP-CCYY            PIC X(04) VALUE SPACES.                  
                                                                                
       01  WS-WORK-VAR.                                                         
           05  WS-END-OF-F230-SW       PIC X(01)  VALUE SPACE.                  
               88  C-END-OF-F230       VALUE 'Y'.                               
           05  WS-READ-CTR             PIC 9(08) VALUE ZEROES.                  
           05  WS-WRITE-CTR            PIC 9(08) VALUE ZEROES.                  
           05  WS-NUM-MAX-CAT1         PIC S9(03) VALUE ZEROES.                 
           05  WS-NUM-MAX-CAT2         PIC S9(03) VALUE ZEROES.                 
           05  WS-NUM-MAX-CAT3         PIC S9(03) VALUE ZEROES.                 
                                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *--------------------------------*                                        
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
           PERFORM 2000-PROCESS-RECORDS   THRU 2000-EXIT                        
             UNTIL C-END-OF-F230.                                               
           PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       1000-INITIALIZATION.                                                     
      *--------------------------------*                                        
                                                                                
           OPEN INPUT  BP13F230                                                 
                OUTPUT P13F230O.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
           MOVE WS-SYS-DATE(1:4)       TO WS-DISP-CCYY.                         
           MOVE WS-SYS-DATE(5:2)       TO WS-DISP-MM.                           
           MOVE WS-SYS-DATE(7:2)       TO WS-DISP-DD.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2000-PROCESS-RECORDS.                                                    
      *--------------------------------*                                        
                                                                                
           READ BP13F230                                                        
                AT END   MOVE 'Y'   TO  WS-END-OF-F230-SW                       
                         GO TO 2000-EXIT.                                       
                                                                                
           ADD  1  TO WS-READ-CTR.                                              
                                                                                
           PERFORM 2100-CHECK-RECORD-ID     THRU  2100-EXIT.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2100-CHECK-RECORD-ID.                                                    
      *--------------------------------*                                        
                                                                                
           MOVE SPACES   TO P13F230O-REC.                                       
                                                                                
           EVALUATE F230-CDE-HEADER                                             
             WHEN '01'                                                          
                PERFORM 2120-CHECK-HEADER          THRU 2120-EXIT               
                IF F230-NUM-CAT1-FULL = 'F' OR                                  
                   F230-NUM-CAT2-FULL = 'F' OR                                  
                   F230-NUM-CAT3-FULL = 'F'                                     
                   PERFORM 2110-EDIT-CATEGORY      THRU 2110-EXIT               
                END-IF                                                          
                PERFORM 2150-COMPUTE-MAX-CAT       THRU 2150-EXIT               
                MOVE F230-HEADER-REC   TO P13F230O-REC                          
                                                                                
             WHEN '02'                                                          
                PERFORM 2130-CHECK-DETAIL          THRU 2130-EXIT               
                MOVE F230-DETAIL-REC   TO P13F230O-REC                          
                                                                                
             WHEN '03'                                                          
                PERFORM 2160-CHECK-TRAILER         THRU 2160-EXIT               
                MOVE F230-TRAILER-REC  TO P13F230O-REC                          
                                                                                
             WHEN '05'                                                          
                PERFORM 2140-CHECK-STARTEND        THRU 2140-EXIT               
                MOVE F230-START-END-REC  TO P13F230O-REC                        
             WHEN '06'                                                          
                PERFORM 2170-CHECK-BLOCK           THRU 2170-EXIT               
                MOVE F230-BLOCK-REC      TO P13F230O-REC                        
             END-EVALUATE.                                                      
                                                                                
           WRITE P13F230O-REC.                                                  
           ADD   1        TO  WS-WRITE-CTR.                                     
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2110-EDIT-CATEGORY.                                                      
      *--------------------------------*                                        
                                                                                
           IF F230-NUM-CAT1-FULL = 'F'                                          
              MOVE SPACES      TO F230-NUM-CAT1-FULL                            
              MOVE ZEROES      TO F230-NUM-MAX-CAT1                             
           END-IF.                                                              
                                                                                
           IF F230-NUM-CAT2-FULL = 'F'                                          
              MOVE SPACES      TO F230-NUM-CAT2-FULL                            
              MOVE ZEROES      TO F230-NUM-MAX-CAT2                             
           END-IF.                                                              
                                                                                
           IF F230-NUM-CAT3-FULL = 'F'                                          
              MOVE SPACES      TO F230-NUM-CAT3-FULL                            
              MOVE ZEROES      TO F230-NUM-MAX-CAT3                             
           END-IF.                                                              
                                                                                
       2110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2120-CHECK-HEADER.                                                       
      *--------------------------------*                                        
           IF F230-NUM-FLAT-TYPE OF F230-HEADER-REC = 'SA' OR 'SP'              
              MOVE '7A' TO F230-NUM-FLAT-TYPE                                   
                        OF F230-HEADER-REC                                      
           ELSE                                                                 
               IF F230-NUM-FLAT-TYPE OF F230-HEADER-REC = 'S1'                  
                  MOVE '1S' TO F230-NUM-FLAT-TYPE                               
                            OF F230-HEADER-REC                                  
               ELSE                                                             
                   IF F230-NUM-FLAT-TYPE OF F230-HEADER-REC = 'S2'              
                      MOVE '2S' TO F230-NUM-FLAT-TYPE                           
                            OF F230-HEADER-REC                                  
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           EVALUATE F230-NUM-SELECTION OF F230-HEADER-REC                       
              WHEN '3R '                                                        
                   MOVE 'WIR'      TO F230-NUM-SELECTION                        
                                   OF F230-HEADER-REC                           
              WHEN 'BTO'                                                        
                   IF F230-DTE-ALLOC OF F230-HEADER-REC = '20070627'            
                      MOVE '1T' TO F230-NUM-FLAT-TYPE                           
                                OF F230-HEADER-REC                              
                   END-IF                                                       
              WHEN 'BE '                                                        
                   MOVE 'OTB'      TO F230-NUM-SELECTION                        
                                   OF F230-HEADER-REC                           
           END-EVALUATE.                                                        
                                                                                
       2120-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2130-CHECK-DETAIL.                                                       
      *--------------------------------*                                        
           IF F230-NUM-FLAT-TYPE OF F230-DETAIL-REC = 'SA' OR 'SP'              
              MOVE '7A' TO F230-NUM-FLAT-TYPE                                   
                        OF F230-DETAIL-REC                                      
           ELSE                                                                 
               IF F230-NUM-FLAT-TYPE OF F230-DETAIL-REC = 'S1'                  
                  MOVE '1S' TO F230-NUM-FLAT-TYPE                               
                        OF F230-DETAIL-REC                                      
               ELSE                                                             
                   IF F230-NUM-FLAT-TYPE OF F230-DETAIL-REC = 'S2'              
                       MOVE '2S' TO F230-NUM-FLAT-TYPE                          
                            OF F230-DETAIL-REC                                  
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           EVALUATE F230-NUM-SELECTION OF F230-DETAIL-REC                       
              WHEN '3R '                                                        
                   MOVE 'WIR'      TO F230-NUM-SELECTION                        
                                   OF F230-DETAIL-REC                           
                   MOVE 'B'        TO F230-NUM-BALANCE                          
                                   OF F230-DETAIL-REC                           
              WHEN 'BTO'                                                        
                   IF F230-DTE-ALLOC OF F230-DETAIL-REC = '20070627'            
                      MOVE '1T' TO F230-NUM-FLAT-TYPE                           
                                OF F230-DETAIL-REC                              
                   END-IF                                                       
              WHEN 'BE '                                                        
                   MOVE 'OTB'      TO F230-NUM-SELECTION                        
                                   OF F230-DETAIL-REC                           
           END-EVALUATE.                                                        
                                                                                
       2130-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2140-CHECK-STARTEND.                                                     
      *--------------------------------*                                        
           IF F230-NUM-FLAT-TYPE OF F230-START-END-REC = 'SA' OR 'SP'           
              MOVE '7A' TO F230-NUM-FLAT-TYPE                                   
                        OF F230-START-END-REC                                   
           ELSE                                                                 
               IF F230-NUM-FLAT-TYPE OF F230-START-END-REC = 'S1'               
                  MOVE '1S' TO F230-NUM-FLAT-TYPE                               
                        OF F230-START-END-REC                                   
               ELSE                                                             
                   IF F230-NUM-FLAT-TYPE OF F230-START-END-REC = 'S2'           
                      MOVE '2S' TO F230-NUM-FLAT-TYPE                           
                            OF F230-START-END-REC                               
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           EVALUATE F230-NUM-SELECTION OF F230-START-END-REC                    
              WHEN '3R '                                                        
                   MOVE 'WIR'      TO F230-NUM-SELECTION                        
                                   OF F230-START-END-REC                        
              WHEN 'BE '                                                        
                   MOVE 'OTB'      TO F230-NUM-SELECTION                        
                                   OF F230-START-END-REC                        
           END-EVALUATE.                                                        
                                                                                
       2140-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2150-COMPUTE-MAX-CAT.                                                    
      *--------------------------------*                                        
                                                                                
           MOVE ZEROES        TO WS-NUM-MAX-CAT1                                
                                 WS-NUM-MAX-CAT2                                
                                 WS-NUM-MAX-CAT3.                               
                                                                                
           COMPUTE WS-NUM-MAX-CAT1 = F230-NUM-MAX-CAT1 -                        
                                     F230-NUM-TOT-CAT1.                         
                                                                                
           COMPUTE WS-NUM-MAX-CAT2 = F230-NUM-MAX-CAT2 -                        
                                     F230-NUM-TOT-CAT2.                         
                                                                                
           COMPUTE WS-NUM-MAX-CAT3 = F230-NUM-MAX-CAT3 -                        
                                     F230-NUM-TOT-CAT3.                         
                                                                                
           IF WS-NUM-MAX-CAT1 < ZEROES                                          
              MOVE ZEROES            TO F230-NUM-MAX-CAT1                       
           ELSE                                                                 
              MOVE WS-NUM-MAX-CAT1   TO F230-NUM-MAX-CAT1                       
           END-IF.                                                              
                                                                                
           IF WS-NUM-MAX-CAT2 < ZEROES                                          
              MOVE ZEROES            TO F230-NUM-MAX-CAT2                       
           ELSE                                                                 
              MOVE WS-NUM-MAX-CAT2   TO F230-NUM-MAX-CAT2                       
           END-IF.                                                              
                                                                                
           IF WS-NUM-MAX-CAT3 < ZEROES                                          
              MOVE ZEROES            TO F230-NUM-MAX-CAT3                       
           ELSE                                                                 
              MOVE WS-NUM-MAX-CAT3   TO F230-NUM-MAX-CAT3                       
           END-IF.                                                              
                                                                                
       2150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2160-CHECK-TRAILER.                                                      
      *--------------------------------*                                        
           IF F230-NUM-FLAT-TYPE OF F230-TRAILER-REC = 'SA' OR 'SP'             
              MOVE '7A' TO F230-NUM-FLAT-TYPE                                   
                        OF F230-TRAILER-REC                                     
           ELSE                                                                 
               IF F230-NUM-FLAT-TYPE OF F230-TRAILER-REC = 'S1'                 
                  MOVE '1S' TO F230-NUM-FLAT-TYPE                               
                        OF F230-TRAILER-REC                                     
               ELSE                                                             
                   IF F230-NUM-FLAT-TYPE OF F230-TRAILER-REC = 'S2'             
                      MOVE '2S' TO F230-NUM-FLAT-TYPE                           
                            OF F230-TRAILER-REC                                 
                   END-IF                                                       
               END-IF                                                           
           END-IF.                                                              
                                                                                
           EVALUATE F230-NUM-SELECTION OF F230-TRAILER-REC                      
              WHEN '3R '                                                        
                   MOVE 'WIR'      TO F230-NUM-SELECTION                        
                                   OF F230-TRAILER-REC                          
              WHEN 'BTO'                                                        
                   IF F230-DTE-ALLOC OF F230-TRAILER-REC = '20070627'           
                      MOVE '1T' TO F230-NUM-FLAT-TYPE                           
                                OF F230-TRAILER-REC                             
                   END-IF                                                       
              WHEN 'BE '                                                        
                   MOVE 'OTB'      TO F230-NUM-SELECTION                        
                                   OF F230-TRAILER-REC                          
           END-EVALUATE.                                                        
                                                                                
       2160-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2170-CHECK-BLOCK.                                                        
      *--------------------------------*                                        
           EVALUATE F230-NUM-SELECTION OF F230-BLOCK-REC                        
              WHEN '3R '                                                        
                   MOVE 'WIR'      TO F230-NUM-SELECTION                        
                                   OF F230-BLOCK-REC                            
              WHEN 'BE '                                                        
                   MOVE 'OTB'      TO F230-NUM-SELECTION                        
                                   OF F230-BLOCK-REC                            
           END-EVALUATE.                                                        
                                                                                
       2170-EXIT.                                                               
           EXIT.                                                                
      *--------------------------------*                                        
       9000-CLOSE-FILES.                                                        
      *--------------------------------*                                        
                                                                                
           DISPLAY 'PROGRAM ID   : BP13C279'.                                   
           DISPLAY 'CURRENT DATE : ' WS-DISP-DATE.                              
           DISPLAY SPACES.                                                      
           DISPLAY '*=============== COUNTERS ===============*'.                
           DISPLAY 'BP13F230 RECORDS READ    : ' WS-READ-CTR.                   
           DISPLAY 'P13F230O RECORDS WRITTEN : ' WS-WRITE-CTR.                  
                                                                                
           CLOSE BP13F230                                                       
                 P13F230O.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
