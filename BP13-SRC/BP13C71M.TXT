       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C71M.                                                  
      *AUTHOR.       IL5.                                                       
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *  OBJECTIVE:                                                *            
      *  MATCH K800 WITH F520-NUM-REGN                             *            
      *                                                            *            
      *  I    :  BP13F520                                          *            
      *  I/O  :  BP13K800                                          *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F520   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 500 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13F520.                                                           
                                                                                
       FD  BP13K800   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 2000 CHARACTERS                           
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13K800.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-SWITCHES.                                                         
           05  WS-K800-STATUS            PIC 99    VALUE ZEROS.                 
           05  WS-F520-EOF               PIC X     VALUE 'N'.                   
           05  WS-F520-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-K800-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-K800-REWRITE           PIC 9(6)  VALUE ZEROS.                 
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
            PERFORM 100-OPEN-FILES      THRU 100-EXIT.                          
            PERFORM 200-READ-BP13F520   THRU 200-EXIT.                          
            PERFORM 500-PROCESS-K800    THRU 500-EXIT                           
                             UNTIL WS-F520-EOF = 'Y'.                           
            PERFORM 999-CLOSE-FILES     THRU 999-EXIT.                          
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F520                                                 
                I-O    BP13K800.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, K800-STATUS ' WS-K800-STATUS              
              MOVE WS-K800-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-BP13F520.                                                       
      *-------------------------------------------------------------            
           READ BP13F520 AT END                                                 
           MOVE 'Y'                      TO WS-F520-EOF                         
           GO TO 200-EXIT.                                                      
                                                                                
           ADD 1                         TO WS-F520-READ.                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-PROCESS-K800.                                                        
      *-------------------------------------------------------------            
           MOVE F520-NUM-REGN TO K800-NUM-REGN.                                 
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              ADD 1 TO  WS-K800-READ                                            
              PERFORM 600-REWRITE-BP13K800 THRU 600-EXIT                        
           ELSE                                                                 
              IF WS-K800-STATUS = 23                                            
                 DISPLAY 'RECORD NOT FND IN BP13K800 ' WS-K800-STATUS           
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K800,STATUS ' WS-K800-STATUS           
                 MOVE WS-K800-STATUS     TO RETURN-CODE                         
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
           PERFORM 200-READ-BP13F520 THRU 200-EXIT.                             
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-REWRITE-BP13K800.                                                    
      *-------------------------------------------------------------            
           IF F520-NUM-REGN = K800-NUM-REGN                                     
              MOVE F520-NUM-PREM-CDE         TO                                 
                   K800-NUM-PREM-CODE                                           
                                                                                
              REWRITE BP13K800-MASTER                                           
                                                                                
              IF WS-K800-STATUS = 00                                            
                 ADD 1                 TO WS-K800-REWRITE                       
              ELSE                                                              
                 DISPLAY 'ERROR REWRITING BP13K800 STATUS '                     
                                                 WS-K800-STATUS                 
                 MOVE WS-K800-STATUS      TO RETURN-CODE                        
                 PERFORM 999-CLOSE-FILES  THRU 999-EXIT                         
              END-IF                                                            
           END-IF.                                                              
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       999-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY '* ------------ BP13C71M ------------- * '.                  
           DISPLAY 'NO OF REC READ    BP13F520 = ' WS-F520-READ.                
           DISPLAY 'NO OF REC READ    BP13K800 = ' WS-K800-READ.                
           DISPLAY 'NO OF REC REWRITE BP13K800 = ' WS-K800-REWRITE.             
                                                                                
           CLOSE BP13F520                                                       
                 BP13K800.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K800, STATUS ' WS-K800-STATUS          
              MOVE WS-K800-STATUS        TO RETURN-CODE.                        
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
