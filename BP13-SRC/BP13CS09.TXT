       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CS09.                                                 
       AUTHOR.        ELAINE S ARGA.                                            
       DATE-WRITTEN.  02 JULY 2014.                                             
      *========================================================*                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO UPDATE BP13KQ10 & BP13KP02 AFTER   *                
      *                  SENDING THE SMS IN BP13D806           *                
      *                                                        *                
      *    INPUT       : BP13.F203.SMS.PD                      *                
      *    I-O         : BP13.KQ10.DOC.TBL                     *                
      *                  BP13.KP02.DOCHIST                     *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE     DETAILS                         *                
      *--------- ---- -------- --------------------------------*                
      * BP135227 ESA1 20140702 NEW PROGRAM                     *                
      *========================================================*                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F203 ASSIGN TO BP13F203.                                  
                                                                                
           SELECT BP13KQ10 ASSIGN TO BP13KQ10                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KQ10-KEY-FLD                               
                  FILE STATUS     IS KQ10-STATUS.                               
                                                                                
           SELECT BP13KP02 ASSIGN TO BP13KP02                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KP02-KEY-FLD                               
                  FILE STATUS     IS KP02-STATUS.                               
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F203                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F203.                                                           
                                                                                
       FD  BP13KQ10                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13KQ10.                                                           
                                                                                
       FD  BP13KP02                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13KP02.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 KQ10-STATUS                 PIC 9(2)  VALUE ZEROS.                
           05 KP02-STATUS                 PIC 9(2)  VALUE ZEROS.                
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-READ-CNT-F203            PIC 9(5)  VALUE ZEROS.                
           05 WS-CNT-KQ10-UPD             PIC 9(5)  VALUE ZEROS.                
           05 WS-CNT-KP02-ADD             PIC 9(5)  VALUE ZEROS.                
                                                                                
       01  WS-EOF-F203                    PIC X     VALUE 'N'.                  
       01  WS-CUR-DATE                    PIC X(8)  VALUE ZEROES.               
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       000-MAIN-ROUTINE.                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F203      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL WS-EOF-F203 = 'Y'                                            
           PERFORM 999-CLOSE-ROUTINE  THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       100-INITIALIZATION.                                                      
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F203                                                 
                I-O    BP13KQ10                                                 
                       BP13KP02.                                                
                                                                                
           IF KQ10-STATUS NOT = ZEROS AND 97                                    
              DISPLAY ' ERROR OPENING BP13KQ10  FILE, STATUS '                  
                  KQ10-STATUS                                                   
              MOVE KQ10-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           IF KP02-STATUS NOT = ZEROS AND 97                                    
              DISPLAY ' ERROR OPENING BP13KP02  FILE, STATUS '                  
                  KP02-STATUS                                                   
              MOVE KP02-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CUR-DATE.                        
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       200-READ-F203.                                                           
      ******************************************************************        
                                                                                
           READ BP13F203 AT END                                                 
                MOVE 'Y' TO WS-EOF-F203                                         
                GO TO 200-EXIT.                                                 
           ADD   1     TO WS-READ-CNT-F203.                                     
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-RTN.                                                         
      ******************************************************************        
           PERFORM 400-READ-KQ10   THRU 400-EXIT.                               
                                                                                
           PERFORM 200-READ-F203   THRU 200-EXIT.                               
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       400-READ-KQ10.                                                           
      ******************************************************************        
                                                                                
           MOVE F203-NUM-REGN       TO KQ10-NUM-REGN.                           
           READ BP13KQ10.                                                       
                                                                                
           IF KQ10-STATUS = 0                                                   
              PERFORM 500-REWRITE-KQ10    THRU 500-EXIT                         
              PERFORM 600-WRITE-KP02      THRU 600-EXIT                         
           ELSE                                                                 
              DISPLAY 'REC NOT FOUND IN DOC TABLE : ' F203-NUM-REGN             
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       500-REWRITE-KQ10.                                                        
      ******************************************************************        
           IF KQ10-DTE-DOC-SMSREMIND1-SENT = SPACES OR                          
                                      LOW-VALUES OR ZEROES                      
              MOVE FUNCTION CURRENT-DATE                                        
                                      TO KQ10-DTE-DOC-SMSREMIND1-SENT           
           ELSE                                                                 
              IF KQ10-DTE-DOC-SMSREMIND3-SENT = SPACES OR                       
                                      LOW-VALUES OR ZEROES                      
                 MOVE FUNCTION CURRENT-DATE                                     
                                      TO KQ10-DTE-DOC-SMSREMIND3-SENT           
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE 'BP13CS08'        TO KQ10-NUM-USERID.                           
           MOVE FUNCTION CURRENT-DATE                                           
                                  TO KQ10-DTE-UPDATE.                           
           ACCEPT KQ10-TME-UPDATE FROM TIME.                                    
                                                                                
           REWRITE BP13KQ10-REC.                                                
                                                                                
            EVALUATE KQ10-STATUS                                                
               WHEN 00                                                          
               WHEN 02                                                          
                    ADD 1    TO WS-CNT-KQ10-UPD                                 
               WHEN OTHER                                                       
                    DISPLAY 'ERROR UPDATING BP13KQ10. STATUS IS '               
                            KQ10-STATUS                                         
                    PERFORM 999-CLOSE-ROUTINE  THRU 999-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       600-WRITE-KP02.                                                          
      ******************************************************************        
           MOVE SPACES                       TO BP13KP02-REC.                   
           INITIALIZE                           BP13KP02-REC.                   
                                                                                
           MOVE F203-NUM-REGN                TO KP02-NUM-REGN.                  
           MOVE F203-NUM-NRIC                TO KP02-NUM-UIN.                   
           MOVE FUNCTION CURRENT-DATE        TO KP02-DTE-MSG.                   
           MOVE SPACES                       TO KP02-TME-MSG.                   
                                                                                
           MOVE '13CS08'                     TO KP02-NUM-USERID.                
           MOVE 'D806'                       TO KP02-NUM-NICKNAME.              
           MOVE ' SMS TRANSMITTED'           TO KP02-TXT-MSG.                   
                                                                                
           WRITE BP13KP02-REC.                                                  
                                                                                
            EVALUATE KP02-STATUS                                                
               WHEN 00                                                          
               WHEN 02                                                          
                    ADD 1    TO WS-CNT-KP02-ADD                                 
               WHEN OTHER                                                       
                    DISPLAY 'ERROR CREATING BP13KP02. STATUS IS '               
                            KP02-STATUS                                         
                    PERFORM 999-CLOSE-ROUTINE  THRU 999-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       999-CLOSE-ROUTINE.                                                       
      ******************************************************************        
           CLOSE BP13F203                                                       
                 BP13KQ10                                                       
                 BP13KP02.                                                      
                                                                                
           IF KQ10-STATUS NOT = ZEROS AND 97                                    
              DISPLAY ' ERROR CLOSING BP13KQ10  FILE, STATUS '                  
                  KQ10-STATUS                                                   
              MOVE KQ10-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           IF KP02-STATUS NOT = ZEROS AND 97                                    
              DISPLAY ' ERROR CLOSING BP13KP02  FILE, STATUS '                  
                  KP02-STATUS                                                   
              MOVE KP02-STATUS TO RETURN-CODE                                   
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CS09 ================*'.              
           DISPLAY '*======= RUN DATE : ' WS-CUR-DATE ' =======*'.              
           DISPLAY '    TOTAL NO OF REC READ    = ' WS-READ-CNT-F203.           
           DISPLAY '    NO. OF KQ10 UPDATED     = ' WS-CNT-KQ10-UPD.            
           DISPLAY '    NO. OF KP02 CREATED     = ' WS-CNT-KP02-ADD.            
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
