      *--------------------------------------------------------*                
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------*                
       PROGRAM-ID.    BP13C339.                                                 
      *AUTHOR.        MCC.                                                      
      *DATE-WRITTEN.  12/04/2003.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *                                                             *           
      *  OBJECTIVE  : CREATE CPF TRANSACTION ON SOC CPF FOR         *           
      *               SOLICITOR BILL OF BANK LOAN CASES.            *           
      *                                                             *           
      *  INPUT FILES: 1. BP13F671                                   *           
      *               2. SY02F001                                   *           
      *                                                             *           
      *  OUTPUT FILES: 1. BP13F121 (CPF TRANSACTIONS FILE)          *           
      *                2. BP13L339 (CONTROL LISTING)                *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  BY    ON        DESCRIPTION                        *           
      * -------- ----- --------  -----------                        *           
      * BP132309 MCC   20030412  NEW PROGRAM                        *           
      * BP132526 SK20  20040618  EXPAND F671/K671 TO 3000 LENGTH    *           
      * =========================================================== *           
                                                                                
      *--------------------------------------------------------*                
       ENVIRONMENT DIVISION.                                                    
      *--------------------------------------------------------*                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F671 ASSIGN TO BP13F671.                                  
                                                                                
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13K671 ASSIGN TO BP13K671                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K671-NUM-REGN                              
                  FILE STATUS     IS WS-K671-STATUS.                            
                                                                                
           SELECT BP13F121 ASSIGN TO BP13F121.                                  
                                                                                
      *--------------------------------------------------------*                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------*                
                                                                                
       FD  BP13F671                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 3000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F671.                                                           
                                                                                
       FD   BP13K671                                                            
            RECORD CONTAINS 3000 CHARACTERS.                                    
       COPY BP13K671.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13F121                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       COPY BP13F121.                                                           
                                                                                
      *--------------------------------------------------------*                
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------*                
                                                                                
       01  WS-STATUS-FLAGS.                                                     
           05  WS-F671-EOF                PIC X       VALUE 'N'.                
           05  WS-K671-STATUS             PIC 99      VALUE ZEROES.             
           05  WS-SOL-PROCESSED           PIC X       VALUE 'N'.                
           05  WS-F001-EOF                PIC X       VALUE 'N'.                
               88 EOF-F001                            VALUE 'Y'.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F671-READ               PIC 9(5)    VALUE ZEROES.             
           05  WS-F671-SKIP               PIC 9(5)    VALUE ZEROES.             
           05  WS-K671-READ               PIC 9(5)    VALUE ZEROES.             
           05  WS-K671-REWR               PIC 9(5)    VALUE ZEROES.             
           05  WS-F121-WRITE              PIC 9(5)    VALUE ZEROES.             
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NRIC                    PIC X(9)    VALUE SPACES.             
           05  WS-CDE-APPLT-CPF           PIC X(1)    VALUE SPACES.             
           05  WS-SOL-CPF                 PIC 9(7)V99 VALUE ZEROES.             
                                                                                
       01  WS-DTE-VARIABLES.                                                    
           05  WS-DTE-TRANS               PIC X(8)    VALUE SPACES.             
           05  WS-DTE-CPF1                PIC X(8)    VALUE SPACES.             
           05  WS-DTE-CPF2                PIC X(8)    VALUE SPACES.             
           05  WS-DTE-CPF3                PIC X(8)    VALUE SPACES.             
           05  WS-DTE-CPF4                PIC X(8)    VALUE SPACES.             
           05  WS-SYSTEM-DATE             PIC X(8).                             
                                                                                
                                                                                
      **********************************************************                
       PROCEDURE DIVISION.                                                      
      **********************************************************                
       0000-MAIN.                                                               
      *--------------------------------------------------------*                
           PERFORM 1000-OPEN-FILE       THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F671   THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-REC     THRU 3000-EXIT                          
                   UNTIL WS-F671-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       1000-OPEN-FILE.                                                          
      *--------------------------------------------------------*                
           OPEN INPUT  BP13F671                                                 
                       SY02F001                                                 
                I-O    BP13K671                                                 
                OUTPUT BP13F121.                                                
                                                                                
           IF WS-K671-STATUS NOT = 00 AND 97                                    
              MOVE WS-K671-STATUS      TO RETURN-CODE                           
              DISPLAY 'ERROR OPENING BP13K671. STATUS ' WS-K671-STATUS          
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           READ SY02F001 AT END MOVE 'Y'  TO WS-F001-EOF.                       
           IF EOF-F001                                                          
              DISPLAY 'RECORD NOT FOUND IN SY01F001 '                           
              DISPLAY 'PROCESSING TERMINATED '                                  
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           ELSE                                                                 
              MOVE F001-DTE-CURRENT      TO   WS-SYSTEM-DATE                    
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       2000-READ-BP13F671.                                                      
      *--------------------------------------------------------*                
           READ BP13F671                                                        
                AT END  MOVE 'Y' TO WS-F671-EOF                                 
                GO TO 2000-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F671-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       3000-PROCESS-REC.                                                        
      *--------------------------------------------------------*                
           MOVE SPACES TO WS-NRIC.                                              
           MOVE ZEROS  TO WS-SOL-CPF.                                           
           MOVE 'N'    TO WS-SOL-PROCESSED.                                     
                                                                                
           PERFORM 4000-GET-CPF-POST-DATE THRU 4000-EXIT.                       
                                                                                
           IF F671-AMT-SOLICIT-CPF1-PAID NOT NUMERIC                            
              MOVE ZEROS  TO F671-AMT-SOLICIT-CPF1-PAID                         
           END-IF.                                                              
                                                                                
           IF F671-AMT-SOLICIT-CPF2-PAID NOT NUMERIC                            
              MOVE ZEROS  TO F671-AMT-SOLICIT-CPF2-PAID                         
           END-IF.                                                              
                                                                                
           IF F671-AMT-SOLICIT-CPF3-PAID NOT NUMERIC                            
              MOVE ZEROS  TO F671-AMT-SOLICIT-CPF3-PAID                         
           END-IF.                                                              
                                                                                
           IF F671-AMT-SOLICIT-CPF4-PAID NOT NUMERIC                            
              MOVE ZEROS  TO F671-AMT-SOLICIT-CPF4-PAID                         
           END-IF.                                                              
                                                                                
           IF F671-AMT-SOLICIT-CPF1-PAID > 0                                    
              MOVE WS-DTE-CPF1                TO WS-DTE-TRANS                   
              MOVE F671-NUM-CPF1-NO           TO WS-NRIC                        
              MOVE F671-AMT-SOLICIT-CPF1-PAID TO WS-SOL-CPF                     
              MOVE '1'                        TO WS-CDE-APPLT-CPF               
              PERFORM 5000-WRITE-BP13F121     THRU 5000-EXIT                    
           END-IF.                                                              
                                                                                
           IF F671-AMT-SOLICIT-CPF2-PAID > 0                                    
              MOVE WS-DTE-CPF2                TO WS-DTE-TRANS                   
              MOVE F671-NUM-CPF2-NO           TO WS-NRIC                        
              MOVE F671-AMT-SOLICIT-CPF2-PAID TO WS-SOL-CPF                     
              MOVE '2'                        TO WS-CDE-APPLT-CPF               
              PERFORM 5000-WRITE-BP13F121     THRU 5000-EXIT                    
           END-IF.                                                              
                                                                                
           IF F671-AMT-SOLICIT-CPF3-PAID > 0                                    
              MOVE WS-DTE-CPF3                TO WS-DTE-TRANS                   
              MOVE F671-NUM-CPF3-NO           TO WS-NRIC                        
              MOVE F671-AMT-SOLICIT-CPF3-PAID TO WS-SOL-CPF                     
              MOVE '3'                        TO WS-CDE-APPLT-CPF               
              PERFORM 5000-WRITE-BP13F121     THRU 5000-EXIT                    
           END-IF.                                                              
                                                                                
           IF F671-AMT-SOLICIT-CPF4-PAID > 0                                    
              MOVE WS-DTE-CPF4                TO WS-DTE-TRANS                   
              MOVE F671-NUM-CPF4-NO           TO WS-NRIC                        
              MOVE F671-AMT-SOLICIT-CPF4-PAID TO WS-SOL-CPF                     
              MOVE '4'                        TO WS-CDE-APPLT-CPF               
              PERFORM 5000-WRITE-BP13F121     THRU 5000-EXIT                    
           END-IF.                                                              
                                                                                
           IF WS-SOL-PROCESSED = 'Y'                                            
              PERFORM 6000-READ-BP13K671 THRU 6000-EXIT                         
           ELSE                                                                 
              ADD 1 TO WS-F671-SKIP                                             
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F671 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-GET-CPF-POST-DATE.                                                  
      *--------------------------------------------------------*                
           MOVE WS-SYSTEM-DATE      TO WS-DTE-CPF1,                             
                                       WS-DTE-CPF2,                             
                                       WS-DTE-CPF3,                             
                                       WS-DTE-CPF4.                             
                                                                                
           IF F671-DTE-VALUE-CPF1 NOT = SPACES AND LOW-VALUES AND ZEROES        
               MOVE F671-DTE-VALUE-CPF1 TO WS-DTE-CPF1                          
           END-IF.                                                              
                                                                                
           IF F671-DTE-VALUE-CPF2 NOT = SPACES AND LOW-VALUES AND ZEROES        
               MOVE F671-DTE-VALUE-CPF2 TO WS-DTE-CPF2                          
           END-IF.                                                              
                                                                                
           IF F671-DTE-VALUE-CPF3 NOT = SPACES AND LOW-VALUES AND ZEROES        
               MOVE F671-DTE-VALUE-CPF3 TO WS-DTE-CPF3                          
           END-IF.                                                              
                                                                                
           IF F671-DTE-VALUE-CPF4 NOT = SPACES AND LOW-VALUES AND ZEROES        
               MOVE F671-DTE-VALUE-CPF4 TO WS-DTE-CPF4                          
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       5000-WRITE-BP13F121.                                                     
      *--------------------------------------------------------*                
           MOVE SPACES                TO BP13F121-CPFTRAN.                      
           INITIALIZE                    BP13F121-CPFTRAN.                      
                                                                                
           MOVE F671-NUM-REGN         TO F121-NUM-REGN.                         
           MOVE WS-SYSTEM-DATE        TO F121-DTE-POST.                         
           MOVE WS-DTE-TRANS          TO F121-DTE-TRANS.                        
           MOVE ZEROS                 TO F121-COUNT-HISTORY.                    
           MOVE WS-SOL-CPF            TO F121-AMT-RECIEPT.                      
           MOVE F671-NUM-SCH-ACC      TO F121-NUM-SCH-ACCT.                     
           MOVE '12'                  TO F121-CDE-TRANS-TYPE.                   
           MOVE 'SOL'                 TO F121-CDE-PAYMENT-TYPE.                 
           MOVE WS-NRIC               TO F121-NUM-NRIC                          
                                         F121-NUM-CPF-APPL.                     
           MOVE WS-CDE-APPLT-CPF      TO F121-CDE-APPLT-CPF.                    
                                                                                
           IF F671-NUM-HBL-TYP = '2'                                            
              MOVE 'BANK LOAN CPF2'   TO F121-NUM-RECPT-JRNO                    
           ELSE                                                                 
              MOVE 'BANK LOAN CPF1'   TO F121-NUM-RECPT-JRNO                    
           END-IF.                                                              
                                                                                
           WRITE BP13F121-CPFTRAN.                                              
                                                                                
           ADD 1    TO WS-F121-WRITE.                                           
           MOVE 'Y' TO WS-SOL-PROCESSED.                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       6000-READ-BP13K671.                                                      
      *--------------------------------------------------------*                
           MOVE SPACES           TO BP13K671-REC.                               
           MOVE F671-NUM-REGN    TO K671-NUM-REGN.                              
                                                                                
           READ BP13K671.                                                       
                                                                                
           EVALUATE WS-K671-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-K671-READ                                          
                 PERFORM 7000-REWRITE-K671 THRU 7000-EXIT                       
              WHEN 23                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE WS-K671-STATUS     TO RETURN-CODE                         
                 DISPLAY 'ERROR READING BP13K671, STATUS = '                    
                                        WS-K671-STATUS                          
                 PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       7000-REWRITE-K671.                                                       
      *--------------------------------------------------------*                
           MOVE WS-SOL-PROCESSED TO K671-NUM-SOLICIT-PROCESSED.                 
                                                                                
           REWRITE BP13K671-REC.                                                
                                                                                
           EVALUATE WS-K671-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO WS-K671-REWR                                          
              WHEN 23                                                           
                 DISPLAY 'REC NOTFND BP13K671. REGN ' F671-NUM-REGN             
              WHEN OTHER                                                        
                 MOVE WS-K671-STATUS     TO RETURN-CODE                         
                 DISPLAY 'ERROR REWRITING BP13K671, STATUS = '                  
                                        WS-K671-STATUS                          
                 PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------*                
       9000-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------*                
           DISPLAY '******  BP13C339 *************'.                            
           DISPLAY SPACE.                                                       
           DISPLAY 'NO OF F671 READ     : ' WS-F671-READ.                       
           DISPLAY 'NO OF F671 SKIP     : ' WS-F671-SKIP.                       
           DISPLAY 'NO OF K671 READ     : ' WS-K671-READ.                       
           DISPLAY 'NO OF K671 REWRITE  : ' WS-K671-REWR.                       
           DISPLAY 'NO OF F121 WRITE    : ' WS-F121-WRITE.                      
                                                                                
           CLOSE BP13F671                                                       
                 BP13K671                                                       
                 SY02F001                                                       
                 BP13F121.                                                      
                                                                                
           IF WS-K671-STATUS NOT = 00 AND 97                                    
              MOVE WS-K671-STATUS      TO RETURN-CODE                           
              DISPLAY 'ERROR CLOSING BP13K671. STATUS ' WS-K671-STATUS          
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
