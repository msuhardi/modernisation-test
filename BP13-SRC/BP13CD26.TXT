      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13CD26.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  MARCH 02, 2009.                                           
      *===============================================================*         
      * OBJECTIVE  :  PRINT DBSS HG CANCELLED CASES REPORT.           *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F800                                     *         
      *               2. SY02F001                                     *         
      *               3. BP13KD00                                     *         
      *               4. BP13KD05                                     *         
      *               5. BP13K130                                     *         
      *               6. BM06K110                                     *         
      *               7. BP13K022                                     *         
      *               8. BP13K024                                     *         
      *                                                               *         
      * OUTPUT FILE:  1. BP13LD26                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP133522 02/03/2009 PCL3 NEW PROGRAM.                         *         
      * BP133522 23/06/2009 PCL3 ADD DEBARMENT CODE                   *         
      * BP133909 14/05/2010 PCL3 ADD CHECK ON CANCEL DATE.            *         
      * BP134641 03/08/2012 MDT2 CHANGE LRECL OF BP13KD05 FROM 300 TO *         
      *                          500 IN FD                            *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F800  ASSIGN        TO BP13F800.                         
           SELECT  SY02F001  ASSIGN        TO SY02F001.                         
           SELECT  BP13KD00  ASSIGN        TO BP13KD00                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KD00-KEY-FLD                      
                             FILE STATUS   IS WS-KD00-STATUS.                   
           SELECT  BP13KD05  ASSIGN        TO BP13KD05                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KD05-KEY-FLD                      
                             FILE STATUS   IS WS-KD05-STATUS.                   
           SELECT  BP13K130  ASSIGN        TO BP13K130                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K130-KEY-FLD                      
                             FILE STATUS   IS WS-K130-STATUS.                   
           SELECT  BM06K110  ASSIGN        TO BM06K110                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K110-KEY-FLD                      
                             FILE STATUS   IS WS-K110-STATUS.                   
           SELECT  BP13K022  ASSIGN       TO BP13K022                           
                             ACCESS MODE  IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K022-KEY-FLD                       
                             ALTERNATE RECORD KEY IS K022-NUM-REGN              
                             FILE STATUS  IS WS-K022-STATUS.                    
           SELECT  BP13K024  ASSIGN        TO BP13K024                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K024-KEY-FLD                      
                             FILE STATUS   IS WS-K024-STATUS.                   
           SELECT  P04K010   ASSIGN        TO P04K010                           
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K010-PRIME-KEY                    
                             FILE STATUS   IS WS-K010-STATUS.                   
           SELECT  BP13LD26  ASSIGN        TO BP13LD26.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13KD00                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13KD00.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K024                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K024.                                                           
                                                                                
       FD   BP13KD05                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13KD05.                                                           
                                                                                
       FD   BP13K130                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13K130.                                                           
                                                                                
       FD   P04K010                                                             
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY P04K010.                                                            
                                                                                
       FD   BP13LD26                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   BP13LD26-REC               PIC X(132).                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-F001-EOF             PIC X(1)  VALUE 'N'.                     
               88 EOF-F001                       VALUE 'Y'.                     
           05  WS-EOF-F800             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-KD05             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K130             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K010             PIC X(1)  VALUE 'N'.                     
           05  WS-KD00-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K110-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-KD05-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K130-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K022-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K024-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K010-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F800-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD00-NFND            PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD05-NFND            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-P04K             PIC 9(7)  VALUE ZEROES.                  
           05  WS-P04K-FND             PIC 9(7)  VALUE ZEROES.                  
           05  WS-P04K-NFND            PIC 9(7)  VALUE ZEROES.                  
           05  WS-F800-CDTE            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
           05  WS-CNT-LD26-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(1)  VALUE 2.                       
           05  WS-MAX-LINE             PIC 9(2)  VALUE 55.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-K130-FOUND            PIC X(1)  VALUE 'N'.                     
           05 WS-REGN-NO               PIC X(8)  VALUE SPACES.                  
           05 WS-SNO-ZZZZ9             PIC ZZZZ9 VALUE ZEROES.                  
           05 WS-FIRST-WRITE           PIC X(1)  VALUE 'Y'.                     
           05 WS-ADDRESS               PIC X(27) VALUE SPACES.                  
           05 WS-DTE-LAST-WK           PIC 9(08) VALUE ZEROES.                  
           05 WS-DTE-NUM               PIC 9(07) VALUE ZEROES.                  
           05 WS-DAYS                  PIC 9     VALUE ZERO.                    
                                                                                
       01  WS-DEBAR-CODE               PIC X(02).                               
           88 VALID-CPF-CODE     VALUE 'GD' 'GI' 'GK' 'GE'                      
                                       'GJ' 'GL' '71' 'GF'                      
                                       'GP' 'GY' 'GG' 'GM'                      
                                       'GH' 'GN' 'GQ' 'GS'                      
                                       'GU' 'GT' 'GV' 'GZ'                      
                                       'GF'.                                    
           88 VALID-AHG-CODE     VALUE 'LE' 'LF' 'LG' 'LH'                      
                                       'JG' 'JH' 'JI' 'JJ'                      
                                       'JK' 'JL' 'NI' 'NJ'                      
                                       'NK' 'NL' 'NM' 'NN'                      
                                       'NO' 'NP' 'LI' 'LA'                      
                                       'LJ' 'LB' 'LK' 'LC'                      
                                       'LL' 'LD' 'JM' 'JA'                      
                                       'JN' 'JB' 'JO' 'JC'                      
                                       'JP' 'JD' 'JQ' 'JE'                      
                                       'JR' 'JF' 'NQ' 'NA'                      
                                       'NR' 'NB' 'NS' 'NC'                      
                                       'NT' 'ND' 'NU' 'NE'                      
                                       'NV' 'NF' 'NW' 'NG'                      
                                       'NX' 'NH'.                               
                                                                                
       01  LD26-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13LD26'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LD26-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LD26-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  LD26-HEADER02.                                                       
           05  FILLER              PIC X(45)     VALUE SPACES.                  
           05  FILLER              PIC X(52)     VALUE                          
               'REPORT ON DBSS HOUSING GRANT CANCELLED CASES'.                  
                                                                                
       01  LD26-HEADER03.                                                       
           05  FILLER              PIC X(16)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'CANCEL    '.            
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'HSGNT '.                
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'AHG   '.                
           05  FILLER              PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE 'DEBAR'.                 
           05  FILLER              PIC X(16)     VALUE SPACES.                  
           05  FILLER              PIC X(27)     VALUE SPACES.                  
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'NAME'.                  
           05  FILLER              PIC X(01)     VALUE SPACES.                  
                                                                                
       01  LD26-HEADER04.                                                       
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(05)    VALUE '  S/N'.                  
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(08)    VALUE 'REGN NO '.               
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(09)    VALUE 'NRIC NO  '.              
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(04)    VALUE 'DATE'.                   
           05  FILLER              PIC X(07)    VALUE SPACES.                   
           05  FILLER              PIC X(06)    VALUE 'AMOUNT'.                 
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(06)    VALUE 'AMOUNT'.                 
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(06)    VALUE 'PAY-TY'.                 
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(05)    VALUE 'CODE '.                  
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(09)    VALUE 'CR CODE  '.              
           05  FILLER              PIC X(06)    VALUE SPACES.                   
           05  FILLER              PIC X(27)                                    
                                       VALUE 'BLK/HSE NO/POSTAL CODE'.          
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(03)    VALUE 'CSM'.                    
           05  FILLER              PIC X(01)    VALUE SPACES.                   
                                                                                
       01  LD26-DETAIL01.                                                       
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  LD26-SN             PIC X(05)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  LD26-REGN           PIC X(08)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  LD26-NRIC           PIC X(09)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  LD26-HSGNT-CANCEL   PIC X(10)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  LD26-HG-AMT         PIC ZZ,ZZ9       VALUE ZEROES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  LD26-AHG-AMT        PIC ZZ,ZZ9       VALUE ZEROES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  LD26-PAY-TY         PIC X(06)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  LD26-DBR-CODE       PIC X(05)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  LD26-CR-CODE        PIC X(14)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  LD26-ADDRESS        PIC X(27)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  LD26-NME-OCCP       PIC X(45)        VALUE SPACES.               
                                                                                
       01  LD26-DETAIL02.                                                       
           05  FILLER              PIC X(58)        VALUE SPACES.               
           05  LD26-DBR-CODE-2     PIC X(05)        VALUE SPACES.               
           05  FILLER              PIC X(16)        VALUE SPACES.               
           05  LD26-ADDRESS-2      PIC X(27)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  LD26-CSM            PIC X(45)        VALUE SPACES.               
                                                                                
       COPY AV02COMM.                                                           
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F800      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F800 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F800                                                 
                       SY02F001                                                 
                       BP13KD00                                                 
                       BP13KD05                                                 
                       BM06K110                                                 
                       BP13K022                                                 
                       BP13K024                                                 
                       BP13K130                                                 
                       P04K010                                                  
              OUTPUT   BP13LD26.                                                
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD00 - ERROR OPENING : ' WS-KD00-STATUS              
              MOVE WS-KD00-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR OPENING : ' WS-K110-STATUS              
              MOVE WS-K110-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-KD05-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD05 - ERROR OPENING : ' WS-KD05-STATUS              
              MOVE WS-KD05-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K130-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K130 - ERROR OPENING : ' WS-K130-STATUS              
              MOVE WS-K130-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 - ERROR OPENING : ' WS-K022-STATUS              
              MOVE WS-K022-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K024 - ERROR OPENING : ' WS-K024-STATUS              
              MOVE WS-K024-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'P04K010 - ERROR OPENING : ' WS-K010-STATUS               
              MOVE WS-K010-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           READ SY02F001 AT END MOVE 'Y'  TO WS-F001-EOF.                       
                                                                                
           IF EOF-F001                                                          
              DISPLAY 'RECORD NOT FOUND IN SY01F001 '                           
              DISPLAY 'PROCESSING TERMINATED '                                  
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           ELSE                                                                 
              MOVE F001-DTE-CURRENT      TO   WS-SYSTEM-DATE                    
           END-IF.                                                              
                                                                                
           PERFORM 1100-CHECK-DATE     THRU 1100-EXIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1100-CHECK-DATE.                                                         
      ******************************************************************        
                                                                                
           MOVE WS-SYSTEM-DATE                    TO WS-DTE-LAST-WK.            
                                                                                
           COMPUTE WS-DAYS = 0.                                                 
           PERFORM UNTIL WS-DAYS = 7                                            
                   COMPUTE WS-DTE-NUM =                                         
                           FUNCTION INTEGER-OF-DATE (WS-DTE-LAST-WK)            
                   COMPUTE WS-DTE-NUM = WS-DTE-NUM - 1                          
                   COMPUTE WS-DTE-LAST-WK =                                     
                           FUNCTION DATE-OF-INTEGER (WS-DTE-NUM)                
                                                                                
                   MOVE WS-DTE-LAST-WK            TO WS-IN-DATE                 
                   PERFORM 1200-VALIDATE-DATE   THRU 1200-EXIT                  
                                                                                
           END-PERFORM.                                                         
                                                                                
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1200-VALIDATE-DATE.                                                      
      ******************************************************************        
                                                                                
           MOVE 'DW'                   TO WS-IN-OPTION.                         
           MOVE '000'                  TO WS-RETURN.                            
                                                                                
           CALL 'AV02C001'          USING LINK-REC.                             
                                                                                
           IF WS-RETURN = 'SUN' OR 'MON' OR 'TUE' OR 'WED' OR                   
                          'THU' OR 'FRI' OR 'SAT'                               
              ADD 1                                    TO WS-DAYS               
           END-IF.                                                              
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F800.                                                      
      ******************************************************************        
                                                                                
           READ BP13F800           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F800                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F800-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF F800-NUM-REGN > SPACES                                            
              IF F800-DTE-CANCEL NOT = SPACES AND LOW-VALUES AND ZEROES         
                 IF (F800-DTE-CANCEL >= WS-DTE-LAST-WK) AND                     
                    (F800-DTE-CANCEL <= WS-SYSTEM-DATE)                         
                     PERFORM 3200-READ-BP13KD00    THRU 3200-EXIT               
                     PERFORM 3400-READ-BM06K110    THRU 3400-EXIT               
                     PERFORM 4000-START-BP13KD05   THRU 4000-EXIT               
                 ELSE                                                           
                     ADD 1                           TO WS-F800-CDTE            
                 END-IF                                                         
              ELSE                                                              
                 ADD 1                               TO WS-F800-CDTE            
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F800       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3200-READ-BP13KD00.                                                      
      ******************************************************************        
                                                                                
           MOVE F800-NUM-REGN TO KD00-NUM-REGN.                                 
                                                                                
           READ BP13KD00.                                                       
                                                                                
           EVALUATE WS-KD00-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN 23                                                          
                    ADD 1                            TO WS-KD00-NFND            
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13KD00 : ' WS-KD00-STATUS          
                            ' REGN = ' KD00-NUM-REGN                            
                    PERFORM  9999-CLOSE-ROUTINE    THRU 9999-EXIT               
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3400-READ-BM06K110.                                                      
      ******************************************************************        
                                                                                
           MOVE F800-NUM-SCH-ACC                       TO K110-KEY-FLD.         
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BM06K110 : ' WS-K110-STATUS          
                            ' SCHEME ACCT = ' K110-KEY-FLD                      
                    PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-START-BP13KD05.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                                TO BP13KD05-REC.          
           INITIALIZE                                    BP13KD05-REC.          
                                                                                
           MOVE 'N'                                   TO WS-EOF-KD05.           
           MOVE F800-NUM-REGN                         TO KD05-NUM-REGN.         
                                                                                
           START BP13KD05 KEY = KD05-NUM-REGN.                                  
                                                                                
           EVALUATE WS-KD05-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 4200-READNEXT-BP13KD05  THRU 4200-EXIT              
                    PERFORM 4400-PROCESS-BP13KD05   THRU 4400-EXIT              
                            UNTIL WS-EOF-KD05 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    ADD 1                             TO WS-KD05-NFND           
               WHEN OTHER                                                       
                  DISPLAY 'START ERROR BP13KD05 : ' WS-KD05-STATUS              
                          ' REGN : ' KD05-NUM-REGN                              
                  PERFORM 9999-CLOSE-ROUTINE        THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4200-READNEXT-BP13KD05.                                                  
      ******************************************************************        
                                                                                
           READ BP13KD05 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-EOF-KD05.                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4400-PROCESS-BP13KD05.                                                   
      ******************************************************************        
                                                                                
           IF KD05-NUM-REGN = F800-NUM-REGN                                     
              PERFORM 5000-GET-FIELDS        THRU 5000-EXIT                     
              PERFORM 4200-READNEXT-BP13KD05 THRU 4200-EXIT                     
           ELSE                                                                 
             MOVE 'Y'                          TO WS-EOF-KD05                   
           END-IF.                                                              
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4600-START-P04K010.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                              TO P04K010-REC.             
           INITIALIZE                                  P04K010-REC.             
                                                                                
           MOVE 'N'                                 TO WS-EOF-K010.             
           MOVE ZEROES                              TO WS-CNT-P04K.             
           MOVE SPACES                              TO WS-DEBAR-CODE            
                                                       K010-PRIME-KEY.          
           MOVE KD05-NUM-NRIC                       TO K010-NUM-UINFIN.         
                                                                                
           START P04K010  KEY = K010-NUM-UINFIN.                                
                                                                                
           EVALUATE WS-K010-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 4700-READNEXT-P04K010  THRU 4700-EXIT               
                    PERFORM 4800-PROCESS-P04K010   THRU 4800-EXIT               
                            UNTIL WS-EOF-K010 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    ADD 1                            TO WS-P04K-NFND            
               WHEN OTHER                                                       
                    DISPLAY 'START ERROR P04K010 : ' WS-K010-STATUS             
                            ' NRIC : ' K010-NUM-UINFIN                          
                    PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT               
            END-EVALUATE.                                                       
                                                                                
       4600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       4700-READNEXT-P04K010.                                                   
      ******************************************************************        
                                                                                
           READ P04K010 NEXT RECORD                                             
                         AT END MOVE 'Y' TO WS-EOF-K010.                        
                                                                                
       4700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4800-PROCESS-P04K010.                                                    
      ******************************************************************        
                                                                                
           IF (K010-NUM-UINFIN = KD05-NUM-NRIC)                                 
               MOVE K010-REASON-CODE           TO WS-DEBAR-CODE                 
                                                                                
               IF LD26-PAY-TY NOT = SPACES AND LOW-VALUES                       
                  IF LD26-PAY-TY = 'HGT'                                        
                     IF VALID-CPF-CODE                                          
                        PERFORM 4900-CHECK-DEBARMENT-CODES                      
                                                        THRU 4900-EXIT          
                     ELSE                                                       
                        ADD 1                           TO WS-P04K-NFND         
                     END-IF                                                     
                  ELSE                                                          
                     IF VALID-AHG-CODE                                          
                        PERFORM 4900-CHECK-DEBARMENT-CODES                      
                                                        THRU 4900-EXIT          
                     ELSE                                                       
                        ADD 1                           TO WS-P04K-NFND         
                     END-IF                                                     
                  END-IF                                                        
               ELSE                                                             
                 IF VALID-AHG-CODE OR VALID-CPF-CODE                            
                    PERFORM 4900-CHECK-DEBARMENT-CODES  THRU 4900-EXIT          
                 ELSE                                                           
                   ADD 1                                TO WS-P04K-NFND         
                 END-IF                                                         
               END-IF                                                           
                                                                                
               PERFORM 4700-READNEXT-P04K010            THRU 4700-EXIT          
           ELSE                                                                 
               MOVE 'Y'                            TO WS-EOF-K010               
           END-IF.                                                              
                                                                                
       4800-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       4900-CHECK-DEBARMENT-CODES.                                              
      ******************************************************************        
                                                                                
            ADD 1                                    TO WS-CNT-P04K             
                                                        WS-P04K-FND.            
            STRING K010-BLOCK ' #'                                              
                   K010-HOUSE-NO 'S('                                           
                   K010-POSTCODE ')'                                            
               DELIMITED BY SIZE INTO WS-ADDRESS.                               
                                                                                
            EVALUATE WS-CNT-P04K                                                
                WHEN 1                                                          
                     MOVE WS-DEBAR-CODE              TO LD26-DBR-CODE           
                     MOVE WS-ADDRESS                 TO LD26-ADDRESS            
                     WRITE BP13LD26-REC              FROM LD26-DETAIL01         
                WHEN 2                                                          
                     MOVE WS-DEBAR-CODE              TO LD26-DBR-CODE-2         
                     MOVE WS-ADDRESS                 TO LD26-ADDRESS-2          
                     WRITE BP13LD26-REC              FROM LD26-DETAIL02         
                WHEN OTHER                                                      
                     PERFORM 6100-CHECK-WRITE-HEADER THRU 6100-EXIT             
                                                                                
                     MOVE SPACES                     TO LD26-DETAIL02           
                     MOVE WS-DEBAR-CODE              TO LD26-DBR-CODE-2         
                     MOVE WS-ADDRESS                 TO LD26-ADDRESS-2          
                     WRITE BP13LD26-REC              FROM LD26-DETAIL02         
                     ADD 1                           TO WS-LINE-CTR             
                     ADD 1                         TO WS-CNT-LD26-WRITE         
            END-EVALUATE.                                                       
                                                                                
       4900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-GET-FIELDS.                                                         
      ******************************************************************        
                                                                                
           MOVE SPACES                                TO LD26-DETAIL01          
                                                         LD26-DETAIL02.         
           INITIALIZE                                    LD26-DETAIL01          
                                                         LD26-DETAIL02.         
                                                                                
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
              DELIMITED BY SIZE INTO LD26-DATE-HDR.                             
                                                                                
           IF F800-DTE-CANCEL NOT = SPACES AND LOW-VALUES AND ZEROES            
              STRING F800-DTE-CANCEL(7:2) '/'                                   
                     F800-DTE-CANCEL(5:2) '/'                                   
                     F800-DTE-CANCEL(1:4)                                       
                 DELIMITED BY SIZE INTO LD26-HSGNT-CANCEL                       
           END-IF.                                                              
                                                                                
           MOVE F800-NUM-REGN                  TO LD26-REGN.                    
           MOVE KD05-NME-OCCP                  TO LD26-NME-OCCP.                
           MOVE KD05-NUM-NRIC                  TO LD26-NRIC.                    
                                                                                
           IF KD05-AMT-AHG-GRANT NOT NUMERIC                                    
              MOVE ZEROES                      TO KD05-AMT-AHG-GRANT            
           END-IF.                                                              
           MOVE KD05-AMT-AHG-GRANT             TO LD26-AHG-AMT.                 
                                                                                
           IF KD05-AMT-HSGNT NOT NUMERIC                                        
              MOVE ZEROES                      TO KD05-AMT-AHG-GRANT            
           END-IF.                                                              
           MOVE KD05-AMT-HSGNT                 TO LD26-HG-AMT.                  
                                                                                
           PERFORM 5200-GET-CSM-NAME         THRU 5200-EXIT.                    
           MOVE 'N'                            TO WS-K130-FOUND.                
           PERFORM 5500-START-BP13K130       THRU 5500-EXIT.                    
                                                                                
           IF WS-K130-FOUND = 'N'                                               
              PERFORM 6000-PRINT-REPORT      THRU 6000-EXIT                     
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5200-GET-CSM-NAME.                                                       
      ******************************************************************        
                                                                                
            MOVE SPACES                               TO BP13K022-REC.          
            INITIALIZE                                   BP13K022-REC.          
                                                                                
            MOVE F800-NUM-REGN                        TO K022-NUM-REGN.         
                                                                                
            READ BP13K022 KEY IS K022-NUM-REGN.                                 
                                                                                
            EVALUATE WS-K022-STATUS                                             
                WHEN 00                                                         
                     PERFORM 5400-READ-BP13K024       THRU 5400-EXIT            
                WHEN 23                                                         
                     MOVE SPACES TO LD26-CSM                                    
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K022 : ' WS-K022-STATUS         
                             ' REGN NO = ' K022-NUM-REGN                        
                     PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT            
            END-EVALUATE.                                                       
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5400-READ-BP13K024.                                                      
      ******************************************************************        
                                                                                
            MOVE SPACES                                TO BP13K024-REC.         
            INITIALIZE                                    BP13K024-REC.         
                                                                                
            MOVE K022-NUM-OIC                          TO K024-NUM-OIC.         
                                                                                
            READ BP13K024.                                                      
                                                                                
            EVALUATE WS-K024-STATUS                                             
                WHEN 00                                                         
                     MOVE K024-NME-OIC                 TO LD26-CSM              
                WHEN 23                                                         
                     MOVE SPACES                       TO LD26-CSM              
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K024 : ' WS-K024-STATUS         
                             ' OIC  = ' K024-NUM-OIC                            
                     PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT             
            END-EVALUATE.                                                       
                                                                                
       5400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5500-START-BP13K130.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                           TO BP13K130-CDHIST.            
           INITIALIZE                               BP13K130-CDHIST.            
           MOVE 'N'                              TO WS-EOF-K130.                
                                                                                
           MOVE KD05-NUM-REGN                    TO K130-NUM-ORIG-REGN.         
                                                                                
           START BP13K130 KEY = K130-NUM-ORIG-REGN.                             
                                                                                
           EVALUATE WS-K130-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 5600-READNEXT-BP13K130 THRU 5600-EXIT               
                    PERFORM 5700-PROCESS-BP13K130  THRU 5700-EXIT               
                            UNTIL WS-EOF-K130 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    MOVE SPACES                      TO LD26-PAY-TY             
                                                        LD26-CR-CODE            
               WHEN OTHER                                                       
                  DISPLAY 'START ERROR BP13K130 : ' WS-K130-STATUS              
                          ' REGN : ' K130-NUM-ORIG-REGN                         
                  PERFORM 9999-CLOSE-ROUTINE       THRU 9999-EXIT               
           END-EVALUATE.                                                        
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5600-READNEXT-BP13K130.                                                  
      ******************************************************************        
                                                                                
           READ BP13K130 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-EOF-K130.                        
                                                                                
       5600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5700-PROCESS-BP13K130.                                                   
      ******************************************************************        
                                                                                
           IF (KD05-NUM-REGN     = K130-NUM-ORIG-REGN)                          
              IF (KD05-NUM-NRIC     = K130-NUM-NRIC) AND                        
                 (K130-PAYMENT-TYPE = 'AHG' OR 'HGT')                           
                  MOVE 'Y'                            TO WS-K130-FOUND          
                  MOVE K130-PAYMENT-TYPE              TO LD26-PAY-TY            
                  MOVE K130-NUM-RECPT-JRNO            TO LD26-CR-CODE           
                  PERFORM 6000-PRINT-REPORT         THRU 6000-EXIT              
              END-IF                                                            
              PERFORM 5600-READNEXT-BP13K130        THRU 5600-EXIT              
           ELSE                                                                 
               MOVE 'Y'                               TO WS-EOF-K130            
           END-IF.                                                              
                                                                                
       5700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6000-PRINT-REPORT.                                                       
      ******************************************************************        
                                                                                
           IF (F800-NUM-REGN = WS-REGN-NO)                                      
               MOVE SPACES                       TO LD26-SN                     
                                                    LD26-REGN                   
           ELSE                                                                 
               ADD 1                              TO WS-SNO                     
               MOVE WS-SNO                        TO WS-SNO-ZZZZ9               
               MOVE WS-SNO-ZZZZ9                  TO LD26-SN                    
               MOVE F800-NUM-REGN                 TO WS-REGN-NO                 
           END-IF.                                                              
                                                                                
           PERFORM 6100-CHECK-WRITE-HEADER      THRU 6100-EXIT.                 
           PERFORM 4600-START-P04K010           THRU 4600-EXIT.                 
                                                                                
           IF WS-CNT-P04K = 0                                                   
              WRITE BP13LD26-REC                FROM LD26-DETAIL01              
              WRITE BP13LD26-REC                FROM LD26-DETAIL02              
           END-IF.                                                              
                                                                                
           ADD 1                                  TO WS-LINE-CTR.               
           ADD 1                                  TO WS-CNT-LD26-WRITE.         
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6100-CHECK-WRITE-HEADER.                                                 
      ******************************************************************        
                                                                                
           IF (WS-LINE-CTR + WS-CNT-WRITE) > WS-MAX-LINE OR                     
                                             WS-FIRST-WRITE = 'Y'               
              MOVE 'N'                         TO WS-FIRST-WRITE                
              PERFORM 7000-PRINT-HEADER      THRU 7000-EXIT                     
           END-IF.                                                              
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7000-PRINT-HEADER.                                                       
      ******************************************************************        
                                                                                
           ADD 1                               TO WS-PAGE-CTR.                  
           MOVE ZEROES                         TO LD26-PAGE-HDR.                
           MOVE WS-PAGE-CTR                    TO LD26-PAGE-HDR.                
                                                                                
           WRITE BP13LD26-REC    FROM LD26-HEADER01 AFTER PAGE.                 
           WRITE BP13LD26-REC    FROM LD26-HEADER02.                            
           WRITE BP13LD26-REC    FROM LD26-HEADER03 AFTER 2.                    
           WRITE BP13LD26-REC    FROM LD26-HEADER04.                            
                                                                                
           INITIALIZE WS-LINE-CTR.                                              
           MOVE 5  TO WS-LINE-CTR.                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CD26'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F800 RECORDS READ............. : '            
                    WS-F800-READ.                                               
           DISPLAY '(2) NO OF BP13F800 RECS READ OUTSIDE PREV WK-DTE: '         
                    WS-F800-CDTE.                                               
           DISPLAY '(3) NO OF BP13KD00 RECORDS NOT FOUND........ : '            
                    WS-KD00-NFND.                                               
           DISPLAY '(4) NO OF BP13KD05 RECORDS NOT FOUND........ : '            
                    WS-KD05-NFND.                                               
           DISPLAY '(5) NO OF S/N ROWS FOUND.................... : '            
                    WS-SNO.                                                     
           DISPLAY '(6) NO OF BP13LD26 RECORDS WRITTEN.......... : '            
                    WS-CNT-LD26-WRITE.                                          
           DISPLAY '(7) NO OF P04K010  RECORDS FOUND............ : '            
                    WS-P04K-FND.                                                
           DISPLAY '(8) NO OF P04K010  RECORDS NOT FOUND........ : '            
                    WS-P04K-NFND.                                               
                                                                                
           CLOSE    BP13F800                                                    
                    SY02F001                                                    
                    BP13KD00                                                    
                    BP13KD05                                                    
                    BM06K110                                                    
                    BP13K022                                                    
                    BP13K024                                                    
                    BP13K130                                                    
                    P04K010                                                     
                    BP13LD26.                                                   
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD00 - ERROR CLOSING : ' WS-KD00-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K130-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K130 - ERROR CLOSING : ' WS-K130-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR CLOSING : ' WS-K110-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-KD05-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD05 - ERROR CLOSING : ' WS-KD05-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 - ERROR CLOSING : ' WS-K022-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K024 - ERROR CLOSING : ' WS-K024-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'P04K010  - ERROR CLOSING : ' WS-K010-STATUS              
              MOVE WS-K010-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
