       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C479.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  18/05/2017.                                               
      * ========================================================== *            
      * OBJECTIVE    : WEEKLY PROGRAM TO PRINT REPORT ON THE NO OF *            
      *                EHPS TAKEUP                                 *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13F410                               *            
      * OUTPUT FILES :  1.) BP13L479                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP136798 KSJ3 18/05/2017 NEW PROGRAM                       *            
      * BP136851 PCL4 20/06/2017 TO UPDATE EMAIL RECIPIENTS        *            
      * BP136869 KSJ3 26/07/2017 TO CHECK 0 WS-NUM-TOT-NO-LOAN     *            
      *                          BEFORE COMPUTE TO FIX SOCB ABEND  *            
      * BP139030 ZAR7 07/01/2022 REPLACE CHUA YEE MIEN TO LIM KOK CHUN          
      * BP139276 KAC1 05/04/2024 REPLACE LIM KOK CHUN BY           *            
      *                                  KATHIJAH_BEE_ALI_MOHAMED  *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410  ASSIGN        TO BP13F410.                          
                                                                                
           SELECT BP13L479  ASSIGN        TO BP13L479.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F410                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
           COPY BP13F410.                                                       
                                                                                
       FD  BP13L479                                                             
           RECORD CONTAINS  132 CHARACTERS.                                     
       01  BP13L479-REC                  PIC X(132).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F410-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F410-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-L479-WRITE         PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-PAGE               PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-LINE               PIC 9(2)   VALUE 99.                   
           05  WS-CNT-SNO                PIC 9(4)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F410-EOF               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE ZEROES.               
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-DTE-SO-FIRST           PIC X(8)   VALUE SPACES.               
           05  WS-DTE-SO-PREV            PIC X(8)   VALUE SPACES.               
           05  WS-NUM-TOT-E              PIC 9(4)   VALUE ZEROES.               
           05  WS-NUM-TOT-H              PIC 9(4)   VALUE ZEROES.               
           05  WS-NUM-TOT-BANK-LOAN      PIC 9(4)   VALUE ZEROES.               
           05  WS-NUM-TOT-NO-LOAN        PIC 9(4)   VALUE ZEROES.               
           05  WS-NUM-TOT-OVERALL        PIC 9(4)   VALUE ZEROES.               
           05  WS-NUM-TOT-E-P            PIC 9(3)V9999 VALUE ZEROES.            
           05  WS-NUM-TOT-H-P            PIC 9(3)V9999 VALUE ZEROES.            
                                                                                
      *-------------------------------------------------------------            
      *    EMAIL REPORT COMMANDS                                                
      *-------------------------------------------------------------            
       01  MAIL-COMMANDS.                                                       
           05  WS-MAIL-HELO              PIC X(132) VALUE                       
               'HELO SGPHDB1'.                                                  
           05  WS-MAIL-MAIL-FROM         PIC X(132) VALUE                       
               'MAIL FROM:<OPCP@SGPHDB1>'.                                      
           05  WS-MAIL-RCPT-TO-2         PIC X(132) VALUE                       
               'RCPT TO:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                            
           05  WS-MAIL-RCPT-TO-3         PIC X(132) VALUE                       
               'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                 
           05  WS-MAIL-RCPT-TO-4         PIC X(132) VALUE                       
               'RCPT TO:<SERENA_JM_TEH@HDB.GOV.SG>'.                            
           05  WS-MAIL-RCPT-TO-5         PIC X(132) VALUE                       
               'RCPT TO:<JAMILAH_BAHROM@HDB.GOV.SG>'.                           
           05  WS-MAIL-DATA              PIC X(132) VALUE 'DATA'.               
           05  WS-MAIL-FROM              PIC X(132) VALUE                       
               'FROM:Soc System - Email Alert'.                                 
           05  WS-MAIL-TO-2              PIC X(132) VALUE                       
               'TO:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                                 
           05  WS-MAIL-TO-3              PIC X(132) VALUE                       
               'TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                      
           05  WS-MAIL-TO-4              PIC X(132) VALUE                       
               'TO:<SERENA_JM_TEH@HDB.GOV.SG>'.                                 
           05  WS-MAIL-TO-5              PIC X(132) VALUE                       
               'TO:<JAMILAH_BAHROM@HDB.GOV.SG>'.                                
           05  WS-MAIL-SUBJECT.                                                 
               10  FILLER                PIC X(132) VALUE                       
                   'SUBJECT : REPORT ON TP/SATP CASES FOR CPF PAYMENT AN        
      -            'ND/OR HEALTH DECLARATION FOR HPS COVER'.                    
           05  WS-MAIL-DATE.                                                    
               10  FILLER                PIC X(6)   VALUE                       
                   'DATE: '.                                                    
               10  WS-MAIL-DTE-CURRENT   PIC X(10)  VALUE SPACES.               
                                                                                
      *-------------------------------------------------------------            
      *    REPORT LAYOUT                                                        
      *-------------------------------------------------------------            
       01  L479-PR-HEAD-01.                                                     
           05  FILLER                 PIC X(14)   VALUE 'BP13L479'.             
           05  FILLER                 PIC X(8)    VALUE 'HDB3    '.             
           05  FILLER                 PIC X(25)   VALUE SPACES.                 
           05  FILLER                 PIC X(39)   VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                 PIC X(13)   VALUE SPACES.                 
           05  FILLER                 PIC X(7)    VALUE 'DATE: '.               
           05  PRINT-CURRENT-DATE     PIC X(10)   VALUE SPACES.                 
           05  FILLER                 PIC X(3)    VALUE SPACES.                 
           05  FILLER                 PIC X(7)    VALUE 'PAGE: '.               
           05  PRINT-PAGE             PIC ZZZ9.                                 
                                                                                
       01  L479-PR-HEAD-02.                                                     
           05  FILLER                 PIC X(14)   VALUE SPACES.                 
           05  FILLER                 PIC X(104)  VALUE                         
              'REPORT ON TP/SATP CASES FOR CPF PAYMENT AND/OR HEALTH DEC        
      -       'LARATION FOR HPS COVER (ELECTRONIC AND MANUAL)'.                 
                                                                                
       01  L479-PR-HEAD-03.                                                     
           05  FILLER                 PIC X(45)   VALUE SPACES.                 
           05  FILLER                 PIC X(01)   VALUE '('.                    
           05  PRINT-WEEK-START       PIC X(10)   VALUE SPACES.                 
           05  FILLER                 PIC X(04)   VALUE ' TO '.                 
           05  PRINT-WEEK-END         PIC X(10)   VALUE SPACES.                 
           05  FILLER                 PIC X(01)   VALUE ')'.                    
                                                                                
       01  L479-PR-TITLE-1.                                                     
           05  FILLER                 PIC X(132)  VALUE                         
              '     ----------     -------------------------------------        
      -       '---------------------------     ----------------     ----        
      -       '------------'.                                                   
                                                                                
       01  L479-PR-TITLE-2.                                                     
           05  FILLER                 PIC X(132)  VALUE                         
              '       SALES                      H D B   L O A N   /   N        
      -       ' O   L O A N                    B A N K  L O A N'.               
                                                                                
       01  L479-PR-TITLE-3.                                                     
           05  FILLER                 PIC X(132)  VALUE                         
              '       ORDER        -------------------------------------        
      -       '---------------------------     ----------------      OVE        
      -       'RALL TOTAL'.                                                     
                                                                                
       01  L479-PR-TITLE-4.                                                     
           05  FILLER                 PIC X(132)  VALUE                         
              '       DATE         NO OF E-CPF CASES SENT     NO OF MANU        
      -       'AL CPF CASES SENT     TOTAL     NO OF CASES SENT     NO O        
      -       'F CASES SENT'.                                                   
                                                                                
       01  L479-PR-TITLE-5.                                                     
           05  FILLER                 PIC X(132)  VALUE                         
              '     ----------     ----------------------     ----------        
      -       '-----------------     -----     ----------------     ----        
      -       '------------'.                                                   
                                                                                
       01  L479-PR-DTL.                                                         
           05  FILLER                 PIC X(5)    VALUE SPACES.                 
           05  L479-DTE-SO            PIC X(10).                                
           05  FILLER                 PIC X(14)   VALUE SPACES.                 
           05  L479-NUM-ECPF-SENT     PIC ZZ9.                                  
           05  FILLER                 PIC X(2)    VALUE ' ('.                   
           05  L479-NUM-ECPF-SENT-P   PIC ZZ9.99.                               
           05  FILLER                 PIC X(2)    VALUE '%)'.                   
           05  FILLER                 PIC X(19)   VALUE SPACES.                 
           05  L479-NUM-HCOPY-SENT    PIC ZZ9.                                  
           05  FILLER                 PIC X(2)    VALUE ' ('.                   
           05  L479-NUM-HCOPY-SENT-P  PIC ZZ9.99.                               
           05  FILLER                 PIC X(2)    VALUE '%)'.                   
           05  FILLER                 PIC X(6)    VALUE SPACES.                 
           05  L479-NUM-TOT-NOLOAN    PIC ZZZ9.                                 
           05  FILLER                 PIC X(18)   VALUE SPACES.                 
           05  L479-NUM-BANK-LOAN     PIC ZZ9.                                  
           05  FILLER                 PIC X(17)   VALUE SPACES.                 
           05  L479-NUM-OVERALL-TOT   PIC ZZZ9.                                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
           PERFORM 2000-BP13F410-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F410-EOF = 'Y'                                      
           PERFORM 3900-PRINT-REPORT       THRU 3900-EXIT.                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13F410                                                 
                OUTPUT BP13L479.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    GET CURRENT DATE                                                     
      *-------------------------------------------------------------            
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13F410-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13F410 NEXT                                                   
              AT END MOVE 'Y'              TO   WS-F410-EOF.                    
                                                                                
           IF WS-F410-EOF NOT = 'Y'                                             
              ADD 1                        TO   WS-CNT-F410-READ                
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           IF F410-DTE-SO-EXECUTION = SPACES OR LOW-VALUES OR ZEROES            
              GO TO 3000-CONTINUE-NEXT-REC.                                     
                                                                                
           IF (WS-DTE-SO-PREV NOT = F410-DTE-SO-EXECUTION) AND                  
              (WS-DTE-SO-PREV NOT = SPACES AND LOW-VALUES AND ZEROES)           
                                                                                
              IF WS-DTE-SO-FIRST = SPACES OR LOW-VALUES OR ZEROES               
                 MOVE WS-DTE-SO-PREV       TO   WS-DTE-SO-FIRST                 
              END-IF                                                            
                                                                                
              PERFORM 3900-PRINT-REPORT    THRU 3900-EXIT                       
                                                                                
              MOVE ZEROES                  TO   WS-NUM-TOT-E                    
                                                WS-NUM-TOT-H                    
                                                WS-NUM-TOT-BANK-LOAN            
           END-IF.                                                              
                                                                                
           IF F410-NUM-WARRANT-ACT = 'ES '                                      
              ADD 1                        TO   WS-NUM-TOT-BANK-LOAN            
           ELSE                                                                 
              IF F410-TAG-EHPS-APPLN = 'Y'                                      
                 ADD 1                     TO   WS-NUM-TOT-E                    
              ELSE                                                              
                 ADD 1                     TO   WS-NUM-TOT-H                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-CONTINUE-NEXT-REC.                                                  
                                                                                
           MOVE F410-DTE-SO-EXECUTION      TO   WS-DTE-SO-PREV.                 
                                                                                
           PERFORM 2000-BP13F410-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3900-PRINT-REPORT.                                                       
      *=============================================================            
                                                                                
           IF WS-CNT-LINE > 55                                                  
              PERFORM 3910-PRINT-REPORT-HDR     THRU 3910-EXIT                  
           END-IF.                                                              
                                                                                
      *    MOVE SPACES                     TO   L479-PR-DTL.                    
                                                                                
           ADD 1                           TO   WS-CNT-SNO.                     
           STRING WS-DTE-SO-PREV(7:2) '/' WS-DTE-SO-PREV(5:2) '/'               
                  WS-DTE-SO-PREV(1:4)                                           
                  DELIMITED BY SIZE        INTO L479-DTE-SO                     
           MOVE WS-NUM-TOT-E               TO   L479-NUM-ECPF-SENT              
           MOVE WS-NUM-TOT-H               TO   L479-NUM-HCOPY-SENT             
           COMPUTE WS-NUM-TOT-NO-LOAN     = WS-NUM-TOT-E + WS-NUM-TOT-H.        
                                                                                
           MOVE WS-NUM-TOT-NO-LOAN         TO   L479-NUM-TOT-NOLOAN             
                                                                                
           IF WS-NUM-TOT-NO-LOAN > ZEROES                                       
              COMPUTE WS-NUM-TOT-E-P ROUNDED = (WS-NUM-TOT-E /                  
                                             WS-NUM-TOT-NO-LOAN) * 100          
           ELSE                                                                 
              COMPUTE WS-NUM-TOT-E-P = ZEROES                                   
           END-IF.                                                              
                                                                                
           COMPUTE L479-NUM-ECPF-SENT-P ROUNDED = WS-NUM-TOT-E-P.               
                                                                                
           IF WS-NUM-TOT-NO-LOAN > ZEROES                                       
              COMPUTE WS-NUM-TOT-H-P ROUNDED = (WS-NUM-TOT-H /                  
                                             WS-NUM-TOT-NO-LOAN) * 100          
           ELSE                                                                 
              COMPUTE WS-NUM-TOT-H-P = ZEROES                                   
           END-IF.                                                              
                                                                                
           COMPUTE L479-NUM-HCOPY-SENT-P ROUNDED = WS-NUM-TOT-H-P.              
           MOVE WS-NUM-TOT-BANK-LOAN       TO   L479-NUM-BANK-LOAN              
           COMPUTE WS-NUM-TOT-OVERALL     = WS-NUM-TOT-BANK-LOAN +              
                                            WS-NUM-TOT-NO-LOAN.                 
           MOVE WS-NUM-TOT-OVERALL         TO   L479-NUM-OVERALL-TOT            
                                                                                
           WRITE BP13L479-REC FROM L479-PR-DTL.                                 
                                                                                
           ADD 1                           TO   WS-CNT-L479-WRITE.              
                                                                                
       3900-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3910-PRINT-REPORT-HDR.                                                   
      *=============================================================            
                                                                                
           ADD 1                           TO   WS-CNT-PAGE.                    
           MOVE WS-CNT-PAGE                TO   PRINT-PAGE.                     
           MOVE WS-DTE-CURR-10             TO   PRINT-CURRENT-DATE              
                                                PRINT-WEEK-END                  
                                                WS-MAIL-DTE-CURRENT.            
                                                                                
           WRITE BP13L479-REC              FROM WS-MAIL-HELO.                   
           WRITE BP13L479-REC              FROM WS-MAIL-MAIL-FROM.              
           WRITE BP13L479-REC              FROM WS-MAIL-RCPT-TO-2.              
           WRITE BP13L479-REC              FROM WS-MAIL-RCPT-TO-3.              
           WRITE BP13L479-REC              FROM WS-MAIL-RCPT-TO-4.              
           WRITE BP13L479-REC              FROM WS-MAIL-RCPT-TO-5.              
           WRITE BP13L479-REC              FROM WS-MAIL-DATA.                   
           WRITE BP13L479-REC              FROM WS-MAIL-FROM.                   
           WRITE BP13L479-REC              FROM WS-MAIL-TO-2.                   
           WRITE BP13L479-REC              FROM WS-MAIL-TO-3.                   
           WRITE BP13L479-REC              FROM WS-MAIL-TO-4.                   
           WRITE BP13L479-REC              FROM WS-MAIL-TO-5.                   
           WRITE BP13L479-REC              FROM WS-MAIL-SUBJECT.                
           WRITE BP13L479-REC              FROM WS-MAIL-DATE.                   
                                                                                
           STRING WS-DTE-SO-FIRST(7:2) '/' WS-DTE-SO-FIRST(5:2) '/'             
                  WS-DTE-SO-FIRST(1:4)                                          
                  DELIMITED BY SIZE        INTO PRINT-WEEK-START.               
                                                                                
           WRITE BP13L479-REC FROM L479-PR-HEAD-01.                             
           WRITE BP13L479-REC FROM L479-PR-HEAD-02.                             
           WRITE BP13L479-REC FROM L479-PR-HEAD-03.                             
                                                                                
           MOVE SPACES                     TO   BP13L479-REC.                   
           WRITE BP13L479-REC.                                                  
                                                                                
           WRITE BP13L479-REC FROM L479-PR-TITLE-1.                             
           WRITE BP13L479-REC FROM L479-PR-TITLE-2.                             
           WRITE BP13L479-REC FROM L479-PR-TITLE-3.                             
           WRITE BP13L479-REC FROM L479-PR-TITLE-4.                             
           WRITE BP13L479-REC FROM L479-PR-TITLE-5.                             
                                                                                
           MOVE SPACES                     TO   BP13L479-REC.                   
           WRITE BP13L479-REC.                                                  
                                                                                
           MOVE 9                          TO   WS-CNT-LINE.                    
                                                                                
       3910-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13F410                                                       
                 BP13L479.                                                      
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C479 CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13F410 READ      : ' WS-CNT-F410-READ.          
           DISPLAY ' 2. NO OF BP13F410 SKIPPED   : ' WS-CNT-F410-READ.          
           DISPLAY ' 3. NO OF BP13L479 WRITTEN   : ' WS-CNT-L479-WRITE.         
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
