      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C38G.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  NOVEMBER 20, 2013.                                        
      *===============================================================*         
      * OBJECTIVE  :  PRINT EXCEPTION REPORT FOR NO BC DEBARMENT TAG  *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F410                                     *         
      *               2. BP13K800                                     *         
      *               3. BP13K820                                     *         
      *               4. P04K010                                      *         
      *               5. BP13K022                                     *         
      *               6. BP13K024                                     *         
      *                                                               *         
      * OUTPUT FILE:  1. BP13L38G                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP135098 20/11/2013 PCL4 NEW PROGRAM.                         *         
      * BP135098 20/02/2014 PCL4 ADD CHECK ON BP13K893 DTE-REGN       *         
      * BP135545 28/01/2015 PCL4 TO ADD READING OF BP13K060           *         
      * BP135895 19/08/2015 PCL4 TO ADD CHECK ON BP13K893 BY SC CODE  *         
      * BP136077 28/12/2015 PCL4 TO ADD CHECK FOR PPS (BC) DEBAR      *         
      * BP136082 27/01/2016 KVL1 REPLACED SU1 W/ HWM2 IN THE CC AND   *         
      *                          CHANGED ALL RECIPIENTS TO LONG NAME  *         
      * BP136477 18/10/2016 RJB1 TO REPLACE KIM HUAT TO YEE MIEN      *         
      * BP136600 14/03/2017 PCL4 TO UPDATE EMAIL RECIPIENT            *         
      * BP136685 06/04/2017 PCL4 TO ADD CHECK ON BP13K895             *         
      * BP139030 07/01/2022 ZAR7  REPLACE CHUA YEE MIEN TO LIM KOK CHUN         
      * BP139276 05/04/2024 KAC1  REPLACE LIM KOK CHUN BY             *         
      *                                   KATHIJAH_BEE_ALI_MOHAMED    *         
      * BP139160 17/04/2024 KAC1 Update email recipient:              *         
      *                            TO: Chee Khiang CHIAM & Juariah ALI*         
      *                            CC: Soo Chiew low,Catherine HL TAN&*         
      *                                Catherine HL TAN               *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F410  ASSIGN        TO BP13F410.                         
           SELECT  BP13K895  ASSIGN        TO BP13K895                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K895-KEY-FLD                      
                             FILE STATUS   IS WS-K895-STATUS.                   
           SELECT  BP13K893  ASSIGN        TO BP13K893                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K893-KEY-FLD                      
                             FILE STATUS   IS WS-K893-STATUS.                   
           SELECT  BP13K022  ASSIGN        TO BP13K022                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K022-KEY-FLD                      
                             ALTERNATE RECORD KEY IS K022-NUM-REGN              
                             FILE STATUS   IS WS-K022-STATUS.                   
           SELECT  BP13K024  ASSIGN        TO BP13K024                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K024-KEY-FLD                      
                             FILE STATUS   IS WS-K024-STATUS.                   
           SELECT  BP13K800  ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
           SELECT  BP13K820  ASSIGN        TO BP13K820                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K820-KEY-FLD                      
                             FILE STATUS   IS WS-K820-STATUS.                   
           SELECT  P04K010   ASSIGN        TO P04K010                           
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K010-PRIME-KEY                    
                             FILE STATUS   IS WS-K010-STATUS.                   
           SELECT  BP13K060  ASSIGN        TO BP13K060                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K060-KEY-FLD                      
                             FILE STATUS   IS WS-K060-STATUS.                   
           SELECT  BP13L38G  ASSIGN        TO BP13L38G.                         
           SELECT  MAILC38G  ASSIGN        TO MAILC38G.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F410                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13K410.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS 25 CHARACTERS.                                      
       COPY BP13K060.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD   BP13K895                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K895.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K024                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K024.                                                           
                                                                                
       FD   P04K010                                                             
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY P04K010.                                                            
                                                                                
       FD   BP13L38G                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   BP13L38G-REC               PIC X(132).                              
                                                                                
       FD   MAILC38G                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS  100 CHARACTERS.                                    
       01   MAILC38G-REC               PIC X(100).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F410             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K820             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K895             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K010             PIC X(1)  VALUE 'N'.                     
           05  WS-K060-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K893-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K895-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K820-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K022-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K024-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K010-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-HA-INFO.                                                      
               10  HA-INFO OCCURS 2 TIMES.                                      
                   20  HA-NRIC               PIC  X(09).                        
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                    PIC 9(1)  VALUE ZEROES.                  
           05  WS-HA                   PIC 9(1)  VALUE ZEROES.                  
           05  WS-F410-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-K820-NFND            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-P04K             PIC 9(7)  VALUE ZEROES.                  
           05  WS-P04K-FND             PIC 9(7)  VALUE ZEROES.                  
           05  WS-P04K-NFND            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
           05  WS-CNT-L38G-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-L38X-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(1)  VALUE 2.                       
           05  WS-MAX-LINE             PIC 9(2)  VALUE 55.                      
                                                                                
       01  WS-BP13K060-KEY.                                                     
           05 WS-DPC-SERIAL-NO         PIC X(2)  VALUE SPACES.                  
           05 WS-DPC-CODE.                                                      
              10 WS-CODE1              PIC X     VALUE SPACES.                  
              10 WS-CODE2              PIC X     VALUE SPACES.                  
           05 FILLER                   PIC X     VALUE SPACES.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-CTZ                   PIC X(02) VALUE SPACES.                  
           05 WS-BC-CODE               PIC X(01) VALUE SPACES.                  
           05 WS-BC-DEBAR              PIC X(01) VALUE SPACES.                  
           05 WS-SPOUSE-NRIC           PIC X(09) VALUE SPACES.                  
           05 WS-BC-NRIC               PIC X(09) VALUE SPACES.                  
           05 WS-PRIOR-DTE             PIC 9(08) VALUE ZEROES.                  
           05 WS-NUM-DTE-APPT          PIC 9(07) VALUE ZEROES.                  
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-F410-NUM-REGN         PIC X(8)  VALUE SPACES.                  
           05 WS-NUM-NRIC              PIC X(9)  VALUE SPACES.                  
           05 WS-NUM-RELN              PIC X(20) VALUE SPACES.                  
           05 WS-SNO-ZZZZ9             PIC ZZZZ9 VALUE ZEROES.                  
           05 WS-FIRST-WRITE           PIC X(1)  VALUE 'Y'.                     
           05 WS-NUM-RELATIONSHIP      PIC X(02) VALUE SPACES.                  
           05 WS-DCODE                 PIC X(3)  VALUE SPACES.                  
           05 WS-CHECK-FC-DEBAR        PIC X(1)  VALUE SPACES.                  
           05 WS-FC-DEBAR              PIC X(1)  VALUE SPACES.                  
                                                                                
       01  WS-DTE-REGN.                                                         
           05 WS-DTE-CCYY                   PIC 9(4).                           
           05 WS-DTE-MM                     PIC 9(2).                           
           05 WS-DTE-DD                     PIC 9(2).                           
                                                                                
       01  WS-DTE-BIRTH.                                                        
           05 WS-DTE-BIRTH-CCYY        PIC 9(4).                                
           05 WS-DTE-BIRTH-MMDD        PIC 9(4).                                
                                                                                
       01  WS-NUM-AGE                  PIC 9(3)   VALUE ZERO.                   
                                                                                
                                                                                
       01 WS-PRIO-DTE.                                                          
          05  WS-PRIO-CCYY                 PIC 9(04).                           
          05  WS-PRIO-MM                   PIC 9(02).                           
          05  WS-PRIO-DD                   PIC 9(02).                           
                                                                                
       01 WS-CURR-DATE.                                                         
          05  WS-CURR-CCYY                 PIC 9(04).                           
          05  WS-CURR-MM                   PIC 9(02).                           
          05  WS-CURR-DD                   PIC 9(02).                           
                                                                                
       01  WS-DEBAR-CODE               PIC X(02).                               
           88 VALID-YR-CODE          VALUE 'YR'.                                
           88 VALID-YS-CODE          VALUE 'YS'.                                
                                                                                
       01  L38G-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13L38G'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  L38G-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L38G-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
                                                                                
       01  L38G-HEADER02.                                                       
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(48)     VALUE                          
               'EXCEPTION REPORT ON PARENTHOOD PRIORITY SCHEME,'.               
           05  FILLER              PIC X(47)     VALUE                          
               'NO CHILD BC AND NO BC DEBARMENT TAG IMPOSED'.                   
                                                                                
       01  L38G-HEADER03.                                                       
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(05)    VALUE '  S/N'.                  
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(08)    VALUE 'REGN NO '.               
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(13)    VALUE 'KEY ISSUE-DTE'.          
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(09)    VALUE 'NRIC'.                   
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(13)    VALUE 'FIN OF SPOUSE'.          
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(30)    VALUE 'CSM        '.            
           05  FILLER              PIC X(01)    VALUE SPACES.                   
                                                                                
       01  L38G-DETAIL01.                                                       
           05  FILLER1             PIC X(01)        VALUE SPACES.               
           05  L38G-SN             PIC X(05)        VALUE SPACES.               
           05  FILLER2             PIC X(01)        VALUE SPACES.               
           05  L38G-REGN           PIC X(08)        VALUE SPACES.               
           05  FILLER3             PIC X(01)        VALUE SPACES.               
           05  L38G-DTE-ISSUE      PIC X(13)        VALUE SPACES.               
           05  FILLER4             PIC X(01)        VALUE SPACES.               
           05  L38G-NRIC           PIC X(09)        VALUE SPACES.               
           05  FILLER5             PIC X(01)        VALUE SPACES.               
           05  L38G-SPOUSE         PIC X(13)        VALUE SPACES.               
           05  FILLER6             PIC X(01)        VALUE SPACES.               
           05  L38G-CSM            PIC X(30)        VALUE SPACES.               
           05  FILLER7             PIC X(01)        VALUE SPACES.               
                                                                                
       01  WS-REPORT-DTL.                                                       
           05  WS-EMPTY-RPT.                                                    
               10 FILLER                  PIC X(30) VALUE SPACES.               
               10 FILLER                  PIC X(21)                             
                                          VALUE '<   EMPTY  REPORT   >'.        
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT3             PIC X(50)                               
               VALUE 'RCPT TO:<CHIAM_Chee_Khiang@hdb.gov.sg>'.                  
           05  WS-MAIL-RPT4             PIC X(50)                               
               VALUE 'RCPT TO:<JUARIAH_ALI@HDB.GOV.SG>'.                        
           05  WS-MAIL-RPT5             PIC X(50)                               
               VALUE 'RCPT TO:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                      
           05  WS-MAIL-RPT5A            PIC X(50)                               
               VALUE 'RCPT TO:<Catherine_HL_TAN@hdb.gov.sg>'.                   
           05  WS-MAIL-RPT6             PIC X(50)                               
               VALUE 'RCPT TO:<JAMILAH_BAHROM@HDB.GOV.SG>'.                     
           05  WS-MAIL-RPT7             PIC X(4)                                
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT8             PIC X(29)                               
               VALUE 'FROM:Soc System - Email Alert'.                           
           05  WS-MAIL-RPT9             PIC X(50)                               
               VALUE 'TO:<CHIAM_Chee_Khiang@hdb.gov.sg>'.                       
           05  WS-MAIL-RPT10            PIC X(50)                               
               VALUE 'TO:<JUARIAH_ALI@HDB.GOV.SG>'.                             
           05  WS-MAIL-RPT11            PIC X(50)                               
               VALUE 'CC:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                           
           05  WS-MAIL-RPT11A           PIC X(50)                               
               VALUE 'CC:<Catherine_HL_TAN@hdb.gov.sg>'.                        
           05  WS-MAIL-RPT12            PIC X(50)                               
               VALUE 'CC:<JAMILAH_BAHROM@HDB.GOV.SG>'.                          
           05  WS-MAIL-RPT13            PIC X(80)                               
               VALUE 'SUBJECT: EXCEPTION REPORT FOR NO CHILD BC TAG'.           
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F410      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F410 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F410                                                 
                       BP13K060                                                 
                       BP13K820                                                 
                       BP13K800                                                 
                       BP13K893                                                 
                       BP13K895                                                 
                       P04K010                                                  
                       BP13K022                                                 
                       BP13K024                                                 
              OUTPUT   BP13L38G                                                 
                       MAILC38G.                                                
                                                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K060 - ERROR OPENING : ' WS-K060-STATUS              
              MOVE WS-K060-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 - ERROR OPENING : ' WS-K820-STATUS              
              MOVE WS-K820-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K893 - ERROR OPENING : ' WS-K893-STATUS              
              MOVE WS-K893-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K895-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K895 - ERROR OPENING : ' WS-K895-STATUS              
              MOVE WS-K895-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 - ERROR OPENING : ' WS-K022-STATUS              
              MOVE WS-K022-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K024 - ERROR OPENING : ' WS-K024-STATUS              
              MOVE WS-K024-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'P04K010 - ERROR OPENING : ' WS-K010-STATUS               
              MOVE WS-K010-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 7100-GET-DATE           THRU 7100-EXIT.                      
           PERFORM 7000-PRINT-HEADER       THRU 7000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F410.                                                      
      ******************************************************************        
                                                                                
           READ BP13F410           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F410                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F410-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       2050-READ-BP13K060.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                TO BP13K060-REC.                          
           INITIALIZE                    BP13K060-REC.                          
                                                                                
           MOVE SPACES                TO WS-CTZ.                                
                                                                                
           MOVE 41                    TO WS-DPC-SERIAL-NO.                      
           MOVE K820-NUM-CITIZENSHIP  TO WS-DPC-CODE.                           
                                                                                
           MOVE WS-BP13K060-KEY       TO K060-KEY-FLD.                          
                                                                                
           READ BP13K060.                                                       
                                                                                
           EVALUATE WS-K060-STATUS                                              
               WHEN 00                                                          
                    MOVE K060-DESC    TO WS-CTZ                                 
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES       TO BP13K060-REC                           
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K060 READ ERROR ' WS-K060-STATUS               
                    DISPLAY 'K060 KEY: ' K060-KEY-FLD                           
                    MOVE WS-K060-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2050-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       2100-READ-BP13K800.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP13K800-MASTER.                    
           INITIALIZE                       BP13K800-MASTER.                    
                                                                                
           MOVE K410-NUM-REGN            TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    PERFORM 2200-START-BP13K893  THRU 2200-EXIT                 
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    DISPLAY 'K800 KEY: ' K800-NUM-REGN                          
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       2200-START-BP13K893.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                           TO BP13K893-MASTER             
                                                    BP13K800-MASTER.            
           INITIALIZE                               BP13K893-MASTER             
                                                    BP13K800-MASTER.            
                                                                                
           MOVE SPACES                           TO K893-KEY-FLD.               
           MOVE K410-NUM-REGN                    TO K893-NUM-REGN-HIST.         
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K893-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 2300-READ-BP13K893  THRU 2300-EXIT                  
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K893: ' WS-K893-STATUS          
                            ' REGN = ' K410-NUM-REGN                            
                    PERFORM  9999-CLOSE-ROUTINE THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2300-READ-BP13K893.                                                      
      ******************************************************************        
                                                                                
            READ  BP13K893 NEXT AT END                                          
                  MOVE SPACES               TO K893-KEY-FLD.                    
                                                                                
            EVALUATE WS-K893-STATUS                                             
                WHEN 00                                                         
                WHEN 02                                                         
                     IF K893-NUM-REGN-HIST = K410-NUM-REGN                      
                        MOVE BP13K893-MASTER(1:2000) TO BP13K800-MASTER         
                     END-IF                                                     
                WHEN 23                                                         
                     CONTINUE                                                   
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K893 : ' WS-K893-STATUS         
                             ' REGN = ' K893-NUM-REGN-HIST                      
                     PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                
            END-EVALUATE.                                                       
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF K410-DTE-KEY-ISSUE >= WS-PRIO-DTE AND                             
              K410-DTE-KEY-ISSUE <= WS-SYSTEM-DATE                              
                                                                                
              MOVE SPACES                       TO WS-SPOUSE-NRIC               
                                                   WS-BC-NRIC                   
                                                                                
              PERFORM 2100-READ-BP13K800        THRU 2100-EXIT                  
              PERFORM 3100-START-BP13K820       THRU 3100-EXIT                  
                                                                                
              IF (WS-BC-DEBAR = 'Y') AND                                        
                 (K800-NUM-PPS-ELIG  = 'E') AND                                 
                 (K800-NUM-PPS-QUOTA = 'S' OR 'R' OR 'W') AND                   
                 (K800-NUM-PPS-BIRTH-CERT NOT = 'W')                            
                  MOVE WS-BC-NRIC                 TO HA-NRIC(1)                 
                  MOVE WS-SPOUSE-NRIC             TO HA-NRIC(2)                 
                                                                                
                  MOVE 'N'                        TO WS-BC-CODE                 
                                                                                
                  PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 2               
                    IF HA-NRIC(WS-I) NOT = SPACES AND LOW-VALUES                
                       PERFORM 4100-START-P04K010 THRU 4100-EXIT                
                    END-IF                                                      
                  END-PERFORM                                                   
                                                                                
                                                                                
                  IF WS-BC-CODE NOT = 'Y'                                       
                     PERFORM 4000-PROCESS-RECORDS THRU 4000-EXIT                
                  END-IF                                                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F410           THRU 2000-EXIT.                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-START-BP13K820.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                                TO BP13K820-REC.          
           INITIALIZE                                    BP13K820-REC.          
                                                                                
           MOVE 'N'                                   TO WS-EOF-K820.           
           MOVE 'Y'                                   TO WS-BC-DEBAR.           
                                                                                
           MOVE K410-NUM-REGN                         TO K820-NUM-REGN.         
                                                                                
           START BP13K820 KEY = K820-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K820-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3200-READNEXT-BP13K820  THRU 3200-EXIT              
                    PERFORM 3300-PROCESS-BP13K820   THRU 3300-EXIT              
                            UNTIL WS-EOF-K820 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    PERFORM 3500-START-BP13K895     THRU 3500-EXIT              
                                                                                
               WHEN OTHER                                                       
                  DISPLAY 'START ERROR BP13K820 : ' WS-K820-STATUS              
                          ' REGN : ' K820-NUM-REGN                              
                  PERFORM 9999-CLOSE-ROUTINE        THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3200-READNEXT-BP13K820.                                                  
      ******************************************************************        
                                                                                
           READ BP13K820 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-EOF-K820.                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3300-PROCESS-BP13K820.                                                   
      ******************************************************************        
                                                                                
           IF K820-NUM-REGN = K410-NUM-REGN                                     
                                                                                
              IF K820-NUM-PPS-PREGNANT = 'Y'                                    
                 MOVE K820-NUM-NRIC            TO WS-BC-NRIC                    
                 MOVE K820-NUM-NRIC-SPOUSE     TO WS-SPOUSE-NRIC                
              END-IF                                                            
                                                                                
              IF K800-DTE-REGN NOT = SPACES AND LOW-VALUES                      
                 MOVE K800-DTE-REGN            TO WS-DTE-REGN                   
              END-IF                                                            
                                                                                
              IF K820-NUM-CITIZENSHIP IS NOT ALPHABETIC                         
                 PERFORM 2050-READ-BP13K060 THRU 2050-EXIT                      
              ELSE                                                              
                 MOVE K820-NUM-CITIZENSHIP  TO WS-CTZ                           
              END-IF                                                            
                                                                                
              COMPUTE WS-DTE-MM = WS-DTE-MM + 10                                
                                                                                
              IF WS-DTE-MM > 12                                                 
                 COMPUTE WS-DTE-MM = WS-DTE-MM - 12                             
                 COMPUTE WS-DTE-CCYY = WS-DTE-CCYY + 1                          
              END-IF                                                            
                                                                                
              IF K820-DTE-BIRTH >= K800-DTE-REGN AND                            
                 K820-DTE-BIRTH <= WS-DTE-REGN                                  
                                                                                
                 IF WS-CTZ = 'SC'                                               
                    MOVE 'N'                   TO WS-BC-DEBAR                   
                 END-IF                                                         
              ELSE                                                              
                 IF K820-DTE-BIRTH < K800-DTE-REGN                              
                    MOVE K820-DTE-BIRTH TO WS-DTE-BIRTH                         
                    MOVE K800-DTE-REGN  TO WS-DTE-REGN                          
                                                                                
                    COMPUTE WS-NUM-AGE = WS-DTE-CCYY -                          
                                         WS-DTE-BIRTH-CCYY                      
                                                                                
                    IF WS-NUM-AGE <= 16                                         
                       IF WS-CTZ = 'SC'                                         
                          MOVE 'N'       TO WS-BC-DEBAR                         
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
                                                                                
              PERFORM 3200-READNEXT-BP13K820 THRU 3200-EXIT                     
           ELSE                                                                 
              MOVE 'Y'                         TO WS-EOF-K820                   
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3500-START-BP13K895.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                                TO BP13K895-REC.          
           INITIALIZE                                    BP13K895-REC.          
                                                                                
           MOVE 'N'                                   TO WS-EOF-K895.           
           MOVE K410-NUM-REGN                  TO K895-NUM-REGN-HIST.           
                                                                                
           START BP13K895 KEY = K895-NUM-REGN-HIST.                             
                                                                                
           EVALUATE WS-K895-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3600-READNEXT-BP13K895  THRU 3600-EXIT              
                    PERFORM 3700-PROCESS-BP13K895   THRU 3700-EXIT              
                            UNTIL WS-EOF-K895 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                  DISPLAY 'START ERROR BP13K895 : ' WS-K895-STATUS              
                          ' REGN : ' K895-NUM-REGN                              
                  PERFORM 9999-CLOSE-ROUTINE        THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3600-READNEXT-BP13K895.                                                  
      ******************************************************************        
                                                                                
           READ BP13K895 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-EOF-K895.                        
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3700-PROCESS-BP13K895.                                                   
      ******************************************************************        
                                                                                
           IF K895-NUM-REGN-HIST = K410-NUM-REGN                                
                                                                                
              IF K895-NUM-PPS-PREGNANT = 'Y'                                    
                 MOVE K895-NUM-NRIC            TO WS-BC-NRIC                    
                 MOVE K895-NUM-NRIC-SPOUSE     TO WS-SPOUSE-NRIC                
              END-IF                                                            
                                                                                
              IF K800-DTE-REGN NOT = SPACES AND LOW-VALUES                      
                 MOVE K800-DTE-REGN            TO WS-DTE-REGN                   
              END-IF                                                            
                                                                                
              IF K895-NUM-CITIZENSHIP IS NOT ALPHABETIC                         
                 MOVE K895-NUM-CITIZENSHIP  TO  K820-NUM-CITIZENSHIP            
                 PERFORM 2050-READ-BP13K060 THRU 2050-EXIT                      
              ELSE                                                              
                 MOVE K895-NUM-CITIZENSHIP  TO WS-CTZ                           
              END-IF                                                            
                                                                                
              COMPUTE WS-DTE-MM = WS-DTE-MM + 10                                
                                                                                
              IF WS-DTE-MM > 12                                                 
                 COMPUTE WS-DTE-MM = WS-DTE-MM - 12                             
                 COMPUTE WS-DTE-CCYY = WS-DTE-CCYY + 1                          
              END-IF                                                            
                                                                                
              IF K895-DTE-BIRTH >= K800-DTE-REGN AND                            
                 K895-DTE-BIRTH <= WS-DTE-REGN                                  
                                                                                
                 IF WS-CTZ = 'SC'                                               
                    MOVE 'N'                   TO WS-BC-DEBAR                   
                 END-IF                                                         
              ELSE                                                              
                 IF K895-DTE-BIRTH < K800-DTE-REGN                              
                    MOVE K895-DTE-BIRTH TO WS-DTE-BIRTH                         
                    MOVE K800-DTE-REGN  TO WS-DTE-REGN                          
                                                                                
                    COMPUTE WS-NUM-AGE = WS-DTE-CCYY -                          
                                         WS-DTE-BIRTH-CCYY                      
                                                                                
                    IF WS-NUM-AGE <= 16                                         
                       IF WS-CTZ = 'SC'                                         
                          MOVE 'N'       TO WS-BC-DEBAR                         
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
                                                                                
              PERFORM 3600-READNEXT-BP13K895 THRU 3600-EXIT                     
           ELSE                                                                 
              MOVE 'Y'                         TO WS-EOF-K895                   
           END-IF.                                                              
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE SPACES                       TO L38G-DETAIL01                   
           INITIALIZE                           L38G-DETAIL01.                  
                                                                                
           MOVE ';'                       TO FILLER1 FILLER2 FILLER3            
                                             FILLER4 FILLER5 FILLER6            
                                             FILLER7.                           
                                                                                
           ADD 1                          TO WS-SNO.                            
           MOVE WS-SNO                    TO WS-SNO-ZZZZ9.                      
           MOVE WS-SNO-ZZZZ9              TO L38G-SN.                           
           MOVE K410-NUM-REGN             TO L38G-REGN.                         
                                                                                
           IF K410-DTE-KEY-ISSUE NOT = SPACES AND LOW-VALUES                    
                                              AND ZEROES                        
              STRING K410-DTE-KEY-ISSUE(7:2) '/'                                
                     K410-DTE-KEY-ISSUE(5:2) '/'                                
                     K410-DTE-KEY-ISSUE(1:4)                                    
              DELIMITED BY SIZE INTO L38G-DTE-ISSUE                             
           END-IF.                                                              
                                                                                
           MOVE WS-SPOUSE-NRIC                    TO L38G-SPOUSE.               
           MOVE WS-BC-NRIC                        TO L38G-NRIC.                 
                                                                                
           PERFORM 5100-GET-CSM-NAME            THRU 5100-EXIT.                 
           PERFORM 6000-PRINT-REPORT            THRU 6000-EXIT.                 
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4100-START-P04K010.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                              TO P04K010-REC.             
           INITIALIZE                                  P04K010-REC.             
                                                                                
           MOVE 'N'                                 TO WS-EOF-K010.             
                                                                                
           MOVE SPACES                              TO K010-PRIME-KEY.          
           MOVE HA-NRIC(WS-I)                       TO K010-NUM-UINFIN.         
                                                                                
           START P04K010  KEY = K010-NUM-UINFIN.                                
                                                                                
           EVALUATE WS-K010-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 4200-READNEXT-P04K010  THRU 4200-EXIT               
                    PERFORM 4300-PROCESS-P04K010   THRU 4300-EXIT               
                            UNTIL WS-EOF-K010 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'START ERROR P04K010 : ' WS-K010-STATUS             
                            ' NRIC : ' K010-NUM-UINFIN                          
                    PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT               
            END-EVALUATE.                                                       
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       4200-READNEXT-P04K010.                                                   
      ******************************************************************        
                                                                                
           READ P04K010 NEXT RECORD                                             
                         AT END MOVE 'Y' TO WS-EOF-K010.                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4300-PROCESS-P04K010.                                                    
      ******************************************************************        
                                                                                
           IF (K010-NUM-UINFIN = HA-NRIC(WS-I))                                 
               IF (K010-REASON-CODE  = 'BC')                                    
                    MOVE 3                         TO WS-I                      
                    MOVE 'Y'                       TO WS-BC-CODE                
                                                      WS-EOF-K010               
               END-IF                                                           
                                                                                
               PERFORM 4200-READNEXT-P04K010     THRU 4200-EXIT                 
           ELSE                                                                 
               MOVE 'Y'                            TO WS-EOF-K010               
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       5000-HEADER-EMAIL.                                                       
      ******************************************************************        
                                                                                
            WRITE MAILC38G-REC FROM WS-MAIL-RPT1.                               
            WRITE MAILC38G-REC FROM WS-MAIL-RPT2.                               
            WRITE MAILC38G-REC FROM WS-MAIL-RPT3.                               
            WRITE MAILC38G-REC FROM WS-MAIL-RPT4.                               
            WRITE MAILC38G-REC FROM WS-MAIL-RPT5.                               
            WRITE MAILC38G-REC FROM WS-MAIL-RPT5A.                              
            WRITE MAILC38G-REC FROM WS-MAIL-RPT6.                               
            WRITE MAILC38G-REC FROM WS-MAIL-RPT7.                               
            WRITE MAILC38G-REC FROM WS-MAIL-RPT8.                               
            WRITE MAILC38G-REC FROM WS-MAIL-RPT9.                               
            WRITE MAILC38G-REC FROM WS-MAIL-RPT10.                              
            WRITE MAILC38G-REC FROM WS-MAIL-RPT11.                              
            WRITE MAILC38G-REC FROM WS-MAIL-RPT11A.                             
            WRITE MAILC38G-REC FROM WS-MAIL-RPT12.                              
                                                                                
            STRING 'SUBJECT: EXCEPTION REPORT FOR PPS '                         
                   'EXPECTANT CASE - ('                                         
                    WS-PRIO-DD '/' WS-PRIO-MM '/' WS-PRIO-CCYY ' TO '           
                    WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CCYY  ')'             
              DELIMITED BY SIZE INTO WS-MAIL-RPT13.                             
                                                                                
            WRITE MAILC38G-REC FROM WS-MAIL-RPT13.                              
                                                                                
            STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CCYY                   
                    DELIMITED BY SIZE INTO WS-DATE-RPT.                         
                                                                                
            WRITE MAILC38G-REC FROM WS-DATE-RPT-DTL                             
                                                                                
            MOVE SPACES TO MAILC38G-REC.                                        
            WRITE MAILC38G-REC.                                                 
                                                                                
            WRITE MAILC38G-REC FROM L38G-HEADER01.                              
            WRITE MAILC38G-REC FROM L38G-HEADER02.                              
                                                                                
            MOVE SPACES TO MAILC38G-REC.                                        
            WRITE MAILC38G-REC.                                                 
                                                                                
            WRITE MAILC38G-REC FROM L38G-HEADER03.                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       5100-GET-CSM-NAME.                                                       
      ******************************************************************        
                                                                                
            MOVE SPACES                               TO BP13K022-REC.          
            INITIALIZE                                   BP13K022-REC.          
                                                                                
            MOVE K410-NUM-REGN                        TO K022-NUM-REGN.         
                                                                                
            READ BP13K022 KEY IS K022-NUM-REGN.                                 
                                                                                
            EVALUATE WS-K022-STATUS                                             
                WHEN 00                                                         
                     PERFORM 5200-READ-BP13K024     THRU 5200-EXIT              
                WHEN 23                                                         
                     CONTINUE                                                   
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K022 : ' WS-K022-STATUS         
                             ' REGN NO = ' K022-NUM-REGN                        
                     PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT            
            END-EVALUATE.                                                       
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5200-READ-BP13K024.                                                      
      ******************************************************************        
                                                                                
            MOVE SPACES                                TO BP13K024-REC.         
            INITIALIZE                                    BP13K024-REC.         
                                                                                
            MOVE K022-NUM-OIC                          TO K024-NUM-OIC.         
                                                                                
            READ BP13K024.                                                      
                                                                                
            EVALUATE WS-K024-STATUS                                             
                WHEN 00                                                         
                     MOVE K024-NME-OIC                 TO L38G-CSM              
                WHEN 23                                                         
                     MOVE SPACES                       TO L38G-CSM              
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K024 : ' WS-K024-STATUS         
                             ' OIC  = ' K024-NUM-OIC                            
                     PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT             
            END-EVALUATE.                                                       
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6000-PRINT-REPORT.                                                       
      ******************************************************************        
                                                                                
           WRITE BP13L38G-REC               FROM L38G-DETAIL01.                 
           WRITE MAILC38G-REC               FROM L38G-DETAIL01.                 
           ADD 1                              TO WS-CNT-L38G-WRITE.             
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7000-PRINT-HEADER.                                                       
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)      TO WS-SYSTEM-DATE               
                                                   WS-CURR-DATE.                
                                                                                
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
              DELIMITED BY SIZE INTO L38G-DATE-HDR.                             
                                                                                
                                                                                
           ADD 1                               TO WS-PAGE-CTR.                  
           MOVE ZEROES                         TO L38G-PAGE-HDR.                
           MOVE WS-PAGE-CTR                    TO L38G-PAGE-HDR.                
                                                                                
           WRITE BP13L38G-REC    FROM L38G-HEADER01 AFTER PAGE.                 
           WRITE BP13L38G-REC    FROM L38G-HEADER02.                            
           WRITE BP13L38G-REC    FROM L38G-HEADER03 AFTER 2.                    
                                                                                
           INITIALIZE WS-LINE-CTR.                                              
           MOVE 5  TO WS-LINE-CTR.                                              
                                                                                
           PERFORM 5000-HEADER-EMAIL           THRU 5000-EXIT.                  
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7100-GET-DATE.                                                           
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)     TO  WS-PRIOR-DTE.                
                                                                                
           COMPUTE WS-NUM-DTE-APPT =                                            
                   FUNCTION INTEGER-OF-DATE(WS-PRIOR-DTE).                      
                                                                                
           SUBTRACT 6 FROM  WS-NUM-DTE-APPT.                                    
                                                                                
           COMPUTE  WS-PRIOR-DTE =                                              
                    FUNCTION DATE-OF-INTEGER(WS-NUM-DTE-APPT).                  
                                                                                
           MOVE WS-PRIOR-DTE        TO WS-PRIO-DTE.                             
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C38G'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F410 RECORDS READ............. : '            
                    WS-F410-READ.                                               
           DISPLAY '(2) NO OF BP13L38G RECORDS WRITTEN.......... : '            
                    WS-CNT-L38G-WRITE.                                          
                                                                                
           CLOSE    BP13F410                                                    
                    BP13K800                                                    
                    BP13K893                                                    
                    BP13K895                                                    
                    BP13K022                                                    
                    BP13K024                                                    
                    BP13K820                                                    
                    BP13K060                                                    
                    P04K010                                                     
                    BP13L38G                                                    
                    MAILC38G.                                                   
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 - ERROR CLOSING : ' WS-K820-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'P04K010  - ERROR CLOSING : ' WS-K010-STATUS              
              MOVE WS-K010-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR CLOSING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K893 - ERROR CLOSING : ' WS-K893-STATUS              
              MOVE WS-K893-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K895-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K895 - ERROR CLOSING : ' WS-K895-STATUS              
              MOVE WS-K895-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 - ERROR CLOSING : ' WS-K022-STATUS              
              MOVE WS-K022-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K024 - ERROR CLOSING : ' WS-K024-STATUS              
              MOVE WS-K024-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K060 - ERROR CLOSING : ' WS-K060-STATUS              
              MOVE WS-K060-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
