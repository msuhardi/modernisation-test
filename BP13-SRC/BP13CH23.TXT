       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH23.                                                 
       AUTHOR.        JOHN BARCEGA.                                             
      *DATE-WRITTEN.  22/02/2013.                                               
      * =========================================================== *           
      *              SYSTEM OF COMMITMENT (BP13)                    *           
      * =========================================================== *           
      *   OBJECTIVE  :  EXTRACT REGN NOS WITH VALID CASE STATUS     *           
      *                 USING DATA FROM BP13CH22                    *           
      *                                                             *           
      *   INPUT      :  BP13K800 -- SOC APPLN MASTER                *           
      *                 BP27K800 -- HLA MASTER                      *           
      *                 BP13FAIN -- REGN NOS W/ 1YR FROM COMPLETION *           
      *   OUTPUT     :  BP13FR00 -- REGN FROM INPT W/ VALID CASE    *           
      *                             STATUS                          *           
      *                                                             *           
      *   AMENDMENTS:                                               *           
      *    REF      DATE     BY          DESCRIPTIONS               *           
      *****************************************************************         
      * BP134765 22/02/2013 JGO1     NEW PROGRAM                    *           
      * BP138047 25/11/2019 KAC1 RECOMPILE FOR BP27 COPYBOOK UPDATE *           
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FAIN  ASSIGN TO BP13FAIN.                                 
                                                                                
           SELECT BP13K800  ASSIGN TO BP13K800                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K800-NUM-REGN                             
                  FILE STATUS      IS K800-STATUS                               
                  ACCESS           IS RANDOM.                                   
                                                                                
           SELECT BP27K800  ASSIGN TO BP27K800                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K800-NUM-HLA                              
                                      OF BP27K800-MASTER                        
                  FILE STATUS      IS BP27K800-STATUS                           
                  ACCESS           IS RANDOM.                                   
                                                                                
           SELECT BP13FR00  ASSIGN TO BP13FR00.                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K800                                                             
                                      RECORD CONTAINS 2000 CHARACTERS.          
       COPY BP13K800.                                                           
                                                                                
       FD  BP27K800                                                             
                                      RECORD CONTAINS 1200 CHARACTERS.          
       COPY BP27K800.                                                           
                                                                                
       FD  BP13FR00                                                             
                                      RECORD CONTAINS 50 CHARACTERS             
                                      RECORDING MODE IS F                       
                                      LABEL RECORD IS OMITTED.                  
       COPY BP13FR00.                                                           
                                                                                
       FD   BP13FAIN                                                            
            RECORD CONTAINS 50 CHARACTERS.                                      
       01  BP13FAIN-REC.                                                        
           05 BP13FAIN-REGNO           PIC X(8).                                
           05 BP13FAIN-PACD.                                                    
              10 BP13FAIN-PACD-QTR     PIC X(6).                                
              10 BP13FAIN-PACD-DD      PIC X(2).                                
           05 FILLER                   PIC X(34).                               
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-STATUS.                                                           
           05 K800-STATUS              PIC 99       VALUE ZEROS.                
           05 BP27K800-STATUS          PIC 99       VALUE ZEROS.                
           05 WS-FAIN-END              PIC X        VALUE 'N'.                  
           05 WS-FAIN-EOF              PIC X        VALUE 'N'.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-FAIN-RD           PIC 9(6) COMP VALUE 0.                   
           05 WS-CNT-FR00-WRITE        PIC 9(6) COMP VALUE 0.                   
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-START-UP       THRU 1000-EXIT.                          
           PERFORM 2000-MAIN-PROCESS   THRU 2000-EXIT                           
                   UNTIL WS-FAIN-EOF = 'Y' .                                    
           PERFORM 9999-WRAP-UP        THRU 9999-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-START-UP.                                                           
      ******************************************************************        
                                                                                
            OPEN INPUT  BP13K800                                                
                        BP27K800                                                
                        BP13FAIN                                                
                 OUTPUT BP13FR00.                                               
                                                                                
           IF  K800-STATUS NOT = '00' AND '97'                                  
               DISPLAY 'ERROR OPEN BP13K800 : ' K800-STATUS                     
               MOVE K800-STATUS TO RETURN-CODE                                  
               PERFORM 9999-WRAP-UP        THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF  BP27K800-STATUS NOT = '00' AND '97'                              
               DISPLAY 'ERROR OPEN BP27K800 : ' BP27K800-STATUS                 
               MOVE BP27K800-STATUS TO RETURN-CODE                              
               PERFORM 9999-WRAP-UP        THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-MAIN-PROCESS.                                                       
      ******************************************************************        
                                                                                
           PERFORM 3000-READ-BP13FAIN       THRU 3000-EXIT.                     
           PERFORM 3100-READ-BP13K800       THRU 3100-EXIT.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-READ-BP13FAIN.                                                      
      ******************************************************************        
                                                                                
           READ BP13FAIN                                                        
                AT END MOVE 'Y' TO WS-FAIN-EOF                                  
                GO TO 2000-EXIT.                                                
           ADD 1 TO WS-CNT-FAIN-RD.                                             
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-READ-BP13K800.                                                      
      ******************************************************************        
                                                                                
           INITIALIZE                   BP13K800-MASTER.                        
           MOVE  BP13FAIN-REGNO      TO K800-NUM-REGN.                          
           READ BP13K800.                                                       
           IF K800-STATUS = 00 OR 02                                            
             PERFORM 3200-READ-BP27K800  THRU 3200-EXIT                         
           ELSE                                                                 
             IF K800-STATUS = 23                                                
                DISPLAY 'BP13K800-RECORD NOT FOUND: ' K800-NUM-REGN             
             ELSE                                                               
                DISPLAY 'BP13K800-READ ERROR. STATUS: '                         
                       K800-STATUS                                              
                MOVE   K800-STATUS   TO   RETURN-CODE                           
                PERFORM 9999-WRAP-UP        THRU 9999-EXIT                      
             END-IF                                                             
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3200-READ-BP27K800.                                                      
      ******************************************************************        
                                                                                
           MOVE  SPACES              TO     BP27K800-MASTER.                    
           INITIALIZE                       BP27K800-MASTER.                    
           MOVE K800-NUM-HLA OF BP13K800-MASTER                                 
                  TO K800-NUM-HLA OF BP27K800-MASTER.                           
                                                                                
           READ BP27K800.                                                       
           IF BP27K800-STATUS = 00 OR 02                                        
             IF K800-NUM-CASE-STATUS = 'EXP' OR 'PP' OR 'CA'                    
                PERFORM 3300-WRITE-FR00     THRU 3300-EXIT                      
             END-IF                                                             
           ELSE                                                                 
             IF BP27K800-STATUS = 23                                            
                DISPLAY 'BP27K800-RECORD NOT FOUND: ' K800-NUM-REGN             
             ELSE                                                               
                DISPLAY 'BP27K800-READ ERROR. STATUS: '                         
                   BP27K800-STATUS                                              
                MOVE  BP27K800-STATUS   TO   RETURN-CODE                        
                PERFORM 9999-WRAP-UP        THRU 9999-EXIT                      
             END-IF                                                             
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3300-WRITE-FR00.                                                         
      ******************************************************************        
                                                                                
           INITIALIZE BP13FR00-REC.                                             
           MOVE BP13FAIN-REGNO TO FR00-NUM-REGN.                                
           MOVE K800-NUM-HLA OF BP13K800-MASTER                                 
                      TO FR00-NUM-HLA.                                          
           MOVE BP13FAIN-PACD TO FR00-DTE-PACD.                                 
           WRITE BP13FR00-REC.                                                  
           ADD 1 TO WS-CNT-FR00-WRITE.                                          
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-WRAP-UP.                                                            
      ******************************************************************        
           DISPLAY  SPACES.                                                     
           DISPLAY '******* CONTROL   COUNTS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CH23'                                      
           DISPLAY  SPACES.                                                     
           DISPLAY 'NO. OF BP13FAIN RECS READ    : ' WS-CNT-FAIN-RD.            
           DISPLAY 'NO. OF BP13FR00 RECS WRITTEN : ' WS-CNT-FR00-WRITE.         
                                                                                
           CLOSE    BP13FR00                                                    
                    BP13K800                                                    
                    BP27K800                                                    
                    BP13FAIN.                                                   
                                                                                
           IF  K800-STATUS NOT = '00'                                           
               DISPLAY 'ERROR CLOSE BP13K800 : ' K800-STATUS                    
               MOVE K800-STATUS TO RETURN-CODE                                  
           END-IF.                                                              
                                                                                
           IF  BP27K800-STATUS NOT = '00'                                       
               DISPLAY 'ERROR CLOSE BP27K800 : ' BP27K800-STATUS                
               MOVE BP27K800-STATUS TO RETURN-CODE                              
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
