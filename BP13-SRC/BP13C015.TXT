                                                                                
       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C015.                                                    
       AUTHOR. ANC1.                                                            
       DATE-WRITTEN. 31/07/2002.                                                
                                                                                
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *                                                        *                
      *      1.  GET RECORDS FROM BM06F110 AND PUT TO AN OUTPUT*                
      *          FILE WHERE F110-DTE-ACCEPTANCE IS EQUAL TO    *                
      *          PREVIOUS MONTH'S DATE IN SY02F001 FILE.       *                
      *                                                        *                
      *      2.  MOVE K110-DTE-ACCEPTANCE TO DTE-BK-APPMT      *                
      *                                                        *                
      * CHGNO    BY  ON   DESCRIPTION                          *                
      * -----------------------------                          *                
      * BP132172 ANC 070802 - NEW PGM                          *                
      * BP132235 ANC 050303 - CHANGE F001-DTE-LAST-DAY-PREV TO *                
      *                       F001-DTE-CURRENT                 *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F110   ASSIGN       TO BM06F110.                          
           SELECT SY02F001   ASSIGN       TO SY02F001.                          
           SELECT BM6F110A   ASSIGN       TO BM6F110A.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BM06F110                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BM06F110.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BM6F110A                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       01   BM6F110A-REC           PIC X(500).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-F110-READ            PIC 9(08) VALUE 0.                           
       01  WS-F110A-WRITE          PIC 9(08) VALUE 0.                           
                                                                                
       01  WS-CUR-DATE.                                                         
           05 WS-CC                PIC 9(02).                                   
           05 WS-YY                PIC 9(02).                                   
           05 WS-MM                PIC 9(02).                                   
           05 WS-DD                PIC 9(02).                                   
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      **********                                                                
       000-MAIN.                                                                
      **********                                                                
                                                                                
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
                                                                                
           PERFORM 200-READ-F110-REC THRU 200-EXIT.                             
                                                                                
           PERFORM 300-PROCESS-F110  THRU 300-EXIT                              
                   UNTIL F110-SCH-ACC-NO = HIGH-VALUE.                          
                                                                                
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ****************                                                          
       100-OPEN-FILES.                                                          
      ****************                                                          
                                                                                
           OPEN INPUT  BM06F110                                                 
                       SY02F001.                                                
           OPEN OUTPUT BM6F110A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
                                                                                
           READ SY02F001.                                                       
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *******************                                                       
       200-READ-F110-REC.                                                       
      *******************                                                       
                                                                                
           READ BM06F110 AT END                                                 
                MOVE HIGH-VALUE TO F110-SCH-ACC-NO                              
                GO TO 200-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F110-READ.                                               
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************                                                         
       300-PROCESS-F110.                                                        
      *****************                                                         
                                                                                
           IF F001-DTE-CURRENT(1:6) = F110-DTE-ACCEPTANCE(1:6)                  
               MOVE F110-REC         TO BM6F110A-REC                            
               WRITE BM6F110A-REC                                               
               ADD  1                TO  WS-F110A-WRITE                         
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F110-REC THRU 200-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************                                                        
       900-CLOSE-ROUTINE.                                                       
      ******************                                                        
                                                                                
           DISPLAY 'PROGRAM-ID : BP13C015 '.                                    
           DISPLAY '                                      '.                    
           DISPLAY 'CURR-DATE  : ' WS-CUR-DATE.                                 
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM  F110  ' WS-F110-READ.              
           DISPLAY 'NO OF RECORDS WRITTEN TO F110A ' WS-F110A-WRITE.            
                                                                                
           CLOSE BM06F110                                                       
                 SY02F001                                                       
                 BM6F110A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
