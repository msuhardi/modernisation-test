       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH53.                                                 
       AUTHOR.        ZDD1.                                                     
      *DATE-WRITTEN.  05/10/2015.                                               
      * ===============================================================*        
      * BP13 SYSTEM OF COMMITMENT                                      *        
      * ===============================================================*        
      * OBJECTIVE   : TO SEND EMAIL FOR THOSE CASES WITH CHANGES ON    *        
      *               FURTHER PARTICULAR                               *        
      *                                                                *        
      *                                                                *        
      *   INPUT FILES :  1. BP13FH50                                   *        
      *                  2. BP13K022                                   *        
      *                  2. BP13Kh50                                   *        
      *                                                                *        
      *   OUTPUT      :  1. BP13LH53                                   *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ------------------------------------*        
      *   BP135825  ZDD1  05/10/15 NEW PROGRAM                         *        
      *================================================================*        
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K022  ASSIGN        TO BP13K022                           
                            ACCESS MODE      IS DYNAMIC                         
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K022-KEY-FLD                    
                            ALTERNATE KEY    IS K022-NUM-REGN                   
                            FILE STATUS      IS WS-K022-STATUS.                 
                                                                                
           SELECT BP13KH50  ASSIGN        TO BP13KH50                           
                            ACCESS MODE      IS DYNAMIC                         
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS KH50-KEY-FLD                    
                            FILE STATUS      IS WS-KH50-STATUS.                 
                                                                                
           SELECT BP13FH50  ASSIGN        TO BP13FH50.                          
           SELECT BP13LH53  ASSIGN        TO BP13LH53.                          
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13LH53                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13LH53-REC        PIC X(132).                                      
                                                                                
       FD  BP13FH50                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 3000 CHARACTERS.                                     
       COPY BP13FH50.                                                           
                                                                                
       FD  BP13K022                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K022.                                                           
                                                                                
       FD  BP13KH50                                                             
           RECORD CONTAINS 3000 CHARACTERS.                                     
       COPY BP13KH50.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY             PIC 9(04).                               
           05  WS-CUR-MM               PIC 9(02).                               
           05  WS-CUR-DD               PIC 9(02).                               
                                                                                
       01  WS-REPORT-DATE.                                                      
           05  WS-REP-CCYY             PIC X(04).                               
           05  WS-REP-MM               PIC X(02).                               
                                                                                
       01  WS-COUNT.                                                            
           05  WS-FH50-INPUT           PIC 9(08) VALUE 0.                       
           05  WS-LH53-OUT             PIC 9(08) VALUE 0.                       
           05  WS-LH53A-OUT            PIC 9(08) VALUE 0.                       
           05  WS-FH50-OUT             PIC 9(08) VALUE 0.                       
           05  WS-WRITTEN              PIC 9(08) VALUE 0.                       
           05  WS-KH50-UPD             PIC 9(08) VALUE 0.                       
                                                                                
       01  WS-TIME                     PIC 9(08) VALUE 0.                       
       01  WS-FH50-EOF                 PIC X(01) VALUE SPACES.                  
       01  WS-FOUND-K022               PIC X(01) VALUE 'N'.                     
       01  WS-PROCESSED                PIC X(01) VALUE 'N'.                     
       01  WS-FILE-STATUS.                                                      
           05 WS-K022-STATUS           PIC 99    VALUE ZEROS.                   
           05 WS-KH50-STATUS           PIC 99    VALUE ZEROS.                   
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-OIC                   PIC X(5)  VALUE SPACES.                  
           05 WS-OIC-NME               PIC X(30) VALUE SPACES.                  
           05 WS-EMAIL                 PIC X(50) VALUE SPACES.                  
           05 WS-SAVE-KEY              PIC X(16) VALUE SPACES.                  
           05  WS-DTE-SYS-VALID.                                                
              10 WS-YY-SYS-VALID       PIC 9(4)  VALUE ZEROES.                  
              10 WS-MM-SYS-VALID       PIC 9(2)  VALUE ZEROES.                  
              10 WS-DD-SYS-VALID       PIC 9(2)  VALUE ZEROES.                  
                                                                                
                                                                                
      *-------------------------------------------------------------            
      * LAYOUT BP13MAIL  -  DATASET FOR EMAIL                                   
      *-------------------------------------------------------------            
       01  MAIL-HDR1.                                                           
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  MAIL-HDR2.                                                           
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  MAIL-SENDID     PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
      *01  MAIL-DTL1A.                                                          
      *    05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
      *    05  FILLER          PIC X(60)  VALUE                                 
      *        'TEEU_HUI@HDB.GOV.SG>'.                                          
                                                                                
      *01  MAIL-DTL1B.                                                          
      *    05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
      *    05  FILLER          PIC X(60)  VALUE                                 
      *        'LIM_KIM_HUAT@HDB.GOV.SG>'.                                      
                                                                                
       01  MAIL-DTL3.                                                           
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4.                                                           
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  MAIL-SENDMAILID PIC X(26)  VALUE                                 
               'SOC System - Email Alert'.                                      
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
      *01  MAIL-DTL5A.                                                          
      *    05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
      *    05  FILLER          PIC X(60)  VALUE                                 
      *        'TEEU_HUI@HDB.GOV.SG>'.                                          
      *                                                                         
      *01  MAIL-DTL5B.                                                          
      *    05  FILLER          PIC X(4)   VALUE 'CC:<'.                         
      *    05  FILLER          PIC X(60)  VALUE                                 
      *        'LIM_KIM_HUAT@HDB.GOV.SG>'.                                      
                                                                                
       01  MAIL-DTL7.                                                           
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  FILLER          PIC X(19)  VALUE 'Notification of Cha'.          
           05  FILLER          PIC X(20)  VALUE 'nge in Particulars  '.         
                                                                                
       01  MAIL-DTL8.                                                           
           05 FILLER                   PIC X(6)   VALUE 'DATE: '.               
           05 MAIL-DATE                PIC X(10)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY1.                                                          
           05 FILLER                   PIC X(55)  VALUE                         
              'Please follow up with the Notification of Change in '.           
                                                                                
       01  MAIL-BODY2.                                                          
           05 FILLER                   PIC X(45)  VALUE                         
              'Particulars for Sales Registration Number :'.                    
           05 MAIL-REGNO               PIC X(08)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY3.                                                          
           05 FILLER                   PIC X(50)  VALUE                         
              'OIC          TYPE OF CHANGE          REASON'.                    
                                                                                
       01  MAIL-BODY4.                                                          
           05 MAIL-OIC1                PIC X(05)  VALUE SPACES.                 
           05 FILLER                   PIC X(08)  VALUE SPACES.                 
           05 MAIL-TYPE-CHG1           PIC X(20)  VALUE SPACES.                 
           05 FILLER                   PIC X(04)  VALUE SPACES.                 
           05 MAIL-REASON1             PIC X(50)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY5.                                                          
           05 MAIL-OIC2                PIC X(05)  VALUE SPACES.                 
           05 FILLER                   PIC X(08)  VALUE SPACES.                 
           05 MAIL-TYPE-CHG2           PIC X(20)  VALUE SPACES.                 
           05 FILLER                   PIC X(04)  VALUE SPACES.                 
           05 MAIL-REASON2             PIC X(50)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY6.                                                          
           05 MAIL-OIC3                PIC X(05)  VALUE SPACES.                 
           05 FILLER                   PIC X(08)  VALUE SPACES.                 
           05 MAIL-TYPE-CHG3           PIC X(20)  VALUE SPACES.                 
           05 FILLER                   PIC X(04)  VALUE SPACES.                 
           05 MAIL-REASON3             PIC X(50)  VALUE SPACES.                 
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                   PIC X      VALUE '.'.                    
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(80)  VALUE SPACES.                 
                                                                                
                                                                                
      *----------------------------------------------------------------*        
      *        LINKAGE FOR SUB-PROGRAM BP13CCNT                        *        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  LINK-CNTCT-TYPE         PIC X(3).                                
           05  LINK-STAFF-NICK         PIC X(6).                                
           05  LINK-STAFF-NUM          PIC X(6).                                
           05  LINK-STAFF-CNTCT        PIC X(50).                               
           05  LINK-SYSERR             PIC X(4).                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================*           
       MAIN.                                                                    
      *=============================================================*           
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-READ-INPUT      THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-DATA    THRU 3000-EXIT                          
           UNTIL WS-FH50-EOF = 'Y'.                                             
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
      *=============================================================*           
       1000-OPEN-ROUTINE.                                                       
      *=============================================================*           
                                                                                
           OPEN INPUT  BP13FH50                                                 
                       BP13K022                                                 
                I-O    BP13KH50                                                 
                OUTPUT BP13LH53.                                                
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K022 ERROR ' WS-K022-STATUS                  
              MOVE     WS-K022-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-KH50-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KH50 ERROR ' WS-KH50-STATUS                  
              MOVE     WS-KH50-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           INITIALIZE  WS-COUNT.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-TIME.                          
                                                                                
           MOVE WS-CUR-DATE(1:6)           TO WS-REPORT-DATE.                   
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2000-READ-INPUT.                                                         
      *=============================================================*           
                                                                                
            READ BP13FH50                                                       
                 AT END MOVE 'Y'  TO WS-FH50-EOF                                
            END-READ.                                                           
                                                                                
            IF WS-FH50-EOF NOT = 'Y'                                            
               ADD 1 TO WS-FH50-INPUT                                           
            END-IF.                                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       3000-PROCESS-DATA.                                                       
      *=============================================================*           
                                                                                
           INITIALIZE  WS-FOUND-K022 WS-PROCESSED.                              
           MOVE SPACES    TO MAIL-REGNO                                         
                             MAIL-BODY4                                         
                             MAIL-BODY5                                         
                             MAIL-BODY6.                                        
                                                                                
           PERFORM 4500-GET-OIC         THRU 4500-EXIT.                         
           IF WS-OIC = SPACES OR LOW-VALUES                                     
             ADD 1 TO WS-FH50-OUT                                               
           ELSE                                                                 
             PERFORM 6000-CALL-BP13CCNT   THRU 6000-EXIT                        
             PERFORM 4000-CREATE-EMAIL    THRU 4000-EXIT                        
             PERFORM 4600-READ-BP13KH50   THRU 4600-EXIT                        
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-INPUT                                              
              THRU 2000-EXIT.                                                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       4000-CREATE-EMAIL.                                                       
      *================================================================*        
                                                                                
                                                                                
           STRING WS-CUR-DATE(7:2) '/'                                          
                  WS-CUR-DATE(5:2) '/'                                          
                  WS-CUR-DATE(1:4)                                              
                  DELIMITED BY SIZE                                             
                  INTO MAIL-DATE                                                
           END-STRING                                                           
                                                                                
           WRITE BP13LH53-REC FROM MAIL-HDR1                                    
           WRITE BP13LH53-REC FROM MAIL-HDR2                                    
                                                                                
           MOVE SPACES  TO BP13LH53-REC                                         
           STRING 'RCPT TO:<' DELIMITED BY SIZE                                 
                  WS-EMAIL '>'                                                  
                  DELIMITED BY SPACES INTO BP13LH53-REC                         
           WRITE BP13LH53-REC                                                   
                                                                                
           WRITE BP13LH53-REC FROM MAIL-DTL3                                    
           WRITE BP13LH53-REC FROM MAIL-DTL4                                    
                                                                                
           MOVE SPACES  TO BP13LH53-REC                                         
           STRING 'TO:<' DELIMITED BY SIZE                                      
                  WS-EMAIL '>'                                                  
                  DELIMITED BY SPACES                                           
                  INTO BP13LH53-REC                                             
           WRITE BP13LH53-REC                                                   
                                                                                
           WRITE BP13LH53-REC FROM MAIL-DTL7                                    
           WRITE BP13LH53-REC FROM MAIL-DTL8                                    
                                                                                
           WRITE BP13LH53-REC FROM MAIL-SPACES                                  
                                                                                
           WRITE BP13LH53-REC FROM MAIL-BODY1                                   
                                                                                
           MOVE FH50-NUM-REGN   TO MAIL-REGNO.                                  
           WRITE BP13LH53-REC FROM MAIL-BODY2                                   
                                                                                
           WRITE BP13LH53-REC FROM MAIL-BODY3                                   
                                                                                
                                                                                
           IF FH50-NUM-CDE-MARITAL-STAT-CHG = 'Y'                               
              MOVE K022-NUM-OIC  TO    MAIL-OIC1                                
              MOVE 'Marital Status'   TO   MAIL-TYPE-CHG1                       
              STRING 'Change / ' FH50-NUM-CDE-MARITAL-STAT                      
                 DELIMITED BY SIZE   INTO MAIL-REASON1                          
              WRITE BP13LH53-REC FROM MAIL-BODY4                                
           END-IF.                                                              
                                                                                
           IF FH50-NUM-CDE-FAMILY-COMP-CHG = 'Y'                                
              MOVE K022-NUM-OIC  TO    MAIL-OIC2                                
              MOVE 'Family Composition'   TO   MAIL-TYPE-CHG2                   
              STRING 'Change / ' FH50-NUM-CDE-FAMILY-COMP                       
                 DELIMITED BY SIZE   INTO MAIL-REASON2                          
              WRITE BP13LH53-REC FROM MAIL-BODY5                                
           END-IF.                                                              
                                                                                
           IF FH50-NUM-CDE-CITI-CHG   = 'Y'                                     
              MOVE K022-NUM-OIC  TO    MAIL-OIC3                                
              MOVE 'Citizenship       '   TO   MAIL-TYPE-CHG3                   
              IF FH50-NUM-CDE-CITI = 'OSC'                                      
                STRING 'Change /  Obtain Singapore Citizenship'                 
                   DELIMITED BY SIZE   INTO MAIL-REASON3                        
              END-IF                                                            
              IF FH50-NUM-CDE-CITI = 'OSP'                                      
                STRING 'Change /  Obtain Singapore Permanent Residence'         
                   DELIMITED BY SIZE   INTO MAIL-REASON3                        
              END-IF                                                            
              IF FH50-NUM-CDE-CITI = 'OTH'                                      
                STRING 'Change /  Other Citizenship'                            
                   DELIMITED BY SIZE   INTO MAIL-REASON3                        
              END-IF                                                            
              WRITE BP13LH53-REC FROM MAIL-BODY6                                
           END-IF.                                                              
                                                                                
           MOVE SPACES TO BP13LH53-REC                                          
           WRITE BP13LH53-REC                                                   
                                                                                
           WRITE BP13LH53-REC FROM MAIL-END.                                    
           ADD 1 TO WS-WRITTEN.                                                 
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4500-GET-OIC.                                                            
      *================================================================*        
           MOVE SPACES                     TO K022-KEY-FLD.                     
           MOVE FH50-NUM-REGN              TO K022-NUM-REGN.                    
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN ZEROS                                                        
                 MOVE 'Y'                     TO WS-FOUND-K022                  
                 MOVE K022-NUM-OIC            TO WS-OIC                         
              WHEN 23                                                           
                 MOVE SPACES                  TO WS-OIC                         
              WHEN OTHER                                                        
                MOVE  WS-K022-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BP13K022 FILE : ' WS-K022-STATUS         
                DISPLAY 'K022-NUM-REGN = ' K022-NUM-REGN                        
                PERFORM 9000-CLOSE-ROUTINE                                      
                   THRU 9000-EXIT                                               
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4600-READ-BP13KH50.                                                      
      *================================================================*        
           MOVE SPACES                     TO KH50-KEY-FLD.                     
           MOVE FH50-NUM-REGN              TO KH50-NUM-REGN.                    
                                                                                
           READ BP13KH50.                                                       
                                                                                
           EVALUATE WS-KH50-STATUS                                              
              WHEN ZEROS                                                        
                 MOVE 'Y'              tO KH50-CDE-EMAIL-SENT                   
                 PERFORM 4650-UPDATE-BP13KH50  THRU 4650-EXIT                   
              WHEN 23                                                           
                 MOVE SPACES                  TO BP13KH50-REC                   
              WHEN OTHER                                                        
                MOVE  WS-KH50-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BP13KH50 FILE : ' WS-KH50-STATUS         
                DISPLAY 'KH50-NUM-REGN = ' KH50-NUM-REGN                        
                PERFORM 9000-CLOSE-ROUTINE                                      
                   THRU 9000-EXIT                                               
           END-EVALUATE.                                                        
                                                                                
       4600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4650-UPDATE-BP13KH50.                                                    
      *================================================================*        
            REWRITE BP13KH50-REC.                                               
                                                                                
            EVALUATE WS-KH50-STATUS                                             
              WHEN 00                                                           
                   ADD 1 TO WS-KH50-UPD                                         
                                                                                
              WHEN OTHER                                                        
                   DISPLAY 'REWRITE BP13KH50 ERROR ' WS-KH50-STATUS             
                   MOVE WS-KH50-STATUS              TO RETURN-CODE              
                   PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       4650-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       6000-CALL-BP13CCNT.                                                      
      *================================================================*        
           MOVE 'EML'                      TO LINK-CNTCT-TYPE.                  
           MOVE WS-OIC                     TO LINK-STAFF-NICK.                  
           MOVE SPACES                     TO LINK-STAFF-NUM.                   
                                                                                
           CALL 'BP13CCNT' USING WS-LINK-REC.                                   
                                                                                
           EVALUATE LINK-SYSERR                                                 
           WHEN ZEROES                                                          
              MOVE LINK-STAFF-CNTCT        TO WS-EMAIL                          
           WHEN 100                                                             
              DISPLAY 'OIC NOT FOUND IN OFFICE_CNTCT TABLE - '                  
                         WS-OIC  ', REGN : ' FH50-NUM-REGN                      
              MOVE  SPACES                 TO WS-EMAIL                          
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING OFFICE_CNTCT TABLE = '                     
                       LINK-SYSERR                                              
              MOVE  SPACES                 TO WS-EMAIL                          
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-ROUTINE.                                                      
      *================================================================*        
                                                                                
           DISPLAY 'PROGRAM: BP13CH53'.                                         
           DISPLAY 'TOTAL INPUT RECORDS        : ' WS-FH50-INPUT.               
           DISPLAY 'TOTAL RECORD WRITTEN       : ' WS-WRITTEN.                  
           DISPLAY 'TOTAL RECORD NOT WRITTEN   : ' WS-FH50-OUT.                 
           DISPLAY 'TOTAL RECORD UPD KH50      : ' WS-KH50-UPD.                 
           DISPLAY '        '                                                   
                                                                                
           CLOSE BP13FH50                                                       
                 BP13K022                                                       
                 BP13LH53                                                       
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K022 ERROR ' WS-K022-STATUS                  
              MOVE     WS-K022-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
