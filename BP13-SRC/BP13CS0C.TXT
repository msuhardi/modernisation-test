       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CS0C.                                                 
       AUTHOR.        MRR5.                                                     
       DATE-WRITTEN.  18/05/2023.                                               
      *========================================================*                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO PROCESS BP13F740                   *                
      *                  AND OUTPUT TO BP13F203 FORMAT         *                
      *    INPUT       : BP13F740                              *                
      *                  BP13.K820.OCCUP                       *                
      *    OUTPUT      : BP13.F203.SMS.M2                      *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO    BY    DATE      DETAILS                      *                
      * BP139857  MRR5  20230518  NEW PROGRAM                  *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
           SELECT BP13F203 ASSIGN TO BP13F203.                                  
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS K820-STATUS.                               
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F740                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F740.                                                           
                                                                                
       FD  BP13F203                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F203.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 K820-STATUS                 PIC 9(2)  VALUE ZEROS.                
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F203-WRITE               PIC 9(5)  VALUE ZEROS.                
           05 WS-F203-NOT-WRITE           PIC 9(5)  VALUE ZEROS.                
           05 WS-F740-READ                PIC 9(5)  VALUE ZEROS.                
                                                                                
       01  WS-CUR-DATE1                   PIC 9(8)  VALUE ZEROES.               
       01  WS-DATE-VALUES.                                                      
           05  WS-CUR-DATE                PIC X(8).                             
           05  WS-CUR-DATE-NUM REDEFINES WS-CUR-DATE                            
                                          PIC 9(8).                             
                                                                                
       01  WS-F740-EOF                    PIC X(1)  VALUE SPACES.               
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       000-MAIN-ROUTINE.                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-BP13F740  THRU 200-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 999-CLOSE-ROUTINE  THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       100-INITIALIZATION.                                                      
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F740                                                 
                       BP13K820                                                 
                OUTPUT BP13F203.                                                
                                                                                
           IF K820-STATUS NOT = ZEROS AND 97                                    
              DISPLAY ' ERROR OPENING BP13K820  FILE, STATUS '                  
                  K820-STATUS                                                   
              MOVE K820-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                          
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CUR-DATE.                        
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       200-READ-BP13F740.                                                       
      *-------------------------------------------------------------            
           READ BP13F740 AT END                                                 
                MOVE 'Y' TO WS-F740-EOF                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1         TO WS-F740-READ.                                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-RTN.                                                         
      ******************************************************************        
                                                                                
           PERFORM 600-READ-K820       THRU 600-EXIT.                           
                                                                                
           IF K820-NUM-HP-PGR NOT = SPACES AND LOW-VALUES AND ZEROES            
              PERFORM 330-WRITE-F203      THRU 330-EXIT                         
           ELSE                                                                 
              IF K820-NUM-HP-PGR = SPACES OR LOW-VALUES OR ZEROES               
                 DISPLAY 'NO HANDPHONE: ' F740-NUM-REGN                         
              END-IF                                                            
              ADD   1                     TO WS-F203-NOT-WRITE                  
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-BP13F740  THRU 200-EXIT.                            
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       330-WRITE-F203.                                                          
      ******************************************************************        
                                                                                
           MOVE SPACES                 TO BP13F203-REC.                         
           MOVE K820-NUM-NRIC          TO F203-NUM-NRIC.                        
           MOVE K820-NME-OCCP          TO F203-NME-NRIC.                        
           MOVE K820-NUM-HP-PGR        TO F203-NUM-HP.                          
           MOVE 'HA1'                  TO F203-NUM-APLCNT.                      
           MOVE SPACES                 TO F203-DTE-APPMT.                       
           MOVE SPACES                 TO F203-TME-APPMT.                       
           MOVE 'M2'                   TO F203-TYPE-APPMT.                      
           MOVE F740-NUM-ALLO-CAT      TO F203-NUM-ALLO-MODE.                   
           MOVE SPACES                 TO F203-NUM-NEW-TOWN.                    
           MOVE F740-CDE-FLAT-TYPE     TO F203-NUM-FLAT-TYPE.                   
           MOVE F740-CDE-ZONE          TO F203-NUM-ZONE.                        
           MOVE F740-CDE-CATEGORY      TO F203-NUM-ETHNIC.                      
           MOVE F740-NUM-NT-FT-QUEUE   TO F203-NUM-QUEUE.                       
           MOVE F740-NUM-REGN          TO F203-NUM-REGN.                        
           MOVE F740-DTE-BALLOT        TO F203-DTE-BALLOT.                      
                                                                                
           WRITE BP13F203-REC.                                                  
           ADD   1                     TO WS-F203-WRITE.                        
                                                                                
       330-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       600-READ-K820.                                                           
      ******************************************************************        
                                                                                
           MOVE SPACES              TO BP13K820-REC.                            
           MOVE F740-NUM-REGN       TO K820-NUM-REGN.                           
           MOVE F740-NUM-NRIC1      TO K820-NUM-NRIC.                           
                                                                                
           READ BP13K820.                                                       
           IF K820-STATUS = 0                                                   
              CONTINUE                                                          
           ELSE                                                                 
             IF K820-STATUS = 23                                                
                DISPLAY 'MAIN APPLICANT NOT FOUND: ' F740-NUM-REGN              
             ELSE                                                               
                DISPLAY 'ERROR READING K820 : ' K820-STATUS                     
             END-IF                                                             
           END-IF.                                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       999-CLOSE-ROUTINE.                                                       
      ******************************************************************        
           CLOSE BP13F740                                                       
                 BP13K820                                                       
                 BP13F203.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CS0C ================*'.              
           DISPLAY '*======= RUN DATE : ' WS-CUR-DATE ' =======*'.              
           DISPLAY '    TOTAL NO OF REC READ    = ' WS-F740-READ.               
           DISPLAY '    NO. OF REC NOT SENT     = ' WS-F203-NOT-WRITE.          
           DISPLAY '    NO. OF SMS FOR SENDING  = ' WS-F203-WRITE.              
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
