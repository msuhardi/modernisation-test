       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CS02.                                                 
       AUTHOR.        CHAN KIN KEONG.                                           
       DATE-WRITTEN.  26/10/2006.                                               
      *========================================================*                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO EXTRACT BOOKING CASES FOR SENDING  *                
      *                  SMS. TO CHECK FOR THE NEXT DAY CASES. *                
      *    INPUT       : BP13.F203.SMS                         *                
      *                  BP13.K800.APPLN                       *                
      *    OUTPUT      : BP13.F203.SMS.BK                      *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE     DETAILS                          *               
      *--------------------------------------------------------*                
      * BP133908 PCL3 20100514 SET +3 DAYS TO CURRENT-DATE      *               
      *========================================================*                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F203 ASSIGN TO BP13F203.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS K800-STATUS.                               
                                                                                
           SELECT BP13K76D ASSIGN TO BP13K76D                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K76D-KEY-FLD                               
                  FILE STATUS     IS K76D-STATUS.                               
                                                                                
           SELECT BP13F20A ASSIGN TO BP13F20A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F203                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F203.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K76D                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K76D.                                                           
                                                                                
       FD  BP13F20A                                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  BP13F20A-REC                   PIC X(200).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 K800-STATUS                 PIC 9(2)  VALUE ZEROS.                
           05 K76D-STATUS                 PIC 9(2)  VALUE ZEROS.                
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F203-WRITE               PIC 9(5)  VALUE ZEROS.                
           05 WS-F203-NOT-WRITE           PIC 9(5)  VALUE ZEROS.                
           05 WS-READ-CNT-F203            PIC 9(5)  VALUE ZEROS.                
           05 WS-BLOCKED                  PIC 9(5)  VALUE ZEROS.                
                                                                                
       01  WS-EOF-F203                    PIC X     VALUE 'N'.                  
       01  WS-APPT                        PIC X     VALUE 'Y'.                  
       01  WS-K76D-FOUND                  PIC X     VALUE 'N'.                  
                                                                                
       01  WS-CUR-DATE1                   PIC 9(8)  VALUE ZEROES.               
       01  WS-DATE-VALUES.                                                      
           05  WS-CUR-DATE                PIC X(8).                             
           05  WS-CUR-DATE-NUM REDEFINES WS-CUR-DATE                            
                                          PIC 9(8).                             
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       000-MAIN-ROUTINE.                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F203      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL WS-EOF-F203 = 'Y'                                            
           PERFORM 999-CLOSE-ROUTINE  THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       100-INITIALIZATION.                                                      
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F203                                                 
                       BP13K800                                                 
                       BP13K76D                                                 
                OUTPUT BP13F20A.                                                
                                                                                
           IF K800-STATUS NOT = ZEROS AND 97                                    
              DISPLAY ' ERROR OPENING BP13K800  FILE, STATUS '                  
                  K800-STATUS                                                   
              MOVE K800-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                          
                                                                                
           IF K76D-STATUS NOT = ZEROS AND 97                                    
              DISPLAY ' ERROR OPENING BP13K76D  FILE, STATUS '                  
                  K76D-STATUS                                                   
              MOVE K76D-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                          
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CUR-DATE.                        
                                                                                
      * FIND NEXT DAY                                                           
                                                                                
           COMPUTE WS-CUR-DATE-NUM =                                            
                   FUNCTION DATE-OF-INTEGER(                                    
                   FUNCTION INTEGER-OF-DATE(WS-CUR-DATE-NUM) + 3).              
                                                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       200-READ-F203.                                                           
      ******************************************************************        
                                                                                
           READ BP13F203 AT END                                                 
                MOVE 'Y' TO WS-EOF-F203                                         
                GO TO 200-EXIT.                                                 
           ADD   1     TO WS-READ-CNT-F203.                                     
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-RTN.                                                         
      ******************************************************************        
           IF WS-CUR-DATE = F203-DTE-APPMT                                      
              MOVE 'Y' TO WS-APPT                                               
              PERFORM 400-READ-K800         THRU 400-EXIT                       
              IF ((WS-APPT = 'Y') AND                                           
                 (F203-NUM-HP NOT = SPACES AND LOW-VALUES AND ZEROES))          
                 PERFORM 500-READ-K76D       THRU 500-EXIT                      
                 IF WS-K76D-FOUND NOT = 'Y'                                     
                   PERFORM 330-WRITE-F203      THRU 330-EXIT                    
                 ELSE                                                           
                   ADD   1                     TO WS-BLOCKED                    
                   PERFORM 200-READ-F203       THRU 200-EXIT                    
                 END-IF                                                         
              ELSE                                                              
                 IF F203-NUM-HP = SPACES OR LOW-VALUES OR ZEROES                
                    DISPLAY 'NO HANDPHONE: ' K800-NUM-REGN                      
                 END-IF                                                         
                 IF WS-APPT = 'N'                                               
                    DISPLAY 'CANCELLED CASE: ' K800-NUM-REGN                    
                 END-IF                                                         
                 ADD   1                     TO WS-F203-NOT-WRITE               
                 PERFORM 200-READ-F203       THRU 200-EXIT                      
              END-IF                                                            
           ELSE                                                                 
              PERFORM 200-READ-F203       THRU 200-EXIT                         
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       330-WRITE-F203.                                                          
      ******************************************************************        
                                                                                
           MOVE SPACES                 TO BP13F20A-REC.                         
           MOVE BP13F203-REC           TO BP13F20A-REC.                         
                                                                                
           WRITE BP13F20A-REC.                                                  
           ADD   1                     TO WS-F203-WRITE.                        
                                                                                
           PERFORM 200-READ-F203       THRU 200-EXIT.                           
                                                                                
       330-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       400-READ-K800.                                                           
      ******************************************************************        
                                                                                
           MOVE F203-NUM-REGN       TO K800-NUM-REGN.                           
           READ BP13K800 KEY IS K800-NUM-REGN.                                  
                                                                                
           IF K800-STATUS = 0                                                   
              IF K800-NUM-STATUS = 'C'                                          
                 DISPLAY 'REC CANCELLED IN SOC MASTER: ' F203-NUM-REGN          
                 MOVE 'N' TO WS-APPT                                            
              ELSE                                                              
                 IF ((K800-DTE-BK-APPT = SPACES OR LOW-VALUES) OR               
                    (K800-TME-BK-APPT = SPACES OR LOW-VALUES))                  
                  DISPLAY 'REC HAVE INCORRECT DTE/TME : ' F203-NUM-REGN         
                  MOVE 'N' TO WS-APPT                                           
                 ELSE                                                           
                   IF K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES              
                      DISPLAY 'REC ACCEPTED A FLAT : '                          
                              F203-NUM-REGN '-'  K800-NUM-SCH-ACC               
                      MOVE 'N' TO WS-APPT                                       
                   ELSE                                                         
                      CONTINUE                                                  
                   END-IF                                                       
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              DISPLAY 'REC NOT FOUND IN SOC MASTER: ' F203-NUM-REGN             
              MOVE 'N' TO WS-APPT                                               
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       500-READ-K76D.                                                           
      ******************************************************************        
                                                                                
           MOVE SPACES              TO BP13K76D-REC                             
                                       WS-K76D-FOUND.                           
           MOVE F203-NUM-ALLO-MODE  TO K76D-NUM-SALES-MODE                      
           MOVE F203-DTE-BALLOT     TO K76D-NUM-DTE-BALLOT                      
           MOVE F203-NUM-REGN       TO K76D-NUM-REGN                            
                                                                                
           READ BP13K76D KEY IS K76D-KEY-FLD.                                   
                                                                                
           IF K76D-STATUS = 0                                                   
              MOVE 'Y'    TO WS-K76D-FOUND                                      
           ELSE                                                                 
              DISPLAY 'REC NOT FOUND IN K76D MASTER: ' F203-NUM-REGN            
              MOVE 'N' TO WS-K76D-FOUND                                         
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       999-CLOSE-ROUTINE.                                                       
      ******************************************************************        
           CLOSE BP13F203                                                       
                 BP13K800                                                       
                 BP13K76D                                                       
                 BP13F20A.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CS02 ================*'.              
           DISPLAY '*======= RUN DATE : ' WS-CUR-DATE ' =======*'.              
           DISPLAY '    TOTAL NO OF REC READ    = ' WS-READ-CNT-F203.           
           DISPLAY '    NO. OF REC NOT SENT     = ' WS-F203-NOT-WRITE.          
           DISPLAY '    NO. OF SMS SENT         = ' WS-F203-WRITE.              
           DISPLAY '    NO. OF REC FOUND IN K76D= ' WS-BLOCKED.                 
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
