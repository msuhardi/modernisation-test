      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CD42.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  25/04/08.                                                 
      *****************************************************************         
      *OBJECTIVE - WHEN DBSS FLAT IS KEY-ISSUED, SALE ORDER FILES IS  *         
      *            CREATED.                                           *         
      *            WHEN DBSS FLAT PASS DOWN TO SMS, SOC WILL CLOSE    *         
      *            ACCOUNT AND DELETE DBSS FILE.                      *         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13F400                                  *         
      *                  2. BP13F400                                  *         
      *                                                               *         
      *     I-O FILE   : 1. BP13KD00                                  *         
      *                  2. BP13KD63                                  *         
      *                                                               *         
      *     OUTPUT FILE:                                              *         
      *                  1. BP13KD03                                  *         
      *                  2. BP13FD60                                  *         
      *                  3. BP13KD64                                  *         
      *                  4. BP13F440                                  *         
      *                  5. BP13F136                                  *         
      *                  6. BP13FD38                                  *         
      *                  6. BP13LD42                                  *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * REF NO   DATE      BY   AMENDMENTS/ENHANCEMENTS               *         
      * -------  --------  ---  ------------------------              *         
      * BP133298 08/05/2008  JB8  NEW PROGRAM.                        *         
      *          07/08/2008  JB8  NEW OUTPUT FILE BP13K136            *         
      *          09/07/2008  JB8  REMOVE BP13KD60 ,BP13KD61 ,BP13FD60 *         
      *          17/09/2008  JB8  BROWSE BP13KD38 TO GET SCHL = '08'  *         
      *          18/09/2008  JB8  OUTPUT FD60(FROM KD60 & KD61)       *         
      *                           OUTPUT FD63(FROM KD63)              *         
      *                           NEW REPORT FOR REC NOT FOUND(LD42)  *         
      * BP133505 14/01/2009  JB8  OUTPUT F136(INS 71)                 *         
      * BP133446 10/02/2009  LSB  TO CATER FOR DUPLICATE KEY          *         
      * BP133545 06/03/2009  JB8  TO WRITE LOAN-SCH TO BP13F136       *         
      * BP133588 06/05/2009  JB8  WON'T OUTPUT ZERO F136 RECS         *         
      * BP133638 27/07/2009  JB8  TO CATER FOR PP-LUMPSUM             *         
      * BP133612 14/08/2009  JB8  COMPARE SYS DATE & DUE DATE WHEN    *         
      *                           OUTPUT FD38 TO SMS                  *         
      * BP134089 31/05/2011  JB8  TO CATER FOR LOAN PAID              *         
      * BP134245 15/06/2011  JB8  TO CATER FOR REC WITH OPEN LOAN BAL *         
      * BP134310 17/08/2011  JB8  TO CATER FOR SEQ NO                 *         
      * BP135161 23/01/2014  IMC1 TO REMOVE CHECKING OF LOAN BALANCE  *         
      * BP135260 31/07/2014  IMC1 CATER FOR LAST RECORD NOT FND KD63  *         
      * BP135454 11/08/2014  IMC1 CATER FOR DEBIT AMOUNT              *         
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  SY02F001  ASSIGN        TO SY02F001.                         
           SELECT  BP13F400  ASSIGN        TO BP13F400.                         
           SELECT  BP13F440  ASSIGN        TO BP13F440.                         
           SELECT  BP13F136  ASSIGN        TO BP13F136.                         
           SELECT  BP13FD38  ASSIGN        TO BP13FD38.                         
           SELECT  BP13FD60  ASSIGN        TO BP13FD60.                         
           SELECT  BP13FD63  ASSIGN        TO BP13FD63.                         
           SELECT  BP13LD42  ASSIGN        TO BP13LD42.                         
           SELECT  BP13KD00  ASSIGN        TO BP13KD00                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KD00-KEY-FLD                      
                             FILE STATUS   IS WS-KD00-STATUS.                   
           SELECT  BP13KD03  ASSIGN        TO BP13KD03                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KD03-KEY-FLD                      
                             FILE STATUS   IS WS-KD03-STATUS.                   
           SELECT  BP13KD38  ASSIGN        TO BP13KD38                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KD38-KEY-FLD                      
                             FILE STATUS   IS WS-KD38-STATUS.                   
           SELECT  BP13KD60  ASSIGN        TO BP13KD60                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KD60-KEY-FLD                      
                             FILE STATUS   IS WS-KD60-STATUS.                   
           SELECT  BP13KD61  ASSIGN        TO BP13KD61                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KD61-KEY-FLD                      
                             FILE STATUS   IS WS-KD61-STATUS.                   
           SELECT  BP13KD63  ASSIGN        TO BP13KD63                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KD63-KEY-FLD                      
                             FILE STATUS   IS WS-KD63-STATUS.                   
           SELECT  BP13KD64  ASSIGN        TO BP13KD64                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KD64-KEY-FLD                      
                             FILE STATUS   IS WS-KD64-STATUS.                   
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13F400                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 2000 CHARACTERS          
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F400.                                                           
                                                                                
       FD   BP13KD00                                                            
                                       RECORD CONTAINS 1600 CHARACTERS.         
       COPY BP13KD00.                                                           
                                                                                
       FD   BP13KD03                                                            
                                       RECORD CONTAINS 1600 CHARACTERS.         
       COPY BP13KD03.                                                           
                                                                                
       FD   BP13KD38                                                            
                                       RECORD CONTAINS 400 CHARACTERS.          
       COPY BP13KD38.                                                           
                                                                                
       FD   BP13KD60                                                            
                                       RECORD CONTAINS 250 CHARACTERS.          
       COPY BP13KD60.                                                           
                                                                                
       FD   BP13KD61                                                            
                                       RECORD CONTAINS 250 CHARACTERS.          
       COPY BP13KD61.                                                           
                                                                                
       FD   BP13KD63                                                            
                                       RECORD CONTAINS 150 CHARACTERS.          
       COPY BP13KD63.                                                           
                                                                                
       FD   BP13KD64                                                            
                                       RECORD CONTAINS 150 CHARACTERS.          
       COPY BP13KD64.                                                           
                                                                                
       FD   BP13F440                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 100 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F440.                                                           
                                                                                
       FD   BP13FD38                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 400 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13FD38.                                                           
                                                                                
       FD   BP13F136                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 150 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F136.                                                           
                                                                                
       FD   BP13FD60                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 250 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13FD60.                                                           
                                                                                
       FD   BP13FD63                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 150 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13FD63.                                                           
                                                                                
       FD   BP13LD42                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 132 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       01   BP13LD42-REC               PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-F001-EOF             PIC X(1)  VALUE 'N'.                     
               88 EOF-F001                       VALUE 'Y'.                     
           05  WS-EOF-F400             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-KD63             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-KD61             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-KD38             PIC X(1)  VALUE 'N'.                     
           05  WS-KD00-STATUS          PIC 9(2)  VALUE ZEROES.                  
           05  WS-KD03-STATUS          PIC 9(2)  VALUE ZEROES.                  
           05  WS-KD38-STATUS          PIC 9(2)  VALUE ZEROES.                  
           05  WS-KD63-STATUS          PIC 9(2)  VALUE ZEROES.                  
           05  WS-KD64-STATUS          PIC 9(2)  VALUE ZEROES.                  
           05  WS-KD60-STATUS          PIC 9(2)  VALUE ZEROES.                  
           05  WS-KD61-STATUS          PIC 9(2)  VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F400-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SKIP-LOAN-BAL        PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD00-FOUND           PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD00-DELETE          PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD00-NOTFND          PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD03-WRITE           PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD63-FOUND           PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD63-NOTFND          PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD63-DELETE          PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD64-WRITE           PIC 9(7)  VALUE ZEROES.                  
           05  WS-F440-WRITE           PIC 9(7)  VALUE ZEROES.                  
           05  WS-F136-WRITE-PP        PIC 9(7)  VALUE ZEROES.                  
           05  WS-F136-WRITE-LON       PIC 9(7)  VALUE ZEROES.                  
           05  WS-F136-WRITE-INS       PIC 9(7)  VALUE ZEROES.                  
           05  WS-FD38-WRITE           PIC 9(7)  VALUE ZEROES.                  
           05  WS-FD60-WRITE-KD60      PIC 9(7)  VALUE ZEROES.                  
           05  WS-FD60-WRITE-KD61      PIC 9(7)  VALUE ZEROES.                  
           05  WS-FD63-WRITE           PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD61-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD60-READ            PIC 9(7)  VALUE ZEROES.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-ICP-TOTAL             PIC 9(7)V99 VALUE ZEROES.                
           05 WS-KD63-FND              PIC X(01) VALUE 'N'.                     
           05 WS-KD61-FND              PIC X(01) VALUE 'N'.                     
           05 WS-KD38-FND              PIC X(01) VALUE 'N'.                     
                                                                                
       01  WS-CNT                      PIC 9(02) VALUE ZEROES.                  
       01  WS-I                        PIC 9(02) VALUE ZEROES.                  
       01  WS-J                        PIC 9(02) VALUE ZEROES.                  
       01  WS-TMP                      PIC 9(02) VALUE ZEROES.                  
       01  WS-BP13KD38-REC.                                                     
           05  WS-KD38-REC OCCURS 15 TIMES.                                     
               10  WS-KD38-REGN        PIC X(08).                               
               10  WS-KD38-TRANS       PIC X(03).                               
               10  WS-KD38-PP          PIC X(02).                               
               10  FILLER              PIC X(10).                               
               10  WS-KD38-DUE         PIC X(08).                               
               10  FILLER              PIC X(369).                              
           05  WS-KD38-SKIP OCCURS 15 TIMES  PIC X(01).                         
                                                                                
       01  WS-PREV-KD63.                                                        
           05  WS-KD63-REGN            PIC X(08) VALUE SPACES.                  
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  WS-KD63-PROCESS         PIC X(08) VALUE SPACES.                  
           05  WS-KD63-SEQ             PIC 9(03) VALUE ZEROES.                  
                                                                                
       77  WS-NUM-SEQ-KD63             PIC 9(03) VALUE 001.                     
                                                                                
       01  WS-REPORT-VARIABLES.                                                 
           05  WS-REC-CNT              PIC 9(07) VALUE ZEROES.                  
           05  WS-LINE-CNT             PIC 9(07) VALUE ZEROES.                  
           05  WS-PAGENO               PIC 9(04) VALUE ZEROES.                  
           05  WS-FILENAME             PIC X(08) VALUE SPACES.                  
           05  WS-KEY-FLD              PIC X(20) VALUE SPACES.                  
           05  WS-KEY-VALUE            PIC X(30) VALUE SPACES.                  
                                                                                
      *-----------------------------------------------------------*             
      * LAYOUT FOR NOT FOUND REPORT                               *             
      *-----------------------------------------------------------*             
       01 LD42-HEADER1.                                                         
          05 FILLER                      PIC X(08) VALUE 'BP13LD42'.            
          05 FILLER                      PIC X(06) VALUE SPACES.                
          05 FILLER                      PIC X(04) VALUE 'HDB3'.                
          05 FILLER                      PIC X(38) VALUE SPACES.                
          05 FILLER                      PIC X(20) VALUE                        
                                         'SYSTEM OF COMMITMENT'.                
          05 FILLER                      PIC X(26) VALUE SPACES.                
          05 FILLER                      PIC X(07) VALUE 'DATE : '.             
          05 LD42-DATE                   PIC X(10).                             
          05 FILLER                      PIC X(02) VALUE SPACES.                
          05 FILLER                      PIC X(07) VALUE 'PAGE : '.             
          05 LD42-PAGE                   PIC Z(04).                             
                                                                                
       01 LD42-HEADER2.                                                         
          05 FILLER                      PIC X(53) VALUE SPACES.                
          05 FILLER                      PIC X(27) VALUE                        
             'REPORT OF RECORDS NOT FOUND'.                                     
          05 FILLER                      PIC X(52) VALUE SPACES.                
                                                                                
       01 LD42-HEADER3.                                                         
          05 FILLER                      PIC X(05) VALUE SPACES.                
          05 FILLER                      PIC X(07) VALUE 'S/N'.                 
          05 FILLER                      PIC X(03) VALUE SPACES.                
          05 FILLER                      PIC X(08) VALUE 'FILENAME'.            
          05 FILLER                      PIC X(03) VALUE SPACES.                
          05 FILLER                      PIC X(20) VALUE 'KEY-FLD'.             
          05 FILLER                      PIC X(03) VALUE SPACES.                
          05 FILLER                      PIC X(30) VALUE 'KEY-VALUE'.           
          05 FILLER                      PIC X(55) VALUE SPACES.                
                                                                                
       01 LD42-HEADER4.                                                         
          05 FILLER                      PIC X(05) VALUE SPACES.                
          05 FILLER                      PIC X(07) VALUE ALL '-'.               
          05 FILLER                      PIC X(03) VALUE SPACES.                
          05 FILLER                      PIC X(08) VALUE ALL '-'.               
          05 FILLER                      PIC X(03) VALUE SPACES.                
          05 FILLER                      PIC X(20) VALUE ALL '-'.               
          05 FILLER                      PIC X(03) VALUE SPACES.                
          05 FILLER                      PIC X(30) VALUE ALL '-'.               
          05 FILLER                      PIC X(53) VALUE SPACES.                
                                                                                
       01 LD42-DETAIL.                                                          
          05 FILLER                      PIC X(05) VALUE SPACES.                
          05 LD42-SN                     PIC ZZZZZZ9 VALUE ZEROES.              
          05 FILLER                      PIC X(03) VALUE SPACES.                
          05 LD42-FILENAME               PIC X(08) VALUE SPACES.                
          05 FILLER                      PIC X(03) VALUE SPACES.                
          05 LD42-KEY-FLD                PIC X(20) VALUE SPACES.                
          05 FILLER                      PIC X(03) VALUE SPACES.                
          05 LD42-KEY-VALUE              PIC X(30) VALUE SPACES.                
          05 FILLER                      PIC X(53) VALUE SPACES.                
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F400      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F400 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F400                                                 
                       SY02F001                                                 
                       BP13KD38                                                 
                       BP13KD60                                                 
                       BP13KD61                                                 
              I-O      BP13KD03                                                 
                       BP13KD00                                                 
                       BP13KD63                                                 
                       BP13KD64                                                 
              OUTPUT   BP13F440                                                 
                       BP13F136                                                 
                       BP13FD38                                                 
                       BP13FD60                                                 
                       BP13FD63                                                 
                       BP13LD42.                                                
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD00 - ERROR OPENING : '                             
                      WS-KD00-STATUS                                            
              MOVE WS-KD00-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF WS-KD03-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD03 - ERROR OPENING : '                             
                      WS-KD03-STATUS                                            
              MOVE WS-KD03-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF WS-KD38-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD38 - ERROR OPENING : '                             
                      WS-KD38-STATUS                                            
              MOVE WS-KD38-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF WS-KD60-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD60 - ERROR OPENING : '                             
                      WS-KD60-STATUS                                            
              MOVE WS-KD60-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF WS-KD61-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD61 - ERROR OPENING : '                             
                      WS-KD61-STATUS                                            
              MOVE WS-KD61-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF WS-KD63-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD63 - ERROR OPENING : '                             
                      WS-KD63-STATUS                                            
              MOVE WS-KD63-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF WS-KD64-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD64 - ERROR OPENING : '                             
                      WS-KD64-STATUS                                            
              MOVE WS-KD64-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           READ SY02F001 AT END MOVE 'Y'  TO WS-F001-EOF.                       
           IF EOF-F001                                                          
              DISPLAY 'RECORD NOT FOUND IN SY01F001 '                           
              DISPLAY 'PROCESSING TERMINATED '                                  
              GO TO 9999-CLOSE-ROUTINE                                          
           ELSE                                                                 
              MOVE F001-DTE-CURRENT      TO   WS-SYSTEM-DATE                    
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F400.                                                      
      ******************************************************************        
                                                                                
           READ BP13F400           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F400                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO   WS-F400-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF F400-NUM-REGN = SPACES OR LOW-VALUES                              
              GO TO 3000-READNEXT                                               
           END-IF.                                                              
                                                                                
           PERFORM 4100-READ-BP13KD00             THRU 4100-EXIT.               
                                                                                
           IF WS-KD00-STATUS = ZEROS                                            
              PERFORM 4500-WRITE-BP13F440         THRU 4500-EXIT                
              PERFORM 4900-OUTPUT-INS70           THRU 4900-EXIT                
                                                                                
              PERFORM 4200-START-BP13KD63         THRU 4200-EXIT                
              PERFORM 4210-CREATE-SUMMARY         THRU 4210-EXIT                
                                                                                
              PERFORM 5300-READ-BP13KD60          THRU 5300-EXIT                
              PERFORM 5400-START-BP13KD61         THRU 5400-EXIT                
                                                                                
              PERFORM 5000-BROWSE-BP13KD38        THRU 5000-EXIT                
              PERFORM 5200-WRITE-BP13FD38         THRU 5200-EXIT                
              PERFORM 4600-OUTPUT-BP13F136        THRU 4600-EXIT                
              PERFORM 4130-WRITE-BP13KD03         THRU 4130-EXIT                
              PERFORM 4170-DELETE-BP13KD00        THRU 4170-EXIT                
           END-IF.                                                              
                                                                                
       3000-READNEXT.                                                           
           PERFORM 2000-READ-BP13F400             THRU 2000-EXIT.               
       3000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       4100-READ-BP13KD00.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                   TO BP13KD00-REC.                       
           INITIALIZE                       BP13KD00-REC.                       
                                                                                
           MOVE F400-NUM-REGN            TO KD00-NUM-REGN.                      
                                                                                
           READ BP13KD00 KEY IS KD00-KEY-FLD.                                   
                                                                                
           EVALUATE WS-KD00-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 ADD 1                   TO WS-KD00-FOUND                       
              WHEN 23                                                           
                 ADD 1                   TO WS-KD00-NOTFND                      
                 MOVE SPACES TO WS-FILENAME WS-KEY-FLD WS-KEY-VALUE             
                 MOVE 'BP13KD00'         TO WS-FILENAME                         
                 MOVE 'REGN'             TO WS-KEY-FLD                          
                 MOVE F400-NUM-REGN      TO WS-KEY-VALUE                        
                 PERFORM 7000-WRITE-REPORT THRU 7000-EXIT                       
              WHEN OTHER                                                        
                 DISPLAY 'BP13KD00 - ERROR READING : '                          
                         WS-KD00-STATUS                                         
                 MOVE WS-KD00-STATUS     TO RETURN-CODE                         
                 GO TO 9999-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
           IF KD00-AMT-LOAN-PAID IS NOT NUMERIC                                 
              MOVE ZEROS TO KD00-AMT-LOAN-PAID.                                 
                                                                                
           IF KD00-AMT-LOAN-PD-DEVLP-TOT IS NOT NUMERIC                         
              MOVE ZEROS TO KD00-AMT-LOAN-PD-DEVLP-TOT.                         
                                                                                
           IF KD00-AMT-PP-LUMPSUM-PAID IS NOT NUMERIC                           
              MOVE ZEROS TO KD00-AMT-PP-LUMPSUM-PAID.                           
                                                                                
           IF KD00-AMT-PP-PD-DEVLP-TOT IS NOT NUMERIC                           
              MOVE ZEROS TO KD00-AMT-PP-PD-DEVLP-TOT.                           
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4130-WRITE-BP13KD03.                                                     
      ******************************************************************        
                                                                                
           MOVE F001-DTE-CURRENT              TO KD00-DTE-TRANSFER-SMS.         
           MOVE KD00-AMT-LOAN-BAL             TO KD00-AMT-TRANSFER-SMS.         
           MOVE ZEROS                         TO KD00-AMT-LOAN-BAL.             
           MOVE BP13KD00-REC                  TO BP13KD03-REC.                  
                                                                                
           WRITE BP13KD03-REC.                                                  
                                                                                
           IF WS-KD03-STATUS = 00 OR 22 OR 02                                   
              ADD 1                         TO WS-KD03-WRITE                    
           ELSE                                                                 
              DISPLAY 'BP13KD03 - ERROR WRITING : '                             
                      WS-KD03-STATUS                                            
              MOVE WS-KD03-STATUS           TO RETURN-CODE                      
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       4130-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4170-DELETE-BP13KD00.                                                    
      ******************************************************************        
                                                                                
           DELETE BP13KD00.                                                     
                                                                                
           IF WS-KD00-STATUS = 00                                               
              ADD 1                         TO WS-KD00-DELETE                   
           ELSE                                                                 
              DISPLAY 'BP13KD00 - ERROR WRITING : '                             
                      WS-KD00-STATUS                                            
              MOVE WS-KD00-STATUS           TO RETURN-CODE                      
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       4170-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4200-START-BP13KD63.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                   TO BP13KD63-REC.                       
           INITIALIZE                       BP13KD63-REC.                       
           MOVE 'N'                      TO WS-EOF-KD63                         
                                            WS-KD63-FND.                        
           MOVE SPACES                   TO WS-PREV-KD63.                       
           INITIALIZE                       WS-PREV-KD63.                       
                                                                                
           MOVE F400-NUM-REGN            TO KD63-NUM-REGN.                      
                                                                                
           START BP13KD63 KEY >= KD63-KEY-FLD.                                  
                                                                                
           EVALUATE WS-KD63-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 PERFORM 4240-READNEXT-BP13KD63 THRU 4240-EXIT                  
                   UNTIL KD63-NUM-REGN NOT = F400-NUM-REGN                      
                      OR WS-EOF-KD63 = 'Y'                                      
                                                                                
                 IF WS-KD63-FND NOT = 'Y'                                       
                    MOVE SPACES TO WS-FILENAME WS-KEY-FLD WS-KEY-VALUE          
                    MOVE 'BP13KD63'      TO WS-FILENAME                         
                    MOVE 'REGN'          TO WS-KEY-FLD                          
                    MOVE F400-NUM-REGN   TO WS-KEY-VALUE                        
                    PERFORM 7000-WRITE-REPORT THRU 7000-EXIT                    
                 END-IF                                                         
              WHEN 23                                                           
                 MOVE SPACES TO WS-FILENAME WS-KEY-FLD WS-KEY-VALUE             
                 MOVE 'BP13KD63'      TO WS-FILENAME                            
                 MOVE 'REGN'          TO WS-KEY-FLD                             
                 MOVE F400-NUM-REGN   TO WS-KEY-VALUE                           
                 PERFORM 7000-WRITE-REPORT THRU 7000-EXIT                       
              WHEN OTHER                                                        
                 DISPLAY 'BP13KD63 - ERROR READING : '                          
                         WS-KD63-STATUS                                         
                 MOVE WS-KD63-STATUS     TO RETURN-CODE                         
                 GO TO 9999-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4210-CREATE-SUMMARY.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                   TO BP13FD63-REC.                       
           INITIALIZE                       BP13FD63-REC.                       
                                                                                
           MOVE F400-NUM-REGN            TO FD63-NUM-REGN.                      
           IF WS-SYSTEM-DATE = WS-KD63-PROCESS                                  
              COMPUTE WS-NUM-SEQ-KD63 = WS-KD63-SEQ + 1                         
           ELSE                                                                 
              COMPUTE WS-NUM-SEQ-KD63 = 1                                       
           END-IF.                                                              
           MOVE WS-NUM-SEQ-KD63          TO FD63-NUM-SEQ                        
           MOVE WS-SYSTEM-DATE           TO FD63-DTE-PROCESS                    
           MOVE WS-SYSTEM-DATE(5:2)      TO FD63-DTE-PYMT-MTH.                  
           MOVE KD00-AMT-LOAN-BAL        TO FD63-AMT-LOAN.                      
           IF KD00-AMT-LOAN-BAL >= 0                                            
              MOVE 'C'                   TO FD63-CDE-CR-DR                      
           ELSE                                                                 
              MOVE 'D'                   TO FD63-CDE-CR-DR                      
           END-IF                                                               
           MOVE KD00-NUM-SCH-ACCNT       TO FD63-NUM-SCH-ACCNT.                 
           MOVE 'DBSS-TRF'               TO FD63-TXT-DESC.                      
                                                                                
           MOVE BP13FD63-REC             TO BP13KD64-REC.                       
           PERFORM 4260-WRITE-BP13KD64   THRU 4260-EXIT.                        
           WRITE BP13FD63-REC                                                   
           ADD 1                         TO WS-FD63-WRITE.                      
                                                                                
       4210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4240-READNEXT-BP13KD63.                                                  
      ******************************************************************        
                                                                                
           READ BP13KD63 NEXT                                                   
             AT END MOVE 'Y' TO WS-EOF-KD63.                                    
                                                                                
           EVALUATE WS-KD63-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 IF KD63-NUM-REGN = F400-NUM-REGN                               
                    MOVE 'Y'                         TO WS-KD63-FND             
                    ADD 1                            TO WS-KD63-FOUND           
                    MOVE BP13KD63-REC                TO BP13KD64-REC            
                    MOVE KD63-KEY-FLD                TO WS-PREV-KD63            
                    PERFORM 4260-WRITE-BP13KD64    THRU 4260-EXIT               
                    PERFORM 6000-OUTPUT-BP13FD63   THRU 6000-EXIT               
                    PERFORM 4280-DELETE-BP13KD63   THRU 4280-EXIT               
                 ELSE                                                           
                    MOVE 'Y'                      TO WS-EOF-KD63                
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
                 MOVE 'Y'                         TO WS-EOF-KD63                
              WHEN OTHER                                                        
                 DISPLAY '**BP13KD63 - ERROR READING : '                        
                         WS-KD63-STATUS                                         
                 MOVE WS-KD63-STATUS              TO RETURN-CODE                
                 GO TO 9999-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       4240-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4260-WRITE-BP13KD64.                                                     
      ******************************************************************        
                                                                                
           WRITE BP13KD64-REC.                                                  
                                                                                
           IF WS-KD64-STATUS = 00 OR 22                                         
              ADD 1                         TO WS-KD64-WRITE                    
           ELSE                                                                 
              DISPLAY 'BP13KD64 - ERROR WRITING : '                             
                      WS-KD64-STATUS                                            
              MOVE WS-KD64-STATUS           TO RETURN-CODE                      
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       4260-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4280-DELETE-BP13KD63.                                                    
      ******************************************************************        
                                                                                
           DELETE BP13KD63.                                                     
                                                                                
           IF WS-KD63-STATUS = 00                                               
              ADD 1                         TO WS-KD63-DELETE                   
           ELSE                                                                 
              DISPLAY 'BP13KD63 - ERROR WRITING : '                             
                      WS-KD63-STATUS                                            
              MOVE WS-KD63-STATUS           TO RETURN-CODE                      
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       4280-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4500-WRITE-BP13F440.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                   TO BP13F440-REC.                       
           INITIALIZE BP13F440-REC.                                             
                                                                                
           MOVE WS-SYSTEM-DATE(7:2)      TO F440-DTE-TRANS-DD                   
                                            F440-DTE-JR-DD-NW.                  
           MOVE WS-SYSTEM-DATE(5:2)      TO F440-DTE-TRANS-MM                   
                                            F440-DTE-JR-MM-NW.                  
           MOVE WS-SYSTEM-DATE(3:2)      TO F440-DTE-TRANS-YY                   
                                            F440-DTE-JR-YY-NW.                  
           MOVE WS-SYSTEM-DATE(1:2)      TO F440-DTE-TRANS-CC.                  
                                                                                
           MOVE 'DBSS-TRF'               TO F440-TXT-DESC.                      
           IF KD00-AMT-LOAN-BAL >= 0                                            
              MOVE 'D'                   TO F440-CDE-CR-DR                      
           ELSE                                                                 
              MOVE 'C'                   TO F440-CDE-CR-DR                      
           END-IF                                                               
           MOVE 'JR'                     TO F440-CDE-JR-TYP-NW.                 
           MOVE 'DB'                     TO F440-CDE-TRANS-POSTNG.              
           MOVE F400-NUM-SCH-ACC         TO F440-NUM-HDB-REF.                   
                                                                                
           MOVE KD00-AMT-LOAN-BAL        TO F440-AMT-INST-LOAN                  
                                            F440-AMT-TOTL.                      
                                                                                
           WRITE BP13F440-REC.                                                  
           ADD 1 TO WS-F440-WRITE.                                              
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4600-OUTPUT-BP13F136.                                                    
      ******************************************************************        
      ** ON LOAN-PAID/PAID-TO-DEVEPLER, FOR UPDATE BP13K160                     
      **                                                                        
           IF (KD00-AMT-LOAN-PD-DEVLP-TOT > ZEROES OR                           
               KD00-AMT-LOAN-PAID > ZEROES) AND                                 
              (KD00-AMT-LOAN-PD-DEVLP-TOT NOT =                                 
               KD00-AMT-LOAN-PAID)                                              
              PERFORM 4800-WRITE-F136-INS71 THRU 4800-EXIT                      
           END-IF.                                                              
                                                                                
      ** ON PP-PAID/PAID-TO-DEVEPLER, FOR UPDATE BP13K160                       
      **                                                                        
           IF (KD00-AMT-PP-PD-DEVLP-TOT > ZEROES OR                             
               KD00-AMT-PP-LUMPSUM-PAID > ZEROES) AND                           
              (KD00-AMT-PP-PD-DEVLP-TOT NOT =                                   
               KD00-AMT-PP-LUMPSUM-PAID)                                        
              PERFORM 4850-WRITE-F136-PP71 THRU 4850-EXIT                       
           END-IF.                                                              
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       4800-WRITE-F136-INS71.                                                   
      **************************************************************            
      *-----------------------------------------------------                    
      * LOAN PAYMENT PAID - DELETE FROM BP13KD00                                
      *-----------------------------------------------------                    
           MOVE SPACES                   TO BP13F136-CASHTRAN.                  
           INITIALIZE                       BP13F136-CASHTRAN.                  
                                                                                
           MOVE '71'                     TO F136-CDE-TRANS-TYPE.                
           MOVE 'INS'                    TO F136-CDE-PAYMENT-TYPE.              
           MOVE WS-SYSTEM-DATE           TO F136-DTE-POST                       
                                            F136-DTE-TRANS.                     
           MOVE 'D'                      TO F136-CDE-CRDR.                      
                                                                                
           MOVE KD00-CDE-LOAN-SCH        TO F136-NUM-LOAN-SCH.                  
           MOVE F400-NUM-REGN            TO F136-NUM-REGN.                      
           MOVE F400-NUM-SCH-ACC         TO F136-NUM-SCH-ACCT.                  
           COMPUTE F136-AMT-RECEIPT = KD00-AMT-LOAN-PD-DEVLP-TOT -              
                                      KD00-AMT-LOAN-PAID.                       
                                                                                
           WRITE BP13F136-CASHTRAN.                                             
           ADD 1                         TO WS-F136-WRITE-PP.                   
                                                                                
       4800-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4850-WRITE-F136-PP71.                                                    
      **************************************************************            
      *-----------------------------------------------------                    
      * LUMPSUM PAYMENT PAID - DELETE FROM BP13KD00                             
      *-----------------------------------------------------                    
           MOVE SPACES                   TO BP13F136-CASHTRAN.                  
           INITIALIZE                       BP13F136-CASHTRAN.                  
                                                                                
           MOVE '71'                     TO F136-CDE-TRANS-TYPE.                
           MOVE 'PP '                    TO F136-CDE-PAYMENT-TYPE.              
           MOVE WS-SYSTEM-DATE           TO F136-DTE-POST                       
                                            F136-DTE-TRANS.                     
           MOVE 'D'                      TO F136-CDE-CRDR.                      
                                                                                
           MOVE KD00-CDE-LOAN-SCH        TO F136-NUM-LOAN-SCH.                  
           MOVE F400-NUM-REGN            TO F136-NUM-REGN.                      
           MOVE F400-NUM-SCH-ACC         TO F136-NUM-SCH-ACCT.                  
           COMPUTE F136-AMT-RECEIPT = KD00-AMT-PP-PD-DEVLP-TOT -                
                                      KD00-AMT-PP-LUMPSUM-PAID.                 
                                                                                
           WRITE BP13F136-CASHTRAN.                                             
           ADD 1                         TO WS-F136-WRITE-PP.                   
                                                                                
       4850-EXIT.                                                               
           EXIT.                                                                
      **************************************************************            
       4900-OUTPUT-INS70.                                                       
      **************************************************************            
                                                                                
      ** ON LOAN-BAL AMOUNT                                                     
      **                                                                        
           MOVE SPACES                   TO BP13F136-CASHTRAN.                  
           INITIALIZE                       BP13F136-CASHTRAN.                  
                                                                                
           MOVE '70'                     TO F136-CDE-TRANS-TYPE.                
           MOVE 'INS'                    TO F136-CDE-PAYMENT-TYPE.              
           MOVE WS-SYSTEM-DATE           TO F136-DTE-POST                       
                                            F136-DTE-TRANS.                     
           IF KD00-AMT-LOAN-BAL >= 0                                            
              MOVE 'C'                   TO F136-CDE-CRDR                       
           ELSE                                                                 
              MOVE 'D'                   TO F136-CDE-CRDR                       
           END-IF                                                               
                                                                                
           MOVE KD00-CDE-LOAN-SCH        TO F136-NUM-LOAN-SCH.                  
           MOVE F400-NUM-REGN            TO F136-NUM-REGN.                      
           MOVE F400-NUM-SCH-ACC         TO F136-NUM-SCH-ACCT.                  
           COMPUTE F136-AMT-RECEIPT = KD00-AMT-LOAN-BAL.                        
                                                                                
           WRITE BP13F136-CASHTRAN.                                             
           ADD 1                         TO WS-F136-WRITE-INS.                  
                                                                                
       4900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-BROWSE-BP13KD38.                                                    
      ******************************************************************        
                                                                                
           MOVE LOW-VALUES               TO KD38-KEY-FLD                        
           MOVE F400-NUM-REGN            TO KD38-NUM-REGN                       
           MOVE 'DUE'                    TO KD38-NUM-TRANS.                     
           MOVE 'N'                      TO WS-EOF-KD38                         
                                            WS-KD38-FND.                        
           MOVE ZEROES                   TO WS-I  WS-CNT.                       
           MOVE SPACES                   TO WS-BP13KD38-REC.                    
                                                                                
           START BP13KD38 KEY >= KD38-KEY-FLD.                                  
                                                                                
           EVALUATE WS-KD38-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 PERFORM 5100-READNEXT-BP13KD38 THRU 5100-EXIT                  
                   UNTIL KD38-NUM-REGN NOT = F400-NUM-REGN                      
                      OR KD38-NUM-TRANS NOT = 'DUE'                             
                      OR WS-EOF-KD38 = 'Y'                                      
                                                                                
                 IF WS-KD38-FND NOT = 'Y'                                       
                    MOVE SPACES TO WS-FILENAME WS-KEY-FLD WS-KEY-VALUE          
                    MOVE 'BP13KD38'      TO WS-FILENAME                         
                    MOVE 'REGN'          TO WS-KEY-FLD                          
                    MOVE F400-NUM-REGN   TO WS-KEY-VALUE                        
                    PERFORM 7000-WRITE-REPORT THRU 7000-EXIT                    
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'BP13KD38 - ERROR READING : '                          
                         WS-KD38-STATUS                                         
                 MOVE WS-KD38-STATUS     TO RETURN-CODE                         
                 GO TO 9999-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5100-READNEXT-BP13KD38.                                                  
      ******************************************************************        
      * WS-SYSTEM-DATE = F001-DTE-CURRENT                                       
                                                                                
           READ BP13KD38 NEXT                                                   
             AT END MOVE 'Y' TO WS-EOF-KD38.                                    
                                                                                
           EVALUATE WS-KD38-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 IF KD38-NUM-REGN = F400-NUM-REGN AND                           
                    KD38-NUM-TRANS = 'DUE'                                      
                    IF KD38-DTE-DUE <= WS-SYSTEM-DATE AND                       
                       KD38-DTE-INSTAL-NEW > WS-SYSTEM-DATE                     
                       MOVE 'Y'                   TO WS-KD38-FND                
                       ADD 1                      TO WS-CNT                     
                       MOVE BP13KD38-TRANS-REC    TO WS-KD38-REC(WS-CNT)        
                    END-IF                                                      
                 ELSE                                                           
                    MOVE 'Y'                      TO WS-EOF-KD38                
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
                 MOVE 'Y'                         TO WS-EOF-KD38                
              WHEN OTHER                                                        
                 DISPLAY '**BP13KD38 - ERROR READING : '                        
                         WS-KD38-STATUS                                         
                 MOVE WS-KD38-STATUS              TO RETURN-CODE                
                 GO TO 9999-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5200-WRITE-BP13FD38.                                                     
      ******************************************************************        
      * IF MORE THAN 1 REC DUE IN SAME MONTH , GET THE LATEST ONE               
                                                                                
           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > WS-CNT                 
              IF WS-KD38-SKIP(WS-I) NOT = 'Y'                                   
                 COMPUTE WS-TMP = WS-I + 1                                      
                 PERFORM VARYING WS-J FROM WS-TMP BY 1                          
                   UNTIL WS-J > WS-CNT                                          
                    IF WS-KD38-DUE(WS-I)(1:6) = WS-KD38-DUE(WS-J)(1:6)          
                       IF WS-KD38-DUE(WS-J) > WS-KD38-DUE(WS-I)                 
                          MOVE 'Y'                TO WS-KD38-SKIP(WS-I)         
                       ELSE                                                     
                          MOVE 'Y'                TO WS-KD38-SKIP(WS-J)         
                       END-IF                                                   
                    END-IF                                                      
                 END-PERFORM                                                    
                 IF WS-KD38-SKIP(WS-I) NOT = 'Y'                                
                    WRITE BP13FD38-TRANS-REC    FROM WS-KD38-REC(WS-I)          
                    ADD 1                         TO WS-FD38-WRITE              
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5300-READ-BP13KD60.                                                      
      ******************************************************************        
                                                                                
           MOVE KD00-NUM-REGN                     TO KD60-NUM-REGN.             
           READ BP13KD60.                                                       
                                                                                
           IF WS-KD60-STATUS = 00 OR 02                                         
              ADD  1                              TO WS-KD60-READ               
              PERFORM 6100-OUTPUT-BP13FD60-KD60 THRU 6100-EXIT                  
           ELSE                                                                 
              IF WS-KD60-STATUS = 23                                            
                 MOVE SPACES TO WS-FILENAME WS-KEY-FLD WS-KEY-VALUE             
                 MOVE 'BP13KD60'         TO WS-FILENAME                         
                 MOVE 'REGN'             TO WS-KEY-FLD                          
                 MOVE F400-NUM-REGN      TO WS-KEY-VALUE                        
                 PERFORM 7000-WRITE-REPORT THRU 7000-EXIT                       
              ELSE                                                              
                 DISPLAY 'BP13KD60 - ERROR READING : ' WS-KD60-STATUS           
                 MOVE WS-KD60-STATUS              TO RETURN-CODE                
                 GO TO 9999-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5400-START-BP13KD61.                                                     
      ******************************************************************        
                                                                                
           MOVE LOW-VALUES                        TO KD61-KEY-FLD.              
           MOVE KD00-NUM-REGN                     TO KD61-NUM-REGN.             
           MOVE 'N'                               TO WS-EOF-KD61                
                                                     WS-KD61-FND.               
                                                                                
           START BP13KD61 KEY >= KD61-KEY-FLD.                                  
                                                                                
           EVALUATE WS-KD61-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 PERFORM 5500-READNEXT-BP13KD61 THRU 5500-EXIT                  
                   UNTIL KD61-NUM-REGN NOT = KD00-NUM-REGN                      
                      OR WS-EOF-KD61 = 'Y'                                      
                                                                                
                 IF WS-KD61-FND NOT = 'Y'                                       
                    MOVE SPACES TO WS-FILENAME WS-KEY-FLD WS-KEY-VALUE          
                    MOVE 'BP13KD61'      TO WS-FILENAME                         
                    MOVE 'REGN'          TO WS-KEY-FLD                          
                    MOVE F400-NUM-REGN   TO WS-KEY-VALUE                        
                    PERFORM 7000-WRITE-REPORT THRU 7000-EXIT                    
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'BP13KD61 - ERROR READING : '                          
                         WS-KD61-STATUS                                         
                 MOVE WS-KD61-STATUS     TO RETURN-CODE                         
                 GO TO 9999-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       5400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5500-READNEXT-BP13KD61.                                                  
      ******************************************************************        
                                                                                
           READ BP13KD61 NEXT                                                   
             AT END MOVE 'Y' TO WS-EOF-KD61.                                    
                                                                                
           EVALUATE WS-KD61-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 IF KD61-NUM-REGN = KD00-NUM-REGN                               
                    ADD 1                         TO WS-KD61-READ               
                    MOVE 'Y'                      TO WS-KD61-FND                
                    PERFORM 6200-OUTPUT-BP13FD60-KD61  THRU 6200-EXIT           
                 ELSE                                                           
                    MOVE 'Y'                      TO WS-EOF-KD61                
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
                 MOVE 'Y'                         TO WS-EOF-KD61                
              WHEN OTHER                                                        
                 DISPLAY '**BP13KD61 - ERROR READING : '                        
                         WS-KD61-STATUS                                         
                 MOVE WS-KD61-STATUS              TO RETURN-CODE                
                 GO TO 9999-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6000-OUTPUT-BP13FD63.                                                    
      ******************************************************************        
                                                                                
           MOVE BP13KD63-REC                   TO BP13FD63-REC.                 
           WRITE BP13FD63-REC                                                   
           ADD 1                               TO WS-FD63-WRITE.                
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6100-OUTPUT-BP13FD60-KD60.                                               
      ******************************************************************        
                                                                                
           MOVE BP13KD60-REC                   TO BP13FD60-REC.                 
           WRITE BP13FD60-REC                                                   
           ADD 1                               TO WS-FD60-WRITE-KD60.           
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6200-OUTPUT-BP13FD60-KD61.                                               
      ******************************************************************        
                                                                                
           MOVE BP13KD61-REC                   TO BP13FD60-REC.                 
           WRITE BP13FD60-REC                                                   
           ADD 1                               TO WS-FD60-WRITE-KD61.           
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7000-WRITE-REPORT.                                                       
      ******************************************************************        
                                                                                
           IF WS-PAGENO = ZEROES OR WS-LINE-CNT > 54                            
              PERFORM 7100-WRITE-HEADER  THRU 7100-EXIT                         
           END-IF.                                                              
                                                                                
           ADD 1                      TO WS-REC-CNT.                            
           ADD 1                      TO WS-LINE-CNT.                           
           MOVE WS-REC-CNT            TO LD42-SN                                
           MOVE WS-FILENAME           TO LD42-FILENAME                          
           MOVE WS-KEY-FLD            TO LD42-KEY-FLD                           
           MOVE WS-KEY-VALUE          TO LD42-KEY-VALUE.                        
                                                                                
           WRITE BP13LD42-REC       FROM LD42-DETAIL.                           
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7100-WRITE-HEADER.                                                       
      ******************************************************************        
                                                                                
           ADD   1                    TO WS-PAGENO.                             
           MOVE WS-PAGENO             TO LD42-PAGE.                             
                                                                                
           WRITE BP13LD42-REC       FROM LD42-HEADER1   AFTER PAGE.             
           WRITE BP13LD42-REC       FROM LD42-HEADER2   AFTER 1.                
           WRITE BP13LD42-REC       FROM LD42-HEADER3   AFTER 2.                
           WRITE BP13LD42-REC       FROM LD42-HEADER4   AFTER 1.                
                                                                                
           MOVE   5                   TO WS-LINE-CNT.                           
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CD42'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F400 RECORDS READ............. : '            
                    WS-F400-READ.                                               
           DISPLAY '(2) NO OF BP13KD00 RECORDS FOUND............ : '            
                    WS-KD00-FOUND.                                              
           DISPLAY '(3) NO OF BP13KD00 RECORDS DELETED.......... : '            
                    WS-KD00-DELETE.                                             
           DISPLAY '(4) NO OF BP13KD00 RECORDS NOT FOUND........ : '            
                    WS-KD00-NOTFND.                                             
           DISPLAY '(5) NO OF BP13KD03 RECORDS WRITTEN.......... : '            
                    WS-KD03-WRITE.                                              
           DISPLAY '(6) NO OF BP13KD63 RECORDS FOUND............ : '            
                    WS-KD63-FOUND.                                              
           DISPLAY '(7) NO OF BP13KD63 RECORDS DELETED.......... : '            
                    WS-KD63-DELETE.                                             
           DISPLAY '(8) NO OF BP13KD63 RECORDS NOT FOUND........ : '            
                    WS-KD63-NOTFND.                                             
           DISPLAY '(9) NO OF BP13KD64 RECORDS WRITTEN.......... : '            
                    WS-KD64-WRITE.                                              
           DISPLAY '(10)NO OF BP13F440 RECORDS WRITTEN..............  :'        
                    WS-F440-WRITE.                                              
           DISPLAY '(11)NO OF BP13F136 RECORDS WRITTEN(LON).........  :'        
                    WS-F136-WRITE-LON.                                          
           DISPLAY '(12)NO OF BP13F136 RECORDS WRITTEN(PP)..........  :'        
                    WS-F136-WRITE-PP.                                           
           DISPLAY '(13)NO OF BP13KD00 RECORDS SKIPPED(AMT-LOAN-BAL=0):'        
                    WS-SKIP-LOAN-BAL.                                           
           DISPLAY '(14)NO OF BP13FD38 RECORDS WRITTEN..............  :'        
                    WS-FD38-WRITE.                                              
           DISPLAY '(15)NO OF BP13KD60 RECORDS FOUND................  :'        
                    WS-KD60-READ.                                               
           DISPLAY '(16)NO OF BP13KD61 RECORDS FOUND................  :'        
                    WS-KD61-READ.                                               
           DISPLAY '(17)NO OF BP13FD60 RECORDS WRITTEN FROM BP13KD60  :'        
                    WS-FD60-WRITE-KD60.                                         
           DISPLAY '(18)NO OF BP13FD60 RECORDS WRITTEN FROM BP13KD61  :'        
                    WS-FD60-WRITE-KD61.                                         
           DISPLAY '(19)NO OF BP13FD63 RECORDS WRITTEN .............  :'        
                    WS-FD63-WRITE.                                              
           DISPLAY 'CHECK (2) = (3) = (5) '.                                    
           DISPLAY 'CHECK (1) = (2) + (4) '.                                    
           DISPLAY 'CHECK (6) = (7) = (9)'.                                     
                                                                                
           CLOSE SY02F001                                                       
                 BP13F400                                                       
                 BP13KD00                                                       
                 BP13KD03                                                       
                 BP13KD38                                                       
                 BP13KD61                                                       
                 BP13KD60                                                       
                 BP13KD63                                                       
                 BP13KD64                                                       
                 BP13F136                                                       
                 BP13F440                                                       
                 BP13FD60                                                       
                 BP13FD63                                                       
                 BP13FD38                                                       
                 BP13LD42.                                                      
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD00 - ERROR CLOSING : '                             
                      WS-KD00-STATUS                                            
              MOVE WS-KD00-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KD03-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD03 - ERROR CLOSING : '                             
                      WS-KD03-STATUS                                            
              MOVE WS-KD03-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KD38-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD38 - ERROR CLOSING : '                             
                      WS-KD38-STATUS                                            
              MOVE WS-KD38-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KD63-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD63 - ERROR CLOSING : '                             
                      WS-KD63-STATUS                                            
              MOVE WS-KD63-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KD64-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD64 - ERROR CLOSING : '                             
                      WS-KD64-STATUS                                            
              MOVE WS-KD64-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KD61-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD61 - ERROR CLOSING : '                             
                      WS-KD61-STATUS                                            
              MOVE WS-KD61-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KD60-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD60 - ERROR CLOSING : '                             
                      WS-KD60-STATUS                                            
              MOVE WS-KD61-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
