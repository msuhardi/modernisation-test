       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC67.                                                 
       AUTHOR.        BALA AMUTHAN.                                             
       DATE-WRITTEN.  22/02/2007.                                               
      *****************************************************************         
      * OBJECTIVE  - CREATE FILE WITH TOTAL LOAN AMOUNT FOR                     
      *              PARTICULAR CASE.                                           
      *                                                                         
      * SUBROUTINE    - NIL                                                     
      *                                                                         
      *----------------------------------------------------------------         
      * REF NO   DATE    BY   AMENDMENTS/ENHANCEMENTS                           
      * -------  ----    ---  -----------------------                           
      * BP132934 220207  BA2  NEW PRG.                                          
      *          270207  BA3  CHANGE DATE CHECKING.                             
      *          280207  BA3  INCLUDE REPORT.                                   
      *****************************************************************         
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT  BP13FD30  ASSIGN  TO   BP13FD30.                             
                                                                                
           SELECT  SY02F001  ASSIGN  TO   SY02F001.                             
                                                                                
           SELECT  P13FD30A  ASSIGN  TO   P13FD30A.                             
                                                                                
           SELECT  BP13LC67  ASSIGN  TO   BP13LC67.                             
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13FD30                     BLOCK CONTAINS 0 RECORDS                
                                        RECORD CONTAINS 2000 CHARACTERS         
                                        LABEL RECORDS ARE STANDARD              
                                        RECORDING MODE IS F.                    
       COPY BP13FD30.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  P13FD30A                                                             
                                        BLOCK  CONTAINS    0 RECORDS            
                                        RECORD CONTAINS 2000 CHARACTERS         
                                        LABEL RECORDS ARE STANDARD              
                                        RECORDING MODE IS F.                    
                                                                                
       01  P13FD30A-REC.                                                        
           05  WS-FD30-KEY-FLD.                                                 
               10  WS-FD30-NUM-REGN                  PIC X(8).                  
           05  FILLER                                PIC X(10).                 
           05  WS-FD30-NUM-SCH-ACC.                                             
               10  WS-FD30-NUM-SCH                   PIC X(4).                  
               10  WS-FD30-NUM-ACC                   PIC X(5).                  
               10  WS-FD30-NUM-LESSEE-CODE           PIC X(2).                  
           05  WS-FD30-AMT-CURR-SELLPR               PIC 9(8).                  
           05  WS-FD30-NUM-FLAT-TYPE                 PIC X(2).                  
           05  FILLER                                PIC X(20).                 
           05  WS-FD30-PP-SCHEDULE-TOT OCCURS  15 TIMES.                        
               10  WS-FD30-NUM-PP-SCHDL              PIC X(2).                  
               10  WS-FD30-NUM-PP-PERCENT            PIC X(2).                  
               10  WS-FD30-DTE-PP-DUE                PIC X(8).                  
               10  WS-FD30-AMT-PP-REQ                PIC S9(6)V99.              
               10  WS-FD30-AMT-PP-PAID               PIC S9(6)V99.              
               10  WS-FD30-AMT-PP-LOAN               PIC S9(6)V99.              
               10  WS-FD30-AMT-PP-INSTAL             PIC S9(6)V99.              
               10  WS-FD30-DTE-FIN-PROCESS           PIC X(8).                  
               10  WS-FD30-DTE-CHEQUE-SENT           PIC X(8).                  
               10  WS-FD30-NUM-PV-SYSTEM             PIC X(11).                 
               10  WS-FD30-NUM-PV-MANUAL             PIC X(11).                 
               10  FILLER                            PIC X(18).                 
           05  FILLER                                PIC X(406).                
           05  WS-FD30-PP-REBATE                     PIC X(02).                 
           05  WS-FD30-AMT-LOAN-TOT                  PIC S9(6)V99.              
           05  WS-FD30-DTE-UPDATE                    PIC X(8).                  
           05  WS-FD30-TME-UPDATE                    PIC X(9).                  
           05  WS-FD30-NUM-USERID                    PIC X(8).                  
                                                                                
       FD  BP13LC67                                                             
                                        RECORD CONTAINS 132 CHARACTERS          
                                        RECORDING MODE  IS  F                   
                                        DATA RECORD     IS  PRINT-REC.          
       01  PRINT-REC                    PIC X(132).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-SYSTEM-DATE-TIME.                                                 
           05  WS-SYS-DATE                  PIC X(08) VALUE SPACES.             
           05  WS-SYS-TIME                  PIC X(08) VALUE SPACES.             
           05  FILLER                       PIC X(05) VALUE SPACES.             
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-FD30                  PIC 9(7)  VALUE 0.                  
           05  WS-CNT-FD30A                 PIC 9(7)  VALUE 0.                  
           05  WS-SN                        PIC 9(05) VALUE ZEROES.             
           05  WS-LINE-CNT                  PIC 9(02) VALUE 60.                 
           05  WS-LC67-WRITE                PIC 9(05) VALUE ZEROES.             
           05  WS-PAGE-CNT                  PIC 9(05) VALUE ZEROES.             
                                                                                
       01  WS-MIS-VAR.                                                          
           05  WS-TOT-AMT-LOAN              PIC S9(9)V99 VALUE 0.               
           05  WS-PP                        PIC 9(2)     VALUE  0.              
           05  WS-CTR                       PIC 9(2)     VALUE  0.              
      *    05  WS-IDX                       PIC 9(2)     VALUE  0.              
           05  WS-FD30-DTE-PP-DUE-CHAR      PIC X(8).                           
           05  WS-FD30-DTE-DUE-NUM  REDEFINES WS-FD30-DTE-PP-DUE-CHAR           
                                            PIC 9(8).                           
           05  WS-FD30-DTE-CHQ-SENT-CHAR PIC X(8).                              
           05  WS-FD30-DTE-CHQ-SENT-NUM  REDEFINES                              
               WS-FD30-DTE-CHQ-SENT-CHAR    PIC 9(8).                           
                                                                                
       01  LC67-PR-HEAD-01.                                                     
           05  FILLER                  PIC X(8)  VALUE 'BP13LC67'.              
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(8)  VALUE 'HDBCAT 3'.              
           05  FILLER                  PIC X(24) VALUE SPACES.                  
           05  FILLER                  PIC X(20) VALUE                          
                                       'S Y S T E M   O F   '.                  
           05  FILLER                  PIC X(19) VALUE                          
                                       'C O M M I T M E N T'.                   
           05  FILLER                  PIC X(15) VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'DATE :'.                
           05  LC67-DATE               PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'PAGE : '.               
           05  LC67-PAGENO             PIC ZZZZ9 VALUE ZEROES.                  
                                                                                
       01  LC67-PR-HEAD-02.                                                     
           05  FILLER                  PIC X(41) VALUE SPACES.                  
           05  FILLER                  PIC X(58) VALUE                          
               ' REPORT ON UNPAID LOAN ON THE PREVIOUS MONTH'.                  
                                                                                
       01  LC67-PR-HEAD-03.                                                     
           05  FILLER                  PIC X(26) VALUE SPACES.                  
           05  FILLER                  PIC X(5)  VALUE ' S/N '.                 
           05  FILLER                  PIC X(6)  VALUE SPACES.                  
           05  FILLER                  PIC X(8)  VALUE 'REG NO '.               
           05  FILLER                  PIC X(6)  VALUE SPACES.                  
           05  FILLER                  PIC X(3)  VALUE 'SCH'.                   
           05  FILLER                  PIC X(6)  VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE ' DUE-DATE '.            
           05  FILLER                  PIC X(5)  VALUE SPACES.                  
           05  FILLER                  PIC X(11) VALUE 'CHEQUE-SENT'.           
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE 'LOAN-AMOUT'.            
                                                                                
       01  LC67-PR-DETAILS.                                                     
           05  FILLER                  PIC X(26) VALUE SPACES.                  
           05  LC67-SN                 PIC ZZZZ9 VALUE ZEROES.                  
           05  FILLER                  PIC X(6)  VALUE SPACES.                  
           05  LC67-REG-NO             PIC X(8)  VALUE SPACES.                  
           05  FILLER                  PIC X(6)  VALUE SPACES.                  
           05  LC67-SCH                PIC X(3)  VALUE SPACES.                  
           05  FILLER                  PIC X(6)  VALUE SPACES.                  
           05  LC67-DUE-DATE           PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(5)  VALUE SPACES.                  
           05  LC67-CHEQUE-SENT        PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(5)  VALUE SPACES.                  
           05  LC67-LOAN-AMOUNT        PIC $,$$$,$$9.99                         
                                                 VALUE ZEROES.                  
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *----------------------------------------------------------------*        
       0000-MAIN-PARA.                                                          
      *----------------------------------------------------------------*        
                                                                                
           PERFORM 1000-START-UP         THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13FD30    THRU 2000-EXIT.                        
           PERFORM 3000-PROCESSING       THRU 3000-EXIT                         
                UNTIL FD30-NUM-REGN = HIGH-VALUES.                              
           PERFORM 9999-WRAP-UP.                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-START-UP.                                                           
      *----------------------------------------------------------------*        
                                                                                
           OPEN INPUT  BP13FD30                                                 
                       SY02F001                                                 
               OUTPUT  P13FD30A                                                 
                       BP13LC67.                                                
                                                                                
           READ SY02F001 AT END                                                 
                DISPLAY 'NO RECORDS FOUND AT SY02F001'.                         
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE-TIME.                   
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-READ-BP13FD30.                                                      
      *----------------------------------------------------------------*        
                                                                                
           READ BP13FD30 AT END                                                 
                MOVE HIGH-VALUES TO FD30-NUM-REGN                               
                GO TO 2000-EXIT.                                                
           ADD 1 TO WS-CNT-FD30.                                                
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       3000-PROCESSING.                                                         
      *----------------------------------------------------------------*        
           MOVE ZEROES TO WS-TOT-AMT-LOAN                                       
      *                   WS-IDX                                                
                          WS-CTR.                                               
                                                                                
           IF FD30-NUM-REGN NOT = SPACES AND LOW-VALUES                         
              MOVE  SPACES      TO   P13FD30A-REC                               
              INITIALIZE  P13FD30A-REC                                          
              PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 15              
                 PERFORM 3500-CHECK-DATE   THRU 3500-EXIT                       
                                                                                
              END-PERFORM                                                       
              IF WS-TOT-AMT-LOAN > 0                                            
                 PERFORM 4100-MOVE-MAIN-REC  THRU 4100-EXIT                     
                 PERFORM 5000-WRITE-FD30A    THRU 5000-EXIT                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FD30     THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
      *----------------------------------------------------------------*        
       3500-CHECK-DATE.                                                         
      *----------------------------------------------------------------*        
                                                                                
           IF FD30-AMT-PP-LOAN(WS-CTR) NOT NUMERIC                              
              MOVE ZEROES TO FD30-AMT-PP-LOAN(WS-CTR)                           
           END-IF.                                                              
                                                                                
           IF FD30-DTE-PP-DUE(WS-CTR) = SPACES OR LOW-VALUES                    
              OR FD30-AMT-PP-LOAN(WS-CTR) = 0                                   
                 GO TO 3500-EXIT.                                               
                                                                                
           MOVE FD30-DTE-PP-DUE(WS-CTR)     TO WS-FD30-DTE-PP-DUE-CHAR          
           MOVE FD30-DTE-CHEQUE-SENT(WS-CTR) TO                                 
                WS-FD30-DTE-CHQ-SENT-CHAR                                       
                                                                                
           IF (WS-FD30-DTE-DUE-NUM(1:6) < F001-DTE-CURRENT(1:6))                
                                                                                
              IF (FD30-DTE-CHEQUE-SENT(WS-CTR) = SPACES OR LOW-VALUES)          
                                                                                
                PERFORM 4000-MOVE-OCC-REC   THRU 4000-EXIT                      
                COMPUTE WS-TOT-AMT-LOAN = WS-TOT-AMT-LOAN +                     
                                          FD30-AMT-PP-LOAN(WS-CTR)              
                MOVE WS-CTR                 TO WS-PP                            
                                                                                
                PERFORM 6000-WRITE-LC67     THRU 6000-EXIT                      
                                                                                
              ELSE                                                              
              IF WS-FD30-DTE-CHQ-SENT-NUM(1:6) = F001-DTE-CURRENT(1:6)          
                IF  (WS-FD30-DTE-DUE-NUM(1:6) <                                 
                             WS-FD30-DTE-CHQ-SENT-NUM(1:6))                     
                                                                                
                  PERFORM 4000-MOVE-OCC-REC   THRU 4000-EXIT                    
                  COMPUTE WS-TOT-AMT-LOAN = WS-TOT-AMT-LOAN +                   
                                            FD30-AMT-PP-LOAN(WS-CTR)            
                  MOVE WS-CTR TO WS-PP                                          
                                                                                
                  PERFORM 6000-WRITE-LC67   THRU 6000-EXIT                      
                                                                                
                END-IF                                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3500-EXIT.  EXIT.                                                        
                                                                                
      *----------------------------------------------------------------*        
       4000-MOVE-OCC-REC.                                                       
      *----------------------------------------------------------------*        
                                                                                
      *    ADD +1               TO WS-IDX.                                      
           MOVE FD30-NUM-PP-SCHDL(WS-CTR)                                       
                                TO WS-FD30-NUM-PP-SCHDL(WS-CTR).                
           MOVE FD30-NUM-PP-PERCENT(WS-CTR)                                     
                                TO WS-FD30-NUM-PP-PERCENT(WS-CTR).              
           MOVE FD30-DTE-PP-DUE(WS-CTR)                                         
                                TO WS-FD30-DTE-PP-DUE(WS-CTR).                  
           MOVE FD30-AMT-PP-REQ(WS-CTR)                                         
                                TO WS-FD30-AMT-PP-REQ(WS-CTR).                  
           MOVE FD30-AMT-PP-PAID(WS-CTR)                                        
                                TO WS-FD30-AMT-PP-PAID(WS-CTR).                 
           MOVE FD30-AMT-PP-LOAN(WS-CTR)                                        
                                TO WS-FD30-AMT-PP-LOAN(WS-CTR).                 
           MOVE FD30-AMT-PP-INSTAL(WS-CTR)                                      
                                TO WS-FD30-AMT-PP-INSTAL(WS-CTR).               
           MOVE FD30-DTE-FIN-PROCESS(WS-CTR)                                    
                                TO WS-FD30-DTE-FIN-PROCESS(WS-CTR).             
           MOVE FD30-DTE-CHEQUE-SENT(WS-CTR)                                    
                                TO WS-FD30-DTE-CHEQUE-SENT(WS-CTR).             
           MOVE FD30-NUM-PV-SYSTEM(WS-CTR)                                      
                                TO WS-FD30-NUM-PV-SYSTEM(WS-CTR).               
           MOVE FD30-NUM-PV-MANUAL(WS-CTR)                                      
                                TO WS-FD30-NUM-PV-MANUAL(WS-CTR).               
                                                                                
       4000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       4100-MOVE-MAIN-REC.                                                      
      *----------------------------------------------------------------*        
           MOVE FD30-NUM-REGN           TO WS-FD30-NUM-REGN.                    
           MOVE FD30-NUM-SCH-ACC        TO WS-FD30-NUM-SCH-ACC.                 
           MOVE FD30-AMT-CURR-SELLPR    TO WS-FD30-AMT-CURR-SELLPR.             
           MOVE FD30-NUM-FLAT-TYPE      TO WS-FD30-NUM-FLAT-TYPE.               
                                                                                
           MOVE WS-TOT-AMT-LOAN         TO WS-FD30-AMT-LOAN-TOT.                
           MOVE WS-PP                   TO WS-FD30-PP-REBATE.                   
           MOVE WS-SYS-DATE             TO WS-FD30-DTE-UPDATE.                  
           MOVE WS-SYS-TIME             TO WS-FD30-TME-UPDATE.                  
           MOVE 'BP13CC67'              TO WS-FD30-NUM-USERID.                  
                                                                                
       4100-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       5000-WRITE-FD30A.                                                        
      *----------------------------------------------------------------*        
                                                                                
              WRITE P13FD30A-REC.                                               
              ADD +1 TO WS-CNT-FD30A.                                           
                                                                                
       5000-EXIT.  EXIT.                                                        
                                                                                
      *----------------------------------------------------------------*        
       6000-WRITE-LC67.                                                         
      *----------------------------------------------------------------*        
                                                                                
           MOVE SPACES                    TO LC67-PR-DETAILS.                   
                                                                                
           IF WS-LINE-CNT > 50                                                  
              PERFORM 6500-WRITE-HEADING  THRU 6500-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE FD30-NUM-REGN          TO LC67-REG-NO.                          
           MOVE WS-CTR                 TO LC67-SCH.                             
                                                                                
           STRING FD30-DTE-PP-DUE(WS-CTR)(7:2)  '/'                             
                  FD30-DTE-PP-DUE(WS-CTR)(5:2)  '/'                             
                  FD30-DTE-PP-DUE(WS-CTR)(1:4)                                  
                  DELIMITED BY SIZE INTO LC67-DUE-DATE.                         
                                                                                
           IF FD30-DTE-CHEQUE-SENT(WS-CTR) NOT = SPACES AND LOW-VALUES          
              STRING FD30-DTE-CHEQUE-SENT(WS-CTR)(7:2)  '/'                     
                     FD30-DTE-CHEQUE-SENT(WS-CTR)(5:2)  '/'                     
                     FD30-DTE-CHEQUE-SENT(WS-CTR)(1:4)                          
                     DELIMITED BY SIZE INTO LC67-CHEQUE-SENT                    
           END-IF.                                                              
                                                                                
           MOVE FD30-AMT-PP-LOAN(WS-CTR)  TO LC67-LOAN-AMOUNT.                  
                                                                                
           ADD 1                          TO WS-SN                              
                                             WS-LINE-CNT                        
                                             WS-LC67-WRITE.                     
                                                                                
           MOVE WS-SN                     TO LC67-SN.                           
                                                                                
           WRITE PRINT-REC                FROM LC67-PR-DETAILS AFTER 1.         
                                                                                
       6000-EXIT.  EXIT.                                                        
                                                                                
      *----------------------------------------------------------------*        
       6500-WRITE-HEADING.                                                      
      *----------------------------------------------------------------*        
                                                                                
           ADD 1                          TO WS-PAGE-CNT.                       
           MOVE WS-PAGE-CNT               TO LC67-PAGENO.                       
                                                                                
           MOVE  SPACES                   TO PRINT-REC.                         
           WRITE PRINT-REC                      AFTER PAGE.                     
           WRITE PRINT-REC FROM LC67-PR-HEAD-01 AFTER 3.                        
           WRITE PRINT-REC FROM LC67-PR-HEAD-02 AFTER 1.                        
           WRITE PRINT-REC FROM LC67-PR-HEAD-03 AFTER 2.                        
           MOVE 7                         TO WS-LINE-CNT.                       
                                                                                
       6500-EXIT.  EXIT.                                                        
                                                                                
      *----------------------------------------------------------------*        
      *CLOSING FILES & END OF PROGRAME                                 *        
      *----------------------------------------------------------------*        
                                                                                
      *----------------------------------------------------------------*        
       9999-WRAP-UP.                                                            
      *----------------------------------------------------------------*        
                                                                                
           DISPLAY  SPACES.                                                     
           DISPLAY '******* CONTROL   TOTALS *******'.                          
           DISPLAY 'PROGRAM-ID : BP13CC67'.                                     
           DISPLAY  SPACES.                                                     
                                                                                
           DISPLAY 'NUMBER OF BP13FD30 RECORDS READ     : '                     
                    WS-CNT-FD30.                                                
           DISPLAY 'NUMBER OF P13FD30A RECORDS WRITTEN  : '                     
                    WS-CNT-FD30A.                                               
           DISPLAY 'NUMBER OF BP13LC67 RECORDS WRITTEN  : '                     
                    WS-LC67-WRITE.                                              
                                                                                
           CLOSE BP13FD30                                                       
                 SY02F001                                                       
                 P13FD30A                                                       
                 BP13LC67.                                                      
                                                                                
           STOP RUN.                                                            
