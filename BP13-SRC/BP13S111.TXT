  *================================================================*            
  * SAS PROGRAM TO PRINT MONTHLY REPORT FOR TRANSACTION ON         *            
  * REGN FEE, BOOKING FEE, COMMITMENT DEPOSIT FOR THE MONTH.       *            
  *================================================================*;           
  *  CHG-NO  BY   DATE   DESC                                      *            
  * -------- --- ------  ----                                      *            
  * BP130015 SSS 130995  CONVERT TO LASER PRINT REPORT             *            
  *                      CHG REPORT-ID POSITION TO COL 1           *            
  * BP130418 RHB 250898  TO CATER FOR Y2000.                       *            
  * BP131039 SN  200701  CATER FOR FLT TYPE '1A' OR '2A'           *            
  * BP132341 MCC 030603  FORMAT POSTDTE                            *            
  * BP136224 KSJ 030616  CATER FOR LBS (VALID 'L')                 *            
  *================================================================*;           
  OPTIONS  NODATE NONUMBER;                                                     
                                                                                
     PROC FORMAT;                                                               
        PICTURE DOL LOW-<0 = '000,000,009.99'                                   
                             (PREFIX='-$' MULT=100.00000000001)                 
                    0-HIGH = '000,000,009.99'                                   
                             (PREFIX='$' MULT=100.00000000001);                 
                                                                                
  DATA BP13F127 ;                                                               
     INFILE BP13F127;                                                           
     INPUT  @01  TRANCDE  $CHAR2.      @05  ORIGNO   $CHAR8.                    
            @13  POSTDTE  YYMMDD8.     @25  NRIC     $CHAR9.                    
            @34  NAME     $CHAR32.     @66  RECPT    $CHAR14.                   
            @75  DOCNO    $CHAR5.      @81  AMTRE    ZD9.2                      
            @90  CRDR     $CHAR1.      @91  TRANDTE  YYMMDD8.                   
            @99  PAYTYPE  $CHAR3.      @114 VALID    $CHAR1.                    
            @118 FT       $CHAR2.;                                              
                                                                                
     FORMAT AMTRE DOL.;                                                         
                                                                                
     IF TRANCDE=11;                                                             
                                                                                
     PROC SORT; BY PAYTYPE TRANDTE VALID;                                       
                                                                                
     DATA _NULLS_;                                                              
     SET BP13F127 END=FINAL;                                                    
     BY PAYTYPE TRANDTE;                                                        
     OPTIONS PAGESIZE=55 LS=132 NODATE;                                         
     FILE BP13L111   HEADER=HH NOTITLES;                                        
                                                                                
     SUBAMT+AMTRE;                                                              
     TOTAMT+AMTRE;                                                              
     GRDTOT+AMTRE;                                                              
     N+1;                                                                       
     M+1;                                                                       
                                                                                
     IF FIRST.PAYTYPE THEN PUT _PAGE_;                                          
                                                                                
     L+1;                                                                       
     LENGTH STAT $1;                                                            
     IF VALID = 'V' THEN STAT = '*';                                            
     PUT @20 STAT  @22  TRANDTE  DDMMYY10.     @33  ORIGNO                      
         @46  NAME                  @83  RECPT                                  
         @100 AMTRE DOL.;                                                       
                                                                                
     IF L=50 THEN DO;                                                           
        PUT _PAGE_;                                                             
        L=0;                                                                    
        END;                                                                    
                                                                                
     IF LAST.TRANDTE THEN DO;                                                   
        PUT //@66 'TOTAL FOR ' TRANDTE DDMMYY10.                                
        @100 SUBAMT DOL.//;                                                     
        SUBAMT=0;                                                               
        END;                                                                    
                                                                                
     IF LAST.PAYTYPE THEN DO;                                                   
        PUT @20 'NUMBER OF CASES = ' @40 N                                      
        @66 'TOTAL FOR TRANSACTION ' PAYTYPE @100 TOTAMT DOL.;                  
        TOTAMT=0;                                                               
        N=0;                                                                    
        END;                                                                    
                                                                                
     IF FINAL THEN DO;                                                          
        PUT //@20 'TOTAL CASES = ' @40 M                                        
        @66 'GRAND-TOTAL ' @100 GRDTOT DOL.;                                    
        END;                                                                    
     RETURN;                                                                    
  HH:                                                                           
     PAGE + 1;                                                                  
     CURRENT =DATE();                                                           
     YR = YEAR(POSTDTE);                                                        
     IF FT = '1A' OR FT = '2A' OR VALID = 'L' THEN                              
        DO;                                                                     
          PUT @1   'BP13L111' @ 15 'HDBCAT3'                                    
              @47  'S Y S T E M   O F   C O M M I T M E N T'                    
              @98  'DATE :' @105 CURRENT DDMMYY10.                              
              @119 'PAGE :' @126 PAGE 3./;                                      
          PUT @45  'STUDIO APARTMENT CASES RECEIPTS FOR'                        
              @82  POSTDTE WORDDATE9. @92 YR//;                                 
        END;                                                                    
     ELSE                                                                       
        DO;                                                                     
          PUT @1   'BP13L111-A' @ 15 'HDBCAT3'                                  
              @47  'S Y S T E M   O F   C O M M I T M E N T'                    
              @98  'DATE :' @105 CURRENT DDMMYY10.                              
              @119 'PAGE :' @126 PAGE 3./;                                      
          PUT @53  'RECEIPTS FOR'                                               
              @69  POSTDTE WORDDATE9. @90 YR//;                                 
        END;                                                                    
     PUT @1   'TRANSACTION TYPE : 'PAYTYPE//;                                   
     PUT @24  'DATE'                                                            
         @33  'REGN-NO    '                                                     
         @62  'NAME'                                                            
         @83  'RECEIPT NO'                                                      
         @104 'AMOUNT';                                                         
     PUT @24  '----'                                                            
         @33  '-----------'                                                     
         @62  '----'                                                            
         @83  '----------'                                                      
         @104 '------';                                                         
     RETURN;                                                                    
