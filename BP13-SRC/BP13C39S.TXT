      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C39S.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  DECEMBER 26, 2017.                                        
      *===============================================================*         
      * OBJECTIVE  :  REPORT ON SERS CASES WITH SER ICP = 'Y'         *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F410                                     *         
      *               2. BP13K800                                     *         
      *               3. BP13K022                                     *         
      *               4. BP17K001                                     *         
      * OUTPUT FILE:  1. BP13L39S                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP137131 26/12/2017 PCL4 NEW PROGRAM.                         *         
      * BP137131 29/12/2017 PCL4 TO PRINT SERS CONTRA IP CHQ = 'Y'    *         
      * BP139259 23/08/2022 ZAR7 REPLACE chia meng with Yusuf MOHAMMED*         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F410  ASSIGN        TO BP13F410.                         
                                                                                
           SELECT  BP13K800  ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
                                                                                
           SELECT  BP17K120  ASSIGN        TO BP17K120                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K120-KEY-FLD                      
                             FILE STATUS   IS WS-K120-STATUS.                   
                                                                                
           SELECT  BP13K022  ASSIGN        TO BP13K022                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K022-KEY-FLD                      
                             ALTERNATE KEY IS K022-NUM-REGN                     
                             FILE STATUS   IS WS-K022-STATUS.                   
                                                                                
           SELECT  BP17K000  ASSIGN        TO BP17K000                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K000-KEY-FLD                      
                             ALTERNATE KEY IS K000-NUM-REGNO                    
                             FILE STATUS   IS WS-K000-STATUS.                   
                                                                                
           SELECT  MAILC39S  ASSIGN        TO MAILC39S.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F410                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F410.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP17K000                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP17K000.                                                           
                                                                                
       FD   BP17K120                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP17K120.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   MAILC39S                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS  100 CHARACTERS.                                    
       01   MAILC39S-REC               PIC X(100).                              
                                                                                
                                                                                
      ******************************************************************        
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F410             PIC X(1)  VALUE 'N'.                     
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K022-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K000-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K120-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                    PIC 9(1)  VALUE ZEROES.                  
           05  WS-F410-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
           05  WS-CNT-L39S-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(1)  VALUE 2.                       
           05  WS-MAX-LINE             PIC 9(2)  VALUE 55.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-NUM-REGN              PIC X(8)  VALUE SPACES.                  
           05 WS-SNO-ZZZZ9             PIC ZZZZ9 VALUE ZEROES.                  
           05 WS-FIRST-WRITE           PIC X(1)  VALUE 'Y'.                     
           05 WS-PRIOR-DTE             PIC 9(08) VALUE ZEROES.                  
           05 WS-NUM-DTE-APPT          PIC 9(07) VALUE ZEROES.                  
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-PRINT-DATE            PIC 9(8)  VALUE ZEROES.                  
                                                                                
       01  WS-DTE-REGN.                                                         
           05 WS-DTE-CCYY                   PIC 9(4).                           
           05 WS-DTE-MM                     PIC 9(2).                           
           05 WS-DTE-DD                     PIC 9(2).                           
                                                                                
       01 WS-PRIO-DTE.                                                          
          05  WS-PRIO-CCYY                 PIC 9(04).                           
          05  WS-PRIO-MM                   PIC 9(02).                           
          05  WS-PRIO-DD                   PIC 9(02).                           
                                                                                
       01 WS-CURR-DATE.                                                         
          05  WS-CURR-CCYY                 PIC 9(04).                           
          05  WS-CURR-MM                   PIC 9(02).                           
          05  WS-CURR-DD                   PIC 9(02).                           
                                                                                
                                                                                
       01  WS-L39S-HEADER          PIC X(130)    VALUE SPACES.                  
                                                                                
       01  L39S-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13L39S'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  L39S-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L39S-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
                                                                                
       01  L39S-HEADER02.                                                       
           05  FILLER              PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(38)     VALUE                          
               'DAILY REPORT ON SERS CASES TP/SATP ON '.                        
           05  L39S-DATE-HDR2      PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(30)     VALUE                          
               ' WITH SER CONTRA IP-CHQ = Y'.                                   
                                                                                
       01  L39S-HEADER03.                                                       
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(05)    VALUE '  S/N'.                  
           05  FILLER              PIC X(01)    VALUE ' '.                      
           05  FILLER              PIC X(08)    VALUE 'REGN NO '.               
           05  FILLER              PIC X(01)    VALUE ' '.                      
           05  FILLER              PIC X(02)    VALUE 'LT'.                     
           05  FILLER              PIC X(01)    VALUE ' '.                      
           05  FILLER              PIC X(05)    VALUE 'CRM'.                    
           05  FILLER              PIC X(01)    VALUE ' '.                      
           05  FILLER              PIC X(12)    VALUE 'NEW SCH ACCT'.           
           05  FILLER              PIC X(01)    VALUE ' '.                      
           05  FILLER              PIC X(12)    VALUE 'SER SCH ACCT'.           
           05  FILLER              PIC X(01)    VALUE ' '.                      
           05  FILLER              PIC X(10)    VALUE 'IP AMT'.                 
           05  FILLER              PIC X(01)    VALUE ' '.                      
                                                                                
       01  L39S-DETAIL01.                                                       
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L39S-SN             PIC X(05)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L39S-REGN           PIC X(08)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L39S-LT             PIC X(02)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L39S-CRM            PIC X(05)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L39S-SCH-ACCT       PIC X(12)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L39S-SER-ACCT       PIC X(12)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L39S-IP-AMT         PIC ZZZ,ZZ9.99   VALUE ZEROES.               
                                                                                
       01  WS-REPORT-DTL.                                                       
           05  WS-EMPTY-RPT.                                                    
               10 FILLER                  PIC X(30) VALUE SPACES.               
               10 FILLER                  PIC X(21)                             
                                          VALUE '<   EMPTY  REPORT   >'.        
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT3             PIC X(50)                               
               VALUE 'RCPT TO:<CHOY_MIU_YENG@HDB.GOV.SG>'.                      
           05  WS-MAIL-RPT4             PIC X(50)                               
               VALUE 'RCPT TO:<Yusuf_MOHAMMED@hdb.gov.sg>'.                     
           05  WS-MAIL-RPT5             PIC X(50)                               
               VALUE 'RCPT TO:<SHAMSURIANOR_SIBENGAT@HDB.GOV.SG>'.              
           05  WS-MAIL-RPT6             PIC X(50)                               
               VALUE 'RCPT TO:<TAN_SIEW_LAY@HDB.GOV.SG>'.                       
           05  WS-MAIL-RPT9             PIC X(4)                                
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT10            PIC X(29)                               
               VALUE 'FROM:Soc System - Email Alert'.                           
           05  WS-MAIL-RPT11            PIC X(50)                               
               VALUE 'TO:<CHOY_MIU_YENG@HDB.GOV.SG>'.                           
           05  WS-MAIL-RPT12            PIC X(50)                               
               VALUE 'TO:<Yusuf_MOHAMMED@hdb.gov.sg>'.                          
           05  WS-MAIL-RPT13            PIC X(50)                               
               VALUE 'CC:<SHAMSURIANOR_SIBENGAT@HDB.GOV.SG>'.                   
           05  WS-MAIL-RPT14            PIC X(50)                               
               VALUE 'CC:<TAN_SIEW_LAY@HDB.GOV.SG>'.                            
           05  WS-MAIL-RPT17            PIC X(100)                              
               VALUE 'SUBJECT: SERS CASES ON TP/SATP'.                          
                                                                                
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F410      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F410 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F410                                                 
                       BP13K800                                                 
                       BP13K022                                                 
                       BP17K000                                                 
                       BP17K120                                                 
              OUTPUT   MAILC39S.                                                
                                                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 - ERROR OPENING : ' WS-K022-STATUS              
              MOVE WS-K022-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K120-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP17K120 - ERROR OPENING : ' WS-K120-STATUS              
              MOVE WS-K120-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K000-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP17K000 - ERROR OPENING : ' WS-K000-STATUS              
              MOVE WS-K000-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 7000-PRINT-HEADER       THRU 7000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F410.                                                      
      ******************************************************************        
                                                                                
           READ BP13F410           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F410                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F410-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       2100-READ-BP13K800.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP13K800-MASTER                     
                                            BP13K022-REC.                       
           INITIALIZE                       BP13K800-MASTER                     
                                            BP13K022-REC.                       
                                                                                
           MOVE F410-NUM-REGN            TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    PERFORM 2200-READ-BP13K022   THRU 2200-EXIT                 
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES                  TO BP13K800-MASTER             
                                                    BP13K022-REC                
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    DISPLAY 'K800 KEY: ' K800-NUM-REGN                          
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       2200-READ-BP13K022.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP13K022-REC.                       
           INITIALIZE                       BP13K022-REC.                       
                                                                                
           MOVE SPACES                   TO K022-KEY-FLD.                       
           MOVE F410-NUM-REGN            TO K022-NUM-REGN.                      
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K022 READ ERROR ' WS-K022-STATUS               
                    DISPLAY 'K022 KEY: ' K022-NUM-REGN                          
                    MOVE WS-K022-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       2300-READ-BP17K000.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP17K000-REC.                       
           INITIALIZE                       BP17K000-REC.                       
                                                                                
           MOVE SPACES                   TO K000-KEY-FLD.                       
           MOVE F410-NUM-REGN            TO K000-NUM-REGNO.                     
                                                                                
           READ BP17K000 KEY IS K000-NUM-REGNO.                                 
                                                                                
           EVALUATE WS-K000-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
               WHEN 10                                                          
                    PERFORM 2400-READ-BP17K120  THRU 2400-EXIT                  
                                                                                
               WHEN 23                                                          
                    MOVE SPACES                   TO BP17K120-REC               
                    INITIALIZE                       BP17K120-REC               
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP17K000 READ ERROR ' WS-K000-STATUS               
                    DISPLAY 'K000 KEY: ' K000-NUM-REGNO                         
                    MOVE WS-K000-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       2400-READ-BP17K120.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP17K120-REC.                       
           INITIALIZE                       BP17K120-REC.                       
                                                                                
           MOVE K000-NUM-SCH-ACC         TO K120-NUM-SCH-ACC.                   
                                                                                
           READ BP17K120.                                                       
                                                                                
           EVALUATE WS-K120-STATUS                                              
               WHEN 00                                                          
                                                                                
                    IF K120-AMT-INTERIM-COMP NOT NUMERIC                        
                       MOVE ZEROES       TO K120-AMT-INTERIM-COMP               
                    END-IF                                                      
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE ZEROES          TO K120-AMT-INTERIM-COMP               
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP17K120 READ ERROR ' WS-K120-STATUS               
                    DISPLAY 'K120 KEY: ' K120-NUM-SCH-ACC                       
                    MOVE WS-K120-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2400-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF F410-NUM-SER-CHQ = 'Y'                                            
              PERFORM 2100-READ-BP13K800        THRU 2100-EXIT                  
              PERFORM 2300-READ-BP17K000        THRU 2300-EXIT                  
              PERFORM 4000-PROCESS-RECORDS      THRU 4000-EXIT                  
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F410           THRU 2000-EXIT.                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE SPACES                    TO L39S-DETAIL01                      
           INITIALIZE                        L39S-DETAIL01.                     
                                                                                
           ADD 1                          TO WS-SNO.                            
           MOVE WS-SNO                    TO WS-SNO-ZZZZ9.                      
           MOVE WS-SNO-ZZZZ9              TO L39S-SN.                           
           MOVE F410-NUM-REGN             TO L39S-REGN.                         
                                                                                
           MOVE K800-NUM-LOAN-TAG         TO L39S-LT.                           
           MOVE K022-NUM-OIC              TO L39S-CRM.                          
           MOVE F410-NUM-SCH-ACC          TO L39S-SCH-ACCT.                     
           MOVE K120-NUM-SCH-ACC          TO L39S-SER-ACCT.                     
                                                                                
           IF K120-AMT-INTERIM-COMP NOT NUMERIC                                 
              MOVE ZEROES                 TO K120-AMT-INTERIM-COMP              
           END-IF.                                                              
                                                                                
           MOVE K120-AMT-INTERIM-COMP     TO L39S-IP-AMT.                       
                                                                                
           PERFORM 5000-PRINT-REPORT    THRU 5000-EXIT.                         
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-PRINT-REPORT.                                                       
      ******************************************************************        
                                                                                
           MOVE SPACES                        TO MAILC39S-REC.                  
           INITIALIZE                            MAILC39S-REC.                  
                                                                                
           WRITE MAILC39S-REC               FROM L39S-DETAIL01.                 
           ADD 1                              TO WS-CNT-L39S-WRITE.             
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7000-PRINT-HEADER.                                                       
      ******************************************************************        
                                                                                
           PERFORM 7100-HEADER-EMAIL         THRU 7100-EXIT.                    
           STRING WS-PRIO-DTE(7:2) '/'                                          
                  WS-PRIO-DTE(5:2) '/'                                          
                  WS-PRIO-DTE(1:4)                                              
                  DELIMITED BY SIZE INTO L39S-DATE-HDR.                         
                                                                                
           MOVE L39S-DATE-HDR                  TO L39S-DATE-HDR2.               
                                                                                
                                                                                
           ADD 1                               TO WS-PAGE-CTR.                  
           MOVE ZEROES                         TO L39S-PAGE-HDR.                
           MOVE WS-PAGE-CTR                    TO L39S-PAGE-HDR.                
                                                                                
           MOVE SPACES             TO WS-L39S-HEADER.                           
           WRITE MAILC39S-REC    FROM WS-L39S-HEADER.                           
                                                                                
           WRITE MAILC39S-REC    FROM L39S-HEADER01 AFTER PAGE.                 
           WRITE MAILC39S-REC    FROM L39S-HEADER02.                            
           WRITE MAILC39S-REC    FROM L39S-HEADER03 AFTER 2.                    
                                                                                
                                                                                
           INITIALIZE WS-LINE-CTR.                                              
           MOVE 5  TO WS-LINE-CTR.                                              
                                                                                
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7100-HEADER-EMAIL.                                                       
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)     TO WS-SYSTEM-DATE                
                                                  WS-CURR-DATE.                 
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)     TO WS-PRIO-DTE.                  
                                                                                
            WRITE MAILC39S-REC FROM WS-MAIL-RPT1.                               
            WRITE MAILC39S-REC FROM WS-MAIL-RPT2.                               
            WRITE MAILC39S-REC FROM WS-MAIL-RPT3.                               
            WRITE MAILC39S-REC FROM WS-MAIL-RPT4.                               
            WRITE MAILC39S-REC FROM WS-MAIL-RPT5.                               
            WRITE MAILC39S-REC FROM WS-MAIL-RPT6.                               
                                                                                
            WRITE MAILC39S-REC FROM WS-MAIL-RPT9.                               
            WRITE MAILC39S-REC FROM WS-MAIL-RPT10.                              
            WRITE MAILC39S-REC FROM WS-MAIL-RPT11.                              
            WRITE MAILC39S-REC FROM WS-MAIL-RPT12.                              
            WRITE MAILC39S-REC FROM WS-MAIL-RPT13.                              
            WRITE MAILC39S-REC FROM WS-MAIL-RPT14.                              
                                                                                
            STRING 'SUBJECT: SERS CASES ON TP/SATP WITH '                       
                   'CONTRA IP CHQ = Y'                                          
              DELIMITED BY SIZE INTO WS-MAIL-RPT17.                             
                                                                                
            WRITE MAILC39S-REC FROM WS-MAIL-RPT17.                              
                                                                                
                                                                                
       7100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C39S'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F410 RECORDS READ............. : '            
                    WS-F410-READ.                                               
           DISPLAY '(2) NO OF BP13L39S RECORDS WRITTEN.......... : '            
                    WS-CNT-L39S-WRITE.                                          
                                                                                
           CLOSE    BP13F410                                                    
                    BP13K800                                                    
                    BP13K022                                                    
                    BP17K000                                                    
                    BP17K120                                                    
                    MAILC39S.                                                   
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 - ERROR CLOSING : ' WS-K022-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR CLOSING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K120-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP17K120 - ERROR CLOSING : ' WS-K120-STATUS              
              MOVE WS-K120-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K000-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP17K000 - ERROR CLOSING : ' WS-K000-STATUS              
              MOVE WS-K120-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
