       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C647.                                                  
      *AUTHOR.       RAYMOND H. BUNADO                                          
      *DATE-WRITTEN. 04/02/98.                                                  
      ***********************************************************               
      *  OBJECTIVE : TO EXTRACT TAG 77 OCCUPIERS.               *               
      *                                                         *               
      *                                                         *               
      *                                                         *               
      *  INPUT FILE :- BP13F410                                 *               
      *                BP13K820                                 *               
      *  OUTPUT FILE:- P13F820A.                                *               
      *                                                         *               
      * BP130302 RHB 04/02/1998  NEW PROGRAM.                   *               
      * BP130418 RHB 08/06/1998  Y2K                            *               
      ***********************************************************               
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410 ASSIGN TO BP13F410.                                  
                                                                                
           SELECT BP13K820  ASSIGN TO BP13K820                                  
                  ORGANIZATION     IS INDEXED                                   
                  ACCESS MODE      IS DYNAMIC                                   
                  RECORD KEY       IS K820-KEY-FLD                              
                  FILE STATUS      IS WS-K820-STATUS.                           
                                                                                
           SELECT P13F820A ASSIGN TO P13F820A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F410   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 2000 CHARACTERS                           
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13F410.                                                           
                                                                                
       FD BP13K820    BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 400 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13K820.                                                           
                                                                                
       FD  P13F820A   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 400 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       01  P13F820A-REC  PIC X(400).                                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F410-EOF               PIC X    VALUE 'N'.                    
               88 F410-EOF                        VALUE 'Y'.                    
           05  WS-FOUND                  PIC X    VALUE SPACES.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-CNT-F410          PIC 9(6) VALUE ZEROS.                  
           05  WS-K800-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K820-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-F820-WRITE             PIC 9(6) VALUE ZEROS.                  
                                                                                
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K820-STATUS            PIC 99   VALUE ZEROS.                  
                                                                                
       01  WS-CURR-DATE.                                                        
           05 WS-CURR-CC           PIC 9(02).                                   
           05 WS-YYMMDD.                                                        
              10 WS-CURR-YY        PIC 9(02).                                   
              10 WS-CURR-MM        PIC 9(02).                                   
              10 WS-CURR-DD        PIC 9(02).                                   
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       000-MAIN-LOGIC.                                                          
            PERFORM 100-OPEN-FILES   THRU 100-EXIT.                             
            PERFORM 200-READ-F410    THRU 200-EXIT.                             
            PERFORM 300-PROCESS-F410 THRU 300-EXIT                              
                    UNTIL F410-EOF.                                             
            PERFORM 999-CLOSE-FILES  THRU 999-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------                                                        
       100-OPEN-FILES.                                                          
      *-----------------                                                        
           OPEN INPUT  BP13F410                                                 
                       BP13K820                                                 
                OUTPUT P13F820A.                                                
                                                                                
           IF WS-K820-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K820-STATUS ' WS-K820-STATUS              
              MOVE WS-K820-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
                                                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------                                                        
       200-READ-F410.                                                           
      *-----------------                                                        
           READ BP13F410 AT END                                                 
                MOVE 'Y'                      TO WS-F410-EOF                    
                GO TO 200-EXIT.                                                 
           ADD   1     TO WS-READ-CNT-F410.                                     
           MOVE SPACES TO WS-FOUND.                                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------                                                        
       300-PROCESS-F410.                                                        
      *-----------------                                                        
                                                                                
      *--> VALIDATE REGN FROM MASTER                                            
                                                                                
           MOVE LOW-VALUES                 TO K820-NUM-NRIC.                    
           MOVE F410-NUM-REGN              TO K820-NUM-REGN.                    
                                                                                
           START BP13K820 KEY IS >= K820-KEY-FLD                                
                 INVALID KEY                                                    
                 DISPLAY 'READING ERROR, K820-STATUS ' WS-K820-STATUS           
                 MOVE WS-K820-STATUS       TO RETURN-CODE                       
                 PERFORM 999-CLOSE-FILES THRU 999-EXIT.                         
                                                                                
           PERFORM 0400-PROCESS-K820  THRU 0400-EXIT                            
                   UNTIL K820-NUM-REGN NOT = F410-NUM-REGN                      
                   OR    K820-KEY-FLD      = HIGH-VALUES.                       
                                                                                
           PERFORM 200-READ-F410                THRU 200-EXIT.                  
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------                                                     
       0400-PROCESS-K820.                                                       
      *--------------------                                                     
                                                                                
            READ BP13K820 NEXT                                                  
                 AT END MOVE HIGH-VALUES TO K820-KEY-FLD                        
                 GO TO 0400-EXIT.                                               
                                                                                
            ADD 1 TO WS-K820-READ                                               
            DISPLAY 'NRIC :' K820-NUM-NRIC.                                     
            DISPLAY 'REGN :' K820-NUM-REGN.                                     
            DISPLAY 'LOAN :' K820-NUM-LOAN-TAG.                                 
            DISPLAY 'CA   :' K820-NUM-CA-TAG.                                   
            IF K820-NUM-REGN = F410-NUM-REGN AND                                
              (K820-NUM-FREEZE-OCCP-TAG = '77')                                 
               MOVE BP13K820-REC TO P13F820A-REC                                
               WRITE P13F820A-REC                                               
               ADD 1  TO    WS-F820-WRITE                                       
            END-IF.                                                             
       0400-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
                                                                                
       999-CLOSE-FILES.                                                         
                                                                                
           CLOSE BP13F410                                                       
                 BP13K820                                                       
                 P13F820A.                                                      
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K820, STATUS ' WS-K820-STATUS          
              MOVE WS-K820-STATUS        TO RETURN-CODE.                        
                                                                                
                                                                                
           DISPLAY '*------------------ BP13C645 -------------------'.          
           DISPLAY '                  ' WS-CURR-DATE                            
           DISPLAY 'NO OF REC READ        BP13F410   = '                        
                    WS-READ-CNT-F410.                                           
           DISPLAY 'NO OF REC READ        BP13K820   = '                        
                    WS-K820-READ                                                
           DISPLAY 'NO OF REC WRITTEN     P13F820A   = '                        
                    WS-F820-WRITE                                               
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
