       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC59.                                                 
      *AUTHOR.        ARIEL HERNANDEZ.                                          
      *DATE-WRITTEN : 14 MARCH 2007.                                            
      *===============================================================*         
      *  SYSTEM OF COMMITMENT (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      *  OBJECTIVE   : UPDATE BP13KD00 FROM BP13FD00 FOR NEW OR       *         
      *                ADDITIONAL LOAN DISBURSED ON THE DUE-DATE.     *         
      *                BP13FD00 IS OUTPUT FROM BP13CC02 IN BP13D102.  *         
      *                PROGRAM WILL UPDATE AMT-LOAN-DISBURSED,        *         
      *                AMT-LOAN-BAL. FOR FIRST LOAN, PROGRAM WILL     *         
      *                UPDATE LOAN-EXPIRY-DATE, COMM-DATE.            *         
      *                                                               *         
      *  INPUT : BP13FD00                                             *         
      *  I-O   : BP13KD00                                             *         
      *---------------------------------------------------------------*         
      *  CHG-NO   BY    DATE   DESCRIPTION                            *         
      * -------- ---- -------- ---------------------------------------*         
      * BP132888 BA3  14032007 NEW PROGRAM                            *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FD00 ASSIGN TO BP13FD00.                                  
                                                                                
           SELECT BP13KD00 ASSIGN       TO BP13KD00                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS KD00-KEY-FLD                         
                           FILE STATUS  IS KD00-STATUS.                         
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD  BP13FD00                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 1600 CHARACTERS              
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13FD00.                                                           
                                                                                
       FD   BP13KD00               RECORD CONTAINS 1600 CHARACTERS.             
       COPY BP13KD00.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-VARIABLES.                                                        
           05  WS-FD00-READ            PIC 9(5)     VALUE ZEROS.                
           05  WS-KD00-FOUND-CNT       PIC 9(5)     VALUE ZEROS.                
           05  WS-KD00-NOTFND          PIC 9(5)     VALUE ZEROS.                
           05  WS-KD00-FOUND           PIC X(1)     VALUE SPACE.                
           05  WS-KD00-REWRITE         PIC 9(5)     VALUE ZEROS.                
           05  KD00-STATUS             PIC 9(2)     VALUE ZEROS.                
           05  WS-EOF-FD00             PIC X(3).                                
               88  EOF-OF-FD00                      VALUE 'YES'.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ***********************                                                   
       0000-MAIN-LOGIC.                                                         
      ***********************                                                   
           PERFORM 1000-INITIALIZATION THRU 1000-EXIT.                          
           PERFORM 3000-READ-FD00     THRU 3000-EXIT.                           
           PERFORM 4000-PROCESS       THRU 4000-EXIT                            
                   UNTIL EOF-OF-FD00.                                           
           PERFORM 9000-FINALIZATION  THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       1000-INITIALIZATION.                                                     
      ***********************                                                   
           OPEN INPUT  BP13FD00                                                 
                I-O    BP13KD00.                                                
           MOVE 'NO' TO WS-EOF-FD00.                                            
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       3000-READ-FD00.                                                          
      ***********************                                                   
                                                                                
           READ BP13FD00                                                        
             AT END MOVE 'YES' TO WS-EOF-FD00                                   
                    MOVE HIGH-VALUE TO FD00-KEY-FLD                             
               GO TO 3000-EXIT.                                                 
            ADD 1 TO WS-FD00-READ.                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       4000-PROCESS.                                                            
      ***********************                                                   
           PERFORM 5000-READ-BP13KD00  THRU 5000-EXIT.                          
           IF WS-KD00-FOUND = 'Y'                                               
              PERFORM 6000-REWRITE-BP13KD00 THRU 6000-EXIT                      
              ADD  1 TO WS-KD00-REWRITE                                         
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-FD00     THRU 3000-EXIT.                           
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       5000-READ-BP13KD00.                                                      
      ***********************                                                   
           MOVE 'N'            TO WS-KD00-FOUND.                                
           MOVE FD00-KEY-FLD   TO KD00-KEY-FLD.                                 
           READ BP13KD00.                                                       
                                                                                
           IF KD00-STATUS = ZEROS                                               
              ADD 1    TO WS-KD00-FOUND-CNT                                     
              MOVE 'Y' TO WS-KD00-FOUND                                         
           ELSE                                                                 
              IF KD00-STATUS = 23                                               
                 ADD 1    TO WS-KD00-NOTFND                                     
                 MOVE 'N' TO WS-KD00-FOUND                                      
              ELSE                                                              
                 MOVE KD00-STATUS   TO RETURN-CODE                              
                 DISPLAY 'ERROR READING BP13KD00, STATUS = '                    
                         KD00-STATUS ' KEY IS ' FD00-KEY-FLD                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ***********************                                                   
       6000-REWRITE-BP13KD00.                                                   
      ***********************                                                   
                                                                                
           IF FD00-AMT-LOAN-BAL IS NOT NUMERIC                                  
              MOVE ZEROES       TO FD00-AMT-LOAN-BAL                            
           END-IF.                                                              
           IF FD00-AMT-LOAN-DISBURSED IS NOT NUMERIC                            
              MOVE ZEROES       TO FD00-AMT-LOAN-DISBURSED                      
           END-IF.                                                              
                                                                                
           MOVE FD00-DTE-LOAN-EXPIRY      TO KD00-DTE-LOAN-EXPIRY.              
           MOVE FD00-DTE-LOAN-COMM        TO KD00-DTE-LOAN-COMM.                
           MOVE FD00-CDE-ACCT-STAT        TO KD00-CDE-ACCT-STAT.                
                                                                                
           MOVE FD00-AMT-LOAN-BAL         TO KD00-AMT-LOAN-BAL.                 
           MOVE FD00-AMT-ARR-INTR         TO KD00-AMT-ARR-INTR.                 
           MOVE FD00-AMT-LOAN-DISBURSED   TO KD00-AMT-LOAN-DISBURSED.           
           MOVE FD00-DTE-LOAN-DISBURSED   TO KD00-DTE-LOAN-DISBURSED.           
           MOVE FD00-AMT-NEXT-INSTAL      TO KD00-AMT-NEXT-INSTAL.              
           MOVE FD00-DTE-NEXT-INSTAL      TO KD00-DTE-NEXT-INSTAL.              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO KD00-DTE-UPDATE.                  
           MOVE FUNCTION CURRENT-DATE(9:8) TO KD00-TME-UPDATE.                  
           MOVE 'BP13CC59'  TO KD00-NUM-USERID.                                 
                                                                                
           REWRITE BP13KD00-REC.                                                
                                                                                
           IF KD00-STATUS = ZEROS                                               
              CONTINUE                                                          
           ELSE                                                                 
              MOVE KD00-STATUS   TO RETURN-CODE                                 
              DISPLAY 'ERROR REWRITING BP13KD00, STATUS = '                     
                      KD00-STATUS                                               
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ***********************                                                   
       9000-FINALIZATION.                                                       
      ***********************                                                   
             DISPLAY '*-------- BP13CC59 -----------*'.                         
             DISPLAY ' FD00 RECS READ     ' WS-FD00-READ.                       
             DISPLAY ' KD00 RECS READ     ' WS-KD00-FOUND-CNT.                  
             DISPLAY ' KD00 RECS NOT FND  ' WS-KD00-NOTFND.                     
             DISPLAY ' KD00 RECS REWRITE  ' WS-KD00-REWRITE.                    
                                                                                
           CLOSE      BP13FD00                                                  
                      BP13KD00.                                                 
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
