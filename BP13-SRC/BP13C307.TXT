      *-------------------------*                                               
       IDENTIFICATION DIVISION.                                                 
      *-------------------------*                                               
       PROGRAM-ID.    BP13C307.                                                 
      *AUTHOR.        J MOHD SHAMSUDEEN                                         
      *DATE-WRITTEN.  19/02/99.                                                 
                                                                                
      * ========================================================= *             
      *                SYSTEM OF COMMITMENT  (BP13)               *             
      * ========================================================= *             
      *    OBJECTIVE :                                            *             
      *      1. TO PRINT THE OUTSTANDING CASES FOR STAMPING       *             
      *         FOR MORE THAN 1 MONTH  (SA CASES)                 *             
      *                                                           *             
      *    I/O FILES :                                            *             
      *       INPUT  :  BP13F800 - APLLICANT FILE                 *             
      *                 BP13K330 - ACCUMSO   FILE                 *             
      *                 BP13K022 - OIC       FILE                 *             
      *                 BM06K006 - SFMAST    FILE                 *             
      *      OUTPUT  :  BP13L307 - PRINT     FILE                 *             
      *                                                           *             
      *    FUNCTION :                                             *             
      *      1. BATCH PROGRAMMING IN COBOL                        *             
      *      2. PRINT LISTINGS OF OUTSTANDING CASES FOR STAMPING  *             
      *                                                           *             
      * ========================================================= *             
      * CHGE-NO  DATE     BY   DESCRIPTIONS                       *             
      * -------- -------- ---  ------------                       *             
      * BP130565 19/02/99 JMS  NEW PROGRAM                        *             
      * BP130883 08/04/00 RAS  TO CHANGE BP13C904 TO BP13C913     *             
      * BP132504 25/03/04 JF4  PRINT NEW REPORT FOR BANK CASES    *             
      * BP132734 13/09/05 JF4  MONITOR 30 DAYS                    *             
      * BP134779 13/06/13 PCL4 TO CONVERT THE REPORT TO SENDMAIL  *             
      * BP135901 27/08/15 KSJ3 - TO SKIP RECS WITH NO STAMP FEE   *             
      *                        - TO SKIP RECS WITH S/T VR DATE &  *             
      *                          SA VR DATE                       *             
      *                        - TO UPDATE EMAIL HEADERS          *             
      * BP137087 17/11/17 PCL4 - TO UPDATE EMAIL RECIPIENTS       *             
      * BP138202 20/02/20 KV5  - ADDED EMAIL RECIPIENTS           *             
      * BP138757 23/07/21 KV8  - REPLACEING KATHIJAH TO SUZANNE   *             
      * BP139030 07/01/22 ZAR7   REPLACE CHUA YEE MIEN TO LIM KOK CHUN          
      * BP139159 13/07/22 KAC1   ADDED  zoe_sm_aw@hdb.gov.sg      *             
      *                                 jocelyn_wt_seah@hdb.gov.sg              
      * BP139276 05/04/24 KAC1   REPLACE LIM KOK CHUN BY          *             
      *                                  KATHIJAH_BEE_ALI_MOHAMED *             
      * ========================================================= *             
                                                                                
      *----------------------*                                                  
       ENVIRONMENT DIVISION.                                                    
      *----------------------*                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K330  ASSIGN        TO BP13K330                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K330-KEY-FLD                       
                            FILE STATUS   IS WS-K330-STATUS.                    
                                                                                
           SELECT BP13K022  ASSIGN        TO BP13K022                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K022-KEY-FLD                       
                            ALTERNATE KEY IS K022-NUM-REGN                      
                            FILE STATUS   IS WS-K022-STATUS.                    
                                                                                
           SELECT B05K006   ASSIGN        TO B05K006                            
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K006-HDB-REF-SF                    
                            FILE STATUS   IS WS-K006-STATUS.                    
                                                                                
           SELECT BP13F800  ASSIGN        TO BP13F800.                          
                                                                                
           SELECT BP13L307  ASSIGN        TO BP13L307.                          
                                                                                
                                                                                
      *---------------*                                                         
       DATA DIVISION.                                                           
      *---------------*                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13K330.                                                           
       COPY BP13K330.                                                           
                                                                                
       FD   BP13K022.                                                           
       COPY BP13K022.                                                           
                                                                                
       FD   B05K006.                                                            
       COPY B05K006.                                                            
                                                                                
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13L307                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   BP13L307-REC         PIC X(132).                                    
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-EMAIL-RPT-VARIABLES.                                              
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT3             PIC X(100)                              
      *        VALUE 'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.           
               VALUE 'RCPT TO:<SUZANNE_SC_HENG@HDB.GOV.SG>'.                    
           05  WS-MAIL-RPT4             PIC X(100)                              
               VALUE 'RCPT TO:<Jaisi_S_BHASI@hdb.gov.sg>'.                      
           05  WS-MAIL-RPT5             PIC X(100)                              
               VALUE 'RCPT TO:<Balasubramaniam_ARUNASALAM@hdb.gov.sg>'.         
           05  WS-MAIL-RPT6             PIC X(100)                              
               VALUE 'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.           
           05  WS-MAIL-RPT6A            PIC X(100)                              
               VALUE 'RCPT TO:<NOORJAHAN_SHAIK_MOHAMED_GULEMKADER@hdb.go        
      -         'v.sg>'.                                                        
           05  WS-MAIL-RPT6B            PIC X(100)                              
               VALUE 'RCPT TO:<OH_Meng_Huat@hdb.gov.sg>'.                       
           05  WS-MAIL-RPT6C            PIC X(100)                              
               VALUE 'RCPT TO:<Muhammed_shariff_sahrom@hdb.gov.sg>'.            
           05  WS-MAIL-RPT6D            PIC X(100)                              
               VALUE 'RCPT TO:<Chandra_lagha_arumugam@hdb.gov.sg>'.             
           05  WS-MAIL-RPT6E            PIC X(100)                              
               VALUE 'RCPT TO:<NGAI_Chyau_Jiann@hdb.gov.sg>'.                   
           05  WS-MAIL-RPT6F            PIC X(100)                              
               VALUE 'RCPT TO:<zoe_sm_aw@hdb.gov.sg>'.                          
           05  WS-MAIL-RPT6H            PIC X(100)                              
               VALUE 'RCPT TO:<jocelyn_wt_seah@hdb.gov.sg>'.                    
           05  WS-MAIL-RPT7             PIC X(4)                                
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT8             PIC X(40)                               
               VALUE 'FROM:Soc System - Email Alert'.                           
           05  WS-MAIL-RPT9             PIC X(100)                              
      *        VALUE 'TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                
               VALUE 'TO:<SUZANNE_SC_HENG@HDB.GOV.SG>'.                         
           05  WS-MAIL-RPT10            PIC X(100)                              
               VALUE 'TO:<Jaisi_S_BHASI@hdb.gov.sg>'.                           
           05  WS-MAIL-RPT11            PIC X(100)                              
               VALUE 'TO:<Balasubramaniam_ARUNASALAM@hdb.gov.sg>'.              
           05  WS-MAIL-RPT11A           PIC X(100)                              
               VALUE   'TO:<Noorjahan_SHAIK_MOHAMED_GULEMKADER@hdb.gov.s        
      -         'g>'.                                                           
           05  WS-MAIL-RPT11B           PIC X(100)                              
               VALUE 'TO:<OH_Meng_Huat@hdb.gov.sg>'.                            
           05  WS-MAIL-RPT11C           PIC X(100)                              
               VALUE 'TO:<Muhammed_shariff_sahrom@hdb.gov.sg>'.                 
           05  WS-MAIL-RPT11D           PIC X(100)                              
               VALUE 'TO:<Chandra_lagha_arumugam@hdb.gov.sg>'.                  
           05  WS-MAIL-RPT11E           PIC X(100)                              
               VALUE 'TO:<NGAI_Chyau_Jiann@hdb.gov.sg>'.                        
           05  WS-MAIL-RPT11F           PIC X(100)                              
               VALUE 'TO:<zoe_sm_aw@hdb.gov.sg>'.                               
           05  WS-MAIL-RPT11H           PIC X(100)                              
               VALUE 'TO:<jocelyn_wt_seah@hdb.gov.sg>'.                         
           05  WS-MAIL-RPT12            PIC X(100)                              
               VALUE 'CC:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                
           05  WS-MAIL-RPT13            PIC X(100) VALUE                        
               'SUBJECT : LIST OF OUTSTANDING CASES (SOCA) '.                   
                                                                                
      *-----------------------------------------------------------*             
      *    DEFINE BP13L307               HEADING LINES            *             
      *-----------------------------------------------------------*             
       01  PRT-L307-HDG-1.                                                      
           05  L307-REPORT-ID       PIC X(8)  VALUE 'BP13L307'.                 
           05  FILLER               PIC X(10) VALUE '      HDB3'.               
           05  FILLER               PIC X(27) VALUE SPACES.                     
           05  FILLER               PIC X(39) VALUE                             
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER               PIC X(14) VALUE SPACES.                     
           05  FILLER               PIC X(7)  VALUE 'DATE : '.                  
           05  L307-CURRENT-DATE    PIC X(10) VALUE SPACES.                     
           05  FILLER               PIC X(4)  VALUE SPACES.                     
           05  FILLER               PIC X(7)  VALUE 'PAGE : '.                  
           05  L307-PAGE            PIC ZZZZ9.                                  
                                                                                
       01  PRT-L307-HDG-2.                                                      
           05  FILLER               PIC X(44) VALUE SPACES.                     
           05  FILLER               PIC X(43) VALUE                             
               'LIST OF OUTSTANDING CASES (SOCA - NON BANK)'.                   
                                                                                
       01  PRT-L307-HDG-2A.                                                     
           05  FILLER               PIC X(34) VALUE SPACES.                     
           05  FILLER               PIC X(64) VALUE                             
               'LIST OF OUTSTANDING CASES (SOCA - BANK CASES EXCLUDING E        
      -        'S CASES)'.                                                      
                                                                                
       01  PRT-L307-HDG-4.                                                      
           05  FILLER               PIC X(04) VALUE 'S/NO'.                     
           05  FILLER               PIC X(04) VALUE SPACES.                     
           05  FILLER               PIC X(08) VALUE 'REGN  NO'.                 
           05  FILLER               PIC X(04) VALUE SPACES.                     
           05  FILLER               PIC X(11) VALUE 'SCH ACCT NO'.              
           05  FILLER               PIC X(04) VALUE SPACES.                     
           05  FILLER               PIC X(35) VALUE 'FLAT ADDRESS'.             
           05  FILLER               PIC X(02) VALUE SPACES.                     
           05  FILLER               PIC X(16) VALUE 'SALES AGRMT DATE'.         
           05  FILLER               PIC X(04) VALUE SPACES.                     
           05  FILLER               PIC X(14) VALUE 'STAMP FEE PAID'.           
           05  FILLER               PIC X(04) VALUE SPACES.                     
           05  FILLER               PIC X(03) VALUE 'OIC'.                      
           05  FILLER               PIC X(05) VALUE SPACES.                     
           05  FILLER               PIC X(07) VALUE 'CF TYPE'.                  
                                                                                
       01  L307-BLANK-LINE          PIC X(132) VALUE SPACES.                    
                                                                                
       01  L307-UNDER-LINE          PIC X(132) VALUE ALL '-'.                   
                                                                                
       01  PRT-L307-NO-CASE.                                                    
           05  FILLER               PIC X(50)  VALUE SPACES.                    
           05  FILLER               PIC X(31)  VALUE                            
               '*******  N O   C A S E  *******'.                               
                                                                                
       01  PRT-L307-DTL-1.                                                      
           05  L307-SEQ-NO          PIC ZZZ9  VALUE ZEROES.                     
           05  FILLER               PIC X(04) VALUE SPACES.                     
           05  L307-REGN-NO         PIC X(08) VALUE SPACES.                     
           05  FILLER               PIC X(04) VALUE SPACES.                     
           05  L307-SCH-ACC         PIC X(11) VALUE SPACES.                     
           05  FILLER               PIC X(04) VALUE SPACES.                     
           05  L307-FLAT-ADD1       PIC X(35) VALUE SPACES.                     
           05  FILLER               PIC X(05) VALUE SPACES.                     
           05  L307-SA-DTE          PIC X(10) VALUE SPACES.                     
           05  FILLER               PIC X(05) VALUE SPACES.                     
           05  L307-STAMP-PAID      PIC Z,ZZZ,ZZZ,ZZ9.99 VALUE ZEROS.           
           05  FILLER               PIC X(04) VALUE SPACES.                     
           05  L307-NUM-OIC         PIC X(05) VALUE SPACES.                     
           05  FILLER               PIC X(05) VALUE SPACES.                     
           05  L307-CF-TYPE         PIC X(02) VALUE SPACES.                     
                                                                                
       01  PRT-L307-DTL-2.                                                      
           05  FILLER               PIC X(29) VALUE SPACES.                     
           05  L307-FLAT-ADD2       PIC X(35) VALUE SPACES.                     
                                                                                
      *-----------------------------------------------------------*             
      *    OTHER VARIABLES                                        *             
      *-----------------------------------------------------------*             
       01  WS-COUNTERS.                                                         
           05  WS-BP13F800-READ-CNT         PIC 9(6)       VALUE ZEROS.         
           05  WS-BP13F800-PROC-CNT         PIC 9(6)       VALUE ZEROS.         
           05  WS-BP13K330-READ-CNT         PIC 9(6)       VALUE ZEROS.         
           05  WS-BP13K006-READ-CNT         PIC 9(6)       VALUE ZEROS.         
           05  WS-BP13K022-READ-CNT         PIC 9(6)       VALUE ZEROS.         
           05  WS-K330-READ-CNT             PIC Z,ZZ,ZZ9   VALUE ZEROS.         
           05  WS-K006-READ-CNT             PIC Z,ZZ,ZZ9   VALUE ZEROS.         
           05  WS-K022-READ-CNT             PIC Z,ZZ,ZZ9   VALUE ZEROS.         
           05  WS-F800-READ-CNT             PIC Z,ZZ,ZZ9   VALUE ZEROS.         
           05  WS-PRINT-CNT                 PIC Z,ZZ,ZZ9   VALUE ZEROS.         
           05  WS-LINE-CNT                  PIC 9(2)       VALUE 99.            
           05  WS-PAGE-CNT                  PIC 9(5)       VALUE ZEROS.         
           05  WS-SEQ-NO                    PIC 9(5)       VALUE ZEROS.         
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K330-STATUS               PIC 9(2)       VALUE ZEROS.         
           05  WS-K006-STATUS               PIC 9(2)       VALUE ZEROS.         
           05  WS-K022-STATUS               PIC 9(2)       VALUE ZEROS.         
           05  EOF-F800                     PIC X          VALUE 'N'.           
                                                                                
       01  WS-FUNC-CUR-DTE                  PIC S9(9).                          
       01  WS-FUNC-AGR-DTE                  PIC S9(9).                          
       01  WS-DATE-DIFF                     PIC 9(9).                           
                                                                                
       01  WS-EDIT-DTE.                                                         
           05  WS-EDIT-DD                   PIC X(2).                           
           05  FILLER                       PIC X(1) VALUE '/'.                 
           05  WS-EDIT-MM                   PIC X(2).                           
           05  FILLER                       PIC X(1) VALUE '/'.                 
           05  WS-EDIT-CC                   PIC X(2).                           
           05  WS-EDIT-YY                   PIC X(2).                           
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CY                    PIC 9(4).                           
           05  WS-CUR-MM                    PIC 9(2).                           
           05  WS-CUR-DD                    PIC 9(2).                           
       01  WS-CUR-DATE-9 REDEFINES                                              
                         WS-CUR-DATE        PIC 9(8).                           
                                                                                
       01  WS-AGRM-DATE.                                                        
           05  WS-AGRM-CY                   PIC 9(4).                           
           05  WS-AGRM-MM                   PIC 9(2).                           
           05  WS-AGRM-DD                   PIC 9(2).                           
       01  WS-AGRM-DATE-9 REDEFINES                                             
                        WS-AGRM-DATE        PIC 9(8).                           
                                                                                
       01  WS-ADDRESS1.                                                         
           05  FILLER                       PIC X(05) VALUE 'BLOCK'.            
           05  WS-BLK-NO                    PIC X(04) VALUE SPACES.             
           05  FILLER                       PIC X(03) VALUE ' , '.              
           05  WS-ST-NAME                   PIC X(20) VALUE SPACES.             
                                                                                
       01  WS-ADDRESS2.                                                         
           05  FILLER                       PIC X(02) VALUE '# '.               
           05  WS-LEVEL-NO                  PIC X(02) VALUE SPACES.             
           05  FILLER                       PIC X(03) VALUE ' - '.              
           05  WS-MAIN-UNIT                 PIC X(04) VALUE SPACES.             
           05  FILLER                       PIC X(03) VALUE ' , '.              
           05  FILLER                       PIC X(09) VALUE 'SINGAPORE'.        
           05  FILLER                       PIC X(02) VALUE ' ('.               
           05  WS-COUNTRY-POSTCDE           PIC X(06) VALUE SPACES.             
           05  FILLER                       PIC X(01) VALUE ')'.                
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-STAMP-PAID            PIC 9(10)V99.                           
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      ***LINKAGE FOR BP13C913*                                                  
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH-ACC.                                              
              10 WS-LINK-NUM-SCH        PIC X(4).                               
              10 WS-LINK-NUM-ACC        PIC X(5).                               
                                                                                
       LINKAGE SECTION.                                                         
       01  PARM-FIELD.                                                          
           05  PARM-LENGTH             PIC S9(04) COMP.                         
           05  PARM-DATA               PIC X(01).                               
                                                                                
      *--------------------------------------*                                  
       PROCEDURE DIVISION USING PARM-FIELD.                                     
      *--------------------------------------*                                  
      ******************************************************************        
       0000-CONTROL.                                                            
      ******************************************************************        
            PERFORM  0100-SET-DATE               THRU  0100-EXIT.               
            PERFORM  0200-OPEN-FILES             THRU  0200-EXIT.               
            PERFORM  0300-READ-BP13F800          THRU  0300-EXIT                
                          UNTIL EOF-F800 = 'Y'.                                 
            PERFORM  7000-CHK-REPORT-STATUS      THRU  7000-EXIT.               
            PERFORM  8000-DISPLAY-STATISTICS     THRU  8000-EXIT.               
            PERFORM  9000-CLOSE-FILES            THRU  9000-EXIT.               
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0100-SET-DATE.                                                           
      ******************************************************************        
            STRING  FUNCTION CURRENT-DATE(7:2) '/'                              
                    FUNCTION CURRENT-DATE(5:2) '/'                              
                    FUNCTION CURRENT-DATE(1:4)                                  
            DELIMITED BY SIZE INTO L307-CURRENT-DATE.                           
                                                                                
                                                                                
       0100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0200-OPEN-FILES.                                                         
      ******************************************************************        
            OPEN INPUT  BP13K330                                                
                        BP13K022                                                
                        BP13F800                                                
                        B05K006                                                 
                 OUTPUT BP13L307.                                               
                                                                                
            IF WS-K330-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING ERROR BP13K330 ' WS-K330-STATUS                 
               PERFORM 8000-DISPLAY-STATISTICS    THRU  8000-EXIT               
               PERFORM 9000-CLOSE-FILES           THRU  9000-EXIT.              
                                                                                
            IF WS-K022-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING ERROR BP13K022 ' WS-K022-STATUS                 
               PERFORM 8000-DISPLAY-STATISTICS    THRU  8000-EXIT               
               PERFORM 9000-CLOSE-FILES           THRU  9000-EXIT.              
                                                                                
            IF WS-K006-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING ERROR B05K006  ' WS-K006-STATUS                 
               PERFORM 8000-DISPLAY-STATISTICS    THRU  8000-EXIT               
               PERFORM 9000-CLOSE-FILES           THRU  9000-EXIT.              
                                                                                
            PERFORM 5000-HEADER-EMAIL             THRU 5000-EXIT.               
                                                                                
       0200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0300-READ-BP13F800.                                                      
      ******************************************************************        
                                                                                
            READ BP13F800                                                       
              AT END                                                            
                 MOVE    'Y'  TO EOF-F800                                       
                 GO TO   0300-EXIT.                                             
                                                                                
            ADD 1  TO  WS-BP13F800-READ-CNT.                                    
                                                                                
            IF (F800-DTE-SALE-AGMT  NOT = (SPACES AND LOW-VALUES AND            
                                         '00000000')) AND                       
               (F800-DTE-STAMP-AGMT     = (SPACES OR  LOW-VALUES OR             
                                         '00000000')) AND                       
               (F800-DTE-STAMP-SUBTRF   = (SPACES OR  LOW-VALUES OR             
                                         '00000000'))                           
               ADD 1 TO WS-BP13F800-PROC-CNT                                    
               PERFORM 0400-PROCESS-PRINTING THRU 0400-EXIT                     
            END-IF.                                                             
                                                                                
       0300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0400-PROCESS-PRINTING.                                                   
      ******************************************************************        
            MOVE  SPACES            TO     BP13K330-REC.                        
            MOVE  F800-NUM-REGN     TO     K330-CURR-REG.                       
            MOVE  '1'               TO     K330-SEQ-NO.                         
                                                                                
            READ  BP13K330.                                                     
                                                                                
            ADD  1  TO  WS-BP13K330-READ-CNT.                                   
                                                                                
            IF WS-K330-STATUS = 00                                              
               IF K330-DTE-AGRM-ORDER NOT = SPACES AND LOW-VALUES AND           
                                           '00000000'                           
                  PERFORM  0500-CHECK-DATE THRU 0500-EXIT                       
               ELSE                                                             
                     DISPLAY ' REGN ' K330-KEY-FLD                              
                     DISPLAY ' AGREEMENT ORDER NOT RAISED '                     
               END-IF                                                           
            ELSE                                                                
               IF WS-K330-STATUS = 23                                           
                  DISPLAY 'BP13K330 REC NOT FOUND : ' K330-CURR-REG             
                  DISPLAY 'CASE ALREADY TP.'                                    
               ELSE                                                             
                  DISPLAY 'READ ERROR IN BP13K330 '  WS-K330-STATUS             
                  PERFORM 9000-CLOSE-FILES           THRU 9000-EXIT             
               END-IF                                                           
            END-IF.                                                             
                                                                                
       0400-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0500-CHECK-DATE.                                                         
      ******************************************************************        
            MOVE   FUNCTION CURRENT-DATE   TO   WS-CUR-DATE.                    
            MOVE   K330-DTE-AGRM-ORDER     TO   WS-AGRM-DATE.                   
                                                                                
            DISPLAY 'WS-CUR-DATE = '  WS-CUR-DATE.                              
            DISPLAY 'K330-DTE-AGRM-ORDER = ' K330-DTE-AGRM-ORDER.               
                                                                                
            COMPUTE  WS-FUNC-CUR-DTE =                                          
                     FUNCTION INTEGER-OF-DATE(WS-CUR-DATE-9).                   
            COMPUTE  WS-FUNC-AGR-DTE =                                          
                     FUNCTION INTEGER-OF-DATE(WS-AGRM-DATE-9).                  
                                                                                
            COMPUTE WS-DATE-DIFF = WS-FUNC-CUR-DTE - WS-FUNC-AGR-DTE.           
            DISPLAY 'DIFF = ' WS-DATE-DIFF.                                     
                                                                                
      ******  THE DIFFERENCE OF 30 DAYS IS TAKEN TO SORT CASES WHOSE            
      *****   AGREEMENT ORDER DATE IS 1 MTHS PRIOR TO CURRENT DATE.             
                                                                                
            IF WS-DATE-DIFF >= 30                                               
               PERFORM 0950-READ-AB05K006     THRU  0950-EXIT                   
                                                                                
               IF WS-STAMP-PAID > ZEROES                                        
                  PERFORM 0600-PRINT-LISTING     THRU   0600-EXIT               
               END-IF                                                           
            END-IF.                                                             
                                                                                
       0500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0600-PRINT-LISTING.                                                      
      ******************************************************************        
            IF WS-LINE-CNT > 50                                                 
               PERFORM  0650-PRINT-HEADING     THRU   0650-EXIT                 
            END-IF.                                                             
                                                                                
            ADD         1                      TO     WS-SEQ-NO.                
            ADD         3                      TO     WS-LINE-CNT.              
            MOVE        WS-SEQ-NO              TO     L307-SEQ-NO.              
            PERFORM     0700-MOVE-DATA         THRU   0700-EXIT.                
            WRITE       BP13L307-REC           FROM   L307-BLANK-LINE.          
            WRITE       BP13L307-REC           FROM   PRT-L307-DTL-1.           
            WRITE       BP13L307-REC           FROM   PRT-L307-DTL-2.           
                                                                                
       0600-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0650-PRINT-HEADING.                                                      
      ******************************************************************        
            ADD    1             TO    WS-PAGE-CNT.                             
            MOVE   WS-PAGE-CNT   TO    L307-PAGE.                               
            MOVE   5             TO    WS-LINE-CNT.                             
            WRITE  BP13L307-REC  FROM  PRT-L307-HDG-1 AFTER PAGE.               
            IF PARM-DATA = 'B'                                                  
               WRITE  BP13L307-REC  FROM  PRT-L307-HDG-2A                       
            ELSE                                                                
               WRITE  BP13L307-REC  FROM  PRT-L307-HDG-2                        
            END-IF.                                                             
            WRITE  BP13L307-REC  FROM  L307-BLANK-LINE.                         
            WRITE  BP13L307-REC  FROM  PRT-L307-HDG-4.                          
            WRITE  BP13L307-REC  FROM  L307-UNDER-LINE.                         
                                                                                
       0650-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0700-MOVE-DATA.                                                          
      ******************************************************************        
            PERFORM  0900-GET-ADDRESS      THRU  0900-EXIT.                     
            PERFORM  0970-READ-BP13K022    THRU  0970-EXIT.                     
                                                                                
            MOVE     F800-NUM-REGN         TO    L307-REGN-NO.                  
            MOVE     F800-NUM-SCH-ACC      TO    L307-SCH-ACC.                  
            MOVE     WS-STAMP-PAID         TO    L307-STAMP-PAID.               
            MOVE     K330-AGRORDER-DTE-CC  TO    WS-EDIT-CC.                    
            MOVE     K330-AGRORDER-DTE-YY  TO    WS-EDIT-YY.                    
            MOVE     K330-AGRORDER-DTE-MM  TO    WS-EDIT-MM.                    
            MOVE     K330-AGRORDER-DTE-DD  TO    WS-EDIT-DD.                    
            MOVE     WS-EDIT-DTE           TO    L307-SA-DTE.                   
            MOVE     K022-NUM-OIC          TO    L307-NUM-OIC.                  
                                                                                
            IF PARM-DATA = 'B'                                                  
               MOVE     F800-NUM-CF-TYPE   TO    L307-CF-TYPE                   
            ELSE                                                                
               MOVE       '--'             TO    L307-CF-TYPE                   
            END-IF.                                                             
                                                                                
       0700-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0900-GET-ADDRESS.                                                        
      ******************************************************************        
           MOVE SPACES                 TO BP13COMM8-REC.                        
           INITIALIZE                     BP13COMM8-REC.                        
                                                                                
           MOVE F800-NUM-SCH-ACC       TO WS-LINK-REC.                          
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                BP13COMM8-REC.                                  
                                                                                
           IF (COMM8-CDE-SYSERR = 0) AND                                        
              (COMM8-NUM-MGMT   NOT = SPACES)                                   
              MOVE COMM8-NUM-BLK         TO WS-BLK-NO                           
              MOVE COMM8-NME-STREET      TO WS-ST-NAME                          
              MOVE COMM8-NUM-UNIT-MAIN   TO WS-MAIN-UNIT                        
              MOVE COMM8-NUM-LEVEL       TO WS-LEVEL-NO                         
              MOVE COMM8-NUM-POSTAL-CODE TO WS-COUNTRY-POSTCDE                  
              MOVE WS-ADDRESS1           TO L307-FLAT-ADD1                      
              MOVE WS-ADDRESS2           TO L307-FLAT-ADD2                      
           ELSE                                                                 
              DISPLAY 'ERROR CALLING BP13C913 '                                 
              DISPLAY 'SQL CODE = ' COMM8-CDE-SYSERR                            
              MOVE SPACES                TO L307-FLAT-ADD1                      
              MOVE SPACES                TO L307-FLAT-ADD2                      
           END-IF.                                                              
                                                                                
       0900-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0950-READ-AB05K006.                                                      
      ******************************************************************        
            MOVE   K330-SCH-ACC            TO   K006-HDB-REF-SF.                
                                                                                
            READ B05K006.                                                       
                                                                                
            ADD  1  TO  WS-BP13K006-READ-CNT.                                   
                                                                                
            IF WS-K006-STATUS = 00                                              
               IF K006-BALANCES NOT NUMERIC                                     
                  MOVE ZEROES        TO WS-STAMP-PAID                           
               ELSE                                                             
                  MOVE K006-BALANCES TO WS-STAMP-PAID                           
               END-IF                                                           
            ELSE                                                                
               IF WS-K006-STATUS = 23                                           
                  DISPLAY 'B05K006  REC NOT FOUND : ' K006-HDB-REF-SF           
                  MOVE ZEROES        TO WS-STAMP-PAID                           
               ELSE                                                             
                  DISPLAY 'READ ERROR IN B05K006  '  WS-K006-STATUS             
                  MOVE ZEROES        TO WS-STAMP-PAID                           
               END-IF                                                           
            END-IF.                                                             
                                                                                
       0950-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0970-READ-BP13K022.                                                      
      ******************************************************************        
            MOVE   SPACES          TO   K022-KEY-FLD.                           
            MOVE   F800-NUM-REGN   TO   K022-NUM-REGN.                          
                                                                                
            READ BP13K022 KEY IS K022-NUM-REGN.                                 
                                                                                
            EVALUATE WS-K022-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
                  ADD    1         TO   WS-BP13K022-READ-CNT                    
               WHEN 23                                                          
                  DISPLAY 'BP13K022 REC NOT FOUND : ' K022-NUM-REGN             
               WHEN OTHER                                                       
                  DISPLAY 'BP13K022 - READ ERROR, STATUS '                      
                                                     WS-K022-STATUS             
                  MOVE     WS-K022-STATUS      TO   RETURN-CODE                 
                  PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                   
            END-EVALUATE.                                                       
                                                                                
       0970-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5000-HEADER-EMAIL.                                                       
      *================================================================*        
                                                                                
            WRITE BP13L307-REC   FROM WS-MAIL-RPT1.                             
            WRITE BP13L307-REC   FROM WS-MAIL-RPT2.                             
            WRITE BP13L307-REC   FROM WS-MAIL-RPT3.                             
            WRITE BP13L307-REC   FROM WS-MAIL-RPT4.                             
            WRITE BP13L307-REC   FROM WS-MAIL-RPT5.                             
            WRITE BP13L307-REC   FROM WS-MAIL-RPT6.                             
            WRITE BP13L307-REC   FROM WS-MAIL-RPT6A.                            
            WRITE BP13L307-REC   FROM WS-MAIL-RPT6B.                            
            WRITE BP13L307-REC   FROM WS-MAIL-RPT6C.                            
            WRITE BP13L307-REC   FROM WS-MAIL-RPT6D.                            
            WRITE BP13L307-REC   FROM WS-MAIL-RPT6E.                            
            WRITE BP13L307-REC   FROM WS-MAIL-RPT6F.                            
            WRITE BP13L307-REC   FROM WS-MAIL-RPT6H.                            
            WRITE BP13L307-REC   FROM WS-MAIL-RPT7.                             
            WRITE BP13L307-REC   FROM WS-MAIL-RPT8.                             
            WRITE BP13L307-REC   FROM WS-MAIL-RPT9.                             
            WRITE BP13L307-REC   FROM WS-MAIL-RPT10.                            
            WRITE BP13L307-REC   FROM WS-MAIL-RPT11.                            
            WRITE BP13L307-REC   FROM WS-MAIL-RPT11A.                           
            WRITE BP13L307-REC   FROM WS-MAIL-RPT11B.                           
            WRITE BP13L307-REC   FROM WS-MAIL-RPT11C.                           
            WRITE BP13L307-REC   FROM WS-MAIL-RPT11D.                           
            WRITE BP13L307-REC   FROM WS-MAIL-RPT11E.                           
            WRITE BP13L307-REC   FROM WS-MAIL-RPT11F.                           
            WRITE BP13L307-REC   FROM WS-MAIL-RPT11H.                           
            WRITE BP13L307-REC   FROM WS-MAIL-RPT12.                            
            WRITE BP13L307-REC   FROM WS-MAIL-RPT13.                            
            MOVE L307-CURRENT-DATE TO WS-DATE-RPT.                              
            WRITE BP13L307-REC   FROM WS-DATE-RPT-DTL.                          
                                                                                
            MOVE SPACES            TO BP13L307-REC.                             
            WRITE BP13L307-REC.                                                 
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       7000-CHK-REPORT-STATUS.                                                  
      ******************************************************************        
           IF WS-SEQ-NO = ZERO                                                  
              PERFORM 0650-PRINT-HEADING THRU 0650-EXIT                         
              WRITE   BP13L307-REC       FROM PRT-L307-NO-CASE AFTER 25         
           END-IF.                                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       8000-DISPLAY-STATISTICS.                                                 
      ******************************************************************        
            MOVE    WS-BP13F800-READ-CNT  TO  WS-F800-READ-CNT.                 
            MOVE    WS-BP13K330-READ-CNT  TO  WS-K330-READ-CNT.                 
            MOVE    WS-BP13K006-READ-CNT  TO  WS-K006-READ-CNT.                 
            MOVE    WS-BP13K022-READ-CNT  TO  WS-K022-READ-CNT.                 
            MOVE    WS-SEQ-NO             TO  WS-PRINT-CNT.                     
                                                                                
            DISPLAY '************* BP13C307 ******************'.                
            DISPLAY ' RECORDS READ FROM BP13F800  : ' WS-F800-READ-CNT.         
            MOVE    WS-BP13F800-PROC-CNT  TO  WS-F800-READ-CNT.                 
            DISPLAY ' RECORDS PROCESSED FROM F800 : ' WS-F800-READ-CNT.         
            DISPLAY ' RECORDS READ FROM BP13K330  : ' WS-K330-READ-CNT.         
            DISPLAY ' RECORDS READ FROM BP13K022  : ' WS-K022-READ-CNT.         
            DISPLAY ' RECORDS READ FROM B05K006   : ' WS-K006-READ-CNT.         
            DISPLAY ' NO OF RECORDS PRINTED       : ' WS-PRINT-CNT.             
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-FILES.                                                        
      ******************************************************************        
            CLOSE  BP13K330                                                     
                   BP13K022                                                     
                   BP13F800                                                     
                   B05K006                                                      
                   BP13L307.                                                    
                                                                                
            IF WS-K330-STATUS NOT = 00 AND 97                                   
               DISPLAY 'ERROR CLOSING BP13K330, STAT = ' WS-K330-STATUS         
               MOVE     WS-K330-STATUS      TO  RETURN-CODE                     
            END-IF.                                                             
                                                                                
            IF WS-K022-STATUS NOT = 00 AND 97                                   
               DISPLAY 'ERROR CLOSING BP13K022, STAT = ' WS-K022-STATUS         
               MOVE     WS-K022-STATUS      TO  RETURN-CODE                     
            END-IF.                                                             
                                                                                
            IF WS-K006-STATUS NOT = 00 AND 97                                   
               DISPLAY 'ERROR CLOSING B05K006 , STAT = ' WS-K006-STATUS         
               MOVE     WS-K006-STATUS      TO  RETURN-CODE                     
            END-IF.                                                             
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
