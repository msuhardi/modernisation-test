      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID. BP13C35A.                                                    
       AUTHOR. ALWYN BENNY.                                                     
                                                                                
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  THIS IS A PROGRAM TO UPDATE SOC MASTER &      *                
      *          FROM SUB-TRANSFER FILE FOR COURT ORDER NON RFD*                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *      1. SORTED SUB-TRANSFEE FILE    (BP13F880)         *                
      *          FOR DATE-SUB-TRANSFER NOT = SPACES            *                
      *                                                        *                
      *     I-O    FILES:                                      *                
      *      1. SOC MASTER (BP13K800)                          *                
      *      2. SOC EX-APPLICANT FILE (BP13K885)               *                
      *      3. SOC ACCUM FILE   (BP13K330)                    *                
      *      4. TRANSACTION FILE (BP13K130)                    *                
      *                                                        *                
      *                                                        *                
      *     OUTPUT FILES:                                      *                
      *      1. KIV FILE (TRANS FORMAT UNSUCCCESSFUL) BP13K800 *                
      *--------------------------------------------------------*                
      *  REVISION HISTORY:                                     *                
      *  CHGNO   BY  DATE            DETAILS:                  *                
      *  =====   ==  ====            ========                  *                
      * BP134561 AB9 18/07/2012       NEW PROGRAM              *                
      **********************************************************                
                                                                                
      *--------------------*                                                    
       ENVIRONMENT DIVISION.                                                    
      *--------------------*                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3890.                                               
       OBJECT-COMPUTER. IBM-3890.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K800-NUM-REGN                                   
                  ACCESS MODE IS RANDOM                                         
                  FILE STATUS IS WS-K800-FILE-STATUS.                           
                                                                                
           SELECT BP13K330 ASSIGN TO BP13K330                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K330-KEY-FLD                               
                  FILE STATUS     IS WS-K330-STATUS.                            
                                                                                
           SELECT BP13K885 ASSIGN TO BP13K885                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K885-KEY-FLD                               
                  FILE STATUS     IS WS-K885-STATUS.                            
                                                                                
           SELECT BP13K130 ASSIGN         TO BP13K130                           
                  ACCESS MODE    IS DYNAMIC                                     
                  ORGANIZATION   IS INDEXED                                     
                  RECORD KEY     IS K130-KEY-FLD                                
                  FILE STATUS    IS WS-K130-STATUS.                             
                                                                                
           SELECT BP13F880 ASSIGN TO BP13F880.                                  
           SELECT BP13L35A ASSIGN TO BP13L35A.                                  
                                                                                
                                                                                
      *-------------*                                                           
       DATA DIVISION.                                                           
      *-------------*                                                           
       FILE SECTION.                                                            
       FD  BP13K800                                                             
                                   RECORD CONTAINS 2000 CHARACTERS.             
       COPY BP13K800.                                                           
       FD  BP13K130                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13K130.                                                           
                                                                                
       FD   BP13K330                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K330.                                                           
                                                                                
       FD   BP13K885                                                            
            RECORD CONTAINS 400  CHARACTERS.                                    
       COPY BP13K885.                                                           
       FD  BP13F880                                                             
                                   BLOCK CONTAINS 0 RECORDS                     
                                   RECORDING MODE  IS  F                        
                                   RECORD CONTAINS 2000 CHARACTERS              
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F880.                                                           
       FD  BP13L35A                                                             
                                   RECORDING MODE  IS  F                        
                                   LABEL RECORDS ARE OMITTED.                   
       01  BP13L35A-REC                PIC X(133).                              
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       77  WS-K800-FILE-STATUS     PIC 99       VALUE ZERO.                     
       77  WS-KD05-FILE-STATUS     PIC 99       VALUE ZERO.                     
       77  WS-K130-STATUS          PIC 99       VALUE ZERO.                     
       77  WS-K330-STATUS          PIC 99       VALUE ZERO.                     
       77  WS-K885-STATUS          PIC 99       VALUE ZERO.                     
       77  WS-F880-INPUT           PIC 9(7)     VALUE ZERO.                     
       77  WS-K800-REWRITE         PIC 9(7)     VALUE ZERO.                     
       77  WS-F800-WRITE           PIC 9(7)     VALUE ZERO.                     
       77  WS-KD05-REWRITE         PIC 9(7)     VALUE ZERO.                     
       77  WS-KD05-FOUND           PIC 9(7)     VALUE ZERO.                     
       77  WS-KD05-NOTFND          PIC 9(7)     VALUE ZERO.                     
       77  WS-K130-WRITE           PIC 9(7)     VALUE ZERO.                     
       77  WS-K885-REWRITE         PIC 9(7)     VALUE ZERO.                     
       77  WS-K330-UPDATE          PIC 9(7)     VALUE ZERO.                     
       77  WS-LINE                 PIC 9(2)     VALUE 60.                       
       77  WS-PAGE                 PIC 9(3)     VALUE ZEROES.                   
       77  WS-EOF-F880             PIC X(3)     VALUE 'NO '.                    
           88  EOF-OF-F880                      VALUE 'YES'.                    
       77  WS-FND-KD05             PIC X(1)     VALUE SPACE.                    
           88 FND-KD05                          VALUE 'Y'.                      
       77  WS-AMT-USED             PIC S9(5)V99 VALUE ZEROES.                   
       01  WS-K330-FOUND           PIC X(1)     VALUE 'N'.                      
       01  WS-K330-EOF             PIC X(01)    VALUE 'N'.                      
       01  WS-SEQ                  PIC 9(3)     VALUE 0.                        
                                                                                
       01  WS-STORE-FIELDS.                                                     
           05 WS-CD                PIC S9(7)V99 VALUE +0.                       
           05 WS-AMT-REFUND        PIC S9(7)V99 VALUE +0.                       
           05 WS-ADDR-UPD          PIC X(1)     VALUE SPACES.                   
                                                                                
       01  WS-TRANS-DATE.                                                       
           05 WS-TRANS-CC          PIC X(2).                                    
           05 WS-TRANS-YYMMDD.                                                  
              10 WS-TRANS-YY       PIC X(2).                                    
              10 WS-TRANS-MM       PIC X(2).                                    
              10 WS-TRANS-DD       PIC X(2).                                    
                                                                                
      *----------------------------------------*                                
      * STORE FIELDS FOR COMPUTE-REFUND-AMOUNT *                                
      *----------------------------------------*                                
       01  WS-STORE-FOR-CRA.                                                    
           05 WS-REFUND-CASH       PIC S9(7)V99 VALUE 0.                        
           05 WS-REFUND-CPF1       PIC S9(7)V99 VALUE 0.                        
           05 WS-REFUND-CPF2       PIC S9(7)V99 VALUE 0.                        
           05 WS-REFUND-CPF3       PIC S9(7)V99 VALUE 0.                        
           05 WS-REFUND-CPF4       PIC S9(7)V99 VALUE 0.                        
           05 WS-NON-REFUND-CPF1   PIC S9(7)V99 VALUE 0.                        
           05 WS-NON-REFUND-CPF2   PIC S9(7)V99 VALUE 0.                        
           05 WS-NON-REFUND-CPF3   PIC S9(7)V99 VALUE 0.                        
           05 WS-NON-REFUND-CPF4   PIC S9(7)V99 VALUE 0.                        
           05 WS-AMT-SOC-PAID-CPF1 PIC S9(7)V99 VALUE 0.                        
           05 WS-AMT-SOC-PAID-CPF2 PIC S9(7)V99 VALUE 0.                        
           05 WS-AMT-SOC-PAID-CPF3 PIC S9(7)V99 VALUE 0.                        
           05 WS-AMT-SOC-PAID-CPF4 PIC S9(7)V99 VALUE 0.                        
                                                                                
       01  WS-K330-REC             PIC X(2000) VALUE SPACES.                    
                                                                                
       01  WS-EDITED-DATE.                                                      
           05 WS-EDITED-DD         PIC X(2)    VALUE SPACES.                    
           05 FILLER               PIC X       VALUE '/'.                       
           05 WS-EDITED-MM         PIC X(2)    VALUE SPACES.                    
           05 FILLER               PIC X       VALUE '/'.                       
           05 WS-EDITED-CCYY       PIC X(4)    VALUE SPACES.                    
                                                                                
       01  WS-CURR-DATE.                                                        
           05 WS-CURR-CENT         PIC X(2)    VALUE SPACES.                    
           05 WS-CURR-DTE.                                                      
              10 WS-CURR-YY        PIC X(2)    VALUE SPACES.                    
              10 WS-CURR-MM        PIC X(2)    VALUE SPACES.                    
              10 WS-CURR-DD        PIC X(2)    VALUE SPACES.                    
                                                                                
       01 WS-OUTGOING-AMT-SF-CPF.                                               
             05 WS-OUTGOING-SF-CPF           PIC S9(5)V99 VALUE ZEROS.          
       01 WS-OUTGOING-AMT-CF-CPF.                                               
             05 WS-OUTGOING-CF-CPF           PIC S9(5)V99 VALUE ZEROS.          
                                                                                
       01  WS-MATCH-TABLE.                                                      
           05 WS-TROR-TREE-MATCH OCCURS 4 TIMES.                                
              10 WS-TROR-MATCH-NO   PIC 9(2).                                   
              10 WS-TREE-MATCH-NO   PIC 9(2).                                   
                                                                                
       01  WS-TROR               PIC 9(02) VALUE ZEROES.                        
       01  WS-TREE               PIC 9(02) VALUE ZEROES.                        
       01  WS-CNT                PIC 9(02) VALUE ZEROES.                        
       01  WS-K130-WRITTEN       PIC X(1)  VALUE SPACES.                        
       01  WS-COUNT-HISTORY      PIC 9(3)  VALUE ZEROS.                         
                                                                                
       01  WS-OUTGOING-CD.                                                      
           05 WS-OUTGOING-CD-CPF       PIC S9(7)V99  VALUE ZEROS.               
       01 WS-F880-AMT-NON-RFD-BAL      PIC S9(7)V99  VALUE ZEROS.               
       01 WS-F880-AMT-NON-RFD-CD       PIC S9(7)V99  VALUE ZEROS.               
       01 WS-BP13F880-REC.                                                      
          05 FILLER                          PIC X(35).                         
          05 WS-F880-TROR OCCURS 4 TIMES.                                       
             10 WS-F880-NRIC-TROR            PIC X(09).                         
             10 WS-F880-SHR-TROR             PIC X(03).                         
             10 WS-F880-DEMISE-TROR          PIC X(01).                         
          05 FILLER                          PIC X(11).                         
          05 WS-F880-TREE OCCURS 4 TIMES.                                       
             10 WS-F880-NRIC-TREE            PIC X(09).                         
             10 WS-F880-SHR-TREE             PIC X(03).                         
             10 WS-F880-DEMISE-TREE          PIC X(01).                         
          05 FILLER                          PIC X(211).                        
          05 WS-F880-AMT-SF-CASH             PIC S9(5)V99.                      
          05 WS-F880-SF-CPF OCCURS 4 TIMES.                                     
             10 WS-F880-AMT-SF-CPF           PIC S9(5)V99.                      
          05 FILLER                          PIC X(15).                         
          05 WS-F880-AMT-CF-CASH             PIC S9(5)V99.                      
          05 WS-F880-CF-CPF OCCURS 4 TIMES.                                     
             10 WS-F880-AMT-CF-CPF           PIC S9(5)V99.                      
          05 FILLER                          PIC X(54).                         
          05 WS-F880-AMT-CD-CASH             PIC S9(7)V99.                      
          05 WS-F880-CD-CPF OCCURS 4 TIMES.                                     
             10 WS-F880-AMT-CD-CPF           PIC S9(7)V99.                      
          05 FILLER                          PIC X(63).                         
          05 WS-F880-REFUND-CPF OCCURS 4 TIMES.                                 
             10 WS-F880-AMT-REFUND-CPF       PIC S9(7)V99.                      
          05 FILLER                          PIC X(188).                        
          05 WS-F880-AMT-TREE-SF-CASH        PIC S9(5)V99.                      
          05 WS-F880-TREE-SF-CPF OCCURS 4 TIMES.                                
             10 WS-F880-AMT-TREE-SF-CPF      PIC S9(5)V99.                      
          05 WS-F880-AMT-TREE-CF-CASH        PIC S9(5)V99.                      
          05 WS-F880-TREE-CF-CPF OCCURS 4 TIMES.                                
             10 WS-F880-AMT-TREE-CF-CPF      PIC S9(5)V99.                      
          05 FILLER                          PIC X(201).                        
          05 WS-NUM-DIVORCE                  PIC X(01).                         
          05 FILLER                          PIC X(216).                        
          05 WS-F880-AMT-CAVEAT-CASH         PIC S9(3)V99.                      
          05 WS-F880-CAVEAT-CPF OCCURS 4 TIMES.                                 
             10 WS-F880-AMT-CAVEAT-CPF       PIC S9(3)V99.                      
          05 FILLER                          PIC X(45).                         
          05 WS-F880-AMT-MORT-CF-CASH        PIC S9(5)V99.                      
          05 WS-F880-MORT-CF-CPF OCCURS 4 TIMES.                                
             10 WS-F880-AMT-MORT-CF-CPF      PIC S9(5)V99.                      
          05 FILLER                          PIC X(110).                        
          05 WS-F880-AMT-TREE-CAVEAT-CASH    PIC S9(3)V99.                      
          05 WS-F880-TREE-CAVEAT-CPF OCCURS 4 TIMES.                            
             10 WS-F880-AMT-TREE-CAVEAT-CPF  PIC S9(3)V99.                      
          05 FILLER                          PIC X(45).                         
          05 WS-F880-AMT-TREE-MORT-CF-CASH   PIC S9(5)V99.                      
          05 WS-F880-TREE-MORT-CF-CPF OCCURS 4 TIMES.                           
             10 WS-F880-AMT-TREE-MORT-CF-CPF PIC S9(5)V99.                      
          05 FILLER                          PIC X(270).                        
          05 WS-NUM-CORDER-REFUND            PIC X(01).                         
          05 WS-F880-TREE-NON-RFD-CPF OCCURS 4 TIMES.                           
             10 WS-F880-AMT-TREE-NON-RFD-CPF PIC S9(7)V99.                      
          05 WS-F880-AMT-TREE-NON-RFD-TOT    PIC S9(7)V99.                      
          05 FILLER                          PIC X(44).                         
                                                                                
       01  PRINT-ERR1.                                                          
           05 FILLER               PIC X(10)   VALUE 'BP13-L35A'.               
           05 FILLER               PIC X(04)   VALUE SPACES.                    
           05 FILLER               PIC X(86)   VALUE                            
              'ERROR LIST FOR UPDATING SUB-TRANSFER INFORMATION TO SOC          
      -        'MASTER & OCCUPIER MASTER'.                                      
           05 FILLER               PIC X(7)    VALUE 'PAGE : '.                 
           05 PRINT-PAGE           PIC ZZZ9.                                    
           05 FILLER               PIC X(2)    VALUE SPACES.                    
           05 FILLER               PIC X(7)    VALUE 'DATE : '.                 
           05 PRINT-DATE1          PIC X(10)   VALUE SPACES.                    
                                                                                
       01  PRINT-ERR2.                                                          
           05 FILLER               PIC X(2)    VALUE SPACES.                    
           05 FILLER               PIC X(7)    VALUE 'REGN NO'.                 
           05 FILLER               PIC X(5)    VALUE SPACES.                    
           05 FILLER               PIC X(10)   VALUE 'REMARKS'.                 
                                                                                
       01  PRINT-ERR3.                                                          
           05 FILLER               PIC X(2)    VALUE SPACES.                    
           05 PRT-NUM-REGN         PIC X(8)    VALUE SPACES.                    
           05 FILLER               PIC X(4)    VALUE SPACES.                    
           05 PRT-REMARKS          PIC X(60)   VALUE SPACES.                    
                                                                                
       01  PRINT-HDG1.                                                          
           05 FILLER               PIC X(10)   VALUE 'BP13-L35A'.               
           05 FILLER               PIC X(11)   VALUE SPACES.                    
           05 FILLER               PIC X(79)   VALUE                            
              'CONTROL LIST FOR UPDATING SOC ON SUB-TRANSFER CASES   '.         
           05 FILLER               PIC X(13)   VALUE SPACES.                    
           05 FILLER               PIC X(7)    VALUE 'DATE : '.                 
           05 PRINT-DATE           PIC X(10)   VALUE SPACES.                    
                                                                                
       01  PRINT-REC.                                                           
           05 FILLER               PIC X(10)   VALUE SPACES.                    
           05 PRINT-DESC           PIC X(30)   VALUE SPACES.                    
           05 PRINT-TOTAL          PIC ZZZZZZZ9.                                
                                                                                
                                                                                
      *------------------*                                                      
       PROCEDURE DIVISION.                                                      
      *------------------*                                                      
      ******************************************************************        
       0000-INITIALIZATION.                                                     
      ******************************************************************        
           OPEN INPUT  BP13F880                                                 
                I-O    BP13K800                                                 
                       BP13K885                                                 
                       BP13K130                                                 
                       BP13K330                                                 
                OUTPUT BP13L35A.                                                
                                                                                
                                                                                
           IF WS-K800-FILE-STATUS NOT = ZEROES AND 97                           
              DISPLAY 'BP13K800 - OPEN ERROR ' WS-K800-FILE-STATUS              
              MOVE WS-K800-FILE-STATUS TO RETURN-CODE                           
              PERFORM 9999-FINALIZATION.                                        
                                                                                
           IF WS-K130-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K130 - OPEN ERROR ' WS-K130-STATUS                   
              MOVE WS-K130-STATUS TO RETURN-CODE                                
              PERFORM 9999-FINALIZATION.                                        
                                                                                
           IF WS-K330-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K330 - OPEN ERROR ' WS-K330-STATUS                   
              MOVE WS-K330-STATUS TO RETURN-CODE                                
              PERFORM 9999-FINALIZATION.                                        
                                                                                
           IF WS-K885-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13K885 - OPEN ERROR ' WS-K885-STATUS                   
              MOVE WS-K885-STATUS TO RETURN-CODE                                
              PERFORM 9999-FINALIZATION.                                        
                                                                                
           STRING FUNCTION CURRENT-DATE(7:2) '/'                                
                  FUNCTION CURRENT-DATE(5:2) '/'                                
                  FUNCTION CURRENT-DATE(1:4)                                    
           DELIMITED BY SIZE INTO WS-EDITED-DATE.                               
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE                           
                                         WS-TRANS-DATE.                         
                                                                                
           MOVE WS-EDITED-DATE TO PRINT-DATE.                                   
           MOVE WS-EDITED-DATE TO PRINT-DATE1.                                  
                                                                                
                                                                                
      ******************************************************************        
       0020-MAIN-PROCESS.                                                       
      ******************************************************************        
           PERFORM 0100-READ-BP13F880 THRU 0100-EXIT.                           
           PERFORM 0200-UPDATE-SOC    THRU 0200-EXIT                            
                   UNTIL EOF-OF-F880.                                           
           PERFORM 8000-PRINT-CONTROL THRU 8000-EXIT.                           
           PERFORM 9999-FINALIZATION.                                           
                                                                                
                                                                                
      ******************************************************************        
       0100-READ-BP13F880.                                                      
      ******************************************************************        
           READ BP13F880 AT END                                                 
                MOVE 'YES' TO WS-EOF-F880                                       
                GO TO 0100-EXIT.                                                
                                                                                
           ADD 1 TO WS-F880-INPUT.                                              
                                                                                
       0100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       0200-UPDATE-SOC.                                                         
      ******************************************************************        
              MOVE ZEROES         TO WS-AMT-REFUND.                             
              MOVE F880-NUM-REGN  TO K800-NUM-REGN.                             
                                                                                
              READ BP13K800                                                     
                                                                                
              IF WS-K800-FILE-STATUS = ZEROES                                   
                 PERFORM 2100-UPD-AMOUNT-INFO  THRU 2100-EXIT                   
                 PERFORM 2200-REWRITE-BP13K800 THRU 2200-EXIT                   
                 PERFORM 2300-READ-BP13K330    THRU 2300-EXIT                   
                 PERFORM 2350-REWRITE-BP13K885 THRU 2350-EXIT                   
              ELSE                                                              
              IF WS-K800-FILE-STATUS = '23'                                     
                 MOVE F880-NUM-REGN               TO PRT-NUM-REGN               
                 MOVE 'RECORD NOT IN SOC MASTER ' TO PRT-REMARKS                
                 PERFORM 2400-PRINT-ERR THRU 2400-EXIT                          
              ELSE                                                              
                 DISPLAY 'ERROR IN READING BP13K800'                            
                          WS-K800-FILE-STATUS                                   
                 MOVE WS-K800-FILE-STATUS TO RETURN-CODE                        
                 GO TO 9999-FINALIZATION                                        
              END-IF                                                            
              END-IF.                                                           
                                                                                
           PERFORM 0100-READ-BP13F880   THRU 0100-EXIT.                         
                                                                                
       0200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      ******************************************************************        
       2100-UPD-AMOUNT-INFO.                                                    
      ******************************************************************        
                                                                                
           PERFORM 2110-FMT-SOC-REQ-AMT   THRU 2110-EXIT.                       
           PERFORM 2120-FMT-SOC-PD-AMT    THRU 2120-EXIT.                       
           PERFORM 2130-WRITE-HISTORY     THRU 2130-EXIT.                       
                                                                                
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
      * COMPUTE THE NEW K800 SOC REQ. AMOUNT                          *         
      *---------------------------------------------------------------*         
      ******************************************************************        
       2110-FMT-SOC-REQ-AMT.                                                    
      ******************************************************************        
      *--------------------------------*                                        
      * COMPUTE K800-AMT-SOC-CPF1-REQ  *                                        
      *--------------------------------*                                        
               COMPUTE K800-AMT-SOC-CPF1-REQ = K800-AMT-SOC-CPF1-REQ            
                                             + F880-AMT-NON-REFUND-CPF1         
               COMPUTE K800-AMT-SOC-CPF2-REQ = K800-AMT-SOC-CPF2-REQ            
                                             + F880-AMT-NON-REFUND-CPF2         
               COMPUTE K800-AMT-SOC-CPF3-REQ = K800-AMT-SOC-CPF3-REQ            
                                             + F880-AMT-NON-REFUND-CPF3         
               COMPUTE K800-AMT-SOC-CPF4-REQ = K800-AMT-SOC-CPF4-REQ            
                                             + F880-AMT-NON-REFUND-CPF4.        
                                                                                
       2110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2120-FMT-SOC-PD-AMT.                                                     
      ******************************************************************        
      *---------------------------------------------------------------*         
      * COMPUTE THE NEW K800 SOC PAID AMOUNTS.                        *         
      *---------------------------------------------------------------*         
                                                                                
      *--------------------------------*                                        
      * COMPUTE K800-AMT-CPF1-PAID     *                                        
      *--------------------------------*                                        
           DISPLAY 'K800-BF '   K800-AMT-CPF1-PAID.                             
              COMPUTE K800-AMT-CPF1-PAID = K800-AMT-CPF1-PAID                   
                                         + F880-AMT-NON-REFUND-CPF1.            
                                                                                
           DISPLAY 'K800-AF '   K800-AMT-CPF1-PAID.                             
                                                                                
      *--------------------------------*                                        
      * COMPUTE K800-AMT-CPF2-PAID     *                                        
      *--------------------------------*                                        
           DISPLAY 'K800-BF '   K800-AMT-CPF2-PAID.                             
              COMPUTE K800-AMT-CPF2-PAID = K800-AMT-CPF2-PAID                   
                                         + F880-AMT-NON-REFUND-CPF2             
           DISPLAY 'K800-AF '   K800-AMT-CPF2-PAID.                             
                                                                                
      *--------------------------------*                                        
      * COMPUTE K800-AMT-CPF3-PAID     *                                        
      *--------------------------------*                                        
              COMPUTE K800-AMT-CPF3-PAID = K800-AMT-CPF3-PAID                   
                                         + F880-AMT-NON-REFUND-CPF3.            
                                                                                
      *--------------------------------*                                        
      * COMPUTE K800-AMT-CPF4-PAID     *                                        
      *--------------------------------*                                        
              COMPUTE K800-AMT-CPF4-PAID = K800-AMT-CPF4-PAID                   
                                         + F880-AMT-NON-REFUND-CPF4.            
                                                                                
       2120-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2130-WRITE-HISTORY.                                                      
      ******************************************************************        
                                                                                
            INITIALIZE WS-BP13F880-REC.                                         
            MOVE SPACES                    TO   WS-BP13F880-REC.                
            MOVE BP13F880-REC              TO   WS-BP13F880-REC.                
            PERFORM 2140-MATCH-TROR-TREE  THRU   2140-EXIT.                     
            PERFORM 2150-WRITE-CPFOUT     THRU   2150-EXIT.                     
            PERFORM 2160-WRITE-CPFIN      THRU   2160-EXIT.                     
            PERFORM 2170-FMT-CD-PD-AMT    THRU   2170-EXIT.                     
                                                                                
       2130-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------*                
       2140-MATCH-TROR-TREE.                                                    
      *--------------------------------------------------------*                
           INITIALIZE  WS-MATCH-TABLE.                                          
                                                                                
           PERFORM VARYING WS-TREE FROM 1 BY 1 UNTIL WS-TREE > 4                
              IF WS-F880-NRIC-TREE (WS-TREE) > SPACES                           
                 PERFORM VARYING WS-TROR FROM 1 BY 1 UNTIL WS-TROR > 4          
                    IF (WS-F880-NRIC-TREE (WS-TREE) =                           
                                   WS-F880-NRIC-TROR (WS-TROR))                 
                       MOVE WS-TROR  TO WS-TREE-MATCH-NO (WS-TREE)              
                       MOVE WS-TREE  TO WS-TROR-MATCH-NO (WS-TROR)              
                    END-IF                                                      
                 END-PERFORM                                                    
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       2140-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2150-WRITE-CPFOUT.                                                       
      *--------------------------------------------------------*                
                                                                                
           MOVE  0  TO WS-COUNT-HISTORY.                                        
           PERFORM VARYING WS-TROR FROM 1 BY 1 UNTIL WS-TROR > 4                
              IF WS-TROR-MATCH-NO (WS-TROR)= 0                                  
                 IF WS-F880-AMT-REFUND-CPF (WS-TROR) > 0                        
                    ADD 1                     TO   WS-COUNT-HISTORY             
                    PERFORM 2151-WRITE-K130    THRU    2151-EXIT                
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
                                                                                
       2150-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *--------------------------------------------------------*                
       2151-WRITE-K130.                                                         
      *--------------------------------------------------------*                
                                                                                
           PERFORM 2152-MOVE-VALUES-TO-K130 THRU 2152-EXIT.                     
                                                                                
           WRITE BP13K130-CDHIST.                                               
                                                                                
           IF WS-K130-STATUS = '00'                                             
              ADD 1                        TO   WS-K130-WRITE                   
           ELSE                                                                 
              IF WS-K130-STATUS = '22'                                          
                 MOVE 'N' TO WS-K130-WRITTEN                                    
                 PERFORM 2153-READ-BP13K130 THRU 2153-EXIT UNTIL                
                             WS-K130-WRITTEN = 'Y'                              
              ELSE                                                              
                 MOVE WS-K130-STATUS       TO    RETURN-CODE                    
                 DISPLAY 'ERROR READING BP13K130. STATUS : '                    
                                                  WS-K130-STATUS                
                 PERFORM 9999-FINALIZATION.                                     
                                                                                
                                                                                
       2151-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------*                
       2152-MOVE-VALUES-TO-K130.                                                
      *--------------------------------------------------------*                
                                                                                
           MOVE  SPACES                TO    BP13K130-CDHIST.                   
           INITIALIZE                        BP13K130-CDHIST.                   
           MOVE '45'                   TO    K130-TRANS-TYPE.                   
           MOVE F880-NUM-REGN          TO    K130-NUM-ORIG-REGN.                
           MOVE WS-F880-NRIC-TROR (WS-TROR) TO   K130-NUM-NRIC.                 
           MOVE WS-TRANS-DATE          TO    K130-DTE-POST                      
                                             K130-DTE-TRANS.                    
           MOVE WS-COUNT-HISTORY       TO    K130-COUNT-HISTORY.                
           IF WS-F880-AMT-REFUND-CPF (WS-TROR) NOT NUMERIC                      
              MOVE ZEROS               TO                                       
                                       WS-F880-AMT-REFUND-CPF (WS-TROR)         
           END-IF.                                                              
           COMPUTE  K130-AMT-CASH =                                             
                                 WS-F880-AMT-REFUND-CPF (WS-TROR) * -1.         
           MOVE 'D'                    TO    K130-AMT-TYP.                      
           MOVE K800-NUM-SCH-ACC       TO    K130-NUM-SCH-ACCT.                 
           MOVE 'SOC'                  TO    K130-PAYMENT-TYPE.                 
           MOVE SPACES                 TO    K130-NME-NAME                      
                                             K130-CDE-ERROR                     
                                             K130-CDE-VALID                     
                                             K130-CDE-CANCEL                    
                                             K130-CDE-BO                        
                                             K130-NUM-FLAT-TYPE-SA              
                                             K130-NUM-FLAT-TYPE-RSL             
                                             K130-NUM-CPF-APPL                  
                                             K130-NUM-RECPT-JRNO                
                                             K130-NUM-RECEIPT-CODE.             
                                                                                
       2152-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2153-READ-BP13K130.                                                      
      ******************************************************************        
                                                                                
           MOVE F880-NUM-REGN          TO    K130-NUM-ORIG-REGN.                
           MOVE WS-TRANS-DATE          TO    K130-DTE-POST.                     
           ADD 1                       TO    WS-COUNT-HISTORY.                  
           MOVE WS-COUNT-HISTORY       TO    K130-COUNT-HISTORY.                
                                                                                
           READ BP13K130.                                                       
                                                                                
           IF WS-K130-STATUS = '00'                                             
              NEXT SENTENCE                                                     
           ELSE                                                                 
              IF  WS-K130-STATUS = '23'                                         
                  MOVE 'Y'  TO WS-K130-WRITTEN                                  
                  PERFORM 2151-WRITE-K130          THRU 2151-EXIT.              
                                                                                
       2153-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2160-WRITE-CPFIN.                                                        
      *--------------------------------------------------------*                
                                                                                
           MOVE  0  TO WS-COUNT-HISTORY.                                        
           PERFORM VARYING WS-TREE FROM 1 BY 1 UNTIL WS-TREE > 4                
      *       IF WS-TREE-MATCH-NO (WS-TREE) NOT = 0                             
                 IF WS-F880-AMT-TREE-NON-RFD-CPF ( WS-TREE) > 0                 
                    ADD 1                     TO   WS-COUNT-HISTORY             
                    PERFORM 2161-WRITE-K130    THRU    2161-EXIT                
                 END-IF                                                         
      *       END-IF                                                            
           END-PERFORM.                                                         
                                                                                
                                                                                
       2160-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------*                
       2161-WRITE-K130.                                                         
      *--------------------------------------------------------*                
                                                                                
           PERFORM 2162-MOVE-VALUES-TO-K130 THRU 2162-EXIT.                     
                                                                                
           WRITE BP13K130-CDHIST.                                               
                                                                                
           IF WS-K130-STATUS = '00'                                             
              ADD 1                        TO   WS-K130-WRITE                   
           ELSE                                                                 
              IF WS-K130-STATUS = '22'                                          
                 MOVE 'N' TO WS-K130-WRITTEN                                    
                 PERFORM 2163-READ-BP13K130 THRU 2163-EXIT UNTIL                
                             WS-K130-WRITTEN = 'Y'                              
              ELSE                                                              
                 MOVE WS-K130-STATUS       TO    RETURN-CODE                    
                 DISPLAY 'ERROR READING BP13K130. STATUS : '                    
                                                  WS-K130-STATUS                
                 PERFORM 9999-FINALIZATION.                                     
                                                                                
                                                                                
       2161-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------*                
       2162-MOVE-VALUES-TO-K130.                                                
      *--------------------------------------------------------*                
                                                                                
           MOVE  SPACES                TO    BP13K130-CDHIST.                   
           INITIALIZE                        BP13K130-CDHIST.                   
           MOVE '44'                   TO    K130-TRANS-TYPE.                   
           MOVE F880-NUM-REGN          TO    K130-NUM-ORIG-REGN.                
           MOVE WS-F880-NRIC-TREE (WS-TREE) TO   K130-NUM-NRIC.                 
           MOVE WS-TRANS-DATE          TO    K130-DTE-POST                      
                                             K130-DTE-TRANS.                    
           MOVE WS-COUNT-HISTORY       TO    K130-COUNT-HISTORY.                
           IF WS-F880-AMT-TREE-NON-RFD-CPF ( WS-TREE) NOT NUMERIC               
              MOVE ZEROS               TO                                       
                                WS-F880-AMT-TREE-NON-RFD-CPF ( WS-TREE)         
           END-IF.                                                              
           MOVE WS-F880-AMT-TREE-NON-RFD-CPF ( WS-TREE)                         
                                       TO    K130-AMT-CASH.                     
           MOVE 'C'                    TO    K130-AMT-TYP.                      
           MOVE K800-NUM-SCH-ACC       TO    K130-NUM-SCH-ACCT.                 
           MOVE 'SOC'                  TO    K130-PAYMENT-TYPE.                 
           MOVE SPACES                 TO    K130-NME-NAME                      
                                             K130-CDE-ERROR                     
                                             K130-CDE-VALID                     
                                             K130-CDE-CANCEL                    
                                             K130-CDE-BO                        
                                             K130-NUM-FLAT-TYPE-SA              
                                             K130-NUM-FLAT-TYPE-RSL             
                                             K130-NUM-CPF-APPL                  
                                             K130-NUM-RECPT-JRNO                
                                             K130-NUM-RECEIPT-CODE.             
                                                                                
       2162-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2163-READ-BP13K130.                                                      
      ******************************************************************        
                                                                                
           MOVE F880-NUM-REGN          TO    K130-NUM-ORIG-REGN.                
           MOVE WS-TRANS-DATE          TO    K130-DTE-POST.                     
           ADD 1                       TO    WS-COUNT-HISTORY.                  
           MOVE WS-COUNT-HISTORY       TO    K130-COUNT-HISTORY.                
                                                                                
           READ BP13K130.                                                       
                                                                                
           IF WS-K130-STATUS = '00'                                             
              NEXT SENTENCE                                                     
           ELSE                                                                 
              IF  WS-K130-STATUS = '23'                                         
                  MOVE 'Y'  TO WS-K130-WRITTEN                                  
                  PERFORM 2161-WRITE-K130          THRU 2161-EXIT.              
                                                                                
       2163-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2170-FMT-CD-PD-AMT.                                                      
      ******************************************************************        
                                                                                
            PERFORM VARYING WS-TREE FROM 1 BY 1 UNTIL WS-TREE > 4               
               EVALUATE WS-TREE                                                 
               WHEN 1                                                           
                 IF WS-F880-NRIC-TREE (WS-TREE) > SPACES                        
                    COMPUTE K800-AMT-CD-CPF1 = K800-AMT-CD-CPF1 +               
                            WS-F880-AMT-TREE-NON-RFD-CPF (WS-TREE)              
                 END-IF                                                         
               WHEN 2                                                           
                 IF WS-F880-NRIC-TREE (WS-TREE) > SPACES                        
                    COMPUTE K800-AMT-CD-CPF2 = K800-AMT-CD-CPF2 +               
                            WS-F880-AMT-TREE-NON-RFD-CPF (WS-TREE)              
                 END-IF                                                         
               WHEN 3                                                           
                 IF WS-F880-NRIC-TREE (WS-TREE) > SPACES                        
                    COMPUTE K800-AMT-CD-CPF3 = K800-AMT-CD-CPF3 +               
                            WS-F880-AMT-TREE-NON-RFD-CPF (WS-TREE)              
                 END-IF                                                         
               WHEN 4                                                           
                 IF WS-F880-NRIC-TREE (WS-TREE) > SPACES                        
                    COMPUTE K800-AMT-CD-CPF4 = K800-AMT-CD-CPF4 +               
                            WS-F880-AMT-TREE-NON-RFD-CPF (WS-TREE)              
                 END-IF                                                         
                END-EVALUATE                                                    
            END-PERFORM.                                                        
                                                                                
       2170-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       2200-REWRITE-BP13K800.                                                   
      ******************************************************************        
           MOVE WS-CURR-DATE    TO K800-DTE-UPDATE.                             
           MOVE F880-NUM-USERID TO K800-NUM-USERID.                             
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           IF WS-K800-FILE-STATUS NOT = ZEROES                                  
              DISPLAY 'ERROR IN UPDATING BP13K800 ' WS-K800-FILE-STATUS         
              GO TO 9999-FINALIZATION.                                          
                                                                                
           ADD 1 TO WS-K800-REWRITE.                                            
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------------------------------------------*            
       2300-READ-BP13K330.                                                      
      *------------------------------------------------------------*            
                                                                                
           MOVE 'N'               TO WS-K330-FOUND                              
                                     WS-K330-EOF.                               
           MOVE SPACES            TO WS-K330-REC.                               
           MOVE LOW-VALUES        TO K330-KEY-FLD.                              
           MOVE F880-NUM-REGN   TO K330-CURR-REG.                               
                                                                                
           START BP13K330 KEY >= K330-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K330-STATUS                                              
              WHEN 00                                                           
                 PERFORM 2310-READNEXT-K330 THRU 2310-EXIT                      
                   UNTIL K330-CURR-REG NOT = F880-NUM-REGN                      
                      OR WS-K330-EOF = 'Y'                                      
              WHEN 10                                                           
              WHEN 23                                                           
                 DISPLAY 'REGN NOT FOUND IN K330 - ' F880-NUM-REGN              
              WHEN OTHER                                                        
                 DISPLAY 'ERRROR READING BP13K330. STATUS - '                   
                                            WS-K330-STATUS                      
                 MOVE WS-K330-STATUS        TO RETURN-CODE                      
                 GO TO 9999-FINALIZATION                                        
           END-EVALUATE.                                                        
                                                                                
           IF WS-K330-FOUND = 'Y'                                               
               PERFORM  2320-REWRITE-BP13K330   THRU  2320-EXIT                 
           END-IF.                                                              
                                                                                
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       2310-READNEXT-K330.                                                      
      *------------------------------------------------------------*            
                                                                                
           READ BP13K330 NEXT RECORD                                            
               AT END                                                           
                  MOVE 'Y' TO WS-K330-EOF                                       
               NOT AT END                                                       
                  IF F880-NUM-REGN = K330-CURR-REG                              
                     MOVE BP13K330-REC   TO WS-K330-REC                         
                     MOVE 'Y'            TO WS-K330-FOUND                       
                  END-IF                                                        
           END-READ.                                                            
                                                                                
       2310-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------*            
       2320-REWRITE-BP13K330.                                                   
      *------------------------------------------------------------*            
                                                                                
             MOVE WS-K330-REC  TO BP13K330-REC.                                 
             PERFORM VARYING WS-CNT FROM 1 BY 1 UNTIL WS-CNT > 4                
               IF WS-F880-AMT-TREE-NON-RFD-CPF (WS-CNT)   NOT NUMERIC           
                MOVE ZEROES  TO WS-F880-AMT-TREE-NON-RFD-CPF (WS-CNT)           
               END-IF                                                           
             END-PERFORM.                                                       
             PERFORM 2321-GET-OUTGOING-SFCF   THRU 2321-EXIT.                   
             PERFORM 2322-COMP-TREE-SFCF      THRU 2322-EXIT.                   
                                                                                
             REWRITE BP13K330-REC.                                              
             EVALUATE WS-K330-STATUS                                            
                 WHEN 00                                                        
                    ADD 1  TO WS-K330-UPDATE                                    
                 WHEN OTHER                                                     
                      DISPLAY 'ERROR REWRITING BP13K330.'                       
                               WS-K330-STATUS                                   
                      MOVE WS-K330-STATUS        TO RETURN-CODE                 
                      GO TO 9999-FINALIZATION                                   
             END-EVALUATE.                                                      
                                                                                
       2320-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------*            
       2321-GET-OUTGOING-SFCF.                                                  
      *------------------------------------------------------------*            
                                                                                
      * GET OUTGOING PERSORN'S SF,CF TO ADD WITH TREE.                          
             PERFORM VARYING WS-TROR FROM 1 BY 1 UNTIL WS-TROR > 4              
               IF WS-TROR-MATCH-NO (WS-TROR) = 0 AND                            
                  WS-F880-AMT-REFUND-CPF (WS-TROR) > 0                          
                     COMPUTE WS-OUTGOING-SF-CPF = WS-OUTGOING-SF-CPF +          
                                            WS-F880-AMT-SF-CPF(WS-TROR)         
                     COMPUTE WS-OUTGOING-CF-CPF  = WS-OUTGOING-CF-CPF +         
                                            WS-F880-AMT-CF-CPF(WS-TROR)         
                 END-IF                                                         
             END-PERFORM.                                                       
                                                                                
                                                                                
       2321-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------*            
       2322-COMP-TREE-SFCF.                                                     
      *------------------------------------------------------------*            
                                                                                
      * ADD OUTGOING PERSORN'S SF,CF TO TREE.                                   
            IF  WS-F880-AMT-TREE-NON-RFD-CPF (1) > 0                            
                COMPUTE K330-AMT-LEASE-STAMP-CPF1 =                             
                        K330-AMT-LEASE-STAMP-CPF1 +                             
                        WS-OUTGOING-SF-CPF                                      
                COMPUTE K330-AMT-CONV-CPF1        =                             
                        K330-AMT-CONV-CPF1        +                             
                        WS-OUTGOING-CF-CPF                                      
            ELSE                                                                
               IF  WS-F880-AMT-TREE-NON-RFD-CPF (2) > 0                         
                COMPUTE K330-AMT-LEASE-STAMP-CPF2 =                             
                        K330-AMT-LEASE-STAMP-CPF2 +                             
                        WS-OUTGOING-SF-CPF                                      
                COMPUTE K330-AMT-CONV-CPF2        =                             
                        K330-AMT-CONV-CPF2        +                             
                        WS-OUTGOING-CF-CPF                                      
            ELSE                                                                
               IF  WS-F880-AMT-TREE-NON-RFD-CPF (3) > 0                         
                COMPUTE K330-AMT-LEASE-STAMP-CPF3 =                             
                        K330-AMT-LEASE-STAMP-CPF3 +                             
                        WS-OUTGOING-SF-CPF                                      
                COMPUTE K330-AMT-CONV-CPF3        =                             
                        K330-AMT-CONV-CPF3        +                             
                        WS-OUTGOING-CF-CPF                                      
            ELSE                                                                
               IF  WS-F880-AMT-TREE-NON-RFD-CPF (4) > 0                         
                COMPUTE K330-AMT-LEASE-STAMP-CPF4 =                             
                        K330-AMT-LEASE-STAMP-CPF4 +                             
                        WS-OUTGOING-SF-CPF                                      
                COMPUTE K330-AMT-CONV-CPF4        =                             
                        K330-AMT-CONV-CPF4        +                             
                        WS-OUTGOING-CF-CPF                                      
               END-IF.                                                          
       2322-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------*            
       2350-REWRITE-BP13K885.                                                   
      *------------------------------------------------------------*            
                                                                                
            PERFORM VARYING WS-TROR FROM 1 BY 1 UNTIL WS-TROR > 4               
                IF WS-TROR-MATCH-NO (WS-TROR) = 0 AND                           
                   WS-F880-AMT-REFUND-CPF (WS-TROR) > 0                         
                   PERFORM 2351-READ-BP13K885   THRU    2351-EXIT               
                END-IF                                                          
            END-PERFORM.                                                        
                                                                                
       2350-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------*            
       2351-READ-BP13K885.                                                      
      *------------------------------------------------------------*            
                                                                                
            MOVE F880-NUM-REGN               TO     K885-NUM-REGN.              
            MOVE WS-F880-NRIC-TROR(WS-TROR)  TO     K885-NUM-NRIC.              
            READ BP13K885.                                                      
            EVALUATE WS-K885-STATUS                                             
              WHEN 00                                                           
                 PERFORM  2352-UPD-K885   THRU  2352-EXIT                       
              WHEN 23                                                           
                 DISPLAY 'RECORD NOT FOUND -BP13K885' WS-K885-STATUS            
              WHEN OTHER                                                        
                      DISPLAY 'ERROR READING BP13K885.'                         
                               WS-K885-STATUS                                   
                      MOVE WS-K885-STATUS        TO RETURN-CODE                 
                      GO TO 9999-FINALIZATION                                   
            END-EVALUATE.                                                       
                                                                                
       2351-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------------------------------------------------*            
       2352-UPD-K885.                                                           
      *------------------------------------------------------------*            
                                                                                
            MOVE ZEROS          TO  K885-AMT-REFUND.                            
            REWRITE BP13K885-REC.                                               
            IF WS-K885-STATUS = ZEROS                                           
               ADD 1          TO  WS-K885-REWRITE                               
            ELSE                                                                
                      DISPLAY 'ERROR UPDATING BP13K885.'                        
                               WS-K885-STATUS                                   
                      MOVE WS-K885-STATUS        TO RETURN-CODE                 
                      GO TO 9999-FINALIZATION                                   
            END-IF.                                                             
                                                                                
       2352-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       2400-PRINT-ERR.                                                          
      ******************************************************************        
           IF WS-LINE > 55                                                      
              ADD 1 TO WS-PAGE                                                  
              MOVE WS-PAGE TO PRINT-PAGE                                        
              WRITE BP13L35A-REC FROM PRINT-ERR1 AFTER 1                        
              WRITE BP13L35A-REC FROM PRINT-ERR2 AFTER 3                        
              MOVE 0 TO WS-LINE                                                 
              MOVE SPACES TO BP13L35A-REC                                       
              WRITE BP13L35A-REC AFTER 2.                                       
           WRITE BP13L35A-REC FROM PRINT-ERR3 AFTER 2.                          
           ADD 1 TO WS-LINE.                                                    
                                                                                
       2400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       8000-PRINT-CONTROL.                                                      
      ******************************************************************        
           WRITE BP13L35A-REC FROM PRINT-HDG1 AFTER 1.                          
                                                                                
           MOVE SPACES TO BP13L35A-REC.                                         
           WRITE BP13L35A-REC AFTER 2.                                          
                                                                                
           MOVE 'CONTROL COUNT FOR BP13C35A' TO PRINT-DESC.                     
           WRITE BP13L35A-REC FROM PRINT-REC AFTER 2.                           
                                                                                
           MOVE WS-F880-INPUT  TO PRINT-TOTAL.                                  
           MOVE 'SUB-TRANSFER-BP13F880 READ   : ' TO PRINT-DESC.                
           WRITE BP13L35A-REC FROM PRINT-REC AFTER 2.                           
                                                                                
           MOVE WS-K800-REWRITE TO PRINT-TOTAL.                                 
           MOVE 'UPDATE SOC MASTER BP13K800   : ' TO PRINT-DESC.                
           WRITE BP13L35A-REC FROM PRINT-REC AFTER 2.                           
           MOVE WS-K330-UPDATE  TO PRINT-TOTAL.                                 
           MOVE 'RECORDS  UPDATED - BP13K330  : ' TO PRINT-DESC.                
           WRITE BP13L35A-REC FROM PRINT-REC AFTER 2.                           
                                                                                
           MOVE WS-K885-REWRITE TO PRINT-TOTAL.                                 
           MOVE 'EX APPL REF UPD  - BP13K885  : ' TO PRINT-DESC.                
           WRITE BP13L35A-REC FROM PRINT-REC AFTER 2.                           
                                                                                
           MOVE WS-K130-WRITE   TO PRINT-TOTAL.                                 
           MOVE 'HISTORY TRANS- BP13K130      : ' TO PRINT-DESC.                
           WRITE BP13L35A-REC FROM PRINT-REC AFTER 2.                           
                                                                                
           WRITE BP13L35A-REC FROM PRINT-REC AFTER 2.                           
                                                                                
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9999-FINALIZATION.                                                       
      ******************************************************************        
           CLOSE      BP13F880                                                  
                      BP13K800                                                  
                      BP13K885                                                  
                      BP13K130                                                  
                      BP13K330                                                  
                      BP13L35A.                                                 
                                                                                
           IF WS-K800-FILE-STATUS NOT = ZEROES                                  
              DISPLAY 'BP13K800 - CLOSE ERROR ' WS-K800-FILE-STATUS             
              MOVE WS-K800-FILE-STATUS TO RETURN-CODE.                          
                                                                                
           IF WS-K130-STATUS NOT = ZEROES                                       
              DISPLAY 'BP13K130 - CLOSE ERROR ' WS-K130-STATUS                  
              MOVE WS-K130-STATUS TO RETURN-CODE.                               
                                                                                
           IF WS-K330-STATUS NOT = ZEROES                                       
              DISPLAY 'BP13K330 - CLOSE ERROR ' WS-K330-STATUS                  
              MOVE WS-K330-STATUS TO RETURN-CODE.                               
                                                                                
           IF WS-K885-STATUS NOT = ZEROES                                       
              DISPLAY 'BP13K885 - CLOSE ERROR ' WS-K885-STATUS                  
              MOVE WS-K885-STATUS TO RETURN-CODE.                               
                                                                                
           STOP RUN.                                                            
      *---------------*                                                         
      * END OF SOURCE *                                                         
      *---------------*                                                         
