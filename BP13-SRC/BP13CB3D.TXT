      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13CB3D.                                                 
      *AUTHOR.        MARGE LAM KO.                                             
      *DATE-WRITTEN.  12 APRIL 2016.                                            
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : THIS PROGRAM MATCHES RETURN FILE AB02F935   *           
      *                 WITH AB02F930 BACKUP TO UPDATE GRANT_ASMNT  *           
      *                 TABLE.                                      *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  AB02F935 -- HDB GRANT RETURN FILE                     *           
      *   2.  AB02F930 -- HDB GRANT FILE                            *           
      *                                                             *           
      *   OUTPUT FILES:                                             *           
      *   1.  AB02F93A -- MATCHED CASES                             *           
      *   2.  AB02F93B -- REJECTED CASES                            *           
      *   2.  AB02F93C -- UNMATCHED CASES                           *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136182 12/04/2016 MRN1 NEW PROGRAM                        *           
      * BP136148 03/05/2016 MRN1 NEW TABLE FOR UPD LOG              *           
      * BP136190 17/05/2016 MRN1 TO UPDATE BOTH AHG/SHG             *           
      * BP136696 07/08/2017 MRN1 FIX MATCHING LOGIC                 *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT AB02F935   ASSIGN       TO AB02F935.                          
           SELECT AB02F930   ASSIGN       TO AB02F930.                          
                                                                                
           SELECT BP13KD10 ASSIGN TO BP13KD10                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD10-KEY-FLD                               
                  FILE STATUS     IS BP13KD10-STATUS.                           
                                                                                
           SELECT SY08K010 ASSIGN TO SY08K010                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K010-NUM-USERID                            
                  ALTERNATE KEY   IS K010-NUM-LOTUS-NCKNM                       
                  FILE STATUS     IS SY08K010-STATUS.                           
                                                                                
           SELECT AB02F93A   ASSIGN       TO AB02F93A.                          
           SELECT AB02F93B   ASSIGN       TO AB02F93B.                          
           SELECT AB02F93C   ASSIGN       TO AB02F93C.                          
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   AB02F935                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  200 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY AB02F935.                                                   00055007
                                                                                
       FD   AB02F930                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       01  WS-F930-REC.                                                 00055007
           05  WS-F930-REC-TYPE-D               PIC X.                          
           05  WS-F930-NUM-CPF-ACCT             PIC X(9).                       
           05  WS-F930-NUM-PREF-HDB.                                            
               10  WS-F930-NUM-PREFIX           PIC X.                          
               10  WS-F930-NUM-HDB-REF.                                         
                   15  WS-F930-NUM-SCH-ACCNT.                                   
                       20  WS-F930-NUM-SCH      PIC X(4).                       
                       20  WS-F930-NUM-ACCNT    PIC X(4).                       
                       20  WS-F930-CDE-CDG      PIC X.                          
                   15  WS-F93C-CDE-LESSEE       PIC X(2).                       
           05  WS-F930-TAG-CHANNEL              PIC X.                          
           05  WS-F930-SIGN-GRNT                PIC X(1).                       
           05  WS-F930-AMT-GRNT                 PIC 9(7)V99.                    
           05  WS-F930-SIGN-GRNT-EXCS           PIC X(1).                       
           05  WS-F930-AMT-GRNT-EXCS            PIC 9(7)V99.                    
           05  WS-F930-AMT-THRSHOLD             PIC 9(7)V99.                    
           05  WS-F930-CDE-SYSID                PIC X(4).                       
           05  WS-F930-NUM-REGN                 PIC X(8).                       
           05  WS-F930-GRANT-TYPE               PIC X(3).                       
           05  WS-F930-DATE-SENT                PIC X(8).                       
           05  WS-F930-USERID                   PIC X(8).                       
           05  FILLER                           PIC X(67).                      
                                                                                
       FD   BP13KD10                                                            
            RECORD CONTAINS  1400 CHARACTERS.                                   
       COPY BP13KD10.                                                           
                                                                                
       FD  SY08K010                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY SY08K010.                                                           
                                                                                
       FD   AB02F93A                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       01  AB02F93A-REC      PIC X(150).                                00055007
                                                                                
       FD   AB02F93B                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  200 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       01  AB02F93B-REC      PIC X(200).                                00055007
                                                                                
       FD   AB02F93C                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       01  AB02F93C-REC      PIC X(150).                                00055007
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-GRNT-FND                PIC X       VALUE SPACE.              
           05  WS-F935-EOF                PIC X       VALUE SPACE.              
           05  WS-F930-EOF                PIC X       VALUE SPACE.              
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-GRANT-TYPE              PIC X(3)    VALUE SPACES.             
           05  WS-RETURN-CODE             PIC 9(2)    VALUE ZEROES.             
           05  WS-CNT                     PIC 9(2)    VALUE ZEROES.             
           05  CNT-F935-READ              PIC 9(5)    VALUE ZEROES.             
           05  CNT-F930-READ              PIC 9(5)    VALUE ZEROES.             
           05  CNT-WRITE-F63A             PIC 9(5)    VALUE ZEROES.             
           05  CNT-WRITE-F63B             PIC 9(5)    VALUE ZEROES.             
           05  CNT-WRITE-F63C             PIC 9(5)    VALUE ZEROES.             
           05  CNT-UPD-TBL                PIC 9(5)    VALUE ZEROES.             
           05  WS-GRNT-UPD-LOG            PIC 9(5)    VALUE ZEROES.             
           05  WS-INBOX-DELETE            PIC 9(5)    VALUE ZEROES.             
           05  CNT-KD10-DELETE            PIC 9(5)    VALUE ZEROES.             
           05  BP13KD10-STATUS            PIC 9(2)    VALUE ZEROES.             
           05  SY08K010-STATUS            PIC 9(2)    VALUE ZEROES.             
                                                                                
       01  WS-DATE                        PIC X(08)   VALUE SPACES.             
       01  WS-TIME-HHMMSS.                                                      
           05 WS-TIME-HH                    PIC X(02) VALUE SPACES.             
           05 WS-TIME-MM                    PIC X(02) VALUE SPACES.             
           05 WS-TIME-SS                    PIC X(02) VALUE SPACES.             
           05 WS-TIME-MS                    PIC X(02) VALUE SPACES.             
                                                                                
       01  WS-LOG-VARIABLE.                                                     
           05  WS-BEFORE-IMAGE        PIC X(600).                               
           05  WS-AFTER-IMAGE         PIC X(600).                               
           05  WS-SOURCE              PIC X(30).                                
           05  WS-RGSTRN              PIC X(8).                                 
           05  WS-ACTION              PIC X(1).                                 
           05  WS-GRNT-TYPE           PIC X(6).                                 
           05  WS-TIMESTAMP-LOG       PIC X(26) VALUE SPACES.                   
                                                                                
       01  WS-SQL-CODES.                                                        
           05  WS-SQL-OK                   PIC S9(4) COMP VALUE +000.           
           05  WS-SQL-NFND                 PIC S9(4) COMP VALUE +100.           
           05  WS-SQL-MORE-ROWS            PIC S9(4) COMP VALUE -811.           
                                                                                
       01  WS-SQLCODE                      PIC ---9.                            
                                                                                
       COPY BE08W001.                                                           
                                                                                
      *--------------------------------------------------------------*          
      *                          SQLCA                               *          
      *--------------------------------------------------------------*          
           EXEC SQL INCLUDE SQLCA          END-EXEC.                            
           EXEC SQL INCLUDE P13VGASM       END-EXEC.                            
           EXEC SQL INCLUDE P13VGUPD       END-EXEC.                            
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F935    THRU 2000-EXIT.                            
           PERFORM 2100-READ-F930    THRU 2100-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-F930-EOF = 'Y'                                      
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  AB02F935                                                 
                       AB02F930                                                 
                       SY08K010                                                 
                I-O    BP13KD10                                                 
                OUTPUT AB02F93A                                                 
                       AB02F93B                                                 
                       AB02F93C.                                                
                                                                                
           IF BP13KD10-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KD10 : ' BP13KD10-STATUS             
              MOVE BP13KD10-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           ACCEPT WS-TIME-HHMMSS    FROM   TIME.                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F935.                                                          
      *---------------*                                                         
                                                                                
           READ AB02F935 AT END                                                 
                MOVE HIGH-VALUES TO AB02F935-DETAIL-REC                         
                MOVE 'Y' TO WS-F935-EOF                                         
                GO TO 2000-EXIT.                                                
           ADD 1 TO CNT-F935-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2100-READ-F930.                                                          
      *---------------*                                                         
                                                                                
           READ AB02F930 AT END                                                 
                MOVE HIGH-VALUES TO WS-F930-REC                                 
                MOVE 'Y' TO WS-F930-EOF                                         
                GO TO 2100-EXIT.                                                
           ADD 1 TO CNT-F930-READ.                                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
           IF AB02F935-DETAIL-REC(1:33) = WS-F930-REC(1:33)                     
              IF F935-NUM-REGN = WS-F930-NUM-REGN                               
                 PERFORM 3100-PROCESS-MATCHED  THRU 3100-EXIT                   
                 PERFORM 2000-READ-F935    THRU 2000-EXIT                       
                 PERFORM 2100-READ-F930    THRU 2100-EXIT                       
              ELSE                                                              
                 MOVE WS-F930-REC TO AB02F93C-REC                               
                 WRITE AB02F93C-REC                                             
                 ADD 1 TO CNT-WRITE-F63C                                        
                 PERFORM 2100-READ-F930    THRU 2100-EXIT                       
              END-IF                                                            
           ELSE                                                                 
              IF AB02F935-DETAIL-REC(1:33) > WS-F930-REC(1:33)                  
                 MOVE WS-F930-REC TO AB02F93C-REC                               
                 WRITE AB02F93C-REC                                             
                 ADD 1 TO CNT-WRITE-F63C                                        
                 PERFORM 2100-READ-F930 THRU 2100-EXIT                          
              ELSE                                                              
                 PERFORM 2000-READ-F935 THRU 2000-EXIT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3100-PROCESS-MATCHED.                                                    
      *---------------------*                                                   
                                                                                
           MOVE 'N'    TO WS-GRNT-FND.                                          
           IF F935-CDE-STATUS = 'ACC'                                           
              MOVE WS-F930-GRANT-TYPE TO WS-GRANT-TYPE                          
              PERFORM 3200-RETRIEVE-GRANT-ASMNT THRU 3200-EXIT                  
              IF WS-GRNT-FND = 'Y'                                              
                 PERFORM 3300-UPDATE-GRANT-ASMNT THRU 3300-EXIT                 
                 MOVE WS-F930-REC TO AB02F93A-REC                               
                 WRITE AB02F93A-REC                                             
                 ADD 1 TO CNT-WRITE-F63A                                        
                 IF WS-F930-GRANT-TYPE = 'SRG'                                  
                    PERFORM 3600-DELETE-INTRAY   THRU 3600-EXIT                 
                 END-IF                                                         
                 PERFORM 3700-READ-DEL-KD10      THRU 3700-EXIT                 
              END-IF                                                            
              IF WS-F930-GRANT-TYPE = 'AHG' OR 'SHG'                            
                 IF WS-F930-GRANT-TYPE = 'AHG'                                  
                    MOVE 'SHG' TO WS-GRANT-TYPE                                 
                 ELSE                                                           
                    IF WS-F930-GRANT-TYPE = 'SHG'                               
                       MOVE 'AHG' TO WS-GRANT-TYPE                              
                    END-IF                                                      
                 END-IF                                                         
                 PERFORM 3200-RETRIEVE-GRANT-ASMNT THRU 3200-EXIT               
                 IF WS-GRNT-FND = 'Y'                                           
                    PERFORM 3300-UPDATE-GRANT-ASMNT THRU 3300-EXIT              
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              IF F935-CDE-STATUS = 'REJ'                                        
                 MOVE AB02F935-DETAIL-REC TO AB02F93B-REC                       
                 WRITE AB02F93B-REC                                             
                 ADD 1 TO CNT-WRITE-F63B                                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3200-RETRIEVE-GRANT-ASMNT.                                               
      ******************************************************************        
           MOVE WS-F930-NUM-REGN TO NUM-RGSTRN OF DCLGRANT-ASMNT.               
           MOVE WS-GRANT-TYPE    TO NUM-GRANT-TYPE OF DCLGRANT-ASMNT.           
                                                                                
           MOVE 'N'              TO WS-GRNT-FND.                                
                                                                                
           EXEC SQL                                                             
              SELECT NUM_RGSTRN,                                                
                     NUM_GRANT_TYPE,                                            
                     NUM_ELGBL_GRANT,                                           
                     NUM_GRANT_AMOUNT_ELGBL,                                    
                     NUM_GRANT_STATUS,                                          
                     DTE_APRV,                                                  
                     DTE_CANCEL,                                                
                     DTE_REJECT,                                                
                     DTE_SUBMIT,                                                
                     DTE_WTHDRW,                                                
                     NUM_RJCTN_REASON,                                          
                     NUM_WTDRWL_REASON,                                         
                     TXT_CNCLTN_REASON,                                         
                     DTE_GRANT_RQST,                                            
                     NUM_GRANT_DOC_RCV,                                         
                     DTE_ACK,                                                   
                     DTE_GRANT_FORM_ISSUE,                                      
                     DTE_GRANT_OPT_OUT_FORM_ISSUE,                              
                     NUM_GRANT_OPT_OUT_REASON,                                  
                     DTE_GRANT_OPT_OUT_REASON,                                  
                     NUM_ALCTN_SCHEME,                                          
                     NUM_HSHLD_CTZNSP,                                          
                     NUM_WRKNG_CHILD_ASMNT,                                     
                     QTY_MONTH_WORK,                                            
                     NUM_APPEAL_ATCHMT,                                         
                     NUM_PARENT_ADRS_BLDNG,                                     
                     NUM_PARENT_ADRS_LEVEL,                                     
                     NUM_PARENT_ADRS_UNIT,                                      
                     TXT_PARENT_ADRS1,                                          
                     TXT_PARENT_ADRS2,                                          
                     NUM_PARENT_ADRS_POSTAL,                                    
                     DTE_PYMNT,                                                 
                     DTE_CPF_ACK_GRANT_RCV,                                     
                     NUM_GRANT_CREDIT,                                          
                     TME_APNTMT,                                                
                     DTE_APNTMT,                                                
                     TME_CREATE_ASMNT,                                          
                     NUM_UPDATE_USERID,                                         
                     TME_UPDATE,                                                
                     DTE_UPDATE,                                                
                     NUM_SOURCE_SYSTEM,                                         
                     NUM_ACNT,                                                  
                     NUM_SCHEME,                                                
                     NUM_TNCY,                                                  
                     NUM_HALF_GRANT                                             
              INTO  :DCLGRANT-ASMNT.NUM-RGSTRN,                                 
                    :DCLGRANT-ASMNT.NUM-GRANT-TYPE,                             
                    :DCLGRANT-ASMNT.NUM-ELGBL-GRANT,                            
                    :DCLGRANT-ASMNT.NUM-GRANT-AMOUNT-ELGBL,                     
                    :DCLGRANT-ASMNT.NUM-GRANT-STATUS,                           
                    :DCLGRANT-ASMNT.DTE-APRV,                                   
                    :DCLGRANT-ASMNT.DTE-CANCEL,                                 
                    :DCLGRANT-ASMNT.DTE-REJECT,                                 
                    :DCLGRANT-ASMNT.DTE-SUBMIT,                                 
                    :DCLGRANT-ASMNT.DTE-WTHDRW,                                 
                    :DCLGRANT-ASMNT.NUM-RJCTN-REASON,                           
                    :DCLGRANT-ASMNT.NUM-WTDRWL-REASON,                          
                    :DCLGRANT-ASMNT.TXT-CNCLTN-REASON,                          
                    :DCLGRANT-ASMNT.DTE-GRANT-RQST,                             
                    :DCLGRANT-ASMNT.NUM-GRANT-DOC-RCV,                          
                    :DCLGRANT-ASMNT.DTE-ACK,                                    
                    :DCLGRANT-ASMNT.DTE-GRANT-FORM-ISSUE,                       
                    :DCLGRANT-ASMNT.DTE-GRANT-OPT-OUT-FORM-ISSUE,               
                    :DCLGRANT-ASMNT.NUM-GRANT-OPT-OUT-REASON,                   
                    :DCLGRANT-ASMNT.DTE-GRANT-OPT-OUT-REASON,                   
                    :DCLGRANT-ASMNT.NUM-ALCTN-SCHEME,                           
                    :DCLGRANT-ASMNT.NUM-HSHLD-CTZNSP,                           
                    :DCLGRANT-ASMNT.NUM-WRKNG-CHILD-ASMNT,                      
                    :DCLGRANT-ASMNT.QTY-MONTH-WORK,                             
                    :DCLGRANT-ASMNT.NUM-APPEAL-ATCHMT,                          
                    :DCLGRANT-ASMNT.NUM-PARENT-ADRS-BLDNG,                      
                    :DCLGRANT-ASMNT.NUM-PARENT-ADRS-LEVEL,                      
                    :DCLGRANT-ASMNT.NUM-PARENT-ADRS-UNIT,                       
                    :DCLGRANT-ASMNT.TXT-PARENT-ADRS1,                           
                    :DCLGRANT-ASMNT.TXT-PARENT-ADRS2,                           
                    :DCLGRANT-ASMNT.NUM-PARENT-ADRS-POSTAL,                     
                    :DCLGRANT-ASMNT.DTE-PYMNT,                                  
                    :DCLGRANT-ASMNT.DTE-CPF-ACK-GRANT-RCV,                      
                    :DCLGRANT-ASMNT.NUM-GRANT-CREDIT,                           
                    :DCLGRANT-ASMNT.TME-APNTMT,                                 
                    :DCLGRANT-ASMNT.DTE-APNTMT,                                 
                    :DCLGRANT-ASMNT.TME-CREATE-ASMNT,                           
                    :DCLGRANT-ASMNT.NUM-UPDATE-USERID,                          
                    :DCLGRANT-ASMNT.TME-UPDATE,                                 
                    :DCLGRANT-ASMNT.DTE-UPDATE,                                 
                    :DCLGRANT-ASMNT.NUM-SOURCE-SYSTEM,                          
                    :DCLGRANT-ASMNT.NUM-ACNT,                                   
                    :DCLGRANT-ASMNT.NUM-SCHEME,                                 
                    :DCLGRANT-ASMNT.NUM-TNCY,                                   
                    :DCLGRANT-ASMNT.NUM-HALF-GRANT                              
              FROM                                                              
                GRANT_ASMNT                                                     
              WHERE NUM_RGSTRN       = :DCLGRANT-ASMNT.NUM-RGSTRN               
               AND  NUM_GRANT_TYPE   = :DCLGRANT-ASMNT.NUM-GRANT-TYPE           
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN WS-SQL-OK                                                       
                MOVE 'Y' TO WS-GRNT-FND                                         
           WHEN WS-SQL-NFND                                                     
                MOVE SQLCODE   TO WS-SQLCODE                                    
                DISPLAY 'CASE NOTFND IN GRANT_ASMNT'                            
                DISPLAY 'SQLCODE IS : ' WS-SQLCODE                              
                DISPLAY 'REGNO : ' NUM-RGSTRN OF DCLGRANT-ASMNT                 
                DISPLAY 'GRANT TYPE: ' NUM-GRANT-TYPE OF DCLGRANT-ASMNT         
                DISPLAY SPACES                                                  
                                                                                
           WHEN OTHER                                                           
                MOVE SQLCODE   TO WS-SQLCODE                                    
                DISPLAY 'ERROR READING GRANT_ASMNT'                             
                DISPLAY 'SQLCODE IS : ' WS-SQLCODE                              
                DISPLAY SPACES                                                  
                PERFORM 9999-SQL-ERROR      THRU 9999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       3300-UPDATE-GRANT-ASMNT.                                                 
      *----------------------*                                                  
           MOVE DCLGRANT-ASMNT TO WS-BEFORE-IMAGE.                              
           MOVE WS-F930-NUM-REGN TO NUM-RGSTRN OF DCLGRANT-ASMNT.               
           MOVE WS-GRANT-TYPE    TO NUM-GRANT-TYPE OF DCLGRANT-ASMNT.           
                                                                                
           STRING WS-DATE(1:4) '-'                                              
                  WS-DATE(5:2) '-'                                              
                  WS-DATE(7:2)                                                  
              DELIMITED BY SIZE INTO DTE-CPF-ACK-GRANT-RCV                      
                                                  OF DCLGRANT-ASMNT.            
           MOVE 'BP13CB3D ' TO NUM-UPDATE-USERID OF DCLGRANT-ASMNT.             
                                                                                
           STRING WS-DATE(1:4) '-'                                              
                  WS-DATE(5:2) '-'                                              
                  WS-DATE(7:2)                                                  
              DELIMITED BY SIZE INTO DTE-UPDATE OF DCLGRANT-ASMNT.              
                                                                                
           STRING WS-TIME-HHMMSS(1:2) '.'                                       
                  WS-TIME-HHMMSS(3:2) '.'                                       
                  WS-TIME-HHMMSS(5:2)                                           
                  DELIMITED BY SIZE INTO TME-UPDATE OF DCLGRANT-ASMNT.          
                                                                                
           EXEC SQL                                                             
              UPDATE GRANT_ASMNT                                                
              SET DTE_CPF_ACK_GRANT_RCV                                         
                               =: DCLGRANT-ASMNT.DTE-CPF-ACK-GRANT-RCV,         
                  NUM_UPDATE_USERID = :DCLGRANT-ASMNT.NUM-UPDATE-USERID,        
                  TME_UPDATE        = :DCLGRANT-ASMNT.TME-UPDATE,               
                  DTE_UPDATE        = :DCLGRANT-ASMNT.DTE-UPDATE                
              WHERE NUM_RGSTRN      = :DCLGRANT-ASMNT.NUM-RGSTRN                
               AND  NUM_GRANT_TYPE  = :DCLGRANT-ASMNT.NUM-GRANT-TYPE            
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN WS-SQL-OK                                                       
                ADD 1          TO CNT-UPD-TBL                                   
                MOVE 'GRANT_ASMNT'    TO WS-SOURCE                              
                MOVE 'U'              TO WS-ACTION                              
                MOVE NUM-RGSTRN OF DCLGRANT-ASMNT TO WS-RGSTRN                  
                MOVE NUM-GRANT-TYPE OF DCLGRANT-ASMNT TO WS-GRNT-TYPE           
                MOVE DCLGRANT-ASMNT TO WS-AFTER-IMAGE                           
                PERFORM 6400-INSERT-TO-LOG     THRU 6400-EXIT                   
           WHEN WS-SQL-NFND                                                     
                MOVE SQLCODE   TO WS-SQLCODE                                    
                DISPLAY 'CASE NOTFND IN GRANT_ASMNT'                            
                DISPLAY 'SQLCODE IS : ' WS-SQLCODE                              
                DISPLAY 'REGNO : ' NUM-RGSTRN OF DCLGRANT-ASMNT                 
                DISPLAY 'GRANT TYPE: ' NUM-GRANT-TYPE OF DCLGRANT-ASMNT         
                DISPLAY SPACES                                                  
           WHEN OTHER                                                           
                MOVE SQLCODE   TO WS-SQLCODE                                    
                DISPLAY 'ERROR UPDATING GRANT_ASMNT'                            
                DISPLAY 'SQLCODE IS : ' WS-SQLCODE                              
                DISPLAY SPACES                                                  
                PERFORM 9999-SQL-ERROR      THRU 9999-EXIT                      
                                                                                
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3600-DELETE-INTRAY.                                                      
      *--------------------*                                                    
           MOVE SPACES                    TO BE08W001-COMMAREA.                 
           INITIALIZE                        BE08W001-COMMAREA.                 
           MOVE 'D'                       TO W001-IN-TXN-TYPE.                  
           MOVE 'BP13'                    TO W001-IN-SYSTEM-ID.                 
           MOVE '0068'                    TO W001-IN-NUM-PRCS-CTGRY.            
           MOVE WS-F930-NUM-REGN          TO W001-IN-NUM-CASE-REF.              
           MOVE '000001'                  TO W001-IN-NUM-BRNCH.                 
           MOVE WS-F930-USERID            TO K010-NUM-USERID.                   
           PERFORM 3650-GET-STAFF-ID      THRU 3650-EXIT.                       
           MOVE K010-NUM-HDB-STAFF-ID     TO W001-IN-NUM-STAFF.                 
           MOVE WS-F930-NUM-REGN          TO W001-IN-HDBREF.                    
                                                                                
           CALL 'BE08C001'    USING BE08W001-COMMAREA                           
           IF W001-OUT-RTN-MSGCDE = ZEROES                                      
              ADD 1 TO WS-INBOX-DELETE                                          
           ELSE                                                                 
              DISPLAY 'ERROR IN BE08C001 ' WS-F930-NUM-REGN ' '                 
              DISPLAY W001-OUT-RTN-MSGNO '-' W001-OUT-RTN-DESC                  
      *       MOVE 99 TO RETURN-CODE                                            
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3650-GET-STAFF-ID.                                                       
      *---------------------------------------------------------------*         
           READ SY08K010 KEY IS K010-NUM-USERID.                                
           EVALUATE SY08K010-STATUS                                             
           WHEN 00                                                              
                CONTINUE                                                        
           WHEN 10                                                              
           WHEN 23                                                              
                DISPLAY 'SY08K010 RECORD NOT FOUND: '                           
                         K010-NUM-USERID                                        
           WHEN OTHER                                                           
                DISPLAY 'SY08K010 READ ERROR: ' SY08K010-STATUS                 
                MOVE SY08K010-STATUS      TO RETURN-CODE                        
                PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3650-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3700-READ-DEL-KD10.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                TO KD10-KEY-FLD.                          
           IF WS-F930-GRANT-TYPE = 'AHG' OR 'SHG'                               
              MOVE 'ASG'              TO KD10-NUM-TRANS                         
           ELSE                                                                 
              MOVE WS-F930-GRANT-TYPE    TO KD10-NUM-TRANS                      
           END-IF.                                                              
           MOVE WS-F930-NUM-REGN      TO KD10-NUM-REGN.                         
                                                                                
           READ BP13KD10.                                                       
           IF BP13KD10-STATUS = 0                                               
              PERFORM 3750-DELETE-BP13KD10 THRU 3750-EXIT                       
           ELSE                                                                 
              IF BP13KD10-STATUS = 23                                           
                 DISPLAY 'REGNO NOT FOUND IN BP13KD10 : ' KD10-NUM-REGN         
      *          MOVE BP13KD10-STATUS     TO RETURN-CODE                        
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13KD10 : ' BP13KD10-STATUS          
                 MOVE BP13KD10-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3750-DELETE-BP13KD10.                                                    
      *---------------------*                                                   
                                                                                
           DELETE BP13KD10.                                                     
                                                                                
           EVALUATE BP13KD10-STATUS                                             
               WHEN 00                                                          
                    ADD 1 TO CNT-KD10-DELETE                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR DELETING BP13KD10-REC ...'                   
                    MOVE BP13KD10-STATUS TO RETURN-CODE                         
                    PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3750-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       6400-INSERT-TO-LOG.                                                      
      *--------------------*                                                    
            EXEC SQL                                                            
                 SET :WS-TIMESTAMP-LOG = CURRENT TIMESTAMP                      
            END-EXEC.                                                           
            IF SQLCODE NOT = 000                                                
               MOVE SQLCODE          TO WS-SQLCODE                              
               DISPLAY 'SQL ERROR SET DB2 TIMESTAMP'                            
               DISPLAY 'SQL ERROR CODE  : '  WS-SQLCODE                         
               PERFORM 9999-SQL-ERROR THRU 9999-EXIT                            
            END-IF.                                                             
                                                                                
           MOVE WS-TIMESTAMP-LOG TO TME-UPDATE OF DCLGRANT-UPDATE-LOG.          
           MOVE 'BP13CB3D'  TO NUM-UPDATE-USERID OF DCLGRANT-UPDATE-LOG.        
           MOVE WS-SOURCE   TO NUM-SOURCE-RECORD OF DCLGRANT-UPDATE-LOG.        
           MOVE WS-ACTION     TO NUM-ACTION OF DCLGRANT-UPDATE-LOG.             
           MOVE WS-RGSTRN     TO NUM-RGSTRN OF DCLGRANT-UPDATE-LOG.             
           MOVE WS-GRNT-TYPE TO NUM-GRANT-TYPE OF DCLGRANT-UPDATE-LOG.          
           MOVE WS-BEFORE-IMAGE TO TXT-BEFORE-IMAGE-TEXT                        
                                              OF DCLGRANT-UPDATE-LOG.           
           MOVE LENGTH OF WS-BEFORE-IMAGE TO TXT-BEFORE-IMAGE-LEN               
                                              OF DCLGRANT-UPDATE-LOG.           
           MOVE WS-AFTER-IMAGE TO TXT-AFTER-IMAGE-TEXT                          
                                              OF DCLGRANT-UPDATE-LOG.           
           MOVE LENGTH OF WS-AFTER-IMAGE TO TXT-AFTER-IMAGE-LEN                 
                                              OF DCLGRANT-UPDATE-LOG.           
            EXEC SQL                                                            
                 INSERT INTO GRANT_UPDATE_LOG                                   
                        ( TME_UPDATE,                                           
                          NUM_UPDATE_USERID,                                    
                          NUM_SOURCE_RECORD,                                    
                          NUM_ACTION,                                           
                          NUM_RGSTRN,                                           
                          NUM_GRANT_TYPE,                                       
                          TXT_BEFORE_IMAGE,                                     
                          TXT_AFTER_IMAGE )                                     
                 VALUES ( :DCLGRANT-UPDATE-LOG.TME-UPDATE,                      
                          :DCLGRANT-UPDATE-LOG.NUM-UPDATE-USERID,               
                          :DCLGRANT-UPDATE-LOG.NUM-SOURCE-RECORD,               
                          :DCLGRANT-UPDATE-LOG.NUM-ACTION,                      
                          :DCLGRANT-UPDATE-LOG.NUM-RGSTRN,                      
                          :DCLGRANT-UPDATE-LOG.NUM-GRANT-TYPE,                  
                          :DCLGRANT-UPDATE-LOG.TXT-BEFORE-IMAGE,                
                          :DCLGRANT-UPDATE-LOG.TXT-AFTER-IMAGE )                
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
               WHEN 000                                                         
                    ADD 1                 TO WS-GRNT-UPD-LOG                    
                                                                                
               WHEN -803                                                        
                    CONTINUE                                                    
                                                                                
               WHEN OTHER                                                       
                    MOVE SQLCODE          TO WS-SQLCODE                         
                    DISPLAY 'SQL ERROR (INSERT) IN GRANT_UPDATE_LOG'            
                    DISPLAY 'SQL ERROR CODE  : '  WS-SQLCODE                    
                    PERFORM 9999-SQL-ERROR      THRU 9999-EXIT                  
            END-EVALUATE.                                                       
                                                                                
       6400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13CB3D *************'.                            
           DISPLAY 'NO OF RECS READ FROM  AB02F935  : ' CNT-F935-READ.          
           DISPLAY 'NO OF RECS READ FROM  AB02F930  : ' CNT-F930-READ.          
           DISPLAY 'NO OF RECS WRITTEN TO AB02F93A  : ' CNT-WRITE-F63A.         
           DISPLAY 'NO OF RECS UPD IN GRANT_ASMNT   : ' CNT-UPD-TBL.            
           DISPLAY 'NO OF RECS CREATED IN GRANT LOG : ' WS-GRNT-UPD-LOG.        
           DISPLAY 'NO OF RECS UNMATCHED            : ' CNT-WRITE-F63C.         
           DISPLAY 'NO OF RECS WITH STATUS = REJ    : ' CNT-WRITE-F63B.         
           DISPLAY 'NO OF SRG DELETED FROM INTRAY   : ' WS-INBOX-DELETE         
           DISPLAY 'NO OF RECS DELETED FROM BP13KD10: ' CNT-KD10-DELETE         
                                                                                
           CLOSE AB02F935                                                       
                 AB02F930                                                       
                 SY08K010                                                       
                 AB02F93A                                                       
                 AB02F93B                                                       
                 AB02F93C.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------------         
       9999-SQL-ERROR.                                                          
      *----------------------------------------------------------------         
           CALL 'DBATIAR'  USING SQLCA.                                         
           EXEC SQL ROLLBACK               END-EXEC.                            
           MOVE 99                         TO    WS-RETURN-CODE.                
           PERFORM 9000-CLOSE-FILES        THRU  9000-EXIT.                     
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
