       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C45O.                                                    
      *AUTHOR. RJB1.                                                            
      *DATE-WRITTEN. 23/10/17.                                                  
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (SOC)                             *                
      * ====================================================== *                
      *                                                        *                
      *    OBJECTIVE : REPORT FOR NUMBER OF APPLICANTS WHO     *                
      *                SELFBOOK A TP APPOINTMENT (BY MONTH)    *                
      *                                                        *                
      *  INPUT FILES:                                          *                
      *    1. BP13FC20 - SELF BOOK REGN LIST                   *                
      *                                                        *                
      *  OUTPUT FILE:                                          *                
      *    1. BP13L45O                                         *                
      *--------------------------------------------------------*                
      * CHG REF  BY   DATE      DESCRIPTION                    *                
      * -------- ---- --------  -----------                    *                
      * BP137141 RJB1 23/10/17  NEW PROGRAM                    *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FC20  ASSIGN TO BP13FC20.                                 
                                                                                
           SELECT BP13L45O  ASSIGN TO BP13L45O.                                 
                                                                                
           SELECT  BP13K410  ASSIGN        TO BP13K410                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K410-NUM-REGN                     
                             FILE STATUS   IS WS-K410-STATUS.                   
                                                                                
           SELECT  BP13KC30  ASSIGN        TO BP13KC30                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KC30-KEY-FLD                      
                             ALTERNATE KEY IS KC30-NUM-REGN                     
                             FILE STATUS   IS WS-KC30-STATUS.                   
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13FC20                                                            
            RECORD CONTAINS 150 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13FC20.                                                           
                                                                                
       FD   BP13K410                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K410.                                                           
                                                                                
       FD   BP13KC30                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13KC30.                                                           
                                                                                
                                                                                
       FD   BP13L45O                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 132  CHARACTERS.                                    
       01   BP13L45O-REC                 PIC X(132).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  FILE-VARIABLES.                                                      
           05  WS-KC30-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K410-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-1ST-RECORD           PIC X(01) VALUE SPACES.                  
           05  WS-NUM-APPT             PIC X(01) VALUE SPACES.                  
           05  WS-NUM-ISSUED           PIC X(01) VALUE SPACES.                  
                                                                                
                                                                                
       01  WS-DTE-SENT-SBK.                                                     
           05  WS-DTE-YY-SBK             PIC X(4)   VALUE SPACES.               
           05  WS-DTE-MM-SBK             PIC X(2)   VALUE SPACES.               
           05  WS-DTE-DD-SBK             PIC X(2)   VALUE SPACES.               
                                                                                
       01  WS-FC20-READ                  PIC 9(8)   VALUE ZEROES.               
       01  WS-FC20-INVALID               PIC 9(8)   VALUE ZEROES.               
       01  WS-KC30-INVALID               PIC 9(8)   VALUE ZEROES.               
       01  WS-K410-ISSUED                PIC 9(8)   VALUE ZEROES.               
       01  WS-NOT-ISSUED                 PIC 9(8)   VALUE ZEROES.               
       01  WS-K410-FND                   PIC 9(8)   VALUE ZEROES.               
       01  WS-K410-NFND                  PIC 9(8)   VALUE ZEROES.               
       01  WS-CNT-TOTAL-SENT             PIC 9(4)   VALUE ZEROES.               
       01  WS-CNT-TOT-AUTO-CHG           PIC 9(4)   VALUE ZEROES.               
       01  WS-CNT-TOT-AUTO-DNC           PIC 9(4)   VALUE ZEROES.               
       01  WS-CNT-TOT-BOOK-CHG           PIC 9(4)   VALUE ZEROES.               
       01  WS-CNT-TOT-BOOK-DNC           PIC 9(4)   VALUE ZEROES.               
       01  WS-CNT-TOT-REC-DEL            PIC 9(4)   VALUE ZEROES.               
       01  WS-NUM-STATUS                 PIC X      VALUE SPACES.               
       01  FC20-EOF                      PIC X      VALUE 'N'.                  
       01  WS-MONTH                      PIC X(3)   VALUE SPACES.               
       01  WS-YY-MM.                                                            
           05 WS-YEAR-CC                 PIC 99.                                
           05 WS-YEAR-YY                 PIC 99.                                
           05 WS-MONTH-MM                PIC 99.                                
       01  WS-CNT                        PIC 9(4)   VALUE ZEROES.               
                                                                                
       01  WS-DATE.                                                             
           05  WS-DATE-CCYY        PIC 9(4)     VALUE ZEROES.                   
           05  WS-DATE-MM          PIC 9(2)     VALUE ZEROES.                   
           05  WS-DATE-DD          PIC 9(2)     VALUE ZEROES.                   
                                                                                
       01  WS-MONTHS-IN-DIGITS-AND-WORDS.                                       
           05 FILLER                   PIC X(06) VALUE                          
             '01 JAN'.                                                          
           05 FILLER                   PIC X(06) VALUE                          
             '02 FEB'.                                                          
           05 FILLER                   PIC X(06) VALUE                          
             '03 MAR'.                                                          
           05 FILLER                   PIC X(06) VALUE                          
             '04 APR'.                                                          
           05 FILLER                   PIC X(06) VALUE                          
             '05 MAY'.                                                          
           05 FILLER                   PIC X(06) VALUE                          
             '06 JUN'.                                                          
           05 FILLER                   PIC X(06) VALUE                          
             '07 JUL'.                                                          
           05 FILLER                   PIC X(06) VALUE                          
             '08 AUG'.                                                          
           05 FILLER                   PIC X(06) VALUE                          
             '09 SEP'.                                                          
           05 FILLER                   PIC X(06) VALUE                          
             '10 OCT'.                                                          
           05 FILLER                   PIC X(06) VALUE                          
             '11 NOV'.                                                          
           05 FILLER                   PIC X(06) VALUE                          
             '12 DEC'.                                                          
       01  FILLER  REDEFINES  WS-MONTHS-IN-DIGITS-AND-WORDS.                    
           05 WS-MONTH-TABLE               OCCURS 12                            
                                           INDEXED BY WS-MTH-IND.               
              10 WS-MONTH-IN-DIGITS    PIC X(02).                               
              10 FILLER                PIC X(01).                               
              10 WS-MONTH-IN-WORDS     PIC X(03).                               
                                                                                
      *-------------------------------------------------------------            
      *    REPORT LAYOUT                                                        
      *-------------------------------------------------------------            
       01  L45O-PR-HEAD-01.                                                     
           05  FILLER              PIC X(14)   VALUE 'BP13L45O      '.          
           05  FILLER              PIC X(8)    VALUE 'HDB3    '.                
           05  FILLER              PIC X(18)   VALUE SPACES.                    
           05  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(13)   VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'DATE: '.                  
           05  L45O-DATE           PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(3)    VALUE SPACES.                    
                                                                                
       01  L45O-PR-HEAD-02.                                                     
           05  FILLER              PIC X(34)   VALUE SPACES.                    
           05  FILLER              PIC X(66)   VALUE                            
           'NUMBER OF APPLICANTS WHO SELFBOOK A TP APPOINTMENT (BY MONTH        
      -    ')'.                                                                 
                                                                                
       01  L45O-HDG1               PIC X(132) VALUE                             
           'MONTH    TOTAL SENT        SELF BOOK                AUTO BOO        
      -    'K            NO. OF DELETED RECS '.                                 
                                                                                
       01  L45O-HDG2               PIC X(132) VALUE                             
           '                       CHG APPT     DNC APPT   CHG APPT             
      -    ' DNC APPT'.                                                         
                                                                                
       01  L45O-HDG3               PIC X(132) VALUE                             
           '------   ----------    --------   --------   --------   ----        
      -    '----'.                                                              
                                                                                
       01  L45O-PR-DETAIL.                                                      
           05  L45O-MO             PIC X(06)  VALUE SPACES.                     
           05  WS-FIL1             PIC X(06)  VALUE SPACES.                     
           05  L45O-TOT-SENT       PIC ZZZ9.                                    
           05  WS-FIL2             PIC X(09)  VALUE SPACES.                     
           05  L45O-SBK-CHG        PIC ZZZ9.                                    
           05  WS-FIL3             PIC X(09)  VALUE SPACES.                     
           05  L45O-SBK-DNC        PIC ZZZ9.                                    
           05  WS-FIL4             PIC X(07)  VALUE SPACES.                     
           05  L45O-ABK-CHG        PIC ZZZ9.                                    
           05  WS-FIL5             PIC X(09)  VALUE SPACES.                     
           05  L45O-ABK-DNC        PIC ZZZ9.                                    
           05  WS-FIL6             PIC X(14)  VALUE SPACES.                     
           05  L45O-REC-DEL        PIC ZZZ9.                                    
           05  WS-FIL7             PIC X(07)  VALUE SPACES.                     
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-INITIALIZATION.                                         
           PERFORM 2000-READ-FC20       THRU 2000-EXIT.                         
                                                                                
           PERFORM 3000-PROCESS         THRU 3000-EXIT                          
             UNTIL FC20-EOF = 'Y'.                                              
                                                                                
           PERFORM 4500-PRINT-DETAIL    THRU 4500-EXIT.                         
                                                                                
           PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT.                         
                                                                                
       0000-FINISH.                                                             
            EXIT.                                                               
                                                                                
                                                                                
       1000-INITIALIZATION.                                                     
      *-------------------------------------------------------------            
            OPEN INPUT  BP13FC20                                                
                        BP13K410                                                
                        BP13KC30                                                
                 OUTPUT BP13L45O.                                               
                                                                                
           IF WS-KC30-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KC30 - ERROR OPENING : ' WS-KC30-STATUS              
              MOVE WS-KC30-STATUS                   TO RETURN-CODE              
              PERFORM  9000-CLOSING-ROUTINE THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K410 - ERROR OPENING : ' WS-K410-STATUS              
              MOVE WS-K410-STATUS                   TO RETURN-CODE              
              PERFORM  9000-CLOSING-ROUTINE THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
                                                                                
           MOVE ZEROES                     TO WS-CNT-TOTAL-SENT                 
                                              WS-CNT-TOT-AUTO-CHG               
                                              WS-CNT-TOT-AUTO-DNC               
                                              WS-CNT-TOT-BOOK-CHG               
                                              WS-CNT-TOT-BOOK-DNC               
                                              WS-CNT-TOT-REC-DEL.               
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-DATE.                          
                                                                                
           PERFORM 4000-PRINT-HEADER       THRU 4000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2000-READ-FC20.                                                          
      *-------------------------------------------------------------            
            READ BP13FC20  AT END MOVE 'Y' TO FC20-EOF                          
                 GO TO 2000-EXIT.                                               
            ADD 1 TO WS-FC20-READ.                                              
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2100-READ-BP13KC30.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                   TO BP13KC30-REC.                       
           INITIALIZE                       BP13KC30-REC.                       
                                                                                
           MOVE SPACES                   TO WS-NUM-APPT                         
                                            WS-NUM-ISSUED                       
                                            KC30-KEY-FLD.                       
                                                                                
           MOVE FC20-NUM-REGN            TO KC30-NUM-REGN.                      
                                                                                
           READ BP13KC30 KEY IS KC30-NUM-REGN.                                  
                                                                                
           EVALUATE WS-KC30-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 2200-READ-BP13K410 THRU 2200-EXIT                   
                                                                                
               WHEN 23                                                          
                    ADD  1        TO WS-KC30-INVALID                            
                                                                                
                    MOVE SPACES   TO BP13KC30-REC                               
                    INITIALIZE       BP13KC30-REC                               
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13KC30 READ ERROR ' WS-KC30-STATUS               
                    DISPLAY 'KC30 KEY: ' KC30-NUM-REGN                          
                    MOVE WS-KC30-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2200-READ-BP13K410.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                   TO BP13K410-REC.                       
           INITIALIZE                       BP13K410-REC.                       
                                                                                
           MOVE KC30-NUM-REGN            TO K410-NUM-REGN.                      
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE WS-K410-STATUS                                              
               WHEN 00                                                          
                    ADD 1            TO WS-K410-FND                             
                                                                                
                    IF K410-DTE-KEY-ISSUE NOT = SPACES AND LOW-VALUES           
                                                       AND ZEROES               
                       ADD 1         TO WS-K410-ISSUED                          
                       MOVE 'Y'      TO WS-NUM-ISSUED                           
                                                                                
                       IF K410-DTE-SO-EXECUTION = KC30-DTE-APPT                 
                          MOVE 'Y'   TO WS-NUM-APPT                             
                       ELSE                                                     
                          MOVE 'N'   TO WS-NUM-APPT                             
                       END-IF                                                   
                                                                                
                    ELSE                                                        
                       ADD 1         TO WS-NOT-ISSUED                           
                    END-IF                                                      
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1         TO WS-K410-NFND                               
                                                                                
                    MOVE SPACES   TO BP13K410-REC                               
                    INITIALIZE       BP13K410-REC                               
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K410 READ ERROR ' WS-K410-STATUS               
                    DISPLAY 'K410 KEY: ' K410-NUM-REGN                          
                    MOVE WS-K410-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3000-PROCESS.                                                            
      *-------------------------------------------------------------            
                                                                                
           IF FC20-NUM-APPT-TYPE NOT = 'TP'                                     
              ADD 1 TO WS-FC20-INVALID                                          
           ELSE                                                                 
              PERFORM 3100-PROCESS-SELFBOOK THRU 3100-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-FC20           THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3100-PROCESS-SELFBOOK.                                                   
      *-------------------------------------------------------------            
                                                                                
           IF FC20-DTE-SENT-SELF-BK(1:6) NOT = WS-YY-MM                         
              IF WS-1ST-RECORD = 'Y'                                            
                 PERFORM 4500-PRINT-DETAIL THRU 4500-EXIT                       
                 MOVE ZEROES               TO WS-CNT-TOTAL-SENT                 
                                              WS-CNT-TOT-AUTO-CHG               
                                              WS-CNT-TOT-AUTO-DNC               
                                              WS-CNT-TOT-BOOK-CHG               
                                              WS-CNT-TOT-BOOK-DNC               
                                              WS-CNT-TOT-REC-DEL                
                 DISPLAY FC20-DTE-SENT-SELF-BK                                  
              ELSE                                                              
                 MOVE 'Y'                  TO WS-1ST-RECORD                     
              END-IF                                                            
                                                                                
                                                                                
              MOVE FC20-DTE-SENT-SELF-BK(1:6) TO WS-YY-MM                       
           END-IF.                                                              
                                                                                
           SET WS-MTH-IND TO 1.                                                 
                                                                                
           SEARCH WS-MONTH-TABLE                                                
               AT END                                                           
               MOVE '   ' TO WS-MONTH                                           
            WHEN WS-MONTH-IN-DIGITS(WS-MTH-IND) = FC20-DTE-SENT-SELF-BK         
                                                  (5:2)                         
               MOVE WS-MONTH-IN-WORDS(WS-MTH-IND) TO WS-MONTH                   
           END-SEARCH.                                                          
                                                                                
           MOVE FC20-NUM-STATUS              TO WS-NUM-STATUS.                  
                                                                                
           ADD 1                             TO WS-CNT-TOTAL-SENT.              
                                                                                
           PERFORM 2100-READ-BP13KC30      THRU 2100-EXIT.                      
                                                                                
                                                                                
           IF WS-NUM-ISSUED = 'Y'                                               
              EVALUATE WS-NUM-STATUS                                            
              WHEN 'A'                                                          
                IF WS-NUM-APPT = 'N'                                            
                   ADD 1                   TO   WS-CNT-TOT-BOOK-CHG             
                ELSE                                                            
                   ADD 1                   TO   WS-CNT-TOT-BOOK-DNC             
                END-IF                                                          
                                                                                
              WHEN 'S'                                                          
                IF WS-NUM-APPT = 'N'                                            
                   ADD 1                   TO   WS-CNT-TOT-AUTO-CHG             
                ELSE                                                            
                   ADD 1                   TO   WS-CNT-TOT-AUTO-DNC             
                END-IF                                                          
                                                                                
              WHEN OTHER                                                        
                  ADD  1                   TO   WS-CNT-TOT-REC-DEL              
                                                                                
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4000-PRINT-HEADER.                                                       
      *-------------------------------------------------------------            
                                                                                
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO L45O-DATE                              
           WRITE BP13L45O-REC FROM L45O-PR-HEAD-01 AFTER 2.                     
           WRITE BP13L45O-REC FROM L45O-PR-HEAD-02                              
           WRITE BP13L45O-REC FROM L45O-HDG1                                    
           WRITE BP13L45O-REC FROM L45O-HDG2                                    
           MOVE  SPACES       TO   BP13L45O-REC                                 
           WRITE BP13L45O-REC.                                                  
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4500-PRINT-DETAIL.                                                       
      *-------------------------------------------------------------            
           STRING WS-MONTH ' ' WS-YEAR-YY                                       
              DELIMITED BY SIZE          INTO L45O-MO                           
           END-STRING.                                                          
                                                                                
           MOVE WS-CNT-TOTAL-SENT          TO L45O-TOT-SENT.                    
                                                                                
           MOVE WS-CNT-TOT-BOOK-CHG        TO L45O-SBK-CHG.                     
           MOVE WS-CNT-TOT-BOOK-DNC        TO L45O-SBK-DNC.                     
                                                                                
           MOVE WS-CNT-TOT-AUTO-CHG        TO L45O-ABK-CHG.                     
           MOVE WS-CNT-TOT-AUTO-DNC        TO L45O-ABK-DNC.                     
           MOVE WS-CNT-TOT-REC-DEL         TO L45O-REC-DEL.                     
                                                                                
           MOVE SPACES TO BP13L45O-REC                                          
           WRITE BP13L45O-REC FROM L45O-PR-DETAIL.                              
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           DISPLAY '- BP13C45O CONTROL TOTAL ----------'.                       
           DISPLAY ' FC20-READ             : ' WS-FC20-READ.                    
           DISPLAY ' FC20-APPT NOT = TP    : ' WS-FC20-INVALID.                 
           DISPLAY ' INVALID KC30 REGN     : ' WS-KC30-INVALID.                 
           DISPLAY ' BP13K410 REC NOT FOUND: ' WS-K410-NFND.                    
           DISPLAY ' BP13K410 REC FOUND    : ' WS-K410-FND.                     
           DISPLAY ' BP13K410 KEY ISSUED   : ' WS-K410-ISSUED.                  
           DISPLAY ' BP13K410 NOT ISSUED   : ' WS-NOT-ISSUED.                   
           DISPLAY ' '.                                                         
                                                                                
           CLOSE      BP13FC20                                                  
                      BP13K410                                                  
                      BP13KC30                                                  
                      BP13L45O.                                                 
                                                                                
           IF WS-KC30-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KC30 - ERROR CLOSING : ' WS-KC30-STATUS              
              MOVE WS-KC30-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K410 - ERROR CLOSING : ' WS-K410-STATUS              
              MOVE WS-K410-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
