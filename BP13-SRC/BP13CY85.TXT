       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY85.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  17/06/2011.                                               
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVE   : MATCH BP13F55A WITH BM06F120 TO GET   *                
      *                  FIS DATA, SELLING PRICE, UNIT-NO.     *                
      *                                                        *                
      *    INPUT FILE  : BP13F55A                              *                
      *                  BM06F120                              *                
      *                                                        *                
      *   OUTPUT FILE  : P13F55AA                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE     DETAILS                         *                
      * -------- ---- -------- ------------------------------- *                
      * BP134306 PCL3 17/06/11 NEW PROGRAM                     *                
      * BP137358 FNP1 30/05/18 EXPAND BP13F55A FROM 800 TO 1200*                
      *========================================================*                
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F120  ASSIGN       TO BM06F120.                           
           SELECT BP13F55A  ASSIGN       TO BP13F55A.                           
           SELECT P13F55AA  ASSIGN       TO P13F55AA.                           
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BM06F120                                                            
            RECORD CONTAINS 700 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       COPY BM06F120.                                                           
                                                                                
       FD   BP13F55A                                                            
            RECORD CONTAINS 1200 CHARACTERS                                     
            RECORDING MODE  IS  F.                                              
       COPY BP13F55A.                                                           
                                                                                
       FD   P13F55AA                                                            
            RECORD CONTAINS 1200 CHARACTERS                                     
            RECORDING MODE  IS  F.                                              
       01   P13F55AA-REC            PIC X(1200).                                
                                                                                
      ***************************                                               
       WORKING-STORAGE SECTION.                                                 
      ***************************                                               
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-SYS-YY             PIC X(04) .                                 
           05 WS-SYS-MM             PIC X(02) .                                 
           05 WS-SYS-DD             PIC X(02) .                                 
                                                                                
       01  WS-EDIT-DATE.                                                        
           05 WS-EDIT-DAY           PIC X(02) .                                 
           05 FILLER                PIC X(01) VALUE '/'.                        
           05 WS-EDIT-MONTH         PIC X(02) .                                 
           05 FILLER                PIC X(01) VALUE '/'.                        
           05 WS-EDIT-YEAR          PIC X(04) .                                 
                                                                                
       01  WS-ACCUMULATORS.                                                     
           05 WS-F55A-READ               PIC 9(8)  VALUE ZEROES.                
           05 WS-F55A-MATCH              PIC 9(8)  VALUE ZEROES.                
           05 WS-F55A-NOMATCH            PIC 9(8)  VALUE ZEROES.                
           05 WS-F120-NOMATCH            PIC 9(8)  VALUE ZEROES.                
           05 WS-F55AA-WRITE             PIC 9(8)  VALUE ZEROES.                
           05 WS-F120-READ               PIC 9(8)  VALUE ZEROES.                
                                                                                
       01  WS-SWITCHES.                                                         
           05 EOF-F120-IND          PIC X(3)  VALUE SPACES.                     
              88 EOF-F120                     VALUE 'YES'.                      
           05 EOF-F55A-IND          PIC X(3)  VALUE SPACES.                     
              88 EOF-F55A                     VALUE 'YES'.                      
           05 WS-F120-MATCH         PIC X(1)  VALUE SPACE.                      
              88 F120-MATCH                   VALUE 'Y'.                        
       01  WS-SCH-ACC-NO            PIC X(9)  VALUE SPACES.                     
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      *--------*                                                                
       000-MAIN.                                                                
      *--------*                                                                
                                                                                
           PERFORM 100-OPEN-FILES      THRU 100-EXIT.                           
           PERFORM 200-READ-BP13F55A   THRU 200-EXIT.                           
           PERFORM 300-READ-BM06F120   THRU 300-EXIT.                           
           PERFORM 400-PROCESS-INDATA  THRU 400-EXIT                            
             UNTIL F55A-NUM-SCH-ACCNT = HIGH-VALUES                             
               AND WS-SCH-ACC-NO      = HIGH-VALUES.                            
           PERFORM 900-CLOSE-FILES     THRU 900-EXIT.                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------                                                       
       100-OPEN-FILES.                                                          
      *------------------                                                       
                                                                                
           OPEN INPUT  BM06F120                                                 
                       BP13F55A                                                 
                OUTPUT P13F55AA.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)   TO  WS-SYS-DATE.                   
           MOVE   WS-SYS-DD       TO WS-EDIT-DAY                                
           MOVE   WS-SYS-MM       TO WS-EDIT-MONTH                              
           MOVE   WS-SYS-YY       TO WS-EDIT-YEAR.                              
                                                                                
                                                                                
           MOVE 'NO '     TO EOF-F120-IND.                                      
           MOVE 'NO '     TO EOF-F55A-IND.                                      
                                                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       200-READ-BP13F55A.                                                       
      *-----------------*                                                       
                                                                                
           READ BP13F55A  AT END                                                
                MOVE 'YES'       TO EOF-F55A-IND                                
                MOVE HIGH-VALUES TO F55A-NUM-SCH-ACCNT                          
                GO TO 200-EXIT                                                  
           END-READ                                                             
                                                                                
           ADD  1  TO WS-F55A-READ.                                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       300-READ-BM06F120.                                                       
      *-----------------*                                                       
                                                                                
           READ BM06F120  AT END                                                
                MOVE 'YES'       TO EOF-F120-IND                                
                MOVE HIGH-VALUES TO WS-SCH-ACC-NO                               
                GO TO 300-EXIT                                                  
           END-READ                                                             
                                                                                
           MOVE F120-SCH-ACC-NO(1:9)             TO WS-SCH-ACC-NO.              
                                                                                
           ADD  1  TO WS-F120-READ.                                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------                                                       
       400-PROCESS-INDATA.                                                      
      *------------------*                                                      
                                                                                
           IF F55A-NUM-SCH-ACCNT = WS-SCH-ACC-NO                                
              ADD 1                              TO WS-F55A-MATCH               
              PERFORM 490-WRITE-P13F55AA       THRU 490-EXIT                    
                UNTIL F55A-NUM-SCH-ACCNT NOT = WS-SCH-ACC-NO                    
                   OR F55A-NUM-SCH-ACCNT     = HIGH-VALUES                      
              PERFORM 300-READ-BM06F120        THRU 300-EXIT                    
           ELSE                                                                 
              IF F55A-NUM-SCH-ACCNT < WS-SCH-ACC-NO                             
                 ADD  1                          TO WS-F55A-NOMATCH             
                 WRITE P13F55AA-REC FROM BP13F55A-REC                           
                 ADD 1                           TO WS-F55AA-WRITE              
                 PERFORM 200-READ-BP13F55A     THRU 200-EXIT                    
              ELSE                                                              
                 ADD  1                          TO WS-F120-NOMATCH             
                 PERFORM 300-READ-BM06F120     THRU 300-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       490-WRITE-P13F55AA.                                                      
      *-------------------*                                                     
                                                                                
           IF F120-CURR-SELLING-PRICE NOT NUMERIC                               
              MOVE ZEROES                 TO F120-CURR-SELLING-PRICE            
           END-IF.                                                              
                                                                                
           MOVE F120-CURR-SELLING-PRICE   TO F55A-AMT-SELLING-PRICE.            
           MOVE F120-UNIT-NO              TO F55A-NUM-UNITLVL.                  
                                                                                
           WRITE P13F55AA-REC            FROM BP13F55A-REC.                     
           ADD 1                           TO WS-F55AA-WRITE.                   
                                                                                
           PERFORM 200-READ-BP13F55A     THRU 200-EXIT.                         
                                                                                
       490-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------                                                         
       900-CLOSE-FILES.                                                         
      *----------------                                                         
                                                                                
           CLOSE BP13F55A                                                       
                 P13F55AA                                                       
                 BM06F120.                                                      
                                                                                
           DISPLAY 'PROGRAM-ID : BP13CY85                 '.                    
           DISPLAY '                                      '.                    
           DISPLAY 'DATE : ' WS-EDIT-DATE.                                      
           DISPLAY '                                      '.                    
           DISPLAY 'NO. OF RECORDS READ FROM F55A   = ' WS-F55A-READ.           
           DISPLAY 'NO. OF RECORDS READ FROM F120   = ' WS-F120-READ.           
           DISPLAY 'NO. OF RECORDS OF F55A MATCHD   = ' WS-F55A-MATCH.          
           DISPLAY 'NO. OF RECORDS OF F55A NOT MATCHED ='                       
                   WS-F55A-NOMATCH.                                             
           DISPLAY 'NO. OF RECORDS OF F120 NOT MATCHED ='                       
                   WS-F120-NOMATCH.                                             
           DISPLAY 'NO. OF RECORDS WRITTEN TO F55AA = ' WS-F55AA-WRITE.         
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
