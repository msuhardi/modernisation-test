       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB56.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  18/08/2017.                                               
      * ========================================================== *            
      *                 SYSTEM OF COMMITMENT (SOC)                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVES   : DETERMINE THE CLASSIFICATION OF EACH ROF   *            
      *                 CASES                                      *            
      *                                                            *            
      *  INPUT FILE   : BP13F740 - FULL FILE                       *            
      *  OUTPUT FILES : BP13F74A - FT CASES                        *            
      *                 BP13F74B - ST CASES                        *            
      *                 BP13F74C - OTH CASES                       *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF# BY   DATE     DESCRIPTION                         *            
      * -------- ---- -------- ----------------------------------- *            
      * BP136970 SMR2 18082017 NEW PROGRAM                         *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
           SELECT BP13F74A ASSIGN TO BP13F74A.                                  
           SELECT BP13F74B ASSIGN TO BP13F74B.                                  
           SELECT BP13F74C ASSIGN TO BP13F74C.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F740                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS   ARE STANDARD.                                       
       COPY BP13F740.                                                           
                                                                                
       FD   BP13F74A                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS   ARE STANDARD.                                       
       01   BP13F74A-REC                   PIC X(500).                          
                                                                                
       FD   BP13F74B                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS   ARE STANDARD.                                       
       01   BP13F74B-REC                   PIC X(500).                          
                                                                                
       FD   BP13F74C                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS   ARE STANDARD.                                       
       01   BP13F74C-REC                   PIC X(500).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ                PIC 9(06)  VALUE ZEROES.             
           05  WS-F74A-WRITE               PIC 9(06)  VALUE ZEROES.             
           05  WS-F74B-WRITE               PIC 9(06)  VALUE ZEROES.             
           05  WS-F74C-WRITE               PIC 9(06)  VALUE ZEROES.             
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F740-EOF                 PIC X(01)  VALUE 'N'.                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 1000-INIT-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F740      THRU 2000-EXIT                       
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 4000-CLOSE-ROUTINE      THRU 4000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-INIT-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT BP13F740                                                  
               OUTPUT BP13F74A                                                  
                      BP13F74B                                                  
                      BP13F74C.                                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F740.                                                      
      *-------------------------------------------------------------            
           READ BP13F740                                                        
             AT END                                                             
                MOVE 'Y'                   TO WS-F740-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F740-READ.                     
                                                                                
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
      *** FIRST TIMER - FAMILY ***                                              
           IF ((F740-NUM-ELDERLY = 'N' OR SPACES) AND                           
               (F740-CDE-BALLOT-HOUSEHOLD = 'H' OR 'F' OR 'T') AND              
               (F740-CDE-ALLOC-SCH = 'PA '))                                    
           OR ((F740-NUM-ELDERLY = 'F') AND                                     
               (F740-CDE-BALLOT-HOUSEHOLD = 'H' OR 'F' OR 'T') AND              
               (F740-NUM-PPO = 'E' OR SPACES))                                  
              IF F740-CDE-BALLOT-HOUSEHOLD = 'F' OR 'T'                         
                 MOVE 'H'                  TO F740-CDE-BALLOT-HOUSEHOLD         
              END-IF                                                            
              MOVE 'FT '                   TO F740-CDE-ALLOC-SCH                
              MOVE SPACES                  TO F740-CDE-CATEGORY                 
              WRITE BP13F74A-REC           FROM BP13F740-REC                    
              ADD 1                        TO WS-F74A-WRITE                     
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
      *** SECOND TIMER - FAMILY ***                                             
           IF ((F740-NUM-ELDERLY = 'N' OR SPACES) AND                           
               (F740-CDE-BALLOT-HOUSEHOLD = 'G' OR 'N') AND                     
               (F740-CDE-ALLOC-SCH = 'PA '))                                    
           OR ((F740-NUM-ELDERLY = 'F') AND                                     
               (F740-CDE-BALLOT-HOUSEHOLD = 'G') AND                            
               (F740-NUM-PPO = 'E' OR SPACES))                                  
              IF F740-CDE-BALLOT-HOUSEHOLD = 'N'                                
                 MOVE 'G'                  TO F740-CDE-BALLOT-HOUSEHOLD         
              END-IF                                                            
              MOVE 'ST '                   TO F740-CDE-ALLOC-SCH                
              MOVE SPACES                  TO F740-CDE-CATEGORY                 
              WRITE BP13F74B-REC           FROM BP13F740-REC                    
              ADD 1                        TO WS-F74B-WRITE                     
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
      *** OTHERS - SINGLE ***                                                   
           MOVE 'H'                        TO F740-CDE-BALLOT-HOUSEHOLD.        
           MOVE 'OTH'                      TO F740-CDE-ALLOC-SCH.               
           MOVE SPACES                     TO F740-CDE-CATEGORY.                
           WRITE BP13F74C-REC              FROM BP13F740-REC.                   
           ADD 1                           TO WS-F74C-WRITE.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '********** SUMMARY REPORT FOR BP13CB56 **********'.         
           DISPLAY ' NO OF REC READ FROM BP13F740   = ' WS-F740-READ.           
           DISPLAY ' NO OF REC WRITTEN IN F74A(FT)  = ' WS-F74A-WRITE.          
           DISPLAY ' NO OF REC WRITTEN IN F74B(ST)  = ' WS-F74B-WRITE.          
           DISPLAY ' NO OF REC WRITTEN IN F74C(OTH) = ' WS-F74C-WRITE.          
                                                                                
           CLOSE  BP13F740                                                      
                  BP13F74A                                                      
                  BP13F74B                                                      
                  BP13F74C.                                                     
                                                                                
           STOP RUN.                                                            
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
