       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C327.                                                 
      *AUTHOR.        SAUL SIO.                                                 
      *DATE-WRITTEN.  16/04/2003.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :                                              *            
      *      1. PRINT REPORT ON PROTECTED FLATS TAKEN POSSESSION   *            
      *      2. RETRIEVE FLAT ADDRESS FROM PIDB                    *            
      *                                                            *            
      *  INPUT :  1. BP13F410                                      *            
      *           2. BM06K110                                      *            
      *                                                            *            
      *  OUTPUT:  1. BP13L327                                      *            
      *                                                            *            
      * CHG REF  BY    DATE      DESCRIPTION                       *            
      * -------- ----- --------  -----------                       *            
      * BP132304 SSS2  20030416  NEW PGM                           *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410 ASSIGN TO BP13F410.                                  
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ACCESS MODE IS RANDOM                                         
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K110-KEY-FLD                                    
                  ALTERNATE KEY IS K110-REGN-NO                                 
                  FILE STATUS IS BM06K110-STATUS.                               
                                                                                
           SELECT BP13L327 ASSIGN TO BP13L327.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BP13F410                                                             
           RECORDING MODE F                                                     
           BLOCK  CONTAINS 0 CHARACTERS                                         
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS IS STANDARD.                                           
       COPY BP13F410.                                                           
                                                                                
       FD  BP13L327                                                             
           RECORDING MODE F                                                     
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  L327-PRTREC                    PIC X(132).                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  SQL-CODE                PIC ZZZ9.                                    
       01  WS-F410-EOF             PIC X     VALUE 'N'.                         
                                                                                
       01  BM06K110-STATUS         PIC 99       VALUE ZEROS.                    
       01  BP13K800-STATUS         PIC 99       VALUE ZEROS.                    
       01  NO-F410-READ            PIC 9(5)     VALUE ZEROS.                    
       01  WS-SNO                  PIC 9(5)     VALUE ZEROS.                    
       01  WS-DATE                 PIC X(8)     VALUE SPACES.                   
       01  WS-PAGE-CNT             PIC 9(4)     VALUE ZEROS.                    
       01  WS-LINE-CNT             PIC 99       VALUE 77.                       
                                                                                
                                                                                
      *-----------------------*                                                 
      * LINKAGE FOR BP13C913. *                                                 
      *-----------------------*                                                 
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH          PIC X(4).                           
               10  WS-LINK-NUM-ACC          PIC X(5).                           
                                                                                
       COPY P13COMM8.                                                           
                                                                                
       01  L327-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13L327'.              
           05  FILLER              PIC X(5)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDB3'.                  
           05  FILLER              PIC X(26)     VALUE SPACES.                  
           05  FILLER              PIC X(40)     VALUE                          
               'S Y S T E M    O F   C O M M I T M E N T'.                      
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(6)      VALUE 'DATE:'.                 
           05  L327-DATE           PIC X(10)     VALUE SPACES.                  
                                                                                
       01  L327-HEADER02.                                                       
           05  FILLER              PIC X(29)     VALUE SPACES.                  
           05  FILLER              PIC X(50)     VALUE                          
               'REPORT ON PROTECTED FLATS TAKEN POSSESSION IN THE'.             
           05  FILLER              PIC X(9)      VALUE 'MONTH OF'.              
           05  L327-HDR-MTH        PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X         VALUE SPACES.                  
           05  L327-HDR-YEAR       PIC X(6)      VALUE SPACES.                  
           05  FILLER              PIC X(14)     VALUE SPACES.                  
           05  FILLER              PIC X(6)      VALUE 'PAGE: '.                
           05  L327-PAGE           PIC ZZZ9      VALUE ZEROS.                   
                                                                                
       01  L327-HEADER03.                                                       
           05  FILLER              PIC X(8)      VALUE 'S/NO.   '.              
           05  FILLER              PIC X(52)     VALUE                          
               'REGN NO    BLK    STREET NAME '.                                
           05  FILLER              PIC X(13)     VALUE 'UNIT'.                  
           05  FILLER              PIC X(46)     VALUE                          
               'RM TY  DTE ACCEPT   DTE CV SENT   DTE CV'.                      
           05  FILLER              PIC X(7)      VALUE 'DTE TP'.                
                                                                                
       01  L327-HEADER04.                                                       
           05  FILLER              PIC X(130)    VALUE ALL '-'.                 
                                                                                
       01  L327-DETAILS.                                                        
           05  L327-SNO                 PIC ZZZZ9   VALUE ZEROS.                
           05  FILLER                   PIC X(3)    VALUE SPACES.               
           05  L327-NUM-REGN            PIC X(8)    VALUE SPACES.               
           05  FILLER                   PIC X(3)    VALUE SPACES.               
           05  L327-NUM-BLK             PIC X(5)    VALUE SPACES.               
           05  FILLER                   PIC X(2)    VALUE SPACES.               
           05  L327-NME-STREET          PIC X(32)   VALUE SPACES.               
           05  FILLER                   PIC X(3)    VALUE SPACES.               
           05  L327-NUM-UNIT.                                                   
               10 L327-NUM-LEVEL        PIC X(2)    VALUE SPACES.               
               10 L327-DASH01           PIC X       VALUE SPACES.               
               10 L327-NUM-MAINUNIT     PIC X(4)    VALUE SPACES.               
               10 L327-DASH02           PIC X       VALUE SPACES.               
               10 L327-NUM-SUBUNIT      PIC X(3)    VALUE SPACES.               
           05  FILLER                   PIC X(3)    VALUE SPACES.               
           05  L327-NUM-ROOM            PIC X(2)    VALUE SPACES.               
           05  FILLER                   PIC X(3)    VALUE SPACES.               
           05  L327-DTE-ACCEPT          PIC X(10)   VALUE SPACES.               
           05  FILLER                   PIC X(3)    VALUE SPACES.               
           05  L327-DTE-CV-SENT         PIC X(10)   VALUE SPACES.               
           05  FILLER                   PIC X(4)    VALUE SPACES.               
           05  L327-DTE-CV              PIC X(10)   VALUE SPACES.               
           05  FILLER                   PIC X(3)    VALUE SPACES.               
           05  L327-DTE-TP              PIC X(10)   VALUE SPACES.               
                                                                                
       01  L327-FOOTER.                                                         
           05  FILLER                   PIC X(3)    VALUE SPACES.               
           05  FILLER                   PIC X(21)   VALUE                       
               'TOTAL NO. OF CASES : '.                                         
           05  L327-TOT-CASES           PIC ZZZZ9   VALUE ZEROS.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-OPENING-ROUTINE      THRU 1000-EXIT.                    
           PERFORM 1500-READ-BP13F410        THRU 1500-EXIT.                    
           PERFORM 2000-PROCESS-ROUTINE      THRU 2000-EXIT                     
                       UNTIL WS-F410-EOF = 'Y'.                                 
           PERFORM 6000-PRINT-FOOTER         THRU 6000-EXIT.                    
           PERFORM 9000-CLOSING-ROUTINE      THRU 9000-EXIT.                    
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F410                                                 
                       BM06K110                                                 
                OUTPUT BP13L327.                                                
                                                                                
           IF BM06K110-STATUS NOT = 0 AND 97                                    
              DISPLAY 'OPENING ERROR, K110-STATUS ' BM06K110-STATUS             
              MOVE BM06K110-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT.                      
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO L327-DATE.                             
                                                                                
           EVALUATE WS-DATE(5:2)                                                
           WHEN '01'                                                            
                MOVE 'JAN' TO L327-HDR-MTH                                      
           WHEN '02'                                                            
                MOVE 'FEB' TO L327-HDR-MTH                                      
           WHEN '03'                                                            
                MOVE 'MAR' TO L327-HDR-MTH                                      
           WHEN '04'                                                            
                MOVE 'APR' TO L327-HDR-MTH                                      
           WHEN '05'                                                            
                MOVE 'MAY' TO L327-HDR-MTH                                      
           WHEN '06'                                                            
                MOVE 'JUN' TO L327-HDR-MTH                                      
           WHEN '07'                                                            
                MOVE 'JUL' TO L327-HDR-MTH                                      
           WHEN '08'                                                            
                MOVE 'AUG' TO L327-HDR-MTH                                      
           WHEN '09'                                                            
                MOVE 'SEP' TO L327-HDR-MTH                                      
           WHEN '10'                                                            
                MOVE 'OCT' TO L327-HDR-MTH                                      
           WHEN '11'                                                            
                MOVE 'NOV' TO L327-HDR-MTH                                      
           WHEN '12'                                                            
                MOVE 'DEC' TO L327-HDR-MTH                                      
           END-EVALUATE.                                                        
                                                                                
           MOVE WS-DATE(1:4) TO L327-HDR-YEAR.                                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1500-READ-BP13F410.                                                      
      *-------------------------------------------------------------            
           READ BP13F410                                                        
                AT END MOVE 'Y' TO WS-F410-EOF                                  
                GO TO 1500-EXIT.                                                
                                                                                
           ADD 1 TO NO-F410-READ.                                               
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-PROCESS-ROUTINE.                                                    
      *-------------------------------------------------------------            
           MOVE SPACES TO          L327-NUM-REGN                                
                                   L327-NUM-BLK                                 
                                   L327-NME-STREET                              
                                   L327-NUM-UNIT                                
                                   L327-NUM-ROOM                                
                                   L327-DTE-ACCEPT                              
                                   L327-DTE-CV-SENT                             
                                   L327-DTE-CV                                  
                                   L327-DTE-TP.                                 
                                                                                
           MOVE SPACES        TO K110-REGN-NO.                                  
           MOVE F410-NUM-REGN TO K110-REGN-NO                                   
                                 L327-NUM-REGN.                                 
           READ BM06K110 KEY IS K110-REGN-NO.                                   
                                                                                
           IF BM06K110-STATUS = 00                                              
              IF K110-CDE-PROTECTED = 'Y'                                       
                 IF K110-DTE-CV-ENDORSEMENT = SPACES OR                         
                    K110-DTE-CV-ENDORSEMENT = ZEROS  OR                         
                    K110-DTE-CV-ENDORSEMENT < WS-DATE                           
                    PERFORM 3000-PRINT-REPORT THRU 3000-EXIT                    
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              IF BM06K110-STATUS = 23                                           
                 DISPLAY '.REC NOT FND IN BM06K110, REGN IS '                   
                         K110-REGN-NO                                           
               ELSE                                                             
                 DISPLAY 'ERROR READING BM06K110, STATUS '                      
                         BM06K110-STATUS                                        
                 MOVE BM06K110-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSING-ROUTINE THRU 9000-EXIT.                   
                                                                                
           PERFORM 1500-READ-BP13F410 THRU 1500-EXIT.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PRINT-REPORT.                                                       
      *-------------------------------------------------------------            
           PERFORM 4000-LINK-BP13C913 THRU 4000-EXIT.                           
                                                                                
           STRING K110-DTE-ACCEPTANCE(7:2) '/'                                  
                  K110-DTE-ACCEPTANCE(5:2) '/'                                  
                  K110-DTE-ACCEPTANCE(1:4)                                      
                  DELIMITED BY SIZE INTO L327-DTE-ACCEPT.                       
                                                                                
           STRING K110-DTE-CV-SENT(7:2) '/'                                     
                  K110-DTE-CV-SENT(5:2) '/'                                     
                  K110-DTE-CV-SENT(1:4)                                         
                  DELIMITED BY SIZE INTO L327-DTE-CV-SENT.                      
                                                                                
           STRING K110-DTE-CV-ENDORSEMENT(7:2) '/'                              
                  K110-DTE-CV-ENDORSEMENT(5:2) '/'                              
                  K110-DTE-CV-ENDORSEMENT(1:4)                                  
                  DELIMITED BY SIZE INTO L327-DTE-CV.                           
                                                                                
           STRING K110-DTE-KEY-ISSUED(7:2) '/'                                  
                  K110-DTE-KEY-ISSUED(5:2) '/'                                  
                  K110-DTE-KEY-ISSUED(1:4)                                      
                  DELIMITED BY SIZE INTO L327-DTE-TP.                           
                                                                                
           MOVE COMM8-NUM-BLK             TO L327-NUM-BLK.                      
           MOVE COMM8-NME-STREET          TO L327-NME-STREET.                   
           MOVE COMM8-NUM-LEVEL           TO L327-NUM-LEVEL.                    
           MOVE '-'                       TO L327-DASH01.                       
           MOVE COMM8-NUM-UNIT-MAIN       TO L327-NUM-MAINUNIT.                 
                                                                                
           IF COMM8-NUM-UNIT-SUB = SPACES                                       
              MOVE SPACE       TO  L327-DASH02                                  
                                   L327-NUM-SUBUNIT                             
           ELSE                                                                 
              MOVE COMM8-NUM-UNIT-SUB    TO L327-NUM-SUBUNIT                    
              MOVE '-'                   TO L327-DASH02.                        
                                                                                
           MOVE COMM8-CDE-TYP-ACTLUSE     TO L327-NUM-ROOM.                     
                                                                                
           IF WS-LINE-CNT > 55                                                  
              PERFORM 5000-PRINT-HEADER THRU 5000-EXIT.                         
                                                                                
           ADD 1 TO WS-SNO.                                                     
           MOVE WS-SNO TO L327-SNO.                                             
           WRITE L327-PRTREC FROM L327-DETAILS AFTER 2.                         
           ADD 2 TO WS-LINE-CNT.                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-LINK-BP13C913.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES               TO BP13COMM8-REC.                          
           INITIALIZE                   BP13COMM8-REC.                          
                                                                                
           MOVE K110-SCH-ACC-NO      TO WS-LINK-REC.                            
                                                                                
      *--------------------------------*                                        
      * CALL BP13C913 TO GET FLAT INFO *                                        
      *--------------------------------*                                        
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              MOVE COMM8-CDE-SYSERR TO SQL-CODE                                 
              DISPLAY 'ERROR IN READING PBF TABLE ' SQL-CODE.                   
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-PRINT-HEADER.                                                       
      *-------------------------------------------------------------            
           ADD 1 TO WS-PAGE-CNT.                                                
           MOVE WS-PAGE-CNT TO L327-PAGE.                                       
           MOVE SPACES TO L327-PRTREC.                                          
           WRITE L327-PRTREC FROM L327-HEADER01 AFTER PAGE.                     
           WRITE L327-PRTREC FROM L327-HEADER02.                                
           WRITE L327-PRTREC FROM L327-HEADER03 AFTER 2.                        
           WRITE L327-PRTREC FROM L327-HEADER04.                                
           MOVE 5 TO WS-LINE-CNT.                                               
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-PRINT-FOOTER.                                                       
      *-------------------------------------------------------------            
           IF WS-LINE-CNT > 55                                                  
              PERFORM 5000-PRINT-HEADER THRU 5000-EXIT.                         
                                                                                
           MOVE NO-F410-READ TO L327-TOT-CASES.                                 
           WRITE L327-PRTREC FROM L327-FOOTER AFTER 3.                          
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
                                                                                
           CLOSE BP13F410                                                       
                 BM06K110                                                       
                 BP13L327.                                                      
                                                                                
           IF BM06K110-STATUS NOT = 0                                           
              MOVE BM06K110-STATUS TO RETURN-CODE                               
              DISPLAY 'CLOSING ERROR, K110-STATUS ' BM06K110-STATUS.            
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
