       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C029.                                                 
       AUTHOR.        SENTHILVEL.                                               
       DATE-WRITTEN.  13/04/2004                                                
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE  : TO UPDATE THE K520-RECORD                     *            
      * ========================================================== *            
      * INPUT FILES  :  1.  BP13F520                               *            
      *                 2.  BP13K520                               *            
      *                                                            *            
      * OUTPUT FILES :  1.  BP13K520  (UPDATE)                     *            
      * ---------------------------------------------------------- *            
      * CHG REF   DATE     BY    DESCRIPTIONS                      *            
      * --------- ----     --    ------------                      *            
      * BP132496  13042004 PGS1  NEW PROGRAM                       *            
      * BP132496  03052004 PGS1  TO CHANGE THE KEY TO K520-KEY-NEW *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
            SELECT BP13F520 ASSIGN TO BP13F520.                                 
                                                                                
            SELECT BP13K520 ASSIGN TO BP13K520                                  
                   ACCESS MODE     IS RANDOM                                    
                   ORGANIZATION    IS INDEXED                                   
                   RECORD KEY      IS K520-KEY-NEW                              
                   ALTERNATE KEY   IS K520-ALT1-KEY                             
                   FILE STATUS     IS BP13K520-STATUS.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F520                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       COPY BP13F520.                                                           
                                                                                
       FD   BP13K520.                                                           
       COPY BP13K520.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01 BP13K520-STATUS                    PIC 9(2)   VALUE ZEROS.            
       01 WS-BP13K520-READ                   PIC 9(6)   VALUE ZEROS.            
       01 WS-BP13F520-READ                   PIC 9(6)   VALUE ZEROS.            
       01 WS-BP13K520-REWRITE                PIC 9(6)   VALUE ZEROS.            
       01 WS-K520-NOTFND                     PIC 9(6)   VALUE ZEROS.            
       01 WS-ALT-UNMATCH                     PIC 9(6)   VALUE ZEROS.            
       01 WS-JBREGN-NTFND                    PIC 9(6)   VALUE ZEROS.            
                                                                                
       01 WS-NUM-SEQUENCE                    PIC X(8)   VALUE SPACES.           
       01 WS-NUM-JB-PARENT-REGN              PIC X(8)   VALUE SPACES.           
       01 WS-NUM-JB-REGN                     PIC X(8)   VALUE SPACES.           
       01 WS-K520-NUM-JB-REGN                PIC X(8)   VALUE SPACES.           
       01 WS-K520-NUM-REGN                   PIC X(8)   VALUE SPACES.           
                                                                                
       01 WS-CUR-DATE                        PIC X(8)   VALUE SPACES.           
       01 WS-EDT-DATE                        PIC X(10)  VALUE SPACES.           
       01 WS-F520-EOF                        PIC X      VALUE 'N'.              
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
            PERFORM 1000-OPEN-ROUTINE     THRU 1000-EXIT.                       
            PERFORM 2000-READ-BP13F520    THRU 2000-EXIT.                       
            PERFORM 3000-PROCESS-DETAIL   THRU 3000-EXIT                        
                    UNTIL WS-F520-EOF = 'Y'.                                    
            PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT.                       
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
            OPEN  INPUT  BP13F520                                               
                  I-O    BP13K520.                                              
                                                                                
            IF BP13K520-STATUS NOT = 00 AND 97                                  
               DISPLAY 'OPENING ERROR K520-STATUS IS ' BP13K520-STATUS          
               MOVE BP13K520-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                        
            END-IF.                                                             
                                                                                
            MOVE FUNCTION CURRENT-DATE (1:8) TO WS-CUR-DATE.                    
                                                                                
            STRING WS-CUR-DATE(7:2) '/' WS-CUR-DATE(5:2) '/'                    
                   WS-CUR-DATE(1:4) DELIMITED BY SIZE INTO WS-EDT-DATE.         
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       2000-READ-BP13F520.                                                      
      ******************************************************************        
            READ BP13F520 AT END MOVE 'Y' TO WS-F520-EOF                        
                             GO TO  2000-EXIT.                                  
            ADD 1   TO  WS-BP13F520-READ.                                       
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3000-PROCESS-DETAIL.                                                     
      ******************************************************************        
            MOVE  SPACES  TO  K520-ALT1-KEY     WS-NUM-JB-PARENT-REGN           
                              WS-NUM-SEQUENCE   WS-NUM-JB-REGN                  
                              WS-K520-NUM-REGN  WS-K520-NUM-JB-REGN.            
                                                                                
            MOVE  F520-ALT1-KEY        TO   K520-ALT1-KEY.                      
            PERFORM 3100-READ-BP13K520 THRU 3100-EXIT.                          
                                                                                
            IF K520-NUM-REGN  NOT = SPACES AND LOW-VALUES                       
               MOVE K520-NUM-JB-REGN    TO   WS-K520-NUM-JB-REGN                
               MOVE K520-NUM-REGN       TO   WS-K520-NUM-REGN                   
                                                                                
               IF K520-ALT1-KEY  =  K520-NUM-SEQUENCE                           
                  MOVE K520-NUM-REGN TO  WS-NUM-SEQUENCE                        
                                         WS-NUM-JB-PARENT-REGN                  
               ELSE                                                             
                 ADD   1                    TO   WS-ALT-UNMATCH                 
                 MOVE  SPACES               TO   K520-ALT1-KEY                  
                 MOVE  K520-NUM-SEQUENCE    TO   K520-ALT1-KEY                  
                 PERFORM 3100-READ-BP13K520 THRU 3100-EXIT                      
                                                                                
                 IF K520-NUM-REGN  =  SPACES OR LOW-VALUES                      
                  MOVE K520-NUM-SEQUENCE       TO WS-NUM-SEQUENCE               
                  MOVE K520-NUM-JB-PARENT-REGN TO WS-NUM-JB-PARENT-REGN         
                 ELSE                                                           
                  MOVE K520-NUM-REGN      TO WS-NUM-SEQUENCE                    
                                             WS-NUM-JB-PARENT-REGN              
                 END-IF                                                         
               END-IF                                                           
                                                                                
               IF F520-ALT1-KEY     =  WS-K520-NUM-JB-REGN                      
                  MOVE WS-K520-NUM-REGN      TO   K520-NUM-JB-REGN              
                  MOVE WS-NUM-SEQUENCE       TO   K520-NUM-SEQUENCE             
                  MOVE WS-NUM-JB-PARENT-REGN TO K520-NUM-JB-PARENT-REGN         
                  PERFORM 3200-REWRITE-K520  THRU 3200-EXIT                     
               ELSE                                                             
                  MOVE  SPACES               TO   K520-ALT1-KEY                 
                  MOVE  WS-K520-NUM-JB-REGN  TO   K520-ALT1-KEY                 
                  PERFORM 3100-READ-BP13K520 THRU 3100-EXIT                     
                                                                                
                  IF K520-NUM-REGN = SPACES OR LOW-VALUES                       
                     MOVE K520-NUM-JB-REGN   TO   WS-NUM-JB-REGN                
                     ADD       1             TO   WS-JBREGN-NTFND               
                  ELSE                                                          
                    MOVE K520-NUM-REGN       TO   WS-NUM-JB-REGN                
                  END-IF                                                        
                                                                                
      ** TO READ THE FIRST RECORD AGAIN                                         
                  MOVE  SPACES               TO   K520-ALT1-KEY                 
                  MOVE  F520-ALT1-KEY        TO   K520-ALT1-KEY                 
                  PERFORM 3100-READ-BP13K520 THRU 3100-EXIT                     
                                                                                
                  MOVE WS-NUM-SEQUENCE       TO   K520-NUM-SEQUENCE             
                  MOVE WS-NUM-JB-PARENT-REGN TO K520-NUM-JB-PARENT-REGN         
                  MOVE WS-NUM-JB-REGN        TO   K520-NUM-JB-REGN              
                  PERFORM 3200-REWRITE-K520  THRU 3200-EXIT                     
               END-IF                                                           
            END-IF.                                                             
            PERFORM 2000-READ-BP13F520     THRU 2000-EXIT.                      
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3100-READ-BP13K520.                                                      
      ******************************************************************        
            READ BP13K520 KEY IS K520-ALT1-KEY.                                 
            ADD  1 TO WS-BP13K520-READ.                                         
            EVALUATE BP13K520-STATUS                                            
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN 23                                                          
                    MOVE SPACES  TO BP13K520-REC                                
                    ADD     1    TO WS-K520-NOTFND                              
               WHEN OTHER                                                       
                    DISPLAY 'BP13K520 READ ERROR : ' BP13K520-STATUS            
                    DISPLAY 'BP13K520 REGN       : ' K520-ALT1-KEY              
                    MOVE BP13K520-STATUS         TO   RETURN-CODE               
                    PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                 
            END-EVALUATE.                                                       
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3200-REWRITE-K520.                                                       
      ******************************************************************        
            REWRITE BP13K520-REC.                                               
            ADD  1  TO WS-BP13K520-REWRITE.                                     
            EVALUATE BP13K520-STATUS                                            
               WHEN 00                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K520 REWRITE ERROR : ' BP13K520-STATUS         
                    DISPLAY 'BP13K520 REGN          : ' K520-ALT1-KEY           
                    MOVE BP13K520-STATUS         TO   RETURN-CODE               
                    PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                 
            END-EVALUATE.                                                       
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       9000-CLOSE-ROUTINE.                                                      
      ******************************************************************        
            DISPLAY ' '.                                                        
            DISPLAY '-----------------------------------'.                      
            DISPLAY '       BP13C029 CONTROL LIST       '.                      
            DISPLAY '       RUN DATE : '  WS-EDT-DATE.                          
            DISPLAY '-----------------------------------'.                      
            DISPLAY ' 1) TOT BP13F520 READ     : ' WS-BP13F520-READ.            
            DISPLAY ' 2) TOT BP13K520 READ     : ' WS-BP13K520-READ.            
            DISPLAY ' 3) TOT BP13K520 REWRITE  : ' WS-BP13K520-REWRITE.         
            DISPLAY ' 4) TOT BP13K520 NOTFND   : ' WS-K520-NOTFND.              
            DISPLAY ' 5) TOT BP13K520 ALT UNMAT: ' WS-ALT-UNMATCH.              
            DISPLAY ' 6) TOT BP13K520 JBREG NF : ' WS-JBREGN-NTFND.             
            DISPLAY ' '.                                                        
                                                                                
            CLOSE  BP13F520                                                     
                   BP13K520.                                                    
                                                                                
            IF BP13K520-STATUS NOT = 00 AND 97                                  
               DISPLAY 'CLOSING ERROR BP13K520 : ' BP13K520-STATUS              
               MOVE BP13K520-STATUS TO RETURN-CODE                              
            END-IF.                                                             
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
