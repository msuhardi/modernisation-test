       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C919.                                                 
      *AUTHOR.        EVM.                                                      
      *DATE-WRITTEN.  10/01/02.                                                 
      *DATE-COMPILED.                                                           
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *    1.   PROGRAM TO MATCH NRIC AGAINST DEBARMENT FILE   *                
      *         AND OUTPUT TO FILE IF WITHIN VALID REASON CODE.*                
      *                                                        *                
      *    INPUT FILES :                                       *                
      *    1.   BP13F824                                       *                
      *    2.   P04K010                                        *                
      *                                                        *                
      *    OUTPUT FILE  :                                      *                
      *    1.   OUTF824  (SIMILAR FORMAT AS BP13F824)          *                
      *                                                        *                
      * CHG-NO.  BY  ON       DESCRIPTION                      *                
      * -------- --- -------- -----------                      *                
      * BP131064 LSB 20020110 NEW PROGRAM                      *                
      * BP132396 CKK 20030825 TO INCLUDE DEBARMENT CODE 71 & EC*                
      * BP132684 CKK 20050425 TO INCLUDE NEW DEBARMENT CODE    *                
      * BP133320 IL5 20080609 TO INCLUDE NEW DEBARMENT CODE    *                
      * BP133611 ZDD120090520 TO INCLUDE NEW DEBARMENT CODE    *                
      * BP133964 ZDD120100727 TO INCLUDE NEW DEBARMENT CODE    *                
      * BP134242 ZDD120110519 TO INCLUDE NEW DEBARMENT CODE    *                
      * BP134778 IL5 20130125 TO INCLUDE 'PH'                  *                
      * BP134906 SMR 20130510 TO INCLUDE 'DZ', 'DX', 'DY'      *                
      * BP134996 SMR 20130722 TO INCLUDE 'FM', 'FN', 'AS'      *                
      * BP135080 SMR 20130925 TO INCLUDE 'SS'                  *                
      * BP135148 IL5 20131125 TO INCLUDE '4F' 'FX' 'FY' 'AT'   *                
      *                                  'FD' 'FE'             *                
      * BP136007 IL5 20151103 ORGANIZE DEBARMENT CODES         *                
      * BP137048 SMR 20171106 TO INCLUDE 'FF', '5T', '5U'      *                
      * BP137378 IL5 20180724 TO INCLUDE 'MH', 'MJ'            *                
      * BP13XXXX IL5 20180925 TO INCL PV/PX/PY/PZ/Q1/Q2/Q3/Q4  *                
      * BP137662 AM2 20190129 TO INCLUDE DEBAR CODE 'RX'       *                
      * BP137920 AM2 20190718 TO INCLUDE DEBAR CODE 'TB' 'TC'  *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F824   ASSIGN TO BP13F824.                                
                                                                                
           SELECT P04K010    ASSIGN TO P04K010                                  
                  ACCESS MODE IS SEQUENTIAL                                     
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K010-PRIME-KEY                                  
                  FILE STATUS IS WS-K010-STATUS.                                
                                                                                
           SELECT OUTF824    ASSIGN TO OUTF824.                                 
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F824                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 250 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F824.                                                           
                                                                                
       FD   P04K010                                                             
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY P04K010.                                                            
                                                                                
       FD   OUTF824                                                             
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 250 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       01   OUTF824-REC.                                                        
            05 FILLER                     PIC X(200).                           
            05 OUTF824-REGN-BIG           PIC X(8).                             
            05 OUTF824-MULTIPLE           PIC X(1).                             
            05 OUTF824-ZONE               PIC X(2).                             
            05 OUTF824-FT                 PIC X(2).                             
            05 FILLER                     PIC X(7).                             
            05 OUTF824-REASON-CDE         PIC X(2).                             
            05 OUTF824-DTE-BALLOT         PIC X(6).                             
            05 OUTF824-DTE-RAISE-AO       PIC X(8).                             
            05 FILLER                     PIC X(14).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-K010-EOF                 PIC X(1).                                
           88  K010-EOF                           VALUE 'Y'.                    
       01  WS-F824-EOF                 PIC X(1).                                
           88  F824-EOF                           VALUE 'Y'.                    
                                                                                
       01  WS-K010-STATUS              PIC 9(2)   VALUE ZEROES.                 
       01  WS-K010-UPDATE              PIC 9(8)   VALUE ZEROES.                 
       01  WS-CHECK                    PIC 9(6)   VALUE ZEROES.                 
       01  WS-CURRENT-DATE             PIC 9(8)   VALUE ZEROES.                 
       01  WS-SYS-DTE-CCYYMMDD         PIC 9(8)   VALUE ZEROES.                 
       01  WS-K010-LUPDATE             PIC 9(8)   VALUE ZEROES.                 
       01  WS-K010-LUPDATE-INTEGER     PIC 9(8)   VALUE ZEROES.                 
       01  WS-CURR-DATE-INTEGER        PIC 9(8)   VALUE ZEROES.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F824-READ            PIC 9(7)   VALUE ZEROES.                 
           05  WS-F824-NMR             PIC 9(7)   VALUE ZEROES.                 
           05  WS-F824-K010-MR         PIC 9(7)   VALUE ZEROES.                 
           05  WS-K010-READ            PIC 9(7)   VALUE ZEROES.                 
           05  WS-K010-NMR             PIC 9(7)   VALUE ZEROES.                 
           05  WS-K010-BYPASS-RSN      PIC 9(7)   VALUE ZEROES.                 
           05  WS-K010-BYPASS-CHECK    PIC 9(7)   VALUE ZEROES.                 
           05  WS-PROCESS-CTR          PIC 9(7)   VALUE ZEROES.                 
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      ******************************************************************        
       MAIN-ROUTINE SECTION.                                                    
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F824   THRU 2000-EXIT.                         
           PERFORM 2100-READ-P04K010    THRU 2100-EXIT.                         
           PERFORM 3000-PROCESS-F824    THRU 3000-EXIT                          
            UNTIL  F824-NUM-NRIC = HIGH-VALUE                                   
             AND   K010-NUM-UINFIN = HIGH-VALUE.                                
           PERFORM 9999-CLOSE-RTN THRU 9999-EXIT.                               
                                                                                
       MAIN-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
                                                                                
           OPEN INPUT   BP13F824 P04K010                                        
                OUTPUT  OUTF824.                                                
                                                                                
           IF WS-K010-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'P04K010 OPEN ERROR ' WS-K010-STATUS                      
              MOVE WS-K010-STATUS TO RETURN-CODE                                
              GO TO 9999-CLOSE-RTN.                                             
                                                                                
           MOVE FUNCTION CURRENT-DATE (1:8) TO WS-CURRENT-DATE.                 
           COMPUTE WS-CURR-DATE-INTEGER =                                       
                   FUNCTION INTEGER-OF-DATE (WS-CURRENT-DATE).                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F824.                                                      
      ******************************************************************        
                                                                                
           READ BP13F824 AT END                                                 
                MOVE HIGH-VALUE   TO F824-NUM-NRIC                              
                MOVE HIGH-VALUE   TO K010-NUM-UINFIN                            
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                  TO WS-F824-READ.                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2100-READ-P04K010.                                                       
      ******************************************************************        
                                                                                
           READ P04K010  AT END                                                 
                MOVE HIGH-VALUE   TO K010-NUM-UINFIN                            
                GO TO 2100-EXIT.                                                
                                                                                
           ADD 1                  TO WS-K010-READ.                              
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-F824.                                                       
      ******************************************************************        
           IF F824-NUM-NRIC = K010-NUM-UINFIN                                   
                ADD 1 TO WS-F824-K010-MR                                        
                PERFORM 3100-PROCESS-P04K010  THRU 3100-EXIT                    
                  UNTIL K010-NUM-UINFIN NOT = F824-NUM-NRIC                     
                    OR  K010-NUM-UINFIN = HIGH-VALUE                            
                    OR  F824-NUM-NRIC = HIGH-VALUE                              
                PERFORM 2000-READ-BP13F824    THRU 2000-EXIT                    
           ELSE                                                                 
               IF F824-NUM-NRIC < K010-NUM-UINFIN                               
                  ADD 1 TO WS-F824-NMR                                          
                  PERFORM 2000-READ-BP13F824    THRU 2000-EXIT                  
               ELSE                                                             
                  ADD 1 TO WS-K010-NMR                                          
                  PERFORM 2100-READ-P04K010 THRU 2100-EXIT                      
               END-IF                                                           
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-PROCESS-P04K010.                                                    
      ******************************************************************        
                                                                                
      *        DISPLAY ' ' F824-KEY-FLD                                         
      *               ' REASON= ' K010-REASON-CODE.                             
                                                                                
           IF K010-REASON-CODE = '07' OR '11' OR '18' OR                        
                                 '33' OR '39' OR                                
                                 '40' OR '41' OR '4E' OR '4F' OR                
                                 '4J' OR                                        
                                 '5B' OR '5C' OR '5T' OR '5U' OR                
                                 '62' OR '64' OR '66' OR                        
                                 '6A' OR '6B' OR '6C' OR '6D' OR                
                                 '70' OR '71' OR '77' OR '7A' OR                
                                 'AC' OR 'AD' OR 'AS' OR 'AT' OR                
                                 'CG' OR                                        
                                 'DU' OR 'DV' OR                                
                                 'DW' OR 'DX' OR 'DY' OR 'DZ' OR                
                                 'E5' OR 'E6' OR                                
                                 'EA' OR 'EB' OR 'EC' OR 'ED' OR                
                                 'EE' OR 'EF' OR 'EG' OR 'EH' OR                
                                 'EI' OR 'EK' OR 'EM' OR 'EO' OR                
                                 'EQ' OR 'ER' OR 'ES' OR 'ET' OR                
                                 'EU' OR                                        
                                 'FC' OR 'FD' OR 'FE' OR 'FF' OR                
                                 'FM' OR                                        
                                 'FN' OR 'FS' OR 'FW' OR 'FX' OR                
                                 'FY' OR                                        
                                 'GA' OR 'GB' OR 'GC' OR 'GD' OR                
                                 'GE' OR 'GF' OR 'GG' OR 'GH' OR                
                                 'GI' OR 'GJ' OR 'GK' OR 'GL' OR                
                                 'GM' OR 'GN' OR 'GP' OR 'GQ' OR                
                                 'GR' OR 'GS' OR 'GT' OR 'GU' OR                
                                 'GV' OR 'GW' OR 'GX' OR 'GY' OR                
                                 'GZ' OR                                        
                                 'JA' OR 'JB' OR 'JC' OR 'JD' OR                
                                 'JE' OR 'JF' OR 'JG' OR 'JH' OR                
                                 'JI' OR 'JJ' OR 'JK' OR 'JL' OR                
                                 'JM' OR 'JN' OR 'JO' OR 'JP' OR                
                                 'JQ' OR 'JR' OR                                
                                 'LA' OR 'LB' OR 'LC' OR 'LD' OR                
                                 'LE' OR 'LF' OR 'LG' OR 'LH' OR                
                                 'LI' OR 'LJ' OR 'LK' OR 'LL' OR                
                                 'MA' OR 'MB' OR 'MC' OR 'MD' OR                
                                 'ME' OR 'MF' OR 'MH' OR 'MJ' OR                
                                 'NA' OR 'NB' OR 'NC' OR 'ND' OR                
                                 'NE' OR 'NF' OR 'NG' OR 'NH' OR                
                                 'NI' OR 'NJ' OR 'NK' OR 'NL' OR                
                                 'NM' OR 'NN' OR 'NO' OR 'NP' OR                
                                 'NQ' OR 'NR' OR 'NS' OR 'NT' OR                
                                 'NU' OR 'NV' OR 'NW' OR 'NX' OR                
                                 'PA' OR 'PB' OR 'PC' OR 'PD' OR                
                                 'PE' OR 'PF' OR 'PH' OR 'PO' OR                
                                 'PU' OR 'PP' OR                                
                                 'PV' OR 'PX' OR 'PY' OR 'PZ' OR                
                                 'Q1' OR 'Q2' OR 'Q3' OR 'Q4' OR                
                                 'QF' OR                                        
                                 'R1' OR 'R2' OR 'RA' OR 'RX' OR                
                                 'SS' OR                                        
                                 'TB' OR 'TC' OR                                
                                 'UA' OR 'UC' OR 'UD' OR 'UE' OR                
                                 'UF' OR 'UG' OR 'UH' OR 'UI' OR                
                                 'UL' OR 'UM' OR                                
                                 'YR' OR 'YS' OR                                
                                 'Z1' OR 'Z2' OR 'Z5' OR 'Z6' OR                
                                 'ZA' OR 'ZC'                                   
                                                                                
      *        DISPLAY '*** ' F824-KEY-FLD                                      
      *               ' REASON= ' K010-REASON-CODE                              
             MOVE WS-CURRENT-DATE TO F824-DTE-UPDATE                            
             MOVE K010-EX-SCH-ACCTNO TO F824-NUM-SCH-ACC                        
             MOVE BP13F824-REC   TO OUTF824-REC                                 
             MOVE K010-REASON-CODE TO OUTF824-REASON-CDE                        
             WRITE OUTF824-REC                                                  
             ADD 1               TO WS-PROCESS-CTR                              
           END-IF.                                                              
                                                                                
           PERFORM 2100-READ-P04K010 THRU 2100-EXIT.                            
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-RTN.                                                          
      ******************************************************************        
           DISPLAY 'PROGRAM : BP13C919    '.                                    
           DISPLAY 'DATE    : ' WS-CURRENT-DATE.                                
           DISPLAY '*=========== COUNTERS ===========*'                         
           DISPLAY 'F824-REC-READ        : ' WS-F824-READ.                      
           DISPLAY 'K010-REC-READ        : ' WS-K010-READ.                      
           DISPLAY ' '                                                          
           DISPLAY 'K010-REC-NOT FND     : ' WS-K010-NMR.                       
           DISPLAY 'F824-REC-NOT FND     : ' WS-F824-NMR.                       
           DISPLAY 'NO. OF REC MR        : ' WS-F824-K010-MR.                   
           DISPLAY ' '                                                          
           DISPLAY 'REC DEBAR UNDER SERS : ' WS-PROCESS-CTR.                    
                                                                                
           CLOSE P04K010  BP13F824                                              
                 OUTF824.                                                       
                                                                                
           IF WS-K010-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'P04K010 CLOSE ERROR ' WS-K010-STATUS                     
              MOVE WS-K010-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
