       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C10E.                                                 
       AUTHOR.        ELGER DE GUZMAN                                           
      *DATE-WRITTEN.  16/04/2014.                                               
      * ===============================================================*        
      * SYSTEM NAME (SYSTEM ID)                                        *        
      * ===============================================================*        
      * OBJECTIVE   : CREATE MAIL FILE FOR DUPLICATE DEBARMENT         *        
      * BP134740  ESD1  170514  NEW PROGRAM                            *        
      * BP135537  RJB1  141015  CHANGE THE EMAIL RECIPIENT FROM NICKNAME        
      *                         INTO COMPLETE NAME                     *        
      *================================================================*        
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT P04K010   ASSIGN        TO P04K010.                           
                                                                                
           SELECT BP13K800  ASSIGN TO BP13K800                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K800-NUM-REGN                             
                  FILE STATUS      IS K800-STATUS                               
                  ACCESS           IS RANDOM.                                   
                                                                                
           SELECT BP13K893  ASSIGN TO BP13K893                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K893-KEY-FLD                              
                  FILE STATUS      IS BP13K893-STATUS                           
                  ACCESS           IS DYNAMIC.                                  
                                                                                
           SELECT BP13K895  ASSIGN TO BP13K895                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K895-KEY-FLD                              
                  ALTERNATE RECORD KEY IS K895-NUM-NRIC-HIST                    
                  FILE STATUS      IS BP13K895-STATUS                           
                  ACCESS           IS DYNAMIC.                                  
                                                                                
           SELECT BP27K025  ASSIGN TO BP27K025                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K025-KEY-FLD                              
                  ALTERNATE RECORD KEY IS K025-NUM-HLA                          
                  FILE STATUS      IS BP27K025-STATUS                           
                  ACCESS           IS DYNAMIC.                                  
                                                                                
           SELECT BP13K820  ASSIGN TO BP13K820                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K820-KEY-FLD                              
                  ALTERNATE RECORD KEY IS K820-NUM-NRIC                         
                  FILE STATUS      IS K820-STATUS                               
                  ACCESS           IS RANDOM.                                   
                                                                                
           SELECT BP13FOUT  ASSIGN        TO BP13FOUT.                          
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  P04K010                                                              
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 300 CHARACTERS.                                      
       COPY P04K010.                                                            
                                                                                
       FD  BP13K800                                                             
                                      RECORD CONTAINS 2000 CHARACTERS.          
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K893                                                             
                                      RECORD CONTAINS 2050 CHARACTERS.          
       COPY BP13K893.                                                           
                                                                                
       FD  BP27K025                                                             
                                      RECORD CONTAINS 100 CHARACTERS.           
       COPY BP27K025.                                                           
                                                                                
       FD  BP13K820                                                             
                                      RECORD CONTAINS 400 CHARACTERS.           
       COPY BP13K820.                                                           
                                                                                
       FD  BP13K895                                                             
                                      RECORD CONTAINS 450 CHARACTERS.           
       COPY BP13K895.                                                           
                                                                                
       FD  BP13FOUT                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13FOUT-REC      PIC X(132).                                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  VARIABLES.                                                           
           05  WS-K010-EOF                  PIC X      VALUE 'N'.               
       01  WS-K010RPT-COUNT                 PIC 9(7)   VALUE ZERO.              
       01  WS-CTR                           PIC 9(2)   VALUE ZERO.              
       01  WS-FIRST-REPORT                  PIC X      VALUE 'Y'.               
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY                  PIC 9(04).                          
           05  WS-CUR-MM                    PIC 9(02).                          
           05  WS-CUR-DD                    PIC 9(02).                          
                                                                                
       01  WS-STATUS.                                                           
           05 FOUT-STATUS              PIC 99       VALUE ZEROS.                
           05 K800-STATUS              PIC 99       VALUE ZEROS.                
           05 K820-STATUS              PIC 99       VALUE ZEROS.                
           05 WS-K010-END              PIC X        VALUE 'N'.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-K010-RD           PIC 9(6) COMP VALUE 0.                   
           05 WS-CNT-FOUT-WRITE        PIC 9(6) COMP VALUE 0.                   
           05 WS-CURR-MM-DAYS          PIC 9(2) COMP VALUE 0.                   
           05 WS-LINE-CTR              PIC 9(4) VALUE 0.                        
           05 WS-SNO                   PIC 9(4) VALUE 0.                        
           05 WS-SNO-Z                 PIC ZZZ9 VALUE ZEROES.                   
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-NUM-DAYS              PIC 9(4) COMP VALUE 0.                   
           05 WS-INT-NUM-DAYS          PIC 9(9) COMP VALUE 0.                   
           05 WS-INT-DTE-ACCEPTANCE    PIC 9(9) COMP VALUE 0.                   
           05 WS-INT-CURRENT-DATE      PIC 9(9) COMP VALUE 0.                   
           05 WS-NUM-QUOTIENT          PIC 9(6) COMP VALUE 0.                   
           05 WS-NUM-REMAINDER         PIC 9(6) COMP VALUE 0.                   
           05 WS-TEMP-SCHEME           PIC X(9) VALUE SPACES.                   
           05 WS-TEMP-REASON           PIC X(2) VALUE SPACES.                   
           05 WS-TEMP-UIN              PIC X(9) VALUE SPACES.                   
           05 WS-TEMP-REFNO            PIC X(15) VALUE SPACES.                  
           05 WS-TEMP-EFF-DTE          PIC X(10) VALUE SPACES.                  
           05 WS-DUP-FOUND             PIC X(1) VALUE SPACES.                   
           05 WS-ONE-TIME-SW           PIC X(1) VALUE SPACES.                   
           05 WS-TEMP-WRITE            PIC X(1) VALUE SPACES.                   
           05  WS-K893-EOF             PIC X(01) VALUE SPACE.                   
               88 K893-EOF                      VALUE 'Y'.                      
           05  BP13K893-STATUS         PIC 9(2)      VALUE ZEROS.               
           05  WS-K893-FND             PIC X(01) VALUE SPACE.                   
               88 K893-FND                      VALUE 'Y'.                      
                                                                                
           05  WS-K025-EOF             PIC X(01) VALUE SPACE.                   
               88 K025-EOF                      VALUE 'Y'.                      
           05  BP27K025-STATUS         PIC 9(2)      VALUE ZEROS.               
           05  WS-K025-FND             PIC X(01) VALUE SPACE.                   
               88 K025-FND                      VALUE 'Y'.                      
                                                                                
           05  WS-K895-EOF             PIC X(01) VALUE SPACE.                   
               88 K895-EOF                      VALUE 'Y'.                      
           05  BP13K895-STATUS         PIC 9(2)      VALUE ZEROS.               
           05  WS-K895-FND             PIC X(01) VALUE SPACE.                   
               88 K895-FND                      VALUE 'Y'.                      
                                                                                
       01  WS-CURR-DATE-NUM            PIC 9(8).                                
       01  WS-ACCEPT-DT-NUM            PIC 9(8).                                
                                                                                
       01  WS-CURRENT-DATE1.                                                    
           05  WS-DATE1.                                                        
                10  WS-DATE1-CYM.                                               
                    15  WS-DATE1-CCYY  PIC X(4).                                
                    15  WS-DATE1-MM    PIC X(2).                                
                10  WS-DATE1-DD        PIC X(2).                                
           05  WS-CURRENT-TIME         PIC X(9).                                
           05  WS-TIME REDEFINES WS-CURRENT-TIME.                               
                10  WS-HH              PIC 9(2).                                
                10  WS-MM              PIC 9(2).                                
                10  WS-SS              PIC 9(2).                                
                10  WS-MS              PIC 9(2).                                
                10  WS-GM              PIC 9(1).                                
           05  FILLER                  PIC X(4).                                
                                                                                
       01  CS-DATE-VARIABLES.                                                   
           05  WS-DATE.                                                         
               10  WS-CCYY                   PIC 9(4).                          
               10  WS-MM                     PIC 9(2).                          
               10  WS-DD                     PIC 9(2).                          
           05  WS-DTE-KEY-ISSUED.                                               
               10  WS-KI-CCYY                PIC 9(4).                          
               10  WS-KI-MM                  PIC 9(2).                          
               10  WS-KI-DD                  PIC 9(2).                          
           05  WS-FUTURE-DATE.                                                  
               10  WS-DUE-CCYY               PIC 9(4).                          
               10  WS-DUE-MM                 PIC 9(2).                          
               10  WS-DUE-DD                 PIC 9(2).                          
                                                                                
       01  FOUT-LAYOUT.                                                         
           05  FOUT-HEADER.                                                     
               10  FILLER                    PIC X(35) VALUE SPACES.            
               10  FOUT-TITLE                PIC X(66).                         
               10  FOUT-DATE                 PIC X(10).                         
                                                                                
           05  FOUT-HEADER3.                                                    
               10  FILLER           PIC X(4)  VALUE 'S/NO'.                     
               10  FILLER           PIC X(3)  VALUE SPACES.                     
               10  FILLER           PIC X(9)  VALUE 'REGN NO.'.                 
               10  FILLER           PIC X(1)  VALUE SPACES.                     
               10  FILLER           PIC X(7)  VALUE 'HLE NO.'.                  
               10  FILLER           PIC X(4)  VALUE SPACES.                     
               10  FILLER           PIC X(7)  VALUE 'COS OIC'.                  
               10  FILLER           PIC X(4)  VALUE SPACES.                     
               10  FILLER           PIC X(4)  VALUE 'NRIC'.                     
               10  FILLER           PIC X(7)  VALUE SPACES.                     
               10  FILLER           PIC X(12) VALUE 'DEBAR REF.NO'.             
               10  FILLER           PIC X(5)  VALUE SPACES.                     
               10  FILLER           PIC X(11) VALUE 'REASON CODE'.              
               10  FILLER           PIC X(2)  VALUE SPACES.                     
               10  FILLER           PIC X(14) VALUE 'EFFECTIVE DATE'.           
               10  FILLER           PIC X(3)  VALUE SPACES.                     
               10  FILLER           PIC X(14) VALUE 'SCHEME A/C NO.'.           
                                                                                
           05  FOUT-DETAIL1.                                                    
               10  FOUT-SNO                  PIC Z,ZZZ  VALUE ZEROES.           
               10  FILLER                    PIC X(2)   VALUE SPACES.           
               10  FOUT-NUM-REGN             PIC X(8)   VALUE SPACES.           
               10  FILLER                    PIC X(2)   VALUE SPACES.           
               10  FOUT-HLE-NO               PIC X(09)  VALUE SPACES.           
               10  FILLER                    PIC X(2)   VALUE SPACES.           
               10  FOUT-OIC-ID               PIC X(09)  VALUE SPACES.           
               10  FILLER                    PIC X(2)   VALUE SPACES.           
               10  FOUT-NRIC-NO              PIC X(9)   VALUE SPACES.           
               10  FILLER                    PIC X(2)   VALUE SPACES.           
               10  FOUT-DEB-REF-NO           PIC X(15)  VALUE SPACES.           
               10  FILLER                    PIC X(2)   VALUE SPACES.           
               10  FOUT-DEB-REASON           PIC X(2)   VALUE SPACES.           
               10  FILLER                    PIC X(11)  VALUE SPACES.           
               10  FOUT-EFF-DTE              PIC X(10)  VALUE SPACES.           
               10  FILLER                    PIC X(7)   VALUE SPACES.           
               10  FOUT-SCH-ACCNT            PIC X(15)  VALUE SPACES.           
                                                                                
           05  FOUT-DETAIL2.                                                    
               10  FILLER                    PIC X(7)   VALUE SPACES.           
               10  FILLER                    PIC X(4)   VALUE 'NAME'.           
               10  FILLER                    PIC X(6)   VALUE SPACES.           
               10  FOUT-APPLN-NME            PIC X(60)  VALUE SPACES.           
                                                                                
           05  FOUT-DETAIL3.                                                    
               10  FILLER                    PIC X(7)   VALUE SPACES.           
               10  FILLER                    PIC X(7)   VALUE 'ADDRESS'.        
               10  FILLER                    PIC X(3)   VALUE SPACES.           
               10  FOUT-ADDRESS              PIC X(125) VALUE SPACES.           
                                                                                
                                                                                
       01 WS-REPORT-FIELDS.                                                     
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT3             PIC X(40)                               
               VALUE 'RCPT TO:<PATRICIA_SS_CHIN@HDB.GOV.SG>'.                   
           05  WS-MAIL-RPT4             PIC X(40)                               
               VALUE 'RCPT TO:<VIVIAN_LP_CHIA@HDB.GOV.SG>'.                     
           05  WS-MAIL-RPT4A            PIC X(40)                               
               VALUE 'RCPT TO:<TAN_TSIN_GIAP@HDB.GOV.SG>'.                      
           05  WS-MAIL-RPT4B            PIC X(40)                               
               VALUE 'RCPT TO:<TEH_SHIJIN@HDB.GOV.SG>'.                         
           05  WS-MAIL-RPT9             PIC X(4)                                
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT10            PIC X(29)                               
               VALUE 'FROM:SOC_SYSTEM@HDB.GOV.SG'.                              
           05  WS-MAIL-RPT11            PIC X(40)                               
               VALUE 'TO:<PATRICIA_SS_CHIN@HDB.GOV.SG>'.                        
           05  WS-MAIL-RPT12            PIC X(40)                               
               VALUE 'TO:<VIVIAN_LP_CHIA@HDB.GOV.SG>'.                          
           05  WS-MAIL-RPT12A           PIC X(40)                               
               VALUE 'TO:<TAN_TSIN_GIAP@HDB.GOV.SG>'.                           
           05  WS-MAIL-RPT12B           PIC X(40)                               
               VALUE 'TO:<TEH_SHIJIN@HDB.GOV.SG>'.                              
           05  WS-MAIL-RPT17            PIC X(34)                               
               VALUE  'SUBJECT : DUPLICATE DEBARMENT RPT '.                     
           05  WS-MAIL-RPT18.                                                   
               10 WS-ADDR-RPT           PIC X(98).                              
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================*           
       MAIN.                                                                    
      *=============================================================*           
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-PROCESS-INDATA  THRU 2000-EXIT.                         
           MOVE  1 TO WS-SNO                                                    
           MOVE 'Y' TO WS-TEMP-WRITE                                            
           PERFORM 2500-MAIN-PROCESS    THRU 2500-EXIT                          
           UNTIL WS-K010-EOF = 'Y'.                                             
           PERFORM 9999-WRAP-UP         THRU 9999-EXIT.                         
                                                                                
      *=============================================================*           
       1000-OPEN-ROUTINE.                                                       
      *=============================================================*           
                                                                                
            OPEN INPUT  P04K010                                                 
                        BP13K800                                                
                        BP13K893                                                
                        BP13K895                                                
                        BP27K025                                                
                        BP13K820                                                
                 OUTPUT BP13FOUT.                                               
                                                                                
           IF  K800-STATUS NOT = '00' AND '97'                                  
               DISPLAY 'ERROR OPEN BP13K800 : ' K800-STATUS                     
               MOVE K800-STATUS TO RETURN-CODE                                  
               PERFORM 9999-WRAP-UP        THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF  BP13K893-STATUS NOT = '00' AND '97'                              
               DISPLAY 'ERROR OPEN BP13K893 : ' BP13K893-STATUS                 
               MOVE BP13K893-STATUS TO RETURN-CODE                              
               PERFORM 9999-WRAP-UP        THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF  BP13K895-STATUS NOT = '00' AND '97'                              
               DISPLAY 'ERROR OPEN BP13K895 : ' BP13K895-STATUS                 
               MOVE BP13K895-STATUS TO RETURN-CODE                              
               PERFORM 9999-WRAP-UP        THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF  BP27K025-STATUS NOT = '00' AND '97'                              
               DISPLAY 'ERROR OPEN BP27K025 : ' BP27K025-STATUS                 
               MOVE BP27K025-STATUS TO RETURN-CODE                              
               PERFORM 9999-WRAP-UP        THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF  K820-STATUS NOT = '00' AND '97'                                  
               DISPLAY 'ERROR OPEN BP13K820 : ' K820-STATUS                     
               MOVE K820-STATUS TO RETURN-CODE                                  
               PERFORM 9999-WRAP-UP        THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO  WS-CURRENT-DATE1.                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2000-PROCESS-INDATA.                                                     
      *=============================================================*           
                                                                                
            READ P04K010                                                        
                 AT END MOVE 'Y'  TO WS-K010-EOF                                
            END-READ.                                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2500-MAIN-PROCESS.                                                       
      ******************************************************************        
                                                                                
           PERFORM 4000-PROCESS-REPORT  THRU  4000-EXIT                         
                                                                                
           PERFORM 2000-PROCESS-INDATA THRU 2000-EXIT.                          
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-READ-P04K010.                                                       
      ******************************************************************        
                                                                                
           READ P04K010                                                         
                AT END MOVE 'Y' TO WS-K010-END.                                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-READ-BP13K800.                                                      
      ******************************************************************        
                                                                                
           MOVE  SPACES              TO     BP13K800-MASTER.                    
           INITIALIZE                       BP13K800-MASTER.                    
           MOVE  K820-NUM-REGN       TO     K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
           IF K800-STATUS = 00 OR 02                                            
             CONTINUE                                                           
           ELSE                                                                 
             IF K800-STATUS = 23                                                
                PERFORM 3110-READ-BP13K893 THRU 3110-EXIT                       
             ELSE                                                               
                DISPLAY 'BP13K800-READ ERROR. STATUS: '                         
                       K800-STATUS                                              
                MOVE   K800-STATUS   TO   RETURN-CODE                           
                PERFORM 9999-WRAP-UP        THRU 9999-EXIT                      
             END-IF                                                             
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3110-READ-BP13K893.                                                      
      ******************************************************************        
      *------------------------------------------------------------*            
      *    READ APPLICATION MASTER TO GET ACTUAL LEASE STAMP FEE   *            
      *------------------------------------------------------------*            
           MOVE SPACES                           TO BP13K893-MASTER.            
           INITIALIZE                               BP13K893-MASTER.            
                                                                                
           MOVE  K820-NUM-REGN        TO    K893-NUM-REGN-HIST.                 
                                                                                
           MOVE 'N'                              TO WS-K893-EOF                 
                                                    WS-K893-FND                 
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           EVALUATE BP13K893-STATUS                                             
              WHEN 00                                                           
                 PERFORM 3120-READNEXT-BP13K893 THRU 3120-EXIT                  
                   UNTIL K893-NUM-REGN-HIST NOT = K820-NUM-REGN                 
                      OR WS-K893-EOF = 'Y'                                      
              WHEN 10                                                           
              WHEN 23                                                           
                 DISPLAY 'K893 RECORD NOT FOUND. REGN NO = '                    
                          K820-NUM-REGN                                         
              WHEN OTHER                                                        
                 DISPLAY 'ERROR START BP13K893   ' BP13K893-STATUS              
                 MOVE     BP13K893-STATUS    TO   RETURN-CODE                   
                 PERFORM 9999-WRAP-UP        THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       3120-READNEXT-BP13K893.                                                  
      *-------------------------------------------------------------*           
                                                                                
           READ BP13K893 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                         TO WS-K893-EOF                 
           END-READ.                                                            
                                                                                
           EVALUATE BP13K893-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                 IF K893-NUM-REGN-HIST = K820-NUM-REGN                          
                    MOVE BP13K893-MASTER(1:2000) TO  BP13K800-MASTER            
                    MOVE 'Y'            TO  WS-K893-EOF                         
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
                 DISPLAY 'K893 RECORD NOT FOUND. REGN = ' K820-NUM-REGN         
                 MOVE 'Y'            TO  WS-K893-EOF                            
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K893 ' BP13K893-STATUS              
                 MOVE     BP13K893-STATUS    TO   RETURN-CODE                   
                 PERFORM 9999-WRAP-UP        THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3120-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3200-READ-BP27K025.                                                      
      ******************************************************************        
           MOVE SPACES                           TO BP27K025-REC.               
           INITIALIZE                               BP27K025-REC.               
                                                                                
           MOVE  K800-NUM-HLA         TO    K025-NUM-HLA                        
                                                                                
           MOVE 'N'                              TO WS-K025-EOF                 
                                                    WS-K025-FND                 
                                                                                
           START BP27K025 KEY >= K025-NUM-HLA                                   
                                                                                
           EVALUATE BP27K025-STATUS                                             
              WHEN 00                                                           
                 PERFORM 3210-READNEXT-BP27K025 THRU 3210-EXIT                  
                   UNTIL K025-NUM-HLA  NOT = K800-NUM-HLA                       
                      OR WS-K025-EOF = 'Y'                                      
              WHEN 10                                                           
              WHEN 23                                                           
                 DISPLAY 'K025 RECORD NOT FOUND. HLE NO = '                     
                          K800-NUM-HLA                                          
              WHEN OTHER                                                        
                 DISPLAY 'ERROR START BP27K025   ' BP27K025-STATUS              
                 MOVE     BP27K025-STATUS    TO   RETURN-CODE                   
                 PERFORM 9999-WRAP-UP        THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       3210-READNEXT-BP27K025.                                                  
      *-------------------------------------------------------------*           
                                                                                
           READ BP27K025 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                         TO WS-K025-EOF                 
           END-READ.                                                            
                                                                                
           EVALUATE BP27K025-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                 IF K025-NUM-HLA = K800-NUM-HLA AND                             
                    K025-NUM-OIC-TYPE = 'CA'                                    
                    MOVE K025-NUM-OIC   TO  FOUT-OIC-ID                         
                    MOVE 'Y'            TO  WS-K025-EOF                         
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
                 DISPLAY 'K025 RECORD NOT FOUND. REGN = ' K820-NUM-REGN         
                 MOVE 'Y'            TO  WS-K025-EOF                            
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP27K025 ' BP27K025-STATUS              
                 MOVE     BP27K025-STATUS    TO   RETURN-CODE                   
                 PERFORM 9999-WRAP-UP        THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3210-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3300-MOVE-DATA.                                                          
      ******************************************************************        
                                                                                
           INITIALIZE FOUT-DETAIL1.                                             
                                                                                
           IF WS-TEMP-WRITE = 'Y'                                               
              IF WS-DUP-FOUND = 'Y'                                             
                 MOVE  ZEROES TO FOUT-SNO                                       
              ELSE                                                              
                 MOVE  WS-SNO TO  FOUT-SNO                                      
              END-IF                                                            
              ADD 1         TO WS-CNT-FOUT-WRITE                                
              MOVE K800-NUM-REGN        TO FOUT-NUM-REGN                        
              MOVE K800-NUM-HLA         TO FOUT-HLE-NO                          
              IF K800-NUM-HLA NOT = SPACES AND LOW-VALUES                       
                 PERFORM 3200-READ-BP27K025  THRU 3200-EXIT                     
              END-IF                                                            
              MOVE K010-NUM-UINFIN(1:9) TO FOUT-NRIC-NO                         
              MOVE K010-REASON-CODE     TO FOUT-DEB-REASON                      
              MOVE WS-TEMP-REFNO  TO FOUT-DEB-REF-NO                            
              MOVE  WS-TEMP-EFF-DTE  TO FOUT-EFF-DTE                            
                                                                                
              IF K800-NUM-SCH-ACC = SPACES OR LOW-VALUES                        
                 MOVE K010-EX-SCH-ACCTNO TO FOUT-SCH-ACCNT                      
              ELSE                                                              
                 STRING K800-NUM-SCH-ACC(1:4) '-'                               
                        K800-NUM-SCH-ACC(5:4) '-'                               
                        K800-NUM-SCH-ACC(9:1) '-'                               
                        K800-NUM-SCH-ACC(10:2)                                  
                        DELIMITED BY SIZE                                       
                        INTO FOUT-SCH-ACCNT                                     
                 END-STRING                                                     
              END-IF                                                            
              WRITE BP13FOUT-REC FROM FOUT-DETAIL1                              
              MOVE  'N' TO  WS-TEMP-WRITE                                       
           END-IF.                                                              
                                                                                
           MOVE  ZEROES TO FOUT-SNO.                                            
           ADD 1         TO WS-CNT-FOUT-WRITE                                   
           MOVE K800-NUM-REGN        TO FOUT-NUM-REGN                           
           MOVE K800-NUM-HLA         TO FOUT-HLE-NO                             
           IF K800-NUM-HLA NOT = SPACES AND LOW-VALUES                          
              PERFORM 3200-READ-BP27K025  THRU 3200-EXIT                        
           END-IF                                                               
           MOVE K010-NUM-UINFIN(1:9) TO FOUT-NRIC-NO                            
           MOVE K010-REASON-CODE     TO FOUT-DEB-REASON                         
           STRING 'CO/' K010-DEBREF-ID '/' K010-DEBREF-YR                       
                  DELIMITED BY SIZE                                             
                  INTO FOUT-DEB-REF-NO                                          
           END-STRING                                                           
           STRING K010-EFF-DATE(7:2) '/'                                        
                  K010-EFF-DATE(5:2) '/'                                        
                  K010-EFF-DATE(1:4)                                            
                  DELIMITED BY SIZE                                             
                  INTO FOUT-EFF-DTE                                             
           END-STRING                                                           
           IF K800-NUM-SCH-ACC = SPACES OR LOW-VALUES                           
              MOVE K010-EX-SCH-ACCTNO TO FOUT-SCH-ACCNT                         
           ELSE                                                                 
              STRING K800-NUM-SCH-ACC(1:4) '-'                                  
                     K800-NUM-SCH-ACC(5:4) '-'                                  
                     K800-NUM-SCH-ACC(9:1) '-'                                  
                     K800-NUM-SCH-ACC(10:2)                                     
                     DELIMITED BY SIZE                                          
                     INTO FOUT-SCH-ACCNT                                        
              END-STRING                                                        
           END-IF                                                               
                                                                                
           WRITE BP13FOUT-REC FROM FOUT-DETAIL1.                                
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3400-READ-BP13K820.                                                      
      ******************************************************************        
                                                                                
           MOVE  SPACES               TO    BP13K820-REC.                       
           INITIALIZE                       BP13K820-REC.                       
           MOVE K010-NUM-UINFIN(1:9)  TO    K820-NUM-NRIC                       
                                                                                
           READ BP13K820 KEY IS K820-NUM-NRIC                                   
           IF K820-STATUS = 00 OR 02 OR 15                                      
             PERFORM 3100-READ-BP13K800       THRU 3100-EXIT                    
           ELSE                                                                 
             IF K820-STATUS = 23                                                
                PERFORM 3410-READ-BP13K895 THRU 3410-EXIT                       
             ELSE                                                               
                DISPLAY 'BP13K820-READ ERROR. STATUS: '                         
                   K820-STATUS                                                  
                MOVE  K820-STATUS       TO   RETURN-CODE                        
                PERFORM 9999-WRAP-UP        THRU 9999-EXIT                      
             END-IF                                                             
           END-IF.                                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3410-READ-BP13K895.                                                      
      ******************************************************************        
                                                                                
           MOVE  SPACES               TO    BP13K895-REC.                       
           INITIALIZE                       BP13K895-REC.                       
           MOVE K010-NUM-UINFIN(1:9)  TO    K895-NUM-NRIC-HIST                  
                                                                                
           READ BP13K895 KEY IS K895-NUM-NRIC-HIST                              
           IF BP13K895-STATUS = 00 OR 02 OR 15                                  
             MOVE BP13K895-REC(1:400) TO BP13K820-REC                           
             PERFORM 3100-READ-BP13K800       THRU 3100-EXIT                    
           ELSE                                                                 
             IF BP13K895-STATUS = 23                                            
                DISPLAY 'BP13K895-RECORD NOT FOUND: ' K895-NUM-NRIC-HIST        
             ELSE                                                               
                DISPLAY 'BP13K895-READ ERROR. STATUS: '                         
                   BP13K895-STATUS                                              
                MOVE  BP13K895-STATUS   TO   RETURN-CODE                        
                PERFORM 9999-WRAP-UP        THRU 9999-EXIT                      
             END-IF                                                             
           END-IF.                                                              
                                                                                
       3410-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3500-WRITE-HEADER.                                                       
      ******************************************************************        
                                                                                
            MOVE SPACES  TO BP13FOUT-REC.                                       
            MOVE 'REPORT ON DUPLICATE DEARMENT RECORDS '                        
                                                  TO FOUT-TITLE                 
            STRING WS-DATE1-DD   '/' WS-DATE1-MM '/' WS-DATE1-CCYY              
                   DELIMITED BY SIZE  INTO  FOUT-DATE.                          
                                                                                
            WRITE BP13FOUT-REC FROM FOUT-HEADER.                                
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3600-NRIC-ADDRESS.                                                       
      ******************************************************************        
           MOVE K010-NAME             TO FOUT-APPLN-NME                         
                                                                                
           WRITE BP13FOUT-REC FROM FOUT-DETAIL2.                                
                                                                                
           STRING 'BLK '                                                        
                  K010-BLOCK DELIMITED BY SIZE                                  
                  ' '                                                           
                  K010-STREETNAME DELIMITED BY '  '                             
                  ' '                                                           
                  K010-HOUSE-NO DELIMITED BY SIZE                               
           INTO FOUT-ADDRESS                                                    
                                                                                
           WRITE BP13FOUT-REC FROM FOUT-DETAIL3                                 
           MOVE SPACES TO BP13FOUT-REC                                          
           WRITE BP13FOUT-REC.                                                  
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       4000-PROCESS-REPORT.                                                     
      *================================================================*        
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
                                                                                
      *    ADD   1   TO   WS-K010RPT-COUNT.                                     
                                                                                
      *    IF WS-K010RPT-COUNT > 1                                              
      *       MOVE '.'     TO   BP13FOUT-REC                                    
      *       WRITE BP13FOUT-REC                                                
      *    END-IF.                                                              
                                                                                
      *    PERFORM 4600-CREATE-EMAIL    THRU  4600-EXIT.                        
           PERFORM 4500-CREATE-BODY THRU 4500-EXIT.                             
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4500-CREATE-BODY.                                                        
      *================================================================*        
                                                                                
           IF K010-NUM-UINFIN(1:9) = WS-TEMP-UIN    AND                         
              K010-REASON-CODE     = WS-TEMP-REASON AND                         
              K010-EX-SCH-ACCTNO   = WS-TEMP-SCHEME                             
              IF WS-ONE-TIME-SW = SPACES OR LOW-VALUES                          
                 PERFORM 4600-CREATE-EMAIL    THRU  4600-EXIT                   
                 PERFORM 3500-WRITE-HEADER THRU 3500-EXIT                       
                 MOVE SPACES TO BP13FOUT-REC                                    
                 WRITE BP13FOUT-REC                                             
                 WRITE BP13FOUT-REC FROM FOUT-HEADER3                           
                 MOVE 'Y' TO WS-ONE-TIME-SW                                     
              END-IF                                                            
              PERFORM 3400-READ-BP13K820       THRU 3400-EXIT                   
              PERFORM 3300-MOVE-DATA           THRU 3300-EXIT                   
              MOVE 'Y' TO WS-DUP-FOUND                                          
           ELSE                                                                 
              IF WS-DUP-FOUND = 'Y'                                             
                 PERFORM 3600-NRIC-ADDRESS THRU 3600-EXIT                       
                 MOVE  'N' TO  WS-DUP-FOUND                                     
                 MOVE  'Y' TO  WS-TEMP-WRITE                                    
                 ADD 1 TO WS-SNO                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE K010-NUM-UINFIN(1:9) TO WS-TEMP-UIN                             
           MOVE K010-REASON-CODE     TO WS-TEMP-REASON                          
           MOVE K010-EX-SCH-ACCTNO   TO WS-TEMP-SCHEME                          
                                                                                
           STRING 'CO/' K010-DEBREF-ID '/' K010-DEBREF-YR                       
                  DELIMITED BY SIZE                                             
                  INTO WS-TEMP-REFNO                                            
           END-STRING                                                           
           STRING K010-EFF-DATE(7:2) '/'                                        
                  K010-EFF-DATE(5:2) '/'                                        
                  K010-EFF-DATE(1:4)                                            
                  DELIMITED BY SIZE                                             
                  INTO WS-TEMP-EFF-DTE                                          
           END-STRING.                                                          
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4600-CREATE-EMAIL.                                                       
      *================================================================*        
                                                                                
            WRITE BP13FOUT-REC FROM WS-MAIL-RPT1                                
            WRITE BP13FOUT-REC FROM WS-MAIL-RPT2                                
            WRITE BP13FOUT-REC FROM WS-MAIL-RPT3                                
            WRITE BP13FOUT-REC FROM WS-MAIL-RPT4                                
            WRITE BP13FOUT-REC FROM WS-MAIL-RPT4A                               
            WRITE BP13FOUT-REC FROM WS-MAIL-RPT4B                               
            WRITE BP13FOUT-REC FROM WS-MAIL-RPT9                                
            WRITE BP13FOUT-REC FROM WS-MAIL-RPT10                               
            WRITE BP13FOUT-REC FROM WS-MAIL-RPT11                               
            WRITE BP13FOUT-REC FROM WS-MAIL-RPT12                               
            WRITE BP13FOUT-REC FROM WS-MAIL-RPT12A                              
            WRITE BP13FOUT-REC FROM WS-MAIL-RPT12B                              
                                                                                
            MOVE   WS-MAIL-RPT17  TO BP13FOUT-REC                               
            WRITE BP13FOUT-REC                                                  
                                                                                
            STRING WS-CUR-DD '/' WS-CUR-MM '/' WS-CUR-CCYY                      
                   DELIMITED BY SIZE INTO WS-DATE-RPT                           
            WRITE BP13FOUT-REC FROM WS-DATE-RPT-DTL                             
            MOVE SPACES TO  BP13FOUT-REC                                        
            WRITE   BP13FOUT-REC.                                               
                                                                                
       4600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       9999-WRAP-UP.                                                            
      *================================================================*        
                                                                                
           DISPLAY  SPACES.                                                     
           DISPLAY '******* CONTROL   COUNTS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13C10E'                                      
           DISPLAY  SPACES.                                                     
           DISPLAY 'NO. OF BP13FOUT RECS WRITTEN : ' WS-CNT-FOUT-WRITE.         
                                                                                
           CLOSE    BP13K800                                                    
                    BP13K893                                                    
                    BP13K895                                                    
                    BP27K025                                                    
                    BP13FOUT                                                    
                    BP13K820                                                    
                    P04K010.                                                    
                                                                                
           IF  K800-STATUS NOT = '00'                                           
               DISPLAY 'ERROR CLOSE BP13K800 : ' K800-STATUS                    
               MOVE K800-STATUS TO RETURN-CODE                                  
           END-IF.                                                              
                                                                                
           IF  BP13K893-STATUS NOT = '00'                                       
               DISPLAY 'ERROR CLOSE BP13K893 : ' BP13K893-STATUS                
               MOVE BP13K893-STATUS TO RETURN-CODE                              
           END-IF.                                                              
                                                                                
           IF  BP13K895-STATUS NOT = '00'                                       
               DISPLAY 'ERROR CLOSE BP13K895 : ' BP13K895-STATUS                
               MOVE BP13K895-STATUS TO RETURN-CODE                              
           END-IF.                                                              
                                                                                
           IF  BP27K025-STATUS NOT = '00'                                       
               DISPLAY 'ERROR CLOSE BP27K025 : ' BP27K025-STATUS                
               MOVE BP27K025-STATUS TO RETURN-CODE                              
           END-IF.                                                              
                                                                                
           IF  K820-STATUS NOT = '00'                                           
               DISPLAY 'ERROR CLOSE BP13K820 : ' K820-STATUS                    
               MOVE K820-STATUS TO RETURN-CODE                                  
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
