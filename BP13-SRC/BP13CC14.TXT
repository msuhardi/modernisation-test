                                                                                
       IDENTIFICATION DIVISION.                                                 
                                                                                
       PROGRAM-ID.    BP13CC14.                                                 
       AUTHOR.        B.BALA AMUTHAN.                                           
       DATE-WRITTEN.  05 MAY 2006.                                              
                                                                                
      ******************************************************************        
      *=================================================================        
      *  OBJECTIVE  : THIS IS SUB-ROUTINE FOR COLLECTION SYSTEM TO              
      *               VALIDATE THE CASE FOUND IN BP13KD00 (PROGRESS             
      *               PAYMENT FILE) MEANT FOR DBSS FLAT.                        
      *               PROGRAM WILL RETURN LOAN INFORMATION TO BE COLLECT        
      *               BEFORE KEY ISSUED.                                        
      *=================================================================        
      *-----------------------------------------------------------------        
      * REF NO   DATE     BY  AMENDMENTS/ENHANCEMENTS                           
      * -------- -------- --- ------------------------------------------        
      * BP132857 20060505 BA2 NEW SUB-ROUTINE                                   
      * BP133091 20071017 RB12 EXPAND KD00 LENGTH FROM 1200 TO 1600             
      * BP134744 20130923 IMC1 CATER FOR 'X' CASES WITH LOAN BALANCE            
      ******************************************************************        
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13KD00 ASSIGN       TO BP13KD00                             
                           ACCESS       IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KD00-KEY-FLD                         
                           ALTERNATE RECORD KEY IS KD00-ALT-KEY1                
                           FILE STATUS  IS WS-BP13KD00-STATUS.                  
                                                                                
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ACCESS       IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS WS-BP13K800-STATUS.                  
                                                                                
           SELECT BP13K820 ASSIGN       TO BP13K820                             
                           ACCESS       IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K820-KEY-FLD                         
                           FILE STATUS  IS WS-BP13K820-STATUS.                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13KD00                     RECORD CONTAINS 1600 CHARACTERS.        
                                                                                
       COPY BP13KD00.                                                           
                                                                                
       FD  BP13K800                     RECORD CONTAINS 2000 CHARACTERS.        
                                                                                
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                     RECORD CONTAINS 400  CHARACTERS.        
                                                                                
       COPY BP13K820.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 WS-BP13KD00-STATUS        PIC 9(02).                              
           05 WS-BP13K800-STATUS        PIC 9(02).                              
           05 WS-BP13K820-STATUS        PIC 9(02).                              
           05 WS-BP13KD00-FOUND         PIC X(01).                              
                                                                                
      *****************************************************                     
      *  COMMAREA COPY BOOK.                              *                     
      *****************************************************                     
       COPY P13COM60.                                                           
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
       01  LINK-CC14-REC                      PIC X(200).                       
                                                                                
       PROCEDURE DIVISION USING LINK-CC14-REC.                                  
      *------------------------------------------------------------*            
      *  COM60-ERR-CDE       = '0000' - NO ERROR                   *            
      *                      = '0001' - SCH-ACC NOT FOUND BP13KD00 *            
      *                      = '0002' - ACC-STAT NOT EQUAL TO C    *            
      *                      = '0003' - RECORD NOT FOUND BP13K800  *            
      *                      = '0004' - RECORD NOT FOUND BP13K820  *            
      *                      = '0090' - BP13KD00 NOT OPEN          *            
      *                      = '0091' - BP13K800 NOT OPEN          *            
      *                      = '0092' - BP13K820 NOT OPEN          *            
      *------------------------------------------------------------*            
                                                                                
      *-----------------------------------------------------------------        
       0000-CONTROL.                                                            
      *-----------------------------------------------------------------        
           PERFORM 1000-INIT-PROCESS       THRU 1000-EXIT.                      
           PERFORM 2000-MAIN-PROCESS       THRU 2000-EXIT.                      
           PERFORM 9000-GOBACK             THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------------        
       1000-INIT-PROCESS.                                                       
      *-----------------------------------------------------------------        
                                                                                
           MOVE SPACES        TO COM60-AREA.                                    
           INITIALIZE            COM60-AREA.                                    
           MOVE LINK-CC14-REC TO COM60-AREA.                                    
                                                                                
           OPEN INPUT BP13KD00                                                  
                      BP13K800                                                  
                      BP13K820.                                                 
                                                                                
           IF WS-BP13KD00-STATUS NOT = 0 AND 97                                 
              DISPLAY 'BP13CC14 OPEN ERROR ON BP13KD00 : '                      
                      WS-BP13KD00-STATUS                                        
              MOVE '0090'             TO COM60-ERR-CDE                          
              PERFORM 9000-GOBACK            THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-BP13K800-STATUS NOT = 0 AND 97                                 
              DISPLAY 'BP13CC14 OPEN ERROR ON BP13K800 : '                      
                      WS-BP13K800-STATUS                                        
              MOVE '0091'             TO COM60-ERR-CDE                          
              PERFORM 9000-GOBACK            THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-BP13K820-STATUS NOT = 0 AND 97                                 
              DISPLAY 'BP13CC14 OPEN ERROR ON BP13K820 : '                      
                      WS-BP13K820-STATUS                                        
              MOVE '0092'             TO COM60-ERR-CDE                          
              PERFORM 9000-GOBACK            THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           MOVE '0000'        TO COM60-ERR-CDE.                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------------        
       2000-MAIN-PROCESS.                                                       
      *-----------------------------------------------------------------        
           MOVE    SPACES                  TO   BP13KD00-REC                    
                                                BP13K800-MASTER                 
                                                BP13K820-REC.                   
                                                                                
           MOVE    'N'                     TO   COM60-CDE-FOUND.                
           MOVE    COM60-NUM-SCH-ACCNT     TO   KD00-NUM-SCH-ACCNT.             
           PERFORM 2100-READ-BP13KD00-AIX  THRU 2100-EXIT.                      
           PERFORM 2200-MOVE-VALUES        THRU 2200-EXIT.                      
                                                                                
           MOVE  KD00-NUM-REGN             TO   K800-NUM-REGN.                  
           PERFORM 2300-READ-BP13K800      THRU 2300-EXIT.                      
                                                                                
           MOVE  K800-NUM-REGN             TO   K820-NUM-REGN.                  
           MOVE  K800-NUM-NRIC1            TO   K820-NUM-NRIC.                  
           PERFORM 2400-READ-BP13K820      THRU 2400-EXIT.                      
                                                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       2100-READ-BP13KD00-AIX.                                                  
      *--------------------------------------------------------------*          
                                                                                
           READ BP13KD00 KEY IS KD00-ALT-KEY1                                   
                                                                                
           IF WS-BP13KD00-STATUS = 00 OR 02                                     
              IF KD00-CDE-ACCT-STAT = 'C' OR                                    
                (KD00-CDE-ACCT-STAT = 'X' AND                                   
                 KD00-AMT-LOAN-BAL  > ZEROES)                                   
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE '0002'                  TO COM60-ERR-CDE                  
                 MOVE 'ACCOUNT STATUS NOT EQUAL TO C.'                          
                                               TO COM60-ERR-MSG                 
                 PERFORM 9000-GOBACK            THRU 9000-EXIT                  
              END-IF                                                            
           ELSE                                                                 
              IF WS-BP13KD00-STATUS = 23                                        
                 MOVE '0001'                     TO COM60-ERR-CDE               
                 MOVE 'SCHEME ACCOUNT NOT FOUND' TO COM60-ERR-MSG               
                 PERFORM 9000-GOBACK            THRU 9000-EXIT                  
              ELSE                                                              
                 DISPLAY 'ERROR WITH READ FILE-BP13KD00 (BP13CC14) RC -'        
                          WS-BP13KD00-STATUS                                    
                 MOVE WS-BP13KD00-STATUS         TO COM60-ERR-CDE               
                 PERFORM 9000-GOBACK            THRU 9000-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       2200-MOVE-VALUES.                                                        
      *------------------------*                                                
           MOVE    'Y'                     TO COM60-CDE-FOUND.                  
           MOVE KD00-CDE-ACCT-STAT         TO COM60-CDE-ACCT-STAT.              
           MOVE KD00-AMT-LOAN-BAL          TO COM60-AMT-LOAN-BAL.               
           MOVE KD00-AMT-INSTAL            TO COM60-AMT-INSTAL.                 
           MOVE KD00-AMT-INSTAL-CASH       TO COM60-AMT-INSTAL-CASH.            
           MOVE KD00-AMT-INSTAL-CPF1       TO COM60-AMT-INSTAL-CPF1.            
           MOVE KD00-AMT-INSTAL-CPF2       TO COM60-AMT-INSTAL-CPF2.            
           MOVE KD00-AMT-INSTAL-CPF3       TO COM60-AMT-INSTAL-CPF3.            
           MOVE KD00-AMT-INSTAL-CPF4       TO COM60-AMT-INSTAL-CPF4.            
           MOVE KD00-AMT-ARR-INSTAL        TO COM60-AMT-ARR-INSTAL.             
           MOVE KD00-AMT-CPF-RATE          TO COM60-AMT-CPF-RATE.               
           MOVE KD00-CDE-CPF-STATUS        TO COM60-CDE-CPF-STATUS.             
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       2300-READ-BP13K800.                                                      
      *--------------------------------------------------------------*          
                                                                                
           READ BP13K800 KEY IS K800-NUM-REGN                                   
                                                                                
           IF WS-BP13K800-STATUS = 00                                           
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-BP13K800-STATUS = 23                                        
                 MOVE '0003'                     TO COM60-ERR-CDE               
                 MOVE 'RECORD NOT FOUND - BP13K800 FILE'                        
                                                 TO COM60-ERR-MSG               
                 PERFORM 9000-GOBACK            THRU 9000-EXIT                  
              ELSE                                                              
                 DISPLAY 'ERROR WITH READ FILE-BP13K800 (BP13CC14) RC -'        
                          WS-BP13K800-STATUS                                    
                 MOVE WS-BP13K800-STATUS         TO COM60-ERR-CDE               
                 PERFORM 9000-GOBACK            THRU 9000-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       2400-READ-BP13K820.                                                      
      *--------------------------------------------------------------*          
                                                                                
           READ BP13K820 KEY IS K820-KEY-FLD                                    
                                                                                
           IF WS-BP13K820-STATUS = 00                                           
              MOVE K820-NME-OCCP                 TO COM60-NME-APPL1             
           ELSE                                                                 
              IF WS-BP13K820-STATUS = 23                                        
                 MOVE '0004'                     TO COM60-ERR-CDE               
                 MOVE 'RECORD NOT FOUND - BP13K820 FILE'                        
                                                 TO COM60-ERR-MSG               
                 PERFORM 9000-GOBACK            THRU 9000-EXIT                  
              ELSE                                                              
                 DISPLAY 'ERROR WITH READ FILE-BP13K820 (BP13CC14) RC -'        
                          WS-BP13K820-STATUS                                    
                 MOVE WS-BP13K820-STATUS         TO COM60-ERR-CDE               
                 PERFORM 9000-GOBACK            THRU 9000-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------------        
       9000-GOBACK.                                                             
      *-----------------------------------------------------------------        
                                                                                
           CLOSE BP13KD00                                                       
                 BP13K800                                                       
                 BP13K820.                                                      
                                                                                
           IF WS-BP13KD00-STATUS NOT = 0                                        
              DISPLAY 'BP13CC14 CLOSE ERROR ON BP13KD00 : '                     
                      WS-BP13KD00-STATUS                                        
              MOVE WS-BP13KD00-STATUS TO COM60-ERR-CDE                          
           END-IF.                                                              
                                                                                
           IF WS-BP13K800-STATUS NOT = 0                                        
              DISPLAY 'BP13CC14 CLOSE ERROR ON BP13K800 : '                     
                      WS-BP13K800-STATUS                                        
              MOVE WS-BP13K800-STATUS TO COM60-ERR-CDE                          
           END-IF.                                                              
                                                                                
           IF WS-BP13K820-STATUS NOT = 0                                        
              DISPLAY 'BP13CC14 CLOSE ERROR ON BP13K820 : '                     
                      WS-BP13K820-STATUS                                        
              MOVE WS-BP13K820-STATUS TO COM60-ERR-CDE                          
           END-IF.                                                              
                                                                                
           MOVE COM60-AREA TO LINK-CC14-REC.                                    
           GOBACK.                                                              
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------- END BP13CC14 SUB-ROUTINE --------------------        
                                                                                
