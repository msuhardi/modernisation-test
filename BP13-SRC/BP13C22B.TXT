       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C22B.                                                 
       AUTHOR.        SENTHILVEL.                                               
       DATE-WRITTEN.  09/03/2004.                                               
      * ========================================================== *            
      *                    BP13                                    *            
      * ========================================================== *            
      * OBJECTIVE  : TO EXTRACT THE CASES WHERE THE FLAT TYPE EQUAL*            
      *              TO RP, ST OR CV.                              *            
      * ---------------------------------------------------------- *            
      * CHG REF   DATE     BY    DESCRIPTIONS                      *            
      * --------- ----     --    ------------                      *            
      * BP132486  09032004 PGS1  NEW PROGRAM                       *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
            SELECT BP13F310 ASSIGN TO BP13F310.                                 
                                                                                
            SELECT BM06K110 ASSIGN TO BM06K110                                  
                   ACCESS MODE     IS RANDOM                                    
                   ORGANIZATION    IS INDEXED                                   
                   RECORD KEY      IS K110-SCH-ACC-NO                           
                   FILE STATUS     IS BM06K110-STATUS.                          
                                                                                
            SELECT BP13K800 ASSIGN TO BP13K800                                  
                   ACCESS MODE     IS RANDOM                                    
                   ORGANIZATION    IS INDEXED                                   
                   RECORD KEY      IS K800-NUM-REGN                             
                   FILE STATUS     IS BP13K800-STATUS.                          
                                                                                
            SELECT BM06F120 ASSIGN TO BM06F120.                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F310                                                            
            RECORD CONTAINS 150 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       COPY BP13F310.                                                           
                                                                                
       FD   BM06K110.                                                           
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K800.                                                           
       COPY BP13K800.                                                           
                                                                                
       FD   BM06F120                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 700 CHARACTERS                                      
            RECORDING MODE IS F.                                                
                                                                                
       COPY BM06F120.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01 BM06K110-STATUS                    PIC 99     VALUE 00.               
       01 BP13K800-STATUS                    PIC 99     VALUE 00.               
       01 WS-BM06F120-WRITE                  PIC 9(8)   VALUE ZEROS.            
       01 WS-BM06K110-READ                   PIC 9(8)   VALUE ZEROS.            
       01 WS-BP13K800-READ                   PIC 9(8)   VALUE ZEROS.            
       01 WS-K110-FLT-UNMATCH                PIC 9(8)   VALUE ZEROS.            
       01 WS-K800-REC-UNMATCH                PIC 9(8)   VALUE ZEROS.            
       01 WS-BP13F310-READ                   PIC 9(8)   VALUE ZEROS.            
       01 WS-K110-NOTFND                     PIC 9(8)   VALUE ZEROS.            
       01 WS-K800-NOTFND                     PIC 9(8)   VALUE ZEROS.            
       01 WS-DATE                            PIC X(8)   VALUE SPACES.           
       01 WS-EDIT-DATE                       PIC X(10)  VALUE SPACES.           
       01 WS-RETCODE                         PIC -ZZZZ.                         
                                                                                
      *-------------------------------------------------------------            
      * BP13C913 PARAMETERS                                                     
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH-ACC.                                              
              10 WS-LINK-NUM-SCH       PIC X(4).                                
              10 WS-LINK-NUM-ACC       PIC X(5).                                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
            PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                        
            PERFORM 2000-READ-BP13F310   THRU 2000-EXIT.                        
            PERFORM 3000-PROCESS-DETAIL  THRU 3000-EXIT                         
                    UNTIL F310-SCH-ACC = HIGH-VALUES.                           
            PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                        
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
            OPEN  INPUT  BP13F310                                               
                         BM06K110                                               
                         BP13K800                                               
                  OUTPUT BM06F120.                                              
                                                                                
            IF BM06K110-STATUS NOT = 00 AND 97                                  
               DISPLAY 'OPENING ERROR K110-STATUS IS ' BM06K110-STATUS          
               MOVE BM06K110-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                        
            END-IF.                                                             
                                                                                
            IF BP13K800-STATUS NOT = 00 AND 97                                  
               DISPLAY 'OPENING ERROR K800-STATUS IS ' BP13K800-STATUS          
               MOVE BP13K800-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                        
            END-IF.                                                             
                                                                                
            MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DATE.                         
            STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)               
                   DELIMITED BY SIZE INTO WS-EDIT-DATE.                         
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       2000-READ-BP13F310.                                                      
      ******************************************************************        
            READ BP13F310 AT END MOVE HIGH-VALUES TO F310-SCH-ACC               
                 GO TO 2000-EXIT.                                               
            ADD 1   TO WS-BP13F310-READ.                                        
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3000-PROCESS-DETAIL.                                                     
      ******************************************************************        
            PERFORM 3100-READ-BP13K800 THRU 3100-EXIT.                          
            PERFORM 2000-READ-BP13F310    THRU 2000-EXIT.                       
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3100-READ-BP13K800.                                                      
      ******************************************************************        
            MOVE SPACES        TO K800-NUM-REGN.                                
            MOVE F310-REGN-NO  TO K800-NUM-REGN.                                
            READ BP13K800.                                                      
            ADD  1 TO WS-BP13K800-READ.                                         
            EVALUATE BP13K800-STATUS                                            
               WHEN 00                                                          
                    IF K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES AND         
                                              ZEROES                            
                       PERFORM 3200-READ-BM06K110 THRU 3200-EXIT                
                    ELSE                                                        
                       ADD  1 TO WS-K800-REC-UNMATCH                            
                    END-IF                                                      
               WHEN 23                                                          
                    ADD  1 TO WS-K800-NOTFND                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR : ' BP13K800-STATUS            
                    DISPLAY 'BP13K800 REGN       : ' K800-NUM-REGN              
                    MOVE BP13K800-STATUS         TO   RETURN-CODE               
                    PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                 
            END-EVALUATE.                                                       
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3200-READ-BM06K110.                                                      
      ******************************************************************        
            MOVE SPACES        TO K110-SCH-ACC-NO.                              
            MOVE F310-SCH-ACC  TO K110-SCH-ACC-NO.                              
            READ BM06K110                                                       
            ADD  1 TO WS-BM06K110-READ.                                         
            EVALUATE BM06K110-STATUS                                            
               WHEN 00                                                          
                    IF (K110-FLAT-STATUS = 'RP' OR 'ST' OR 'CV') AND            
                       (K110-REGN-NO NOT = SPACES AND                           
                        K110-REGN-NO (1:2) NOT = '##')                          
                       PERFORM 3300-CREATE-F120 THRU 3300-EXIT                  
                    ELSE                                                        
                       ADD  1 TO WS-K110-FLT-UNMATCH                            
                    END-IF                                                      
               WHEN 23                                                          
                    ADD  1 TO WS-K110-NOTFND                                    
               WHEN OTHER                                                       
                    DISPLAY 'BM06K110 READ ERROR : ' BM06K110-STATUS            
                    DISPLAY 'BM06K110 KEY-FLD    : ' K110-SCH-ACC-NO            
                    MOVE BM06K110-STATUS         TO   RETURN-CODE               
                    PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                 
            END-EVALUATE.                                                       
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3300-CREATE-F120.                                                        
      ******************************************************************        
                                                                                
      **-- READ PIDB                                                            
            MOVE SPACES           TO BP13COMM8-REC.                             
            INITIALIZE               BP13COMM8-REC.                             
            MOVE K110-SCH-ACC-NO  TO WS-LINK-REC.                               
            PERFORM 3400-READ-PIDB           THRU 3400-EXIT.                    
                                                                                
            MOVE SPACES             TO F120-REC.                                
            INITIALIZE                 F120-REC.                                
            MOVE K110-REC           TO F120-REC(1:500).                         
                                                                                
            MOVE COMM8-NUM-MGMT(1:2)    TO F120-BO-CODE.                        
            MOVE COMM8-NME-STREET       TO F120-STREET-NAME.                    
            MOVE COMM8-NUM-LEVEL        TO F120-LEVEL-NO.                       
            MOVE COMM8-NUM-UNIT-MAIN    TO F120-MAIN-UNIT.                      
            MOVE COMM8-NUM-UNIT-SUB     TO F120-SUB-UNIT.                       
            MOVE COMM8-NUM-POSTAL-CODE  TO F120-POSTAL-CODE.                    
            MOVE COMM8-CDE-TYP-ACTLUSE  TO F120-CDE-TYP-ACTUSE.                 
                                                                                
            WRITE F120-REC.                                                     
            ADD 1 TO WS-BM06F120-WRITE.                                         
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       3400-READ-PIDB.                                                          
      *****************************************************************         
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
              WHEN +000                                                         
                 CONTINUE                                                       
              WHEN +100                                                         
                 MOVE SPACES   TO BP13COMM8-REC                                 
                 INITIALIZE       BP13COMM8-REC                                 
                 DISPLAY 'REC NOT FOUND IN PIDB: ' K110-SCH-ACC-NO              
              WHEN OTHER                                                        
                 MOVE COMM8-CDE-SYSERR      TO WS-RETCODE                       
                 DISPLAY 'ERROR IN CALLING BP13C913, RETCODE: '                 
                         WS-RETCODE                                             
                 MOVE 99                    TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-ROUTINE.                                                      
      ******************************************************************        
            DISPLAY ' '.                                                        
            DISPLAY '-----------------------------------'.                      
            DISPLAY ' CONTROL LIST : BP13C22B '.                                
            DISPLAY ' RUN DATE     : ' WS-EDIT-DATE.                            
            DISPLAY '-----------------------------------'.                      
            DISPLAY ' 1) TOT BP13F310 READ      : ' WS-BP13F310-READ.           
            DISPLAY ' 2) TOT BM06K110 READ      : ' WS-BM06K110-READ.           
            DISPLAY ' 3) TOT BM06K110 NOT FND   : ' WS-K110-NOTFND.             
            DISPLAY ' 4) TOT BM06K110 FLT UNMTH : ' WS-K110-FLT-UNMATCH.        
            DISPLAY ' 5) TOT BP13K800 READ      : ' WS-BP13K800-READ.           
            DISPLAY ' 6) TOT BP13K800 NOT FND   : ' WS-K800-NOTFND.             
            DISPLAY ' 7) TOT BP13K800 FLT UNMTH : ' WS-K800-REC-UNMATCH.        
            DISPLAY ' 8) TOT BM06F120 WRITE     : ' WS-BM06F120-WRITE.          
                                                                                
            CLOSE  BP13F310                                                     
                   BM06K110                                                     
                   BP13K800                                                     
                   BM06F120.                                                    
                                                                                
            IF BM06K110-STATUS NOT = 00 AND 97                                  
               DISPLAY 'CLOSING ERROR BM06K110 : ' BM06K110-STATUS              
               MOVE BM06K110-STATUS TO RETURN-CODE                              
            END-IF.                                                             
                                                                                
            IF BP13K800-STATUS NOT = 00 AND 97                                  
               DISPLAY 'CLOSING ERROR BP13K800 : ' BP13K800-STATUS              
               MOVE BP13K800-STATUS TO RETURN-CODE                              
            END-IF.                                                             
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
