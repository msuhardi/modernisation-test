       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH52.                                                 
       AUTHOR.        RJB1.                                                     
      *DATE-WRITTEN.  14/05/2015.                                               
      * ===============================================================*        
      * BP13 SYSTEM OF COMMITMENT                                      *        
      * ===============================================================*        
      * OBJECTIVE   : TO CREATE A REPORT FOR THOSE RUN DATE IS 1 MONTH *        
      *               FROM PCD (K100-DTE-PACD)                         *        
      *                                                                *        
      * CONDITIONS:                                                    *        
      *            - HA & OCCUPIER   = K820-NUM-FREEZE-OCCP IS 'Y'     *        
      *            - DEBARMENT CODES = 5A,5B,5C,5D,6A,6B,6C,6D,6E,     *        
      *                                07,7A,44,UM,UH,UI,UL,UD,UE,UF,UG*        
      *                                                                *        
      *   INPUT FILES :  1. BM06F100                                   *        
      *                  2. BM06K110                                   *        
      *                  3. BP13K800                                   *        
      *                  4. BP13K820                                   *        
      *                  5. BP13K022                                   *        
      *                  6. P04K010                                    *        
      *                                                                *        
      *   OUTPUT      :  1. BP13LH52                                   *        
      *                  2. P13LH52A                                   *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ------------------------------------*        
      *   BP135711  RJB1  14/05/15 NEW PROGRAM                         *        
      *   BP135711  RJB1  04/06/15 TO CALL BP13CCNT TO GET EMAIL ADDR  *        
      *   BP136082  KVL1  11/02/16 TO REPLACE TEEU_HUI@HDB.GOV.SG TO   *        
      *                            KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG *        
      *   BP136337  PCL4  04/07/16 TO ADD CHECK FOR PROP-TYPE = 1 AND  *        
      *                            UPDATE REPORT RUN TO 1 MTH FROM PCD *        
      *   BP136477  RJB1  18/10/16 TO REPLACE KIM HUAT TO YEE MIEN     *        
      *   BP136851  PCL4  20/06/17 TO UPDATE EMAIL RECIPIENTS          *        
      *   BP138757  KV51  28/07/21 TO REPLACE HONG HUAT TO BERNICE     *        
      *   BP139030  ZAR7  07/01/22 REPLACE CHUA YEE MIEN TO LIM KOK CHUN        
      *   BP139276  KAC1  05/04/24 REPLACING LIM KOK CHUN BY           *        
      *                                      KATHIJAH_BEE_ALI_MOHAMED  *        
      *   BP139175  KAC1  29/04/24 REPL Bernice_JH_GAN@hdb.gov.sg WITH *        
      *                            Iris_LL_KOR@hdb.gov.sg              *        
      *================================================================*        
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K800-NUM-REGN                   
                            FILE STATUS      IS WS-K800-STATUS.                 
                                                                                
           SELECT BP13K022  ASSIGN        TO BP13K022                           
                            ACCESS MODE      IS DYNAMIC                         
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K022-KEY-FLD                    
                            ALTERNATE KEY    IS K022-NUM-REGN                   
                            FILE STATUS      IS WS-K022-STATUS.                 
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE      IS DYNAMIC                         
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K110-KEY-FLD                    
                            ALTERNATE KEY    IS K110-AIX1                       
                                             WITH DUPLICATES                    
                            FILE STATUS      IS WS-K110-STATUS.                 
                                                                                
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ACCESS MODE      IS DYNAMIC                         
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K820-KEY-FLD                    
                            FILE STATUS      IS WS-K820-STATUS.                 
                                                                                
           SELECT P04K010   ASSIGN        TO P04K010                            
                            ACCESS MODE      IS DYNAMIC                         
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K010-PRIME-KEY                  
                            FILE STATUS      IS WS-K010-STATUS.                 
                                                                                
           SELECT BM06F100  ASSIGN        TO BM06F100.                          
           SELECT BP13LH52  ASSIGN        TO BP13LH52.                          
           SELECT P13LH52A  ASSIGN        TO P13LH52A.                          
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13LH52                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13LH52-REC        PIC X(132).                                      
                                                                                
       FD  P13LH52A                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  P13LH52A-REC        PIC X(132).                                      
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BM06F100                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06F100.                                                           
                                                                                
       FD  BP13K022                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K022.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  P04K010                                                              
           RECORD CONTAINS 300 CHARACTERS.                                      
       COPY P04K010.                                                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY             PIC 9(04).                               
           05  WS-CUR-MM               PIC 9(02).                               
           05  WS-CUR-DD               PIC 9(02).                               
                                                                                
       01  WS-REPORT-DATE.                                                      
           05  WS-REP-CCYY             PIC X(04).                               
           05  WS-REP-MM               PIC X(02).                               
                                                                                
       01  WS-COUNT.                                                            
           05  WS-F100-INPUT           PIC 9(08) VALUE 0.                       
           05  WS-LH52-OUT             PIC 9(08) VALUE 0.                       
           05  WS-LH52A-OUT            PIC 9(08) VALUE 0.                       
           05  WS-F100-OUT             PIC 9(08) VALUE 0.                       
           05  WS-WRITTEN              PIC 9(08) VALUE 0.                       
                                                                                
       01  WS-TIME                     PIC 9(08) VALUE 0.                       
       01  WS-F100-EOF                 PIC X(01) VALUE SPACES.                  
       01  WS-K110-EOF                 PIC X(01) VALUE SPACES.                  
       01  WS-K820-EOF                 PIC X(01) VALUE SPACES.                  
       01  WS-K010-EOF                 PIC X(01) VALUE SPACES.                  
       01  WS-FOUND-K010               PIC X(01) VALUE 'N'.                     
       01  WS-FOUND-K800               PIC X(01) VALUE 'N'.                     
       01  WS-FOUND-K110               PIC X(01) VALUE 'N'.                     
       01  WS-FOUND-K022               PIC X(01) VALUE 'N'.                     
       01  WS-PROCESSED                PIC X(01) VALUE 'N'.                     
       01  WS-FILE-STATUS.                                                      
           05 WS-K800-STATUS           PIC 99    VALUE ZEROS.                   
           05 WS-K022-STATUS           PIC 99    VALUE ZEROS.                   
           05 WS-K110-STATUS           PIC 99    VALUE ZEROS.                   
           05 WS-K820-STATUS           PIC 99    VALUE ZEROS.                   
           05 WS-K010-STATUS           PIC 99    VALUE ZEROS.                   
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-OIC                   PIC X(5)  VALUE SPACES.                  
           05 WS-OIC-NME               PIC X(30) VALUE SPACES.                  
           05 WS-EMAIL                 PIC X(50) VALUE SPACES.                  
           05 WS-SAVE-KEY              PIC X(16) VALUE SPACES.                  
           05  WS-DTE-SYS-VALID.                                                
              10 WS-YY-SYS-VALID       PIC 9(4)  VALUE ZEROES.                  
              10 WS-MM-SYS-VALID       PIC 9(2)  VALUE ZEROES.                  
              10 WS-DD-SYS-VALID       PIC 9(2)  VALUE ZEROES.                  
           05 WS-DEBAR-CODE            PIC X(02).                               
              88 VALID-DEB-CODE                  VALUE '5A', '5B', '5C',        
                                       '5D', '6A', '6B', '6C', '6D',            
                                       '6E', '07', '7A', '44', 'UM',            
                                       'UM', 'UI', 'UL', 'UD', 'UE',            
                                       'UF', 'UG'.                              
                                                                                
       01  WS-DATE-TAB.                                                         
           05  XX-DATE.                                                         
               10  XXXX-YEAR           PIC 9(4).                                
                   88  VALID-YEAR                VALUE 1900 THRU 2200.          
               10  XX-MONTH            PIC 9(2).                                
                   88  31-DAY-MONTH              VALUE 01, 03, 05,              
                                                    07, 08, 10.                 
                   88  30-DAY-MONTH              VALUE 04, 06, 09, 11.          
                   88  28-29-DAY-MONTH           VALUE 02.                      
                   88  VALID-MONTH               VALUE 01 THRU 12.              
                   88  MONTH-DEC                 VALUE 12.                      
               10  XX-DAY              PIC 9(2).                                
                   88  VALID-DAY                 VALUE 01 THRU 31.              
                                                                                
                                                                                
      *-------------------------------------------------------------            
      * LAYOUT BP13MAIL  -  DATASET FOR EMAIL                                   
      *-------------------------------------------------------------            
       01  MAIL-HDR1.                                                           
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  MAIL-HDR2.                                                           
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  MAIL-SENDID     PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1A.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(60)  VALUE                                 
      *        'TEEU_HUI@HDB.GOV.SG>'.                                          
               'Iris_LL_KOR@hdb.gov.sg'.                                        
                                                                                
       01  MAIL-DTL1B.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(60)  VALUE                                 
               'KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                          
                                                                                
       01  MAIL-DTL3.                                                           
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4.                                                           
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  MAIL-SENDMAILID PIC X(26)  VALUE                                 
               'SOC System - Email Alert'.                                      
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5A.                                                          
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(60)  VALUE                                 
      *        'TEEU_HUI@HDB.GOV.SG>'.                                          
      *        'LAU_HONG_HUAT@HDB.GOV.SG>'.                                     
               'Iris_LL_KOR@hdb.gov.sg>'.                                       
                                                                                
       01  MAIL-DTL5B.                                                          
           05  FILLER          PIC X(4)   VALUE 'CC:<'.                         
           05  FILLER          PIC X(60)  VALUE                                 
               'KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                          
                                                                                
       01  MAIL-DTL7.                                                           
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  FILLER          PIC X(19)  VALUE 'Payment of Outstand'.          
           05  FILLER          PIC X(20)  VALUE 'ing Arrears (1 month'.         
           05  FILLER          PIC X(20)  VALUE ' from PCD)          '.         
                                                                                
       01  MAIL-DTL8.                                                           
           05 FILLER                   PIC X(6)   VALUE 'DATE: '.               
           05 MAIL-DATE                PIC X(10)  VALUE SPACES.                 
                                                                                
       01  MAIL-FOOTER.                                                         
           05 FILLER                   PIC X(50)  VALUE                         
              'Please follow up on these arrears cases.'.                       
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                   PIC X      VALUE '.'.                    
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(80)  VALUE SPACES.                 
                                                                                
       01  WS-PRINT-VARIABLES.                                                  
           05 LH52-HDG1.                                                        
              10 FILLER                   PIC X(55)   VALUE SPACES.             
              10 FILLER                   PIC X(22)   VALUE                     
                'SYSTEM  OF  COMMITMENT'.                                       
              10 FILLER                   PIC X(55)   VALUE SPACES.             
                                                                                
           05 LH52-HDG2.                                                        
              10 FILLER                   PIC X(44)   VALUE 'BP13LH52'.         
              10 FILLER                   PIC X(50)   VALUE                     
                'PAYMENT OF OUTSTANDING ARREARS (1 MONTH FROM PCD)'.            
              10 FILLER                   PIC X(12)   VALUE SPACES.             
              10 FILLER                   PIC X(07)   VALUE 'DATE : '.          
              10 LH52-SYS-DATE            PIC X(10).                            
                                                                                
           05 LH52-HDG3.                                                        
              10 FILLER                   PIC X(06)   VALUE                     
                'S/NO. '.                                                       
              10 FILLER                   PIC X(02)   VALUE SPACES.             
              10 FILLER                   PIC X(08)   VALUE                     
                'REGN NO.'.                                                     
              10 FILLER                   PIC X(02)   VALUE SPACES.             
              10 FILLER                   PIC X(35)   VALUE 'NAME'.             
              10 FILLER                   PIC X(02)   VALUE SPACES.             
              10 FILLER                   PIC X(09)   VALUE 'NRIC'.             
              10 FILLER                   PIC X(02)   VALUE SPACES.             
              10 FILLER                   PIC X(14)   VALUE                     
                 'DEBARMENT CODE'.                                              
              10 FILLER                   PIC X(02)   VALUE SPACES.             
              10 FILLER                   PIC X(12)   VALUE                     
                 'ARREARS AMT'.                                                 
              10 FILLER                   PIC X(02)   VALUE SPACES.             
              10 FILLER                   PIC X(08)   VALUE 'OIC'.              
              10 FILLER                   PIC X(02)   VALUE SPACES.             
              10 FILLER                   PIC X(24)   VALUE                     
                 'DEBARMENT EFFECTIVE DATE'.                                    
                                                                                
           05 LH52-HDG4.                                                        
              10 FILLER                   PIC X(06)   VALUE ALL '='.            
              10 FILLER                   PIC X(02)   VALUE SPACES.             
              10 FILLER                   PIC X(08)   VALUE ALL '='.            
              10 FILLER                   PIC X(02)   VALUE SPACES.             
              10 FILLER                   PIC X(35)   VALUE ALL '='.            
              10 FILLER                   PIC X(02)   VALUE SPACES.             
              10 FILLER                   PIC X(09)   VALUE ALL '='.            
              10 FILLER                   PIC X(02)   VALUE SPACES.             
              10 FILLER                   PIC X(14)   VALUE ALL '='.            
              10 FILLER                   PIC X(02)   VALUE SPACES.             
              10 FILLER                   PIC X(12)   VALUE ALL '='.            
              10 FILLER                   PIC X(02)   VALUE SPACES.             
              10 FILLER                   PIC X(08)   VALUE ALL '='.            
              10 FILLER                   PIC X(02)   VALUE SPACES.             
              10 FILLER                   PIC X(24)   VALUE ALL '='.            
                                                                                
           05 LH52-DTL1.                                                        
              10 LH52-SNO                 PIC ZZZZZ9  VALUE ZEROES.             
              10 FILLER                   PIC X(02)   VALUE SPACES.             
              10 LH52-REGN                PIC X(08)   VALUE SPACES.             
              10 FILLER                   PIC X(02)   VALUE SPACES.             
              10 LH52-NAME                PIC X(35)   VALUE SPACES.             
              10 FILLER                   PIC X(02)   VALUE SPACES.             
              10 LH52-NRIC                PIC X(09)   VALUE SPACES.             
              10 FILLER                   PIC X(02)   VALUE SPACES.             
              10 FILLER                   PIC X(06)   VALUE SPACES.             
              10 LH52-DEBAR               PIC X(02)   VALUE SPACES.             
              10 FILLER                   PIC X(06)   VALUE SPACES.             
              10 FILLER                   PIC X(02)   VALUE SPACES.             
              10 LH52-AMT                 PIC ZZZZZZ9.99 VALUE ZEROS.           
              10 FILLER                   PIC X(04)   VALUE SPACES.             
              10 LH52-OIC                 PIC X(05)   VALUE SPACES.             
              10 FILLER                   PIC X(03)   VALUE SPACES.             
              10 FILLER                   PIC X(02)   VALUE SPACES.             
              10 LH52-DTE-EFF             PIC X(08)   VALUE SPACES.             
                                                                                
      *----------------------------------------------------------------*        
      *        LINKAGE FOR SUB-PROGRAM BP13CCNT                        *        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  LINK-CNTCT-TYPE         PIC X(3).                                
           05  LINK-STAFF-NICK         PIC X(6).                                
           05  LINK-STAFF-NUM          PIC X(6).                                
           05  LINK-STAFF-CNTCT        PIC X(50).                               
           05  LINK-SYSERR             PIC X(4).                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================*           
       MAIN.                                                                    
      *=============================================================*           
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-READ-INPUT      THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-DATA    THRU 3000-EXIT                          
           UNTIL WS-F100-EOF = 'Y'.                                             
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
      *=============================================================*           
       1000-OPEN-ROUTINE.                                                       
      *=============================================================*           
                                                                                
           OPEN INPUT  BM06F100                                                 
                       BM06K110                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13K022                                                 
                       P04K010                                                  
                OUTPUT BP13LH52                                                 
                       P13LH52A.                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K820 ERROR ' WS-K820-STATUS                  
              MOVE     WS-K820-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K022 ERROR ' WS-K022-STATUS                  
              MOVE     WS-K022-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING P04K010 ERROR ' WS-K010-STATUS                   
              MOVE     WS-K010-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           INITIALIZE  WS-COUNT.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-TIME.                          
                                                                                
           MOVE WS-CUR-DATE(1:6)           TO WS-REPORT-DATE.                   
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2000-READ-INPUT.                                                         
      *=============================================================*           
                                                                                
            READ BM06F100                                                       
                 AT END MOVE 'Y'  TO WS-F100-EOF                                
            END-READ.                                                           
                                                                                
            IF WS-F100-EOF NOT = 'Y'                                            
               ADD 1 TO WS-F100-INPUT                                           
            END-IF.                                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       3000-PROCESS-DATA.                                                       
      *=============================================================*           
                                                                                
           MOVE SPACES                     TO BP13K800-MASTER                   
           INITIALIZE WS-FOUND-K800 WS-FOUND-K022 WS-PROCESSED.                 
                                                                                
           PERFORM 3100-COMPUTE-DATE                                            
              THRU 3100-EXIT.                                                   
                                                                                
           IF F100-DTE-PACD-QTR = WS-DTE-SYS-VALID(1:6)                         
              DISPLAY 'VALID 1 MONTH = ' F100-DTE-PACD-QTR ' KEY IS '           
                      F100-KEY-FLD                                              
              PERFORM 3200-READ-BM06K110                                        
                 THRU 3200-EXIT                                                 
                                                                                
              IF WS-PROCESSED NOT = 'Y'                                         
                 ADD 1 TO WS-F100-OUT                                           
                 DISPLAY 'NOT PRINTED = ' F100-KEY-FLD '/'                      
                    K110-SCH-ACC-NO '/' K110-REGN-NO '/' K820-NUM-NRIC          
                    '/' WS-DEBAR-CODE                                           
              END-IF                                                            
           ELSE                                                                 
              ADD 1 TO WS-F100-OUT                                              
              DISPLAY 'NOT 1 MONTH FROM PCD = ' F100-DTE-PACD                   
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-INPUT                                              
              THRU 2000-EXIT.                                                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=========================================================*               
       3100-COMPUTE-DATE.                                                       
      *=========================================================*               
                                                                                
            MOVE WS-CUR-DATE            TO WS-DTE-SYS-VALID                     
                                                                                
            COMPUTE WS-MM-SYS-VALID = WS-MM-SYS-VALID + 1                       
                                                                                
            IF WS-MM-SYS-VALID > 12                                             
               COMPUTE WS-MM-SYS-VALID = WS-MM-SYS-VALID - 12                   
               COMPUTE WS-YY-SYS-VALID = WS-MM-SYS-VALID + 1                    
            END-IF.                                                             
                                                                                
            MOVE WS-DTE-SYS-VALID    TO WS-DATE-TAB.                            
            IF VALID-YEAR AND VALID-MONTH AND VALID-DAY                         
               CONTINUE                                                         
            ELSE                                                                
               DISPLAY 'INVALID DATE = ' WS-DTE-SYS-VALID                       
            END-IF.                                                             
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3200-READ-BM06K110.                                                      
      *================================================================*        
                                                                                
           MOVE SPACES                TO K110-REC.                              
           INITIALIZE                    K110-REC.                              
           MOVE 'N'                   TO WS-K110-EOF.                           
                                                                                
           MOVE F100-KEY-FLD          TO K110-AIX1.                             
                                                                                
           START BM06K110 KEY >= K110-AIX1.                                     
                                                                                
           EVALUATE WS-K110-STATUS                                              
              WHEN 00                                                           
                 PERFORM 3210-READNEXT-BM06K110                                 
                    THRU 3210-EXIT                                              
                   UNTIL WS-K110-EOF = 'Y'                                      
              WHEN 10                                                           
              WHEN 23                                                           
              WHEN 46                                                           
                 MOVE 'Y' TO WS-K110-EOF                                        
                 DISPLAY 'BM06K110 REC NOT FND : ' WS-K110-STATUS               
                         ' F100-KEY-FLD = ' F100-KEY-FLD                        
              WHEN OTHER                                                        
                 DISPLAY 'ERROR STARTBROWSE BM06K110. STATUS IS '               
                            WS-K110-STATUS                                      
                 MOVE WS-K110-STATUS TO RETURN-CODE                             
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3210-READNEXT-BM06K110.                                                  
      *================================================================*        
           READ BM06K110 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K110-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 IF K110-AIX1 = F100-KEY-FLD                                    
                    IF K110-REGN-NO NOT = SPACES AND LOW-VALUES AND             
                       K110-REGN-NO(1:2) NOT = '##'                             
                       MOVE SPACES         TO BP13K800-MASTER                   
                       MOVE K110-REGN-NO   TO K800-NUM-REGN                     
                       PERFORM 3500-READ-BP13K800                               
                          THRU 3500-EXIT                                        
                    ELSE                                                        
                       DISPLAY 'INVALID K110-REGN-NO = ' K110-REGN-NO           
                    END-IF                                                      
                 ELSE                                                           
                    DISPLAY 'K110-AIX1 = ' K110-AIX1                            
                    DISPLAY 'F100-KEY-FLD = ' F100-KEY-FLD                      
                    MOVE 'Y'               TO WS-K110-EOF                       
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
              WHEN 46                                                           
                 MOVE 'Y'                  TO   WS-K110-EOF                     
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READ NEXT BM06K110. STATUS IS '                 
                            WS-K110-STATUS                                      
                 MOVE WS-K110-STATUS TO RETURN-CODE                             
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3500-READ-BP13K800.                                                      
      *================================================================*        
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
                IF K800-NUM-ALLO-CAT = 'NPL'                                    
                   DISPLAY 'K800-NUM-ALLO-CAT : ' K800-NUM-ALLO-CAT             
                      ' - ' K800-NUM-REGN                                       
                ELSE                                                            
                   MOVE 'Y'                   TO WS-FOUND-K800                  
                   PERFORM 3520-START-BP13K820                                  
                      THRU 3520-EXIT                                            
                END-IF                                                          
              WHEN 23                                                           
                MOVE 'N'                      TO WS-FOUND-K800                  
                DISPLAY 'BP13K800 REC NOT FND : ' WS-K800-STATUS                
                        ' K800-NUM-REGN = ' K800-NUM-REGN                       
              WHEN OTHER                                                        
                MOVE  WS-K800-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BP13K800 FILE : ' WS-K800-STATUS         
                DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                        
                PERFORM 9000-CLOSE-ROUTINE                                      
                   THRU 9000-EXIT                                               
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3520-START-BP13K820.                                                     
      *================================================================*        
           MOVE SPACES                     TO K820-KEY-FLD.                     
           MOVE K800-NUM-REGN              TO K820-NUM-REGN.                    
           MOVE 'N'                        TO WS-K820-EOF.                      
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                PERFORM 3525-READNEXT-BP13K820                                  
                   THRU 3525-EXIT                                               
                  UNTIL WS-K820-EOF = 'Y'                                       
              WHEN 10                                                           
              WHEN 23                                                           
                MOVE 'Y'                   TO WS-K820-EOF                       
                DISPLAY 'BP13K820 REC NOT FND : ' WS-K820-STATUS                
                        ' K820-NUM-REGN = ' K820-NUM-REGN                       
              WHEN OTHER                                                        
                MOVE  WS-K820-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BP13K820 FILE : ' WS-K820-STATUS         
                DISPLAY 'K820-NUM-REGN = ' K820-NUM-REGN                        
                PERFORM 9000-CLOSE-ROUTINE                                      
                   THRU 9000-EXIT                                               
           END-EVALUATE.                                                        
                                                                                
       3520-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3525-READNEXT-BP13K820.                                                  
      *================================================================*        
                                                                                
           READ BP13K820 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 IF K820-NUM-REGN = K800-NUM-REGN                               
                    IF K820-NUM-FREEZE-OCCP = 'Y'                               
                       PERFORM 3600-READ-P04K010                                
                          THRU 3600-EXIT                                        
                    END-IF                                                      
                 ELSE                                                           
                    MOVE 'Y'               TO WS-K820-EOF                       
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
              WHEN 46                                                           
                 MOVE 'Y'                  TO WS-K820-EOF                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READ NEXT BP13K820. STATUS IS '                 
                            WS-K820-STATUS                                      
                 MOVE WS-K820-STATUS TO RETURN-CODE                             
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       3525-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3600-READ-P04K010.                                                       
      *================================================================*        
                                                                                
           MOVE SPACES                              TO P04K010-REC.             
           INITIALIZE                                  P04K010-REC.             
                                                                                
           MOVE 'N'                                 TO WS-K010-EOF.             
                                                                                
           MOVE SPACES                              TO K010-PRIME-KEY.          
           MOVE K820-NUM-NRIC                       TO K010-NUM-UINFIN.         
                                                                                
           START P04K010 KEY >= K010-NUM-UINFIN.                                
                                                                                
           EVALUATE WS-K010-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              PERFORM 3610-READNEXT-P04K010                                     
                 THRU 3610-EXIT                                                 
                UNTIL WS-K010-EOF = 'Y'                                         
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                   TO WS-K010-EOF                         
              DISPLAY 'P04K010 REC NOT FND : ' WS-K010-STATUS                   
                      ' NRIC : ' K010-NUM-UINFIN                                
           WHEN OTHER                                                           
              MOVE WS-K010-STATUS TO RETURN-CODE                                
              DISPLAY 'ERROR STARTBR P04K010 FILE : ' WS-K010-STATUS            
                      ' NRIC : ' K010-NUM-UINFIN                                
              PERFORM 9000-CLOSE-ROUTINE                                        
                 THRU 9000-EXIT                                                 
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3610-READNEXT-P04K010.                                                   
      *================================================================*        
                                                                                
           READ P04K010 NEXT RECORD.                                            
                                                                                
           EVALUATE WS-K010-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 IF K010-NUM-UINFIN(1:9) = K820-NUM-NRIC                        
                    PERFORM 3620-PROCESS-P04K010                                
                       THRU 3620-EXIT                                           
                 ELSE                                                           
                    MOVE 'Y'               TO WS-K010-EOF                       
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
              WHEN 46                                                           
                 MOVE 'Y'                  TO WS-K010-EOF                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READ NEXT P04K010. STATUS IS '                  
                            WS-K010-STATUS                                      
                 MOVE WS-K010-STATUS TO RETURN-CODE                             
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       3610-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3620-PROCESS-P04K010.                                                    
      *================================================================*        
                                                                                
           MOVE K010-REASON-CODE  TO WS-DEBAR-CODE.                             
                                                                                
           IF VALID-DEB-CODE AND K010-PROP-TYPE = '1'                           
              PERFORM 4000-CREATE-REPORT                                        
                 THRU 4000-EXIT                                                 
              IF WS-OIC NOT = SPACES AND LOW-VALUES                             
                 PERFORM 6000-CALL-BP13CCNT                                     
                    THRU 6000-EXIT                                              
                 IF WS-EMAIL NOT = SPACES AND LOW-VALUES                        
                    PERFORM 4100-CREATE-REPORT2                                 
                       THRU 4100-EXIT                                           
                 END-IF                                                         
              END-IF                                                            
              MOVE 'Y' TO WS-K010-EOF                                           
                          WS-PROCESSED                                          
           END-IF.                                                              
                                                                                
       3620-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4000-CREATE-REPORT.                                                      
      *================================================================*        
                                                                                
           ADD  1                   TO WS-LH52-OUT.                             
                                                                                
           IF WS-LH52-OUT = 1                                                   
              STRING WS-CUR-DATE(7:2) '/'                                       
                     WS-CUR-DATE(5:2) '/'                                       
                     WS-CUR-DATE(1:4)                                           
                     DELIMITED BY SIZE                                          
                     INTO LH52-SYS-DATE                                         
              END-STRING                                                        
              MOVE LH52-SYS-DATE TO MAIL-DATE                                   
              WRITE BP13LH52-REC FROM MAIL-HDR1                                 
              WRITE BP13LH52-REC FROM MAIL-HDR2                                 
              WRITE BP13LH52-REC FROM MAIL-DTL1A                                
              WRITE BP13LH52-REC FROM MAIL-DTL1B                                
              WRITE BP13LH52-REC FROM MAIL-DTL3                                 
              WRITE BP13LH52-REC FROM MAIL-DTL4                                 
              WRITE BP13LH52-REC FROM MAIL-DTL5A                                
              WRITE BP13LH52-REC FROM MAIL-DTL5B                                
              WRITE BP13LH52-REC FROM MAIL-DTL7                                 
              WRITE BP13LH52-REC FROM MAIL-DTL8                                 
              WRITE BP13LH52-REC FROM MAIL-SPACES                               
              WRITE BP13LH52-REC FROM LH52-HDG1                                 
              WRITE BP13LH52-REC FROM LH52-HDG2                                 
              MOVE SPACES TO BP13LH52-REC                                       
              WRITE BP13LH52-REC                                                
              WRITE BP13LH52-REC FROM LH52-HDG3                                 
              WRITE BP13LH52-REC FROM LH52-HDG4                                 
              MOVE SPACES TO BP13LH52-REC                                       
              WRITE BP13LH52-REC                                                
           END-IF.                                                              
                                                                                
           MOVE WS-LH52-OUT               TO LH52-SNO.                          
           MOVE K800-NUM-REGN             TO LH52-REGN.                         
           MOVE K820-NUM-NRIC             TO LH52-NRIC.                         
           MOVE K820-NME-OCCP             TO LH52-NAME.                         
           MOVE K010-REASON-CODE          TO LH52-DEBAR.                        
           MOVE K010-AMT-OWING            TO LH52-AMT.                          
           MOVE K010-EFF-DATE             TO LH52-DTE-EFF.                      
           PERFORM 4500-GET-OIC                                                 
              THRU 4500-EXIT.                                                   
           MOVE WS-OIC                    TO LH52-OIC.                          
           WRITE BP13LH52-REC           FROM LH52-DTL1.                         
           IF F100-KEY-FLD = WS-SAVE-KEY                                        
              ADD 1                        TO WS-F100-INPUT                     
           END-IF                                                               
           ADD 1                           TO WS-WRITTEN.                       
           MOVE F100-KEY-FLD               TO WS-SAVE-KEY.                      
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4100-CREATE-REPORT2.                                                     
      *================================================================*        
                                                                                
           MOVE 1                          TO WS-LH52A-OUT.                     
                                                                                
           STRING WS-CUR-DATE(7:2) '/'                                          
                  WS-CUR-DATE(5:2) '/'                                          
                  WS-CUR-DATE(1:4)                                              
                  DELIMITED BY SIZE                                             
                  INTO LH52-SYS-DATE                                            
           END-STRING                                                           
           MOVE LH52-SYS-DATE TO MAIL-DATE                                      
           WRITE P13LH52A-REC FROM MAIL-HDR1                                    
           WRITE P13LH52A-REC FROM MAIL-HDR2                                    
                                                                                
           MOVE SPACES  TO P13LH52A-REC                                         
           STRING 'RCPT TO:<' DELIMITED BY SIZE                                 
                  WS-EMAIL '>'                                                  
                  DELIMITED BY SPACES INTO P13LH52A-REC                         
           WRITE P13LH52A-REC                                                   
                                                                                
           WRITE P13LH52A-REC FROM MAIL-DTL3                                    
           WRITE P13LH52A-REC FROM MAIL-DTL4                                    
                                                                                
           MOVE SPACES  TO P13LH52A-REC                                         
           STRING 'TO:<' DELIMITED BY SIZE                                      
                  WS-EMAIL '>'                                                  
                  DELIMITED BY SPACES                                           
                  INTO P13LH52A-REC                                             
           WRITE P13LH52A-REC                                                   
                                                                                
           WRITE P13LH52A-REC FROM MAIL-DTL7                                    
           WRITE P13LH52A-REC FROM MAIL-DTL8                                    
           WRITE P13LH52A-REC FROM MAIL-SPACES                                  
           WRITE P13LH52A-REC FROM LH52-HDG1                                    
           WRITE P13LH52A-REC FROM LH52-HDG2                                    
           MOVE SPACES TO P13LH52A-REC                                          
           WRITE P13LH52A-REC                                                   
           WRITE P13LH52A-REC FROM LH52-HDG3                                    
           WRITE P13LH52A-REC FROM LH52-HDG4                                    
           MOVE SPACES TO P13LH52A-REC                                          
           WRITE P13LH52A-REC                                                   
                                                                                
           MOVE WS-LH52A-OUT              TO LH52-SNO.                          
           MOVE K800-NUM-REGN             TO LH52-REGN.                         
           MOVE K820-NUM-NRIC             TO LH52-NRIC.                         
           MOVE K820-NME-OCCP             TO LH52-NAME.                         
           MOVE K010-REASON-CODE          TO LH52-DEBAR.                        
           MOVE K010-AMT-OWING            TO LH52-AMT.                          
           MOVE K010-EFF-DATE             TO LH52-DTE-EFF.                      
           MOVE WS-OIC                    TO LH52-OIC.                          
           WRITE P13LH52A-REC            FROM LH52-DTL1.                        
                                                                                
           MOVE  SPACES         TO P13LH52A-REC.                                
           WRITE P13LH52A-REC.                                                  
           WRITE P13LH52A-REC FROM MAIL-FOOTER.                                 
           WRITE P13LH52A-REC FROM MAIL-END.                                    
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4500-GET-OIC.                                                            
      *================================================================*        
           MOVE SPACES                     TO K022-KEY-FLD.                     
           MOVE K800-NUM-REGN              TO K022-NUM-REGN.                    
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN ZEROS                                                        
                 MOVE 'Y'                     TO WS-FOUND-K022                  
                 MOVE K022-NUM-OIC            TO WS-OIC                         
              WHEN 23                                                           
                 MOVE SPACES                  TO WS-OIC                         
              WHEN OTHER                                                        
                MOVE  WS-K022-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BP13K022 FILE : ' WS-K022-STATUS         
                DISPLAY 'K022-NUM-REGN = ' K022-NUM-REGN                        
                PERFORM 9000-CLOSE-ROUTINE                                      
                   THRU 9000-EXIT                                               
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5000-EMAIL-FOOTER.                                                       
      *================================================================*        
                                                                                
           MOVE  SPACES         TO BP13LH52-REC.                                
           WRITE BP13LH52-REC.                                                  
           WRITE BP13LH52-REC FROM MAIL-FOOTER.                                 
           WRITE BP13LH52-REC FROM MAIL-END.                                    
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       6000-CALL-BP13CCNT.                                                      
      *================================================================*        
           MOVE 'EML'                      TO LINK-CNTCT-TYPE.                  
           MOVE WS-OIC                     TO LINK-STAFF-NICK.                  
           MOVE SPACES                     TO LINK-STAFF-NUM.                   
                                                                                
           CALL 'BP13CCNT' USING WS-LINK-REC.                                   
                                                                                
           EVALUATE LINK-SYSERR                                                 
           WHEN ZEROES                                                          
              MOVE LINK-STAFF-CNTCT        TO WS-EMAIL                          
           WHEN 100                                                             
              DISPLAY 'OIC NOT FOUND IN OFFICE_CNTCT TABLE - '                  
                         WS-OIC  ', REGN : ' K110-REGN-NO                       
              MOVE  SPACES                 TO WS-EMAIL                          
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING OFFICE_CNTCT TABLE = '                     
                       LINK-SYSERR                                              
              MOVE  SPACES                 TO WS-EMAIL                          
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-ROUTINE.                                                      
      *================================================================*        
           IF WS-WRITTEN > 0                                                    
              PERFORM 5000-EMAIL-FOOTER                                         
                 THRU 5000-EXIT                                                 
           END-IF.                                                              
                                                                                
           DISPLAY 'PROGRAM: BP13CH52'.                                         
           DISPLAY 'TOTAL INPUT RECORDS        : ' WS-F100-INPUT.               
           DISPLAY 'TOTAL RECORD WRITTEN       : ' WS-WRITTEN.                  
           DISPLAY 'TOTAL RECORD NOT WRITTEN   : ' WS-F100-OUT.                 
           DISPLAY '        '                                                   
                                                                                
           CLOSE BM06F100                                                       
                 BM06K110                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K022                                                       
                 P04K010                                                        
                 BP13LH52                                                       
                 P13LH52A.                                                      
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K820 ERROR ' WS-K820-STATUS                  
              MOVE     WS-K820-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K022 ERROR ' WS-K022-STATUS                  
              MOVE     WS-K022-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING P04K010 ERROR ' WS-K010-STATUS                   
              MOVE     WS-K010-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
