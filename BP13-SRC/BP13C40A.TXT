       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C40A.                                                 
      *AUTHOR.        SMP3.                                                     
      *DATE-WRITTEN.  31/01/2020.                                               
      * ============================================================ *          
      *                  SYSTEM OF COMMITMENT (SOC)                  *          
      * ============================================================ *          
      *                                                              *          
      *   OBJECTIVES   :                                             *          
      *        1. TAKE IN MONTHLY COMPLETED NEW SALES CASE           *          
      *        2. FORMAT THE FILE ACCORDING TO IRAS'S REQUEST        *          
      *        3. INCLUDE PROPERTY DATA FROM PIDB AS WELL AS         *          
      *           1ST SALES DATE FROM FIS AND TOWN/BO CODE FROM      *          
      *           ACCUMULATED SALES ORDER                            *          
      *                                                              *          
      *    INPUT FILES :                                             *          
      *        1. BP13F430 - SALES ORDER ACCEPTED CASES              *          
      *        2. BM06K110 - FLAT INVENTORY MASTER                   *          
      *        3. BM06K100 - FIS BLOCK FILE                          *          
      *        4. BP13K410 - ACCUMULATED SALES ORDER                 *          
      *                                                              *          
      *    OUTPUT FILE :                                             *          
      *        1. BP13F40A - PROCESSED SALES ORDER                   *          
      *                                                              *          
      * ============================================================ *          
      * REQ-NO   DATE       OIC  DESCRIPTION                         *          
      * -------- ---------- ---- ----------------------------------- *          
      * BP138168 31/01/2020 SMP3 COMBINE AB03C009/AB03C014/AB10C001/ *          
      *                          AB10C002 TO GENERATE IRAS FILE      *          
      * ============================================================ *          
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F430 ASSIGN       TO BP13F430.                            
                                                                                
           SELECT BM06K110 ASSIGN       TO BM06K110                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K110-KEY-FLD                         
                           FILE STATUS  IS WS-K110-STATUS.                      
                                                                                
           SELECT BM06K100 ASSIGN       TO BM06K100                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K100-KEY-FLD                         
                           FILE STATUS  IS WS-K100-STATUS.                      
                                                                                
           SELECT BP13K410 ASSIGN       TO BP13K410                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K410-KEY-FLD                         
                           FILE STATUS  IS WS-K410-STATUS.                      
                                                                                
           SELECT BP13F40A ASSIGN       TO BP13F40A.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F430                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS 1000 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORD      IS STANDARD.                                      
       COPY BP13F430.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BM06K110.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BM06K100.                                                           
                                                                                
       FD   BP13K410                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K410.                                                           
                                                                                
       FD   BP13F40A                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  500 CHARACTERS                                     
            RECORDING MODE    IS F                                              
            LABEL RECORD      IS STANDARD.                                      
       COPY BP13F40A.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K110-STATUS          PIC 9(02)   VALUE ZEROES.                
           05  WS-K100-STATUS          PIC 9(02)   VALUE ZEROES.                
           05  WS-K410-STATUS          PIC 9(02)   VALUE ZEROES.                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F430-EOF             PIC X(01)   VALUE 'N'.                   
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DTE-CURR             PIC X(08)   VALUE SPACES.                
                                                                                
      ************************************************************              
      *    OUTPUT   FORMAT                                                      
      ************************************************************              
       01  WS-F40A-HDG.                                                         
           05  FILLER                  PIC X(02)   VALUE SPACES.                
           05  FILLER                  PIC X(18)   VALUE                        
               'HDB-SALES-OF-FLATS'.                                            
           05  FILLER                  PIC X(379)  VALUE SPACES.                
           05  FILLER                  PIC X(01)   VALUE '0'.                   
                                                                                
       01  WS-TRAILER.                                                          
           05  FILLER                  PIC X(18)   VALUE                        
               ' NO-OF-ACCOUNT = '.                                             
           05  WS-OUTCNT               PIC 9(06)   VALUE ZEROES.                
           05  FILLER                  PIC X(375)  VALUE SPACES.                
           05  FILLER                  PIC X(01)   VALUE '9'.                   
                                                                                
      **************************************************************            
      *    CONTROL     LISTING                                                  
      **************************************************************            
       01  COUNT-REC.                                                           
           05  WS-CNT-F430-READ        PIC 9(06)   VALUE ZEROES.                
           05  WS-CNT-F40A-WRITE       PIC 9(06)   VALUE ZEROES.                
           05  WS-CNT-PIDB-NFND        PIC 9(06)   VALUE ZEROES.                
           05  WS-CNT-PIDB-FND         PIC 9(06)   VALUE ZEROES.                
           05  WS-CNT-K110-NFND        PIC 9(06)   VALUE ZEROES.                
           05  WS-CNT-K110-FND         PIC 9(06)   VALUE ZEROES.                
           05  WS-CNT-K100-NFND        PIC 9(06)   VALUE ZEROES.                
           05  WS-CNT-K100-FND         PIC 9(06)   VALUE ZEROES.                
           05  WS-CNT-K410-NFND        PIC 9(06)   VALUE ZEROES.                
           05  WS-CNT-K410-FND         PIC 9(06)   VALUE ZEROES.                
                                                                                
      *-------------------------------------------------------*                 
      *    COPY BOOK FOR SUBROUTINE - BP13C913.               *                 
      *-------------------------------------------------------*                 
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------*                 
      *    LINKAGE VARIABLES                                  *                 
      *-------------------------------------------------------*                 
       01   WS-LINK-REC.                                                        
            05 WS-LINK-NUM-SCH         PIC X(04).                               
            05 WS-LINK-NUM-ACC         PIC X(04).                               
            05 WS-LINK-NUM-CHK         PIC X(01).                               
                                                                                
      *********************                                                     
       PROCEDURE DIVISION.                                                      
      *********************                                                     
                                                                                
       0000-MAIN.                                                               
                                                                                
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F430       THRU 2000-EXIT.                     
           PERFORM 3000-MAIN-ROUTINE        THRU 3000-EXIT                      
             UNTIL WS-F430-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
                                                                                
           OPEN INPUT  BP13F430                                                 
                       BM06K110                                                 
                       BM06K100                                                 
                       BP13K410                                                 
                OUTPUT BP13F40A.                                                
                                                                                
           IF WS-K110-STATUS NOT = 0 AND 97                                     
              MOVE WS-K110-STATUS           TO RETURN-CODE                      
              DISPLAY 'OPEN ERROR IN BM06K110 ' WS-K110-STATUS                  
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = 0 AND 97                                     
              MOVE WS-K100-STATUS           TO RETURN-CODE                      
              DISPLAY 'OPEN ERROR IN BM06K100 ' WS-K100-STATUS                  
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K410-STATUS NOT = 0 AND 97                                     
              MOVE WS-K410-STATUS           TO RETURN-CODE                      
              DISPLAY 'OPEN ERROR IN BP13K410 ' WS-K410-STATUS                  
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO WS-DTE-CURR.                     
                                                                                
           MOVE SPACES                      TO BP13F40A-REC.                    
           INITIALIZE                          BP13F40A-REC.                    
                                                                                
           WRITE BP13F40A-REC               FROM WS-F40A-HDG.                   
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F430.                                                      
                                                                                
           READ BP13F430                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-F430-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-CNT-F430-READ.                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-MAIN-ROUTINE.                                                       
                                                                                
           PERFORM 4000-MOVE-BP13F430       THRU 4000-EXIT.                     
           PERFORM 5000-CALL-BP13C913       THRU 5000-EXIT.                     
           PERFORM 6000-READ-BM06K110       THRU 6000-EXIT.                     
           PERFORM 7000-READ-BP13K410       THRU 7000-EXIT.                     
           PERFORM 8000-WRITE-BP13F40A      THRU 8000-EXIT.                     
                                                                                
           PERFORM 2000-READ-BP13F430       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-MOVE-BP13F430.                                                      
                                                                                
           MOVE SPACES                      TO BP13F40A-REC.                    
           INITIALIZE                          BP13F40A-REC.                    
                                                                                
           MOVE F430-DTE-EXE-SALE-AGRMT     TO F40A-SOLD-DATE.                  
           IF F430-AMT-SELL-PRICE-NEW IS NUMERIC AND                            
              F430-AMT-SELL-PRICE-NEW > 0                                       
              MOVE F430-AMT-SELL-PRICE-NEW  TO F40A-SELLING-PRICE               
           ELSE                                                                 
              MOVE F430-AMT-SELL-PRICE      TO F40A-SELLING-PRICE               
           END-IF.                                                              
           MOVE F430-NME-LESSEE1            TO F40A-NAME1.                      
           MOVE F430-NUM-UIN1               TO F40A-UIN1.                       
           MOVE F430-NME-LESSEE2            TO F40A-NAME2.                      
           MOVE F430-NUM-UIN2               TO F40A-UIN2.                       
           MOVE F430-NME-LESSEE3            TO F40A-NAME3.                      
           MOVE F430-NUM-UIN3               TO F40A-UIN3.                       
           MOVE F430-NME-LESSEE4            TO F40A-NAME4.                      
           MOVE F430-NUM-UIN4               TO F40A-UIN4.                       
           MOVE F430-NUM-HDB-REF            TO F40A-HDB-REF.                    
           IF F430-CDE-SEX1 = 'M'                                               
              MOVE 'MR'                     TO F40A-SALUTATION1                 
           ELSE                                                                 
              IF F430-CDE-SEX1 = 'F'                                            
                 MOVE 'MS'                  TO F40A-SALUTATION1                 
              END-IF                                                            
           END-IF.                                                              
           IF F430-CDE-SEX2 = 'M'                                               
              MOVE 'MR'                     TO F40A-SALUTATION2                 
           ELSE                                                                 
              IF F430-CDE-SEX2 = 'F'                                            
                 MOVE 'MS'                  TO F40A-SALUTATION2                 
              END-IF                                                            
           END-IF.                                                              
           IF F430-CDE-SEX3 = 'M'                                               
              MOVE 'MR'                     TO F40A-SALUTATION3                 
           ELSE                                                                 
              IF F430-CDE-SEX3 = 'F'                                            
                 MOVE 'MS'                  TO F40A-SALUTATION3                 
              END-IF                                                            
           END-IF.                                                              
           IF F430-CDE-SEX4 = 'M'                                               
              MOVE 'MR'                     TO F40A-SALUTATION4                 
           ELSE                                                                 
              IF F430-CDE-SEX4 = 'F'                                            
                 MOVE 'MS'                  TO F40A-SALUTATION4                 
              END-IF                                                            
           END-IF.                                                              
           MOVE F430-CDE-SALES-TYP          TO F40A-CDE-SALES-TYP.              
           MOVE '1'                         TO F40A-REC-TYPE.                   
           MOVE F430-NUM-SHARE-FLAT1        TO F40A-SHARE-FLAT1.                
           MOVE F430-NUM-SHARE-FLAT2        TO F40A-SHARE-FLAT2.                
           MOVE F430-NUM-SHARE-FLAT3        TO F40A-SHARE-FLAT3.                
           MOVE F430-NUM-SHARE-FLAT4        TO F40A-SHARE-FLAT4.                
           MOVE F430-NUM-SHARE-FLAT-BASE    TO F40A-SHARE-FLAT-BASE.            
           MOVE F430-CDE-HOLDING-TYPE       TO F40A-CDE-HOLD-TYPE.              
           MOVE F430-DTE-PROPERTY-TAX       TO F40A-DTE-PROPERTY-TAX.           
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-CALL-BP13C913.                                                      
                                                                                
           MOVE SPACES                      TO WS-LINK-REC.                     
           MOVE F430-NUM-HDB-REF(1:4)       TO WS-LINK-NUM-SCH.                 
           MOVE F430-NUM-HDB-REF(5:4)       TO WS-LINK-NUM-ACC.                 
           MOVE F430-NUM-HDB-REF(9:1)       TO WS-LINK-NUM-CHK.                 
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
           IF COMM8-CDE-SYSERR = ZEROES                                         
              ADD 1                         TO WS-CNT-PIDB-FND                  
              PERFORM 5100-MOVE-PIDB        THRU 5100-EXIT                      
           ELSE                                                                 
              ADD 1                         TO WS-CNT-PIDB-NFND                 
              DISPLAY 'ERROR READING PBF TABLE: ' COMM8-CDE-SYSERR              
                      ', KEY FLD: ' F430-NUM-HDB-REF                            
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5100-MOVE-PIDB.                                                          
                                                                                
           MOVE COMM8-CDE-ST                TO F40A-ST-CODE.                    
           MOVE COMM8-NUM-BLK               TO F40A-BLK.                        
           MOVE COMM8-NUM-LEVEL             TO F40A-NUM-LEVEL.                  
           MOVE COMM8-NUM-UNIT-MAIN         TO F40A-NUM-UNIT(1:4).              
           MOVE COMM8-NUM-UNIT-SUB          TO F40A-NUM-UNIT(5:3).              
           MOVE COMM8-CDE-BUC               TO F40A-PROP-CLASS.                 
           MOVE COMM8-NUM-POSTAL-CODE       TO F40A-POSTAL-DIST.                
           MOVE COMM8-QTY-AREA-TOTAL        TO F40A-FLOOR-AREA.                 
           MOVE COMM8-NUM-ESTATE-CIM        TO F40A-EST-CODE.                   
           MOVE COMM8-NME-STREET            TO F40A-NME-STABBRV.                
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       6000-READ-BM06K110.                                                      
                                                                                
           MOVE SPACES                      TO K110-REC.                        
           INITIALIZE                          K110-REC.                        
                                                                                
           MOVE F430-NUM-HDB-REF            TO K110-KEY-FLD.                    
                                                                                
           READ BM06K110.                                                       
           EVALUATE WS-K110-STATUS                                              
           WHEN 00                                                              
                IF (K110-DTE-1ST-SALE IS NOT NUMERIC) OR                        
                   (K110-DTE-1ST-SALE = SPACES OR ZEROES OR LOW-VALUES)         
                   ADD 1                    TO WS-CNT-K110-NFND                 
                   PERFORM 6500-READ-BM06K100 THRU 6500-EXIT                    
                ELSE                                                            
                   ADD 1                    TO WS-CNT-K110-FND                  
                   MOVE K110-DTE-1ST-SALE   TO F40A-EFF-1ST-SALE                
                END-IF                                                          
                                                                                
           WHEN 23                                                              
                ADD 1                       TO WS-CNT-K110-NFND                 
                MOVE ZEROES                 TO F40A-EFF-1ST-SALE                
                DISPLAY 'NOT FOUND IN BM06K110: ' F430-NUM-HDB-REF              
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'BM06K110 READ ERROR, STATUS: ' WS-K110-STATUS          
                        ', KEY FLD: ' K110-KEY-FLD                              
                MOVE WS-K110-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       6500-READ-BM06K100.                                                      
                                                                                
           MOVE SPACES                      TO K100-REC.                        
           INITIALIZE                          K100-REC.                        
                                                                                
           MOVE K110-AIX1                   TO K100-KEY-FLD.                    
                                                                                
           READ BM06K100.                                                       
           EVALUATE WS-K100-STATUS                                              
           WHEN 00                                                              
                IF (K100-DTE-1ST-SALE IS NOT NUMERIC) OR                        
                   (K100-DTE-1ST-SALE = SPACES OR ZEROES OR LOW-VALUES)         
                   ADD 1                    TO WS-CNT-K100-NFND                 
                   MOVE ZEROES              TO F40A-EFF-1ST-SALE                
                ELSE                                                            
                   ADD 1                    TO WS-CNT-K100-FND                  
                   MOVE K100-DTE-1ST-SALE   TO F40A-EFF-1ST-SALE                
                END-IF                                                          
                                                                                
           WHEN 23                                                              
                ADD 1                       TO WS-CNT-K100-NFND                 
                MOVE ZEROES                 TO F40A-EFF-1ST-SALE                
                DISPLAY 'NOT FOUND IN BM06K100: ' F430-NUM-HDB-REF              
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'BM06K100 READ ERROR, STATUS: ' WS-K100-STATUS          
                        ', KEY FLD: ' K100-KEY-FLD                              
                MOVE WS-K100-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
       7000-READ-BP13K410.                                                      
                                                                                
           MOVE SPACES                      TO BP13K410-REC.                    
           INITIALIZE                          BP13K410-REC.                    
                                                                                
           MOVE F430-NUM-REG                TO K410-KEY-FLD.                    
                                                                                
           READ BP13K410.                                                       
           EVALUATE WS-K410-STATUS                                              
           WHEN 00                                                              
                ADD 1                       TO WS-CNT-K410-FND                  
                MOVE K410-CDE-NT            TO F40A-NUM-TOWN(1:3)               
                MOVE K410-CDE-BO            TO F40A-BO                          
                                                                                
           WHEN 23                                                              
                ADD 1                       TO WS-CNT-K410-NFND                 
                DISPLAY 'NOT FOUND IN BP13K410: ' F430-NUM-HDB-REF              
                                                                                
           WHEN OTHER                                                           
                DISPLAY 'BP13K410 READ ERROR, STATUS: ' WS-K410-STATUS          
                        ', KEY FLD: ' K410-KEY-FLD                              
                MOVE WS-K410-STATUS         TO RETURN-CODE                      
                PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       8000-WRITE-BP13F40A.                                                     
                                                                                
           WRITE BP13F40A-REC.                                                  
           ADD 1                            TO WS-CNT-F40A-WRITE.               
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'PROGRAM : BP13C40A          ' WS-DTE-CURR.                  
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF BP13F430 RECS READ    ' WS-CNT-F430-READ.             
           DISPLAY 'NO OF BP13F40A RECS WRITTEN ' WS-CNT-F40A-WRITE.            
           DISPLAY SPACES.                                                      
           DISPLAY 'PIDB INFO      - FOUND IN PIDB      '                       
                                     WS-CNT-PIDB-FND.                           
           DISPLAY '               - NOT FOUND IN PIDB  '                       
                                     WS-CNT-PIDB-NFND.                          
           DISPLAY SPACES.                                                      
           DISPLAY '1ST SALE DATE  - FOUND IN BM06K110  '                       
                                     WS-CNT-K110-FND.                           
           DISPLAY '               - NOT FOUND IN K110  '                       
                                     WS-CNT-K110-NFND.                          
           DISPLAY SPACES.                                                      
           DISPLAY '1ST SALE DATE  - FOUND IN BM06K100  '                       
                                     WS-CNT-K100-FND.                           
           DISPLAY '               - NOT FOUND IN K100  '                       
                                     WS-CNT-K100-NFND.                          
           DISPLAY SPACES.                                                      
           DISPLAY 'TOWN/BO CODE   - FOUND IN BP13K410  '                       
                                     WS-CNT-K410-FND.                           
           DISPLAY '               - NOT FOUND IN K410  '                       
                                     WS-CNT-K410-NFND.                          
                                                                                
           MOVE SPACES                      TO BP13F40A-REC.                    
           INITIALIZE                          BP13F40A-REC.                    
                                                                                
           MOVE WS-CNT-F40A-WRITE           TO WS-OUTCNT.                       
           WRITE BP13F40A-REC               FROM WS-TRAILER.                    
                                                                                
           CLOSE BP13F430                                                       
                 BM06K110                                                       
                 BM06K100                                                       
                 BP13K410                                                       
                 BP13F40A.                                                      
                                                                                
           IF WS-K110-STATUS NOT = ZEROES                                       
              MOVE WS-K110-STATUS           TO RETURN-CODE                      
              DISPLAY 'CLOSE ERROR IN BM06K110 ' WS-K110-STATUS                 
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = ZEROES                                       
              MOVE WS-K100-STATUS           TO RETURN-CODE                      
              DISPLAY 'CLOSE ERROR IN BM06K100 ' WS-K100-STATUS                 
           END-IF.                                                              
                                                                                
           IF WS-K410-STATUS NOT = ZEROES                                       
              MOVE WS-K410-STATUS           TO RETURN-CODE                      
              DISPLAY 'CLOSE ERROR IN BP13K410 ' WS-K410-STATUS                 
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
