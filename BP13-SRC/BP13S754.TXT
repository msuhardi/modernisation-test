  /*---------------------------------------------------------------*/           
  /*  OBJECTIVES : TO TAB THE REPORT ON SUCCESSFUL APPLICANTS IN   */           
  /*               MATURE ESTATES UNDER THE RESIDENTS PRIORITY     */           
  /*               SCHEME FOR                                      */           
  /*               1) REN/RHO                                      */           
  /*               2) GRO                                          */           
  /*               3) TCP/MTF                                      */           
  /*               4) PUBLIC APPLICANTS                            */           
  /*               5) RDC                                          */           
  /*---------------------------------------------------------------*/           
  /*   CHG REQ  DATE   OIC    DESCRIPTION                          */           
  /*   -------  -----  ---    -----------                          */           
  /*  N940014  221294  YCH    CHANGE TO ZONE AND EXTRACT PRIOFMT   */           
  /*  BP132098 180202  SN     TO CATER FOR HOUSE HOLD              */           
  /*  BP132162 120702  ANC1   DELETE CHECKING OF 'T' FOR RES_HA1   */           
  /*                          FOR NON-RESIDENT.                    */           
  /*---------------------------------------------------------------*/           
  PROC FORMAT;                                                                  
   VALUE $RES_FMT " "="SUCCESS"                                                 
                  "U"="UNSUCC";                                                 
                                                                                
   VALUE $RSCH_FMT "1"="MCPS        "                                           
                   "2"="NON-MCPS    ";                                          
                                                                                
   VALUE $RM_FMT  "3 "="3-ROOM"                                                 
                  "4 "="4-ROOM"                                                 
                  "4D"="4D-ROOM"                                                
                  "5 "="5-ROOM"                                                 
                  "5D"="5D-ROOM"                                                
                  "6 "="EXEC"                                                   
                  "6D"="EXEC-D";                                                
                                                                                
   VALUE $HSE_FMT "H"="H/T"                                                     
                  "T"="H/T"                                                     
                  "G"="G";                                                      
                                                                                
   %GLOBAL DATE;                                                                
   OPTIONS NODATE;                                                              
   DATA BP13F520;                                                               
        INFILE BP13F520 END=EOF;                                                
        INPUT @78  REQ_ZN  $CHAR2.                                              
              @116 HSE_HLD $CHAR1.                                              
              @187 SEL_NT  $CHAR3.                                              
              @226 SUCC_NT  $CHAR3.                                             
              @253 HA1_RES  $CHAR1.  @254 HA2_RES $CHAR1.                       
              @121 HA3_RES  $CHAR1.  @122 HA4_RES $CHAR1.                       
              @211 FLT_TYP  $CHAR2.                                             
              @229 RESULT   $CHAR1.  @213 ALL_SCH  $CHAR3.                      
              ;                                                                 
                                                                                
        IF _N_=1 THEN                                                           
           DO;                                                                  
              CUR_DTE=PUT(TODAY(),DDMMYY8.);                                    
              CALL SYMPUT("DATE",CUR_DTE);                                      
           END;                                                                 
                                                                                
        IF ((HA1_RES=" ") AND                                                   
            (HA2_RES=" ") AND                                                   
            (HA3_RES=" ") AND                                                   
            (HA4_RES=" ")) THEN                                                 
           RES_SCH="2";                    /* NON-RESIDENT  */                  
        ELSE                                                                    
           RES_SCH="1";                    /* RESIDENT      */                  
                                                                                
        IF FLT_TYP="E"  THEN FLT_TYP="6 ";                                      
        IF FLT_TYP="ED" THEN FLT_TYP="6D";                                      
        IF RESULT^=" "  THEN RESULT="U";                                        
                                                                                
        IF SUCC_NT = '   ' THEN                                                 
           DO;                                                                  
              IF REQ_ZN=' ' THEN                                                
                 CODE_NT = SEL_NT ;                                             
              ELSE                                                              
                 CODE_NT = REQ_ZN;                                              
           END;                                                                 
        ELSE                                                                    
           CODE_NT = SUCC_NT ;                                                  
                                                                                
   RUN;                                                                         
   PROC SORT; BY ALL_SCH;                                                       
   RUN;                                                                         
                                                                                
   DATA BP13PRIO;                                                               
        INFILE PRIOFMT;                                                         
        INPUT  @2 ALL_SCH $CHAR3. @9 PRIO_SCH $CHAR3.;                          
   RUN;                                                                         
                                                                                
   PROC SORT; BY ALL_SCH;                                                       
   RUN;                                                                         
                                                                                
   DATA BP13SCH;                                                                
   MERGE BP13F520(IN=M) BP13PRIO(IN=T); BY ALL_SCH;                             
         IF M;                                                                  
   RUN;                                                                         
                                                                                
   OPTIONS MISSING=0;                                                           
   PROC PRINTTO UNIT=18 NEW;                                                    
   PROC TABULATE DATA=BP13SCH MISSING FORMAT=7.1;                               
   CLASS  PRIO_SCH RES_SCH HSE_HLD FLT_TYP RESULT CODE_NT;                      
                                                                                
   FORMAT FLT_TYP $RM_FMT.                                                      
          RES_SCH $RSCH_FMT.                                                    
          RESULT  $RES_FMT.                                                     
          HSE_HLD $HSE_FMT.;                                                    
                                                                                
   KEYLABEL N  =" "                                                             
            PCTN="RATE(%)"                                                      
            ALL="TOTAL";                                                        
                                                                                
   LABEL CODE_NT="NEW TOWN : "                                                  
         FLT_TYP="FLAT TYPE";                                                   
                                                                                
   TABLE PRIO_SCH="SCHEME : " ALL,                                              
         (RES_SCH=" ")*(ALL="REQUESTS"                                          
         RESULT=" " (RESULT=" ")*PCTN<RESULT>),                                 
         (CODE_NT=" " ALL)*(FLT_TYP=" ")*(HSE_HLD=" ")                          
         / PRINTMISS RTS=25                                                     
         BOX="NEW TOWN/ZONE:        FLAT TYPE:";                                
                                                                                
                                                                                
TITLE1 "BP13L754 HDB3                                   SYSTEM OF COMMITMENT    
                       DATE: &DATE        PAGE : ";                             
TITLE2 "                                REPORT ON SUCC/UNSUCC BY RESIDENT/NON-RE
SIDENT AND ZONE                                  ";                             
