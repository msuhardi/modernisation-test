      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CY05.                                                 
       AUTHOR.        JIANG BO.                                                 
       DATE-WRITTEN.  25/04/08.                                                 
      *****************************************************************         
      *OBJECTIVE -                                                    *         
      * MATCH BTO AND BALLOT HOSTORY REC, COMPUTE NO OF BE/BTO REQUEST*         
      * BP13OUT - SUM UP NUMBER OF PREVIOUS BE/BTO ATTEMPTS K730)     *         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13F730                                  *         
      *                  2. BP13F595                                  *         
      *                                                               *         
      *     OUTPUT FILE:                                              *         
      *                  1. BP13OUTA                                  *         
      *                  2. BP13OUTB                                  *         
      *                  3. BP13OUTC                                  *         
      *                                                               *         
      *     TITLE-TYPE : 'A' - MULTIPLE                               *         
      *                  'C' - LATEST                                 *         
      *---------------------------------------------------------------*         
      * REF NO    DATE      BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------   --------  ---  ------------------------             *         
      * BP133340  14042008  JB8  NEW PROGRAM.                         *         
      *           13062008  JB8  NEW OUTPUT FILE - BP13OUTC           *         
      * BP133430  10092008  LMS1 NEW SELECTION TYPE WIH.              *         
      * BP133490  22112008  JB8  OUTA (MULTIPLE) OUTC (LATEST)        *         
      * BP133728  14102009  JB8  TO CATER FOR NEW ALLO-CAT 'SBF'      *         
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F730  ASSIGN        TO BP13F730.                         
           SELECT  BP13F595  ASSIGN        TO BP13F595.                         
           SELECT  BP13OUTA  ASSIGN        TO BP13OUTA.                         
           SELECT  BP13OUTB  ASSIGN        TO BP13OUTB.                         
           SELECT  BP13OUTC  ASSIGN        TO BP13OUTC.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F730                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 500 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F730.                                                           
                                                                                
       FD   BP13F595                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 500 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F595.                                                           
                                                                                
       FD   BP13OUTA                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 100 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       01   BP13OUTA-REC.                                                       
            05 OUTA-NUM-REGN          PIC X(08).                                
            05 OUTA-NUM-NRIC          PIC X(09).                                
            05 OUTA-DTE-BALLOT        PIC X(06).                                
            05 OUTA-NUM-ALLOCAT       PIC X(03).                                
            05 OUTA-NUM-FTYPE         PIC X(02).                                
            05 OUTA-NUM-HH            PIC X(01).                                
            05 OUTA-NUM-NT            PIC X(03).                                
            05 OUTA-CAN-NT            PIC X(03).                                
            05 OUTA-NUM-MODE          PIC X(03).                                
            05 OUTA-CDE-FT            PIC X(02).                                
            05 OUTA-CDE-HH            PIC X(01).                                
            05 FILLER                 PIC X(57).                                
            05 OUTA-TITLE-TYPE        PIC X(01).                                
            05 OUTA-CDE-NEWAPP        PIC X(01).                                
                                                                                
       FD   BP13OUTB                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 2000 CHARACTERS          
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       01   BP13OUTB-REC.                                                       
            05 OUTB-NUM-REGN          PIC X(08).                                
            05 OUTB-NUM-NRIC          PIC X(09).                                
            05 FILLER                 PIC X(12).                                
            05 OUTB-NUM-FT            PIC X(02).                                
            05 FILLER                 PIC X(19).                                
            05 OUTB-NT                PIC X(03).                                
            05 OUTB-NUM-HOUSEHOLD     PIC X(01).                                
            05 OUTB-DBS-CNT           PIC 9(02).                                
            05 OUTB-WIS-CNT           PIC 9(02).                                
            05 OUTB-SBF-CNT           PIC 9(02).                                
            05 FILLER                 PIC X(1910).                              
            05 OUTB-TOT-CNT           PIC 9(02).                                
            05 OUTB-NUM-ALLOCAT       PIC X(03).                                
            05 FILLER                 PIC X(21).                                
            05 OUTB-BE-CNT            PIC 9(02).                                
            05 OUTB-BTO-CNT           PIC 9(02).                                
                                                                                
       FD   BP13OUTC                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 100 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       01   BP13OUTC-REC.                                                       
            05 OUTC-NUM-REGN          PIC X(08).                                
            05 OUTC-NUM-NRIC          PIC X(09).                                
            05 OUTC-DTE-BALLOT        PIC X(06).                                
            05 OUTC-NUM-ALLOCAT       PIC X(03).                                
            05 OUTC-CDE-FLAT-TYPE     PIC X(02).                                
            05 OUTC-CDE-HOUSEHOLD     PIC X(01).                                
            05 OUTC-NUM-NEW-TOWN      PIC X(03).                                
            05 OUTC-CAN-NT            PIC X(03).                                
            05 OUTC-NUM-MODE          PIC X(03).                                
            05 OUTC-CDE-FT            PIC X(02).                                
            05 OUTC-CDE-HH            PIC X(01).                                
            05 FILLER                 PIC X(57).                                
            05 OUTC-TITLE-TYPE        PIC X(01).                                
            05 OUTC-CDE-NEW           PIC X(01).                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
                                                                                
       01  WS-F595-MAX.                                                         
           05  WS-F595-NRIC            PIC X(9)  VALUE SPACES.                  
           05  WS-F595-MODE            PIC X(3)  VALUE SPACES.                  
           05  WS-F595-BALDATE         PIC X(8)  VALUE SPACES.                  
           05  WS-F595-FT              PIC X(2)  VALUE SPACES.                  
           05  WS-F595-HH              PIC X(1)  VALUE SPACES.                  
           05  WS-F595-NT              PIC X(3)  VALUE SPACES.                  
                                                                                
       01  VARIABLES.                                                           
           05  WS-TMP-FLAG             PIC X(1)  VALUE SPACES.                  
           05  WS-NUM-ALLO-CAT         PIC X(3)  VALUE SPACES.                  
           05  WS-EOF-F595             PIC X(1)  VALUE SPACE.                   
           05  WS-EOF-F730             PIC X(1)  VALUE SPACE.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-BP13F730-READ        PIC 9(7)  VALUE ZEROES.                  
           05  WS-BP13F595-READ        PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-OUTA             PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-OUTB             PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-OUTC             PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-TOT              PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-BE               PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-BTO              PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-DBS              PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WIS              PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-SBF              PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-NOTMATCH         PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-MATCH            PIC 9(7)  VALUE ZEROES.                  
           05  WS-F595-QTRFD           PIC X(1)  VALUE 'N'.                     
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2100-READ-BP13F730      THRU 2100-EXIT.                      
           PERFORM 2200-READ-BP13F595      THRU 2200-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL F730-NUM-NRIC1 = HIGH-VALUES                                 
              AND  F595-NUM-NRIC1 = HIGH-VALUES.                                
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13F595                                                 
                OUTPUT BP13OUTA                                                 
                       BP13OUTB                                                 
                       BP13OUTC.                                                
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2100-READ-BP13F730.                                                      
      ******************************************************************        
                                                                                
           READ BP13F730           AT   END                                     
                MOVE HIGH-VALUES   TO   F730-NUM-NRIC1                          
                MOVE 'Y'           TO   WS-EOF-F730                             
                GO                 TO   2100-EXIT.                              
                                                                                
           ADD  1                  TO   WS-BP13F730-READ.                       
                                                                                
       2100-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2200-READ-BP13F595.                                                      
      ******************************************************************        
                                                                                
           READ BP13F595           AT   END                                     
                MOVE HIGH-VALUES   TO   F595-NUM-NRIC1                          
                MOVE 'Y'           TO   WS-EOF-F595                             
                GO                 TO   2200-EXIT.                              
                                                                                
           ADD  1                  TO   WS-BP13F595-READ.                       
                                                                                
       2200-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE 0                                   TO WS-CNT-TOT               
                                                       WS-CNT-BE                
                                                       WS-CNT-BTO               
                                                       WS-CNT-DBS               
                                                       WS-CNT-WIS               
                                                       WS-CNT-SBF.              
                                                                                
           IF F730-NUM-NRIC1 = F595-NUM-NRIC1                                   
                                                                                
              PERFORM 3500-PROCESS-BP13F595       THRU 3500-EXIT                
                UNTIL F730-NUM-NRIC1 NOT = F595-NUM-NRIC1                       
                   OR WS-EOF-F595 = 'Y'                                         
                                                                                
              PERFORM 4200-WRITE-BP13OUTB         THRU 4200-EXIT                
              PERFORM 4300-WRITE-BP13OUTC         THRU 4300-EXIT                
              PERFORM 2100-READ-BP13F730          THRU 2100-EXIT                
           ELSE                                                                 
              IF F730-NUM-NRIC1 < F595-NUM-NRIC1                                
                 ADD 1                              TO WS-CNT-NOTMATCH          
                 PERFORM 4150-WRITE-BP13OUTA      THRU 4150-EXIT                
                 PERFORM 4200-WRITE-BP13OUTB      THRU 4200-EXIT                
                 PERFORM 4350-WRITE-BP13OUTC      THRU 4350-EXIT                
                 PERFORM 2100-READ-BP13F730       THRU 2100-EXIT                
              ELSE                                                              
                 PERFORM 2200-READ-BP13F595       THRU 2200-EXIT                
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3500-PROCESS-BP13F595.                                                   
      ******************************************************************        
                                                                                
           ADD 1                                    TO WS-CNT-TOT               
           EVALUATE F595-NUM-ALLO-CAT                                           
              WHEN 'BE '                                                        
                 ADD 1                              TO WS-CNT-BE                
              WHEN 'BTO'                                                        
                 ADD 1                              TO WS-CNT-BTO               
              WHEN 'SBF'                                                        
                 ADD 1                              TO WS-CNT-SBF               
              WHEN 'DBS'                                                        
                 ADD 1                              TO WS-CNT-DBS               
              WHEN 'WIA'                                                        
              WHEN 'WIB'                                                        
              WHEN 'WIC'                                                        
              WHEN 'WIS'                                                        
              WHEN 'WIH'                                                        
                 ADD 1                              TO WS-CNT-WIS               
           END-EVALUATE.                                                        
                                                                                
           IF F595-DTE-BALLOT > WS-F595-BALDATE                                 
              MOVE F595-NUM-ALLO-CAT            TO   WS-F595-MODE               
              MOVE F595-DTE-BALLOT              TO   WS-F595-BALDATE            
              MOVE F595-CDE-FLAT-TYPE           TO   WS-F595-FT                 
              MOVE F595-CDE-HOUSEHOLD           TO   WS-F595-HH                 
              MOVE F595-CDE-NT1                 TO   WS-F595-NT                 
           END-IF.                                                              
                                                                                
           PERFORM 4100-WRITE-BP13OUTA            THRU 4100-EXIT                
           PERFORM 2200-READ-BP13F595             THRU 2200-EXIT.               
                                                                                
       3500-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       4100-WRITE-BP13OUTA.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES               TO BP13OUTA-REC.                           
           INITIALIZE                   BP13OUTA-REC.                           
                                                                                
           MOVE F730-NUM-REGN        TO OUTA-NUM-REGN.                          
           MOVE F730-NUM-NRIC1       TO OUTA-NUM-NRIC.                          
           MOVE F730-CDE-FLAT-TYPE   TO OUTA-NUM-FTYPE.                         
           MOVE F730-CDE-HOUSEHOLD   TO OUTA-NUM-HH.                            
           MOVE F730-CDE-NT1         TO OUTA-NUM-NT.                            
           MOVE F730-NUM-ALLO-CAT    TO OUTA-NUM-ALLOCAT.                       
           MOVE F595-CDE-NT1         TO OUTA-CAN-NT.                            
           MOVE F595-CDE-FLAT-TYPE   TO OUTA-CDE-FT                             
           MOVE F595-CDE-HOUSEHOLD   TO OUTA-CDE-HH                             
           MOVE F595-DTE-BALLOT      TO OUTA-DTE-BALLOT                         
           MOVE F595-NUM-ALLO-CAT    TO OUTA-NUM-MODE                           
           MOVE 'A'                  TO OUTA-TITLE-TYPE.                        
                                                                                
           WRITE BP13OUTA-REC.                                                  
           ADD 1                         TO WS-CNT-OUTA.                        
                                                                                
       4100-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       4150-WRITE-BP13OUTA.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES               TO BP13OUTA-REC.                           
           INITIALIZE                   BP13OUTA-REC.                           
                                                                                
           MOVE F730-NUM-REGN        TO OUTA-NUM-REGN.                          
           MOVE F730-NUM-NRIC1       TO OUTA-NUM-NRIC.                          
           MOVE F730-CDE-FLAT-TYPE   TO OUTA-NUM-FTYPE                          
           MOVE F730-CDE-HOUSEHOLD   TO OUTA-NUM-HH.                            
           MOVE F730-CDE-NT1         TO OUTA-NUM-NT.                            
           MOVE F730-NUM-ALLO-CAT    TO OUTA-NUM-ALLOCAT.                       
           MOVE F730-CDE-FLAT-TYPE   TO OUTA-CDE-FT                             
           MOVE F730-CDE-HOUSEHOLD   TO OUTA-CDE-HH                             
           MOVE SPACES               TO OUTA-CAN-NT                             
                                        OUTA-DTE-BALLOT                         
                                        OUTA-NUM-MODE                           
           MOVE 'Y'                  TO OUTA-CDE-NEWAPP.                        
           MOVE 'A'                  TO OUTA-TITLE-TYPE.                        
                                                                                
           WRITE BP13OUTA-REC.                                                  
           ADD 1                         TO WS-CNT-OUTA.                        
                                                                                
       4150-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       4200-WRITE-BP13OUTB.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                   TO BP13OUTB-REC.                       
           INITIALIZE                       BP13OUTB-REC.                       
                                                                                
           MOVE F730-NUM-REGN            TO OUTB-NUM-REGN.                      
           MOVE F730-NUM-NRIC1           TO OUTB-NUM-NRIC.                      
           MOVE F730-CDE-NT1             TO OUTB-NT.                            
           MOVE F730-CDE-FLAT-TYPE       TO OUTB-NUM-FT.                        
           MOVE F730-NUM-ALLO-CAT        TO OUTB-NUM-ALLOCAT.                   
           MOVE WS-CNT-TOT               TO OUTB-TOT-CNT.                       
           MOVE WS-CNT-BE                TO OUTB-BE-CNT.                        
           MOVE WS-CNT-BTO               TO OUTB-BTO-CNT.                       
           MOVE WS-CNT-SBF               TO OUTB-SBF-CNT.                       
           MOVE WS-CNT-DBS               TO OUTB-DBS-CNT.                       
           MOVE WS-CNT-WIS               TO OUTB-WIS-CNT.                       
                                                                                
           WRITE BP13OUTB-REC.                                                  
           ADD 1                         TO WS-CNT-OUTB.                        
                                                                                
       4200-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       4300-WRITE-BP13OUTC.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                   TO BP13OUTC-REC.                       
           INITIALIZE                       BP13OUTC-REC.                       
                                                                                
           MOVE F730-NUM-REGN            TO OUTC-NUM-REGN.                      
           MOVE F730-NUM-NRIC1           TO OUTC-NUM-NRIC.                      
           MOVE F730-CDE-FLAT-TYPE       TO OUTC-CDE-FLAT-TYPE.                 
           MOVE F730-CDE-HOUSEHOLD       TO OUTC-CDE-HOUSEHOLD.                 
           MOVE F730-NUM-ALLO-CAT        TO OUTC-NUM-ALLOCAT                    
           MOVE F730-CDE-NT1             TO OUTC-NUM-NEW-TOWN.                  
           MOVE WS-F595-NT               TO OUTC-CAN-NT.                        
           MOVE WS-F595-FT               TO OUTC-CDE-FT.                        
           MOVE WS-F595-HH               TO OUTC-CDE-HH.                        
           MOVE WS-F595-MODE             TO OUTC-NUM-MODE.                      
           MOVE WS-F595-BALDATE          TO OUTC-DTE-BALLOT.                    
           MOVE 'C'                      TO OUTC-TITLE-TYPE.                    
                                                                                
           MOVE SPACES                   TO WS-F595-NT                          
                                            WS-F595-FT                          
                                            WS-F595-HH                          
                                            WS-F595-MODE                        
                                            WS-F595-BALDATE                     
                                                                                
           WRITE BP13OUTC-REC.                                                  
           ADD 1                         TO WS-CNT-OUTC.                        
                                                                                
       4300-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       4350-WRITE-BP13OUTC.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                   TO BP13OUTC-REC.                       
           INITIALIZE                       BP13OUTC-REC.                       
                                                                                
           MOVE F730-NUM-REGN            TO OUTC-NUM-REGN.                      
           MOVE F730-NUM-NRIC1           TO OUTC-NUM-NRIC.                      
           MOVE F730-CDE-FLAT-TYPE       TO OUTC-CDE-FLAT-TYPE.                 
           MOVE F730-CDE-HOUSEHOLD       TO OUTC-CDE-HOUSEHOLD.                 
           MOVE F730-NUM-ALLO-CAT        TO OUTC-NUM-ALLOCAT                    
           MOVE F730-CDE-NT1             TO OUTC-NUM-NEW-TOWN                   
           MOVE F730-CDE-FLAT-TYPE       TO OUTC-CDE-FT                         
           MOVE F730-CDE-HOUSEHOLD       TO OUTC-CDE-HH                         
           MOVE SPACES                   TO OUTC-NUM-MODE                       
                                            OUTC-DTE-BALLOT                     
                                            OUTC-CAN-NT.                        
           MOVE 'Y'                      TO OUTC-CDE-NEW.                       
           MOVE 'C'                      TO OUTC-TITLE-TYPE.                    
                                                                                
           WRITE BP13OUTC-REC.                                                  
           ADD 1                         TO WS-CNT-OUTC.                        
                                                                                
       4350-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CY05'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY 'NO OF BP13F730 RECORDS READ................. : '            
                    WS-BP13F730-READ.                                           
           DISPLAY 'NO OF BP13F595 RECORDS READ................. : '            
                    WS-BP13F595-READ.                                           
           DISPLAY 'NO OF RECORDS MATCH(FOUND IN F730 AND F595)  : '            
                    WS-CNT-MATCH.                                               
           DISPLAY 'NO OF RECORDS NOT MATCH '                                   
           DISPLAY '(RECORDS IN F730 NOT FOUND IN F595)..........: '            
                    WS-CNT-NOTMATCH.                                            
           DISPLAY 'NO OF BP13OUTA RECORDS WRITTEN ............. : '            
                    WS-CNT-OUTA.                                                
           DISPLAY 'NO OF BP13OUTB RECORDS WRITTEN ............. : '            
                    WS-CNT-OUTB.                                                
           DISPLAY 'NO OF BP13OUTC RECORDS WRITTEN ............. : '            
                    WS-CNT-OUTC.                                                
                                                                                
           CLOSE BP13F730                                                       
                 BP13F595                                                       
                 BP13OUTA                                                       
                 BP13OUTB                                                       
                 BP13OUTC.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
