       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C802.                                                 
       AUTHOR.        LSB.                                                      
       DATE-WRITTEN.  20/07/94.                                                 
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : CHECK PARENT/CHILD RELATIONSHIP IN    *                
      *                  RDB                                   *                
      *                                                        *                
      *    INPUT FILE  : BP13F558                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY   DATE     DETAILS                          *                
      * ------  --   ----     -------                          *                
      * N940014 YCH  111094   CHANGE OF KEY FIELD              *                
      * BP130630 NOR 04021999 CHGE F715 F716 EXPAND TO 350     *                
      * BP130833)YCH 13102000 ENHANCEMEMNT IN P/C              *                
      * BP130919)YCH 13102000  IMPL AS BP130833                *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F716 ASSIGN TO BP13F716.                                  
           SELECT P13F716A ASSIGN TO P13F716A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F716                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 350 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F716.                                                           
                                                                                
       FD  P13F716A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 350 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F716A-REC                   PIC X(350).                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F716-READ                PIC 9(8)    VALUE ZEROS.              
           05 WS-OUTPUT                   PIC 9(8)    VALUE ZEROS.              
           05 WS-INPUT-HH                 PIC 9(8)    VALUE ZEROS.              
           05 WS-RECORD-BYPASS            PIC 9(8)    VALUE ZEROS.              
           05 WS-HH-BYPASS                PIC 9(8)    VALUE ZEROS.              
           05 WS-ADMIN-HH                 PIC 9(8)    VALUE 0.                  
           05 WS-ADMIN-OCCUP              PIC 9(8)    VALUE 0.                  
           05 WS-OUTPUT-HH                PIC 9(8)    VALUE 0.                  
           05 WS-OCCUP-STORED             PIC 9(2)    VALUE 0.                  
                                                                                
       01  WS-PREV-NUM-SCH-ACCNT          PIC X(9)    VALUE SPACES.             
       01  WS-PREV-NUM-REGN.                                                    
           05  WS-PREV-NUM-REF            PIC X(6)    VALUE SPACES.             
           05  FILLER                     PIC X(2)    VALUE SPACES.             
                                                                                
       01  WS-FILE-POINTER.                                                     
           05 WS-F716-SUB                 PIC 9(2)    VALUE 0.                  
                                                                                
       01  WS-INDICATORS.                                                       
           05 WS-WHO-AS-LESSEE            PIC X(1)    VALUE SPACES.             
           05 WS-ADMIN-FND                PIC X(1)    VALUE 'N'.                
                                                                                
       01  WS-EOF-F716                    PIC X.                                
           88 EOF-F716                    VALUE 'Y'.                            
                                                                                
       COPY WSF716.                                                             
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       000-MAIN-ROUTINE.                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 300-READ-F716      THRU 300-EXIT.                            
           PERFORM 400-PROCESS-RTN    THRU 400-EXIT                             
             UNTIL EOF-F716.                                                    
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
                                                                                
           OPEN INPUT  BP13F716                                                 
                OUTPUT P13F716A.                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-READ-F716.                                                           
                                                                                
           READ BP13F716 AT END                                                 
                MOVE HIGH-VALUE TO F716-NUM-REGN                                
                MOVE 'Y'        TO WS-EOF-F716                                  
                GO TO 300-EXIT.                                                 
                                                                                
           ADD  1 TO WS-F716-READ.                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
      *                                                                         
       400-PROCESS-RTN.                                                         
           MOVE F716-NUM-REGN      TO WS-PREV-NUM-REGN.                         
                                                                                
           PERFORM 500-BROWSE-F716 THRU 500-EXIT                                
            UNTIL  F716-NUM-REGN NOT = WS-PREV-NUM-REGN                         
              OR   EOF-F716.                                                    
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-BROWSE-F716.                                                         
           MOVE SPACES TO WS-WHO-AS-LESSEE.                                     
           MOVE SPACES TO WS-F716-HSEHOLD.                                      
           MOVE 'N'    TO WS-ADMIN-FND.                                         
           MOVE 1      TO WS-F716-SUB.                                          
           ADD  1      TO WS-INPUT-HH.                                          
                                                                                
           MOVE ZEROES TO WS-OCCUP-STORED.                                      
           MOVE F716-NUM-SCH-ACCNT TO WS-PREV-NUM-SCH-ACCNT.                    
           PERFORM 550-STORE-F716-BY-SCHACC THRU 550-EXIT                       
             UNTIL F716-NUM-SCH-ACCNT NOT = WS-PREV-NUM-SCH-ACCNT               
              OR   EOF-F716.                                                    
                                                                                
      *                                                                         
           IF WS-ADMIN-FND = 'Y'                                                
              ADD 1               TO WS-ADMIN-HH                                
              ADD WS-OCCUP-STORED TO WS-ADMIN-OCCUP                             
              MOVE SPACES         TO WS-F716-HSEHOLD                            
              GO TO 500-EXIT.                                                   
                                                                                
               ADD 1 TO WS-OUTPUT-HH.                                           
               PERFORM 600-WRITE-OUTPUT THRU 600-EXIT                           
                       VARYING WS-F716-SUB FROM 1 BY 1                          
                       UNTIL  WS-F716-SUB > WS-OCCUP-STORED.                    
                                                                                
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------------------------------------------*                  
      *  CDE-RELATIONSHIP                                    *                  
      *        '02' = FATHER     '12' = MOTHER               *                  
      *        '19' = 2ND MOTHER                             *                  
      *        '22' = F-IN-LAW   '32' = M-IN-LAW             *                  
      *        '41' = SON/DAU    '42' = SON/DAU-IN-LAW       *                  
      *------------------------------------------------------*                  
      *   WS-WHO-AS-LESSEE 'C' = CHILD AS LESSEE                                
      *                    'P' = PARENT AS LESSEE                               
      *   CDE-PARENT-CHILD 'P' = PARENT AS OCCUPIER IN RDB                      
      *                    'C' = SON/DAU AS OCCUPIER IN RDB                     
      *                    'L' = SON/DAU-IN-LAW AS OCCUPIER IN RDB              
      *   CDE-OCCUPIER     '4' = TRUST                                          
      *                    '5' = O-TRUST                                        
      *                    '6' = S-TRUST                                        
      *                    '8' = P-TRUST                                        
      *------------------------------------------------------*                  
       550-STORE-F716-BY-SCHACC.                                                
                                                                                
           IF F716-CDE-OCCUPIER = '4' OR '5' OR '6' OR '8'                      
              MOVE 'Y' TO WS-ADMIN-FND.                                         
                                                                                
           IF F716-CDE-OCCUPIER = '1'                                           
               IF F716-CDE-RELATIONSHIP = '02' OR '12' OR '19' OR               
                                           '22' OR '32'                         
                  MOVE 'C' TO WS-WHO-AS-LESSEE                                  
                  MOVE 'P' TO F716-CDE-PARENT-CHILD                             
               ELSE                                                             
                  IF F716-CDE-RELATIONSHIP = '41'                               
                     MOVE 'P' TO WS-WHO-AS-LESSEE                               
                     MOVE 'C' TO F716-CDE-PARENT-CHILD                          
                  ELSE                                                          
                     IF F716-CDE-RELATIONSHIP = '42'                            
                        MOVE 'P' TO WS-WHO-AS-LESSEE                            
                        MOVE 'L' TO F716-CDE-PARENT-CHILD                       
                     ELSE                                                       
                        NEXT SENTENCE                                           
           ELSE                                                                 
               NEXT SENTENCE.                                                   
                                                                                
           MOVE BP13F716-REC TO WS-F716-REC (WS-F716-SUB).                      
           ADD 1 TO WS-F716-SUB.                                                
           ADD 1 TO WS-OCCUP-STORED.                                            
           PERFORM 300-READ-F716 THRU 300-EXIT.                                 
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
      **--------------------------------------------------**                    
      **  IF LESSEE IS A CHILD THAN                                             
      **     1.  BROTHER OF LESSEE TAG 'C'                                      
      **     2.  BRO/SIS-LAW OF LESSEE TAG 'L'                                  
      **  IF LESSEE IS A CHILD AND PARENT IS NOT CO-LESSEE                      
      **     TAG AS 'E' IN WS-F716-CDE-PARENT-CHILD                             
      **--------------------------------------------------**                    
      **                                                                        
       600-WRITE-OUTPUT.                                                        
                                                                                
           IF WS-WHO-AS-LESSEE = 'C'                                            
              IF WS-F716-CDE-RELATIONSHIP (WS-F716-SUB) = '00'                  
                 MOVE 'C' TO WS-F716-CDE-PARENT-CHILD (WS-F716-SUB)             
              ELSE                                                              
              IF WS-F716-CDE-RELATIONSHIP (WS-F716-SUB) = '06' AND              
                 WS-F716-CDE-OCCUPIER (WS-F716-SUB) = '1'                       
                 MOVE 'B' TO WS-F716-CDE-PARENT-CHILD (WS-F716-SUB)             
              ELSE                                                              
              IF WS-F716-CDE-RELATIONSHIP (WS-F716-SUB) = '20'                  
                 MOVE 'S' TO WS-F716-CDE-PARENT-CHILD (WS-F716-SUB)             
              ELSE                                                              
              IF WS-F716-CDE-RELATIONSHIP (WS-F716-SUB) = '07' OR '26'          
                                                      OR '27'                   
                 MOVE 'L' TO WS-F716-CDE-PARENT-CHILD (WS-F716-SUB)             
              ELSE                                                              
                 NEXT SENTENCE                                                  
           ELSE                                                                 
              IF WS-WHO-AS-LESSEE = 'P'                                         
                 IF WS-F716-CDE-RELATIONSHIP (WS-F716-SUB) = '00'               
                     MOVE 'P' TO WS-F716-CDE-PARENT-CHILD (WS-F716-SUB)         
                 ELSE                                                           
                 IF WS-F716-CDE-RELATIONSHIP (WS-F716-SUB) = '42'               
                     MOVE 'L' TO WS-F716-CDE-PARENT-CHILD (WS-F716-SUB)         
                 ELSE                                                           
                     NEXT SENTENCE                                              
              ELSE                                                              
                 IF WS-F716-CDE-OCCUPIER (WS-F716-SUB) = '0' OR '1'             
                    MOVE 'Z' TO WS-F716-CDE-PARENT-CHILD (WS-F716-SUB).         
                                                                                
           MOVE WS-F716-REC (WS-F716-SUB) TO P13F716A-REC.                      
           WRITE P13F716A-REC.                                                  
           ADD 1 TO WS-OUTPUT.                                                  
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       999-CLOSE-FILES.                                                         
           CLOSE BP13F716                                                       
                 P13F716A.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C802 ================*'.              
           DISPLAY '1.  NO. OF F716 RECORDS READ    = ' WS-F716-READ.           
           DISPLAY '2.  NO. OF F716 HSEHOLD READ    = ' WS-INPUT-HH.            
           DISPLAY ' '.                                                         
           DISPLAY '3.  NO. OF F716 RECORDS OUTPUT  = ' WS-OUTPUT.              
           DISPLAY '4.  NO. OF F716 HSEHOLD OUTPUT  = ' WS-OUTPUT-HH.           
           DISPLAY '5.  NO. OF F716 HSEHOLD BYPASS  = ' WS-HH-BYPASS.           
           DISPLAY '6.  NO. OF F716 RECORDS BYPASS  = ' WS-RECORD-BYPASS        
           DISPLAY '7.  NO. OF F716 ADMIN/TRUST HH  = ' WS-ADMIN-HH.            
           DISPLAY '8.  NO. OF F716 ADMIN/TRUST REC = ' WS-ADMIN-OCCUP.         
           DISPLAY ' '.                                                         
           DISPLAY '   1) = 3) + 6) + 8)'.                                      
           DISPLAY '   2) = 4) + 5) + 7)'.                                      
           DISPLAY ' '.                                                         
           DISPLAY ' '.                                                         
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
                                                                                
