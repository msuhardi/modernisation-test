      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
                                                                                
       PROGRAM-ID.    BP13C24A.                                                 
       AUTHOR.        ROSALYN M. LUMANGLAS.                                     
       DATE-WRITTEN.  01/09/93.                                                 
                                                                                
      *===============================================================*         
      *                                                               *         
      *    SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                 *         
      *                                                               *         
      *    SYSTEM ID   :  BP13                                        *         
      *                                                               *         
      *    OBJECTIVE   :  THIS IS THE PROGRAM TO PRINT THE CHECK      *         
      *                   LIST OF APPOINTMENT LETTERS PRINTED.        *         
      *                                                               *         
      *    INPUT FILES :  1. BP13K200 - BK APPOINTMENT FILE           *         
      *                   2. BP13K800 - SOC APPLN MASTER FILE         *         
      *                   3. BP13K820 - SOC OCCUPIER FILE             *         
      *                   4. BP13K830 - CORR ADDRESS FILE             *         
      *                   5. BP13KB20 -                               *         
      *                                                               *         
      *    OUTPUT LIST :  1. BP13L24A - FINALIZED NAME LIST           *         
      *                                                               *         
      *                                                               *         
      *    CHGE-NO  BY    DATE   DESCRIPTION                          *         
      *    -------  ---  ------  ---------------------------------    *         
      *    BP13XXXX ED1  161106  NEW PROGRAM BASED ON BP13C246        *         
      *                          THE ONLY CHANGE MADE IS THE FF:      *         
      *                          ADD HP AND EMAIL IN REPORT           *         
      *                          AND CHANGE CAT CODES FROM            *         
      *                          1 TO 'M' ,   2 TO 'C'                *         
      *                          3 TO 'I' AND 4 TO 'O'                *         
      *    BP133244 RB12 120208  ADD APPLICANT 3 AND 4 IN REPORT      *         
      *    BP133544 ESA1 120509  UPDATE PHONE NUMBER IN REPORT        *         
      *    BP134400 ZDD1 220311  ADD HH TYPE AND LOAN TAG IN REPORT   *         
      *===============================================================*         
                                                                                
                                                                                
      ***********************                                                   
       ENVIRONMENT DIVISION.                                                    
      ***********************                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F200 ASSIGN       TO BP13F200.                            
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS WS-K800-STATUS.                      
           SELECT BP13K820 ASSIGN       TO BP13K820                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K820-KEY-FLD                         
                           FILE STATUS  IS WS-K820-STATUS.                      
           SELECT BP13K830 ASSIGN       TO BP13K830                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K830-KEY-FLD                         
                           ALTERNATE RECORD KEY IS K830-NUM-REGN                
                           FILE STATUS  IS WS-K830-STATUS.                      
           SELECT BP13KB20 ASSIGN       TO BP13KB20                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS KB20-KEY-FLD                         
                           FILE STATUS  IS WS-KB20-STATUS.                      
           SELECT BP13L24A ASSIGN       TO BP13L24A.                            
                                                                                
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F200                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13F200.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   BP13KB20                                                            
            RECORD CONTAINS 3300 CHARACTERS.                                    
       COPY BP13KB20.                                                           
                                                                                
       FD   BP13L24A                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   PRINT-REC                       PIC X(132).                         
                                                                                
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-INDICATORS.                                                       
           05  FILLER                       PIC X(10)  VALUE                    
               'INDICATORS'.                                                    
           05  WS-K200-STATUS               PIC 99     VALUE ZEROS.             
           05  WS-K800-STATUS               PIC 99     VALUE ZEROS.             
           05  WS-K820-STATUS               PIC 99     VALUE ZEROS.             
           05  WS-K830-STATUS               PIC 99     VALUE ZEROS.             
           05  WS-KB20-STATUS               PIC 99     VALUE ZEROS.             
           05  WS-F200-EOF                  PIC X      VALUE 'N'.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  FILLER                       PIC X(08)  VALUE                    
               'COUNTERS'.                                                      
           05  WS-NUMBER                    PIC 99     VALUE 99.                
           05  WS-SERIAL                    PIC 9(4)   VALUE ZEROS.             
           05  WS-PAGE-CNT                  PIC 9(4)   VALUE ZEROS.             
                                                                                
       01  WS-SAVE-AREA.                                                        
           05  FILLER                       PIC X(09)  VALUE                    
               'SAVE AREA'.                                                     
           05  WS-NAME                      PIC X(22)  VALUE SPACES.            
           05  WS-PREV-FLAT-TYPE            PIC X(2)   VALUE SPACES.            
           05  WS-PREV-NT-ZONE-SW           PIC X(1)   VALUE SPACES.            
           05  WS-PREV-DTE-ALLOC            PIC X(8)   VALUE SPACES.            
           05  WS-PREV-NT-ZONE              PIC X(3)   VALUE SPACES.            
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  FILLER                       PIC X(09)  VALUE                    
               'DATE VARS'.                                                     
           05  WS-CURR-DATE                 PIC X(8)   VALUE SPACES.            
           05  WS-ALLOCN-DATE.                                                  
               10  WS-ALLOCN-YY             PIC 9(4)   VALUE 0000.              
               10  WS-ALLOCN-MM             PIC 99     VALUE 00.                
               10  WS-ALLOCN-DD             PIC 99     VALUE 00.                
           05  WS-BKAPPT-DATE.                                                  
               10  WS-BKAPPT-YY             PIC 9(4)   VALUE 0000.              
               10  WS-BKAPPT-MM             PIC 99     VALUE 00.                
               10  WS-BKAPPT-DD             PIC 99     VALUE 00.                
           05  WS-EDITED-DATE.                                                  
               10  WS-EDIT-DD               PIC 99     VALUE 00.                
               10  FILLER                   PIC X      VALUE '/'.               
               10  WS-EDIT-MM               PIC 99     VALUE 00.                
               10  FILLER                   PIC X      VALUE '/'.               
               10  WS-EDIT-YY               PIC 9(4)   VALUE 0000.              
                                                                                
           05  WS-TIME.                                                         
               10  WS-HH                    PIC 99.                             
               10  WS-MM                    PIC 99.                             
                                                                                
       COPY SERSCH.                                                             
                                                                                
      *---------------------------------------------------------------*         
      *        P R I N T     L A Y O U T                              *         
      *---------------------------------------------------------------*         
       01  L24A-HEADER-1.                                                       
           05  FILLER                       PIC X      VALUE SPACE.             
           05  FILLER                       PIC X(7)   VALUE 'PAGE :'.          
           05  L24A-PAGE                    PIC ZZZ9.                           
                                                                                
       01  L24A-HEADER-2.                                                       
           05  FILLER                       PIC X(42)  VALUE SPACES.            
           05  FILLER                       PIC X(41)  VALUE                    
               'CHECK LIST OF APPOINTMENT LETTERS PRINTED'.                     
           05  FILLER                       PIC X(24)  VALUE SPACES.            
           05  FILLER                       PIC X(21)  VALUE                    
               'LISTING-ID : BP13L24A'.                                         
                                                                                
       01  L24A-HEADER-3.                                                       
           05  FILLER                       PIC X(43)  VALUE SPACES.            
           05  FILLER                       PIC X(19)  VALUE SPACES.            
           05  FILLER                       PIC X(42)  VALUE SPACES.            
           05  FILLER                       PIC X(16)  VALUE                    
               '   RUNDATE :    '.                                              
           05  CONTROL-DATE                 PIC X(10)  VALUE SPACES.            
                                                                                
       01  L24A-LINE1.                                                          
           05  FILLER                       PIC X(21)  VALUE                    
               'Q/NO REGN-NO  ALSCH  '.                                         
           05  FILLER                       PIC X(25)  VALUE                    
               'DATE/TIME OF APPOINTMENT '.                                     
           05  FILLER                       PIC X(32)  VALUE                    
               'CAT  HH  LT   NAME'.                                            
           05  FILLER                       PIC X(51)  VALUE                    
               '      ADDRESS      '.                                           
                                                                                
       01  L24A-LINE2.                                                          
           05  FILLER                       PIC X(23)  VALUE                    
               'PHONE1   PHONE2   EMAIL'.                                       
                                                                                
       01  L24A-DATA1.                                                          
           05  L24A-SERIAL                  PIC X(5)   VALUE SPACES.            
           05  FILLER                       PIC X(1)   VALUE SPACES.            
           05  L24A-REGNO                   PIC X(8)   VALUE SPACES.            
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  L24A-ALLOC-SCH               PIC X(3)   VALUE SPACES.            
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  L24A-DTE-BK-APPT             PIC X(10)  VALUE SPACES.            
           05  FILLER                       PIC X(1)   VALUE SPACES.            
           05  L24A-APPT-HH                 PIC Z9.                             
           05  L24A-DOT                     PIC X(1)   VALUE '.'.               
           05  L24A-APPT-MM                 PIC 99.                             
           05  FILLER                       PIC X(1)   VALUE SPACES.            
           05  L24A-AM-PM                   PIC X(2).                           
           05  FILLER                       PIC X(7)   VALUE SPACES.            
           05  L24A-CAT                     PIC X(1)   VALUE SPACE.             
           05  FILLER                       PIC X(03)  VALUE SPACES.            
           05  L24A-HH                      PIC X(1)   VALUE SPACE.             
           05  FILLER                       PIC X(03)  VALUE SPACES.            
           05  L24A-LT                      PIC X(1)   VALUE SPACE.             
           05  FILLER                       PIC X(04)  VALUE SPACES.            
           05  L24A-APPL-NAME               PIC X(22)  VALUE SPACES.            
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  L24A-BLK-NO                  PIC X(10)  VALUE SPACES.            
           05  FILLER                       PIC X(1)   VALUE SPACE.             
           05  L24A-NUM-FLAT.                                                   
               10  L24A-CHAR                PIC X(1)   VALUE SPACE.             
               10  L24A-LEVEL               PIC X(2)   VALUE SPACES.            
               10  L24A-DASH                PIC X(1)   VALUE SPACE.             
               10  L24A-HSE-MAIN            PIC X(4)   VALUE SPACES.            
               10  L24A-HSE-SUB             PIC X(1)   VALUE SPACE.             
           05  FILLER                       PIC X(1)   VALUE SPACE.             
           05  L24A-STREET-NAME             PIC X(20)  VALUE SPACES.            
                                                                                
       01  L24A-DATA2.                                                          
           05  L24A-PHONE1                  PIC X(08)  VALUE SPACES.            
           05  FILLER                       PIC X      VALUE SPACE.             
           05  L24A-PHONE2                  PIC X(08)  VALUE SPACES.            
           05  FILLER                       PIC X      VALUE SPACE.             
           05  L24A-EMAIL                   PIC X(41)  VALUE SPACES.            
           05  FILLER                       PIC X      VALUE SPACE.             
           05  L24A-COAPPL-NAME             PIC X(22)  VALUE SPACES.            
           05  FILLER                       PIC X(2)   VALUE SPACES.            
           05  L24A-POSTAL-CD               PIC X(9)   VALUE SPACES.            
                                                                                
       01  L24A-DATA3.                                                          
           05  FILLER                       PIC X(60)  VALUE SPACES.            
           05  L24A-COAPPL-NAME2            PIC X(22)  VALUE SPACES.            
                                                                                
       01  L24A-DATA4.                                                          
           05  FILLER                       PIC X(60)  VALUE SPACES.            
           05  L24A-COAPPL-NAME3            PIC X(22)  VALUE SPACES.            
                                                                                
       01  L24A-DATA5.                                                          
           05  FILLER                       PIC X(09)  VALUE SPACES.            
           05  L24A-PHONE3                  PIC X(08)  VALUE SPACES.            
           05  FILLER                       PIC X(43)  VALUE SPACES.            
           05  L24A-PHONE3-NAME             PIC X(22)  VALUE SPACES.            
                                                                                
       01  L24A-LINE                        PIC X(132) VALUE ALL '-'.           
                                                                                
      *********************                                                     
       PROCEDURE DIVISION.                                                      
      *********************                                                     
                                                                                
                                                                                
      ******************************************************************        
       000-MAIN-ROUTINE.                                                        
      ******************************************************************        
                                                                                
           PERFORM 100-OPEN-FILES-RTN      THRU   100-EXIT.                     
           PERFORM 300-PROCESS-F200        THRU   300-EXIT.                     
           PERFORM 900-CLOSE-FILES-RTN     THRU   900-EXIT.                     
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       100-OPEN-FILES-RTN.                                                      
      ******************************************************************        
           OPEN INPUT  BP13F200                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13K830                                                 
                       BP13KB20                                                 
                OUTPUT BP13L24A.                                                
                                                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K800 = ' WS-K800-STATUS              
              MOVE    WS-K800-STATUS  TO RETURN-CODE                            
              PERFORM 900-CLOSE-FILES-RTN.                                      
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K820 = ' WS-K820-STATUS              
              MOVE    WS-K820-STATUS  TO RETURN-CODE                            
              PERFORM 900-CLOSE-FILES-RTN.                                      
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K830 = ' WS-K830-STATUS              
              MOVE    WS-K830-STATUS  TO RETURN-CODE                            
              PERFORM 900-CLOSE-FILES-RTN.                                      
                                                                                
           IF WS-KB20-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13KB20 = ' WS-KB20-STATUS              
              MOVE    WS-KB20-STATUS  TO RETURN-CODE                            
              PERFORM 900-CLOSE-FILES-RTN.                                      
                                                                                
           MOVE 99  TO WS-NUMBER.                                               
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-F200.                                                        
      ******************************************************************        
                                                                                
           MOVE    'N'                     TO    WS-F200-EOF.                   
                                                                                
           MOVE    ZEROS                   TO    WS-SERIAL.                     
                                                                                
           PERFORM 400-READ-F200           THRU  400-EXIT.                      
           PERFORM 500-READ-K800        THRU  500-EXIT                          
                   UNTIL WS-F200-EOF = 'Y'.                                     
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       400-READ-F200.                                                           
      ******************************************************************        
                                                                                
           READ BP13F200                                                        
              AT END MOVE 'Y'   TO WS-F200-EOF                                  
           END-READ.                                                            
                                                                                
           MOVE SPACES       TO L24A-DATA1.                                     
           MOVE '.'          TO L24A-DOT.                                       
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       500-READ-K800.                                                           
      ******************************************************************        
                                                                                
           MOVE F200-NUM-REGN  TO   K800-NUM-REGN.                              
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              PERFORM 550-READ-K820 THRU 550-EXIT                               
           ELSE                                                                 
           IF WS-K800-STATUS = 23                                               
              MOVE SPACES TO L24A-APPL-NAME                                     
                             L24A-COAPPL-NAME                                   
                             L24A-COAPPL-NAME2                                  
                             L24A-COAPPL-NAME3                                  
              DISPLAY 'RECORD NOT FOUND IN BP13K800 ' WS-K800-STATUS            
              DISPLAY '       K800-NUM-REGN = ' K800-NUM-REGN                   
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K800 = ' WS-K800-STATUS                
              DISPLAY '      K800-NUM-REGN    = ' K800-NUM-REGN                 
              MOVE    WS-K800-STATUS  TO  RETURN-CODE                           
              PERFORM 900-CLOSE-FILES-RTN.                                      
                                                                                
           PERFORM 600-READ-KB20      THRU 600-EXIT.                            
           PERFORM 700-GET-ADDRESS    THRU 700-EXIT.                            
           PERFORM 800-PROCESS-PRINT  THRU 800-EXIT.                            
           MOVE    F200-NUM-FLAT-TYPE      TO    WS-PREV-FLAT-TYPE.             
                                                                                
           PERFORM 400-READ-F200           THRU  400-EXIT.                      
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       550-READ-K820.                                                           
      ******************************************************************        
           MOVE    K800-NUM-REGN    TO    K820-NUM-REGN.                        
           MOVE    K800-NUM-NRIC1   TO    K820-NUM-NRIC.                        
           PERFORM 560-ACCESS-K820  THRU  560-EXIT.                             
           MOVE    WS-NAME          TO    L24A-APPL-NAME.                       
                                                                                
           IF K800-NUM-NRIC2(2:1)  NOT =  '#'    AND                            
              K800-NUM-NRIC2       NOT =  SPACES AND LOW-VALUES                 
              MOVE     K800-NUM-REGN   TO   K820-NUM-REGN                       
              MOVE     K800-NUM-NRIC2  TO   K820-NUM-NRIC                       
              PERFORM  560-ACCESS-K820 THRU 560-EXIT                            
              MOVE     WS-NAME         TO   L24A-COAPPL-NAME                    
           ELSE                                                                 
              MOVE     SPACES          TO   L24A-COAPPL-NAME                    
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3(2:1)  NOT =  '#'    AND                            
              K800-NUM-NRIC3       NOT =  SPACES AND LOW-VALUES                 
              MOVE     K800-NUM-REGN   TO   K820-NUM-REGN                       
              MOVE     K800-NUM-NRIC3  TO   K820-NUM-NRIC                       
              PERFORM  560-ACCESS-K820 THRU 560-EXIT                            
              MOVE     WS-NAME         TO   L24A-COAPPL-NAME2                   
           ELSE                                                                 
              MOVE     SPACES          TO   L24A-COAPPL-NAME2                   
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4(2:1)  NOT =  '#'    AND                            
              K800-NUM-NRIC4       NOT =  SPACES AND LOW-VALUES                 
              MOVE     K800-NUM-REGN   TO   K820-NUM-REGN                       
              MOVE     K800-NUM-NRIC4  TO   K820-NUM-NRIC                       
              PERFORM  560-ACCESS-K820 THRU 560-EXIT                            
              MOVE     WS-NAME         TO   L24A-COAPPL-NAME3                   
           ELSE                                                                 
              MOVE     SPACES          TO   L24A-COAPPL-NAME3                   
           END-IF.                                                              
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       560-ACCESS-K820.                                                         
      ******************************************************************        
                                                                                
           READ BP13K820 KEY IS K820-KEY-FLD.                                   
                                                                                
           IF WS-K820-STATUS = 00                                               
              MOVE    K820-NME-OCCP  TO WS-NAME                                 
           ELSE                                                                 
           IF WS-K820-STATUS = 23                                               
              MOVE    SPACES         TO WS-NAME                                 
              DISPLAY 'RECORD NOT FOUND IN K820 ' WS-K820-STATUS                
              DISPLAY '       K820-KEY-FLD = ' K820-KEY-FLD                     
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K820 = ' WS-K820-STATUS                
              DISPLAY '      K820-KEY-FLD     = ' K820-KEY-FLD                  
              MOVE    WS-K820-STATUS  TO RETURN-CODE                            
              PERFORM 900-CLOSE-FILES-RTN.                                      
                                                                                
       560-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       600-READ-KB20.                                                           
      ******************************************************************        
                                                                                
           MOVE F200-NUM-REGN  TO   KB20-KEY-FLD.                               
                                                                                
           READ BP13KB20 KEY IS KB20-KEY-FLD.                                   
                                                                                
           IF WS-KB20-STATUS = 00                                               
                                                                                
              IF KB20-NUM-APPLT1-HP-PAGER NOT = SPACES AND LOW-VALUES           
                 MOVE KB20-NUM-APPLT1-HP-PAGER TO L24A-PHONE1                   
              ELSE                                                              
                 MOVE KB20-NUM-APPLT1-PHONE    TO L24A-PHONE1                   
              END-IF                                                            
                                                                                
              IF KB20-NUM-APPLT2-HP-PAGER NOT = SPACES AND LOW-VALUES           
                 MOVE KB20-NUM-APPLT2-HP-PAGER TO L24A-PHONE2                   
                 MOVE SPACES                   TO L24A-PHONE3                   
                                                  L24A-PHONE3-NAME              
              ELSE                                                              
                 MOVE SPACES                   TO L24A-PHONE2                   
                 PERFORM 610-CHECK-PHONE3      THRU 610-EXIT                    
              END-IF                                                            
                                                                                
              MOVE  KB20-NUM-EMAIL           TO L24A-EMAIL                      
              MOVE  KB20-CDE-ALLOC-SCH       TO L24A-ALLOC-SCH                  
                                                                                
           ELSE                                                                 
             IF WS-KB20-STATUS = 23                                             
                MOVE  SPACES TO L24A-PHONE1                                     
                MOVE  SPACES TO L24A-PHONE2                                     
                MOVE  SPACES TO L24A-PHONE3                                     
                MOVE  SPACES TO L24A-PHONE3-NAME                                
                MOVE  SPACES TO L24A-EMAIL                                      
                MOVE  SPACES TO L24A-ALLOC-SCH                                  
                DISPLAY 'RECORD NOT FOUND IN KB20 ' WS-KB20-STATUS              
                DISPLAY '       KB20-KEY-FLD = ' KB20-KEY-FLD                   
             ELSE                                                               
                DISPLAY 'ERROR READING BP13KB20 = ' WS-KB20-STATUS              
                DISPLAY '      KB20-KEY-FLD     = ' KB20-KEY-FLD                
                MOVE    WS-KB20-STATUS  TO RETURN-CODE                          
                PERFORM 900-CLOSE-FILES-RTN                                     
             END-IF                                                             
                                                                                
           END-IF.                                                              
                                                                                
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       610-CHECK-PHONE3.                                                        
      ******************************************************************        
                                                                                
           IF ((KB20-NUM-APPLT1-PHONE NOT = SPACES AND LOW-VALUES)              
              AND (KB20-NUM-APPLT1-PHONE NOT = L24A-PHONE1))                    
              MOVE KB20-NUM-APPLT1-PHONE TO L24A-PHONE3                         
              MOVE KB20-NME-APPLT1       TO L24A-PHONE3-NAME                    
           ELSE                                                                 
              IF KB20-NUM-APPLT2-PHONE NOT = SPACES AND LOW-VALUES              
                 MOVE KB20-NUM-APPLT2-PHONE TO L24A-PHONE3                      
                 MOVE KB20-NME-APPLT2       TO L24A-PHONE3-NAME                 
              ELSE                                                              
                 IF KB20-NUM-APPLT3-HP-PAGER NOT = SPACES AND LOW-VALUES        
                    MOVE KB20-NUM-APPLT3-HP-PAGER TO L24A-PHONE3                
                    MOVE KB20-NME-APPLT3          TO L24A-PHONE3-NAME           
                 ELSE                                                           
                    IF KB20-NUM-APPLT4-HP-PAGER NOT = SPACES                    
                       AND LOW-VALUES                                           
                       MOVE KB20-NUM-APPLT4-HP-PAGER TO L24A-PHONE3             
                       MOVE KB20-NME-APPLT4         TO L24A-PHONE3-NAME         
                    ELSE                                                        
                       IF KB20-NUM-APPLT3-PHONE NOT = SPACES                    
                          AND LOW-VALUES                                        
                          MOVE KB20-NUM-APPLT3-PHONE TO L24A-PHONE3             
                          MOVE KB20-NME-APPLT3    TO L24A-PHONE3-NAME           
                       ELSE                                                     
                          IF KB20-NUM-APPLT4-PHONE NOT = SPACES                 
                             AND LOW-VALUES                                     
                             MOVE KB20-NUM-APPLT4-PHONE TO L24A-PHONE3          
                             MOVE KB20-NME-APPLT3   TO L24A-PHONE3-NAME         
                          ELSE                                                  
                             PERFORM 620-CHECK-OCCUPIER-HP                      
                                     THRU 620-EXIT                              
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
             END-IF                                                             
           END-IF.                                                              
                                                                                
       610-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       620-CHECK-OCCUPIER-HP.                                                   
      ******************************************************************        
                                                                                
           IF KB20-NUM-OCCUP1-HP-PAGER NOT = SPACES AND LOW-VALUES              
              MOVE KB20-NUM-OCCUP1-HP-PAGER TO L24A-PHONE3                      
              MOVE KB20-NME-OCCUP1          TO L24A-PHONE3-NAME                 
           ELSE                                                                 
              IF KB20-NUM-OCCUP2-HP-PAGER NOT = SPACES AND LOW-VALUES           
                 MOVE KB20-NUM-OCCUP2-HP-PAGER TO L24A-PHONE3                   
                 MOVE KB20-NME-OCCUP2          TO L24A-PHONE3-NAME              
              ELSE                                                              
                IF KB20-NUM-OCCUP3-HP-PAGER NOT = SPACES AND LOW-VALUES         
                   MOVE KB20-NUM-OCCUP3-HP-PAGER TO L24A-PHONE3                 
                   MOVE KB20-NME-OCCUP3          TO L24A-PHONE3-NAME            
                ELSE                                                            
                   IF KB20-NUM-OCCUP4-HP-PAGER NOT = SPACES                     
                      AND LOW-VALUES                                            
                      MOVE KB20-NUM-OCCUP4-HP-PAGER TO L24A-PHONE3              
                      MOVE KB20-NME-OCCUP4          TO L24A-PHONE3-NAME         
                   ELSE                                                         
                      IF KB20-NUM-OCCUP5-HP-PAGER NOT = SPACES                  
                         AND LOW-VALUES                                         
                         MOVE KB20-NUM-OCCUP5-HP-PAGER   TO L24A-PHONE3         
                         MOVE KB20-NME-OCCUP5       TO L24A-PHONE3-NAME         
                      ELSE                                                      
                         IF KB20-NUM-OCCUP6-HP-PAGER NOT = SPACES               
                           AND LOW-VALUES                                       
                           MOVE KB20-NUM-OCCUP6-HP-PAGER TO L24A-PHONE3         
                           MOVE KB20-NME-OCCUP6  TO L24A-PHONE3-NAME            
                         ELSE                                                   
                           IF KB20-NUM-OCCUP7-HP-PAGER NOT = SPACES             
                              AND LOW-VALUES                                    
                            MOVE KB20-NUM-OCCUP7-HP-PAGER TO L24A-PHONE3        
                            MOVE KB20-NME-OCCUP7 TO L24A-PHONE3-NAME            
                           ELSE                                                 
                              IF KB20-NUM-OCCUP8-HP-PAGER NOT = SPACES          
                                 AND LOW-VALUES                                 
                                 MOVE KB20-NUM-OCCUP8-HP-PAGER TO               
                                                         L24A-PHONE3            
                                 MOVE KB20-NME-OCCUP8        TO                 
                                                     L24A-PHONE3-NAME           
                              ELSE                                              
                                 PERFORM 630-CHECK-OCCUPIER-PHONE               
                                         THRU 630-EXIT                          
                              END-IF                                            
                           END-IF                                               
                         END-IF                                                 
                      END-IF                                                    
                   END-IF                                                       
                END-IF                                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       620-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       630-CHECK-OCCUPIER-PHONE.                                                
      ******************************************************************        
                                                                                
           IF KB20-NUM-OCCUP1-PHONE NOT = SPACES AND LOW-VALUES                 
              MOVE KB20-NUM-OCCUP1-PHONE    TO L24A-PHONE3                      
              MOVE KB20-NME-OCCUP1          TO L24A-PHONE3-NAME                 
           ELSE                                                                 
              IF KB20-NUM-OCCUP2-PHONE NOT = SPACES AND LOW-VALUES              
                 MOVE KB20-NUM-OCCUP2-PHONE TO L24A-PHONE3                      
                 MOVE KB20-NME-OCCUP2          TO L24A-PHONE3-NAME              
              ELSE                                                              
                IF KB20-NUM-OCCUP3-PHONE NOT = SPACES AND LOW-VALUES            
                   MOVE KB20-NUM-OCCUP3-PHONE TO L24A-PHONE3                    
                   MOVE KB20-NME-OCCUP3          TO L24A-PHONE3-NAME            
                ELSE                                                            
                   IF KB20-NUM-OCCUP4-PHONE NOT = SPACES                        
                      AND LOW-VALUES                                            
                      MOVE KB20-NUM-OCCUP4-PHONE TO L24A-PHONE3                 
                      MOVE KB20-NME-OCCUP4          TO L24A-PHONE3-NAME         
                   ELSE                                                         
                      IF KB20-NUM-OCCUP5-PHONE NOT = SPACES                     
                         AND LOW-VALUES                                         
                         MOVE KB20-NUM-OCCUP5-PHONE      TO L24A-PHONE3         
                         MOVE KB20-NME-OCCUP5       TO L24A-PHONE3-NAME         
                      ELSE                                                      
                         IF KB20-NUM-OCCUP6-PHONE NOT = SPACES                  
                           AND LOW-VALUES                                       
                           MOVE KB20-NUM-OCCUP6-PHONE TO L24A-PHONE3            
                           MOVE KB20-NME-OCCUP6  TO L24A-PHONE3-NAME            
                         ELSE                                                   
                            IF KB20-NUM-OCCUP7-PHONE NOT = SPACES               
                               AND LOW-VALUES                                   
                             MOVE KB20-NUM-OCCUP7-PHONE TO L24A-PHONE3          
                             MOVE KB20-NME-OCCUP7 TO L24A-PHONE3-NAME           
                            ELSE                                                
                              IF KB20-NUM-OCCUP8-PHONE NOT = SPACES             
                                 AND LOW-VALUES                                 
                                 MOVE KB20-NUM-OCCUP8-PHONE TO                  
                                                         L24A-PHONE3            
                                 MOVE KB20-NME-OCCUP8        TO                 
                                                     L24A-PHONE3-NAME           
                              ELSE                                              
                                 MOVE SPACES    TO   L24A-PHONE3                
                                                     L24A-PHONE3-NAME           
                              END-IF                                            
                            END-IF                                              
                         END-IF                                                 
                      END-IF                                                    
                   END-IF                                                       
                END-IF                                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       630-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       700-GET-ADDRESS.                                                         
      ******************************************************************        
           MOVE F200-NUM-REGN TO K830-NUM-REGN.                                 
                                                                                
           READ BP13K830 KEY  IS K830-NUM-REGN.                                 
                                                                                
           IF WS-K830-STATUS = 00 OR 02                                         
              IF K830-NUM-MAIL-ADDR   =  SPACES OR LOW-VALUES                   
                 IF K830-NUM-ADDR-TYPE = 'H'                                    
                    MOVE SPACES                 TO L24A-STREET-NAME             
                    MOVE K830-NUM-BLK           TO L24A-BLK-NO                  
                    MOVE K830-NUM-LVL-CHAR      TO L24A-CHAR                    
                    MOVE K830-NUM-LVL-NUM       TO L24A-LEVEL                   
                    MOVE K830-NUM-LVL-DASH      TO L24A-DASH                    
                    MOVE K830-NUM-HSEMAIN       TO L24A-HSE-MAIN                
                    MOVE K830-NUM-HSESUB        TO L24A-HSE-SUB                 
                    MOVE K830-NME-STREET-BLDG   TO L24A-STREET-NAME             
                    STRING 'S(' K830-NUM-POSTAL ')'                             
                           DELIMITED BY SIZE                                    
                           INTO L24A-POSTAL-CD                                  
                 ELSE                                                           
                    MOVE SPACES                 TO L24A-STREET-NAME             
                    MOVE K830-NUM-BLK           TO L24A-BLK-NO                  
                    MOVE K830-NUM-FLAT          TO L24A-NUM-FLAT                
                    MOVE K830-NME-STREET-BLDG   TO L24A-STREET-NAME             
                    STRING 'S(' K830-NUM-POSTAL ')'                             
                           DELIMITED BY SIZE                                    
                           INTO L24A-POSTAL-CD                                  
                 END-IF                                                         
              ELSE                                                              
                 IF K830-NUM-MAIL-ADDR-TYPE =  'H'                              
                    MOVE SPACES                 TO L24A-STREET-NAME             
                    MOVE K830-NUM-MAIL-BLK      TO L24A-BLK-NO                  
                    MOVE K830-NUM-MAIL-LVL-CHAR TO L24A-CHAR                    
                    MOVE K830-NUM-MAIL-LVL-NUM  TO L24A-LEVEL                   
                    MOVE K830-NUM-MAIL-LVL-DASH TO L24A-DASH                    
                    MOVE K830-NUM-MAIL-HSEMAIN  TO L24A-HSE-MAIN                
                    MOVE K830-NUM-MAIL-HSESUB   TO L24A-HSE-SUB                 
                    MOVE K830-NME-MAIL-STR-BLDG TO L24A-STREET-NAME             
                    STRING 'S(' K830-NUM-MAIL-POSTAL ')'                        
                           DELIMITED BY SIZE                                    
                           INTO L24A-POSTAL-CD                                  
                 ELSE                                                           
                    MOVE SPACES                 TO L24A-STREET-NAME             
                    MOVE K830-NUM-MAIL-BLK      TO L24A-BLK-NO                  
                    MOVE K830-NUM-MAIL-FLAT     TO L24A-NUM-FLAT                
                    MOVE K830-NME-MAIL-STR-BLDG TO L24A-STREET-NAME             
                    STRING 'S(' K830-NUM-MAIL-POSTAL ')'                        
                           DELIMITED BY SIZE                                    
                           INTO L24A-POSTAL-CD                                  
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-K830-STATUS = 23                                               
              MOVE SPACES    TO  L24A-BLK-NO     L24A-STREET-NAME               
                                 L24A-NUM-FLAT   L24A-POSTAL-CD                 
              DISPLAY 'RECORD NOT FOUND IN BP13K830 ' WS-K830-STATUS            
              DISPLAY '       K830-NUM-REGN  = ' K830-NUM-REGN.                 
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 02 AND 23                             
              DISPLAY 'ERROR READING BP13K830 = ' WS-K830-STATUS                
              DISPLAY '      K830-NUM-REGN    = ' K830-NUM-REGN                 
              MOVE    WS-K830-STATUS  TO  RETURN-CODE                           
              PERFORM 900-CLOSE-FILES-RTN.                                      
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       800-PROCESS-PRINT.                                                       
      ******************************************************************        
                                                                                
           IF (F200-NUM-FLAT-TYPE NOT = WS-PREV-FLAT-TYPE) OR                   
              (F200-DTE-ALLOC     NOT = WS-PREV-DTE-ALLOC) OR                   
              (F200-NUM-NT-ZONE   NOT = WS-PREV-NT-ZONE)                        
              PERFORM 820-PRINT-HEADING THRU 820-EXIT                           
              MOVE F200-NUM-FLAT-TYPE        TO WS-PREV-FLAT-TYPE               
              MOVE F200-DTE-ALLOC            TO WS-PREV-DTE-ALLOC               
              MOVE F200-NUM-NT-ZONE          TO WS-PREV-NT-ZONE                 
              MOVE 0                         TO WS-SERIAL                       
           ELSE                                                                 
              IF WS-NUMBER > 6                                                  
                 PERFORM 820-PRINT-HEADING THRU 820-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 840-PRINT-DETAIL   THRU   840-EXIT.                          
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       820-PRINT-HEADING.                                                       
      ******************************************************************        
                                                                                
           ADD    1                   TO  WS-PAGE-CNT.                          
           MOVE   ZEROS               TO  WS-NUMBER.                            
           MOVE   WS-PAGE-CNT         TO  L24A-PAGE.                            
           WRITE  PRINT-REC         FROM  L24A-HEADER-1 AFTER PAGE.             
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
           STRING WS-CURR-DATE (7:2) '/'                                        
                  WS-CURR-DATE (5:2) '/'                                        
                  WS-CURR-DATE (1:4)                                            
                  DELIMITED BY SIZE                                             
                  INTO CONTROL-DATE.                                            
           WRITE  PRINT-REC         FROM  L24A-HEADER-2 AFTER 1.                
           WRITE  PRINT-REC         FROM  L24A-HEADER-3 AFTER 1.                
           WRITE  PRINT-REC         FROM  L24A-LINE1    AFTER 2.                
           WRITE  PRINT-REC         FROM  L24A-LINE2    AFTER 1.                
           MOVE   SPACES              TO  PRINT-REC.                            
                                                                                
       820-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       840-PRINT-DETAIL.                                                        
      ******************************************************************        
                                                                                
           ADD   1                          TO   WS-NUMBER.                     
           MOVE  F200-NUM-HHTY-QUEUE-SERIAL TO   L24A-SERIAL.                   
           MOVE  K800-NUM-REGN              TO   L24A-REGNO.                    
                                                                                
           EVALUATE K800-NUM-CAT                                                
                                                                                
              WHEN '1'                                                          
                  MOVE 'M'           TO   L24A-CAT                              
              WHEN '2'                                                          
                  MOVE 'C'           TO   L24A-CAT                              
              WHEN '3'                                                          
                  MOVE 'I'           TO   L24A-CAT                              
              WHEN '4'                                                          
                  MOVE 'O'           TO   L24A-CAT                              
                                                                                
           END-EVALUATE.                                                        
                                                                                
           MOVE  K800-NUM-HOUSEHOLD        TO   L24A-HH.                        
           MOVE  K800-NUM-ORIG-LOAN-TAG    TO   L24A-LT.                        
           MOVE  F200-DTE-BKAPPMT-DATE     TO   WS-BKAPPT-DATE.                 
           MOVE  WS-BKAPPT-DD              TO   WS-EDIT-DD.                     
           MOVE  WS-BKAPPT-MM              TO   WS-EDIT-MM.                     
           MOVE  WS-BKAPPT-YY              TO   WS-EDIT-YY.                     
           MOVE  WS-EDITED-DATE            TO   L24A-DTE-BK-APPT.               
           MOVE  F200-TME-BKAPPMT          TO   WS-TIME.                        
                                                                                
           IF WS-HH = 12                                                        
              MOVE 'PM'                    TO   L24A-AM-PM                      
           ELSE                                                                 
           IF WS-HH < 12                                                        
              MOVE 'AM'                    TO   L24A-AM-PM                      
           ELSE                                                                 
              MOVE 'PM'                    TO   L24A-AM-PM                      
              SUBTRACT 12 FROM WS-HH.                                           
                                                                                
           MOVE  WS-HH                     TO   L24A-APPT-HH.                   
           MOVE  WS-MM                     TO   L24A-APPT-MM.                   
                                                                                
           WRITE    PRINT-REC FROM L24A-DATA1 AFTER 2.                          
           WRITE    PRINT-REC FROM L24A-DATA2 AFTER 1.                          
           WRITE    PRINT-REC FROM L24A-DATA3 AFTER 1.                          
           WRITE    PRINT-REC FROM L24A-DATA4 AFTER 1.                          
           WRITE    PRINT-REC FROM L24A-DATA5 AFTER 1.                          
                                                                                
           WRITE    PRINT-REC FROM L24A-LINE  AFTER 1.                          
                                                                                
       840-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       900-CLOSE-FILES-RTN.                                                     
      ******************************************************************        
           CLOSE BP13F200                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 BP13KB20                                                       
                 BP13L24A.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K800  CLOSE ERROR ' WS-K800-STATUS              
              MOVE    WS-K800-STATUS  TO    RETURN-CODE.                        
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K820  CLOSE ERROR ' WS-K820-STATUS              
              MOVE    WS-K820-STATUS  TO    RETURN-CODE.                        
                                                                                
           IF WS-K830-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K830  CLOSE ERROR ' WS-K830-STATUS              
              MOVE    WS-K830-STATUS  TO    RETURN-CODE.                        
                                                                                
           IF WS-KB20-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13KB20  CLOSE ERROR ' WS-KB20-STATUS              
              MOVE    WS-KB20-STATUS  TO    RETURN-CODE.                        
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
