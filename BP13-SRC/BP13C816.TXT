      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C816.                                                 
      *AUTHOR.        EDWARD S CRUZ.                                            
      *DATE-WRITTEN.  29/11/2001.                                               
                                                                                
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE   :     WRITE BP13F757 RECORDS TO BP13K757      *            
      *                    FOR ALL PROCESSED CASES OF THE DAY      *            
      *                                                            *            
      *  INPUT FILE  :     BP13F757  -  REQ FILE                   *            
      *  OUTPUT FILE :     BP13K757  -  REQ FILE                   *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  BY    DATE    DESCRIPTION                         *            
      * -------- ---  ------   -----------                         *            
      * BP131051 ED   20011129 NEW PROGRAM.                        *            
      * BP132599 CLT  20040921 CHANGE FILE TO BP13F757/K757        *            
      * BP133030 BA2  20070326 TO ENHANCE THE REFERRENCE FLD       *            
      * BP134292 ZDD1 20110824 TO UPDATE ONLY DTE-SOC-UPDATED      *            
      * BP139280 EAA2 07112023 EXPAND BP13F757 FROM 3000 TO 4000   *            
      *                        EXPAND BP13K757 FROM 3000 TO 4000   *            
      * ========================================================== *            
                                                                                
      ***********************                                                   
       ENVIRONMENT DIVISION.                                                    
      ***********************                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F757  ASSIGN       TO BP13F757.                           
                                                                                
           SELECT BP13K757  ASSIGN       TO BP13K757                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K757-KEY-FLD                        
                            FILE STATUS  IS BP13K757-STATUS.                    
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13K757                                                            
            RECORD CONTAINS 4000 CHARACTERS.                                    
       COPY BP13K757.                                                           
                                                                                
       FD   BP13F757                                                            
            RECORDING MODE  IS F                                                
            RECORD CONTAINS 4000 CHARACTERS.                                    
       COPY BP13F757.                                                           
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F757-READ-CTR             PIC 9(06)  VALUE ZEROS.             
           05  WS-K757-WRITE-CTR            PIC 9(06)  VALUE ZEROS.             
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F757-EOF                  PIC X(01)  VALUE 'N'.               
           05  BP13K757-STATUS              PIC 99     VALUE 00.                
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE.                                                         
               10  WS-CCYY                  PIC 9(4)   VALUE 0000.              
               10  WS-MM                    PIC 99     VALUE 00.                
               10  WS-DD                    PIC 99     VALUE 00.                
           05  WS-CURR-DATE                 PIC 9(08)  VALUE ZEROES.            
           05  F757-RUN-DATE                PIC X(10)  VALUE ZEROES.            
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
       0000-MAIN-ROUTINE.                                                       
      ******************************************************************        
           PERFORM 1000-OPEN-FILES             THRU 1000-EXIT.                  
           PERFORM 3000-PROCESS-RECORDS        THRU 3000-EXIT                   
                       UNTIL WS-F757-EOF = 'Y'.                                 
           PERFORM 9000-CLOSE-ROUTINE          THRU 9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
           OPEN INPUT   BP13F757                                                
                I-O     BP13K757.                                               
                                                                                
           IF BP13K757-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K757-STATUS ' BP13K757-STATUS         
              MOVE     BP13K757-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE.                                      
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DATE WS-CURR-DATE.             
           STRING WS-DD '/' WS-MM '/' WS-CCYY DELIMITED BY SIZE                 
                  INTO F757-RUN-DATE.                                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
           PERFORM 4000-READ-BP13F757     THRU 4000-EXIT.                       
           PERFORM 5000-REWRITE-BP13K757  THRU 5000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       4000-READ-BP13F757.                                                      
      ******************************************************************        
           READ BP13F757                                                        
                AT END MOVE 'Y' TO WS-F757-EOF                                  
                       GO TO 3000-EXIT.                                         
                                                                                
           ADD  1  TO  WS-F757-READ-CTR.                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       5000-REWRITE-BP13K757.                                                   
      ******************************************************************        
           MOVE  SPACES             TO BP13K757-REC.                            
           INITIALIZE                  BP13K757-REC.                            
                                                                                
           MOVE F757-NUM-REGN       TO K757-NUM-REGN.                           
           READ BP13K757.                                                       
                                                                                
           EVALUATE BP13K757-STATUS                                             
           WHEN 00                                                              
           WHEN 02                                                              
              MOVE  F757-DTE-SOC-UPDATED TO K757-DTE-SOC-UPDATED                
              REWRITE BP13K757-REC                                              
              ADD   1                  TO WS-K757-WRITE-CTR                     
           WHEN 23                                                              
              DISPLAY 'BP13K757 REC MISSING, STATUS IS' BP13K757-STATUS         
              MOVE     BP13K757-STATUS  TO RETURN-CODE                          
           WHEN OTHER                                                           
              DISPLAY 'BP13K757 WRITE ERROR, STATUS IS' BP13K757-STATUS         
              MOVE     BP13K757-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-ROUTINE.                                                      
      ******************************************************************        
            DISPLAY ' '.                                                        
            DISPLAY ' ====  BP13C816 CONTROL TOTALS  ===='.                     
            DISPLAY ' '.                                                        
            DISPLAY ' F757 RECORDS READ          : ' WS-F757-READ-CTR.          
            DISPLAY ' K757 RECORDS WRITTEN       : ' WS-K757-WRITE-CTR.         
            DISPLAY ' '.                                                        
            DISPLAY ' ==================================='.                     
                                                                                
            CLOSE BP13K757                                                      
                  BP13F757.                                                     
                                                                                
            IF BP13K757-STATUS NOT = 00                                         
               DISPLAY 'CLOSING ERROR-BP13K757-STAT ' BP13K757-STATUS.          
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
