       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C50E.                                                 
      *AUTHOR.        KATHRINA VALENZUELA.                                      
      *DATE-WRITTEN.  29/08/2019.                                               
      **************************************************************            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      **************************************************************            
      * OBJECTIVE : TO FORMAT AND SEND EMAIL ALERT TO CRMS ON THE  *            
      *             REPORT ON HA(S) WITH NEW-BORN WHERE HA IC AND  *            
      *             SPOUSE IC MATCHED AS PARENTS                   *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY   DESCRIPTION                         *            
      * -------- -------- ---- -----------                         *            
      * BP137791 20190829 KV6   NEW PGM                            *            
      **************************************************************            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F50D         ASSIGN BP13F50D.                             
                                                                                
           SELECT P13LC50E         ASSIGN P13LC50E.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BP13F50D           BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 150 CHARACTERS                   
                               LABEL RECORD IS STANDARD                         
                               RECORDING MODE  IS  F.                           
       01   BP13F50D-REC.                                                       
            05  F50D-REGN                     PIC X(08).                        
            05  F50D-OIC                      PIC X(05).                        
            05  F50D-SCH-ACC                  PIC X(11).                        
            05  F50D-ELIG-SCH                 PIC X(03).                        
            05  F50D-ALLO-SCH                 PIC X(03).                        
            05  F50D-HA-NRIC                  PIC X(09).                        
            05  F50D-HA-CTZNSHP               PIC X(06).                        
            05  F50D-SP-NRIC                  PIC X(09).                        
            05  F50D-SP-CTZNSHP               PIC X(06).                        
            05  F50D-SP-STATUS                PIC X(03).                        
            05  F50D-NB-NRIC                  PIC X(09).                        
            05  F50D-NB-NAME                  PIC X(20).                        
            05  F50D-NB-DOB                   PIC X(10).                        
            05  F50D-NB-NTNLTY                PIC X(02).                        
            05  FILLER                        PIC X(46).                        
                                                                                
       FD  P13LC50E                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 140 CHARACTERS.                                      
       01  P13LC50E-REC                PIC X(140).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FLAG-N-STATUS.                                                    
           05  WS-F50D-EOF-FLAG             PIC X(01)  VALUE 'N'.               
               88  WS-F50D-EOF              VALUE 'Y'.                          
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-PREV-OIC                  PIC X(5)   VALUE SPACES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F50D-READ                 PIC 9(05)  VALUE ZEROS.             
           05  WS-LC50E-WRTN                PIC 9(05)  VALUE ZEROS.             
           05  WS-NUM-SNO-LC50E             PIC 9(04)  VALUE ZEROES.            
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE ZEROES.               
                                                                                
      *---------------------------------------------------------------*         
      *        MAIL HEADER                                            *         
      *---------------------------------------------------------------*         
       01  MAIL-HDR-HELO.                                                       
           05  FILLER PIC X(140) VALUE 'HELO SGPHDB1'.                          
                                                                                
       01  MAIL-HDR-MAIL-FROM.                                                  
           05  FILLER PIC X(140) VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.              
                                                                                
       01  MAIL-HDR-MAIL-RCPT-TO.                                               
           05  FILLER PIC X(140) VALUE SPACES.                                  
                                                                                
       01  MAIL-HDR-MAIL-RCPT2.                                                 
           05  FILLER PIC X(140) VALUE 'RCPT TO:<TAN_SIEW_LAY@HDB.GOV.SG        
      -      '>'.                                                               
                                                                                
       01  MAIL-HDR-DATA.                                                       
           05  FILLER PIC X(140) VALUE 'DATA'.                                  
                                                                                
       01  MAIL-HDR-FROM.                                                       
           05  FILLER PIC X(140) VALUE 'FROM:Soc System - Email Alert'.         
                                                                                
       01  MAIL-HDR-MAIL-TO.                                                    
           05  FILLER PIC X(140) VALUE SPACES.                                  
                                                                                
       01  MAIL-HDR-MAIL-TO-CC1.                                                
           05  FILLER PIC X(140)                                                
               VALUE 'CC:<tan_siew_lay@hdb.gov.sg>'.                            
                                                                                
       01  MAIL-SUBJECT-LC50E.                                                  
           05  FILLER PIC X(140) VALUE 'SUBJECT : Report on HA(s) with n        
      -        'ew-born where HA IC and spouse IC matched as parents'.          
                                                                                
       01  MAIL-BODY-LC50E             PIC X(140) VALUE SPACES.                 
                                                                                
       01  MAIL-END-LC50E              PIC X(140) VALUE SPACES.                 
                                                                                
       01  MAIL-HDR-DATE.                                                       
           05  FILLER                  PIC X(6)   VALUE 'DATE: '.               
           05  MAIL-DATE               PIC X(10)  VALUE SPACES.                 
           05  FILLER                  PIC X(124) VALUE SPACES.                 
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                   PIC X      VALUE '.'.                    
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(140) VALUE SPACES.                 
                                                                                
      *---------------------------------------------------------------*         
      *                      REPORT DETAILS                           *         
      *---------------------------------------------------------------*         
       01  LC50E-HEADER.                                                        
           05  FILLER               PIC X(23)   VALUE SPACES.                   
           05  FILLER               PIC X(74)   VALUE                           
                       'REPORT ON HA(S) WITH NEW-BORN WHERE HA IC AND SP        
      -         'OUSE IC MATCHED AS PARENTS'.                                   
           05  FILLER               PIC X(26)   VALUE SPACES.                   
           05  FILLER               PIC X(6)    VALUE 'DATE: '.                 
           05  LC50E-NUM-DTE        PIC X(10)   VALUE SPACES.                   
           05  FILLER               PIC X(1)    VALUE SPACES.                   
                                                                                
       01  LC50E-LINE               PIC X(140)  VALUE ALL '-'.                  
                                                                                
       01  LC50E-HEADER1.                                                       
           05  FILLER               PIC X(04)   VALUE ' S/N'.                   
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  FILLER               PIC X(04)   VALUE 'REGN'.                   
           05  FILLER               PIC X(05)   VALUE SPACES.                   
           05  FILLER               PIC X(04)   VALUE 'POST'.                   
           05  FILLER               PIC X(02)   VALUE SPACES.                   
           05  FILLER               PIC X(07)   VALUE 'HDB REF'.                
           05  FILLER               PIC X(05)   VALUE SPACES.                   
           05  FILLER               PIC X(04)   VALUE 'ELIG'.                   
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  FILLER               PIC X(04)   VALUE 'ALLO'.                   
           05  FILLER               PIC X(02)   VALUE SPACES.                   
           05  FILLER               PIC X(02)   VALUE 'HA'.                     
           05  FILLER               PIC X(08)   VALUE SPACES.                   
           05  FILLER               PIC X(02)   VALUE 'HA'.                     
           05  FILLER               PIC X(06)   VALUE SPACES.                   
           05  FILLER               PIC X(06)   VALUE 'SPOUSE'.                 
           05  FILLER               PIC X(04)   VALUE SPACES.                   
           05  FILLER               PIC X(06)   VALUE 'SPOUSE'.                 
           05  FILLER               PIC X(02)   VALUE SPACES.                   
           05  FILLER               PIC X(06)   VALUE 'SPOUSE'.                 
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  FILLER               PIC X(08)   VALUE 'NEW BORN'.               
           05  FILLER               PIC X(03)   VALUE SPACES.                   
           05  FILLER               PIC X(08)   VALUE 'NEW BORN'.               
           05  FILLER               PIC X(13)   VALUE SPACES.                   
           05  FILLER               PIC X(13)   VALUE 'NEW BORN DATE'.          
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  FILLER               PIC X(08)   VALUE 'NEW BORN'.               
                                                                                
       01  LC50E-HEADER2.                                                       
           05  FILLER               PIC X(14)   VALUE SPACES.                   
           05  FILLER               PIC X(03)   VALUE 'OIC'.                    
           05  FILLER               PIC X(15)   VALUE SPACES.                   
           05  FILLER               PIC X(03)   VALUE 'SCH'.                    
           05  FILLER               PIC X(02)   VALUE SPACES.                   
           05  FILLER               PIC X(03)   VALUE 'SCH'.                    
           05  FILLER               PIC X(03)   VALUE SPACES.                   
           05  FILLER               PIC X(04)   VALUE 'NRIC'.                   
           05  FILLER               PIC X(06)   VALUE SPACES.                   
           05  FILLER               PIC X(07)   VALUE 'CTZNSHP'.                
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  FILLER               PIC X(04)   VALUE 'NRIC'.                   
           05  FILLER               PIC X(06)   VALUE SPACES.                   
           05  FILLER               PIC X(07)   VALUE 'CTZNSHP'.                
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  FILLER               PIC X(06)   VALUE 'STATUS'.                 
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  FILLER               PIC X(04)   VALUE 'NRIC'.                   
           05  FILLER               PIC X(07)   VALUE SPACES.                   
           05  FILLER               PIC X(04)   VALUE 'NAME'.                   
           05  FILLER               PIC X(17)   VALUE SPACES.                   
           05  FILLER               PIC X(08)   VALUE 'OF BIRTH'.               
           05  FILLER               PIC X(06)   VALUE SPACES.                   
           05  FILLER               PIC X(06)   VALUE 'NTNLTY'.                 
           05  FILLER               PIC X(02)   VALUE SPACES.                   
                                                                                
       01  LC50E-DETAIL1.                                                       
           05  LC50E-SNO            PIC ZZZ9    VALUE ZEROES.                   
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  LC50E-REGN           PIC X(08)   VALUE SPACES.                   
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  LC50E-OIC            PIC X(05)   VALUE SPACES.                   
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  LC50E-SCH-ACC        PIC X(11)   VALUE SPACES.                   
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  LC50E-ELIG-SCH       PIC X(03)   VALUE SPACES.                   
           05  FILLER               PIC X(02)   VALUE SPACES.                   
           05  LC50E-ALLO-SCH       PIC X(03)   VALUE SPACES.                   
           05  FILLER               PIC X(03)   VALUE SPACES.                   
           05  LC50E-HA-NRIC        PIC X(09)   VALUE SPACES.                   
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  LC50E-HA-CTZNSHP     PIC X(06)   VALUE SPACES.                   
           05  FILLER               PIC X(02)   VALUE SPACES.                   
           05  LC50E-SP-NRIC        PIC X(09)   VALUE SPACES.                   
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  LC50E-SP-CTZNSHP     PIC X(06)   VALUE SPACES.                   
           05  FILLER               PIC X(04)   VALUE SPACES.                   
           05  LC50E-SP-STATUS      PIC X(03)   VALUE SPACES.                   
           05  FILLER               PIC X(02)   VALUE SPACES.                   
           05  LC50E-NB-NRIC        PIC X(09)   VALUE SPACES.                   
           05  FILLER               PIC X(02)   VALUE SPACES.                   
           05  LC50E-NB-NAME        PIC X(20)   VALUE SPACES.                   
           05  FILLER               PIC X(01)   VALUE SPACES.                   
           05  LC50E-NB-DOB         PIC X(10)   VALUE SPACES.                   
           05  FILLER               PIC X(04)   VALUE SPACES.                   
           05  LC50E-NB-NTNLTY      PIC X(02)   VALUE SPACES.                   
           05  FILLER               PIC X(05)   VALUE SPACES.                   
                                                                                
      *---------------------------------------------------------------*         
      *        LINKAGE FOR BP13CCNT                                   *         
      *---------------------------------------------------------------*         
       01  WS-LINK-REC-CCNT.                                                    
           05  WS-LINK-CNTCT-TYPE        PIC X(3).                              
           05  WS-LINK-STAFF-NICK        PIC X(6).                              
           05  WS-LINK-STAFF-NUM         PIC X(6).                              
           05  WS-LINK-STAFF-CNTCT       PIC X(50).                             
           05  WS-LINK-SYSERR            PIC 9(4).                              
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F50D       THRU 2000-EXIT.                     
           PERFORM 3000-MAIN-ROUTINE        THRU 3000-EXIT                      
                   UNTIL WS-F50D-EOF.                                           
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT BP13F50D                                                  
               OUTPUT P13LC50E.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-DTE-CURR-8.                  
           MOVE WS-DTE-CURR-8               TO  WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8            TO  WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10                TO  WS-DTE-CURR-10.                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1100-CHECK-EMAIL.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                      TO WS-LINK-REC-CCNT.                
           MOVE F50D-OIC                    TO WS-LINK-STAFF-NICK.              
           MOVE 'EML'                       TO WS-LINK-CNTCT-TYPE.              
                                                                                
           PERFORM 8000-CALL-BP13CCNT       THRU 8000-EXIT.                     
                                                                                
           MOVE SPACES                      TO MAIL-HDR-MAIL-RCPT-TO            
                                               MAIL-HDR-MAIL-TO.                
                                                                                
           STRING 'RCPT TO:<'          DELIMITED BY SIZE                        
                   WS-LINK-STAFF-CNTCT DELIMITED BY SPACE                       
                  '>'                  DELIMITED BY SIZE                        
                   INTO MAIL-HDR-MAIL-RCPT-TO.                                  
                                                                                
           STRING 'TO:<'               DELIMITED BY SIZE                        
                   WS-LINK-STAFF-CNTCT DELIMITED BY SPACE                       
                  '>'                  DELIMITED BY SIZE                        
                   INTO MAIL-HDR-MAIL-TO.                                       
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1200-PRINT-LC50E-HEADER.                                                 
      *-------------------------------------------------------------            
           MOVE WS-DTE-CURR-10              TO MAIL-DATE                        
                                               LC50E-NUM-DTE.                   
                                                                                
           WRITE P13LC50E-REC               FROM MAIL-HDR-HELO.                 
           WRITE P13LC50E-REC               FROM MAIL-HDR-MAIL-FROM.            
           WRITE P13LC50E-REC               FROM MAIL-HDR-MAIL-RCPT-TO.         
           WRITE P13LC50E-REC               FROM MAIL-HDR-MAIL-RCPT2.           
           WRITE P13LC50E-REC               FROM MAIL-HDR-DATA.                 
           WRITE P13LC50E-REC               FROM MAIL-HDR-FROM.                 
           WRITE P13LC50E-REC               FROM MAIL-HDR-MAIL-TO.              
           WRITE P13LC50E-REC               FROM MAIL-HDR-MAIL-TO-CC1.          
           WRITE P13LC50E-REC               FROM MAIL-SUBJECT-LC50E.            
           WRITE P13LC50E-REC               FROM MAIL-HDR-DATE.                 
                                                                                
           WRITE P13LC50E-REC               FROM MAIL-SPACES.                   
           WRITE P13LC50E-REC               FROM MAIL-SPACES.                   
                                                                                
           WRITE P13LC50E-REC               FROM LC50E-HEADER.                  
           WRITE P13LC50E-REC               FROM MAIL-SPACES.                   
                                                                                
           WRITE P13LC50E-REC               FROM LC50E-LINE.                    
           WRITE P13LC50E-REC               FROM LC50E-HEADER1.                 
           WRITE P13LC50E-REC               FROM LC50E-HEADER2.                 
           WRITE P13LC50E-REC               FROM LC50E-LINE.                    
           WRITE P13LC50E-REC               FROM MAIL-SPACES.                   
                                                                                
           MOVE ZEROES                      TO WS-NUM-SNO-LC50E.                
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F50D.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F50D                                                        
              AT END MOVE 'Y'               TO WS-F50D-EOF-FLAG.                
                                                                                
           IF WS-F50D-EOF-FLAG NOT = 'Y'                                        
              ADD 1                         TO WS-F50D-READ                     
           ELSE                                                                 
              WRITE P13LC50E-REC            FROM MAIL-SPACES                    
              WRITE P13LC50E-REC            FROM MAIL-SPACES                    
              WRITE P13LC50E-REC            FROM MAIL-END                       
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF F50D-OIC NOT = SPACES AND LOW-VALUES AND ZEROES                   
              IF F50D-OIC NOT = WS-PREV-OIC                                     
                 IF WS-PREV-OIC NOT = SPACES                                    
                    WRITE P13LC50E-REC           FROM MAIL-SPACES               
                    WRITE P13LC50E-REC           FROM MAIL-SPACES               
                                                                                
                    WRITE P13LC50E-REC           FROM MAIL-END                  
                 END-IF                                                         
                 PERFORM 1100-CHECK-EMAIL        THRU 1100-EXIT                 
                 PERFORM 1200-PRINT-LC50E-HEADER THRU 1200-EXIT                 
                 MOVE F50D-OIC                   TO WS-PREV-OIC                 
              END-IF                                                            
              PERFORM 3100-WRITE-LC50E-REPORT    THRU 3100-EXIT                 
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F50D  THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3100-WRITE-LC50E-REPORT.                                                 
      *---------------------------------------------------------------*         
           INITIALIZE P13LC50E-REC.                                             
           MOVE SPACES                      TO P13LC50E-REC.                    
           ADD 1                            TO WS-LC50E-WRTN                    
                                               WS-NUM-SNO-LC50E                 
                                                                                
           MOVE WS-NUM-SNO-LC50E            TO LC50E-SNO.                       
           MOVE F50D-REGN                   TO LC50E-REGN.                      
           MOVE F50D-OIC                    TO LC50E-OIC.                       
           MOVE F50D-SCH-ACC                TO LC50E-SCH-ACC.                   
           MOVE F50D-ELIG-SCH               TO LC50E-ELIG-SCH.                  
           MOVE F50D-ALLO-SCH               TO LC50E-ALLO-SCH.                  
           MOVE F50D-HA-NRIC                TO LC50E-HA-NRIC                    
           MOVE F50D-HA-CTZNSHP             TO LC50E-HA-CTZNSHP                 
           MOVE F50D-SP-NRIC                TO LC50E-SP-NRIC                    
           MOVE F50D-SP-CTZNSHP             TO LC50E-SP-CTZNSHP                 
           MOVE F50D-SP-STATUS              TO LC50E-SP-STATUS                  
                                                                                
           MOVE F50D-NB-NRIC                TO LC50E-NB-NRIC.                   
           MOVE F50D-NB-NAME                TO LC50E-NB-NAME.                   
           MOVE F50D-NB-DOB                 TO LC50E-NB-DOB.                    
           MOVE F50D-NB-NTNLTY              TO LC50E-NB-NTNLTY.                 
                                                                                
           WRITE P13LC50E-REC              FROM LC50E-DETAIL1.                  
           WRITE P13LC50E-REC              FROM MAIL-SPACES.                    
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       8000-CALL-BP13CCNT.                                                      
      *------------------------------------------------------------             
                                                                                
           CALL 'BP13CCNT' USING  WS-LINK-REC-CCNT.                             
                                                                                
           EVALUATE WS-LINK-SYSERR                                              
           WHEN 000                                                             
              CONTINUE                                                          
           WHEN 100                                                             
              DISPLAY 'EMAIL ADDRESS NOT FOUND FOR ' F50D-OIC                   
              STRING  F50D-OIC     DELIMITED BY SPACE                           
                     '@hdb.gov.sg' DELIMITED BY SIZE                            
                                           INTO WS-LINK-STAFF-CNTCT             
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'BP13CCNT ERROR STATUS : ' WS-LINK-SYSERR                 
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY SPACES.                                                      
           DISPLAY '------- P13LC50E -------'.                                  
           DISPLAY ' RECORDS READ    IN BP13F50D : ' WS-F50D-READ.              
           DISPLAY ' RECORDS WRITTEN IN P13LC50E : ' WS-LC50E-WRTN.             
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE BP13F50D                                                       
                 P13LC50E.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
