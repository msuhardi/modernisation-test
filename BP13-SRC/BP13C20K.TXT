      *--------------------------------------------------------------*          
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------------*          
       PROGRAM-ID.    BP13C20K.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  09-03-2009                                                
      *--------------------------------------------------------------*          
      *                                                              *          
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)                 *          
      *                                                              *          
      *    SYSTEM ID   : BP13                                        *          
      *                                                              *          
      *    OBJECTIVES :  MONTHLY CANCELLATION OF BTO/BE APPLICATION  *          
      *                                                              *          
      *    INPUT FILES  : BP13F800                                   *          
      *                 : BP13F205                                   *          
      *    OUTPUT       : P13F800A                                  *           
      *--------------------------------------------------------------*          
      * CHG-NO  BY   DATE     DETAILS                                *          
      *-------- ---- -------- ---------------------------------------*          
      *BP133555 ESA1 09032009 NEW PGM                                *          
      *BP134210 ESA1 10082011 CATER FOR DBS CANCELLATION             *          
      *BP134657 ESA1 02112011 CATER FOR 3-BYTES ZONE                 *          
      *BP135111 ESA1 09122013 CATER FOR 3GEN CANCELLATION            *          
      *-------- ---- -------- ---------------------------------------*          
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
           SELECT P13F800A ASSIGN TO P13F800A.                                  
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
                                                                                
      *--------------------------------------------------------------*          
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------------*          
                                                                                
       FD   BP13F205                                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            RECORDING MODE  IS F.                                               
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            RECORDING MODE  IS F.                                               
       COPY BP13F800.                                                           
                                                                                
       FD   P13F800A.                                                           
       01   P13F800A-REC.                                                       
            05 FILLER                    PIC X(258).                            
            05 F800A-ALLOC-TAG           PIC X(02).                             
            05 FILLER                    PIC X(616).                            
            05 F800A-NUM-USERID          PIC X(08).                             
            05 FILLER                    PIC X(1116).                           
                                                                                
      *--------------------------------------------------------------*          
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------*          
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F800-EOF                PIC X(1)  VALUE 'N'.                  
           05  WS-F205-EOF                PIC X(1)  VALUE 'N'.                  
                                                                                
           05  WS-F800-COUNTER            PIC 9(2)  VALUE 0.                    
                                                                                
       01  WS-MISCELLANEOUS-VARS.                                               
           05  WS-EDITED-DATE             PIC X(10) VALUE SPACES.               
           05  WS-CNT-F800-READ           PIC 9(8)  VALUE 0.                    
           05  WS-CNT-F205-READ           PIC 9(8)  VALUE 0.                    
           05  WS-CNT-F800-WRITE          PIC 9(8)  VALUE 0.                    
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY                PIC 9(4).                             
           05  WS-SYS-MM                  PIC 9(2).                             
           05  WS-SYS-DD                  PIC 9(2).                             
                                                                                
      *--------------------------------------------------------------*          
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------------*          
                                                                                
      *--------------------------------------------------------------*          
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
           PERFORM 1100-READ-F205         THRU 1100-EXIT.                       
                                                                                
           PERFORM 2000-PROCESS           THRU 2000-EXIT                        
                UNTIL WS-F205-EOF = 'Y'.                                        
                                                                                
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1000-OPEN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13F205                                                 
                OUTPUT P13F800A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-SYSTEM-DATE.                   
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1100-READ-F205.                                                          
      *--------------------------------------------------------------*          
                                                                                
           READ BP13F205                                                        
                AT END  MOVE 'Y' TO WS-F205-EOF                                 
                MOVE 'Y' TO WS-F800-EOF                                         
                GO TO 1100-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-CNT-F205-READ.                                           
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1200-READ-F800.                                                          
      *--------------------------------------------------------------*          
                                                                                
           READ BP13F800                                                        
                AT END  MOVE 'Y' TO WS-F800-EOF                                 
                GO TO 1200-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-CNT-F800-READ.                                           
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1300-CLOSE-OPEN-F800.                                                    
      *--------------------------------------------------------------*          
           CLOSE      BP13F800.                                                 
           OPEN INPUT BP13F800.                                                 
                                                                                
           ADD 1      TO WS-F800-COUNTER.                                       
           MOVE 'N'   TO WS-F800-EOF.                                           
                                                                                
       1300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       2000-PROCESS.                                                            
      *--------------------------------------------------------------*          
           PERFORM 3000-PROCESS      THRU 3000-EXIT                             
                   UNTIL WS-F800-EOF = 'Y'.                                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       3000-PROCESS.                                                            
      *--------------------------------------------------------------*          
           PERFORM 1200-READ-F800    THRU 1200-EXIT.                            
                                                                                
           IF WS-F800-EOF = 'Y'                                                 
              PERFORM 1100-READ-F205        THRU 1100-EXIT                      
                                                                                
              IF WS-F205-EOF = 'Y'                                              
                 GO TO 3000-EXIT                                                
              END-IF                                                            
                                                                                
              PERFORM 1300-CLOSE-OPEN-F800  THRU 1300-EXIT                      
                                                                                
           END-IF.                                                              
                                                                                
           IF ((F205-NUM-NT-ZONE-SW = F800-NUM-APPLN-TYPE ) AND                 
              ( F205-DTE-BALLOT    = F800-DTE-BALLOT )      AND                 
              ( F205-START-QUEUE <= F800-NUM-ZONE-FT-QUEUE) AND                 
              ( F205-END-QUEUE >= F800-NUM-ZONE-FT-QUEUE)   AND                 
              (F205-NUM-SELECTION = F800-NUM-ALLO-CAT )     AND                 
              ((F205-NUM-NT-ZONE = F800-NUM-BTO-ZONE) OR                        
              (F205-NUM-NT-ZONE = F800-NUM-NEW-TOWN)))                          
                                                                                
              IF F205-NUM-SELECTION NOT = 'DBS'                                 
                 IF F205-NUM-FLAT-TYPE = F800-NUM-FLAT-TYPE                     
                    CONTINUE                                                    
                 ELSE                                                           
                    GO TO 3000-EXIT                                             
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF F205-NUM-GEN3 = 'Y'                                            
                 IF F800-NUM-GEN3 = 'Y'                                         
                    CONTINUE                                                    
                 ELSE                                                           
                    GO TO 3000-EXIT                                             
                 END-IF                                                         
              ELSE                                                              
                 IF F800-NUM-GEN3 = 'Y'                                         
                    GO TO 3000-EXIT                                             
                 ELSE                                                           
                    CONTINUE                                                    
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF F205-CDE-LETTER = 2                                            
               IF ((F205-CDE-CATEGORY = 0 AND F800-NUM-CAT = 0) OR              
                   (F205-CDE-CATEGORY = 1 AND F800-NUM-CAT = 1) OR              
                   (F205-CDE-CATEGORY = 2 AND F800-NUM-CAT = 2) OR              
                  ((F205-CDE-CATEGORY = 3) AND (F800-NUM-CAT = 3 OR 4)))        
                   CONTINUE                                                     
               ELSE                                                             
                  GO TO 3000-EXIT                                               
               END-IF                                                           
              END-IF                                                            
                                                                                
              PERFORM 3500-UPDATE-P13F800A   THRU 3500-EXIT                     
                                                                                
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       3500-UPDATE-P13F800A.                                                    
      *--------------------------------------------------------------*          
                                                                                
            MOVE BP13F800-MASTER TO P13F800A-REC.                               
                                                                                
            IF F205-CDE-LETTER = '2'                                            
               MOVE 'NC'            TO F800A-ALLOC-TAG                          
            ELSE                                                                
               IF F205-CDE-LETTER = '3'                                         
                  MOVE 'NF'         TO F800A-ALLOC-TAG                          
               ELSE                                                             
                  MOVE 'NV'         TO F800A-ALLOC-TAG                          
               END-IF                                                           
            END-IF.                                                             
                                                                                
            IF F205-NUM-SELECTION = 'DBS'                                       
               MOVE '27'         TO F800A-ALLOC-TAG                             
            END-IF.                                                             
                                                                                
            MOVE F205-NUM-USERID TO F800A-NUM-USERID.                           
                                                                                
            WRITE P13F800A-REC.                                                 
            ADD  1  TO WS-CNT-F800-WRITE.                                       
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------*          
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------------*          
                                                                                
           CLOSE BP13F800                                                       
                 P13F800A                                                       
                 BP13F205.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '********* BP13C20K CONTROL *********'.                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF READ (BP13F800) = ' WS-CNT-F800-READ.                
           DISPLAY 'NO. OF READ (BP13F205) = ' WS-CNT-F205-READ.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECS WRITE      = '.                                 
           DISPLAY '    (P13F800A)         = ' WS-CNT-F800-WRITE.               
           DISPLAY SPACES.                                                      
           DISPLAY '************************************'.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************** E N D  O F  P R O G R A M *****************             
