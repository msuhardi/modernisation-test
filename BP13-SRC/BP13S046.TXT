 /******************************************************************/           
 /*               SYSTEM  OF  COMMITMENT  (BP13)                   */           
 /*================================================================*/           
 /* PROGRAM ID   : BP13S046                                        */           
 /* AUTHOR       : CHYEO                                           */           
 /* DATE WRITTEN : 23/04/2004                                      */           
 /* OBJECTIVE    : PRINT PREMIUIM CASES,                           */           
 /*----------------------------------------------------------------*/           
 /* CHG REF  BY   DATE     DESC                                    */           
 /* -------- ---  -------- ----                                    */           
 /* BP132500 YCH2 23042004 PRINT PREMIUM BEFORE AFTER REGN         */           
 /* BP132500 YCH2 30042004 INCLUDE OTHER RESALE STATUS             */           
 /*----------------------------------------------------------------*/           
   OPTIONS NODATE NONUMBER;                                                     
   %GLOBAL REGN;                                                                
                                                                                
   DATA BP13F740;                                                               
        INFILE BP13F740;                                                        
        INPUT  @071  LOW_FLR  $CHAR1.                                           
               @41   LSE_IC1  $CHAR9.                                           
               @82   LSE_IC2  $CHAR9.                                           
               @50   LSE_IC3  $CHAR9.                                           
               @59   LSE_IC4  $CHAR9.                                           
               @123  NUM_REGN $CHAR8.                                           
               @211  FT_TYPE  $CHAR2.                                           
               @216  CDE_JB   $CHAR1.                                           
               @342  CDE_IDP  $CHAR3.                                           
               @389  CDE_PREM $CHAR3.                                           
               @423  BUY_BACK $CHAR1.                                           
               ;                                                                
                                                                                
   PROC SORT DATA=BP13F740;                                                     
        BY NUM_REGN;                                                            
   RUN;                                                                         
                                                                                
   DATA BP13F830;                                                               
        INFILE BP13F830;                                                        
        INPUT @020  NUM_REGN $CHAR8.                                            
              @033  BLOCK    $CHAR4.                                            
              @038  SUB_BLK  $CHAR1.                                            
              @046  LVL      $CHAR2.                                            
              @049  HSEMAIN  $CHAR4.                                            
              @053  HSESUB   $CHAR3.                                            
              @056  STREET   $CHAR32.                                           
              @121  POSTAL   $CHAR6.                                            
              ;                                                                 
                                                                                
   PROC SORT DATA=BP13F830;                                                     
        BY NUM_REGN;                                                            
   RUN;                                                                         
                                                                                
  /*------------------------------------------------------------*/              
  /*MERGE BP13F740 & BP13F830                                   */              
  /*------------------------------------------------------------*/              
   DATA F740F830;                                                               
   MERGE BP13F740(IN=F740) BP13F830(IN=F830);                                   
   BY NUM_REGN;                                                                 
                                                                                
   IF F740 AND F830;                                                            
                                                                                
   PROC SORT DATA=F740F830;                                                     
        BY NUM_REGN;                                                            
   RUN;                                                                         
                                                                                
   DATA BP13F825;                                                               
        INFILE BP13F825;                                                        
        INPUT  @001  NUM_REGN $CHAR8.                                           
               @009  NUM_NRIC $CHAR9.                                           
               @028  NAME     $CHAR66.                                          
               @107  RELN     $CHAR2.                                           
               @131  LSE_CHK  $CHAR1.                                           
               @109  INCOME   5.                                                
               ;                                                                
                                                                                
   PROC SORT DATA=BP13F825;                                                     
        BY NUM_REGN NUM_NRIC;                                                   
   RUN;                                                                         
                                                                                
   DATA BP13F558;                                                               
        INFILE BP13F558;                                                        
        INPUT  @001  NUM_REGN $CHAR8.                                           
               @009  NUM_NRIC $CHAR9.                                           
               @112  RSL_ST   $CHAR3.                                           
               ;                                                                
                                                                                
   PROC SORT DATA=BP13F558;                                                     
        BY NUM_REGN NUM_NRIC;                                                   
   RUN;                                                                         
                                                                                
  /*------------------------------------------------------------*/              
  /*MERGE BP13F825 & BP13F558 TO GET RESALE STATUS              */              
  /*------------------------------------------------------------*/              
                                                                                
   DATA F825F558;                                                               
   MERGE BP13F825(IN=F825) BP13F558(IN=F558);                                   
   BY NUM_REGN NUM_NRIC;                                                        
                                                                                
   IF F825;                                                                     
                                                                                
   PROC SORT DATA=F825F558;                                                     
   BY NUM_REGN ;                                                                
                                                                                
   DATA F740F825;                                                               
   MERGE F740F830(IN=F740) F825F558(IN=F825);                                   
   BY NUM_REGN;                                                                 
                                                                                
   IF F740 AND F825;                                                            
                                                                                
   LENGTH APP_CDE $4.;                                                          
                                                                                
   IF NUM_NRIC=LSE_IC1 THEN                                                     
      APP_CDE="HA1";                                                            
   ELSE                                                                         
      IF NUM_NRIC=LSE_IC2 THEN                                                  
         APP_CDE="HA2";                                                         
      ELSE                                                                      
         IF NUM_NRIC=LSE_IC3 THEN                                               
            APP_CDE="HA3";                                                      
         ELSE                                                                   
            IF NUM_NRIC=LSE_IC4 THEN                                            
               APP_CDE="HA4";                                                   
            ELSE                                                                
               APP_CDE="OCCP";                                                  
                                                                                
   PROC SORT;                                                                   
   BY NUM_REGN;                                                                 
                                                                                
 /* REPORT A - LISTING OF PREMIUM CASE*/                                        
                                                                                
   DATA _NULLS_;                                                                
    SET F740F825 END=EOF;                                                       
        BY NUM_REGN;                                                            
                                                                                
   LENGTH HOUSE_NO $20.;                                                        
   LENGTH ADDRESS  $62.;                                                        
                                                                                
   IF HSESUB='   ' THEN                                                         
      HOUSE_NO='BLK '||BLOCK||' #'||LVL||'-'||HSEMAIN;                          
   ELSE                                                                         
      HOUSE_NO='BLK '||BLOCK||' #'||LVL||'-'||HSEMAIN ||'-'||HSESUB;            
                                                                                
   IF BLOCK ^=' ' THEN                                                          
      ADDRESS=TRIM(HOUSE_NO)||', '||TRIM(STREET)||', S('||POSTAL||')';          
   ELSE                                                                         
      ADDRESS=' ';                                                              
                                                                                
   OPTIONS PAGESIZE=55 LS=132 NODATE;                                           
   FILE BP13L046 HEADER=RPTHDR1 LINESLEFT=LL NOTITLES;                          
                                                                                
   IF FIRST.NUM_REGN THEN                                                       
      DO;                                                                       
        SLNO + 1;                                                               
        IF BUY_BACK=' ' THEN                                                    
           PUT @001 SLNO                                                        
               @006 NUM_REGN                                                    
               @016 ADDRESS                                                     
               @114 CDE_PREM                                                    
               @124 FT_TYPE                                                     
               ;                                                                
        ELSE                                                                    
           PUT @001 SLNO                                                        
               @006 NUM_REGN                                                    
               @016 ADDRESS                                                     
               @114 CDE_PREM                                                    
               @124 FT_TYPE                                                     
               @125 'B'                                                         
               ;                                                                
      END;                                                                      
                                                                                
    IF RELN^=' ' THEN                                                           
       LINK GETCODES;                                                           
                                                                                
    PUT @006 NUM_NRIC                                                           
        @016 NAME                                                               
        @82  APP_CDE $CHAR4.                                                    
        @87  RELN $CHAR2.                                                       
        @90  RELNSHIP $CHAR20.                                                  
        @114 LSE_CHK                                                            
        @122 RSL_ST                                                             
            ;                                                                   
                                                                                
   IF LAST.NUM_REGN THEN                                                        
      DO;                                                                       
        PUT /;                                                                  
      END;                                                                      
                                                                                
     IF EOF THEN                                                                
        DO;                                                                     
          PUT @1 132*'-';                                                       
        END;                                                                    
 RETURN;                                                                        
                                                                                
 GETCODES:                                                                      
    LENGTH K060KEY1 $5.;                                                        
    CDE = '35';                                                                 
    K060KEY1=CDE||RELN||' ';                                                    
    INFILE BP13K060 VSAM KEY=K060KEY1;                                          
    INPUT @;                                                                    
    RC=_IORC_; FBC=_FDBK_;                                                      
    IF RC=0 THEN                                                                
       DO;                                                                      
          INPUT @6   K060DSC $CHAR20.;                                          
          RELNSHIP=K060DSC;                                                     
       END;                                                                     
    ELSE                                                                        
       DO;                                                                      
          RELNSHIP=' ';                                                         
       END;                                                                     
                                                                                
   RETURN;                                                                      
                                                                                
 RPTHDR1:                                                                       
   PAGE+1;                                                                      
   CUR_DATE=TODAY();                                                            
   PUT @001 'BP13L046   HDBCAT 3'                                               
       @039 '    S Y S T E M   O F   C O M M I T M E N T'                       
       @100 'DATE : ' CUR_DATE DDMMYY10.                                        
       @118 'PAGE : ' PAGE;                                                     
   PUT @039 'LIST OF ESSENTIAL OCCUPIER AND THE RESALE STATUS';                 
   PUT /;                                                                       
   PUT @001  'S/NO'                                                             
       @006  'REGN NO'                                                          
       @015  'ADDRESS'                                                          
       @113  'PREMIUM'                                                          
       @122  'FLAT TYPE';                                                       
   PUT @006  'NRIC'                                                             
       @015  'NAME'                                                             
       @81   'STAT'                                                             
       @86   'REL'                                                              
       @90   'RELATIONSHIP'                                                     
       @113  'LSE CHK'                                                          
       @122  'RSL ST'                                                           
       ;                                                                        
   PUT @1 132*'-';                                                              
RETURN;                                                                         
                                                                                
