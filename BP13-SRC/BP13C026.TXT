       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C026.                                                 
      *AUTHOR.        MICHAEL ARRIOLA.                                          
      *DATE-WRITTEN.  02/10/2008.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES: TO UPDATE BP13KA30 ACCEPTANCE BASED ON         *            
      *             VALIDATED DEVELOPER BOOK FILE.                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP133452  MSD1  20081002  NEW PGM                          *            
      * BP133457  JB8   20081210  DETERMIN CDE-NT                  *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13FA30  ASSIGN        TO BP13FA30.                          
                                                                                
           SELECT BP13KA30  ASSIGN        TO BP13KA30                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KA30-KEY-FLD                       
                            FILE STATUS   IS BP13KA30-STATUS.                   
                                                                                
           SELECT BP13KA40  ASSIGN        TO BP13KA40                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KA40-KEY-FLD                       
                            FILE STATUS   IS BP13KA40-STATUS.                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13FA30                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13FA30.                                                           
                                                                                
       FD   BP13KA30                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13KA30.                                                           
                                                                                
       FD   BP13KA40                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13KA40.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-FILE-STATUS.                                                       
          05 BP13KA30-STATUS             PIC 9(02) VALUE ZEROES.                
          05 BP13KA40-STATUS             PIC 9(02) VALUE ZEROES.                
       01 WS-COUNTERS.                                                          
          05 WS-FA30-READ                PIC 9(08) VALUE ZEROES.                
          05 WS-KA30-UPDATE              PIC 9(08) VALUE ZEROES.                
          05 WS-KA30-DUPKEY              PIC 9(08) VALUE ZEROES.                
          05 WS-KA40-WRITE               PIC 9(08) VALUE ZEROES.                
          05 WS-KA40-DUPKEY              PIC 9(08) VALUE ZEROES.                
       01 WS-SWITCHES.                                                          
          05 WS-KA30-FND                 PIC X     VALUE 'N'.                   
       01 WS-VARIABLES.                                                         
          05 WS-CURRENT-DATE             PIC  X(08) VALUE SPACES.               
          05 WS-CURRENT-TIME             PIC  9(08) VALUE ZEROES.               
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13FA30  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-DATA   THRU 3000-EXIT                           
             UNTIL FA30-KEY-FLD = HIGH-VALUES.                                  
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13FA30                                                 
                I-O    BP13KA30                                                 
                       BP13KA40.                                                
                                                                                
           IF BP13KA30-STATUS  NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING - BP13KA30 : ' BP13KA30-STATUS             
              MOVE     BP13KA30-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           IF BP13KA40-STATUS  NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING - BP13KA40 : ' BP13KA40-STATUS             
              MOVE     BP13KA40-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           ACCEPT  WS-CURRENT-TIME  FROM TIME.                                  
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13FA30.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13FA30 AT END                                                 
             MOVE HIGH-VALUES TO FA30-KEY-FLD                                   
             GO TO 2000-EXIT.                                                   
                                                                                
           ADD 1              TO WS-FA30-READ.                                  
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'                      TO WS-KA30-FND.                        
           PERFORM 4000-READ-BP13KA30    THRU 4000-EXIT.                        
           PERFORM 5000-REWRITE-BP13KA30 THRU 5000-EXIT.                        
                                                                                
           PERFORM 6000-WRITE-BP13KA40   THRU 6000-EXIT.                        
                                                                                
           PERFORM 2000-READ-BP13FA30    THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-READ-BP13KA30.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE FA30-KEY-FLD             TO KA30-KEY-FLD.                       
                                                                                
           READ BP13KA30.                                                       
                                                                                
           EVALUATE BP13KA30-STATUS                                             
           WHEN 00                                                              
                MOVE 'Y'                   TO WS-KA30-FND                       
           WHEN 23                                                              
                DISPLAY 'BP13KA30 NOT FOUND ' KA30-KEY-FLD                      
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13KA30, STATUS  '                      
                  BP13KA30-STATUS ', KA30-KEY-FLD ' KA30-KEY-FLD                
                MOVE BP13KA30-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-REWRITE-BP13KA30.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE BP13FA30-REC             TO BP13KA30-REC.                       
                                                                                
           REWRITE BP13KA30-REC.                                                
                                                                                
           EVALUATE BP13KA30-STATUS                                             
           WHEN 00                                                              
               ADD   1      TO WS-KA30-UPDATE                                   
           WHEN 02                                                              
               ADD   1      TO WS-KA30-UPDATE                                   
               ADD   1      TO WS-KA30-DUPKEY                                   
           WHEN 22                                                              
               DISPLAY 'BP13KA30 DUP REC ERROR '                                
                       ' KEY IS ' KA30-KEY-FLD                                  
           WHEN OTHER                                                           
               DISPLAY 'BP13KA30 REWRITE ERROR STATUS ' BP13KA30-STATUS         
                       ' KEY IS ' KA30-KEY-FLD                                  
               MOVE BP13KA30-STATUS        TO RETURN-CODE                       
               PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-WRITE-BP13KA40.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                     TO BP13KA40-TRANS-REC.               
           INITIALIZE                         BP13KA40-TRANS-REC.               
                                                                                
           MOVE FA30-NUM-REGN              TO KA40-REGN-NO.                     
           STRING FA30-NUM-ACCEPT 'I' DELIMITED BY SIZE                         
                                         INTO KA40-CDE-TRANS.                   
           MOVE KA30-NUM-SCH-ACC           TO KA40-SCH-ACC                      
           MOVE KA30-NUM-FLAT-TYPE(3:1)    TO KA40-CDE-FLAT-TYPE                
           MOVE KA30-NUM-BLK               TO KA40-NUM-BLK                      
           MOVE KA30-NUM-LEVEL             TO KA40-NUM-LEVEL-R                  
           MOVE KA30-NUM-MAIN-UNIT         TO KA40-NUM-MAIN                     
           MOVE KA30-NUM-FLOOR-AREA        TO KA40-QTY-AREA-STRATA              
           MOVE WS-CURRENT-DATE            TO KA40-DTE-TRANS                    
           MOVE ZEROES                     TO KA40-NUM-PREMIUM-RATE             
                                              KA40-CDE-PRE-ELIG-LOOSE           
           MOVE KA30-NUM-FLAT-TYPE         TO KA40-CDE-TYP-DESG                 
           MOVE KA30-NUM-FLAT-TYPE(1:1)    TO KA40-CDE-CLS-ACTUSE               
           MOVE KA30-NUM-FLAT-TYPE(2:2)    TO KA40-CDE-TYP-ACTUSE               
           MOVE 'PA'                       TO KA40-NUM-ALLOC-SCH                
           MOVE 'Y'                        TO KA40-NUM-DBSS                     
           IF KA30-NUM-PRJCT = 'D01'                                            
              MOVE 'TAP'                   TO KA40-CDE-NT                       
           ELSE                                                                 
              IF KA30-NUM-PRJCT = 'D02'                                         
                 MOVE 'KWN'                TO KA40-CDE-NT                       
              ELSE                                                              
                 IF KA30-NUM-PRJCT = 'D03'                                      
                    MOVE 'AMK'             TO KA40-CDE-NT                       
                 ELSE                                                           
                    IF KA30-NUM-PRJCT = 'D04'                                   
                       MOVE 'BH '          TO KA40-CDE-NT                       
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE WS-CURRENT-DATE            TO KA40-DTE-UPDATE                   
           MOVE 'BP13C026'                 TO KA40-USERID.                      
                                                                                
           WRITE BP13KA40-TRANS-REC.                                            
                                                                                
           EVALUATE BP13KA40-STATUS                                             
           WHEN 00                                                              
               ADD   1      TO WS-KA40-WRITE                                    
           WHEN OTHER                                                           
               DISPLAY 'BP13KA40 WRITE ERROR STATUS ' BP13KA40-STATUS           
                       ' KEY IS ' KA40-KEY-FLD                                  
               MOVE BP13KA40-STATUS        TO RETURN-CODE                       
               PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13C026 CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ FROM BP13FA30  : ' WS-FA30-READ.            
           DISPLAY 'NO OF RECS UPDATED   BP13KA30  : ' WS-KA30-UPDATE.          
           DISPLAY 'NO OF RECS WRITTEN   BP13KA40  : ' WS-KA40-WRITE.           
                                                                                
           CLOSE BP13FA30                                                       
                 BP13KA30                                                       
                 BP13KA40.                                                      
                                                                                
           IF BP13KA30-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13KA30 : ' BP13KA30-STATUS             
              MOVE BP13KA30-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13KA40-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13KA40 : ' BP13KA40-STATUS             
              MOVE BP13KA40-STATUS TO RETURN-CODE.                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
