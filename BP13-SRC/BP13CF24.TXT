      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
       PROGRAM-ID.    BP13CF24.                                                 
      *AUTHOR.        ZARITA DANE SARMIENTO.                                    
      *DATE-WRITTEN.  18/04/11.                                                 
                                                                                
      ******************************************************************        
      *  PROGRAM NAME : BP13CF24                                       *        
      *  OBJECTIVE    : OUTPUT CASES    WITH MATCH INCOME              *        
      *                                                                *        
      *  INPUT/OUTPUT FILES :                                          *        
      *  ====================                                          *        
      *  TYPE      FILE NAME                                           *        
      *  ------    ---------                                           *        
      *  INPUT     BP13F740                                            *        
      *  INPUT     BP13K800                                            *        
      *  OUTPUT    BP13OUT1                                            *        
      *================================================================*        
      * MODIFICATION HISTORY:                                          *        
      * ---------------------                                          *        
      * CHG REQ. AUTHOR DATE      DESCRIPTIONS                         *        
      * ======== ====== =======   =====================================*        
      * BP134205 ZDD    20101124  NEW PROGRAM.                         *        
      * BP135190 SMR2   20131204  EXPAND BP13K813 TO 1000              *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
      *================================================================*        
       CONFIGURATION SECTION.                                                   
      *================================================================*        
       SOURCE-COMPUTER.  IBM-4341.                                              
       OBJECT-COMPUTER.  IBM-4341.                                              
                                                                                
      *================================================================*        
       INPUT-OUTPUT SECTION.                                                    
      *================================================================*        
      *----------------------------------------------------------------*        
       FILE-CONTROL.                                                            
      *----------------------------------------------------------------*        
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
           SELECT BP13K022  ASSIGN        TO BP13K022                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K022-KEY-FLD                       
                            ALTERNATE KEY IS K022-NUM-REGN                      
                            FILE STATUS   IS WS-K022-STAT.                      
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS WS-K800-STAT.                      
                                                                                
           SELECT BP13K813  ASSIGN        TO BP13K813                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K813-KEY-FLD                       
                            FILE STATUS   IS WS-K813-STAT.                      
                                                                                
           SELECT BP13OUT1 ASSIGN TO BP13OUT1.                                  
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      *================================================================*        
       FILE SECTION.                                                            
      *================================================================*        
                                                                                
       FD   BP13F740                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13OUT1                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   BP13OUT1-REC.                                                       
            05  OUT1-NT             PIC X(03).                                  
            05  FILLER1             PIC X(01).                                  
            05  OUT1-QUEUE-NUM      PIC X(05).                                  
            05  FILLER2             PIC X(01).                                  
            05  OUT1-REGN           PIC X(08).                                  
            05  FILLER3             PIC X(01).                                  
            05  OUT1-NRIC-HA1       PIC X(09).                                  
            05  FILLER4             PIC X(01).                                  
            05  OUT1-CSM            PIC X(05).                                  
            05  FILLER5             PIC X(01).                                  
            05  OUT1-HH-INCOME      PIC ZZZZ9.                                  
            05  FILLER              PIC X(59).                                  
                                                                                
                                                                                
      *================================================================*        
       WORKING-STORAGE SECTION.                                                 
      *================================================================*        
      *----------------------------------------------------------------*        
      *    FLAG VARIABLES                                              *        
      *----------------------------------------------------------------*        
       01 WS-FILE-VARIBLES.                                                     
          05 WS-F740-READ                PIC 9(07) VALUE ZEROES.                
          05 WS-EOF-F740                 PIC X(01) VALUE 'N'.                   
          05 WS-K800-STAT                PIC 9(02) VALUE ZEROES.                
          05 WS-K022-STAT                PIC 9(02) VALUE ZEROES.                
          05 WS-K813-STAT                PIC 9(02) VALUE ZEROES.                
          05 WS-NFND-K800                PIC 9(07) VALUE ZEROES.                
          05 WS-NFND-K022                PIC 9(07) VALUE ZEROES.                
          05 WS-NFND-K813                PIC 9(07) VALUE ZEROES.                
          05 WS-OUT1-WRT                 PIC 9(07) VALUE ZEROES.                
          05 WS-UNMATCH                  PIC 9(07) VALUE ZEROES.                
          05 WS-NFND-K800-K813           PIC 9(07) VALUE ZEROES.                
          05 WS-NO-QUEUE                 PIC 9(07) VALUE ZEROES.                
          05 WS-FOUND-K800               PIC X(01) VALUE 'N'.                   
          05 WS-FOUND-K813               PIC X(01) VALUE 'N'.                   
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *================================================================*        
       0000-MAIN-ROUTINE.                                                       
      *================================================================*        
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 3000-PROCESS-RECORDS     THRU 3000-EXIT                      
                   UNTIL WS-EOF-F740 = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       1000-OPEN-FILES.                                                         
      *================================================================*        
                                                                                
           OPEN INPUT  BP13F740                                                 
                       BP13K800                                                 
                       BP13K813                                                 
                       BP13K022                                                 
                OUTPUT BP13OUT1.                                                
                                                                                
           IF WS-K800-STAT NOT  = 00 AND 97                                     
              DISPLAY 'ERROR READING BP13K800 - STAT ' WS-K800-STAT             
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K813-STAT NOT  = 00 AND 97                                     
              DISPLAY 'ERROR READING BP13K813 - STAT ' WS-K813-STAT             
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K022-STAT NOT  = 00 AND 97                                     
              DISPLAY 'ERROR READING BP13K022 - STAT ' WS-K022-STAT             
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT.                     
                                                                                
           MOVE SPACES TO BP13OUT1-REC.                                         
           INITIALIZE     BP13OUT1-REC.                                         
                                                                                
           STRING 'NT ;QUEUE;REGN    ;'                                         
             'HA1 NRIC ;CSM  ;HH INCOME ' DELIMITED BY SIZE INTO                
                   BP13OUT1-REC.                                                
           WRITE BP13OUT1-REC.                                                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       2000-READ-BP13F740.                                                      
      *================================================================*        
                                                                                
           READ BP13F740                                                        
             AT END                                                             
                MOVE 'Y' TO WS-EOF-F740                                         
                GO TO 2000-EXIT                                                 
           END-READ                                                             
                                                                                
           ADD 1 TO WS-F740-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3000-PROCESS-RECORDS.                                                    
      *================================================================*        
                                                                                
           IF F740-NUM-NT-FT-QUEUE NOT = SPACES AND LOW-VALUES                  
                                         AND ZEROES                             
              MOVE SPACES                 TO BP13OUT1-REC                       
              INITIALIZE                     BP13OUT1-REC                       
                                                                                
              PERFORM 3300-READ-BP13K022     THRU 3300-EXIT                     
              PERFORM 3100-READ-BP13K800     THRU 3100-EXIT                     
              PERFORM 3200-READ-BP13K813     THRU 3200-EXIT                     
                                                                                
              IF WS-FOUND-K800 = 'Y' AND WS-FOUND-K813 = 'Y'                    
                IF K800-AMT-HSE-INCOME = K813-AMT-INCOME-CEILING                
                  PERFORM 4000-MOVE-DELIMITER         THRU 4000-EXIT            
                  PERFORM 5000-MOVE-DETAILS           THRU 5000-EXIT            
                  WRITE BP13OUT1-REC                                            
                  ADD 1 TO WS-OUT1-WRT                                          
                ELSE                                                            
                  ADD 1 TO WS-UNMATCH                                           
                END-IF                                                          
              ELSE                                                              
                ADD 1 TO WS-NFND-K800-K813                                      
              END-IF                                                            
                                                                                
           ELSE                                                                 
              ADD 1 TO WS-NO-QUEUE                                              
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F740          THRU 2000-EXIT.                  
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3100-READ-BP13K800.                                                      
      *================================================================*        
                                                                                
           MOVE SPACES               TO BP13K800-MASTER.                        
           MOVE F740-NUM-REGN        TO K800-NUM-REGN.                          
                                                                                
           READ BP13K800.                                                       
             EVALUATE WS-K800-STAT                                              
                WHEN 00                                                         
                  MOVE 'Y'   TO  WS-FOUND-K800                                  
                WHEN 23                                                         
                  MOVE 'N'   TO  WS-FOUND-K800                                  
                  ADD 1 TO WS-NFND-K800                                         
                  DISPLAY 'RECORD NOT FOUND - K800' K800-NUM-REGN               
                WHEN OTHER                                                      
                  DISPLAY 'ERROR READING BP13K800 - STAT ' WS-K800-STAT         
                  PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                     
             END-EVALUATE.                                                      
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3200-READ-BP13K813.                                                      
      *================================================================*        
                                                                                
           MOVE SPACES               TO BP13K813-REC.                           
           MOVE F740-CDE-NT1         TO K813-NUM-ZONE.                          
           MOVE F740-CDE-FLAT-TYPE   TO K813-NUM-FLAT-TYPE.                     
           MOVE F740-DTE-BALLOT      TO K813-DTE-BALLOT.                        
                                                                                
           READ BP13K813.                                                       
             EVALUATE WS-K813-STAT                                              
                WHEN 00                                                         
                  MOVE 'Y'   TO WS-FOUND-K813                                   
                WHEN 23                                                         
                  MOVE 'N'   TO WS-FOUND-K813                                   
                  ADD 1 TO WS-NFND-K813                                         
                  DISPLAY 'RECORD NOT FOUND - K813' K813-KEY-FLD                
                WHEN OTHER                                                      
                  DISPLAY 'ERROR READING BP13K813 - STAT ' WS-K813-STAT         
                  PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                     
             END-EVALUATE.                                                      
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3300-READ-BP13K022.                                                      
      *================================================================*        
                                                                                
           MOVE SPACES               TO BP13K022-REC.                           
           MOVE F740-NUM-REGN        TO K022-NUM-REGN.                          
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN                                   
             EVALUATE WS-K022-STAT                                              
                WHEN 00                                                         
                WHEN 02                                                         
                  CONTINUE                                                      
                WHEN 23                                                         
                  ADD 1 TO WS-NFND-K022                                         
                  DISPLAY 'RECORD NOT FOUND - K022' K022-NUM-REGN               
                WHEN OTHER                                                      
                  DISPLAY 'ERROR READING BP13K022 - STAT ' WS-K022-STAT         
                  PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                     
             END-EVALUATE.                                                      
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       4000-MOVE-DELIMITER.                                                     
      *================================================================*        
                                                                                
           MOVE     ';'            TO FILLER1   FILLER2                         
                                      FILLER3   FILLER4                         
                                      FILLER5.                                  
                                                                                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       5000-MOVE-DETAILS.                                                       
      *================================================================*        
                                                                                
           MOVE F740-NUM-REGN            TO OUT1-REGN.                          
           MOVE F740-NUM-NT-FT-QUEUE     TO OUT1-QUEUE-NUM.                     
           MOVE F740-NUM-NRIC1           TO OUT1-NRIC-HA1.                      
           MOVE F740-CDE-NT1             TO OUT1-NT.                            
           MOVE K022-NUM-OIC             TO OUT1-CSM.                           
           MOVE K813-AMT-INCOME-CEILING  TO OUT1-HH-INCOME.                     
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-FILES.                                                        
      *================================================================*        
                                                                                
           CLOSE   BP13F740                                                     
                   BP13K800                                                     
                   BP13K813                                                     
                   BP13K022                                                     
                   BP13OUT1.                                                    
                                                                                
                                                                                
           IF WS-K800-STAT NOT  = 00 AND 97                                     
              DISPLAY 'ERROR READING BP13K800 - STAT ' WS-K800-STAT             
           END-IF.                                                              
                                                                                
           IF WS-K813-STAT NOT  = 00 AND 97                                     
              DISPLAY 'ERROR READING BP13K813 - STAT ' WS-K813-STAT             
           END-IF.                                                              
                                                                                
           IF WS-K022-STAT NOT  = 00 AND 97                                     
              DISPLAY 'ERROR READING BP13K022 - STAT ' WS-K022-STAT             
           END-IF.                                                              
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '********* BP13CF24 CONTROL TOTALS *********'.               
           DISPLAY '*'.                                                         
           DISPLAY '* NO. OF F740 READ .............. : '                       
                    WS-F740-READ.                                               
           DISPLAY '* NO. OF OUT1 WRITTEN............ : '                       
                    WS-OUT1-WRT.                                                
           DISPLAY '* NO. OF UNMATCHED INCOME........ : '                       
                    WS-UNMATCH.                                                 
           DISPLAY '* NO. CASE  W/ NO QUEUE #........ : '                       
                    WS-NO-QUEUE.                                                
           DISPLAY '* NO. OF NOT FND IN K800/K813.... : '                       
                    WS-NFND-K800-K813.                                          
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
