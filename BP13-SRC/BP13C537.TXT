      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C537.                                                 
      *AUTHOR.        MARGE ROSALES.                                            
      *DATE-WRITTEN.  05 JUNE 2020.                                             
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : TO CREATE OBF RECORDS TO BP13K290, BP13KQ00,*           
      *                 AND BP13KQ10 (JUST LIKE BP13SRY0)           *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13F800 -- BP13.F800.APPLN.BP13D539.OBF              *           
      *   2.  BP13K800 -- BP13.K800.APPLN                           *           
      *                                                             *           
      *   I-O FILES :                                               *           
      *   1.  BP13K290 -- BP13.K290.QUEUE                           *           
      *   2.  BP13KQ00 -- BP13.KQ00.REGN.APPT                       *           
      *   3.  BP13KQ10 -- BP13.KQ10.DOC.TBL                         *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP138345 05/06/2020 MRR5 NEW PROGRAM                        *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K290 ASSIGN TO BP13K290                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K290-KEY-FLD                               
                  FILE STATUS     IS BP13K290-STATUS.                           
                                                                                
           SELECT BP13KQ00 ASSIGN TO BP13KQ00                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KQ00-KEY-FLD                               
                  FILE STATUS     IS BP13KQ00-STATUS.                           
                                                                                
           SELECT BP13KQ10 ASSIGN TO BP13KQ10                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KQ10-KEY-FLD                               
                  FILE STATUS     IS BP13KQ10-STATUS.                           
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS BP13K800-STATUS.                           
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13K290                                                            
            RECORD CONTAINS  100 CHARACTERS.                                    
       COPY BP13K290.                                                           
                                                                                
       FD   BP13KQ00                                                            
            RECORD CONTAINS  150 CHARACTERS.                                    
       COPY BP13KQ00.                                                           
                                                                                
       FD  BP13KQ10                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13KQ10.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD   BP13F800                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-CURR-DATE           PIC X(08) VALUE ZEROES.                       
       01  WS-CURR-TIME           PIC X(08) VALUE ZEROES.                       
       01  WS-K290-WRITE-CNT      PIC 9(08) VALUE ZEROES.                       
       01  WS-K290-DUPL-REC       PIC 9(08) VALUE ZEROES.                       
       01  WS-K290-EXIST          PIC 9(08) VALUE ZEROES.                       
       01  WS-K290-WRITE-ERR      PIC 9(08) VALUE ZEROES.                       
       01  WS-KQ00-WRITE-CNT      PIC 9(08) VALUE ZEROES.                       
       01  WS-KQ00-DUPL-REC       PIC 9(08) VALUE ZEROES.                       
       01  WS-KQ00-WRITE-ERR      PIC 9(08) VALUE ZEROES.                       
       01  WS-KQ00-EXIST          PIC 9(08) VALUE ZEROES.                       
       01  WS-KQ10-WRITE-CNT      PIC 9(08) VALUE ZEROES.                       
       01  WS-KQ10-DUPL-REC       PIC 9(08) VALUE ZEROES.                       
       01  WS-KQ10-WRITE-ERR      PIC 9(08) VALUE ZEROES.                       
       01  WS-KQ10-EXIST          PIC 9(08) VALUE ZEROES.                       
       01  WS-K800-READ-CNT       PIC 9(08) VALUE ZEROES.                       
       01  WS-K800-FND            PIC X     VALUE SPACES.                       
       01  WS-F800-READ-CNT       PIC 9(08) VALUE ZEROES.                       
       01  WS-F800-EOF            PIC X     VALUE SPACES.                       
       01  BP13K290-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KQ00-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KQ10-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13K800-STATUS        PIC 9(02) VALUE ZEROES.                       
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F800    THRU 2000-EXIT.                            
           PERFORM 2500-PROCESS-REC  THRU 2500-EXIT                             
                   UNTIL WS-F800-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13K800                                                 
                I-O    BP13K290                                                 
                       BP13KQ00                                                 
                       BP13KQ10.                                                
                                                                                
           IF BP13K290-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K290 : ' BP13K290-STATUS             
              MOVE BP13K290-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KQ00-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KQ00 : ' BP13KQ00-STATUS             
              MOVE BP13KQ00-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13KQ10-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KQ10 : ' BP13KQ10-STATUS             
              MOVE BP13KQ10-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K800 : ' BP13K800-STATUS             
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           ACCEPT WS-CURR-DATE FROM DATE YYYYMMDD.                              
           ACCEPT WS-CURR-TIME FROM TIME.                                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F800.                                                          
      *---------------*                                                         
                                                                                
           READ BP13F800 AT END                                                 
                MOVE HIGH-VALUES TO BP13F800-MASTER                             
                MOVE 'Y' TO WS-F800-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F800-READ-CNT.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       2500-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           IF F800-NUM-REGN NOT = SPACES AND LOW-VALUES AND ZEROES              
              MOVE F800-NUM-REGN TO K800-NUM-REGN                               
              PERFORM 2700-READ-BP13K800 THRU 2700-EXIT                         
              IF WS-K800-FND = 'Y'                                              
                 PERFORM 3000-CREATE-BP13K290 THRU 3000-EXIT                    
                 PERFORM 4000-CREATE-BP13KQ00 THRU 4000-EXIT                    
                 PERFORM 5000-CREATE-BP13KQ10 THRU 5000-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F800        THRU 2000-EXIT.                        
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2700-READ-BP13K800.                                                      
      *-------------------*                                                     
           READ BP13K800.                                                       
           EVALUATE BP13K800-STATUS                                             
             WHEN   00                                                          
                    MOVE 'Y'         TO WS-K800-FND                             
                    ADD 1            TO WS-K800-READ-CNT                        
             WHEN   23                                                          
                    MOVE 'N'         TO WS-K800-FND                             
             WHEN   OTHER                                                       
                    DISPLAY 'READ BP13K800 ERROR ' K800-NUM-REGN                
                    DISPLAY 'STATUS ' BP13K800-STATUS                           
                    MOVE BP13K800-STATUS TO RETURN-CODE                         
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
       2700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3000-CREATE-BP13K290.                                                    
      *--------------------*                                                    
            MOVE SPACES                         TO BP13K290-REC.                
            INITIALIZE                             BP13K290-REC.                
                                                                                
            MOVE K800-NUM-NT-FT-QUEUE           TO K290-NUM-QUEUE.              
            MOVE K800-NUM-REGN                  TO K290-NUM-REGN.               
                                                                                
            READ BP13K290 KEY IS K290-KEY-FLD.                                  
            EVALUATE BP13K290-STATUS                                            
            WHEN 00                                                             
                 ADD 1 TO WS-K290-EXIST                                         
                                                                                
            WHEN 23                                                             
                 PERFORM 3050-MOVE-BP13K290  THRU 3050-EXIT                     
                 PERFORM 3100-WRITE-BP13K290 THRU 3100-EXIT                     
                                                                                
            WHEN OTHER                                                          
                 DISPLAY 'BP13K290 - ERROR READ, STATUS: '                      
                          BP13K290-STATUS                                       
            END-EVALUATE.                                                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3050-MOVE-BP13K290.                                                      
      *-------------------*                                                     
                                                                                
           MOVE K800-NUM-NT-FT-QUEUE           TO K290-NUM-QUEUE                
                                                  K290-NUM-QUEUE-ALT.           
           MOVE K800-NUM-REGN                  TO K290-NUM-REGN.                
           MOVE 'OBF'                          TO K290-NUM-NEW-TOWN.            
           MOVE 'OB'                           TO K290-NUM-FLAT-TYPE.           
           MOVE K800-DTE-BK-APPT               TO K290-DTE-APPMT.               
           MOVE K800-TME-BK-APPT               TO K290-TME-APPMT.               
           MOVE 'BK'                           TO K290-NUM-APPMT-TYPE.          
           MOVE 'OBF'                          TO K290-NUM-SELECT-TYPE.         
           MOVE K800-DTE-BALLOT                TO K290-DTE-BALLOT.              
           MOVE '##'                           TO K290-NUM-CSO(1:2)             
           MOVE K800-NUM-REGN(3:6)             TO K290-NUM-CSO(3:6)             
           MOVE K800-NUM-STATUS                TO K290-NUM-STATUS.              
           MOVE 'BP13C537'                     TO K290-NUM-USERID.              
           MOVE WS-CURR-DATE                   TO K290-DTE-UPDATE.              
           MOVE WS-CURR-TIME                   TO K290-TME-UPDATE.              
                                                                                
       3050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3100-WRITE-BP13K290.                                                     
      *--------------------*                                                    
                                                                                
           WRITE BP13K290-REC.                                                  
                                                                                
           IF BP13K290-STATUS = 0                                               
              ADD   1  TO WS-K290-WRITE-CNT                                     
           ELSE                                                                 
           IF BP13K290-STATUS = 2 OR 22                                         
              ADD   1  TO WS-K290-DUPL-REC                                      
              DISPLAY 'DUPLICATE RECORD IN BP13K290:'                           
              DISPLAY ' KEY     : ' KQ00-KEY-FLD                                
           ELSE                                                                 
           IF BP13K290-STATUS NOT = 0                                           
              MOVE BP13K290-STATUS TO RETURN-CODE                               
              DISPLAY 'WRITE ERROR, STATUS : ' BP13K290-STATUS                  
              DISPLAY ' KEY     : ' K290-KEY-FLD                                
              ADD   1   TO WS-K290-WRITE-ERR                                    
              GO        TO 9000-CLOSE-FILES.                                    
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       4000-CREATE-BP13KQ00.                                                    
      *--------------------*                                                    
                                                                                
            MOVE SPACES                         TO BP13KQ00-REC.                
            INITIALIZE                             BP13KQ00-REC.                
                                                                                
            MOVE 'OBF'                          TO KQ00-NUM-ALLO-MODE2          
                                                   KQ00-NUM-NEW-TOWN2.          
            MOVE K800-DTE-BALLOT                TO KQ00-DTE-BALLOT2.            
            MOVE 'OB'                           TO KQ00-NUM-FLAT-TYPE2.         
            MOVE K800-NUM-REGN                  TO KQ00-NUM-REGN.               
                                                                                
            READ BP13KQ00 KEY IS KQ00-KEY-FLD.                                  
            EVALUATE BP13KQ00-STATUS                                            
            WHEN 00                                                             
                 ADD 1 TO WS-KQ00-EXIST                                         
                                                                                
            WHEN 23                                                             
                 PERFORM 4050-MOVE-BP13KQ00  THRU 4050-EXIT                     
                 PERFORM 4100-WRITE-BP13KQ00 THRU 4100-EXIT                     
                                                                                
            WHEN OTHER                                                          
                 DISPLAY 'BP13KQ00 - ERROR READ, STATUS: '                      
                          BP13KQ00-STATUS                                       
            END-EVALUATE.                                                       
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4050-MOVE-BP13KQ00.                                                      
      *-------------------*                                                     
                                                                                
           MOVE K800-NUM-NT-FT-QUEUE           TO KQ00-NUM-QUEUE.               
           MOVE K800-NUM-REGN                  TO KQ00-NUM-REGN.                
           MOVE 'OBF'                          TO KQ00-NUM-NEW-TOWN             
                                                  KQ00-NUM-NEW-TOWN2            
                                                  KQ00-NUM-ALLO-MODE            
                                                  KQ00-NUM-ALLO-MODE2.          
           MOVE 'OB'                           TO KQ00-NUM-FLAT-TYPE            
                                                  KQ00-NUM-FLAT-TYPE2.          
           MOVE K800-DTE-BK-APPT               TO KQ00-DTE-APPMT.               
           MOVE K800-TME-BK-APPT               TO KQ00-TME-APPMT.               
           MOVE K800-DTE-BALLOT                TO KQ00-DTE-BALLOT               
                                                  KQ00-DTE-BALLOT2.             
           MOVE 'N'                            TO KQ00-NUM-BONUS.               
                                                                                
       4050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       4100-WRITE-BP13KQ00.                                                     
      *--------------------*                                                    
                                                                                
           WRITE BP13KQ00-REC.                                                  
                                                                                
           IF BP13KQ00-STATUS = 0                                               
              ADD   1  TO WS-KQ00-WRITE-CNT                                     
           ELSE                                                                 
           IF BP13KQ00-STATUS = 2 OR 22                                         
              ADD   1  TO WS-KQ00-DUPL-REC                                      
              DISPLAY 'DUPLICATE RECORD IN BP13KQ00:'                           
              DISPLAY ' KEY     : ' KQ00-KEY-FLD                                
           ELSE                                                                 
           IF BP13KQ00-STATUS NOT = 0                                           
              MOVE BP13KQ00-STATUS TO RETURN-CODE                               
              DISPLAY 'WRITE ERROR, STATUS : ' BP13KQ00-STATUS                  
              DISPLAY ' KEY     : ' KQ00-KEY-FLD                                
              ADD   1   TO WS-KQ00-WRITE-ERR                                    
              GO        TO 9000-CLOSE-FILES.                                    
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       5000-CREATE-BP13KQ10.                                                    
      *--------------------*                                                    
                                                                                
            MOVE SPACES                         TO BP13KQ10-REC.                
            INITIALIZE                             BP13KQ10-REC.                
                                                                                
            MOVE K800-NUM-REGN                  TO KQ10-NUM-REGN.               
                                                                                
            READ BP13KQ10 KEY IS KQ10-KEY-FLD.                                  
            EVALUATE BP13KQ10-STATUS                                            
            WHEN 00                                                             
                 ADD 1 TO WS-KQ10-EXIST                                         
                                                                                
            WHEN 23                                                             
                 PERFORM 5050-MOVE-BP13KQ10  THRU 5050-EXIT                     
                 PERFORM 5100-WRITE-BP13KQ10 THRU 5100-EXIT                     
                                                                                
            WHEN OTHER                                                          
                 DISPLAY 'BP13KQ10 - ERROR READ, STATUS: '                      
                          BP13KQ10-STATUS                                       
            END-EVALUATE.                                                       
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       5050-MOVE-BP13KQ10.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                      TO BP13KQ10-REC.                    
           INITIALIZE                          BP13KQ10-REC.                    
           MOVE K800-NUM-REGN               TO KQ10-NUM-REGN                    
                                               KQ10-NUM-REGN1                   
                                               KQ10-NUM-REGN2                   
                                               KQ10-NUM-REGN3                   
                                               KQ10-NUM-REGN4                   
                                               KQ10-NUM-REGN5.                  
           MOVE K800-DTE-BALLOT             TO KQ10-DTE-BALLOT.                 
           MOVE ALL '9'                     TO KQ10-DTE-LATEST-DOC-DUE          
                                               KQ10-DTE-LATEST-DOC-DUE2.        
           MOVE K800-NUM-NEW-TOWN           TO KQ10-NUM-NEW-TOWN.               
           MOVE K800-NUM-FLAT-TYPE          TO KQ10-NUM-FLAT-TYPE.              
           MOVE K800-NUM-ELIG-SCHEME        TO KQ10-NUM-ELIG-SCHEME.            
           MOVE K800-NUM-ALLO-SCHEME        TO KQ10-NUM-ALLO-SCHEME.            
           MOVE K800-NUM-ALLO-CAT           TO KQ10-NUM-SEL-TYPE.               
           MOVE 'BP13C537'                  TO KQ10-NUM-USERID.                 
           MOVE WS-CURR-DATE                TO KQ10-DTE-UPDATE.                 
           MOVE WS-CURR-TIME                TO KQ10-TME-UPDATE.                 
                                                                                
       5050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       5100-WRITE-BP13KQ10.                                                     
      *--------------------*                                                    
                                                                                
           WRITE BP13KQ10-REC.                                                  
                                                                                
           IF BP13KQ10-STATUS = 0                                               
              ADD   1  TO WS-KQ10-WRITE-CNT                                     
           ELSE                                                                 
           IF BP13KQ10-STATUS = 2 OR 22                                         
              ADD   1  TO WS-KQ10-DUPL-REC                                      
              DISPLAY 'DUPLICATE RECORD IN BP13KQ10:'                           
              DISPLAY ' KEY     : ' KQ00-KEY-FLD                                
           ELSE                                                                 
           IF BP13KQ10-STATUS NOT = 0                                           
              MOVE BP13KQ10-STATUS TO RETURN-CODE                               
              DISPLAY 'WRITE ERROR, STATUS : ' BP13KQ10-STATUS                  
              DISPLAY ' KEY     : ' KQ10-KEY-FLD                                
              ADD   1   TO WS-KQ10-WRITE-ERR                                    
              GO        TO 9000-CLOSE-FILES.                                    
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13C537  *************'.                           
           DISPLAY 'RECS READ FROM  BP13F800    : ' WS-F800-READ-CNT.           
           DISPLAY 'RECS READ FROM  BP13K800    : ' WS-K800-READ-CNT.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN  IN BP13K290   : ' WS-K290-WRITE-CNT.          
           DISPLAY 'RECS EXIST IN BP13K290      : ' WS-K290-EXIST.              
           DISPLAY 'RECS WRITTEN  IN BP13KQ00   : ' WS-KQ00-WRITE-CNT.          
           DISPLAY 'RECS EXIST IN BP13KQ00      : ' WS-KQ00-EXIST.              
           DISPLAY 'RECS WRITTEN  IN BP13KQ10   : ' WS-KQ10-WRITE-CNT.          
           DISPLAY 'RECS EXIST IN BP13KQ10      : ' WS-KQ10-EXIST.              
                                                                                
           CLOSE BP13K290                                                       
                 BP13KQ00                                                       
                 BP13KQ10                                                       
                 BP13K800                                                       
                 BP13F800.                                                      
                                                                                
           IF BP13K290-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K290-STATUS ' BP13K290-STATUS          
            MOVE BP13K290-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13KQ00-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KQ00-STATUS ' BP13KQ00-STATUS          
            MOVE BP13KQ00-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13KQ10-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KQ10-STATUS ' BP13KQ10-STATUS          
            MOVE BP13KQ10-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K800-STATUS ' BP13K800-STATUS          
            MOVE BP13K800-STATUS TO RETURN-CODE.                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
