      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C605.                                                 
       AUTHOR.        ANNALYN C BANTA.                                          
       DATE-WRITTEN.  26/07/94.                                                 
                                                                                
      * ====================================================== *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO PASS THE CASES THAT REQUIRE        *                
      *                  CPF STATEMENT.                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE     DETAILS                         *                
      * -------  ---  ------   -----------------------------   *                
      * BP139    BAL  031094   TO DELETE BP13F452.             *                
      * BP130154 LJL  021196   TO CATER FOR CPF BACKUP.        *                
      * BP130418 SIA  031398   CHGE BM06K211 TO BM06K110.      *                
      * BP130839 JMS  200500   TO CATER FOR NEW APPT TYPES     *                
      * BP130929 ESC  231200   INCLUDE LOAN-SCHEME IN BP13F870 *                
      * BP133121 MTSR 121007   FOR NON-CONC LOAN               *                
      * ====================================================== *                
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
      *---------------------*                                                   
       INPUT-OUTPUT SECTION.                                                    
      *---------------------*                                                   
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F454 ASSIGN TO BP13F454.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS K800-STATUS.                               
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS K820-STATUS.                               
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K110-KEY-FLD                               
                  ALTERNATE RECORD KEY IS K110-REGN-NO                          
                  FILE STATUS     IS K110-STATUS.                               
                                                                                
           SELECT BP13K640 ASSIGN TO BP13K640                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K640-KEY-FLD                               
                  FILE STATUS     IS K640-STATUS.                               
                                                                                
           SELECT AB02F870 ASSIGN TO AB02F870.                                  
                                                                                
                                                                                
      *-------------*                                                           
       DATA DIVISION.                                                           
      *-------------*                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F454                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F454.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BP13K640                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K640.                                                           
                                                                                
       FD  AB02F870                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY AB02F870.                                                           
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
       01  WS-FILE-STATUS.                                                      
           05 K800-STATUS                 PIC 99    VALUE ZEROS.                
           05 K820-STATUS                 PIC 99    VALUE ZEROS.                
           05 K110-STATUS                 PIC 99    VALUE ZEROS.                
           05 K640-STATUS                 PIC 99    VALUE ZEROS.                
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F454-READ                PIC 9(5)  VALUE 0.                    
           05 WS-F870-WRITE               PIC 9(5)  VALUE 0.                    
                                                                                
       01  WS-EOF-F454                    PIC 9     VALUE 0.                    
           88 EOF-F454                              VALUE 1.                    
                                                                                
       01  WS-CONTROL-DATE.                                                     
           05 WS-CTRL-CC                  PIC XX.                               
           05 WS-CTRL-YY                  PIC XX.                               
           05 WS-CTRL-MM                  PIC XX.                               
           05 WS-CTRL-DD                  PIC XX.                               
                                                                                
       01  WS-DATE.                                                             
           05 WS-CCYY.                                                          
              10 WS-CC                    PIC 99.                               
              10 WS-YY                    PIC 99.                               
           05 WS-CCYY1 REDEFINES WS-CCYY  PIC 9(4).                             
           05 WS-MM                       PIC 99.                               
           05 WS-DD                       PIC 99.                               
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *------------------*                                                      
       000-MAIN-ROUTINE.                                                        
      *------------------*                                                      
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 220-READ-BP13F454  THRU 220-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL EOF-F454.                                                    
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       100-INITIALIZATION.                                                      
      *------------------*                                                      
           DISPLAY '100-EXIT'.                                                  
                                                                                
           OPEN INPUT  BP13F454                                                 
                       BP13K640                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BM06K110                                                 
                OUTPUT AB02F870.                                                
                                                                                
           MOVE ZEROS  TO WS-COUNTERS.                                          
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       220-READ-BP13F454.                                                       
      *------------------*                                                      
           DISPLAY '220-READ-BP13F454'.                                         
           READ BP13F454 AT END                                                 
                MOVE 1   TO WS-EOF-F454                                         
                GO TO 220-EXIT.                                                 
                                                                                
           DISPLAY F454-NUM-REGN.                                               
                                                                                
           ADD  1  TO WS-F454-READ.                                             
                                                                                
       220-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       300-PROCESS-RTN.                                                         
      *------------------*                                                      
           DISPLAY '300-PROCESS-RTN'.                                           
           PERFORM 400-WRITE-OUTPUT  THRU 400-EXIT.                             
           PERFORM 220-READ-BP13F454 THRU 220-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       400-WRITE-OUTPUT.                                                        
      *------------------*                                                      
           DISPLAY '400-WRITE-OUTPUT'.                                          
           MOVE SPACES                  TO AB02F870-REC.                        
           MOVE F454-NUM-REGN           TO K800-NUM-REGN.                       
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF K800-STATUS = 00                                                  
              NEXT SENTENCE                                                     
           ELSE                                                                 
              IF K800-STATUS = 23                                               
                 DISPLAY 'RECORD NOT FOUND IN BP13K800, ' K800-NUM-REGN         
                 GO TO 400-EXIT                                                 
              ELSE                                                              
                 MOVE K800-STATUS       TO RETURN-CODE                          
                 DISPLAY 'ERROR READING BP13K800, ' K800-NUM-REGN               
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
           MOVE 'HPI'                   TO F870-NUM-SYSTEM.                     
           MOVE K800-NUM-REGN           TO F870-NUM-REGN.                       
           MOVE K800-NUM-SCH-ACC        TO F870-NUM-HDB-REF.                    
                                                                                
           IF F454-CDE-APPT-TYPE = 'SA'                                         
              MOVE 'B1'                 TO F870-NUM-TRANS                       
              MOVE ZEROS                TO F870-AMT-LOAN-GRANT                  
              MOVE ZEROS                TO F870-DTE-LOAN-COMM                   
           ELSE                                                                 
           IF F454-CDE-APPT-TYPE = 'TP' OR 'SP' OR 'SR'                         
              IF K800-NUM-CA-TAG = 'N' OR SPACES                                
                 MOVE 'B1'                    TO F870-NUM-TRANS                 
              ELSE                                                              
                 MOVE 'P1'                    TO F870-NUM-TRANS                 
              END-IF                                                            
              MOVE K800-NUM-REGN           TO K110-REGN-NO                      
              PERFORM 420-READ-BM06K110    THRU 420-EXIT                        
              MOVE K110-CURR-SELLING-PRICE TO F870-AMT-LOAN-GRANT               
              MOVE F454-DTE-APPT           TO WS-DATE                           
                                                                                
              IF WS-DD < 16                                                     
                 MOVE WS-DATE           TO F870-DTE-LOAN-COMM                   
              ELSE                                                              
                 IF WS-MM = 12                                                  
                    ADD  1  TO WS-CCYY1                                         
                    MOVE 1  TO WS-MM WS-DD                                      
                 ELSE                                                           
                    ADD  1  TO WS-MM                                            
                    MOVE 1  TO WS-DD                                            
                 END-IF                                                         
                 MOVE WS-DATE           TO F870-DTE-LOAN-COMM                   
              END-IF                                                            
                                                                                
              IF (WS-DD > 15) AND (WS-MM = 02) AND (WS-CCYY1 = 2001)            
                 MOVE '20010301'        TO F870-DTE-LOAN-COMM                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE K800-NUM-REGN           TO K820-NUM-REGN.                       
           MOVE K800-NUM-NRIC1          TO K820-NUM-NRIC.                       
           PERFORM 410-READ-BP13K820    THRU 410-EXIT.                          
           MOVE K820-NUM-NRIC           TO F870-NUM-UIN-LSE1.                   
           MOVE K820-NUM-CPF            TO F870-NUM-CPF1.                       
                                                                                
           IF K800-NUM-NRIC2          = LOW-VALUES OR SPACES                    
              NEXT SENTENCE                                                     
           ELSE                                                                 
              MOVE K800-NUM-REGN        TO K820-NUM-REGN                        
              MOVE K800-NUM-NRIC2       TO K820-NUM-NRIC                        
              PERFORM 410-READ-BP13K820 THRU 410-EXIT                           
              MOVE K820-NUM-NRIC        TO F870-NUM-UIN-LSE2                    
              MOVE K820-NUM-CPF         TO F870-NUM-CPF2.                       
                                                                                
           IF K800-NUM-NRIC3          = LOW-VALUES OR SPACES                    
              NEXT SENTENCE                                                     
           ELSE                                                                 
              MOVE K800-NUM-REGN        TO K820-NUM-REGN                        
              MOVE K800-NUM-NRIC3       TO K820-NUM-NRIC                        
              PERFORM 410-READ-BP13K820 THRU 410-EXIT                           
              MOVE K820-NUM-NRIC        TO F870-NUM-UIN-LSE3                    
              MOVE K820-NUM-CPF         TO F870-NUM-CPF3.                       
                                                                                
           IF K800-NUM-NRIC4          = LOW-VALUES OR SPACES                    
              NEXT SENTENCE                                                     
           ELSE                                                                 
              MOVE K800-NUM-REGN        TO K820-NUM-REGN                        
              MOVE K800-NUM-NRIC4       TO K820-NUM-NRIC                        
              PERFORM 410-READ-BP13K820 THRU 410-EXIT                           
              MOVE K820-NUM-NRIC        TO F870-NUM-UIN-LSE4                    
              MOVE K820-NUM-CPF         TO F870-NUM-CPF4.                       
                                                                                
           MOVE 'SS'                    TO F870-NUM-OFFICE                      
                                           F870-NUM-ORIGINATOR.                 
           MOVE 'P13C605'               TO F870-NUM-USERID.                     
                                                                                
           PERFORM 430-READ-BP13K640    THRU 430-EXIT.                          
                                                                                
           IF K640-CDE-LOAN-OPTION = 'A'                                        
              MOVE 'M'                  TO F870-NUM-LOAN-SCHEME                 
           ELSE                                                                 
             MOVE K640-CDE-LOAN-OPTION  TO F870-NUM-LOAN-SCHEME                 
           END-IF.                                                              
                                                                                
           WRITE AB02F870-REC.                                                  
           ADD 1     TO  WS-F870-WRITE.                                         
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       410-READ-BP13K820.                                                       
      *------------------*                                                      
           READ BP13K820.                                                       
                                                                                
           IF K820-STATUS = 00                                                  
              NEXT SENTENCE                                                     
           ELSE                                                                 
              IF K820-STATUS = 23                                               
                 DISPLAY 'RECORD NOT FOUND IN BP13K820, ' K820-KEY-FLD          
                 MOVE SPACES            TO K820-NUM-NRIC                        
                                           K820-NUM-CPF                         
              ELSE                                                              
                 MOVE K820-STATUS       TO RETURN-CODE                          
                 DISPLAY 'ERROR READING BP13K820, ' K820-KEY-FLD                
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       420-READ-BM06K110.                                                       
      *------------------*                                                      
           READ BM06K110 KEY IS K110-REGN-NO.                                   
                                                                                
           IF K110-STATUS = 00 OR 02                                            
              NEXT SENTENCE                                                     
           ELSE                                                                 
              IF K110-STATUS = 23                                               
                 DISPLAY 'RECORD NOT FOUND IN BM06K110, ' K110-REGN-NO          
                 MOVE ZEROS             TO K110-CURR-SELLING-PRICE              
                                           K110-DTE-SO-EXECUTING                
              ELSE                                                              
                 MOVE K110-STATUS       TO RETURN-CODE                          
                 DISPLAY 'ERROR READING BM06K110, ' K110-REGN-NO                
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------*                                                      
       430-READ-BP13K640.                                                       
      *------------------*                                                      
           MOVE SPACES             TO  BP13K640-TRANS-REC.                      
           MOVE K800-NUM-REGN      TO  K640-KEY-FLD.                            
           READ BP13K640       KEY IS  K640-KEY-FLD.                            
                                                                                
           IF K640-STATUS = 00 OR 02                                            
                 DISPLAY 'RECORD FOUND IN BP13K640, ' K640-KEY-FLD              
           ELSE                                                                 
              IF K640-STATUS = 23                                               
                 DISPLAY 'RECORD NOT FOUND IN BP13K640, ' K640-KEY-FLD          
              ELSE                                                              
                 MOVE K640-STATUS       TO RETURN-CODE                          
                 DISPLAY 'ERROR READING BP13K640, ' K640-KEY-FLD                
                 GO TO 999-CLOSE-FILES.                                         
                                                                                
       430-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------*                                                          
       999-CLOSE-FILES.                                                         
      *--------------*                                                          
           CLOSE BP13F454                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K640                                                       
                 BM06K110                                                       
                 AB02F870.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C605 ================*'.              
           DISPLAY '    NO. OF F454 RECORDS READ     = ' WS-F454-READ.          
           DISPLAY '    NO. OF F870 RECORDS WRITTEN  = ' WS-F870-WRITE.         
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
