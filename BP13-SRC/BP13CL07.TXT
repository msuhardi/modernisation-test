       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CL07.                                                 
       AUTHOR.        ELGER DE GUZMAN                                           
      *DATE-WRITTEN.  06/01/2015.                                               
      * ===============================================================*        
      * SYSTEM NAME (SYSTEM ID)                                        *        
      * ===============================================================*        
      * OBJECTIVE   : PROGRAM TO GET ALL CASES THAT WERE NOT FILED TO  *        
      *               CM.                                                       
      *                                                                         
      * OUTPUT DESCRIPTION:                                            *        
      *                                                                *        
      * BP13K68A : REWRITE RECORD TO UPDATE CPF BALANCE.               *        
      *                                                                *        
      *   INPUT FILES :  1.  BP13F300  - BP13.F300.SUCCESS             *        
      *                                                                *        
      *   I-O            1.  BP13KL00  - BP13.KL00.LETTER              *        
      *                                                                *        
      *   OUTPUT      :  1.  BP13LL07  - CASE NOT FILED REPORT         *        
      *                  2.  OUTF300   - NOT FILED CASE F300 FORMAT    *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ------------------------------------*        
      *                                                                *        
      *   BP135621  ESD1  12/01/15 NEW PROGRAM                         *        
      *   BP135638  ESD1  17/02/15 CATER TO REMOVE TAGGING OF 'Y'      *        
      *                            IN KL00.                            *        
      *   BP135710  RJB1  07/05/15 TO INCLUDE CRE NAME IN THE REPORT   *        
      *================================================================*        
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F300  ASSIGN        TO BP13F300.                          
           SELECT BP13LL07  ASSIGN        TO BP13LL07.                          
           SELECT OUTF300   ASSIGN        TO OUTF300.                           
                                                                                
           SELECT BP13KL00  ASSIGN        TO BP13KL00                           
                            ACCESS MODE      IS RANDOM                          
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS KL00-KEY-FLD                    
                            FILE STATUS      IS WS-KL00-STATUS.                 
                                                                                
           SELECT SY08K010  ASSIGN        TO SY08K010                           
                            ACCESS MODE      IS DYNAMIC                         
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS K010-NUM-USERID                 
                            FILE STATUS      IS WS-K010-STATUS.                 
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F300                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13F300.                                                           
                                                                                
       FD  BP13LL07                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13LL07-REC        PIC X(132).                                      
                                                                                
       FD  OUTF300                                                              
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 2000 CHARACTERS.                                     
       01  OUTF300-REC        PIC X(2000).                                      
                                                                                
       FD  BP13KL00                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13KL00.                                                           
                                                                                
       FD  SY08K010                                                             
           RECORD CONTAINS 100  CHARACTERS.                                     
       COPY SY08K010.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY                  PIC 9(04).                          
           05  WS-CUR-MM                    PIC 9(02).                          
           05  WS-CUR-DD                    PIC 9(02).                          
                                                                                
       01  WS-COUNT.                                                            
           05  WS-F300-INPUT                PIC 9(08)  VALUE 0.                 
           05  WS-LL07-OUT                  PIC 9(08)  VALUE 0.                 
           05  WS-F300-OUT                  PIC 9(08)  VALUE 0.                 
           05  WS-UPD-CNT                   PIC 9(08)  VALUE 0.                 
                                                                                
       01  WS-TIME                          PIC 9(08)  VALUE 0.                 
       01  WS-F300-EOF                      PIC X(01)  VALUE SPACES.            
       01  WS-FOUND-KL00                    PIC X(01)  VALUE SPACES.            
       01  WS-FILE-STATUS.                                                      
           05 WS-KL00-STATUS              PIC 99    VALUE ZEROS.                
           05 WS-K010-STATUS              PIC 99    VALUE ZEROS.                
                                                                                
       01  WS-PRINT-VARIABLES.                                                  
           05 LL07-DTL1.                                                        
              10 FILLER                   PIC X(11)   VALUE SPACES.             
              10 LL07-REGN                PIC X(8)    VALUE SPACES.             
              10 FILLER                   PIC X(17)   VALUE SPACES.             
              10 LL07-SCH-ACCT            PIC X(11)   VALUE SPACES.             
              10 FILLER                   PIC X(12)   VALUE SPACES.             
              10 LL07-APPT-TYPE           PIC X(02)   VALUE SPACES.             
              10 FILLER                   PIC X(08)   VALUE SPACES.             
              10 LL07-SOCA-DTE            PIC X(08)   VALUE SPACES.             
              10 FILLER                   PIC X(06)   VALUE SPACES.             
              10 LL07-CRE-ID              PIC X(09)   VALUE SPACES.             
              10 FILLER                   PIC X(05)   VALUE SPACES.             
              10 LL07-CRE-NME             PIC X(20)   VALUE SPACES.             
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================*           
       MAIN.                                                                    
      *=============================================================*           
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-READ-INPUT      THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-DATA    THRU 3000-EXIT                          
           UNTIL WS-F300-EOF = 'Y'.                                             
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
      *=============================================================*           
       1000-OPEN-ROUTINE.                                                       
      *=============================================================*           
                                                                                
           OPEN INPUT  BP13F300                                                 
                       SY08K010                                                 
                I-O    BP13KL00                                                 
                OUTPUT BP13LL07                                                 
                       OUTF300.                                                 
                                                                                
           INITIALIZE  BP13KL00-LETTER BP13F300-REC                             
                       OUTF300-REC BP13LL07-REC.                                
                                                                                
           IF WS-KL00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KL00 ERROR ' WS-KL00-STATUS                  
              MOVE     WS-KL00-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING SY08K010 ERROR ' WS-K010-STATUS                  
              MOVE     WS-K010-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-TIME.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2000-READ-INPUT.                                                         
      *=============================================================*           
                                                                                
            READ BP13F300                                                       
                 AT END MOVE 'Y'  TO WS-F300-EOF                                
            END-READ.                                                           
                                                                                
            IF WS-F300-EOF NOT = 'Y'                                            
               ADD 1 TO WS-F300-INPUT                                           
            END-IF.                                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       3000-PROCESS-DATA.                                                       
      *=============================================================*           
                                                                                
           MOVE SPACES  TO BP13KL00-LETTER OUTF300-REC BP13LL07-REC             
                           WS-FOUND-KL00.                                       
                                                                                
           INITIALIZE  BP13KL00-LETTER OUTF300-REC BP13LL07-REC.                
                                                                                
                                                                                
           MOVE     F300-CUR-REG        TO   KL00-NUM-REGN                      
           MOVE     'SADOC'             TO   KL00-NUM-STAGE                     
           MOVE     'AFL  '             TO   KL00-NUM-DOC-TYPE                  
                                                                                
           PERFORM  3500-READ-BP13KL00  THRU 3500-EXIT                          
                                                                                
           IF WS-FOUND-KL00 = 'Y'                                               
              MOVE 'Y'   TO   KL00-CDE-INTERNET                                 
              PERFORM 3600-REWRITE-BP13KL00 THRU  3600-EXIT                     
           ELSE                                                                 
              ADD 1 TO WS-F300-OUT                                              
              PERFORM 4000-CREATE-REPORT THRU 4000-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-INPUT      THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3500-READ-BP13KL00.                                                      
      *================================================================*        
                                                                                
           READ BP13KL00.                                                       
                                                                                
           EVALUATE WS-KL00-STATUS                                              
              WHEN 00                                                           
                MOVE 'Y' TO WS-FOUND-KL00                                       
              WHEN 23                                                           
                MOVE 'N' TO WS-FOUND-KL00                                       
              WHEN OTHER                                                        
                MOVE  WS-KL00-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BP13KL00 FILE : ' WS-KL00-STATUS         
                DISPLAY 'KL00-NUM-REGN = ' KL00-NUM-REGN                        
                PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------           
       3600-REWRITE-BP13KL00.                                                   
      *--------------------------------------------------------------           
           REWRITE BP13KL00-LETTER.                                             
                                                                                
           IF WS-KL00-STATUS = '00'                                             
              ADD 1 TO WS-UPD-CNT                                               
           ELSE                                                                 
              DISPLAY 'ERROR REWRITE BP13KL00 FILE : ' WS-KL00-STATUS           
              DISPLAY 'KL00-NUM-REGN = ' KL00-NUM-REGN                          
              MOVE WS-KL00-STATUS         TO RETURN-CODE                        
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       4000-CREATE-REPORT.                                                      
      *================================================================*        
                                                                                
           MOVE F300-DTE-SALE-AGRM  TO LL07-SOCA-DTE                            
           MOVE 'SA'                TO LL07-APPT-TYPE                           
           MOVE F300-USERID         TO LL07-CRE-ID                              
                                       K010-NUM-USERID                          
           PERFORM 8000-READ-SY08K010                                           
              THRU 8000-EXIT.                                                   
                                                                                
           MOVE K010-NME-USER   TO LL07-CRE-NME                                 
           MOVE F300-CUR-REG    TO LL07-REGN                                    
           MOVE F300-SCH-ACC    TO LL07-SCH-ACCT                                
                                                                                
           WRITE BP13LL07-REC    FROM LL07-DTL1.                                
           WRITE OUTF300-REC     FROM BP13F300-REC.                             
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       8000-READ-SY08K010.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ SY08K010.                                                       
                                                                                
           IF WS-K010-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-K010-STATUS = 23                                            
                 DISPLAY 'SY08K010 - RECORD NOT FND ('                          
                         K010-NUM-USERID ')'                                    
                 MOVE SPACES TO K010-NME-USER                                   
              ELSE                                                              
                 DISPLAY 'SY08K010 - ERROR READ (' WS-K010-STATUS ')'           
                 MOVE WS-K010-STATUS             TO RETURN-CODE                 
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-ROUTINE.                                                      
      *================================================================*        
           DISPLAY 'TOTAL INPUT RECORDS        : ' WS-F300-INPUT                
           DISPLAY 'TOTAL RECORD NOT FILED     : ' WS-F300-OUT.                 
           DISPLAY '        '                                                   
                                                                                
           CLOSE BP13F300                                                       
                 BP13KL00                                                       
                 BP13LL07                                                       
                 SY08K010                                                       
                 OUTF300.                                                       
                                                                                
           IF WS-KL00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KL00 ERROR ' WS-KL00-STATUS                  
              MOVE     WS-KL00-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING SY08K010 ERROR ' WS-K010-STATUS                  
              MOVE     WS-K010-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
