       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C20G.                                                 
      *AUTHOR.        LMS.                                                      
      *DATE-WRITTEN.  27/07/04                                                  
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE   :  PROGRAM TO OUTPUT                           *            
      * (COPY FROM BP13C227, TO CATER FOR DUXTON)                  *            
      *                                                            *            
      *                1)  OUTPUT THE FLAT AVAILABLE FOR EACH      *            
      *                    NT/BLK/FT OF THE BOOKING EXERCISE TO    *            
      *                    INTV AT THE TIME OF PRINTING INVITATION *            
      *                    LETTER. THE NO OF UNIT IS THE SAME AS   *            
      *                    THE PRICE LIST.                         *            
      *                                                            *            
      *                2)  RE-FORMAT THE BM06F800 TO THE INTV FILE *            
      *                    FORMAT WITH 3 REC TYPE                  *            
      *                    - HEADER,  REC-TY = 01                  *            
      *                    - DETAIL,  REC-TY = 02                  *            
      *                    - TRAILER, REC-TY = 03                  *            
      *                    - START-END BOOKING APPT., REC-TY = 05  *            
      *                                                            *            
      *  INPUT FILES :  1)  BP13F205  -  CONTROL RECORDS FILE      *            
      *                 2)  BM06F800  -  FLATLIST        FILE      *            
      *                 3)  BM06K510  -  ESTATE CODE     FILE      *            
      *                 4)  BP13K270  -  BOOKING PROGRAM FILE      *            
      *                 5)  BP13K816  -  BTO SITE INFORMATION      *            
      *                 6)  BP13K222  -  SUMMARY ALLOCATION FILE   *            
      *                                                            *            
      *  OUTPUT FILE :  1)  BP13F230  -  FLATLIST FILE(RT 01,02,03)*            
      *                 2)  P13F230B  -  FLATLIST FILE(RT 05)      *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHGE NO  OIC    DATE     DESCRIPTION                       *            
      * -------- ----  ------    -----------                       *            
      * BP132560 LMS  20040727   TO CATER FOR DUXTON.              *            
      * BP133056 RB12 20070614   CHANGE CAT QUOTA COMPUTATION      *            
      * BP133610 ESA1 20090424   TO REPLACE BP13K767 W/ BP13K816   *            
      * BP134731 PCL4 09/11/12   TO CATER FOR MODL-DESIGN 89       *            
      * BP134769 ZDD1 19/03/13   CHANGE PUNGGOL 21 TO PUNGGOL      *            
      * BP135838 ESA1 01/07/15   TO CATER FOR MODEL-DESIGN '92'    *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205  ASSIGN        TO  BP13F205.                         
                                                                                
           SELECT BM06F800  ASSIGN        TO  BM06F800.                         
                                                                                
           SELECT BM06K510  ASSIGN        TO  BM06K510                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  RANDOM                            
                            RECORD KEY    IS  K510-ESTCDE                       
                            FILE STATUS   IS  K510-STATUS.                      
                                                                                
           SELECT BP13K270  ASSIGN        TO  BP13K270                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  RANDOM                            
                            RECORD KEY    IS  K270-KEY-FLD                      
                            FILE STATUS   IS  K270-STATUS.                      
                                                                                
           SELECT BP13K060  ASSIGN        TO  BP13K060                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  RANDOM                            
                            RECORD KEY    IS  K060-KEY-FLD                      
                            FILE STATUS   IS  K060-STATUS.                      
                                                                                
           SELECT BP13K816  ASSIGN        TO  BP13K816                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  RANDOM                            
                            RECORD KEY    IS  K816-KEY-FLD                      
                            FILE STATUS   IS  K816-STATUS.                      
                                                                                
           SELECT BP13K222  ASSIGN        TO  BP13K222                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  RANDOM                            
                            RECORD KEY    IS  K222-KEY-FLD                      
                            FILE STATUS   IS  K222-STATUS.                      
                                                                                
           SELECT BP13F230  ASSIGN        TO  BP13F230.                         
                                                                                
           SELECT P13F230B  ASSIGN        TO  P13F230B.                         
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F205                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 80  CHARACTERS.                                     
       COPY BP13F205.                                                           
                                                                                
       FD   BM06F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 800 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BM06F800.                                                           
                                                                                
       FD   BM06K510                                                            
            RECORD CONTAINS 60 CHARACTERS.                                      
       COPY BM06K510.                                                           
                                                                                
       FD   BP13K270                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K270.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS 25 CHARACTERS.                                      
       COPY BP13K060.                                                           
                                                                                
       FD   BP13K816                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K816.                                                           
                                                                                
       FD   BP13K222                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K222.                                                           
                                                                                
       FD   BP13F230                                                            
            RECORDING MODE  IS  F                                               
            BLOCK  CONTAINS 0   RECORDS                                         
            RECORD CONTAINS 450 CHARACTERS                                      
            LABEL  RECORDS  ARE STANDARD.                                       
       COPY BP13F230.                                                           
                                                                                
       FD   P13F230B                                                            
            RECORDING MODE  IS  F                                               
            BLOCK  CONTAINS 0   RECORDS                                         
            RECORD CONTAINS 450 CHARACTERS                                      
            LABEL  RECORDS  ARE STANDARD.                                       
       01   P13F230B-REC        PIC X(450).                                     
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       COPY AV02COMM.                                                           
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-EOF-F205                PIC X       VALUE 'N'.                
           05  WS-EOF-F800                PIC X       VALUE 'N'.                
           05  K510-STATUS                PIC 9(2)    VALUE  00.                
           05  K060-STATUS                PIC 9(2)    VALUE  00.                
           05  K270-STATUS                PIC 9(2)    VALUE  00.                
           05  K816-STATUS                PIC 9(2)    VALUE  00.                
           05  K222-STATUS                PIC 9(2)    VALUE  00.                
                                                                                
           05  WS-DETL-WRT-IND-F230       PIC X       VALUE 'N'.                
               88  DETAIL-REC-WRITTEN-F230            VALUE 'Y'.                
                                                                                
       01  WS-COUNTERS-POINTERS.                                                
           05  WS-READ-CNT-F800           PIC 9(5)    VALUE ZEROS.              
           05  WS-READ-TOTAL-CNT          PIC 9(5)    VALUE ZEROS.              
           05  WS-WRITE-CNT-HEADER-F230   PIC 9(5)    VALUE ZEROS.              
           05  WS-WRITE-CNT-DETAIL-F230   PIC 9(5)    VALUE ZEROS.              
           05  WS-WRITE-CNT-TRAILER-F230  PIC 9(5)    VALUE ZEROS.              
           05  WS-WRITE-CNT-RT05-F230     PIC 9(5)    VALUE ZEROS.              
                                                                                
       01  WS-OTHER-VARIABLES.                                                  
           05  WS-PREV-F800-AIX1          PIC X(16)   VALUE SPACES.             
           05  WS-NT-NAME                 PIC X(20)   VALUE SPACES.             
           05  WS-ZONE-NAME               PIC X(10)   VALUE SPACES.             
           05  WS-F800-CDE-SELTY          PIC X(03)   VALUE SPACES.             
           05  WS-F800-NUM-BONUS          PIC X(01)   VALUE SPACES.             
           05  WS-DTE-END.                                                      
               10  WS-DTE-END-YY          PIC X(04).                            
               10  WS-DTE-END-MM          PIC X(02).                            
               10  WS-DTE-END-DD          PIC X(02).                            
           05  WS-ALLOC-MONTH-END         PIC X(02).                            
           05  WS-F800-CDE-ZONE           PIC X(02).                            
           05 WS-NUM-MAX-CAT-DEC          PIC 9(03)V99 VALUE ZEROES.            
           05 WS-NUM-MAX-CAT-REDEF REDEFINES WS-NUM-MAX-CAT-DEC.                
              10  WS-NUM-MAX-CAT          PIC 9(03).                            
              10  WS-NUM-MAX-CAT-REM      PIC 9(02).                            
                                                                                
       01  WS-UNIT-NO.                                                          
           05 WS-MAIN-UNIT                PIC X(4)    VALUE SPACES.             
           05 WS-SUB-UNIT                 PIC X(1)    VALUE SPACES.             
                                                                                
       01  WS-F800-EST.                                                         
           05 WS-F800-AIX1                PIC X(16)   VALUE SPACES.             
                                                                                
       01  WS-F800-NUM-BLK                PIC X(5)    VALUE SPACES.             
       01  WS-F800-DTE-BALLOT             PIC X(6)    VALUE SPACES.             
                                                                                
       01  WS-PREV-ALLOCN-DATE            PIC X(8)    VALUE SPACES.             
                                                                                
       01  WS-F800-NUM-FLAT-TYPE          PIC X(2)    VALUE SPACES.             
       01  WS-F230-NUM-FLAT-TYPE          PIC X(2)    VALUE SPACES.             
       01  WS-F230-NUM-FLAT-TYPE2         PIC X(9)    VALUE SPACES.             
                                                                                
       01  WS-F230-NUM-DESIGN             PIC X(4)    VALUE SPACES.             
       01  WS-F230-NUM-DESIGN-3R          PIC X(4)    VALUE SPACES.             
                                                                                
       01  WS-DATE                        PIC X(8)    VALUE SPACES.             
                                                                                
       01  WS-TIME                        PIC X(8)    VALUE SPACES.             
       01  WS-F800-STREET-NAME            PIC X(32)   VALUE SPACES.             
       01  WS-F800-NEIGHBOURHOOD          PIC X(04)   VALUE SPACES.             
       01  WS-F800-CONTRACT-NO            PIC X(04)   VALUE SPACES.             
       01  WS-F800-DTE-DLVRY-POSSESSION   PIC X(08)   VALUE SPACES.             
       01  WS-F800-DTE-LEASE-COMM         PIC X(08)   VALUE SPACES.             
       01  WS-F800-DTE-BLK-KEY-AVAIL      PIC X(08)   VALUE SPACES.             
       01  WS-F800-NUM-UPG-STATUS         PIC X(03)   VALUE SPACES.             
       01  WS-F800-NUM-BTO-NEW-CONTRACT   PIC X(01)   VALUE SPACES.             
                                                                                
       01  WS-NEIGH                       PIC X(04) VALUE SPACES.               
       01  WS-CNTRCT                      PIC X(04) VALUE SPACES.               
                                                                                
       01  WS-LEFT-JUST-TXT               PIC X(04) VALUE SPACES.               
       01  WS-RIGHT-JUST-TXT              PIC X(04) VALUE SPACES                
                                          JUST RIGHT.                           
       01  WS-CHR-CNT                     PIC 9(02) VALUE ZEROES.               
       01  WS-BLANK-CNT                   PIC 9(02) VALUE ZEROES.               
       01  WS-CHR-LEN                     PIC 9(02) VALUE ZEROES.               
       01  WS-START-POSN                  PIC 9(02) VALUE ZEROES.               
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM  100-OPEN-FILES        THRU   100-EXIT.                      
           PERFORM  200-READ-BP13F205     THRU   200-EXIT                       
                        UNTIL WS-EOF-F205 = 'Y'.                                
           PERFORM  9999-CLOSE-ROUTINE     THRU   9999-EXIT.                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F205                                                 
                       BM06K510                                                 
                       BP13K060                                                 
                       BP13K816                                                 
                       BP13K270                                                 
                       BP13K222                                                 
                OUTPUT BP13F230                                                 
                       P13F230B.                                                
                                                                                
           IF K510-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BPM06510 - OPEN ERROR, K510-STATUS ' K510-STATUS         
              MOVE     K510-STATUS      TO       RETURN-CODE                    
              PERFORM  9999-CLOSE-ROUTINE.                                      
                                                                                
           IF K270-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K270 - OPEN ERROR, K270-STATUS ' K270-STATUS         
              MOVE     K270-STATUS      TO       RETURN-CODE                    
              PERFORM  9999-CLOSE-ROUTINE.                                      
                                                                                
           IF K060-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K060 - OPEN ERROR, K060-STATUS ' K060-STATUS         
              MOVE     K060-STATUS      TO       RETURN-CODE                    
              PERFORM  9999-CLOSE-ROUTINE.                                      
                                                                                
           IF K816-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K816 - OPEN ERROR, K816-STATUS ' K816-STATUS         
              MOVE     K816-STATUS      TO       RETURN-CODE                    
              PERFORM  9999-CLOSE-ROUTINE.                                      
                                                                                
           IF K222-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K222 - OPEN ERROR, K222-STATUS ' K222-STATUS         
              MOVE     K222-STATUS      TO       RETURN-CODE                    
              PERFORM  9999-CLOSE-ROUTINE.                                      
                                                                                
           MOVE ZEROS                       TO WS-COUNTERS-POINTERS.            
           MOVE FUNCTION CURRENT-DATE (1:8) TO WS-DATE.                         
           MOVE FUNCTION CURRENT-DATE (9:8) TO WS-TIME.                         
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-BP13F205.                                                       
      *-------------------------------------------------------------            
           READ BP13F205                                                        
                AT END MOVE 'Y'         TO     WS-EOF-F205                      
                       GO TO 200-EXIT.                                          
                                                                                
           ADD     1                    TO     WS-READ-TOTAL-CNT.               
                                                                                
              PERFORM 300-PROCESS-RECORD THRU  300-EXIT.                        
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-RECORD.                                                      
      *-------------------------------------------------------------            
           OPEN    INPUT BM06F800.                                              
           PERFORM 320-READ-BM06F800      THRU   320-EXIT.                      
           MOVE    'N'                    TO     WS-DETL-WRT-IND-F230.          
           MOVE    'N'                    TO     WS-EOF-F800.                   
           PERFORM 400-CHECK-NT-ZONE-ALLOCDTE    THRU 400-EXIT                  
             UNTIL WS-EOF-F800  = 'Y'.                                          
           IF DETAIL-REC-WRITTEN-F230                                           
              PERFORM  600-WRITE-HEADER      THRU   600-EXIT                    
              PERFORM  800-WRITE-TRAILER     THRU   800-EXIT                    
              PERFORM  605-WRITE-REC05       THRU   605-EXIT                    
           END-IF.                                                              
                                                                                
           CLOSE   BM06F800.                                                    
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       320-READ-BM06F800.                                                       
      *-------------------------------------------------------------            
           READ BM06F800                                                        
                AT END MOVE 'Y'  TO  WS-EOF-F800                                
                       GO TO 320-EXIT.                                          
                                                                                
           MOVE F800-AIX1 TO WS-F800-AIX1.                                      
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       340-INITIALISE.                                                          
      *-------------------------------------------------------------            
           MOVE    F800-AIX1             TO   WS-PREV-F800-AIX1                 
                                              WS-F800-AIX1.                     
           MOVE    F800-ESTATE           TO   K510-ESTCDE.                      
           MOVE    F800-CDE-SELTY        TO   WS-F800-CDE-SELTY.                
           MOVE    F800-NUM-BONUS        TO   WS-F800-NUM-BONUS.                
           PERFORM 500-READ-BM06K510   THRU   500-EXIT.                         
           MOVE    F800-DTE-ALLOCN       TO   WS-PREV-ALLOCN-DATE.              
           MOVE    F800-BLK-NO           TO   WS-F800-NUM-BLK.                  
           MOVE    F800-STREET-NAME      TO   WS-F800-STREET-NAME.              
           MOVE    F800-NEIGHBOURHOOD    TO   WS-F800-NEIGHBOURHOOD.            
           MOVE    F800-CONTRACT-NO      TO   WS-F800-CONTRACT-NO.              
           MOVE    F800-DTE-DLVRY-POSSESSION  TO                                
                   WS-F800-DTE-DLVRY-POSSESSION.                                
           MOVE    F800-NUM-BTO-NEW-CONTRACT  TO                                
                   WS-F800-NUM-BTO-NEW-CONTRACT.                                
           MOVE    F800-DTE-LEASE-COMM   TO   WS-F800-DTE-LEASE-COMM.           
           MOVE    F800-DTE-BLK-KEY-AVAIL TO WS-F800-DTE-BLK-KEY-AVAIL.         
           MOVE    F800-NUM-UPG-STATUS   TO  WS-F800-NUM-UPG-STATUS.            
           MOVE    F800-DTE-BALLOT       TO   WS-F800-DTE-BALLOT.               
           MOVE    F800-CDE-ZONE         TO   WS-F800-CDE-ZONE.                 
           MOVE    'N'                   TO   WS-DETL-WRT-IND-F230.             
                                                                                
           IF F800-CDE-SELTY = 'SER'                                            
              MOVE SPACES        TO WS-ZONE-NAME                                
           ELSE                                                                 
              EVALUATE F800-CDE-ZONE                                            
                 WHEN 'PG'                                                      
                      MOVE 'PUNGGOL'     TO WS-ZONE-NAME                        
                 WHEN 'NN'                                                      
                      MOVE 'NORTH      ' TO WS-ZONE-NAME                        
                 WHEN 'WW'                                                      
                      MOVE 'WEST       ' TO WS-ZONE-NAME                        
                 WHEN 'NE'                                                      
                      MOVE 'NORTH-EAST ' TO WS-ZONE-NAME                        
                 WHEN OTHER                                                     
                      MOVE SPACES        TO WS-ZONE-NAME                        
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
                                                                                
                                                                                
       340-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       360-DECODE-FLAT-TYPE-F230.                                               
      *-------------------------------------------------------------            
                                                                                
      *    MOVE 'S1'        TO WS-F230-NUM-FLAT-TYPE.                           
      *    MOVE 'S1/S2    ' TO WS-F230-NUM-FLAT-TYPE2.                          
                                                                                
           EVALUATE F800-CDE-TYP-ACTUSE                                         
           WHEN '01'                                                            
             IF F800-NUM-COMBINE-SA = 'Y'                                       
                MOVE 'SA'        TO WS-F230-NUM-FLAT-TYPE                       
                MOVE 'SA       ' TO WS-F230-NUM-FLAT-TYPE2                      
             ELSE                                                               
                IF F800-CDE-MODL-DESG = '80' OR '89' OR '92'                    
                   MOVE '1A'        TO WS-F230-NUM-FLAT-TYPE                    
                   MOVE 'SA 35SQM ' TO WS-F230-NUM-FLAT-TYPE2                   
                ELSE                                                            
                   MOVE '01'        TO WS-F230-NUM-FLAT-TYPE                    
                   MOVE '1-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2                   
                END-IF                                                          
             END-IF                                                             
           WHEN '02'                                                            
             IF F800-NUM-COMBINE-SA = 'Y'                                       
                MOVE 'SA'        TO WS-F230-NUM-FLAT-TYPE                       
                MOVE 'SA       ' TO WS-F230-NUM-FLAT-TYPE2                      
             ELSE                                                               
                IF F800-CDE-MODL-DESG = '80' OR '89' OR '92'                    
                   MOVE '2A'        TO WS-F230-NUM-FLAT-TYPE                    
                   MOVE 'SA 45SQM ' TO WS-F230-NUM-FLAT-TYPE2                   
                ELSE                                                            
                   IF F800-FLAT-STATUS = 'NW' OR 'CV'                           
                      IF F800-CDE-MODL-DESG = '34' OR '70' OR '71' OR           
                                              '72' OR '73'                      
                         MOVE '2D'        TO WS-F230-NUM-FLAT-TYPE              
                         MOVE '2-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2             
                      ELSE                                                      
                         IF F800-CDE-MODL-DESG = '81' OR '82'                   
                            MOVE '2P'        TO WS-F230-NUM-FLAT-TYPE           
                            MOVE '2-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2          
                         ELSE                                                   
                            MOVE '02'        TO WS-F230-NUM-FLAT-TYPE           
                            MOVE '2-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2          
                         END-IF                                                 
                      END-IF                                                    
                   ELSE                                                         
                      PERFORM 361-DECODE-FT-2R THRU 361-EXIT                    
                      MOVE '2-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2                
                   END-IF                                                       
                END-IF                                                          
             END-IF                                                             
           WHEN '03'                                                            
             IF F800-FLAT-STATUS = 'NW' OR 'CV'                                 
                IF F800-CDE-MODL-DESG = '34' OR '70' OR '71' OR                 
                                        '72' OR '73'                            
                   MOVE '3D'        TO WS-F230-NUM-FLAT-TYPE                    
                   MOVE '3-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2                   
                ELSE                                                            
                   IF F800-CDE-MODL-DESG = '81' OR '82'                         
                      MOVE '3P'        TO WS-F230-NUM-FLAT-TYPE                 
                      MOVE '3-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2                
                   ELSE                                                         
                      MOVE '03'        TO WS-F230-NUM-FLAT-TYPE                 
                      MOVE '3-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2                
                   END-IF                                                       
                END-IF                                                          
             ELSE                                                               
                PERFORM 362-DECODE-FT-3R THRU 362-EXIT                          
                MOVE '3-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2                      
             END-IF                                                             
           WHEN '04'                                                            
             IF F800-CDE-MODL-DESG = '34' OR '70' OR '71' OR                    
                                     '72' OR '73'                               
                MOVE '4D'        TO WS-F230-NUM-FLAT-TYPE                       
                MOVE '4-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2                      
             ELSE                                                               
                IF F800-CDE-MODL-DESG = '81' OR '82'                            
                   MOVE '4P'        TO WS-F230-NUM-FLAT-TYPE                    
                   MOVE '4-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2                   
                ELSE                                                            
                   MOVE '04'        TO WS-F230-NUM-FLAT-TYPE                    
                   MOVE '4-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2                   
                END-IF                                                          
             END-IF                                                             
           WHEN '05'                                                            
             IF F800-CDE-MODL-DESG = '34' OR '70' OR '71' OR                    
                                     '72' OR '73'                               
                MOVE '5D'        TO WS-F230-NUM-FLAT-TYPE                       
                MOVE '5-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2                      
             ELSE                                                               
                IF F800-CDE-MODL-DESG = '81' OR '82'                            
                   MOVE '5P'        TO WS-F230-NUM-FLAT-TYPE                    
                   MOVE '5-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2                   
                ELSE                                                            
                   MOVE '05'        TO WS-F230-NUM-FLAT-TYPE                    
                   MOVE '5-ROOM   ' TO WS-F230-NUM-FLAT-TYPE2                   
                END-IF                                                          
             END-IF                                                             
           WHEN '06'                                                            
             IF F800-CDE-MODL-DESG = '70' OR '71' OR '34' OR                    
                                     '72' OR '73'                               
                MOVE '6D'        TO WS-F230-NUM-FLAT-TYPE                       
                MOVE 'EXECUTIVE' TO WS-F230-NUM-FLAT-TYPE2                      
             ELSE                                                               
                IF F800-CDE-MODL-DESG = '81' OR '82'                            
                   MOVE '6P'        TO WS-F230-NUM-FLAT-TYPE                    
                   MOVE 'EXECUTIVE' TO WS-F230-NUM-FLAT-TYPE2                   
                ELSE                                                            
                   MOVE '06'        TO WS-F230-NUM-FLAT-TYPE                    
                   MOVE 'EXECUTIVE' TO WS-F230-NUM-FLAT-TYPE2                   
                END-IF                                                          
             END-IF                                                             
           WHEN OTHER                                                           
             MOVE F800-CDE-TYP-ACTUSE TO WS-F230-NUM-FLAT-TYPE                  
           END-EVALUATE.                                                        
                                                                                
       360-EXIT.                                                                
           EXIT.                                                                
                                                                                
       361-DECODE-FT-2R.                                                        
      *-------------------------------------------------------------            
           MOVE SPACES       TO WS-F230-NUM-FLAT-TYPE.                          
                                                                                
           EVALUATE F800-CDE-MODL-DESG                                          
              WHEN '03'                                                         
              WHEN '13'                                                         
              WHEN '15'                                                         
              WHEN '21'                                                         
              WHEN '30'                                                         
              WHEN '37'                                                         
                 MOVE '2I'     TO WS-F230-NUM-FLAT-TYPE                         
              WHEN OTHER                                                        
                 MOVE '02'     TO WS-F230-NUM-FLAT-TYPE                         
           END-EVALUATE.                                                        
                                                                                
       361-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       362-DECODE-FT-3R.                                                        
      *-------------------------------------------------------------            
           MOVE SPACES       TO WS-F230-NUM-FLAT-TYPE.                          
                                                                                
           EVALUATE F800-CDE-MODL-DESG                                          
              WHEN '02'                                                         
              WHEN '20'                                                         
                 MOVE '03'     TO WS-F230-NUM-FLAT-TYPE                         
              WHEN '03'                                                         
              WHEN '13'                                                         
              WHEN '15'                                                         
              WHEN '21'                                                         
              WHEN '30'                                                         
              WHEN '37'                                                         
                 MOVE '3I'     TO WS-F230-NUM-FLAT-TYPE                         
              WHEN '04'                                                         
              WHEN '16'                                                         
              WHEN '17'                                                         
              WHEN '23'                                                         
                 MOVE '3N'     TO WS-F230-NUM-FLAT-TYPE                         
              WHEN '05'                                                         
              WHEN '14'                                                         
              WHEN '18'                                                         
              WHEN '24'                                                         
              WHEN '29'                                                         
              WHEN '35'                                                         
                 MOVE '3A'     TO WS-F230-NUM-FLAT-TYPE                         
              WHEN '06'                                                         
              WHEN '22'                                                         
                 MOVE '3S'     TO WS-F230-NUM-FLAT-TYPE                         
           END-EVALUATE.                                                        
                                                                                
       362-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       370-DECODE-MODL-DESG.                                                    
      *-------------------------------------------------------------            
                                                                                
           MOVE 'ALL '          TO WS-F230-NUM-DESIGN.                          
                                                                                
       370-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-CHECK-NT-ZONE-ALLOCDTE.                                              
      *-------------------------------------------------------------            
                                                                                
           IF (F205-NUM-NT-ZONE   = F800-NUM-BTO-OPTION)   AND                  
              (F205-DTE-ALLOCN    = F800-DTE-ALLOCN)                            
              PERFORM 420-PREPARE-DETAIL   THRU   420-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM    320-READ-BM06F800    THRU   320-EXIT.                     
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       420-PREPARE-DETAIL.                                                      
      *-------------------------------------------------------------            
                                                                                
           PERFORM 360-DECODE-FLAT-TYPE-F230 THRU 360-EXIT.                     
           PERFORM 370-DECODE-MODL-DESG      THRU 370-EXIT.                     
                                                                                
           IF WS-F800-EST NOT =  WS-PREV-F800-AIX1                              
              IF DETAIL-REC-WRITTEN-F230                                        
                 PERFORM   600-WRITE-HEADER     THRU  600-EXIT                  
                 PERFORM   800-WRITE-TRAILER    THRU  800-EXIT                  
                 PERFORM   605-WRITE-REC05      THRU  605-EXIT                  
                 PERFORM   340-INITIALISE       THRU  340-EXIT                  
                 MOVE      0                    TO    WS-READ-CNT-F800          
              ELSE                                                              
                 PERFORM   340-INITIALISE       THRU  340-EXIT                  
                 MOVE      0                    TO    WS-READ-CNT-F800          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-F800-EST = WS-PREV-F800-AIX1                                   
              ADD 1 TO WS-READ-CNT-F800.                                        
                                                                                
           PERFORM 700-WRITE-DETAIL THRU 700-EXIT.                              
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       500-READ-BM06K510.                                                       
      *-------------------------------------------------------------            
           READ BM06K510.                                                       
                                                                                
           IF K510-STATUS = 00                                                  
              IF (F800-NUM-SELECTION = 'BTO')                                   
                 MOVE SPACES TO WS-NT-NAME                                      
                 MOVE SPACES TO K816-KEY-FLD                                    
                 MOVE F800-NUM-BTO-OPTION TO K816-KEY-FLD                       
                 PERFORM 550-READ-BP13K816 THRU 550-EXIT                        
              ELSE                                                              
                 MOVE K510-NT-NAME   TO WS-NT-NAME                              
              END-IF                                                            
           ELSE                                                                 
              IF K510-STATUS = 23                                               
                 MOVE SPACES         TO WS-NT-NAME                              
                 DISPLAY 'READ BM06K510-ERROR ' K510-STATUS ' '                 
                                                K510-ESTCDE                     
              ELSE                                                              
                 DISPLAY 'ERROR READING BM06K510, STATUS = '                    
                          K510-STATUS                                           
                         '      K510-ESTCDE    =  ' K510-ESTCDE                 
                 MOVE     K510-STATUS          TO   RETURN-CODE                 
                 CLOSE    BM06F800                                              
                 PERFORM  9999-CLOSE-ROUTINE                                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       550-READ-BP13K816.                                                       
      *-------------------------------------------------------------            
           READ BP13K816.                                                       
           EVALUATE K816-STATUS                                                 
           WHEN 00                                                              
                MOVE K816-NME-SITE TO WS-NT-NAME                                
           WHEN 23                                                              
                DISPLAY 'BP13K816 - ZONE NOT FOUND: ' K816-KEY-FLD              
           WHEN OTHER                                                           
                DISPLAY 'BP13K816 - ERROR READ, STATUS: ' K816-STATUS           
           END-EVALUATE.                                                        
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       600-WRITE-HEADER.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE       SPACES                  TO    F230-HEADER-REC.            
           INITIALIZE F230-HEADER-REC.                                          
           MOVE    'INTV'                     TO                                
                       F230-CDE-INTV          OF    F230-HEADER-REC.            
           MOVE    '01'                       TO    F230-CDE-HEADER             
                                              OF    F230-HEADER-REC.            
           MOVE     WS-PREV-ALLOCN-DATE       TO                                
                       F230-DTE-ALLOC         OF    F230-HEADER-REC.            
           MOVE     '1T'                      TO                                
                       F230-NUM-FLAT-TYPE     OF    F230-HEADER-REC.            
           MOVE     'TYP 3,4,5'               TO                                
                       F230-NUM-FLAT-TYPE2    OF    F230-HEADER-REC             
                       F230-NUM-FLAT-TYPE3    OF    F230-HEADER-REC             
                       F230-NUM-FLAT-TYPE4    OF    F230-HEADER-REC.            
           MOVE     WS-F800-NUM-BLK           TO                                
                       F230-NUM-BLK           OF    F230-HEADER-REC             
                       F230-NUM-BLK2          OF    F230-HEADER-REC             
                       F230-NUM-BLK3          OF    F230-HEADER-REC             
                       F230-NUM-BLK4          OF    F230-HEADER-REC.            
           MOVE     WS-NT-NAME                TO                                
                       F230-NUM-NEW-TOWN      OF    F230-HEADER-REC             
                       F230-NUM-NEW-TOWN2     OF    F230-HEADER-REC             
                       F230-NUM-NEW-TOWN3     OF    F230-HEADER-REC             
                       F230-NUM-NEW-TOWN4     OF    F230-HEADER-REC.            
           MOVE     WS-ZONE-NAME              TO                                
                       F230-NUM-ZONE          OF    F230-HEADER-REC             
                       F230-NUM-ZONE2         OF    F230-HEADER-REC             
                       F230-NUM-ZONE3         OF    F230-HEADER-REC.            
           MOVE     WS-F800-DTE-BALLOT        TO                                
                       F230-DTE-BALLOT        OF    F230-HEADER-REC.            
                                                                                
           MOVE     SPACES                    TO                                
                    F230-NUM-DESIGN           OF    F230-HEADER-REC.            
           MOVE     WS-F230-NUM-DESIGN        TO                                
                    F230-NUM-DESIGN2          OF    F230-HEADER-REC.            
                                                                                
           MOVE     WS-DATE                   TO    F230-DTE-UPDATE.            
           MOVE     WS-TIME                   TO    F230-TME-UPDATE.            
           MOVE     WS-READ-CNT-F800                                            
                               TO F230-NUM-TOTAL OF F230-HEADER-REC.            
           MOVE     WS-F800-CDE-SELTY         TO                                
                       F230-NUM-SELECTION     OF    F230-HEADER-REC             
                       F230-NUM-SELECTION2    OF    F230-HEADER-REC             
                       F230-NUM-SELECTION3    OF    F230-HEADER-REC             
                       F230-NUM-SELECTION4    OF    F230-HEADER-REC.            
           MOVE     WS-F800-NUM-BONUS         TO    F230-NUM-BONUS              
                                              OF    F230-HEADER-REC.            
                                                                                
      ****>> STMNTS ADDED FOR STREET,CNTRCT DETAILS,EST COMPLN DATE             
      ******* LEASE COMMENCE DATE,KEY AVAIL DATE AND UPGRADE STATUS.            
           MOVE  WS-F800-STREET-NAME          TO F230-NME-STREET                
                                              OF F230-HEADER-REC.               
           MOVE  WS-F800-NEIGHBOURHOOD        TO F230-NUM-NEIGH                 
                                              OF F230-HEADER-REC.               
           MOVE  WS-F800-CONTRACT-NO          TO F230-NUM-CNTRCT                
                                              OF F230-HEADER-REC.               
           MOVE  WS-F800-DTE-DLVRY-POSSESSION TO F230-DTE-EST-CMPLTN            
                                              OF F230-HEADER-REC.               
           MOVE  WS-F800-DTE-LEASE-COMM       TO F230-DTE-LSE-COMM              
                                              OF F230-HEADER-REC.               
           MOVE  WS-F800-DTE-BLK-KEY-AVAIL    TO F230-DTE-KEY-AVAIL             
                                              OF F230-HEADER-REC.               
           MOVE  WS-F800-NUM-UPG-STATUS       TO F230-NUM-UPG-STATUS            
                                              OF F230-HEADER-REC.               
           IF    WS-F800-NUM-BTO-NEW-CONTRACT = 'Y'                             
                 MOVE WS-F800-NUM-BTO-NEW-CONTRACT                              
                 TO   F230-NUM-BTO-NEW-CONTRACT    OF F230-HEADER-REC           
           ELSE                                                                 
                 MOVE 'N'                                                       
                 TO   F230-NUM-BTO-NEW-CONTRACT    OF F230-HEADER-REC           
           END-IF.                                                              
                                                                                
           PERFORM 601-GET-NEIGH4-CNTRCT4   THRU 601-EXIT.                      
           MOVE WS-NEIGH               TO F230-NUM-NEIGH4  OF                   
                                          F230-HEADER-REC.                      
           MOVE WS-CNTRCT              TO F230-NUM-CNTRCT4 OF                   
                                          F230-HEADER-REC.                      
                                                                                
           MOVE     WS-PREV-ALLOCN-DATE       TO    K270-ALLOCN-DATE.           
           MOVE     WS-PREV-F800-AIX1         TO    K270-AIX1-KEY.              
           PERFORM  620-READ-BP13K270    THRU  620-EXIT.                        
                                                                                
           WRITE F230-HEADER-REC.                                               
                                                                                
           ADD    1   TO  WS-WRITE-CNT-HEADER-F230.                             
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       601-GET-NEIGH4-CNTRCT4.                                                  
      *---------------------------------------------------------------*         
           MOVE   4                    TO WS-CHR-LEN.                           
           MOVE SPACES                 TO WS-RIGHT-JUST-TXT                     
                                          WS-LEFT-JUST-TXT.                     
           MOVE WS-F800-NEIGHBOURHOOD  TO WS-LEFT-JUST-TXT.                     
           PERFORM 603-RIGHT-JUSTIFY-TXT   THRU 603-EXIT.                       
           MOVE WS-RIGHT-JUST-TXT      TO WS-NEIGH.                             
                                                                                
           MOVE SPACES                 TO WS-RIGHT-JUST-TXT                     
                                          WS-LEFT-JUST-TXT.                     
           MOVE WS-F800-CONTRACT-NO    TO WS-LEFT-JUST-TXT.                     
           PERFORM 603-RIGHT-JUSTIFY-TXT   THRU 603-EXIT.                       
           MOVE WS-RIGHT-JUST-TXT      TO WS-CNTRCT.                            
                                                                                
       601-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       603-RIGHT-JUSTIFY-TXT.                                                   
      *---------------------------------------------------------------*         
           MOVE ZEROES   TO WS-CHR-CNT, WS-START-POSN.                          
           INSPECT WS-LEFT-JUST-TXT TALLYING WS-CHR-CNT                         
                  FOR CHARACTERS BEFORE INITIAL SPACE.                          
           COMPUTE WS-START-POSN = (WS-CHR-LEN  -  WS-CHR-CNT) + 1              
           MOVE WS-LEFT-JUST-TXT(1:WS-CHR-CNT)  TO                              
                WS-RIGHT-JUST-TXT.                                              
                                                                                
       603-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       605-WRITE-REC05.                                                         
      *-------------------------------------------------------------            
                                                                                
      **** >>> WRITE RECORD TYPE 05 <<< ***                                     
           MOVE       SPACES                  TO  F230-START-END-REC.           
           INITIALIZE F230-START-END-REC.                                       
           MOVE    'INTV'                     TO                                
                       F230-CDE-INTV          OF  F230-START-END-REC.           
           MOVE    '05'                       TO  F230-CDE-START-END            
                                              OF  F230-START-END-REC.           
           MOVE     '1T'                      TO                                
                       F230-NUM-FLAT-TYPE     OF  F230-START-END-REC.           
           MOVE     WS-NT-NAME                TO                                
                       F230-NUM-NEW-TOWN      OF  F230-START-END-REC.           
           MOVE     WS-ZONE-NAME              TO                                
                       F230-NUM-ZONE          OF  F230-START-END-REC.           
           MOVE     WS-F800-CDE-SELTY         TO                                
                       F230-NUM-SELECTION     OF  F230-START-END-REC.           
                                                                                
           MOVE   F205-NUM-NT-ZONE      TO K222-NUM-NT-ZONE.                    
           MOVE   F205-NUM-FLAT-TYPE    TO K222-NUM-FLAT-TYPE.                  
           MOVE   F205-DTE-ALLOCN       TO K222-DTE-ALLOC.                      
           PERFORM 610-READ-BP13K222  THRU 610-EXIT.                            
                                                                                
           WRITE  P13F230B-REC FROM F230-START-END-REC.                         
           ADD    1   TO  WS-WRITE-CNT-RT05-F230.                               
                                                                                
       605-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       610-READ-BP13K222.                                                       
      *-------------------------------------------------------------            
                                                                                
           READ BP13K222.                                                       
                                                                                
           EVALUATE K222-STATUS                                                 
           WHEN 00                                                              
              MOVE  K222-DTE-START-BKAPPMT  TO F230-DTE-START                   
                                            OF F230-START-END-REC               
              IF F205-NUM-OPTION  =  'SE'                                       
                 MOVE  F205-DTE-END         TO F230-DTE-END                     
                                            OF F230-START-END-REC               
              ELSE                                                              
                 MOVE  K222-DTE-END-BKAPPMT TO F230-DTE-END                     
                                            OF F230-START-END-REC               
              END-IF                                                            
                                                                                
           WHEN 23                                                              
              MOVE  ZEROES                  TO F230-DTE-START                   
                                            OF F230-START-END-REC,              
                                               F230-DTE-END                     
                                            OF F230-START-END-REC               
              DISPLAY 'BP13K222 - REC NOT FOUND: '                              
              DISPLAY '                NT ZONE : ' F205-NUM-NT-ZONE             
              DISPLAY '              FLAT TYPE : ' F205-NUM-FLAT-TYPE           
              DISPLAY '             DATE ALLOC : ' F205-DTE-ALLOCN              
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K222, STATUS = ' K222-STATUS           
              MOVE     K222-STATUS         TO  RETURN-CODE                      
              PERFORM  9999-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       610-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
       620-READ-BP13K270.                                                       
      *-------------------------------------------------------------            
                                                                                
           READ BP13K270.                                                       
                                                                                
           IF K270-STATUS = 00                                                  
              COMPUTE WS-NUM-MAX-CAT-DEC =                                      
                      (K270-NUM-ALLOC (3) +                                     
                       K270-NUM-ALLOC (4) +                                     
                       K270-NUM-ALLOC (5) +                                     
                       K270-NUM-ALLOC (6)) * 0.22                               
                                                                                
              IF WS-NUM-MAX-CAT-REM > 0                                         
                 ADD 1 TO WS-NUM-MAX-CAT                                        
              END-IF                                                            
                                                                                
              MOVE WS-NUM-MAX-CAT TO F230-NUM-MAX-CAT1                          
                                                                                
              COMPUTE WS-NUM-MAX-CAT-DEC =                                      
                      (K270-NUM-ALLOC (3) +                                     
                       K270-NUM-ALLOC (4) +                                     
                       K270-NUM-ALLOC (5) +                                     
                       K270-NUM-ALLOC (6)) * 0.84                               
                                                                                
              IF WS-NUM-MAX-CAT-REM > 0                                         
                 ADD 1 TO WS-NUM-MAX-CAT                                        
              END-IF                                                            
                                                                                
              MOVE WS-NUM-MAX-CAT TO F230-NUM-MAX-CAT2                          
                                                                                
              COMPUTE WS-NUM-MAX-CAT-DEC =                                      
                      (K270-NUM-ALLOC (3) +                                     
                       K270-NUM-ALLOC (4) +                                     
                       K270-NUM-ALLOC (5) +                                     
                       K270-NUM-ALLOC (6)) * 0.10                               
                                                                                
              IF WS-NUM-MAX-CAT-REM > 0                                         
                 ADD 1 TO WS-NUM-MAX-CAT                                        
              END-IF                                                            
                                                                                
              MOVE WS-NUM-MAX-CAT TO F230-NUM-MAX-CAT3                          
                                                                                
           ELSE                                                                 
              IF K270-STATUS = 23                                               
                 MOVE ZEROES TO F230-NUM-MAX-CAT1                               
                                F230-NUM-MAX-CAT2                               
                                F230-NUM-MAX-CAT3                               
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K270, STATUS = '                    
                          K270-STATUS                                           
                         '      K270-AIX1-KEY = ' K270-AIX1-KEY                 
                 MOVE     K270-STATUS         TO  RETURN-CODE                   
                 CLOSE    BM06F800                                              
                 PERFORM  9999-CLOSE-ROUTINE                                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       620-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       700-WRITE-DETAIL.                                                        
      *-------------------------------------------------------------            
           MOVE  SPACES                  TO F230-DETAIL-REC.                    
           MOVE  'INTV'                  TO                                     
                    F230-CDE-INTV        OF F230-DETAIL-REC.                    
           MOVE '02'                     TO F230-CDE-DETAIL.                    
           MOVE  F800-DTE-ALLOCN         TO                                     
                    F230-DTE-ALLOC       OF F230-DETAIL-REC.                    
           MOVE  WS-F230-NUM-FLAT-TYPE   TO                                     
                   F230-NUM-FLAT-TYPE    OF F230-DETAIL-REC.                    
           MOVE  WS-F230-NUM-FLAT-TYPE2  TO                                     
                   F230-NUM-FLAT-TYPE2   OF F230-DETAIL-REC                     
                   F230-NUM-FLAT-TYPE3   OF F230-DETAIL-REC                     
                   F230-NUM-FLAT-TYPE4   OF F230-DETAIL-REC.                    
           MOVE  WS-F800-NUM-BLK         TO                                     
                   F230-NUM-BLK          OF F230-DETAIL-REC                     
                   F230-NUM-BLK2         OF F230-DETAIL-REC                     
                   F230-NUM-BLK3         OF F230-DETAIL-REC                     
                   F230-NUM-BLK4         OF F230-DETAIL-REC.                    
           MOVE  F800-CDE-FLAT-STATUS    TO                                     
                   F230-NUM-BALANCE      OF F230-DETAIL-REC.                    
           MOVE  F800-SCH-ACC-NO         TO F230-NUM-SCH-ACC.                   
           MOVE  F800-LEVEL-NO           TO F230-NUM-LEVEL.                     
           MOVE  F800-LEVEL-NO           TO F230-NUM-LEVEL2                     
                                            F230-NUM-LEVEL3                     
                                            F230-NUM-LEVEL4.                    
           MOVE  F800-MAIN-UNIT          TO WS-MAIN-UNIT.                       
           MOVE  F800-SUB-UNIT           TO WS-SUB-UNIT.                        
           MOVE  WS-UNIT-NO              TO F230-NUM-UNIT.                      
           MOVE  WS-UNIT-NO              TO F230-NUM-UNIT2                      
                                            F230-NUM-UNIT3                      
                                            F230-NUM-UNIT4.                     
           MOVE  WS-NT-NAME              TO                                     
                 F230-NUM-NEW-TOWN       OF F230-DETAIL-REC                     
                 F230-NUM-NEW-TOWN2      OF F230-DETAIL-REC                     
                 F230-NUM-NEW-TOWN3      OF F230-DETAIL-REC                     
                 F230-NUM-NEW-TOWN4      OF F230-DETAIL-REC.                    
           MOVE  WS-ZONE-NAME            TO                                     
                 F230-NUM-ZONE           OF F230-DETAIL-REC                     
                 F230-NUM-ZONE2          OF F230-DETAIL-REC                     
                 F230-NUM-ZONE3          OF F230-DETAIL-REC.                    
                                                                                
           MOVE  SPACES                  TO                                     
                 F230-NUM-DESIGN         OF F230-DETAIL-REC.                    
           MOVE  WS-F230-NUM-DESIGN      TO                                     
                 F230-NUM-DESIGN2        OF F230-DETAIL-REC.                    
                                                                                
           MOVE  SPACES                  TO F230-NUM-ACCEPT                     
                                            F230-DTE-ACCEPT                     
                                            F230-TME-ACCEPT.                    
           MOVE  F800-CDE-SELTY          TO F230-NUM-SELECTION                  
                                         OF F230-DETAIL-REC                     
                                            F230-NUM-SELECTION2                 
                                         OF F230-DETAIL-REC                     
                                            F230-NUM-SELECTION3                 
                                         OF F230-DETAIL-REC                     
                                            F230-NUM-SELECTION4                 
                                         OF F230-DETAIL-REC.                    
           MOVE  F800-NUM-BONUS          TO F230-NUM-BONUS                      
                                         OF F230-DETAIL-REC.                    
                                                                                
           PERFORM 601-GET-NEIGH4-CNTRCT4   THRU 601-EXIT.                      
           MOVE WS-NEIGH               TO F230-NUM-NEIGH4  OF                   
                                          F230-DETAIL-REC.                      
           MOVE WS-CNTRCT              TO F230-NUM-CNTRCT4 OF                   
                                          F230-DETAIL-REC.                      
                                                                                
           EVALUATE F800-CDE-TYP-ACTUSE                                         
              WHEN '05'                                                         
                   IF F800-CDE-MODL-DESG  = '84'                                
                      MOVE 'Y'        TO F230-NUM-RP                            
                   ELSE                                                         
                      MOVE 'N'        TO F230-NUM-RP                            
                   END-IF                                                       
              WHEN OTHER                                                        
                   MOVE 'N'           TO F230-NUM-RP                            
           END-EVALUATE.                                                        
                                                                                
           WRITE F230-DETAIL-REC.                                               
           ADD   1    TO  WS-WRITE-CNT-DETAIL-F230.                             
           MOVE 'Y'   TO  WS-DETL-WRT-IND-F230.                                 
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       800-WRITE-TRAILER.                                                       
      *-------------------------------------------------------------            
           MOVE  SPACES                TO F230-TRAILER-REC.                     
           MOVE 'INTV'                 TO                                       
                    F230-CDE-INTV      OF F230-TRAILER-REC.                     
           MOVE '03'                   TO F230-CDE-TRAILER.                     
           MOVE  WS-PREV-ALLOCN-DATE   TO                                       
                    F230-DTE-ALLOC     OF F230-TRAILER-REC.                     
           MOVE  '1T'                  TO                                       
                 F230-NUM-FLAT-TYPE    OF F230-TRAILER-REC.                     
           MOVE  'TYP 3,4,5'            TO                                      
                 F230-NUM-FLAT-TYPE2   OF F230-TRAILER-REC                      
                 F230-NUM-FLAT-TYPE3   OF F230-TRAILER-REC                      
                 F230-NUM-FLAT-TYPE4   OF F230-TRAILER-REC.                     
           MOVE  WS-F800-NUM-BLK       TO                                       
                 F230-NUM-BLK          OF F230-TRAILER-REC                      
                 F230-NUM-BLK2         OF F230-TRAILER-REC                      
                 F230-NUM-BLK3         OF F230-TRAILER-REC                      
                 F230-NUM-BLK4         OF F230-TRAILER-REC.                     
           MOVE  WS-NT-NAME            TO                                       
                 F230-NUM-NEW-TOWN     OF F230-TRAILER-REC                      
                 F230-NUM-NEW-TOWN2    OF F230-TRAILER-REC                      
                 F230-NUM-NEW-TOWN3    OF F230-TRAILER-REC                      
                 F230-NUM-NEW-TOWN4    OF F230-TRAILER-REC.                     
           MOVE  WS-ZONE-NAME          TO                                       
                 F230-NUM-ZONE         OF F230-TRAILER-REC                      
                 F230-NUM-ZONE2        OF F230-TRAILER-REC                      
                 F230-NUM-ZONE3        OF F230-TRAILER-REC.                     
           MOVE  WS-F230-NUM-DESIGN    TO                                       
                 F230-NUM-DESIGN2      OF F230-TRAILER-REC.                     
                                                                                
           MOVE     WS-F800-CDE-SELTY         TO                                
                       F230-NUM-SELECTION     OF    F230-TRAILER-REC            
                       F230-NUM-SELECTION2    OF    F230-TRAILER-REC            
                       F230-NUM-SELECTION3    OF    F230-TRAILER-REC            
                       F230-NUM-SELECTION4    OF    F230-TRAILER-REC.           
                                                                                
           MOVE WS-NEIGH               TO F230-NUM-NEIGH4  OF                   
                                          F230-TRAILER-REC.                     
           MOVE WS-CNTRCT              TO F230-NUM-CNTRCT4 OF                   
                                          F230-TRAILER-REC.                     
           WRITE F230-TRAILER-REC.                                              
                                                                                
           ADD   1 TO WS-WRITE-CNT-TRAILER-F230.                                
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '  '.                                                        
           DISPLAY '*----------- BP13C20G CONTROL LIST ----------*'.            
           DISPLAY '  '.                                                        
           DISPLAY 'NO OF BP13F205 READ        = ' WS-READ-TOTAL-CNT.           
           DISPLAY 'NO OF F230 HEADER WRITTEN  = '                              
           WS-WRITE-CNT-HEADER-F230.                                            
           DISPLAY 'NO OF F230 DETAIL WRITTEN  = '                              
           WS-WRITE-CNT-DETAIL-F230.                                            
           DISPLAY 'NO OF F230 TRAILER WRITTEN = '                              
           WS-WRITE-CNT-TRAILER-F230.                                           
           DISPLAY 'NO OF F230 RTYPE05 WRITTEN = '                              
           WS-WRITE-CNT-RT05-F230.                                              
                                                                                
           CLOSE BP13F205                                                       
                 BM06K510                                                       
                 BP13K270                                                       
                 BP13K060                                                       
                 BP13K816                                                       
                 BP13K222                                                       
                 BP13F230                                                       
                 P13F230B.                                                      
                                                                                
           IF K510-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BM06K510 CLOSE ERROR, K510-STATUS ' K510-STATUS          
              MOVE     K510-STATUS      TO       RETURN-CODE.                   
                                                                                
           IF K270-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K270 CLOSE ERROR, K270-STATUS ' K270-STATUS          
              MOVE     K270-STATUS      TO       RETURN-CODE.                   
                                                                                
           IF K060-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K060 CLOSE ERROR, K060-STATUS ' K060-STATUS          
              MOVE     K060-STATUS      TO       RETURN-CODE.                   
                                                                                
           IF K816-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K816 CLOSE ERROR, K816-STATUS ' K816-STATUS          
              MOVE     K816-STATUS      TO       RETURN-CODE.                   
                                                                                
           IF K222-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K222 CLOSE ERROR, K222-STATUS ' K222-STATUS          
              MOVE     K222-STATUS      TO       RETURN-CODE.                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
