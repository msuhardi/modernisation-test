       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF93.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  18/07/2013.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    :  GENERATE FILE WITH INCOMPLETE APPLT/OCCUP  *            
      *                 PER REGN (BE01F500)                        *            
      *                                                            *            
      * INPUT FILES  :  P13LF87I - REGN OF INCOMPLETE CASES        *            
      *                 BE01F500 - MHA INTERFACE FILE              *            
      * OUTPUT FILES :  F500INCO - INCOMPLETE APPLT/OCCUP PER REGN *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP134922  SMR2  18072013  NEW PROGRAM                      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT P13LF87I  ASSIGN       TO P13LF87I.                           
           SELECT BE01F500  ASSIGN       TO BE01F500.                           
           SELECT F500INCO  ASSIGN       TO F500INCO.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   P13LF87I                                                            
            RECORD CONTAINS  20 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
       01   P13LF87I-REC.                                                       
            05 LF87-NUM-REGN       PIC X(08).                                   
            05 FILLER              PIC X(12).                                   
                                                                                
       FD   BE01F500                                                            
            RECORD CONTAINS  50 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
       COPY BE01F500.                                                           
                                                                                
       FD   F500INCO                                                            
            RECORD CONTAINS  50 CHARACTERS                                      
            RECORDING MODE   IS F                                               
            LABEL RECORDS   ARE STANDARD.                                       
       01   F500INCO-REC           PIC X(50).                                   
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-COUNTERS.                                                         
           05  WS-LF87-READ        PIC 9(06)   VALUE ZEROES.                    
           05  WS-F500-READ        PIC 9(06)   VALUE ZEROES.                    
           05  WS-F500-BYPASS      PIC 9(06)   VALUE ZEROES.                    
           05  WS-INCO-WRITE       PIC 9(06)   VALUE ZEROES.                    
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-LF87-REGN        PIC X(8)    VALUE SPACES.                    
           05  WS-F500-REGN        PIC X(8)    VALUE SPACES.                    
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                     
           PERFORM 2000-MAIN-ROUTINE        THRU 2000-EXIT.                     
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT  P13LF87I                                                 
                       BE01F500                                                 
                OUTPUT F500INCO.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 3000-READ-P13LF87I       THRU 3000-EXIT.                     
           PERFORM 4000-READ-BE01F500       THRU 4000-EXIT.                     
           PERFORM 5000-PROCESS-FILES       THRU 5000-EXIT                      
             UNTIL WS-LF87-REGN = HIGH-VALUES                                   
               AND WS-F500-REGN = HIGH-VALUES.                                  
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-P13LF87I.                                                      
      *-------------------------------------------------------------            
           READ P13LF87I AT END                                                 
                MOVE HIGH-VALUES            TO WS-LF87-REGN                     
                GO TO 3000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-LF87-READ.                    
           MOVE LF87-NUM-REGN               TO WS-LF87-REGN.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-READ-BE01F500.                                                      
      *-------------------------------------------------------------            
           READ BE01F500 AT END                                                 
                MOVE HIGH-VALUES            TO WS-F500-REGN                     
                GO TO 4000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F500-READ.                    
           MOVE BE01F500-REF-NO(1:8)        TO WS-F500-REGN.                    
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
           IF WS-LF87-REGN = HIGH-VALUES AND                                    
              WS-F500-REGN NOT = HIGH-VALUES                                    
              ADD 1                         TO WS-F500-BYPASS                   
              PERFORM 4000-READ-BE01F500    THRU 4000-EXIT                      
              GO TO 5000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF WS-LF87-REGN = WS-F500-REGN                                       
              MOVE SPACES                   TO F500INCO-REC                     
              WRITE F500INCO-REC            FROM BE01F500-REC                   
              ADD 1                         TO WS-INCO-WRITE                    
              PERFORM 4000-READ-BE01F500    THRU 4000-EXIT                      
           ELSE                                                                 
              IF WS-LF87-REGN < WS-F500-REGN                                    
                 PERFORM 3000-READ-P13LF87I THRU 3000-EXIT                      
              ELSE                                                              
                 ADD 1                      TO WS-F500-BYPASS                   
                 PERFORM 4000-READ-BE01F500 THRU 4000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY SPACES.                                                      
           DISPLAY '*============== BP13CF93 ==============*'.                  
           DISPLAY '  NO. OF LF87 REGN READ       = ' WS-LF87-READ.             
           DISPLAY '  NO. OF F500 REGN READ       = ' WS-F500-READ.             
           DISPLAY '  NO. OF BYPASSED CASES       = ' WS-F500-BYPASS.           
           DISPLAY '  NO. OF INCOMP CASES WRITTEN = ' WS-INCO-WRITE.            
           DISPLAY '*======================================*'.                  
                                                                                
           CLOSE P13LF87I                                                       
                 BE01F500                                                       
                 F500INCO.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
