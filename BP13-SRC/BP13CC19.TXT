       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC19.                                                 
       AUTHOR. BALA AMUTHAN.                                                    
       DATE-WRITTEN.  10/05/06.                                                 
                                                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *        1.  THIS PROGRAMME WILL VALIDATE                *                
      *            CASH PAYMENT AC28F552 FOR LOAN INSTALLMENT. *                
      *            IF THE RECORD DOES NOT MATCH, THE           *                
      *            OUTPUT RECORD WILL MOVE TO THE KIV          *                
      *            FILE (ERROR FILE).                          *                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *           1. AC28F552 (WEEKLY CPF TRANSACTIONS)        *                
      *           2. BP13F800 (RFF)                            *                
      *           3. BP13KD00                                  *                
      *                                                        *                
      *     OUTPUT FILE:                                       *                
      *           1. BP13K140 (KIV FILE)                       *                
      *           2. BP13F125 (CPF TRANSACTIONS FILE)          *                
      *                                                        *                
      *     LISTINGS:                                          *                
      *           1. BP13LC19 (CONTROL LIST)                   *                
      *                                                        *                
      * BP132857 BA2 100506 - SAME PGM AS BP13C101             *                
      * BP132339 BA3 270407 - INCLUDE LUMP SUM PAYMENT 'LSCS'  *                
      * BP133115 BA3 161007 - ADD NUM-LOAN-SCH                 *                
      * BP133091 RB12 161107  EXPAND KD00 LNGTH FR 1200 TO 1600*                
      * BP133450 JB8 081008   MOVE 'C' TO F125-CDE-CRDR        *                
      * BP133535 JB8 100209   READ KD03 IF KD00 NOT FND        *                
      * BP134033 JB8 101110   TO CATER FOR REPUDIATE DATE AND  *                
      *                       EFF-TRANSFER DATE                *                
      * BP133907 JB8 050111   TO OUTPUT 'C' FOR K140-CDE-CRDR  *                
      * BP134089 JB8 260111   TO CATER FOR 'LCPF'              *                
      * BP134154 JB8 150411   TO CATER FOR RE-FINANCING        *                
      * BP135836 FNP 100615   CATER FOR 'X' ACCOUNT STAT       *                
      **********************************************************                
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AC28F552 ASSIGN TO AC28F552.                                  
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
           SELECT BP13K140 ASSIGN TO BP13K140                                   
                ACCESS MODE IS RANDOM                                           
                ORGANIZATION IS INDEXED                                         
                RECORD KEY IS K140-KEY-FLD                                      
                FILE STATUS IS K140-STATUS.                                     
                                                                                
           SELECT BP13KD00 ASSIGN TO BP13KD00                                   
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY IS KD00-KEY-FLD                                      
                FILE STATUS IS KD00-STATUS.                                     
                                                                                
           SELECT BP13KD03 ASSIGN TO BP13KD03                                   
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY IS KD03-KEY-FLD                                      
                FILE STATUS IS KD03-STATUS.                                     
                                                                                
           SELECT BP13KD92 ASSIGN TO BP13KD92                                   
                ACCESS MODE  IS RANDOM                                          
                ORGANIZATION IS INDEXED                                         
                RECORD KEY IS KD92-KEY-FLD                                      
                FILE STATUS IS KD92-STATUS.                                     
                                                                                
           SELECT BP13F125 ASSIGN TO BP13F125.                                  
                                                                                
           SELECT BP13LC19 ASSIGN TO BP13LC19.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  AC28F552                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 130 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY AC28F552.                                                           
                                                                                
       FD  BP13F800                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F800.                                                           
                                                                                
       FD  BP13F125                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       COPY BP13F125.                                                           
                                                                                
       FD  BP13K140                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13K140.                                                           
                                                                                
       FD  BP13KD00                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
       COPY BP13KD00.                                                           
                                                                                
       FD  BP13KD03                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
       COPY BP13KD03.                                                           
                                                                                
       FD  BP13KD92                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13KD92.                                                           
                                                                                
       FD  BP13LC19                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  PRINT-CONTROL-LIST             PIC X(132).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  F800-STATUS                    PIC 99      VALUE ZEROES.             
       01  K140-STATUS                    PIC 99      VALUE ZEROES.             
       01  KD00-STATUS                    PIC 99      VALUE ZEROES.             
       01  KD03-STATUS                    PIC 99      VALUE ZEROES.             
       01  KD92-STATUS                    PIC 99      VALUE ZEROES.             
                                                                                
       01  WS-K140-STATUS.                                                      
           05  WS-K140-STATUS1            PIC 9.                                
           05  WS-K140-STATUS2            PIC 9.                                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F552-EOF-FLAG           PIC X       VALUE 'N'.                
           05  WS-F800-EOF-FLAG           PIC X       VALUE 'N'.                
                                                                                
       01  WS-KD00-FND                    PIC X(1)    VALUE 'N'.                
       01  WS-KD03-FND                    PIC X(1).                             
           88  KD03-FND                   VALUE 'Y'.                            
       01  WS-KD92-FND                    PIC X(1).                             
           88  KD92-FND                   VALUE 'Y'.                            
       01  WS-CNT-AC28F552-READ           PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-BP13F800-READ           PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-K140-KIV                PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-F125-VALID              PIC 9(5)    VALUE ZEROES.             
       01  WS-CNT-KD00-READ               PIC 9(5)    VALUE ZEROES.             
       01  WS-K140-COUNT-HISTORY          PIC 9(2)    VALUE ZEROES.             
       01  WS-F125-COUNT-HISTORY          PIC 9(3)    VALUE ZEROES.             
       01  WS-CDE-APPLT                   PIC X       VALUE SPACES.             
       01  WS-NUM-NRIC                    PIC X(9)    VALUE SPACES.             
       01  WS-LINE-CNT                    PIC 9(5)    VALUE 00054.              
       01  WS-PAGENO                      PIC 9(5)    VALUE ZEROES.             
       01  WS-SNO                         PIC 9(5)    VALUE ZEROES.             
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05 WS-CCYY                     PIC 9999.                             
           05 WS-MM                       PIC 99.                               
           05 WS-DD                       PIC 99.                               
                                                                                
      ***************************************************************           
      *    WS  OR CONTROL-LIST                                      *           
      ***************************************************************           
                                                                                
       01  WS-CONTROL-HEAD.                                                     
           05  FILLER                  PIC X(8)  VALUE 'BP13CL19'.              
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'HDBCAT3'.               
           05  FILLER                  PIC X(25) VALUE SPACES.                  
           05  FILLER                  PIC X(20) VALUE                          
                                       'S Y S T E M   O F   '.                  
           05  FILLER                  PIC X(19) VALUE                          
                                       'C O M M I T M E N T'.                   
           05  FILLER                  PIC X(15) VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'DATE :'.                
           05  CL-DATE-HD              PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'PAGE : '.               
           05  CL-PAGENO               PIC ZZZZ9 .                              
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
                                                                                
       01  WS-CONTROL-LIST-1.                                                   
           05  FILLER              PIC X(19)  VALUE SPACES.                     
           05  FILLER              PIC X(28)  VALUE                             
               'ERROR REPORT ON CASH PAYMENT'.                                  
           05  FILLER              PIC X(85)  VALUE SPACES.                     
                                                                                
       01  WS-CONTROL-LIST-2.                                                   
           05  FILLER              PIC X(05)  VALUE SPACES.                     
           05  FILLER              PIC X(05)  VALUE ' S/NO'.                    
           05  FILLER              PIC X(02)  VALUE SPACES.                     
           05  FILLER              PIC X(06)  VALUE 'REG NO'.                   
           05  FILLER              PIC X(04)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'SCH-ACC'.                  
           05  FILLER              PIC X(05)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'PAYMENT'.                  
           05  FILLER              PIC X(05)  VALUE SPACES.                     
           05  FILLER              PIC X(05)  VALUE 'JR-NO'.                    
           05  FILLER              PIC X(09)  VALUE SPACES.                     
           05  FILLER              PIC X(11)  VALUE  'AMT-RECEIPT'.             
           05  FILLER              PIC X(60)  VALUE  SPACES.                    
                                                                                
       01  WS-CONTROL-LIST-3.                                                   
           05  FILLER              PIC X(05)  VALUE SPACES.                     
           05  CL-SNO              PIC Z(4)9.                                   
           05  FILLER              PIC X(02)  VALUE SPACES.                     
           05  CL-RENO             PIC X(08)  VALUE SPACES.                     
           05  FILLER              PIC X(02)  VALUE SPACES.                     
           05  CL-SCH-ACC          PIC X(11)  VALUE SPACES.                     
           05  FILLER              PIC X(04)  VALUE SPACES.                     
           05  CL-PAY-TY           PIC X(03)  VALUE SPACES.                     
           05  FILLER              PIC X(06)  VALUE SPACES.                     
           05  CL-JR-NO            PIC X(14)  VALUE SPACES.                     
           05  FILLER              PIC X(05)  VALUE SPACES.                     
           05  CL-AMT-RECPT        PIC ZZZZZZ9.99.                              
           05  FILLER              PIC X(64)  VALUE SPACES.                     
                                                                                
       01  WS-CONTROL-LIST-4.                                                   
           05  FILLER              PIC X(132) VALUE SPACES.                     
                                                                                
       01  WS-CONTROL-LIST-5.                                                   
           05  FILLER              PIC X(4)   VALUE SPACES.                     
           05  FILLER              PIC X(65)  VALUE ALL '-'.                    
           05  FILLER              PIC X(63)  VALUE SPACES.                     
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
                                                                                
      ******************************************************************        
      *        MAIN LINE PROCESS                                       *        
      ******************************************************************        
       MAIN-PROCESS.                                                            
           PERFORM 000-OPEN-FILE                THRU 000-EXIT.                  
           PERFORM 100-READ-F552                THRU 100-EXIT.                  
           PERFORM 200-READ-F800                THRU 200-EXIT.                  
           PERFORM 300-PROCESS-SEQUENTIAL-FILES THRU 300-EXIT                   
               UNTIL WS-F552-EOF-FLAG = 'Y'                                     
               AND   WS-F800-EOF-FLAG = 'Y'.                                    
           PERFORM 1000-CLOSE-ROUTINE           THRU 1000-EXIT.                 
                                                                                
      ***************************************************************           
      *    OPENING FILES SECTION                                    *           
      ***************************************************************           
                                                                                
       000-OPEN-FILE.                                                           
           OPEN INPUT  AC28F552                                                 
                       BP13F800                                                 
                       BP13KD00                                                 
                       BP13KD03                                                 
                       BP13KD92                                                 
                I-O    BP13K140                                                 
                OUTPUT BP13F125                                                 
                       BP13LC19.                                                
                                                                                
           IF K140-STATUS NOT = 0  AND                                          
              K140-STATUS NOT = 97                                              
              DISPLAY ' OPENING ERROR, BP13K140 STATUS ' K140-STATUS            
              MOVE K140-STATUS           TO RETURN-CODE                         
              PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT.                        
                                                                                
           IF KD00-STATUS NOT = 0  AND                                          
              KD00-STATUS NOT = 97                                              
              DISPLAY ' OPENING ERROR, BP13KD00 STATUS ' KD00-STATUS            
              MOVE KD00-STATUS           TO RETURN-CODE                         
              PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT.                        
                                                                                
           IF KD03-STATUS NOT = 0  AND                                          
              KD03-STATUS NOT = 97                                              
              DISPLAY ' OPENING ERROR, BP13KD03 STATUS ' KD03-STATUS            
              MOVE KD03-STATUS           TO RETURN-CODE                         
              PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT.                        
                                                                                
           IF KD92-STATUS NOT = 0  AND                                          
              KD92-STATUS NOT = 97                                              
              DISPLAY ' OPENING ERROR, BP13KD92 STATUS ' KD92-STATUS            
              MOVE KD92-STATUS           TO RETURN-CODE                         
              PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT.                        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURRENT-DATE.                  
           STRING WS-DD '/' WS-MM '/' WS-CCYY                                   
                  DELIMITED BY SIZE INTO CL-DATE-HD.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ************************************************************              
      *    READ SEQUENTIAL FILE AC28F552                         *              
      ************************************************************              
                                                                                
       100-READ-F552.                                                           
                                                                                
           READ AC28F552                                                        
                AT END MOVE HIGH-VALUE  TO F552-NUM-HDBREF                      
                MOVE 'Y'        TO WS-F552-EOF-FLAG                             
                GO TO 100-EXIT.                                                 
           ADD  1                      TO WS-CNT-AC28F552-READ.                 
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
      *    READ SEQUENTIAL FILE BP13F800                         *              
      ************************************************************              
       200-READ-F800.                                                           
           READ BP13F800                                                        
                AT END MOVE HIGH-VALUE  TO F800-NUM-SCH-ACC                     
                MOVE 'Y'         TO WS-F800-EOF-FLAG                            
                GO TO 200-EXIT.                                                 
           ADD  1                       TO WS-CNT-BP13F800-READ.                
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-SEQUENTIAL-FILES.                                            
                                                                                
           MOVE SPACES TO WS-CDE-APPLT  WS-NUM-NRIC.                            
                                                                                
           IF F552-NUM-HDBREF = F800-NUM-SCH-ACC                                
               PERFORM 400-PROCESS-CASH THRU 400-EXIT                           
               PERFORM 100-READ-F552    THRU 100-EXIT                           
           ELSE                                                                 
               IF F552-NUM-HDBREF > F800-NUM-SCH-ACC                            
                  PERFORM 200-READ-F800 THRU 200-EXIT                           
               ELSE                                                             
                   DISPLAY ' KEY1 ' F552-NUM-HDBREF                             
                   MOVE SPACES TO BP13K140-KIVTRAN                              
                   INITIALIZE     BP13K140-KIVTRAN                              
                   MOVE 'A'                   TO K140-CDE-ERROR                 
                   MOVE  SPACES               TO   CL-RENO                      
                   PERFORM 500-WRITE-KIV-REC  THRU 500-EXIT                     
                   PERFORM 100-READ-F552      THRU 100-EXIT                     
               END-IF                                                           
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-PROCESS-CASH.                                                        
                                                                                
           IF F800-NUM-ALLO-CAT = 'DBS'                                         
              PERFORM 410-READ-KD00            THRU 410-EXIT                    
              IF WS-KD00-FND = 'Y'                                              
                 IF KD00-CDE-ACCT-STAT = 'C' OR 'X'                             
                    IF (F552-NUM-TXN-ID = 'LSCS') AND                           
                       (KD00-DTE-REPUDIATE = SPACES OR LOW-VALUES               
                                                    OR ZEROES) AND              
                       (KD00-DTE-EFF-TRANSFER = SPACES OR LOW-VALUES            
                                                        OR ZEROES) AND          
                       (KD00-DTE-REFINANCE = SPACES OR LOW-VALUES               
                                                        OR ZEROES)              
                       PERFORM 801-READ-BP13KD92    THRU 801-EXIT               
                       IF NOT KD92-FND                                          
                          MOVE SPACES               TO BP13K140-KIVTRAN         
                          INITIALIZE                   BP13K140-KIVTRAN         
                          MOVE F800-NUM-REGN        TO CL-RENO                  
                          MOVE SPACES               TO K140-CDE-ERROR           
                          PERFORM 500-WRITE-KIV-REC THRU 500-EXIT               
                          GO TO 400-EXIT                                        
                       END-IF                                                   
                    END-IF                                                      
                    PERFORM 430-OUTPUT-FILE         THRU 430-EXIT               
                 ELSE                                                           
                    MOVE SPACES TO BP13K140-KIVTRAN                             
                    INITIALIZE     BP13K140-KIVTRAN                             
                    MOVE  F800-NUM-REGN        TO   CL-RENO                     
                    PERFORM 500-WRITE-KIV-REC  THRU 500-EXIT                    
                 END-IF                                                         
              ELSE                                                              
                 MOVE SPACES TO BP13K140-KIVTRAN                                
                 INITIALIZE     BP13K140-KIVTRAN                                
                 MOVE  F800-NUM-REGN        TO   CL-RENO                        
                 PERFORM 500-WRITE-KIV-REC  THRU 500-EXIT                       
              END-IF                                                            
           ELSE                                                                 
              MOVE SPACES TO BP13K140-KIVTRAN                                   
              INITIALIZE     BP13K140-KIVTRAN                                   
              MOVE  F800-NUM-REGN        TO   CL-RENO                           
              PERFORM 500-WRITE-KIV-REC  THRU 500-EXIT                          
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
      *    SEARCH F800-NUM-REGN  IN BP13KD00                          *         
      *****************************************************************         
                                                                                
       410-READ-KD00.                                                           
                                                                                
           MOVE   SPACES             TO     BP13KD00-REC.                       
           INITIALIZE                       BP13KD00-REC.                       
           MOVE  F800-NUM-REGN       TO     KD00-NUM-REGN.                      
           READ BP13KD00.                                                       
           IF KD00-STATUS = 00 OR 02                                            
              MOVE 'Y'               TO     WS-KD00-FND                         
              ADD 1                  TO     WS-CNT-KD00-READ                    
           ELSE                                                                 
             IF KD00-STATUS = 23                                                
                PERFORM 411-READ-KD03       THRU 411-EXIT                       
                IF KD03-FND                                                     
                   MOVE BP13KD03-REC TO     BP13KD00-REC                        
                   MOVE 'Y'          TO     WS-KD00-FND                         
                ELSE                                                            
                   MOVE 'N'          TO     WS-KD00-FND                         
                END-IF                                                          
             ELSE                                                               
                DISPLAY 'ERROR WITH READ OF BP13KD00. STATUS IS '               
                       KD00-STATUS  ', KEY IS ' KD00-NUM-REGN                   
                MOVE   KD00-STATUS   TO   RETURN-CODE                           
                PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
       411-READ-KD03.                                                           
                                                                                
           MOVE   SPACES             TO     BP13KD03-REC.                       
           INITIALIZE                       BP13KD03-REC.                       
           MOVE  F800-NUM-REGN       TO     KD03-NUM-REGN.                      
           READ BP13KD03.                                                       
           IF KD03-STATUS = 00 OR 02                                            
              MOVE 'Y'               TO     WS-KD03-FND                         
           ELSE                                                                 
             IF KD03-STATUS = 23                                                
                MOVE 'N'             TO     WS-KD03-FND                         
             ELSE                                                               
                DISPLAY 'ERROR WITH READ OF BP13KD03. STATUS IS '               
                       KD03-STATUS ', KEY IS ' KD03-NUM-REGN                    
                MOVE   KD03-STATUS   TO   RETURN-CODE                           
                PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT                       
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       411-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       430-OUTPUT-FILE.                                                         
                                                                                
           MOVE SPACES                TO BP13F125-CASHTRAN.                     
           INITIALIZE                    BP13F125-CASHTRAN.                     
           MOVE F800-NUM-REGN         TO F125-NUM-REGN.                         
           MOVE F800-NUM-NRIC1        TO F125-NUM-NRIC.                         
           MOVE WS-CURRENT-DATE       TO F125-DTE-POST.                         
           MOVE ZEROS                 TO F125-COUNT-HISTORY.                    
           MOVE F552-AMT-TXN          TO F125-AMT-RECEIPT.                      
           MOVE F552-DTE-TXN          TO F125-DTE-TRANS.                        
           MOVE F552-NUM-HDBREF       TO F125-NUM-SCH-ACCT.                     
           MOVE F552-NUM-RCPT         TO F125-NUM-RECPT-JRNO.                   
           MOVE F552-CDE-BO           TO F125-CDE-BO.                           
           MOVE '11'                  TO F125-CDE-TRANS-TYPE.                   
           MOVE 'C'                   TO F125-CDE-CRDR.                         
                                                                                
           IF F552-NUM-TXN-ID = 'INCS'                                          
              MOVE 'INS'              TO F125-CDE-PAYMENT-TYPE                  
              MOVE KD00-CDE-LOAN-SCH  TO F125-NUM-LOAN-SCH                      
           ELSE                                                                 
              IF F552-NUM-TXN-ID = 'LSCS'                                       
                 MOVE 'LS '           TO F125-CDE-PAYMENT-TYPE                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           WRITE BP13F125-CASHTRAN.                                             
           ADD 1 TO WS-CNT-F125-VALID.                                          
                                                                                
       430-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      * OUTPUT ERROR RECORD TO KIV FILE                                         
                                                                                
       500-WRITE-KIV-REC.                                                       
                                                                                
           IF K140-CDE-ERROR NOT = 'A'                                          
              MOVE F800-NUM-REGN      TO K140-NUM-REGN                          
              MOVE F800-NUM-NRIC1     TO K140-NUM-NRIC                          
           END-IF.                                                              
           MOVE '11'                  TO K140-CDE-TRANS-TYPE.                   
           MOVE 'C'                   TO K140-CDE-CRDR.                         
           MOVE WS-CURRENT-DATE       TO K140-DTE-POST.                         
           ADD  1                     TO WS-K140-COUNT-HISTORY.                 
           MOVE 'N'                   TO K140-COUNT-HIST-CHAR.                  
           MOVE WS-K140-COUNT-HISTORY TO K140-COUNT-HIST-NUM.                   
           MOVE F552-AMT-TXN          TO K140-AMT-RECEIPT.                      
           MOVE F552-DTE-TXN          TO K140-DTE-TRANS.                        
           MOVE F552-NUM-HDBREF       TO K140-NUM-SCH-ACCT.                     
           MOVE F552-NUM-RCPT         TO K140-NUM-RECPT-JRNO.                   
           MOVE  'A'                  TO K140-CDE-ERROR.                        
                                                                                
           IF F552-TXT-DESC-1 = 'STAMP  '                                       
              MOVE 'DSP'              TO K140-CDE-PAYMENT-TYPE                  
           ELSE                                                                 
            IF F552-TXT-DESC-1 = 'CONVY  '                                      
               MOVE 'DCV'             TO K140-CDE-PAYMENT-TYPE                  
            ELSE                                                                
               IF F552-NUM-TXN-ID = 'LSCS'                                      
                  MOVE 'LS '          TO K140-CDE-PAYMENT-TYPE                  
           END-IF.                                                              
                                                                                
           WRITE BP13K140-KIVTRAN.                                              
                                                                                
           MOVE K140-STATUS TO  WS-K140-STATUS.                                 
                                                                                
           IF WS-K140-STATUS1 = 0                                               
              ADD 1 TO WS-CNT-K140-KIV                                          
           ELSE                                                                 
              IF K140-STATUS = 22                                               
                 DISPLAY ' ERROR WRITING DUP REC : ' K140-KEY-FLD               
              ELSE                                                              
                 DISPLAY ' OPENING ERROR, BP13K140 STATUS '                     
                         K140-STATUS                                            
                 MOVE K140-STATUS           TO   RETURN-CODE                    
                 PERFORM 1000-CLOSE-ROUTINE THRU 1000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 600-MOVE-VALUE-REPORT    THRU 600-EXIT.                      
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *    PRINT CONTROL-LIST                                         *         
      *****************************************************************         
                                                                                
       600-MOVE-VALUE-REPORT.                                                   
                                                                                
           IF WS-LINE-CNT > 53                                                  
              MOVE ZEROES              TO   WS-LINE-CNT                         
              PERFORM 610-PRINT-HEAD   THRU 610-EXIT                            
           END-IF.                                                              
                                                                                
           MOVE  F552-NUM-HDBREF       TO   CL-SCH-ACC.                         
           MOVE  K140-CDE-PAYMENT-TYPE TO   CL-PAY-TY.                          
           MOVE  F552-NUM-RCPT         TO   CL-JR-NO.                           
           MOVE  F552-AMT-TXN          TO   CL-AMT-RECPT.                       
           ADD  +1                     TO   WS-SNO.                             
           MOVE WS-SNO                 TO   CL-SNO.                             
           PERFORM 620-PRINT-REC       THRU 620-EXIT.                           
                                                                                
       600-EXIT.                                                                
            EXIT.                                                               
                                                                                
                                                                                
       610-PRINT-HEAD.                                                          
           ADD +1            TO WS-PAGENO.                                      
           MOVE WS-PAGENO    TO CL-PAGENO.                                      
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-HEAD   AFTER PAGE.          
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-1.                     
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-4.                     
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-2.                     
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-5.                     
           ADD +4  TO WS-LINE-CNT.                                              
                                                                                
       610-EXIT.                                                                
            EXIT.                                                               
                                                                                
                                                                                
       620-PRINT-REC.                                                           
           WRITE PRINT-CONTROL-LIST FROM WS-CONTROL-LIST-3.                     
           ADD +1  TO WS-LINE-CNT.                                              
                                                                                
       620-EXIT.                                                                
            EXIT.                                                               
                                                                                
                                                                                
       801-READ-BP13KD92.                                                       
                                                                                
           MOVE SPACES                       TO BP13KD92-REC                    
           INITIALIZE                           BP13KD92-REC                    
                                                                                
           MOVE F800-NUM-REGN                TO KD92-NUM-REGN                   
           MOVE 'LUMP'                       TO KD92-NUM-TRANS                  
                                                                                
           READ BP13KD92.                                                       
                                                                                
           IF KD92-STATUS = '00' OR '02'                                        
              MOVE 'Y'                       TO WS-KD92-FND                     
           ELSE                                                                 
              IF KD92-STATUS = '23'                                             
                 MOVE 'N'                    TO WS-KD92-FND                     
              ELSE                                                              
                 DISPLAY 'BP13KD92 - ERROR READ, KEY (' KD92-KEY-FLD            
                         '), STATUS (' KD92-STATUS ')'                          
                 GO TO 1000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       801-EXIT.                                                                
           EXIT.                                                                
                                                                                
       1000-CLOSE-ROUTINE.                                                      
                                                                                
           DISPLAY '******  BP13CC19 *************'.                            
           DISPLAY 'NO OF F552 READ      (F552) : '                             
                                             WS-CNT-AC28F552-READ.              
           DISPLAY 'NO OF RFF RECS READ  (F800) : '                             
                                             WS-CNT-BP13F800-READ.              
           DISPLAY 'NO OF KD00 REC READ  (KD00) : ' WS-CNT-KD00-READ.           
           DISPLAY 'NO OF F125 REC WRITE (F125) : ' WS-CNT-F125-VALID.          
           DISPLAY 'NO OF K140 REC WRITE (K140) : ' WS-CNT-K140-KIV.            
                                                                                
           CLOSE BP13F800                                                       
                 AC28F552                                                       
                 BP13F125                                                       
                 BP13K140                                                       
                 BP13KD00                                                       
                 BP13KD92                                                       
                 BP13KD03                                                       
                 BP13LC19.                                                      
                                                                                
           IF K140-STATUS NOT = 0 AND K140-STATUS NOT = 97                      
               DISPLAY ' CLOSING ERROR, BP13K140 STATUS ' K140-STATUS           
               MOVE K140-STATUS TO RETURN-CODE.                                 
                                                                                
           IF KD00-STATUS NOT = 0 AND KD00-STATUS NOT = 97                      
               DISPLAY ' CLOSING ERROR, BP13KD00 STATUS ' KD00-STATUS           
               MOVE KD00-STATUS TO RETURN-CODE.                                 
                                                                                
           IF KD03-STATUS NOT = 0 AND KD03-STATUS NOT = 97                      
               DISPLAY ' CLOSING ERROR, BP13KD03 STATUS ' KD03-STATUS           
               MOVE KD03-STATUS TO RETURN-CODE.                                 
                                                                                
           IF KD92-STATUS NOT = 0 AND KD92-STATUS NOT = 97                      
               DISPLAY ' CLOSING ERROR, BP13KD92 STATUS ' KD92-STATUS           
               MOVE KD92-STATUS TO RETURN-CODE.                                 
                                                                                
       1000-EXIT.                                                               
           STOP RUN.                                                            
