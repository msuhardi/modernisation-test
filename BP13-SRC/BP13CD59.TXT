       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD59.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  10/02/2009.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE : TO MATCH F800 WITH F820                        *            
      *                                                            *            
      * INPUT  FILES : BP13F820                                    *            
      *                BP13F800                                    *            
      * OUTPUT FILES : P13F820A                                    *            
      *                                                            *            
      * CHG-NO   BY   ON         DESCRIPTION                       *            
      * -------- ---- ---------- ----------------------------------*            
      * BP133535 JB8  10/02/2009 NEW PGM                           *            
      **************************************************************            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F800   ASSIGN TO BP13F800.                                
           SELECT BP13F820   ASSIGN TO BP13F820.                                
           SELECT P13F820A   ASSIGN TO P13F820A.                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13F820                                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F820.                                                           
                                                                                
       FD   P13F820A                                                            
            RECORD CONTAINS 400  CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       01   P13F820A-REC.                                                       
            05 FILLER             PIC X(400).                                   
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-F800-READ            PIC 9(08) VALUE ZEROES.                       
       01 WS-F820-READ            PIC 9(08) VALUE ZEROES.                       
       01 WS-F820-NOTFD           PIC 9(08) VALUE ZEROES.                       
       01 WS-F820A-WRITE          PIC 9(08) VALUE ZEROES.                       
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F800   THRU 2000-EXIT.                         
           PERFORM 3000-READ-BP13F820   THRU 3000-EXIT.                         
           PERFORM 4000-PROCESS-DATA    THRU 4000-EXIT                          
                   UNTIL F800-NUM-REGN = HIGH-VALUES  AND                       
                         F820-NUM-REGN = HIGH-VALUES                            
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13F820                                                 
               OUTPUT  P13F820A.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F800                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F800-NUM-REGN                            
                NOT AT END                                                      
                   ADD 1    TO WS-F800-READ                                     
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-READ-BP13F820.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F820                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F820-NUM-REGN                            
                NOT AT END                                                      
                   ADD 1    TO WS-F820-READ                                     
           END-READ.                                                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           EVALUATE TRUE                                                        
              WHEN F800-NUM-REGN  =  F820-NUM-REGN                              
                 PERFORM 5000-WRITE-P13F820A THRU 5000-EXIT                     
                    UNTIL (F820-NUM-REGN  NOT =  F800-NUM-REGN)  OR             
                          (F820-NUM-REGN = HIGH-VALUES)                         
                                                                                
                 PERFORM 2000-READ-BP13F800    THRU 2000-EXIT                   
                                                                                
              WHEN F800-NUM-REGN  <  F820-NUM-REGN                              
                 ADD 1    TO WS-F820-NOTFD                                      
                 PERFORM 2000-READ-BP13F800    THRU 2000-EXIT                   
              WHEN F820-NUM-REGN  <  F800-NUM-REGN                              
                 PERFORM 3000-READ-BP13F820    THRU 3000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-WRITE-P13F820A.                                                     
      *-------------------------------------------------------------            
                                                                                
           IF F820-NUM-NRIC = F800-NUM-NRIC1 OR F800-NUM-NRIC2 OR               
                              F800-NUM-NRIC3 OR F800-NUM-NRIC4                  
              MOVE  BP13F820-REC     TO P13F820A-REC                            
              WRITE P13F820A-REC                                                
              ADD 1 TO WS-F820A-WRITE                                           
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-BP13F820    THRU 3000-EXIT.                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13CD59 CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ FROM BP13F800  : ' WS-F800-READ.            
           DISPLAY 'NO OF RECS READ FROM BP13F820  : ' WS-F820-READ.            
           DISPLAY 'NO OF RECS WRITTEN TO P13F820A : ' WS-F820A-WRITE.          
           DISPLAY 'NO OF RECS F820 NOTFD          : ' WS-F820-NOTFD.           
                                                                                
           CLOSE BP13F800                                                       
                 BP13F820                                                       
                 P13F820A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
