       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C124.                                                 
      *AUTHOR.        ED.                                                       
      *DATE-WRITTEN.  02 AUG 2002.                                              
      * ========================================================== *            
      * FLAT INVENTORY SYSTEM (BP13)                               *            
      * ========================================================== *            
      *  OBJECTIVE   : TO DELETE FROM BP13K740 ALL UNSUCCESSFUL    *            
      *                GROPA APPLICATIONS AFTER 3RD ROUND PA       *            
      *                PIGEONHOLDING, (BP13AMP4).                  *            
      *  INPUT FILE  : BP13F740                                    *            
      *  I-O   FILES : BP13K740                                    *            
      *  OUTPUTFILE  : P13F740B                                    *            
      * ---------------------------------------------------------- *            
      * CHG-NO   BY  DATE    DETAILS                               *            
      * -------- --- ------  -------                               *            
      * BP132171 ED  020802  NEW PGM                               *            
      * BP132412 SK  021002  ADD BP13K520                          *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
           SELECT BP13K740 ASSIGN TO BP13K740                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K740-KEY-FLD                                  
                  FILE STATUS  IS WS-K740-STATUS.                               
                                                                                
           SELECT BP13K520 ASSIGN TO BP13K520                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K520-KEY-NEW                               
                  FILE STATUS     IS WS-K520-STATUS.                            
                                                                                
           SELECT P13F740B ASSIGN TO P13F740B.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F740                                                            
            RECORD CONTAINS 500  CHARACTERS                                     
            BLOCK CONTAINS    0  RECORDS                                        
            RECORDING MODE   IS  F                                              
            LABEL RECORDS        STANDARD.                                      
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K740                                                            
            RECORD CONTAINS 500  CHARACTERS.                                    
       COPY BP13K740.                                                           
                                                                                
       FD   BP13K520                                                            
            RECORD CONTAINS 500  CHARACTERS.                                    
       COPY BP13K520.                                                           
                                                                                
       FD   P13F740B                                                            
            RECORD CONTAINS 500  CHARACTERS                                     
            BLOCK CONTAINS    0  RECORDS                                        
            RECORDING MODE   IS  F                                              
            LABEL RECORDS        STANDARD.                                      
       01   P13F740B-REC         PIC X(500).                                    
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-INDICATORS.                                                  
           05  WS-F740-EOF                PIC X(01) VALUE 'N'.                  
           05  WS-K740-STATUS             PIC 9(02) VALUE ZEROS.                
           05  WS-F740-READ               PIC 9(06) VALUE ZEROS.                
           05  WS-K740-DELETE             PIC 9(06) VALUE ZEROS.                
           05  WS-F740-WRITE              PIC 9(06) VALUE ZEROS.                
           05  WS-K740-FOUND              PIC X(01) VALUE 'N'.                  
           05  WS-K520-FOUND              PIC X(01) VALUE 'N'.                  
           05  WS-K520-STATUS             PIC 9(02) VALUE ZEROS.                
           05  WS-K520-DELETE             PIC 9(06) VALUE ZEROS.                
                                                                                
       01  WS-CURRENT-DATE.                                                     
           02  WS-DATE                    PIC X(8).                             
           02  WS-CURRENT-TIME            PIC X(9).                             
           02  FILLER                     PIC X(4).                             
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM 100-INITIALIZATION     THRU 100-EXIT.                        
           PERFORM 200-READ-BP13F740      THRU 200-EXIT.                        
                                                                                
           PERFORM 300-PROCESS-F740       THRU 300-EXIT                         
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 9990-CLOSE-FILES        THRU 9990-EXIT.                      
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-INITIALIZATION.                                                      
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F740                                                 
                  I-O  BP13K740                                                 
                       BP13K520                                                 
               OUTPUT  P13F740B.                                                
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K740  OPEN ERROR ' WS-K740-STATUS               
              MOVE WS-K740-STATUS TO RETURN-CODE                                
              PERFORM 9990-CLOSE-FILES THRU 9990-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-K520-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K520  OPEN ERROR ' WS-K520-STATUS               
              MOVE WS-K520-STATUS TO RETURN-CODE                                
              PERFORM 9990-CLOSE-FILES THRU 9990-EXIT                           
           END-IF.                                                              
                                                                                
           DISPLAY  'BP13C124:          LIST OF DELETED GRO PA APPL'            
                    'ICATIONS'                                                  
           DISPLAY  ' '                                                         
           DISPLAY  'REFNO   REGNO    REQ-NT  FT   UNSUCC-CDE'.                 
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-BP13F740.                                                       
      *-------------------------------------------------------------            
           READ BP13F740 AT END                                                 
                MOVE 'Y'        TO WS-F740-EOF                                  
                GO  TO 200-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1       TO WS-F740-READ.                                         
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-F740.                                                        
      *-------------------------------------------------------------            
                                                                                
           PERFORM 310-READ-K740          THRU 310-EXIT.                        
                                                                                
           IF WS-K740-FOUND = 'Y'                                               
              PERFORM 400-DELETE-K740        THRU 400-EXIT                      
              PERFORM 500-WRITE-P13F740B     THRU 500-EXIT                      
           ELSE                                                                 
              PERFORM 320-READ-K520          THRU 320-EXIT                      
              IF WS-K520-FOUND = 'Y'                                            
                 PERFORM 410-DELETE-K520        THRU 410-EXIT                   
                 PERFORM 500-WRITE-P13F740B     THRU 500-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-BP13F740      THRU 200-EXIT.                        
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       310-READ-K740.                                                           
      *--------------------------------------------------------*                
                                                                                
           MOVE LOW-VALUES       TO K740-KEY-FLD.                               
           MOVE F740-NUM-RANDOM  TO K740-KEY-FLD.                               
           MOVE 'Y'              TO WS-K740-FOUND.                              
                                                                                
           READ BP13K740.                                                       
                                                                                
           EVALUATE WS-K740-STATUS                                              
              WHEN 00                                                           
                 MOVE 'Y' TO WS-K740-FOUND                                      
              WHEN 23                                                           
                 MOVE 'N' TO WS-K740-FOUND                                      
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN READ OF BP13K740. STATUS IS '                
                          WS-K740-STATUS                                        
                 PERFORM 9990-CLOSE-FILES THRU 9990-EXIT                        
            END-EVALUATE.                                                       
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       320-READ-K520.                                                           
      *--------------------------------------------------------*                
                                                                                
           MOVE LOW-VALUES      TO K520-NUM-REGN.                               
           MOVE F740-NUM-REGN   TO K520-NUM-REGN.                               
           MOVE 'Y'             TO WS-K520-FOUND.                               
                                                                                
           READ BP13K520.                                                       
                                                                                
           EVALUATE WS-K520-STATUS                                              
              WHEN 00                                                           
                 MOVE 'Y' TO WS-K520-FOUND                                      
              WHEN 23                                                           
                 MOVE 'N' TO WS-K520-FOUND                                      
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN READ OF BP13K520. STATUS IS '                
                          WS-K520-STATUS                                        
                 PERFORM 9990-CLOSE-FILES THRU 9990-EXIT                        
            END-EVALUATE.                                                       
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-DELETE-K740.                                                         
      *-------------------------------------------------------------            
           MOVE BP13F740-REC        TO BP13K740-REC.                            
           DELETE BP13K740.                                                     
                                                                                
           EVALUATE WS-K740-STATUS                                              
               WHEN 00                                                          
                    ADD 1        TO WS-K740-DELETE                              
               WHEN OTHER                                                       
                    DISPLAY 'ERROR DELETING BP13K740. KEY IS  '                 
                             F740-KEY-FLD ', STATUS IS '                        
                             WS-K740-STATUS                                     
                    GO TO 9990-CLOSE-FILES                                      
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       410-DELETE-K520.                                                         
      *-------------------------------------------------------------            
           MOVE BP13F740-REC        TO BP13K520-REC.                            
           DELETE BP13K520.                                                     
                                                                                
           EVALUATE WS-K520-STATUS                                              
               WHEN 00                                                          
                    ADD 1        TO WS-K520-DELETE                              
               WHEN OTHER                                                       
                    DISPLAY 'ERROR DELETING BP13K520. KEY IS  '                 
                             K520-NUM-REGN ', STATUS IS '                       
                             WS-K520-STATUS                                     
                    GO TO 9990-CLOSE-FILES                                      
           END-EVALUATE.                                                        
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-WRITE-P13F740B.                                                      
      *-------------------------------------------------------------            
           WRITE P13F740B-REC         FROM BP13F740-REC.                        
           ADD   1                      TO WS-F740-WRITE.                       
                                                                                
           DISPLAY F740-NUM-REF     '  ' F740-NUM-REGN      '  '                
                   F740-CDE-NT1 '     '  K740-CDE-FLAT-TYPE '  '                
                   F740-CDE-UNSUCC.                                             
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       9990-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           CLOSE BP13F740                                                       
                 BP13K740                                                       
                 BP13K520.                                                      
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K740  CLOSE ERROR ' WS-K740-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K520-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K520  CLOSE ERROR ' WS-K520-STATUS              
           END-IF.                                                              
                                                                                
           DISPLAY '============ BP13C124 STATISTICS =============='.           
           DISPLAY ' '                                                          
           DISPLAY 'BP13F740 RECORDS READ       ' WS-F740-READ                  
           DISPLAY 'BP13K740 RECORDS DELETED    ' WS-K740-DELETE                
           DISPLAY 'BP13K520 RECORDS DELETED    ' WS-K520-DELETE                
           DISPLAY 'P13F740B RECORDS WRITTEN    ' WS-F740-WRITE                 
           DISPLAY ' '                                                          
           DISPLAY '==============================================='.           
           STOP RUN.                                                            
                                                                                
       9990-EXIT.                                                               
           EXIT.                                                                
