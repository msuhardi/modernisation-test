      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C73B.                                                 
      *AUTHOR.        MARIA REGINA LAM KO.                                      
      *DATE-WRITTEN.  03/01/2012.                                               
                                                                                
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE   :    GET DEMAND COUNT PER PROJECT IN BP13KB78 *            
      *                   COUNT ONLY KB78-DTE-REQUESTED >= 20120106*            
      *                   FOR D10 CASES                            *            
      *              :    FOR DBSS/EC                              *            
      *                                                            *            
      *  INPUT FILE  :     BP13KB78                                *            
      *  OUTPUT FILE :     BP13K76P                                *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  BY    DATE    DESCRIPTION                         *            
      * -------- ---  ------   -----------                         *            
      * BP134402 MRN1 20120301 NEW PROGRAM.                        *            
      * BP13XXXX ZDD1 20121128 GET DEMAND COUNT ONLY FOR E16 IF    *            
      *                        KB78-DTE-REQUESTED >= 20121130      *            
      *                        FOR E18 KB78-DTE-REQUESTED >= 20121129           
      * ========================================================== *            
                                                                                
      ***********************                                                   
       ENVIRONMENT DIVISION.                                                    
      ***********************                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13KB78  ASSIGN       TO BP13KB78                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS KB78-KEY-FLD                        
                            ALTERNATE KEY IS KB78-ALT-KEY                       
                            FILE STATUS  IS BP13KB78-STATUS.                    
                                                                                
           SELECT BP13K76P  ASSIGN       TO BP13K76P                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K76P-KEY-FLD                        
                            FILE STATUS  IS BP13K76P-STATUS.                    
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13KB78                                                            
            RECORD CONTAINS 3300 CHARACTERS.                                    
       COPY BP13KB78.                                                           
                                                                                
       FD   BP13K76P                                                            
            RECORD CONTAINS 50 CHARACTERS.                                      
       COPY BP13K76P.                                                           
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
       01  WS-COUNTERS.                                                         
           05  WS-KB78-READ-CTR             PIC 9(06)  VALUE ZEROS.             
           05  WS-K76P-WRITE-CTR            PIC 9(06)  VALUE ZEROS.             
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-KB78-EOF                  PIC X(01)  VALUE 'N'.               
           05  WS-MULTIPLE                  PIC X(01)  VALUE 'N'.               
           05  BP13KB78-STATUS              PIC 99     VALUE 00.                
           05  BP13K76P-STATUS              PIC 99     VALUE 00.                
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE.                                                         
               10  WS-CCYY                  PIC 9(4)   VALUE 0000.              
               10  WS-MM                    PIC 99     VALUE 00.                
               10  WS-DD                    PIC 99     VALUE 00.                
           05  WS-CURR-DATE                 PIC 9(08)  VALUE ZEROES.            
           05  WS-CURR-TIME                 PIC 9(08)  VALUE ZEROES.            
                                                                                
       01  WS-KB78-NUM-PROJECT              PIC X(03)  VALUE SPACES.            
       01  WS-KB78-DTE-REQUESTED            PIC 9(08)  VALUE ZEROES.            
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
       0000-MAIN-ROUTINE.                                                       
      ******************************************************************        
           PERFORM 1000-OPEN-FILES             THRU 1000-EXIT.                  
           PERFORM 4000-READ-BP13KB78          THRU 4000-EXIT                   
                       UNTIL WS-KB78-EOF = 'Y'.                                 
           PERFORM 9000-CLOSE-ROUTINE          THRU 9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
           OPEN INPUT   BP13KB78                                                
                I-O     BP13K76P.                                               
                                                                                
           IF BP13KB78-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13KB78-STATUS ' BP13KB78-STATUS         
              MOVE     BP13KB78-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE.                                      
                                                                                
           IF BP13K76P-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPENING ERROR, BP13K76P-STATUS ' BP13K76P-STATUS         
              MOVE     BP13K76P-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE.                                      
                                                                                
           MOVE ZEROES                     TO WS-KB78-READ-CTR                  
                                              K76P-NUM-DEMAND.                  
           MOVE LOW-VALUES                 TO KB78-ALT-KEY.                     
           START BP13KB78              KEY >= KB78-ALT-KEY.                     
                                                                                
           IF BP13KB78-STATUS NOT = 00 AND 97                                   
              DISPLAY 'START ERROR, BP13KB78-STATUS ' BP13KB78-STATUS           
              MOVE     BP13KB78-STATUS  TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE.                                      
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DATE WS-CURR-DATE.             
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-CURR-TIME.                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-READ-BP13KB78.                                                      
      ******************************************************************        
           READ BP13KB78 NEXT RECORD.                                           
           EVALUATE BP13KB78-STATUS                                             
           WHEN 00                                                              
                IF (KB78-NUM-PROJECT NOT = SPACES)                              
                   ADD  1  TO  WS-KB78-READ-CTR                                 
                   IF WS-KB78-READ-CTR = 1                                      
                      MOVE KB78-NUM-PROJECT   TO WS-KB78-NUM-PROJECT            
                   END-IF                                                       
                   IF KB78-NUM-PROJECT = WS-KB78-NUM-PROJECT                    
                      MOVE 'Y' TO WS-MULTIPLE                                   
                      IF KB78-NUM-PROJECT NOT = 'E18' AND 'E16'                 
                         ADD  1   TO  K76P-NUM-DEMAND                           
                      END-IF                                                    
                      IF KB78-NUM-PROJECT = 'E18'                               
                         IF (KB78-DTE-REQUESTED >= 20121129)                    
                            ADD  1   TO  K76P-NUM-DEMAND                        
                         END-IF                                                 
                      END-IF                                                    
                      IF KB78-NUM-PROJECT = 'E16'                               
                         IF (KB78-DTE-REQUESTED >= 20121130)                    
                            ADD  1   TO  K76P-NUM-DEMAND                        
                         END-IF                                                 
                      END-IF                                                    
                   ELSE                                                         
                      MOVE WS-KB78-NUM-PROJECT TO K76P-NUM-PROJECT              
                      PERFORM 6000-WRITE-BP13K76P THRU 6000-EXIT                
                      MOVE KB78-NUM-PROJECT TO WS-KB78-NUM-PROJECT              
                      IF KB78-NUM-PROJECT NOT = 'E18' AND 'E16'                 
                         MOVE 1          TO  K76P-NUM-DEMAND                    
                      END-IF                                                    
                      IF KB78-NUM-PROJECT = 'E18'                               
                         IF KB78-DTE-REQUESTED >= 20121129                      
                            MOVE 1       TO K76P-NUM-DEMAND                     
                         ELSE                                                   
                            MOVE ZEROES  TO K76P-NUM-DEMAND                     
                         END-IF                                                 
                      END-IF                                                    
                      IF KB78-NUM-PROJECT = 'E16'                               
                         IF KB78-DTE-REQUESTED >= 20121130                      
                            MOVE 1       TO K76P-NUM-DEMAND                     
                         ELSE                                                   
                            MOVE ZEROES  TO K76P-NUM-DEMAND                     
                         END-IF                                                 
                      END-IF                                                    
                      MOVE 'N' TO WS-MULTIPLE                                   
                   END-IF                                                       
                END-IF                                                          
           WHEN 10                                                              
                MOVE KB78-NUM-PROJECT TO K76P-NUM-PROJECT                       
                IF WS-MULTIPLE = 'N'                                            
                   MOVE 1  TO  K76P-NUM-DEMAND                                  
                END-IF                                                          
                PERFORM 6000-WRITE-BP13K76P THRU 6000-EXIT                      
                MOVE 'Y'                  TO WS-KB78-EOF                        
           WHEN OTHER                                                           
                DISPLAY 'BP13KB78 READNEXT ERROR: ' BP13KB78-STATUS             
                MOVE BP13KB78-STATUS      TO RETURN-CODE                        
                PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6000-WRITE-BP13K76P.                                                     
      ******************************************************************        
                                                                                
           MOVE WS-CURR-DATE  TO K76P-DTE-UPDATE.                               
           MOVE WS-CURR-TIME  TO K76P-TME-UPDATE.                               
                                                                                
           WRITE BP13K76P-REC.                                                  
           EVALUATE BP13K76P-STATUS                                             
           WHEN 00                                                              
           WHEN 02                                                              
                ADD   1   TO WS-K76P-WRITE-CTR                                  
                                                                                
           WHEN 22                                                              
                REWRITE BP13K76P-REC                                            
                ADD   1                  TO WS-K76P-WRITE-CTR                   
                                                                                
           WHEN OTHER                                                           
               DISPLAY 'ADD ERROR  : ' KB78-NUM-PROJECT                         
                       ' ' BP13K76P-STATUS                                      
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-ROUTINE.                                                      
      ******************************************************************        
            DISPLAY ' '.                                                        
            DISPLAY ' ====  BP13C73B CONTROL TOTALS  ===='.                     
            DISPLAY ' '.                                                        
            DISPLAY ' KB78 RECORDS READ          : ' WS-KB78-READ-CTR.          
            DISPLAY ' K76P RECORDS WRITTEN       : ' WS-K76P-WRITE-CTR.         
            DISPLAY ' '.                                                        
            DISPLAY ' ==================================='.                     
                                                                                
            CLOSE BP13KB78                                                      
                  BP13K76P.                                                     
                                                                                
            IF BP13KB78-STATUS NOT = 00                                         
               DISPLAY 'CLOSING ERROR-BP13KB78-STAT ' BP13KB78-STATUS.          
                                                                                
            IF BP13K76P-STATUS NOT = 00                                         
               DISPLAY 'CLOSING ERROR-BP13K76P-STAT ' BP13K76P-STATUS.          
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
