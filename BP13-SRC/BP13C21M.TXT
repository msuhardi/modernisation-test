       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C21M.                                                 
       AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
       DATE-WRITTEN.  14/04/2008.                                               
      * ========================================================= *             
      *              ADD APPOINTMENT SCHEDULES IN K225            *             
      * ========================================================= *             
      *  PURPOSE OF THIS PROGRAM: CREATE APPOINTMENT SCHEDULES    *             
      *                           IN BP13K225 FROM BOOKING APPMT  *             
      *                           DETAILS FLAT FILE BP13F223      *             
      *                                                           *             
      *  INPUT        :  BP13F223                                 *             
      *  I-O          :  BP13K225                                 *             
      *                                                           *             
      * ----------------------------------------------------------*             
      * CHG REF  DATE     BY   DESCRIPTION                        *             
      * -------- -------- --- ------------------------------------*             
      * BP133307 14/04/08 RB12 NEW PROGRAM                        *             
      * BP133071 17/09/08 RB12 CATER FOR SALES MODE 3R1           *             
      * BP133430 15/10/08 RB12 CATER FOR NEW PREFIX FOR WIH       *             
      * BP133531 24/03/09 RB12 CREATE WIH QUEUE NOS W/ PREFIX '2' *             
      * ==========================================================*             
                                                                                
       ENVIRONMENT    DIVISION.                                                 
       CONFIGURATION  SECTION.                                                  
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13K225  ASSIGN        TO BP13K225                           
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K225-KEY-FLD                       
                            ALTERNATE KEY IS K225-ALT-KEY                       
                            ACCESS MODE   IS DYNAMIC                            
                            FILE STATUS   IS K225-FILE-STATUS.                  
                                                                                
           SELECT BP13F223  ASSIGN        TO BP13F223.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13K225                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13K225.                                                           
                                                                                
       FD   BP13F223                                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13K223.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  K225-FILE-STATUS               PIC 9(2) VALUE 0.                     
                                                                                
       01  WS-SAVE-DATE                   PIC X(8) VALUE SPACES.                
       01  WS-PREFIX                      PIC X    VALUE SPACES.                
       01  WS-PREFIX2                     PIC X    VALUE SPACES.                
       01  WS-CTR                         PIC 9(2) VALUE ZEROES.                
                                                                                
       01  WS-SAVE-SALES-MODE             PIC X(3) VALUE SPACES.                
       01  WS-SAVE-SALES-MODE-CNT         PIC 9(5) VALUE ZEROES.                
                                                                                
       01  WS-QUEUE-NUM REDEFINES WS-SAVE-SALES-MODE-CNT.                       
           05 FILLER                      PIC X.                                
           05 WS-QUEUE-NUM-DIGITS         PIC X(4).                             
                                                                                
       01  WS-SAVE-QUEUE                  PIC X(5) VALUE SPACES.                
       01  WS-SAVE-QUEUE-REDEF REDEFINES WS-SAVE-QUEUE.                         
           05 FILLER                      PIC X.                                
           05 WS-SAVE-QUEUE-NUM           PIC 9(4).                             
                                                                                
       01  WS-K223-NUM-CASE-ALPHA         PIC X(2).                             
       01  WS-K223-NUM-CASE REDEFINES WS-K223-NUM-CASE-ALPHA                    
                                          PIC 9(2).                             
                                                                                
       01  WS-F223-READ-CNT               PIC 9(8) VALUE ZEROES.                
       01  WS-K225-KEY-FND                PIC 9(8) VALUE ZEROES.                
       01  WS-K225-WRITE-CNT              PIC 9(8) VALUE ZEROES.                
       01  WS-K225-WRITE-ERR              PIC 9(8) VALUE ZEROES.                
       01  WS-F200-REWRITE-CNT            PIC 9(8) VALUE ZEROES.                
                                                                                
       01  WS-DTE-CURRENT.                                                      
           05  WS-DTE-CCYY                PIC 9(4) VALUE ZEROES.                
           05  WS-DTE-MM                  PIC 9(2) VALUE ZEROES.                
           05  WS-DTE-DD                  PIC 9(2) VALUE ZEROES.                
                                                                                
       01  WS-APP-INFO.                                                         
           05  WS-NME-HA1                 PIC X(30).                            
           05  WS-NUM-TEL                 PIC X(8).                             
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F223-EOF-FLAG           PIC X VALUE 'N'.                      
               88 WS-F223-EOF-YES               VALUE 'Y'.                      
               88 WS-F223-EOF-NO                VALUE 'N'.                      
           05  WS-K225-EOF-FLAG           PIC X VALUE 'N'.                      
               88 WS-K225-EOF-YES               VALUE 'Y'.                      
               88 WS-K225-EOF-NO                VALUE 'N'.                      
           05  WS-K225-KEY-FOUND-FLAG     PIC X VALUE 'N'.                      
               88 WS-K225-KEY-FOUND             VALUE 'Y'.                      
               88 WS-K225-KEY-NOT-FOUND         VALUE 'N'.                      
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-PRE-PROCESS         THRU 1000-EXIT.                     
           PERFORM 1500-READ-BP13F223       THRU 1500-EXIT.                     
           PERFORM 2100-FIND-LAST-QUEUE     THRU 2100-EXIT.                     
           PERFORM 2000-PROCESS-F223        THRU 2000-EXIT                      
             UNTIL WS-F223-EOF-YES.                                             
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-PRE-PROCESS.                                                        
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F223                                                 
                I-O    BP13K225.                                                
                                                                                
           IF K225-FILE-STATUS NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING BP13K225 : ' K225-FILE-STATUS              
              MOVE     K225-FILE-STATUS    TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1500-READ-BP13F223.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F223 AT END                                                 
                MOVE 'Y' TO WS-F223-EOF-FLAG                                    
                GO TO 1500-EXIT.                                                
                                                                                
           ADD 1 TO WS-F223-READ-CNT.                                           
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-PROCESS-F223.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF K223-NUM-SALES-MODE = SPACES OR LOW-VALUES                        
              GO TO 2000-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE K223-NUM-CASES TO WS-K223-NUM-CASE-ALPHA                        
                                                                                
           PERFORM 3000-CREATE-K225 VARYING WS-CTR FROM 1 BY 1                  
             UNTIL WS-CTR > WS-K223-NUM-CASE.                                   
                                                                                
           PERFORM 1500-READ-BP13F223 THRU 1500-EXIT.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2100-FIND-LAST-QUEUE.                                                    
      *-------------------------------------------------------------            
                                                                                
           EVALUATE K223-NUM-SALES-MODE                                         
             WHEN 'WIH'                                                         
                MOVE '2' TO WS-PREFIX                                           
             WHEN '3R '                                                         
                MOVE 'R' TO WS-PREFIX                                           
             WHEN '3R1'                                                         
                MOVE 'S' TO WS-PREFIX                                           
           END-EVALUATE.                                                        
                                                                                
           SET WS-K225-EOF-NO        TO TRUE.                                   
                                                                                
           MOVE K223-NUM-SALES-MODE  TO K225-ALT-KEY.                           
                                                                                
           START BP13K225 KEY IS GREATER OR EQUAL K225-ALT-KEY                  
              END-START.                                                        
                                                                                
           EVALUATE K225-FILE-STATUS                                            
                                                                                
           WHEN 00                                                              
              PERFORM 2110-READNXT-K225 THRU 2110-EXIT                          
                UNTIL WS-K225-EOF-YES                                           
              MOVE WS-SAVE-QUEUE-NUM TO WS-SAVE-SALES-MODE-CNT                  
                                                                                
           WHEN 10                                                              
              DISPLAY 'K225 END OF FILE'                                        
                                                                                
           WHEN 23                                                              
              DISPLAY 'K225 KEY NOT FOUND IN START OF K225: '                   
                          K225-ALT-KEY                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR STARTING BP13K225'                                 
              DISPLAY 'K225-FILE-STATUS: ' K225-FILE-STATUS                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2110-READNXT-K225.                                                       
      *-------------------------------------------------------------            
                                                                                
           READ BP13K225 NEXT RECORD                                            
             AT END                                                             
                SET WS-K225-EOF-YES TO TRUE                                     
                GO TO 2110-EXIT.                                                
                                                                                
           IF K225-NUM-SALES-MODE NOT EQUAL K223-NUM-SALES-MODE                 
              SET WS-K225-EOF-YES TO TRUE                                       
              GO TO 2110-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF K225-NUM-SALES-MODE = 'WIH'                                       
              IF WS-PREFIX EQUAL K225-NUM-QUEUE(1:1)                            
                MOVE K225-NUM-QUEUE    TO WS-SAVE-QUEUE                         
              END-IF                                                            
           ELSE                                                                 
              MOVE K225-NUM-QUEUE      TO WS-SAVE-QUEUE                         
              MOVE K225-NUM-QUEUE(1:1) TO WS-PREFIX                             
           END-IF.                                                              
                                                                                
       2110-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-CREATE-K225.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES TO BP13K225-REC.                                         
           INITIALIZE     BP13K225-REC.                                         
                                                                                
           ADD 1 TO WS-SAVE-SALES-MODE-CNT.                                     
                                                                                
           PERFORM 3100-CREATE-QUEUE-NUM THRU 3100-EXIT.                        
                                                                                
           STRING '##' K225-NUM-QUEUE                                           
              DELIMITED BY SIZE     INTO K225-NUM-NRIC.                         
                                                                                
           MOVE K223-NUM-SALES-MODE   TO K225-NUM-SALES-MODE.                   
           MOVE K223-DTE-APPOINTMENT  TO K225-DTE-APPOINTMENT                   
                                         WS-SAVE-DATE.                          
           MOVE K223-TME-APPOINTMENT  TO K225-TME-APPOINTMENT.                  
                                                                                
           MOVE 'BP13C21M'            TO K225-NUM-USERID.                       
           MOVE FUNCTION CURRENT-DATE TO K225-DTE-UPDATE.                       
           ACCEPT K225-TME-UPDATE   FROM TIME.                                  
                                                                                
           WRITE BP13K225-REC.                                                  
                                                                                
           IF K225-FILE-STATUS EQUAL 00 OR 02                                   
              ADD 1 TO WS-K225-WRITE-CNT                                        
           ELSE                                                                 
              DISPLAY 'ERROR WRITING K225 - ' K225-FILE-STATUS                  
                      ' APPMT NOT WRITTEN: ' K225-KEY-FLD K225-ALT-KEY          
              ADD 1 TO WS-K225-WRITE-ERR                                        
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3100-CREATE-QUEUE-NUM.                                                   
      *-------------------------------------------------------------            
           STRING WS-PREFIX WS-QUEUE-NUM-DIGITS                                 
                  DELIMITED BY SIZE INTO K225-NUM-QUEUE.                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '********* BP13C21M ************'.                           
           DISPLAY '                               '.                           
           DISPLAY '***** UPDATE APPOINTMENT ******'.                           
           DISPLAY 'RECS READ   FROM BP13F223   : ' WS-F223-READ-CNT.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN  IN BP13K225   : ' WS-K225-WRITE-CNT.          
           DISPLAY 'RECS ERROR WRITE BP13K225   : ' WS-K225-WRITE-ERR.          
           DISPLAY '                               '.                           
                                                                                
           CLOSE   BP13F223                                                     
                   BP13K225                                                     
                                                                                
           IF K225-FILE-STATUS NOT = 00                                         
              DISPLAY 'ERROR CLOSING BP13K225 : ' K225-FILE-STATUS              
              MOVE     K225-FILE-STATUS    TO RETURN-CODE.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
