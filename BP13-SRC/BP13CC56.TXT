       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC56.                                                 
      *AUTHOR.        SAUL SIO.                                                 
      *DATE-WRITTEN.  05/09/2006.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      * OBJECTIVE    :  1. PRINT UNSUCCESSFUL LETTER                *           
      *                    (EXCEEDED 2 TIME FLAT SUPPLY)            *           
      *                                                             *           
      * INPUT FILES  :  1.  BP13F740                                *           
      *                 2.  BP13K825 - OCCUPIER FILE                *           
      *                 3.  BP13K830 - ADDRESS FILE                 *           
      *                                                             *           
      * OUTPUT LISTS :  1.  BP13PC56 - INVITATION LETTER            *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  OIC  DATE     DESCRIPTION                          *           
      * -------- ---  -------- ------------------------------------ *           
      * BP132927 SSS  20060905 NEW PROGRAM                          *           
      * BP133157 SSS  20071217 TO CATER FOR D02.                    *           
      * =========================================================== *           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F740  ASSIGN       TO BP13F740.                           
                                                                                
           SELECT BP13K825  ASSIGN       TO BP13K825                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K825-KEY-FLD                        
                            FILE STATUS  IS WS-K825-STATUS.                     
                                                                                
           SELECT BP13K830  ASSIGN       TO BP13K830                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K830-KEY-FLD                        
                            ALTERNATE RECORD KEY IS  K830-NUM-REGN              
                            FILE STATUS  IS WS-K830-STATUS.                     
                                                                                
           SELECT BP13PC56  ASSIGN       TO BP13PC56.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F740                                                            
            RECORDING MODE  F                                                   
            LABEL RECORDS   ARE STANDARD                                        
            BLOCK CONTAINS  0   RECORDS                                         
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K825.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   BP13PC56                                                            
            RECORD CONTAINS 90 CHARACTERS                                       
            RECORDING MODE  IS  F.                                              
       01   PC56-PRTREC                 PIC X(90).                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-INDICATORS.                                                       
           05  WS-K825-STATUS           PIC 99     VALUE 00.                    
           05  WS-K830-STATUS           PIC 99     VALUE 00.                    
           05  WS-EOF-F740              PIC X      VALUE 'N'.                   
                                                                                
       01  WS-COUNTERS-POINTERS.                                                
           05  WS-F740-READ             PIC 9(6)   VALUE ZEROS.                 
           05  WS-LETTER-PRINTED        PIC 9(6)   VALUE ZEROS.                 
                                                                                
       01  WS-DATE                      PIC X(8)   VALUE SPACES.                
       01  WS-NAME                      PIC X(66)  VALUE SPACES.                
                                                                                
                                                                                
      *-------------------------------------------------------------            
      * FORMAT LETTER                                                           
      *-------------------------------------------------------------            
       01  PC56-LINE01.                                                         
           05  FILLER               PIC X(72) VALUE SPACES.                     
           05  PC56-OUR-REF         PIC X(8)  VALUE SPACES.                     
                                                                                
       01  PC56-LINE02.                                                         
           05  FILLER               PIC X(72) VALUE SPACES.                     
           05  PC56-DATE            PIC X(10) VALUE SPACES.                     
                                                                                
       01  PC56-LINE03.                                                         
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC56-NAME1           PIC X(66) VALUE SPACES.                     
                                                                                
       01  PC56-LINE03B.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC56-NAME2           PIC X(66) VALUE SPACES.                     
                                                                                
       01  PC56-LINE03C.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC56-NAME3           PIC X(66) VALUE SPACES.                     
                                                                                
       01  PC56-LINE03D.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC56-NAME4           PIC X(66) VALUE SPACES.                     
                                                                                
       01  PC56-LINE04.                                                         
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC56-BLK             PIC X(10) VALUE SPACES.                     
           05  FILLER               PIC X(01) VALUE SPACES.                     
           05  PC56-UNIT            PIC X(11) VALUE SPACES.                     
                                                                                
       01  PC56-LINE04B.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC56-ADDR-L2         PIC X(47) VALUE SPACES.                     
                                                                                
       01  PC56-LINE04C.                                                        
           05  FILLER               PIC X(12) VALUE SPACES.                     
           05  PC56-ADDR-L3         PIC X(20) VALUE SPACES.                     
                                                                                
       01  PC56-LINE05.                                                         
           05  FILLER               PIC X(10) VALUE SPACES.                     
           05  PC56-BAR-CODE        PIC X(09) VALUE SPACES.                     
                                                                                
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR AV02C015                                                   
      *-------------------------------------------------------------            
        01  WS-LINK-AV02C015.                                                   
            05  AV02C015-IN-POSTCODE          PIC X(6).                         
            05  AV02C015-OUT-POSTCODE-CHKSUM  PIC X(9).                         
                                                                                
       COPY AV02COMM.                                                           
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-FILES-ROUTINE   THRU  1000-EXIT.                  
            PERFORM 2000-READ-BP13F740        THRU  2000-EXIT.                  
            PERFORM 3000-PROCESS-RECORD       THRU  3000-EXIT                   
                    UNTIL WS-EOF-F740 =  'Y'.                                   
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES-ROUTINE.                                                 
      *-------------------------------------------------------------            
            OPEN INPUT  BP13F740                                                
                        BP13K825                                                
                        BP13K830                                                
                 OUTPUT BP13PC56.                                               
                                                                                
           IF WS-K825-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-K825-STATUS ' WS-K825-STATUS           
              MOVE     WS-K825-STATUS TO RETURN-CODE                            
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, WS-K830-STATUS ' WS-K830-STATUS           
              MOVE     WS-K830-STATUS   TO RETURN-CODE                          
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT.                       
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)    TO WS-DATE.                       
           STRING  WS-DATE(7:2) '/'                                             
                   WS-DATE(5:2) '/'                                             
                   WS-DATE(1:4)                                                 
                   DELIMITED BY SIZE INTO PC56-DATE                             
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F740.                                                      
      *-------------------------------------------------------------            
           READ BP13F740                                                        
                AT END MOVE 'Y' TO WS-EOF-F740                                  
                GO TO  2000-EXIT.                                               
                                                                                
           ADD  1  TO   WS-F740-READ.                                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
           MOVE SPACES TO  PC56-OUR-REF                                         
                           PC56-NAME1                                           
                           PC56-NAME2                                           
                           PC56-NAME3                                           
                           PC56-NAME4                                           
                           PC56-BLK                                             
                           PC56-UNIT                                            
                           PC56-ADDR-L2                                         
                           PC56-ADDR-L3                                         
                           PC56-BAR-CODE.                                       
                                                                                
           PERFORM 5000-GET-NAMES        THRU 5000-EXIT.                        
           PERFORM 6000-GET-ADDRESS      THRU 6000-EXIT.                        
           PERFORM 7000-PRINT-LETTER     THRU 7000-EXIT.                        
                                                                                
           PERFORM 2000-READ-BP13F740    THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       5000-GET-NAMES.                                                          
      *-------------------------------------------------------------            
            MOVE    F740-NUM-NRIC1          TO   K825-NUM-NRIC.                 
            PERFORM 5500-READ-BP13K825      THRU 5500-EXIT.                     
            MOVE    WS-NAME                 TO   PC56-NAME1.                    
                                                                                
            IF F740-NUM-NRIC2 NOT =  SPACES AND  LOW-VALUES                     
               MOVE     F740-NUM-NRIC2       TO  K825-NUM-NRIC                  
               PERFORM  5500-READ-BP13K825   THRU 5500-EXIT                     
               MOVE     WS-NAME              TO  PC56-NAME2                     
            ELSE                                                                
               MOVE     SPACES               TO  PC56-NAME2                     
            END-IF.                                                             
                                                                                
            IF F740-NUM-NRIC3 NOT =  SPACES AND  LOW-VALUES                     
               MOVE     F740-NUM-NRIC3       TO  K825-NUM-NRIC                  
               PERFORM  5500-READ-BP13K825   THRU 5500-EXIT                     
               MOVE     WS-NAME              TO  PC56-NAME3                     
            ELSE                                                                
               MOVE     SPACES               TO  PC56-NAME3                     
            END-IF.                                                             
                                                                                
            IF F740-NUM-NRIC4 NOT =  SPACES AND  LOW-VALUES                     
               MOVE     F740-NUM-NRIC4       TO  K825-NUM-NRIC                  
               PERFORM  5500-READ-BP13K825   THRU 5500-EXIT                     
               MOVE     WS-NAME              TO  PC56-NAME4                     
            ELSE                                                                
               MOVE     SPACES               TO  PC56-NAME4                     
            END-IF.                                                             
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       5500-READ-BP13K825.                                                      
      *-------------------------------------------------------------            
            MOVE SPACES        TO WS-NAME.                                      
            MOVE F740-NUM-REGN TO K825-NUM-REGN.                                
            READ BP13K825.                                                      
                                                                                
            IF WS-K825-STATUS = 00                                              
               MOVE  K825-NME-OCCP           TO    WS-NAME                      
            ELSE                                                                
            IF WS-K825-STATUS = 23                                              
               DISPLAY 'RECORD NOTFND IN BP13K825 = ' WS-K825-STATUS            
                       ', KEY-FLD = ' K825-KEY-FLD                              
            ELSE                                                                
               DISPLAY 'ERROR READING BP13K825, STAT ' WS-K825-STATUS           
                       ', KEY-FLD = ' K825-KEY-FLD.                             
                                                                                
       5500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       6000-GET-ADDRESS.                                                        
      *-------------------------------------------------------------            
           MOVE F740-NUM-REGN     TO    K830-NUM-REGN.                          
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K830-STATUS                                              
           WHEN 00                                                              
              IF (K830-NUM-MAIL-ADDR =  SPACES OR LOW-VALUES) OR                
                 ((K830-NUM-MAIL-LVL-CHAR =  '#') AND                           
                 (K830-NUM-MAIL-LVL-NUM =  SPACES OR LOW-VALUES))               
                 MOVE K830-NUM-BLK         TO PC56-BLK                          
                 MOVE K830-NUM-FLAT        TO PC56-UNIT                         
                 MOVE K830-NME-STREET-BLDG(1:47) TO PC56-ADDR-L2                
                 IF K830-NUM-POSTAL  = SPACES OR LOW-VALUES OR ZEROES           
                    MOVE SPACES            TO PC56-ADDR-L3                      
                 ELSE                                                           
                    STRING 'SINGAPORE '  K830-NUM-POSTAL                        
                       DELIMITED BY SIZE    INTO PC56-ADDR-L3                   
                                                                                
                    MOVE SPACES          TO WS-LINK-AV02C015                    
                    MOVE K830-NUM-POSTAL TO AV02C015-IN-POSTCODE                
                    PERFORM 6500-CALL-AV02C015     THRU 6500-EXIT               
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PC56-BAR-CODE          
                 END-IF                                                         
              ELSE                                                              
                 MOVE K830-NUM-MAIL-BLK        TO PC56-BLK                      
                 MOVE K830-NUM-MAIL-FLAT       TO PC56-UNIT                     
                 MOVE K830-NME-MAIL-STR-BLDG(1:47) TO PC56-ADDR-L2              
                 IF K830-NUM-MAIL-POSTAL = SPACES OR LOW-VALUES OR ZEROS        
                    MOVE SPACES                TO PC56-ADDR-L3                  
                 ELSE                                                           
                    STRING 'SINGAPORE '      K830-NUM-MAIL-POSTAL               
                           DELIMITED BY SIZE INTO PC56-ADDR-L3                  
                                                                                
                    MOVE SPACES          TO WS-LINK-AV02C015                    
                    MOVE K830-NUM-MAIL-POSTAL TO AV02C015-IN-POSTCODE           
                    PERFORM 6500-CALL-AV02C015     THRU 6500-EXIT               
                    MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PC56-BAR-CODE          
                 END-IF                                                         
              END-IF                                                            
           WHEN 23                                                              
              MOVE SPACES TO PC56-BLK                                           
                             PC56-UNIT                                          
                             PC56-ADDR-L2                                       
                             PC56-ADDR-L3                                       
              DISPLAY 'BP13K830 RECORD NOT FOUND = ' WS-K830-STATUS             
                      ', KEY ' K830-KEY-FLD                                     
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K830, STATUS ' WS-K830-STATUS          
                      ', KEY ' K830-KEY-FLD                                     
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       6500-CALL-AV02C015.                                                      
      *-------------------------------------------------------------            
           CALL 'AV02C015'          USING                                       
                                    AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM.               
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       7000-PRINT-LETTER.                                                       
      *-------------------------------------------------------------            
           MOVE  F740-NUM-REGN           TO PC56-OUR-REF.                       
                                                                                
           MOVE SPACES TO PC56-PRTREC.                                          
           WRITE PC56-PRTREC        AFTER PAGE.                                 
           WRITE PC56-PRTREC        FROM PC56-LINE01 AFTER 4.                   
           WRITE PC56-PRTREC        FROM PC56-LINE02.                           
           WRITE PC56-PRTREC        FROM PC56-LINE03.                           
           WRITE PC56-PRTREC        FROM PC56-LINE03B.                          
           WRITE PC56-PRTREC        FROM PC56-LINE03C.                          
           WRITE PC56-PRTREC        FROM PC56-LINE03D.                          
           WRITE PC56-PRTREC        FROM PC56-LINE04.                           
           WRITE PC56-PRTREC        FROM PC56-LINE04B.                          
           WRITE PC56-PRTREC        FROM PC56-LINE04C.                          
           WRITE PC56-PRTREC        FROM PC56-LINE05 AFTER 2.                   
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
            CLOSE BP13F740                                                      
                  BP13K825                                                      
                  BP13K830                                                      
                  BP13PC56.                                                     
                                                                                
            IF WS-K825-STATUS   NOT = 00 AND 97                                 
               DISPLAY 'CLOSING ERROR BP13K825, STAT ' WS-K825-STATUS.          
                                                                                
            IF WS-K830-STATUS   NOT = 00 AND 97                                 
               DISPLAY 'CLOSING ERROR BP13K830, STAT ' WS-K830-STATUS.          
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
