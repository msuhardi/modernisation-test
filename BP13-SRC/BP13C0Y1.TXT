       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C0Y1.                                                  
      *DATE-WRITTEN. 05 MAY 2012.                                               
      *AUTHOR.       ALWYN BENNY.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: 1) LIST THE APPLICATIONS WITH THE HLE IN LEASE *            
      *                NOT MATCH WITH HLE IN SOC AND LOAN TAG= B/N *            
      *     INPUT FILE :                                           *            
      *                  1. BP27F310 (LEAS)                        *            
      *                  2. BP27K800 (LEAS MASTER)                 *            
      *                  3. BP13K800 (SOC MASTER FILE)             *            
      *                  4. BP13K820 (OCCUPIER FILE)               *            
      *     OUTPUT FILE:                                           *            
      *                  1. BP13F0Y1 (HLE EMAIL ALERT FILE)        *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP134519 04052012 AB9   NEW PGM                            *            
      * BP134525 25052012 AB9   READ BP13K820 TO GET REG-NO        *            
      * BP134608 21082012 AB9   EXCLUDE 'NPL' AND 'SMR' CASES      *            
      * BP138047 25112019 KAC1  RECOMPILE FOR BP27 COPYBOOK UPDATE *            
      * ========================================================== *            
                                                                                
      *-------------------------------------------------------------            
       ENVIRONMENT DIVISION.                                                    
      *-------------------------------------------------------------            
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
      *-------------------------------------------------------------            
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP27F310 ASSIGN TO BP27F310.                                  
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN OF                   
                                             BP13K800-MASTER                    
                            FILE STATUS   IS K800-STATUS.                       
                                                                                
           SELECT BP27K800  ASSIGN        TO BP27K800                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-HLA OF                    
                                             BP27K800-MASTER                    
                            FILE STATUS   IS BP27K800-STATUS.                   
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K820-KEY-FLD                               
                  ALTERNATE KEY   IS K820-NUM-NRIC OF BP13K820-REC              
                  FILE STATUS     IS K820-STATUS.                               
                                                                                
           SELECT BP13K022  ASSIGN       TO BP13K022                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K022-KEY-FLD                        
                            ALTERNATE RECORD KEY IS K022-NUM-REGN               
                            WITH DUPLICATES                                     
                            FILE STATUS  IS K022-STATUS.                        
                                                                                
           SELECT BP13F0Y1 ASSIGN TO BP13F0Y1.                                  
                                                                                
      *-------------------------------------------------------------            
       DATA DIVISION.                                                           
      *-------------------------------------------------------------            
       FILE SECTION.                                                            
                                                                                
       FD  BP27F310                                                             
           RECORDING MODE  IS  F.                                               
       COPY BP27F310.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400  CHARACTERS.                                    
       COPY BP13K820.                                                           
                                                                                
       FD   BP27K800                                                            
            RECORD CONTAINS 1200 CHARACTERS.                                    
       COPY BP27K800.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD  BP13F0Y1                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  BP13F0Y1-REC.                                                        
           05 F0Y1-NUM-REGN          PIC X(08).                                 
           05 F0Y1-NRIC1             PIC X(09).                                 
           05 F0Y1-NRIC2             PIC X(09).                                 
           05 F0Y1-NRIC3             PIC X(09).                                 
           05 F0Y1-NRIC4             PIC X(09).                                 
           05 F0Y1-SCH-ACC           PIC X(11).                                 
           05 F0Y1-LOAN-TAG          PIC X(01).                                 
           05 F0Y1-NUM-HLE           PIC X(09).                                 
           05 F0Y1-NUM-HLE-SOC       PIC X(09).                                 
           05 F0Y1-CSM-NCK-NAME      PIC X(05).                                 
           05 FILLER                 PIC X(21).                                 
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-F310-READ             PIC 9(4)   VALUE ZERO.                      
       01  WS-K800-READ             PIC 9(4)   VALUE ZERO.                      
       01  WS-K820-READ             PIC 9(4)   VALUE ZERO.                      
       01  WS-BP27K800-READ         PIC 9(4)   VALUE ZERO.                      
       01  WS-F800-MATCH-CNT        PIC 9(4)   VALUE ZERO.                      
       01  WS-K820-NRIC-NOTFND      PIC 9(4)   VALUE ZERO.                      
       01  WS-BP27K800-NOT-FND      PIC 9(4)   VALUE ZERO.                      
       01  WS-F01Y1-WRITE           PIC 9(4)   VALUE ZERO.                      
       01  WS-F01Y2-WRITE           PIC 9(4)   VALUE ZERO.                      
       01  WS-K800-NOT-FND          PIC 9(4)   VALUE ZERO.                      
       01  WS-CSM-FOUND             PIC X(1)   VALUE SPACES.                    
       01  WS-F310-EOF              PIC X      VALUE 'N'.                       
       01  WS-K820-EOF              PIC X      VALUE 'N'.                       
       01  WS-MATCH-TAG             PIC X      VALUE 'N'.                       
       01  WS-SNO                   PIC 9(4)   VALUE ZEROS.                     
       01  WS-CDE-ERROR             PIC X(01)  VALUE SPACES.                    
       01  WS-PREB-CSM              PIC X(5)   VALUE SPACES.                    
       01  WS-POSB-CSM              PIC X(5)   VALUE SPACES.                    
       01  WS-CSM-NKNME             PIC X(5)   VALUE SPACES.                    
       01  WS-NRIC                  PIC X(9)   VALUE SPACES.                    
       01  K800-STATUS              PIC 99     VALUE ZEROS.                     
       01  K820-STATUS              PIC 99     VALUE ZEROS.                     
       01  K022-STATUS              PIC 99     VALUE ZEROS.                     
       01  BP27K800-STATUS          PIC 99     VALUE ZEROS.                     
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
                                                                                
       0000-MAIN-LOGIC.                                                         
                                                                                
           PERFORM 1000-INITIALIZATION THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP27F310  THRU 2000-EXIT.                          
           PERFORM 3000-MATCH-ROUTINE  THRU 3000-EXIT                           
                   UNTIL WS-F310-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-INITIALIZATION.                                                     
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP27F310                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP27K800                                                 
                       BP13K022                                                 
                OUTPUT BP13F0Y1.                                                
                                                                                
            IF K800-STATUS NOT = 00 AND 97                                      
               DISPLAY 'OPEN FAILED FOR BP13K800,STATUS ' K800-STATUS           
               MOVE K800-STATUS TO RETURN-CODE                                  
               PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
            END-IF.                                                             
                                                                                
            IF K820-STATUS NOT = 00 AND 97                                      
               DISPLAY 'OPEN FAILED FOR BP13K820,STATUS ' K820-STATUS           
               MOVE K820-STATUS TO RETURN-CODE                                  
               PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
            END-IF.                                                             
                                                                                
            IF BP27K800-STATUS NOT = 00 AND 97                                  
               DISPLAY 'OPEN FAILED FOR BP27K800,STAT ' BP27K800-STATUS         
               MOVE BP27K800-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
            END-IF.                                                             
                                                                                
            IF K022-STATUS NOT = 00 AND 97                                      
               DISPLAY 'OPEN FAILED FOR BP13K022,STATUS ' K022-STATUS           
               MOVE K022-STATUS TO RETURN-CODE                                  
               PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
            END-IF.                                                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP27F310.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP27F310 AT END                                                 
                MOVE 'Y' TO WS-F310-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F310-READ .                                              
           MOVE SPACES           TO      WS-CDE-ERROR.                          
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-MATCH-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           PERFORM 3100-READ-BP27K800    THRU 3100-EXIT.                        
           PERFORM 3200-PROCESS-K820     THRU 3200-EXIT                         
           PERFORM 2000-READ-BP27F310    THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-READ-BP27K800.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE F310-NUM-HLA TO K800-NUM-HLA OF BP27K800-MASTER.                
           READ BP27K800.                                                       
                                                                                
           IF BP27K800-STATUS = 00                                              
              ADD 1 TO WS-BP27K800-READ                                         
           ELSE                                                                 
           IF K800-STATUS = 23                                                  
              ADD 1                   TO   WS-BP27K800-NOT-FND                  
              DISPLAY 'RECORD NOT FOUND IN LEAS, HLE' F310-NUM-HLA              
              MOVE 'R'                 TO WS-CDE-ERROR                          
           ELSE                                                                 
              DISPLAY 'ERROR READING BP27K800, STATUS '                         
                                           BP27K800-STATUS                      
              MOVE K800-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                       
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3200-PROCESS-K820.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF (K800-NUM-NRIC1 OF BP27K800-MASTER NOT = SPACES AND               
                                               LOW-VALUES AND ZEROS) OR         
              (K800-NUM-NRIC2 OF BP27K800-MASTER NOT = SPACES AND               
                                               LOW-VALUES AND ZEROS) OR         
              (K800-NUM-NRIC3 OF BP27K800-MASTER NOT = SPACES AND               
                                               LOW-VALUES AND ZEROS) OR         
              (K800-NUM-NRIC4 OF BP27K800-MASTER NOT = SPACES AND               
                                               LOW-VALUES AND ZEROS)            
              PERFORM 3300-READ-K820        THRU   3300-EXIT                    
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3300-READ-K820.                                                          
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'        TO    WS-MATCH-TAG.                                  
           MOVE SPACES     TO    WS-NRIC.                                       
           IF K800-NUM-NRIC1 OF BP27K800-MASTER NOT = SPACES AND                
                                              LOW-VALUES                        
              MOVE K800-NUM-NRIC1 OF BP27K800-MASTER TO K820-NUM-NRIC           
                                            OF BP13K820-REC                     
                                               WS-NRIC                          
              PERFORM 3301-READ-K820     THRU  3301-EXIT                        
           END-IF.                                                              
           IF (K800-NUM-NRIC2 OF BP27K800-MASTER NOT = SPACES AND               
                                              LOW-VALUES) AND                   
                             WS-MATCH-TAG NOT = 'Y'                             
              MOVE K800-NUM-NRIC2 OF BP27K800-MASTER TO K820-NUM-NRIC           
                                            OF BP13K820-REC                     
                                               WS-NRIC                          
              PERFORM 3301-READ-K820     THRU  3301-EXIT                        
           END-IF.                                                              
           IF (K800-NUM-NRIC3 OF BP27K800-MASTER NOT = SPACES AND               
                                              LOW-VALUES )   AND                
                                     WS-MATCH-TAG NOT = 'Y'                     
              MOVE K800-NUM-NRIC3 OF BP27K800-MASTER TO K820-NUM-NRIC           
                                            OF BP13K820-REC                     
                                               WS-NRIC                          
              PERFORM 3301-READ-K820     THRU  3301-EXIT                        
           END-IF.                                                              
           IF (K800-NUM-NRIC4 OF BP27K800-MASTER NOT = SPACES AND               
                                              LOW-VALUES) AND                   
                                    WS-MATCH-TAG NOT = 'Y'                      
              MOVE K800-NUM-NRIC4 OF BP27K800-MASTER TO K820-NUM-NRIC           
                                            OF BP13K820-REC                     
                                               WS-NRIC                          
              PERFORM 3301-READ-K820     THRU  3301-EXIT                        
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3301-READ-K820.                                                          
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'       TO WS-K820-EOF.                                       
           START BP13K820 KEY >= K820-NUM-NRIC OF BP13K820-REC.                 
                                                                                
           EVALUATE K820-STATUS                                                 
           WHEN 00                                                              
           WHEN 02                                                              
                PERFORM 3302-READNEXT-K820  THRU 3302-EXIT UNTIL                
                      WS-K820-EOF = 'Y' OR WS-NRIC NOT = K820-NUM-NRIC          
                                                 OF BP13K820-REC                
           WHEN 23                                                              
                ADD 1 TO       WS-K820-NRIC-NOTFND                              
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K820, STATUS ' K820-STATUS             
              MOVE K820-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3301-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3302-READNEXT-K820.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K820 NEXT RECORD                                            
           EVALUATE K820-STATUS                                                 
           WHEN 00                                                              
           WHEN 02                                                              
           WHEN 22                                                              
                IF WS-NRIC  = K820-NUM-NRIC OF BP13K820-REC                     
                   MOVE SPACES TO  K800-NUM-REGN OF BP13K800-MASTER             
                   MOVE K820-NUM-REGN TO K800-NUM-REGN OF                       
                                                   BP13K800-MASTER              
                   PERFORM 3500-READ-BP13K800  THRU 3500-EXIT                   
                ELSE                                                            
                   MOVE 'Y'  TO   WS-K820-EOF                                   
                END-IF                                                          
           WHEN 10                                                              
           WHEN 23                                                              
                  MOVE 'Y'         TO WS-K820-EOF                               
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K820, STATUS ' K820-STATUS             
              MOVE K820-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3302-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3500-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF K800-STATUS = 00                                                  
              ADD 1 TO WS-K800-READ                                             
              PERFORM  3550-CHECK-COND     THRU 3550-EXIT                       
           ELSE                                                                 
           IF K800-STATUS = 23                                                  
              ADD 1                   TO   WS-K800-NOT-FND                      
              DISPLAY 'RECORD NOT FOUND IN SOC, HLE' F310-NUM-HLA               
              DISPLAY '                     REGNO: ' F310-NUM-REGN              
              MOVE 'R'                 TO WS-CDE-ERROR                          
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K800, STATUS ' K800-STATUS             
              MOVE K800-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                       
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3550-CHECK-COND.                                                         
      *-------------------------------------------------------------            
                                                                                
           IF (F310-NUM-HLA NOT = K800-NUM-HLA OF BP13K800-MASTER) AND          
              (K800-NUM-STATUS OF BP13K800-MASTER NOT = 'C')       AND          
              (K800-NUM-LOAN-TAG OF BP13K800-MASTER = 'B' OR 'N')  AND          
              (K800-NUM-ALLO-CAT NOT = 'NPL' AND 'SMR') AND                     
              (F310-NUM-TRANS = 'AC')           AND                             
              (K820-NUM-NRIC OF BP13K820-REC = K800-NUM-NRIC1 OF                
                                               BP13K800-MASTER OR               
               K820-NUM-NRIC OF BP13K820-REC = K800-NUM-NRIC2   OF              
                                               BP13K800-MASTER  OR              
               K820-NUM-NRIC OF BP13K820-REC = K800-NUM-NRIC3   OF              
                                               BP13K800-MASTER  OR              
               K820-NUM-NRIC OF BP13K820-REC = K800-NUM-NRIC4   OF              
                                               BP13K800-MASTER ) AND            
              (K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES AND ZEROS)          
                                                                                
              PERFORM  3900-READ-BP13K022  THRU 3900-EXIT                       
              IF WS-CSM-FOUND = 'Y'                                             
                  PERFORM 4000-WRITE-OUTFILE    THRU 4000-EXIT                  
                  MOVE 'Y'                      TO WS-K820-EOF                  
                                                   WS-MATCH-TAG                 
              END-IF                                                            
           ELSE                                                                 
               DISPLAY 'NOT SATISFY THE CONDN,REGN' K800-NUM-REGN OF            
                                               BP13K800-MASTER                  
               MOVE 'S'                 TO WS-CDE-ERROR                         
           END-IF.                                                              
                                                                                
       3550-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3900-READ-BP13K022.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE K800-NUM-REGN OF BP13K800-MASTER TO K022-NUM-REGN.              
           MOVE SPACES        TO WS-PREB-CSM WS-POSB-CSM                        
                                             WS-CSM-FOUND.                      
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           IF K022-STATUS = 00                                                  
              MOVE 'Y'                     TO   WS-CSM-FOUND                    
              PERFORM  3950-GET-CSM        THRU 3950-EXIT                       
           ELSE                                                                 
           IF K022-STATUS = 23                                                  
              DISPLAY '> CSM FOR ' K022-NUM-REGN ' NOT FOUND.'                  
              MOVE 'C'                 TO WS-CDE-ERROR                          
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K022, STATUS ' K022-STATUS             
              MOVE K022-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                       
                                                                                
       3900-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3950-GET-CSM.                                                            
      *-------------------------------------------------------------            
                                                                                
            MOVE SPACES                 TO   WS-CSM-NKNME.                      
            IF K022-NUM-OIC NOT = SPACES AND LOW-VALUES                         
               IF K022-NUM-OIC-APPL = SPACES OR LOW-VALUES                      
                  MOVE K022-NUM-OIC        TO WS-PREB-CSM                       
               ELSE                                                             
                  MOVE K022-NUM-OIC-APPL   TO WS-PREB-CSM                       
               END-IF                                                           
            END-IF.                                                             
           IF K022-NUM-OIC-APPL = SPACES OR LOW-VALUES                          
              MOVE SPACES                          TO WS-POSB-CSM               
           ELSE                                                                 
              MOVE K022-NUM-OIC                    TO WS-POSB-CSM               
           END-IF.                                                              
           IF K800-NUM-PROV-TAG = 'Y'                                           
              MOVE SPACES         TO    WS-POSB-CSM                             
              MOVE WS-PREB-CSM    TO    WS-CSM-NKNME                            
           ELSE                                                                 
              IF WS-POSB-CSM NOT = SPACES                                       
                 MOVE WS-POSB-CSM  TO   WS-CSM-NKNME                            
              ELSE                                                              
                 MOVE WS-PREB-CSM  TO   WS-CSM-NKNME                            
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3950-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4000-WRITE-OUTFILE.                                                      
      *-------------------------------------------------------------            
                                                                                
            MOVE SPACES              TO   BP13F0Y1-REC.                         
            INITIALIZE                    BP13F0Y1-REC.                         
            MOVE K800-NUM-REGN  OF   BP13K800-MASTER                            
                                     TO   F0Y1-NUM-REGN.                        
            MOVE K800-NUM-NRIC1  OF BP13K800-MASTER                             
                                     TO   F0Y1-NRIC1.                           
            MOVE K800-NUM-NRIC2  OF BP13K800-MASTER                             
                                     TO   F0Y1-NRIC2.                           
            MOVE K800-NUM-NRIC3  OF BP13K800-MASTER                             
                                     TO   F0Y1-NRIC3.                           
            MOVE K800-NUM-NRIC4  OF BP13K800-MASTER                             
                                     TO   F0Y1-NRIC4.                           
            MOVE K800-NUM-SCH-ACC    TO   F0Y1-SCH-ACC.                         
            MOVE K800-NUM-LOAN-TAG OF BP13K800-MASTER                           
                                     TO   F0Y1-LOAN-TAG.                        
            MOVE F310-NUM-HLA        TO   F0Y1-NUM-HLE.                         
            MOVE K800-NUM-HLA       OF BP13K800-MASTER                          
                                     TO   F0Y1-NUM-HLE-SOC.                     
            MOVE WS-CSM-NKNME        TO   F0Y1-CSM-NCK-NAME.                    
                                                                                
            WRITE BP13F0Y1-REC.                                                 
            ADD  1                       TO  WS-F01Y1-WRITE.                    
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
                                                                                
           DISPLAY '- BP13C0Y1 CONTROL TOTAL --------'.                         
           DISPLAY ' NO OF REC READ FROM BP27F310  = '                          
                     WS-F310-READ.                                              
           DISPLAY ' NO OF REC READ FROM BP27K800  = '                          
                     WS-BP27K800-READ.                                          
           DISPLAY ' NO OF REC READ FROM BP13K800  = '                          
                     WS-K800-READ.                                              
           DISPLAY ' NO OF REC NOT FOUND BP27K800  = '                          
                     WS-BP27K800-NOT-FND.                                       
           DISPLAY ' NO OF REC NOT FOUND BP13K800  = '                          
                     WS-K800-NOT-FND.                                           
           DISPLAY ' NO OF REC WRITTEN TO BP13F0Y1 = '                          
                     WS-F01Y1-WRITE.                                            
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BP27F310                                                 
                       BP13K800                                                 
                       BP13K022                                                 
                       BP13F0Y1.                                                
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING ERROR, K800-STATUS ' K800-STATUS                 
              MOVE     K800-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           IF K022-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING ERROR, K022-STATUS ' K022-STATUS                 
              MOVE     K022-STATUS TO RETURN-CODE                               
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
