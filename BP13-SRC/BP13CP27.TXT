      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13CP27.                                                 
      *AUTHOR.        RJB1.                                                     
      *DATE-WRITTEN.  18/08/2016.                                               
                                                                                
      ***************************************************************           
      *            SYSTEM OF COMMITMENT (BP13)                      *           
      *=============================================================*           
      *  OBJECTIVE  : TO RETRIEVE DATA FROM CP27_STAFF TABLE        *           
      *                                                             *           
      *  INPUT      : STAFF NUMBER                                  *           
      *  OUTPUT     : STAFF INFO                                    *           
      *  COMMAREA   : 1500 BYTES                                    *           
      ***************************************************************           
      * MODIFICATIONS :                                           *             
      *                                                           *             
      * CHGE REQ #  DATE        AUTHOR  REMARKS                   *             
      * ----------  ----------  ------  ------------------------- *             
      * BP136413    18/08/2016  RJB1    NEW PROGRAM.              *             
      *************************************************************             
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-SQL-CODES.                                                        
           05  WS-SQL-FOUND              PIC S9(4) COMP VALUE +000.             
           05  WS-SQL-NOT-FOUND          PIC S9(4) COMP VALUE +100.             
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-STAFF-NO               PIC X(6)   VALUE SPACES.               
                                                                                
           EXEC SQL INCLUDE P13VSTAF END-EXEC.                                  
           EXEC SQL INCLUDE SQLCA    END-EXEC.                                  
                                                                                
                                                                                
      *-----------------------*                                                 
       LINKAGE SECTION.                                                         
      *-----------------------*                                                 
       01  LINK-REC.                                                            
           05  LINK-STAFF-NUM               PIC X(6).                           
                                                                                
       COPY P13COM27.                                                           
                                                                                
      *------------------*                                                      
       PROCEDURE DIVISION USING LINK-REC, BP13COM27-REC.                        
      *------------------*                                                      
                                                                                
           EXEC SQL WHENEVER NOT FOUND                                          
                             CONTINUE END-EXEC.                                 
           EXEC SQL WHENEVER SQLERROR                                           
                             GO TO 9000-ABORT-PROCESS END-EXEC.                 
           EXEC SQL WHENEVER SQLWARNING                                         
                             GO TO 9000-ABORT-PROCESS END-EXEC.                 
                                                                                
                                                                                
      *-----------------------*                                                 
       0000-START-ROUTINE.                                                      
      *-----------------------*                                                 
           MOVE SPACES TO  BP13COM27-REC.                                       
           INITIALIZE      BP13COM27-REC.                                       
                                                                                
           MOVE LINK-STAFF-NUM   TO COM27-NUM-STAFF                             
                                    WS-STAFF-NO.                                
                                                                                
           MOVE SPACE            TO COM27-CDE-ERROR.                            
           MOVE ZEROES           TO COM27-CDE-SYSERR.                           
                                                                                
           PERFORM 1000-GET-INFO                                                
              THRU 1000-EXIT.                                                   
                                                                                
           EXEC SQL COMMIT END-EXEC.                                            
                                                                                
           GOBACK.                                                              
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------*                    
      *    RETRIEVES INFORMATION USING STAFF NO                                 
      *    FROM THE CP27_STAFF TABLE                                            
      *----------------------------------------------------*                    
      *-----------------------*                                                 
       1000-GET-INFO.                                                           
      *-----------------------*                                                 
                                                                                
           EXEC SQL                                                             
                SELECT NUM_STAFF,                                               
                       NME_PERSON,                                              
                       NUM_DSGNTN,                                              
                       NUM_APNTMT,                                              
                       NME_DEPT,                                                
                       NME_SECT,                                                
                       NME_UNIT,                                                
                       NME_ALIAS,                                               
                       NUM_DEPT,                                                
                       NUM_ORG_UNIT,                                            
                       NUM_STATUS_EMPLY,                                        
                       NUM_PERSON,                                              
                       NUM_TITLE,                                               
                       NUM_ORG_LEVEL3,                                          
                       NUM_ORG_LEVEL4,                                          
                       NUM_ORG_LEVEL5,                                          
                       DTE_LEFT_SRVC                                            
                  INTO :DCLCP27-STAFF.NUM-STAFF,                                
                       :DCLCP27-STAFF.NME-PERSON,                               
                       :DCLCP27-STAFF.NUM-DSGNTN,                               
                       :DCLCP27-STAFF.NUM-APNTMT,                               
                       :DCLCP27-STAFF.NME-DEPT,                                 
                       :DCLCP27-STAFF.NME-SECT,                                 
                       :DCLCP27-STAFF.NME-UNIT,                                 
                       :DCLCP27-STAFF.NME-ALIAS,                                
                       :DCLCP27-STAFF.NUM-DEPT,                                 
                       :DCLCP27-STAFF.NUM-ORG-UNIT,                             
                       :DCLCP27-STAFF.NUM-STATUS-EMPLY,                         
                       :DCLCP27-STAFF.NUM-PERSON,                               
                       :DCLCP27-STAFF.NUM-TITLE,                                
                       :DCLCP27-STAFF.NUM-ORG-LEVEL3,                           
                       :DCLCP27-STAFF.NUM-ORG-LEVEL4,                           
                       :DCLCP27-STAFF.NUM-ORG-LEVEL5,                           
                       :DCLCP27-STAFF.DTE-LEFT-SRVC                             
                  FROM CP27_STAFF                                               
                WHERE NUM_STAFF = :WS-STAFF-NO                                  
           END-EXEC.                                                            
                                                                                
           IF SQLCODE NOT = WS-SQL-FOUND                                        
              DISPLAY '---------------------------------------------'           
              DISPLAY 'REC NOTFD IN CP27_STAFF  = ' WS-STAFF-NO                 
              DISPLAY 'SQLCODE                  = ' SQLCODE                     
              DISPLAY '---------------------------------------------'           
              MOVE SQLCODE  TO  COM27-CDE-SYSERR                                
           ELSE                                                                 
              MOVE NME-PERSON             TO COM27-NME-PERSON                   
              MOVE NUM-DSGNTN             TO COM27-NUM-DSGNTN                   
              MOVE NUM-APNTMT             TO COM27-NUM-APNTMT                   
              MOVE NME-DEPT               TO COM27-NME-DEPT                     
              MOVE NME-SECT               TO COM27-NME-SECT                     
              MOVE NME-UNIT               TO COM27-NME-UNIT                     
              MOVE NME-ALIAS              TO COM27-NME-ALIAS                    
              MOVE NUM-DEPT               TO COM27-NUM-DEPT                     
              MOVE NUM-ORG-UNIT           TO COM27-NUM-ORG-UNIT                 
              MOVE NUM-STATUS-EMPLY       TO COM27-NUM-STATUS-EMPLY             
              MOVE NUM-PERSON             TO COM27-NUM-PERSON                   
              MOVE NUM-TITLE              TO COM27-NUM-TITLE                    
              MOVE NUM-ORG-LEVEL3         TO COM27-NUM-ORG-LEVEL3               
              MOVE NUM-ORG-LEVEL4         TO COM27-NUM-ORG-LEVEL4               
              MOVE NUM-ORG-LEVEL5         TO COM27-NUM-ORG-LEVEL5               
              MOVE DTE-LEFT-SRVC          TO COM27-DTE-LEFT-SRVC                
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       9000-ABORT-PROCESS.                                                      
      *-----------------------*                                                 
           CALL 'DBATIAR' USING SQLCA.                                          
           MOVE SQLCODE       TO COM27-CDE-SYSERR.                              
                                                                                
           GOBACK.                                                              
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
