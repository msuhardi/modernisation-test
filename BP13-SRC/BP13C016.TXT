       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C016.                                                  
      *AUTHOR.       ANC1.                                                      
      *DATE-WRITTEN. 05/07/2002.                                                
      ************************************************************              
      *                                                          *              
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                      *              
      *                                                          *              
      *  SYSTEM ID   : BP13                                      *              
      *                                                          *              
      *  OBJECTIVE   : TO MATCH BP13F800 SOC MASTER FILE WITH    *              
      *                BM06F110 FIS MASTER FILE USING SCH ACCT   *              
      *                                                          *              
      *  INPUT FILES :                                           *              
      *  1. BP13F800                                             *              
      *  2. BM06F110                                             *              
      *                                                          *              
      *  OUTPUT FILES:                                           *              
      *  1. P13F800A                                             *              
      *                                                          *              
      * CHG-NO    BY    ON   - DESCRIPTION                       *              
      * ----------------------------------                       *              
      * BP132172 ANC1 070802 - NEW PGM                           *              
      * BP132226 ANC1 101002 - TO INCLUDE 1,2,3RM CASES          *              
      * BP132235 ANC1 150203 - BRING OVER F110-SELECTION-TYPE IF *              
      *                        FND IN F110 TO F800-ALLOC-CAT     *              
      ************************************************************              
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800  ASSIGN       TO BP13F800.                           
                                                                                
           SELECT BM06F110  ASSIGN       TO BM06F110.                           
                                                                                
           SELECT P13F800A  ASSIGN       TO P13F800A.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F800                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F800.                                                           
                                                                                
       FD  BM06F110                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500  CHARACTERS                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BM06F110.                                                           
                                                                                
       FD  P13F800A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01  P13F800A-REC                    PIC X(2000).                         
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       COPY SERSCH.                                                             
       01  WS-COUNTERS.                                                         
           05  WS-BP13F800-READ            PIC 9(08) VALUE ZEROES.              
           05  WS-BM06F110-READ            PIC 9(08) VALUE ZEROES.              
           05  WS-P13F800A-WRITE           PIC 9(08) VALUE ZEROES.              
           05  WS-MATCH                    PIC 9(08) VALUE ZEROES.              
           05  WS-UNMATCH                  PIC 9(08) VALUE ZEROES.              
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-BP13F800-EOF             PIC X(01) VALUE 'N'.                 
           05  WS-BM06F110-EOF             PIC X(01) VALUE 'N'.                 
           05  WS-ALLOC-SCH-FOUND-SW       PIC X(01) VALUE SPACES.              
                                                                                
       01  WS-SYSTEM-DATE-TIME.                                                 
           05  WS-SYS-DATE                 PIC X(08) VALUE SPACES.              
           05  WS-SYS-TIME                 PIC X(08) VALUE SPACES.              
           05  FILLER                      PIC X(05) VALUE SPACES.              
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CUR-DD                   PIC X(02) VALUE SPACES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-MM                   PIC X(02) VALUE SPACES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-CCYY                 PIC X(04) VALUE SPACES.              
                                                                                
       PROCEDURE DIVISION.                                                      
      *--------------------------------*                                        
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F800   THRU 2000-EXIT.                         
           PERFORM 3000-READ-BM06F110   THRU 3000-EXIT.                         
           PERFORM 4000-PROCESS-RECORD  THRU 4000-EXIT                          
             UNTIL F800-NUM-SCH-ACC = HIGH-VALUES AND                           
                   F110-SCH-ACC-NO = HIGH-VALUES.                               
           PERFORM 9999-CLOSE-FILES     THRU 9999-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       1000-OPEN-FILES.                                                         
      *--------------------------------*                                        
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BM06F110                                                 
               OUTPUT  P13F800A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE-TIME.                   
           MOVE WS-SYS-DATE(1:4)      TO WS-CUR-CCYY.                           
           MOVE WS-SYS-DATE(5:2)      TO WS-CUR-MM.                             
           MOVE WS-SYS-DATE(7:2)      TO WS-CUR-DD.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2000-READ-BP13F800.                                                      
      *--------------------------------*                                        
                                                                                
           READ BP13F800                                                        
             AT END MOVE HIGH-VALUES TO F800-NUM-SCH-ACC                        
             GO TO 2000-EXIT.                                                   
                                                                                
           ADD 1                     TO WS-BP13F800-READ.                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3000-READ-BM06F110.                                                      
      *--------------------------------*                                        
                                                                                
           READ BM06F110                                                        
             AT END MOVE HIGH-VALUES TO F110-SCH-ACC-NO                         
             GO TO 3000-EXIT.                                                   
                                                                                
           ADD 1                     TO WS-BM06F110-READ.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       4000-PROCESS-RECORD.                                                     
      *--------------------------------*                                        
                                                                                
           IF F800-NUM-SCH-ACC = F110-SCH-ACC-NO                                
              PERFORM 6000-BYPASS-SERS        THRU 6000-EXIT                    
              PERFORM 2000-READ-BP13F800      THRU 2000-EXIT                    
              PERFORM 3000-READ-BM06F110      THRU 3000-EXIT                    
           ELSE                                                                 
              IF F800-NUM-SCH-ACC > F110-SCH-ACC-NO                             
                 ADD 1                      TO WS-UNMATCH                       
                 PERFORM 3000-READ-BM06F110 THRU 3000-EXIT                      
              ELSE                                                              
                 ADD 1                      TO WS-UNMATCH                       
                 PERFORM 2000-READ-BP13F800 THRU 2000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       5000-WRITE-RECORD.                                                       
      *--------------------------------*                                        
                                                                                
           MOVE SPACES             TO P13F800A-REC.                             
           MOVE BP13F800-MASTER    TO P13F800A-REC.                             
                                                                                
           WRITE P13F800A-REC.                                                  
                                                                                
           ADD 1                  TO WS-P13F800A-WRITE.                         
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       6000-BYPASS-SERS.                                                        
      *--------------------------------*                                        
           IF F800-NUM-SCH-ACC = SPACES OR LOW-VALUES                           
              GO TO 6000-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE SPACES        TO WS-ALLOC-SCH-FOUND-SW.                         
           SET WS-SERS-PTR    TO 1.                                             
           SEARCH WS-SERS-SCH                                                   
              AT END                                                            
                 MOVE SPACES         TO WS-ALLOC-SCH-FOUND-SW                   
              WHEN WS-SERS-SCHEME (WS-SERS-PTR) = F800-NUM-ALLO-SCHEME          
                 MOVE 'Y'            TO WS-ALLOC-SCH-FOUND-SW                   
           END-SEARCH.                                                          
                                                                                
           IF WS-ALLOC-SCH-FOUND-SW = 'Y'                                       
              GO TO 6000-EXIT                                                   
           ELSE                                                                 
              IF (F800-NUM-ROOM NOT = ' ') AND                                  
                 (F800-NUM-EXEC-COND-TAG NOT = 'C')                             
                 ADD 1                           TO WS-MATCH                    
                 MOVE F110-DTE-ACCEPTANCE        TO F800-DTE-BK-APPT            
                 IF F110-NUM-SELECTION NOT = SPACES AND LOW-VALUES              
                    MOVE F110-NUM-SELECTION      TO F800-NUM-ALLO-CAT           
                 END-IF                                                         
                 PERFORM 5000-WRITE-RECORD       THRU 5000-EXIT                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       9999-CLOSE-FILES.                                                        
      *--------------------------------*                                        
                                                                                
           CLOSE      BP13F800                                                  
                      BM06F110                                                  
                      P13F800A.                                                 
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '**************************'.                                
           DISPLAY 'PROGRAM ID     :  BP13C016'.                                
           DISPLAY 'CURRENT DATE   : ' WS-CURRENT-DATE.                         
           DISPLAY '**************************'.                                
           DISPLAY 'NO OF REC READ FROM BP13F800 : ' WS-BP13F800-READ.          
           DISPLAY 'NO OF REC READ FROM BM06F110 : ' WS-BM06F110-READ.          
           DISPLAY 'NO OF MATCHED RECORDS        : ' WS-MATCH.                  
           DISPLAY 'NO OF UNMATCHED RECORDS      : ' WS-UNMATCH.                
           DISPLAY 'RECORDS WRITTEN IN P13F800A  : ' WS-P13F800A-WRITE.         
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
