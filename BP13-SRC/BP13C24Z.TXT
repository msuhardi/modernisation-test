       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C24Z.                                                 
      *AUTHOR.        KARL MAGALONA CABUG.                                      
      *DATE-WRITTEN.  23/09/14                                                  
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE   :  PROGRAM TO UPDATE BB14K580 FOR DPD          *            
      *                                                            *            
      *  INPUT FILES :  1)  BM06F110                               *            
      *                 2)  BM06K100                               *            
      *                 3)  BB14K500                               *            
      *                                                            *            
      *  I-O    FILE :  1)  BB14K580                               *            
      * ---------------------------------------------------------- *            
      * CHGE NO   OIC   DATE    DESCRIPTION                        *            
      * -------- ----- ------   ---------------------------------- *            
      * BP135377 KAM4  160914   NEW PROGRAM.                       *            
      * BP138405 MRR5  140920   ADD 18 MONTHS TO COMPLETION DTE    *            
      *                         TO GET WS-DEL-POSSN-DATE           *            
      * BP138577 MRR5  070121   MOVE LAST DAY OF THE MONTH TO DPD  *            
      * BP139110 EL27  290322   CATER FOR 15 MONTHS DPD FOR 6 PROJ *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F110  ASSIGN        TO  BM06F110.                         
                                                                                
           SELECT BM06K100  ASSIGN        TO  BM06K100                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  RANDOM                            
                            RECORD KEY    IS  K100-KEY-FLD                      
                            FILE STATUS   IS  WS-K100-STATUS.                   
                                                                                
           SELECT BB14K500  ASSIGN        TO  BB14K500                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  RANDOM                            
                            RECORD KEY    IS  K500-KEY-FLD                      
                            FILE STATUS   IS  WS-K500-STATUS.                   
                                                                                
           SELECT BB14K580  ASSIGN        TO  BB14K580                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  RANDOM                            
                            RECORD KEY    IS  K580-KEY-FLD                      
                            FILE STATUS   IS  WS-K580-STATUS.                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BM06F110                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06F110.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BB14K500                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BB14K500.                                                           
                                                                                
       FD   BB14K580                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BB14K580.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-EOF-F110                   PIC X     VALUE 'N'.               
           05  WS-K580-REWRITE               PIC X     VALUE 'N'.               
           05  WS-K100-STATUS                PIC 99    VALUE  00.               
           05  WS-K500-STATUS                PIC 99    VALUE  00.               
           05  WS-K580-STATUS                PIC 99    VALUE  00.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-TOTAL-CNT             PIC 9(5)  VALUE ZEROS.             
           05  WS-READ-NOTFND                PIC 9(5)  VALUE ZEROS.             
           05  WS-K580-NOTFND-CNT            PIC 9(5)  VALUE ZEROS.             
           05  WS-UPD-K580-CNT               PIC 9(5)  VALUE ZEROS.             
           05  WS-START-READ                 PIC 9(5)  VALUE ZEROS.             
                                                                                
       01 OTHER-VARIABLES.                                                      
           05 WS-K100-KEY-FLD                PIC X(16) VALUE SPACES.            
           05 WS-DTE-BLDG-COMPLETE           PIC X(08) VALUE SPACES.            
           05 WS-F205-COMP-KEY.                                                 
              10 WS-ESTATE                   PIC X(03) VALUE SPACES.            
              10 WS-NEIGHBOURHOOD            PIC X(04) VALUE SPACES.            
              10 WS-CONTRACT                 PIC X(04) VALUE SPACES.            
              10 WS-BLK-NO                   PIC X(05) VALUE SPACES.            
           05 WS-K110-AIX1                   PIC X(16) VALUE SPACES.            
           05 WS-K580-DTE-UNIT-DELIVERY      PIC X(08) VALUE SPACES.            
           05 WS-DEL-POSSN-DATE.                                                
              10  WS-DEL-POSSN-YYYY          PIC X(4).                          
              10  WS-DEL-POSSN-MM            PIC X(2).                          
              10  WS-DEL-POSSN-DD            PIC X(2).                          
           05  WS-8-DATE-CHAR                PIC X(8).                          
           05  WS-8-DATE  REDEFINES WS-8-DATE-CHAR.                             
               10  WS-8-DATE-YYYY            PIC 9(4).                          
               10  WS-8-DATE-MM              PIC 9(2).                          
               10  WS-8-DATE-DD              PIC 9(2).                          
           05  WS-DAY                        PIC X(2).                          
           05  WS-MONTH                      PIC X(2).                          
               88  MTH-END-WITH-30    VALUE '04' '06' '09' '11'.                
               88  MTH-FEB            VALUE '02'.                               
           05  WS-YEAR                       PIC 999.                           
           05  WS-LEAP-YEAR                  PIC 99.                            
                                                                                
       PROCEDURE DIVISION.                                                      
      **************************************************************            
       000-MAIN-ROUTINE.                                                        
      **************************************************************            
           PERFORM 100-OPEN-FILES            THRU 100-EXIT.                     
           PERFORM 200-READ-BM06F110         THRU 200-EXIT.                     
           PERFORM 300-PROCESS-RECORD        THRU 300-EXIT                      
                   UNTIL WS-EOF-F110 = 'Y'.                                     
                                                                                
           PERFORM  9999-CLOSE-ROUTINE       THRU 9999-EXIT.                    
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *------------------------------------------------------------*            
           OPEN INPUT  BM06F110                                                 
                       BM06K100                                                 
                       BB14K500                                                 
                  I-O  BB14K580.                                                
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K100 - OPEN ERROR :' WS-K100-STATUS                  
              MOVE     WS-K100-STATUS           TO RETURN-CODE                  
              PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
           END-IF.                                                              
                                                                                
           IF WS-K500-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BB14K500 - OPEN ERROR :' WS-K500-STATUS                  
              MOVE     WS-K500-STATUS           TO RETURN-CODE                  
              PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
           END-IF.                                                              
                                                                                
           IF WS-K580-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BB14K580 - OPEN ERROR :' WS-K580-STATUS                  
              MOVE     WS-K580-STATUS           TO RETURN-CODE                  
              PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-BM06F110.                                                       
      *-------------------------------------------------------------            
           READ BM06F110                                                        
                AT END MOVE 'Y'                 TO WS-EOF-F110                  
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1                                TO WS-READ-TOTAL-CNT.           
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-RECORD.                                                      
      *------------------------------------------------------------*            
           MOVE SPACES                         TO WS-DTE-BLDG-COMPLETE.         
                                                                                
           IF F110-AIX1 NOT = WS-K100-KEY-FLD                                   
              PERFORM 301-READ-K100-K500     THRU 301-EXIT                      
              IF WS-DTE-BLDG-COMPLETE NOT = SPACES                              
                 PERFORM 350-COMPUTE-DPD     THRU 350-EXIT                      
                 PERFORM 400-READ-BB14K580-UPDT   THRU 400-EXIT                 
              END-IF                                                            
           ELSE                                                                 
              PERFORM 400-READ-BB14K580-UPDT THRU 400-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-BM06F110         THRU 200-EXIT.                     
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       301-READ-K100-K500.                                                      
      *------------------------------------------------------------*            
           MOVE F110-AIX1                      TO K100-KEY-FLD.                 
                                                                                
           READ BM06K100.                                                       
           EVALUATE WS-K100-STATUS                                              
               WHEN 00                                                          
                    MOVE K100-KEY-FLD            TO WS-K100-KEY-FLD             
                    PERFORM 305-READ-BB14K500     THRU 305-EXIT                 
               WHEN 23                                                          
                    DISPLAY 'BM06K100 - KEY NOTFND : ' K100-KEY-FLD             
               WHEN OTHER                                                       
                    DISPLAY 'BM06K100 - ERROR ERROR : ' WS-K100-STATUS          
                    PERFORM 9999-CLOSE-ROUTINE    THRU 9999-EXIT                
           END-EVALUATE.                                                        
       301-EXIT.                                                                
           EXIT.                                                                
                                                                                
       305-READ-BB14K500.                                                       
      *------------------------------------------------------------*            
           MOVE SPACES                         TO K500-KEY-FLD.                 
           MOVE K100-CDE-BLK                   TO K500-CDE-BLK.                 
                                                                                
           READ BB14K500.                                                       
           EVALUATE WS-K500-STATUS                                              
               WHEN 00                                                          
                  DISPLAY 'K500-DTE-BLDG-CMPLT=>' K500-DTE-BLDG-COMPLETE        
                  MOVE K500-DTE-BLDG-COMPLETE  TO WS-DTE-BLDG-COMPLETE          
               WHEN 23                                                          
                  MOVE SPACES                  TO WS-DTE-BLDG-COMPLETE          
                  DISPLAY 'CDE-BLK NOTFND IN K500 (' K500-CDE-BLK ')'           
               WHEN OTHER                                                       
                    DISPLAY 'BB14K500 - READ ERROR   : ' WS-K500-STATUS         
                    PERFORM 9999-CLOSE-ROUTINE    THRU 9999-EXIT                
           END-EVALUATE.                                                        
       305-EXIT.                                                                
           EXIT.                                                                
                                                                                
       350-COMPUTE-DPD.                                                         
      *------------------------------------------------------------*            
           MOVE SPACES                         TO WS-DEL-POSSN-DATE.            
           MOVE K500-DTE-BLDG-COMPLETE         TO WS-8-DATE-CHAR.               
           IF K100-DTE-1ST-SALE  >= '20200801'                                  
              IF (K100-ESTATE       = '645' AND                                 
                  K100-NEIGHBOURHOOD = 'N1  ' AND                               
                  K100-CONTRACT-NO   = 'C59 ') OR                               
                 (K100-ESTATE       = '646' AND                                 
                  K100-NEIGHBOURHOOD = 'N5  ' AND                               
                  K100-CONTRACT-NO   = 'C7  ') OR                               
                 (K100-ESTATE       = '646' AND                                 
                  K100-NEIGHBOURHOOD = 'N5  ' AND                               
                  K100-CONTRACT-NO   = 'C8  ') OR                               
                 (K100-ESTATE       = '645' AND                                 
                  K100-NEIGHBOURHOOD = 'N2  ' AND                               
                  K100-CONTRACT-NO   = 'C36 ') OR                               
                 (K100-ESTATE       = '644' AND                                 
                  K100-NEIGHBOURHOOD = 'N4  ' AND                               
                  K100-CONTRACT-NO   = 'C50 ') OR                               
                 (K100-ESTATE       = '720' AND                                 
                  K100-NEIGHBOURHOOD = 'N2  ' AND                               
                  K100-CONTRACT-NO   = 'C11 ')                                  
                 ADD 15         TO WS-8-DATE-MM                                 
              ELSE                                                              
                 ADD 18         TO WS-8-DATE-MM                                 
              END-IF                                                            
           ELSE                                                                 
              ADD 12                           TO WS-8-DATE-MM                  
           END-IF.                                                              
           IF WS-8-DATE-MM > 12                                                 
              ADD 1                            TO WS-8-DATE-YYYY                
              SUBTRACT 12                    FROM WS-8-DATE-MM                  
              IF WS-8-DATE-MM > 12                                              
                 ADD 1                         TO WS-8-DATE-YYYY                
                 SUBTRACT 12                 FROM WS-8-DATE-MM                  
              END-IF                                                            
              MOVE WS-8-DATE-MM                TO WS-MONTH                      
              MOVE WS-8-DATE-DD                TO WS-DAY                        
              PERFORM 360-CHECK-DAY          THRU 360-EXIT                      
              MOVE WS-DAY                      TO WS-DEL-POSSN-DD               
              MOVE WS-8-DATE-MM                TO WS-DEL-POSSN-MM               
              MOVE WS-8-DATE-YYYY              TO WS-DEL-POSSN-YYYY             
           ELSE                                                                 
              MOVE WS-8-DATE-MM                TO WS-MONTH                      
              MOVE WS-8-DATE-DD                TO WS-DAY                        
              PERFORM 360-CHECK-DAY          THRU 360-EXIT                      
              MOVE WS-DAY                      TO WS-DEL-POSSN-DD               
              MOVE WS-8-DATE-MM                TO WS-DEL-POSSN-MM               
              MOVE WS-8-DATE-YYYY              TO WS-DEL-POSSN-YYYY             
           END-IF.                                                              
                                                                                
           MOVE WS-DEL-POSSN-DATE         TO WS-K580-DTE-UNIT-DELIVERY.         
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
       360-CHECK-DAY.                                                           
      *------------------------------------------------------------*            
           DIVIDE WS-8-DATE-YYYY BY 4 GIVING WS-YEAR REMAINDER                  
                  WS-LEAP-YEAR.                                                 
                                                                                
           IF MTH-END-WITH-30                                                   
              MOVE '30'                     TO WS-DAY                           
           ELSE                                                                 
              IF MTH-FEB                                                        
                 IF WS-LEAP-YEAR = 0                                            
                    MOVE '29'               TO WS-DAY                           
                 ELSE                                                           
                    MOVE '28'               TO WS-DAY                           
                 END-IF                                                         
              ELSE                                                              
                 MOVE '31'                  TO WS-DAY                           
              END-IF                                                            
           END-IF.                                                              
                                                                                
       360-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-READ-BB14K580-UPDT.                                                  
      *------------------------------------------------------------*            
           MOVE SPACES                         TO K580-KEY-FLD.                 
           MOVE F110-SCH-ACC-NO(1:9)           TO K580-NUM-SCH-ACCNT.           
                                                                                
           READ BB14K580                                                        
                                                                                
           EVALUATE WS-K580-STATUS                                              
             WHEN 00                                                            
             WHEN 02                                                            
                DISPLAY ' '                                                     
                DISPLAY '***FOUND IN BB14K580***'                               
                PERFORM 450-REWRITE-BB14K580 THRU 450-EXIT                      
             WHEN 23                                                            
                DISPLAY 'RECORD NOT FOUND IN K580:' F110-SCH-ACC-NO(1:9)        
                ADD   1                         TO WS-K580-NOTFND-CNT           
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING BB14K580 FILE:' WS-K580-STATUS           
                PERFORM 9999-CLOSE-ROUTINE   THRU 9999-EXIT                     
           END-EVALUATE.                                                        
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       450-REWRITE-BB14K580.                                                    
      *------------------------------------------------------------*            
           DISPLAY 'F110-AIX1       : ' F110-AIX1                               
           DISPLAY 'WS-K100-KEY-FLD : ' WS-K100-KEY-FLD                         
           DISPLAY 'PREV DTE-UNIT-DELIVERY : ' K580-DTE-UNIT-DELIVERY           
           DISPLAY 'UPDT DTE-UNIT-DELIVERY : ' WS-K580-DTE-UNIT-DELIVERY        
           DISPLAY 'K580-NUM-SCH-ACCNT     : ' K580-NUM-SCH-ACCNT               
                                                                                
           MOVE WS-K580-DTE-UNIT-DELIVERY     TO K580-DTE-UNIT-DELIVERY.        
           MOVE FUNCTION CURRENT-DATE(1:8)    TO K580-DTE-UPDATE.               
           MOVE 'BP13C58D'                    TO K580-NUM-USERID.               
                                                                                
           REWRITE BB14K580-REC                                                 
           IF WS-K580-STATUS = 00                                               
              DISPLAY '**REWRITE BB14K580 SUCCESSFUL***'                        
              ADD 1     TO WS-UPD-K580-CNT                                      
           ELSE                                                                 
              DISPLAY ' SCH-ACCT : ' K580-NUM-SCH-ACCNT                         
              DISPLAY ' ERROR IN REWRITE BB14K580:' WS-K580-STATUS              
           END-IF.                                                              
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
       9999-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------*            
           DISPLAY '  '.                                                        
           DISPLAY '*----------- BP13C24Z SUMMARY --------------*'.             
           DISPLAY '  '.                                                        
           DISPLAY 'NO OF BM06F110 READ        = ' WS-READ-TOTAL-CNT.           
           DISPLAY 'NO OF BB14K580 NOTFND      = ' WS-K580-NOTFND-CNT.          
           DISPLAY 'NO OF BB14K580 UPDATED     = ' WS-UPD-K580-CNT.             
                                                                                
           CLOSE BM06F110                                                       
                 BM06K100                                                       
                 BB14K500                                                       
                 BB14K580.                                                      
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
             DISPLAY 'BM06K100 CLOSE ERROR: ' WS-K100-STATUS                    
             MOVE  WS-K100-STATUS      TO       RETURN-CODE.                    
                                                                                
           IF WS-K500-STATUS NOT = 00 AND 97                                    
             DISPLAY 'BB14K500 CLOSE ERROR: ' WS-K500-STATUS                    
             MOVE  WS-K500-STATUS      TO       RETURN-CODE.                    
                                                                                
           IF WS-K580-STATUS NOT = 00 AND 97                                    
             DISPLAY 'BB14K580 CLOSE ERROR: ' WS-K580-STATUS                    
             MOVE  WS-K580-STATUS      TO       RETURN-CODE.                    
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
