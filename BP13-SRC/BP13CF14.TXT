       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF14.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  03/12/2013.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVE    :  DETERMINE SMALLER RANDOM # BETWEERN PARENT *            
      *                 & CHILD THEN ASSIGN IT TO LARGER RANDOM #  *            
      *                 WITH 'S' ON THE 28TH BYTE                  *            
      *                                                            *            
      * INPUT FILE   :  BP13F740  SEQUENTIAL DATASET               *            
      * I-O FILE     :  BP13K740  BP13.K740.MATUREBA               *            
      * OUTPUT FILE  :  P13F740A  BP13.F740.MATUREBA.MGP.RANDUPD   *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY    DDMMCCYY  DESCRIPTION                      *            
      * --------  ----  --------  -------------------------------- *            
      * BP135170  SMR2  03122013  NEW PROGRAM                      *            
      * BP136633  IL5   11032017  BYPASS GRO CASES                 *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740  ASSIGN       TO BP13F740.                           
           SELECT BP13K740  ASSIGN       TO BP13K740                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K740-KEY-FLD                        
                            ALTERNATE RECORD KEY                                
                                         IS K740-ALT1-KEY                       
                            FILE STATUS  IS WS-K740-STATUS.                     
           SELECT P13F740A  ASSIGN       TO P13F740A.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F740                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K740                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K740.                                                           
                                                                                
       FD   P13F740A                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       01   P13F740A-REC        PIC X(500).                                     
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K740-STATUS             PIC 9(02) VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ               PIC 9(06) VALUE ZEROES.               
           05  WS-F740-NTFND              PIC 9(06) VALUE ZEROES.               
           05  WS-740A-WRITE              PIC 9(06) VALUE ZEROES.               
           05  WS-K740-WRITE              PIC 9(06) VALUE ZEROES.               
           05  WS-K740-DELETE             PIC 9(06) VALUE ZEROES.               
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F740-EOF                PIC X(01) VALUE 'N'.                  
           05  WS-K740-EOF                PIC X(01) VALUE 'N'.                  
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY            PIC 9(4).                             
               10  WS-SYS-MM              PIC 9(2).                             
               10  WS-SYS-DD              PIC 9(2).                             
           05  WS-EDITED-DATE             PIC X(10).                            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-TEMP-RANDOM             PIC X(28) VALUE SPACES.               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F740      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-F740       THRU 3000-EXIT                       
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F740                                                 
                  I-O  BP13K740                                                 
               OUTPUT  P13F740A.                                                
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K740 OPEN ERROR ' WS-K740-STATUS                
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-SYSTEM-DATE.                   
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE.                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F740.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F740 AT END                                                 
                MOVE 'Y'                   TO WS-F740-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F740-READ.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-F740.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 5000-STARTBR-K740       THRU 5000-EXIT.                      
           PERFORM 2000-READ-BP13F740      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       5000-STARTBR-K740.                                                       
      *--------------------------------------------------------*                
                                                                                
           MOVE LOW-VALUES                 TO K740-ALT1-KEY.                    
           MOVE F740-NUM-JB-REGN           TO K740-ALT1-KEY.                    
                                                                                
           MOVE 'N'                        TO WS-K740-EOF.                      
                                                                                
           START BP13K740 KEY >= K740-ALT1-KEY.                                 
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROES                                                          
              PERFORM 5100-READNEXT-K740   THRU 5100-EXIT                       
                UNTIL K740-ALT1-KEY NOT = F740-NUM-JB-REGN                      
                   OR WS-K740-EOF = 'Y'                                         
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              ADD 1                        TO WS-F740-NTFND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN STARTBR OF BP13K740. STATUS IS '                
                      WS-K740-STATUS                                            
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       5100-READNEXT-K740.                                                      
      *--------------------------------------------------------*                
                                                                                
           READ BP13K740 NEXT RECORD                                            
                AT END MOVE 'Y'            TO WS-K740-EOF.                      
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              IF (K740-NUM-MGPS NOT = SPACES AND LOW-VALUES) AND                
                 (K740-NUM-JB-REGN = F740-NUM-REGN  AND                         
                  K740-CDE-ALLOC-SCH NOT = 'GRO')                               
                 PERFORM 6000-WRITE-P13F740A  THRU 6000-EXIT                    
                 PERFORM 7000-WRITE-BP13K740  THRU 7000-EXIT                    
              END-IF                                                            
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                     TO WS-K740-EOF                       
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN READ NEXT OF BP13K740. STATUS IS '              
                      WS-K740-STATUS                                            
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-WRITE-P13F740A.                                                     
      *-------------------------------------------------------------            
                                                                                
           IF F740-NUM-RANDOM > K740-NUM-RANDOM                                 
              MOVE K740-NUM-RANDOM         TO F740-NUM-RANDOM                   
              MOVE 'S'                     TO F740-NUM-RANDOM(28:1)             
              WRITE P13F740A-REC           FROM BP13F740-REC                    
              ADD 1                        TO WS-740A-WRITE                     
           ELSE                                                                 
              WRITE P13F740A-REC           FROM BP13F740-REC                    
              ADD 1                        TO WS-740A-WRITE                     
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       7000-WRITE-BP13K740.                                                     
      *-------------------------------------------------------------            
                                                                                
           IF K740-NUM-RANDOM > F740-NUM-RANDOM                                 
              IF K740-NUM-RANDOM(1:27) = F740-NUM-RANDOM(1:27)                  
                 GO TO 7000-EXIT                                                
              END-IF                                                            
              MOVE K740-NUM-RANDOM         TO WS-TEMP-RANDOM                    
              MOVE F740-NUM-RANDOM         TO K740-NUM-RANDOM                   
              MOVE 'S'                     TO K740-NUM-RANDOM(28:1)             
           ELSE                                                                 
              GO TO 7000-EXIT                                                   
           END-IF.                                                              
                                                                                
           WRITE BP13K740-REC.                                                  
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              ADD 1                        TO WS-K740-WRITE                     
              PERFORM 7100-READ-K740       THRU 7100-EXIT                       
                                                                                
           WHEN 22                                                              
              DISPLAY 'DUPLICATE RECORD FOR BP13K740. KEY IS - '                
                      K740-KEY-FLD                                              
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR WRITING TO BP13K740. STATUS IS - '                 
                      WS-K740-STATUS                                            
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       7100-READ-K740.                                                          
      *--------------------------------------------------------*                
                                                                                
           MOVE LOW-VALUES                 TO K740-KEY-FLD.                     
           MOVE WS-TEMP-RANDOM             TO K740-KEY-FLD.                     
                                                                                
           READ BP13K740.                                                       
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROES                                                          
              PERFORM 7200-DELETE-BP13K740 THRU 7200-EXIT                       
                                                                                
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K740' K740-KEY-FLD               
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN READ OF BP13K740. STATUS IS '                   
                      WS-K740-STATUS                                            
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       7200-DELETE-BP13K740.                                                    
      *-------------------------------------------------------------            
                                                                                
           DELETE BP13K740.                                                     
                                                                                
           IF WS-K740-STATUS NOT = ZEROES                                       
              DISPLAY 'ERROR DELETING BP13K740. STATUS IS - '                   
                      WS-K740-STATUS                                            
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           ADD 1                           TO WS-K740-DELETE.                   
                                                                                
       7200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '==============================================='.           
           DISPLAY '      PROGRAM : BP13CF14       ' WS-EDITED-DATE.            
           DISPLAY '==============================================='.           
           DISPLAY ' '.                                                         
           DISPLAY 'BP13F740 RECORDS READ       ' WS-F740-READ.                 
           DISPLAY 'BP13F740 RECORDS NOT FOUND  ' WS-F740-NTFND.                
           DISPLAY 'P13F740A RECORDS WRITTEN    ' WS-740A-WRITE.                
           DISPLAY 'BP13K740 RECORDS WRITTEN    ' WS-K740-WRITE.                
           DISPLAY 'BP13K740 RECORDS DELETED    ' WS-K740-DELETE.               
           DISPLAY ' '.                                                         
           DISPLAY '==============================================='.           
                                                                                
           CLOSE BP13F740                                                       
                 BP13K740                                                       
                 P13F740A.                                                      
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K740 CLOSE ERROR ' WS-K740-STATUS               
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
