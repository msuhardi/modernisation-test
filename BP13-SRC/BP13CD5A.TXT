      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13CD5A.                                                 
       AUTHOR.        IRVIN CHING.                                              
       DATE-WRITTEN.  27/12/12.                                                 
                                                                                
      *========================================================*                
      *    SYSTEM NAME   :   SYSTEM OF COMMITMENT              *                
      *    SYSTEM ID     :   BP13                              *                
      *                                                        *                
      *    OBJECTIVE     :   FOR DBSS FLAT :                   *                
      *                  :   UNDER REPUDIATION , A NEW LESSEE  *                
      *                      FOR SCH-ACC-NO WAS CREATED. WHEN  *                
      *                      REPUDIATION IS EFFECTIVE, PASS    *                
      *                      NEW LESSEE CODE TO SMS.           *                
      *     INPUT  FILE  :                                     *                
      *        1.  BP13FD00                                    *                
      *        2.  BP13F800                                    *                
      *     OUTPUT FILE  :                                     *                
      *        1.  AB03F021                                    *                
      *        2.  B03F021A                                    *                
      *                                                        *                
      * CHG REQ  DATE      BY   DESCRIPTION                    *                
      * =======  ========  ===  ============================   *                
      * BP134768 27122012  IMC1 NEW PGM                        *                
      * BP135670 23042015  ESA1 TO REMOVE CHECKING FOR         *                
      *                         F021-CDE-LESSEE-EXLESSEE(1:1) = '1'             
      *                         FOR '91459477618'              *                
      *========================================================*                
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FD00 ASSIGN       TO BP13FD00.                            
           SELECT SY02F001 ASSIGN       TO SY02F001.                            
                                                                                
           SELECT BP13KA30 ASSIGN TO BP13KA30                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KA30-KEY-FLD                               
                  ALTERNATE KEY   IS KA30-ALT-KEY                               
                  FILE STATUS     IS WS-KA30-STATUS.                            
                                                                                
           SELECT AB03F021 ASSIGN       TO AB03F021.                            
           SELECT B03F021A ASSIGN       TO B03F021A.                            
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13FD00                                                            
            RECORDING MODE  IS   F                                              
            BLOCK  CONTAINS 0    RECORDS                                        
            RECORD CONTAINS 1600 CHARACTERS                                     
            LABEL  RECORDS  ARE  STANDARD.                                      
       COPY BP13FD00.                                                           
                                                                                
       FD   BP13KA30                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13KA30.                                                           
                                                                                
       FD   AB03F021                                                            
            RECORDING MODE  IS   F                                              
            BLOCK  CONTAINS 0    RECORDS                                        
            RECORD CONTAINS 150 CHARACTERS                                      
            LABEL  RECORDS  ARE  STANDARD.                                      
       COPY AB03F021.                                                           
                                                                                
       FD   B03F021A                                                            
            RECORDING MODE  IS   F                                              
            BLOCK  CONTAINS 0    RECORDS                                        
            RECORD CONTAINS 150 CHARACTERS                                      
            LABEL  RECORDS  ARE  STANDARD.                                      
       01 B03F021A-REC      PIC X(150).                                         
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-PREV-MTH               PIC X(06) VALUE ZEROES.                    
       01  WS-CUR-DATE               PIC 9(08) VALUE ZEROES.                    
       01  WS-CUR-TIME               PIC 9(08) VALUE ZEROES.                    
       01  WS-CUR-TIME-X REDEFINES WS-CUR-TIME PIC X(08).                       
       01  WS-K820-STATUS            PIC 9(02) VALUE ZEROES.                    
       01  WS-KB20-STATUS            PIC 9(02) VALUE ZEROES.                    
       01  WS-KA30-STATUS            PIC 9(02) VALUE ZEROES.                    
       01  WS-FD00-READ              PIC 9(07) VALUE ZEROES.                    
       01  WS-F800-READ              PIC 9(07) VALUE ZEROES.                    
       01  WS-KB20-DELETE            PIC 9(07) VALUE ZEROES.                    
       01  WS-KB20-WRITE             PIC 9(07) VALUE ZEROES.                    
       01  WS-F021-WRITE             PIC 9(07) VALUE ZEROES.                    
       01  WS-F021A-WRITE            PIC 9(07) VALUE ZEROES.                    
       01  WS-FD00-EOF               PIC X(01) VALUE SPACES.                    
       01  WS-PREV-REGN              PIC X(08) VALUE SPACES.                    
       01  WS-PREV-KB20-REC          PIC X(3300) VALUE SPACES.                  
       01  WS-NRIC                   PIC X(09) VALUE SPACES.                    
       01  WS-PREV-SCH-ACCT.                                                    
           05  WS-PREV-SCH-ACC       PIC X(09).                                 
           05  WS-OLD-LESSEE-CODE    PIC X(02).                                 
       01  WS-PREV-DTE-EFF-TRANSFER  PIC X(08).                                 
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      **********************                                                    
       1000-MAIN.                                                               
      **********************                                                    
           PERFORM  2000-OPENING-ROUTINE    THRU  2000-EXIT.                    
           PERFORM  3000-READ-FD00-REC      THRU  3000-EXIT.                    
           PERFORM  4000-PROCESS-FD00-REC   THRU  4000-EXIT                     
             UNTIL  WS-FD00-EOF = 'Y'                                           
           PERFORM  9000-CLOSING-ROUTINE    THRU  9000-EXIT.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **********************                                                    
       2000-OPENING-ROUTINE.                                                    
      **********************                                                    
           OPEN INPUT  BP13FD00                                                 
                       SY02F001                                                 
                       BP13KA30                                                 
                OUTPUT AB03F021                                                 
                       B03F021A.                                                
                                                                                
           IF WS-KA30-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13KA30 , '                               
                      'STATUS = ' WS-KA30-STATUS                                
              MOVE WS-KA30-STATUS TO RETURN-CODE                                
              GO TO 9000-CLOSING-ROUTINE                                        
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-LAST-DAY-PREV(1:6)  TO WS-PREV-MTH.                    
           MOVE F001-DTE-CURRENT             TO WS-CUR-DATE.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *         READ INPUT FILE                                                 
      ******************************************************************        
      **********************                                                    
       3000-READ-FD00-REC.                                                      
      **********************                                                    
           READ BP13FD00 AT END                                                 
                         MOVE 'Y'             TO WS-FD00-EOF                    
                         GO TO 3000-EXIT.                                       
                                                                                
           ADD 1 TO WS-FD00-READ.                                               
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       4000-PROCESS-FD00-REC.                                                   
      ***********************                                                   
                                                                                
           IF FD00-DTE-REPUDIATE = WS-CUR-DATE OR                               
             ((FD00-NUM-PROJECT   = 'REJ' OR 'RPD') AND                         
              FD00-DTE-REPUDIATE = WS-CUR-DATE)                                 
              PERFORM 4500-MAIN-PROCESS        THRU 4500-EXIT                   
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-FD00-REC       THRU 3000-EXIT.                     
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       4500-MAIN-PROCESS.                                                       
      ********************                                                      
      * FD00 - FD00-NUM-REGN(OLD) , FD00-NUM-SCH-ACCNT(OLD)                     
      * KA30 - KA30-NUM-REGN(OLD)                                               
      *      - KA30-NUM-SCH-ACC(NEW)                                            
                                                                                
           IF FD00-NUM-PROJECT  = 'REJ' OR 'RPD'                                
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 7600-READ-BP13KA30   THRU 7600-EXIT                       
           END-IF                                                               
                                                                                
           PERFORM 6000-OUTPUT-AB03F021 THRU 6000-EXIT.                         
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************                                                  
       6000-OUTPUT-AB03F021.                                                    
      ************************                                                  
                                                                                
           ACCEPT WS-CUR-TIME FROM TIME.                                        
           MOVE SPACES                   TO AB03F021-REC.                       
           MOVE 'C'                      TO F021-CDE-TRANS-FIS.                 
           MOVE 'OT'                     TO F021-CDE-REASON-FOR-CHGE.           
           MOVE 'BP13'                   TO F021-CDE-SYSTEM.                    
           MOVE 'D'                      TO F021-CDE-ACTION.                    
           MOVE WS-CUR-DATE              TO F021-DTE-UPDTE                      
                                            F021-DTE-CANCEL-WEF.                
           MOVE WS-CUR-TIME-X            TO F021-DATA-ENTRY-TIME.               
                                                                                
      * FOR REJECT CASES                                                        
           IF FD00-NUM-PROJECT   = 'REJ' OR 'RPD'                               
              MOVE BP13FD00-REC(31:11)      TO F021-NUM-HDB-REF                 
           ELSE                                                                 
      * FOR REPUDIATION CASES                                                   
              MOVE KA30-NUM-SCH-ACC         TO F021-NUM-HDB-REF                 
           END-IF                                                               
                                                                                
           MOVE FD00-NUM-SCH-ACCNT       TO F021-NUM-HDB-REF-EXLESSEE.          
                                                                                
           IF F021-NUM-HDB-REF = F021-NUM-HDB-REF-EXLESSEE                      
      *    IF F021-NUM-HDB-REF = F021-NUM-HDB-REF-EXLESSEE OR                   
      *       F021-CDE-LESSEE-EXLESSEE(1:1) = '1'                               
              GO TO 6000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF FD00-NUM-PROJECT   = 'RPD'                                        
              WRITE B03F021A-REC FROM AB03F021-REC                              
              ADD 1                         TO WS-F021A-WRITE                   
           ELSE                                                                 
              WRITE AB03F021-REC                                                
              ADD 1                         TO WS-F021-WRITE                    
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       7600-READ-BP13KA30.                                                      
      ************************                                                  
                                                                                
           INITIALIZE BP13KA30-REC.                                             
                                                                                
           MOVE FD00-NUM-REGN  TO KA30-NUM-REGN.                                
           READ BP13KA30 KEY IS KA30-ALT-KEY.                                   
                                                                                
           IF WS-KA30-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-KA30-STATUS = 23                                            
                 DISPLAY 'BP13KA30 - RECORD NOT FOUND : '                       
                         KA30-NUM-REGN                                          
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13KA30 , STATUS = '                      
                          WS-KA30-STATUS                                        
                 DISPLAY ' KEY = ' KA30-NUM-REGN                                
                 GO TO 9000-CLOSING-ROUTINE                                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
       7600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       9000-CLOSING-ROUTINE.                                                    
      ************************                                                  
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CD5A'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13FD00 RECORDS READ............. : '                
                    WS-FD00-READ.                                               
           DISPLAY 'NO OF AB03F021 RECORDS WRITTEN.......... : '                
                    WS-F021-WRITE.                                              
           DISPLAY 'NO OF B03F021A RECORDS WRITTEN.......... : '                
                    WS-F021A-WRITE.                                             
                                                                                
           CLOSE BP13FD00                                                       
                 BP13KA30                                                       
                 SY02F001                                                       
                 AB03F021                                                       
                 B03F021A.                                                      
                                                                                
           IF WS-KA30-STATUS NOT = 00                                           
              DISPLAY 'ERROR CLOSING BP13KA30 ,'                                
                      'STATUS = ' WS-KA30-STATUS                                
              MOVE WS-KA30-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
