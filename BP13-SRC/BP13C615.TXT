       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C615.                                                  
      *AUTHOR.       RB12.                                                      
      *DATE-WRITTEN. 29/09/2009.                                                
      * ========================================================== *            
      *                 SYSTEM OF COMMITMENT (SOC)                 *            
      * ========================================================== *            
      *                                                            *            
      *  PURPOSE :  UPDATE K800-NUM-GRANT-CREDIT-TAG               *            
      *                                                            *            
      *  INPUT   :  BP13F800                                       *            
      *             BP13K680                                       *            
      *  I-O     :  BP13K800                                       *            
      *             BP13K50A                                       *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      *  CHG NO     DATE     BY   DESCRIPTION                      *            
      * -------- ----------  ---  -------------------------------- *            
      * BP133707 30/10/2009  RB12 NEW PROGRAM                      *            
      * BP133745 17/02/2010  PCL3 DELETE BP13K50A WHEN FOUND IN    *            
      *                           BP13K800 AND GRANT-CREDIT-TAG = Y*            
      * BP136460 02/03/2017  KSJ3 EXPAND BP13K680 (1500-1600)      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
           SELECT BP13K50A ASSIGN TO BP13K50A                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K50A-KEY-FLD                               
                  FILE STATUS     IS WS-K50A-STATUS.                            
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
           SELECT BP13K680 ASSIGN TO BP13K680                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K680-NUM-REGN                              
                  FILE STATUS     IS WS-K680-STATUS.                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F800                                                             
           BLOCK CONTAINS 0 CHARACTERS                                          
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F800.                                                           
                                                                                
       FD  BP13K50A                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13K50A.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K680                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
       COPY BP13K680.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-F800-EOF                   PIC X    VALUE 'N'.                    
       01  WS-K680-EOF                   PIC X    VALUE 'N'.                    
       01  WS-K680-FND                   PIC X    VALUE 'N'.                    
       01  WS-K800-STATUS                PIC 99   VALUE ZEROS.                  
       01  WS-K680-STATUS                PIC 99   VALUE ZEROS.                  
       01  WS-K50A-STATUS                PIC 99   VALUE ZEROS.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F800-READ              PIC 9(9) VALUE ZEROS.                  
           05  WS-K800-READ              PIC 9(9) VALUE ZEROS.                  
           05  WS-K680-READ              PIC 9(9) VALUE ZEROS.                  
           05  WS-K800-UPD               PIC 9(9) VALUE ZEROS.                  
           05  WS-K800-BYPASS            PIC 9(9) VALUE ZEROS.                  
           05  WS-K800-NOTFND            PIC 9(9) VALUE ZEROS.                  
           05  WS-K50A-DEL               PIC 9(9) VALUE ZEROS.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
                                                                                
            PERFORM 100-OPEN-FILES   THRU 100-EXIT.                             
            PERFORM 200-READ-F800    THRU 200-EXIT.                             
            PERFORM 300-PROCESS-F800 THRU 300-EXIT                              
                    UNTIL WS-F800-EOF = 'Y'.                                    
            PERFORM 900-CLOSE-FILES  THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F800                                                 
                       BP13K680                                                 
                I-O    BP13K800                                                 
                       BP13K50A.                                                
                                                                                
           IF WS-K50A-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K50A-STATUS ' WS-K50A-STATUS              
              MOVE WS-K50A-STATUS       TO RETURN-CODE                          
              PERFORM 900-CLOSE-FILES THRU 900-EXIT.                            
                                                                                
           IF WS-K800-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K800-STATUS ' WS-K800-STATUS              
              MOVE WS-K800-STATUS       TO RETURN-CODE                          
              PERFORM 900-CLOSE-FILES THRU 900-EXIT.                            
                                                                                
           IF WS-K680-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K680-STATUS ' WS-K680-STATUS              
              MOVE WS-K680-STATUS       TO RETURN-CODE                          
              PERFORM 900-CLOSE-FILES THRU 900-EXIT.                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F800.                                                           
      *-------------------------------------------------------------            
           READ BP13F800 AT END                                                 
                MOVE 'Y' TO WS-F800-EOF                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1         TO WS-F800-READ.                                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-F800.                                                        
      *-------------------------------------------------------------            
           PERFORM 310-UPDATE-K800  THRU 310-EXIT.                              
                                                                                
           PERFORM 200-READ-F800    THRU 200-EXIT.                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       310-UPDATE-K800.                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE F800-NUM-REGN         TO K800-NUM-REGN.                         
           READ BP13K800.                                                       
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
                ADD 1                        TO WS-K800-READ                    
                PERFORM 350-READ-BP13K680  THRU 350-EXIT                        
           WHEN 23                                                              
                DISPLAY 'REC NOT FOUND IN BP13K800 : ' F800-NUM-REGN            
                        ', STATUS ' WS-K800-STATUS                              
                ADD 1                        TO WS-K800-NOTFND                  
           WHEN OTHER                                                           
                DISPLAY 'ERROR IN READING BP13K800 : ' F800-NUM-REGN            
                        ', STATUS ' WS-K800-STATUS                              
                MOVE WS-K800-STATUS TO RETURN-CODE                              
                PERFORM 900-CLOSE-FILES THRU 900-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
       350-READ-BP13K680.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE K800-NUM-REGN         TO K680-NUM-REGN.                         
           READ BP13K680.                                                       
           EVALUATE WS-K680-STATUS                                              
           WHEN 00                                                              
                ADD 1                        TO WS-K680-READ                    
                IF (K680-AMT-CHG-BLOCK-LSE1 > 0) OR                             
                   (K680-AMT-CHG-BLOCK-LSE2 > 0) OR                             
                   (K680-AMT-CHG-BLOCK-LSE3 > 0) OR                             
                   (K680-AMT-CHG-BLOCK-LSE4 > 0)                                
                   PERFORM 400-REWRITE-BP13K800 THRU 400-EXIT                   
                ELSE                                                            
                   ADD 1 TO WS-K800-BYPASS                                      
                END-IF                                                          
           WHEN 23                                                              
                DISPLAY 'REC NOT FOUND IN BP13K680 : ' K800-NUM-REGN            
                        ', STATUS ' WS-K680-STATUS                              
                ADD 1                        TO WS-K800-NOTFND                  
           WHEN OTHER                                                           
                DISPLAY 'ERROR IN READING BP13K680 : ' K800-NUM-REGN            
                        ', STATUS ' WS-K680-STATUS                              
                MOVE WS-K680-STATUS TO RETURN-CODE                              
                PERFORM 900-CLOSE-FILES THRU 900-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-REWRITE-BP13K800.                                                    
      *-------------------------------------------------------------            
                                                                                
           MOVE 'Y'                        TO K800-NUM-GRANT-CREDIT-TAG.        
           MOVE FUNCTION CURRENT-DATE(1:8) TO K800-DTE-UPDATE.                  
           MOVE 'BP13C615'                 TO K800-NUM-USERID.                  
                                                                                
           REWRITE BP13K800-MASTER.                                             
           IF WS-K800-STATUS = 00 OR 02                                         
              ADD 1 TO WS-K800-UPD                                              
              PERFORM 450-READ-UPD-K50A             THRU 450-EXIT               
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING BP13K800 : ' F800-NUM-REGN               
                      '  STATUS ' WS-K800-STATUS                                
              MOVE WS-K800-STATUS TO RETURN-CODE                                
              PERFORM 900-CLOSE-FILES THRU 900-EXIT                             
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       450-READ-UPD-K50A.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                TO BP13K50A-REC.                          
           INITIALIZE                    BP13K50A-REC.                          
                                                                                
           MOVE F800-NUM-REGN         TO K50A-KEY-FLD.                          
                                                                                
           READ BP13K50A.                                                       
                                                                                
           EVALUATE WS-K50A-STATUS                                              
           WHEN 00                                                              
                PERFORM 500-DELETE-BP13K50A   THRU 500-EXIT                     
           WHEN 23                                                              
                CONTINUE                                                        
           WHEN OTHER                                                           
                DISPLAY 'ERROR IN READING BP13K50A : ' F800-NUM-REGN            
                        ', STATUS ' WS-K50A-STATUS                              
                MOVE WS-K50A-STATUS TO RETURN-CODE                              
                PERFORM 900-CLOSE-FILES       THRU 900-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-DELETE-BP13K50A.                                                     
      *-------------------------------------------------------------            
                                                                                
           DELETE BP13K50A.                                                     
                                                                                
           IF WS-K50A-STATUS = 00 OR 02                                         
              ADD 1                                TO WS-K50A-DEL               
           ELSE                                                                 
              DISPLAY 'ERROR DELETING BP13K50A : ' F800-NUM-REGN                
                      '  STATUS ' WS-K50A-STATUS                                
              MOVE WS-K50A-STATUS                  TO RETURN-CODE               
              PERFORM 900-CLOSE-FILES            THRU 900-EXIT                  
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY '----- BP13C615 RUN STATISTICS --------------'.              
           DISPLAY 'NO OF REC READ    BP13F800   = ' WS-F800-READ.              
           DISPLAY 'NO OF REC READ    BP13K800   = ' WS-K800-READ.              
           DISPLAY 'NO OF REC NOTFND  BP13K800   = ' WS-K800-NOTFND.            
           DISPLAY 'NO OF REC READ    BP13K680   = ' WS-K680-READ.              
           DISPLAY 'NO OF REC BYPASS  BP13K800   = ' WS-K800-BYPASS.            
           DISPLAY 'NO OF REC UPDATED BP13K800   = ' WS-K800-UPD.               
           DISPLAY 'NO OF REC DELETED BP13K50A   = ' WS-K50A-DEL.               
           DISPLAY '----- BP13C615 EOJ -------------------------'.              
                                                                                
           CLOSE BP13F800                                                       
                 BP13K800                                                       
                 BP13K50A                                                       
                 BP13K680.                                                      
                                                                                
           IF WS-K50A-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K50A, STATUS ' WS-K50A-STATUS          
              MOVE WS-K50A-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K800, STATUS ' WS-K800-STATUS          
              MOVE WS-K800-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K680-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K680, STATUS ' WS-K680-STATUS          
              MOVE WS-K680-STATUS        TO RETURN-CODE.                        
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
