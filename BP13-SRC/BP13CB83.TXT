       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB83.                                                 
       AUTHOR.        BO JIANG.                                                 
      *DATE-WRITTEN.  07/01/10.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE   : DETAIL FOR BOOKED AND NOT BOOKED BTO/BE REQ *            
      *                (GLOBAL FIGURE)                             *            
      *                                                            *            
      *  INPUT FILES : 1.  BP13F595  -                             *            
      *                2.  BP13K500  -                             *            
      *                3.  BP13K595  -                             *            
      *                                                            *            
      *  REPORT      : 1.  BP13LB83  -                             *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  BY    DATE    DESCRIPTION                         *            
      * -------- ---- -------- ----------------------------------- *            
      * BP133835 JB8  20100107 NEW PROGRAM                         *            
      * BP133952 PCL3 20100708 TO CHECK K816-NUM-MATURE-EST-TAG    *            
      *                        FOR BTO CASES.                      *            
      * BP133921 PCL3 20100713 TO READ BP13K828 TO GET NAME1 AND   *            
      *                        INCOME.                             *            
      * BP135340 IMC1 20140507 CHANGE BP13K816 TO BP13K813         *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *-------------------------------------------------------------            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F595  ASSIGN        TO BP13F595.                          
                                                                                
           SELECT BP13K500  ASSIGN        TO BP13K500                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K500-NUM-REGN                      
                     ALTERNATE RECORD KEY IS K500-NUM-NRIC1                     
                            FILE STATUS   IS K500-STATUS.                       
                                                                                
           SELECT BP13K828  ASSIGN        TO BP13K828                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K828-KEY-FLD                       
                            FILE STATUS   IS K828-STATUS.                       
                                                                                
           SELECT BP13K813  ASSIGN        TO BP13K813                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K813-KEY-FLD                       
                            FILE STATUS   IS K813-STATUS.                       
                                                                                
           SELECT BP13K595  ASSIGN        TO BP13K595                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K595-KEY-FLD                       
                            ALTERNATE KEY IS K595-NUM-NRIC1                     
                                             WITH DUPLICATES                    
                            ALTERNATE KEY IS K595-NUM-NRIC2                     
                                             WITH DUPLICATES                    
                            ALTERNATE KEY IS K595-NUM-NRIC3                     
                                             WITH DUPLICATES                    
                            ALTERNATE KEY IS K595-NUM-NRIC4                     
                                             WITH DUPLICATES                    
                            FILE STATUS   IS K595-STATUS.                       
                                                                                
           SELECT BP13LB83  ASSIGN        TO BP13LB83.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BP13F595                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 500 CHARACTERS.                                     
       01   BP13F595-REC.                                                       
            05  FILLER                    PIC X(32).                            
            05  F595-NUM-REGN             PIC X(08).                            
            05  F595-NUM-NRIC1            PIC X(09).                            
            05  FILLER                    PIC X(137).                           
            05  F595-CDE-NT1              PIC X(03).                            
            05  FILLER                    PIC X(21).                            
            05  F595-NUM-FLAT-TYPE        PIC X(02).                            
            05  FILLER                    PIC X(30).                            
            05  F595-NUM-SC               PIC X(01).                            
            05  FILLER                    PIC X(46).                            
            05  F595-NUM-BOOK-STATUS      PIC X(02).                            
            05  FILLER                    PIC X(42).                            
            05  F595-DTE-BALLOT           PIC X(06).                            
            05  FILLER                    PIC X(36).                            
            05  F595-ALLO-CAT             PIC X(03).                            
            05  FILLER                    PIC X(86).                            
            05  F595-NUM-BE-CNT           PIC 9(02).                            
            05  F595-NUM-BTO-CNT          PIC 9(02).                            
            05  F595-NUM-BK-TAG           PIC X(01).                            
            05  F595-NUM-BK-CNT           PIC 9(02).                            
            05  F595-NUM-REST-CNT         PIC 9(02).                            
            05  FILLER                    PIC X(02).                            
            05  F595-NUM-SCH-ACC          PIC X(09).                            
            05  F595-DTE-BK-APPT          PIC X(08).                            
            05  F595-NUM-ALLOC-TAG        PIC X(02).                            
            05  FILLER                    PIC X(06).                            
                                                                                
       FD   BP13K500                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K500.                                                           
                                                                                
       FD   BP13K595                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K595.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K828                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K828.                                                           
                                                                                
       FD  BP13LB83                                                             
           RECORDING MODE  IS  F.                                               
       01  BP13LB83-REC                     PIC X(132).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-VARIABLES.                                                   
           05  K500-STATUS                  PIC 99    VALUE ZEROS.              
           05  K595-STATUS                  PIC 99    VALUE ZEROS.              
           05  K813-STATUS                  PIC 99    VALUE ZEROS.              
           05  K828-STATUS                  PIC 99    VALUE ZEROS.              
           05  WS-EOF-F595                  PIC X(01) VALUE 'N'.                
               88  F595-EOF                           VALUE 'Y'.                
           05  WS-EOF-K500                  PIC X(01) VALUE 'N'.                
               88  K500-EOF                           VALUE 'Y'.                
           05  WS-EOF-K595                  PIC X(01) VALUE 'N'.                
               88  K595-EOF                           VALUE 'Y'.                
           05  WS-EOF-K828                  PIC X(01) VALUE 'N'.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  CNT-F595-READ                PIC 9(06)  VALUE ZEROS.             
           05  CNT-LB83-PAGE                PIC 9(05)  VALUE ZEROS.             
           05  CNT-LB83-SUM-SNO             PIC 9(05)  VALUE ZEROS.             
           05  CNT-LB83-DTL-SNO             PIC 9(05)  VALUE ZEROS.             
           05  CNT-LB83-PAGE-REC            PIC 9(05)  VALUE 60.                
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-NUM-ZONE                  PIC X(03) VALUE SPACES.             
           05  WS-NUM-INCOME                PIC 9(06) VALUE ZEROES.             
           05  WS-K813-CDE-NT               PIC X(03) VALUE SPACES.             
           05  WS-NUM-MATURE-EST-TAG        PIC X(01) VALUE 'N'.                
           05  WS-NOT-PURE-BTO              PIC X(01) VALUE 'N'.                
               88  NOT-PURE-BTO                       VALUE 'Y'.                
           05  WS-DATE                      PIC X(8).                           
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR CONTROL LISTING                         *            
      *------------------------------------------------------------*            
       01  LB83-TITLE01.                                                        
           05  FILLER                   PIC X(14) VALUE 'BP13LB83'.             
           05  FILLER                   PIC X(8)  VALUE 'HDB3'.                 
           05  FILLER                   PIC X(18) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(18) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  LB83-DATE                PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(8)  VALUE ' PAGE: '.              
           05  LB83-PAGE                PIC ZZZZ9.                              
                                                                                
       01  LB83-TITLE02.                                                        
           05  FILLER                   PIC X(35) VALUE SPACES.                 
           05  FILLER                   PIC X(63) VALUE                         
               'DETAIL FOR BOOKED AND NOT BOOKED BTO/BE REQUEST (GLOBAL         
      -        'FIGURE)'.                                                       
                                                                                
       01  LB83-TITLE03.                                                        
           05  FILLER                   PIC X(55)  VALUE                        
               '         BK TOT BE  BTO'.                                       
           05  FILLER                   PIC X(54)  VALUE                        
               'DATE   ALLOC   BK    ALLO SC   PURE MIXED INCOME NAME1'.        
                                                                                
       01  LB83-TITLE04.                                                        
           05  FILLER                   PIC X(132) VALUE                        
               '   S/NO TAG CNT CNT CNT REGN      NRIC1      NT  FT   BA        
      -        'LLOT   CAT  STATUS  TAG  TAG  BTO  MODE'.                       
                                                                                
       01  LB83-SUMMARY.                                                        
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LB83-SUM-SNO              PIC ZZZZ9 VALUE ZEROES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LB83-SUM-BK-TAG           PIC X(01) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LB83-SUM-TOT-CNT          PIC 99    VALUE ZEROES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LB83-SUM-BE-CNT           PIC 99    VALUE ZEROES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LB83-SUM-BTO-CNT          PIC 99    VALUE ZEROES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LB83-SUM-REGN             PIC X(08) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LB83-SUM-NRIC1            PIC X(09) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LB83-SUM-NT               PIC X(03) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LB83-SUM-FT               PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LB83-SUM-BALLOT           PIC X(07) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LB83-SUM-ALLO-CAT         PIC X(03) VALUE SPACES.                 
           05 FILLER                    PIC X(04) VALUE SPACES.                 
           05 LB83-SUM-BK-STATUS        PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(04) VALUE SPACES.                 
           05 LB83-SUM-ALLOC-TAG        PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(04) VALUE SPACES.                 
           05 LB83-SUM-SC               PIC X(01) VALUE SPACES.                 
           05 FILLER                    PIC X(04) VALUE SPACES.                 
           05 LB83-SUM-PURE-BTO         PIC X(01) VALUE SPACES.                 
           05 FILLER                    PIC X(04) VALUE SPACES.                 
           05 LB83-SUM-MIXED-MODE       PIC X(01) VALUE SPACES.                 
           05 FILLER                    PIC X(04) VALUE SPACES.                 
           05 LB83-SUM-INCOME           PIC Z(06) VALUE ZEROES.                 
           05 FILLER                    PIC X(01) VALUE SPACES.                 
           05 LB83-SUM-NAME1            PIC X(25) VALUE SPACES.                 
                                                                                
       01  LB83-DETAIL.                                                         
           05 FILLER                    PIC X(07) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(01) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(01) VALUE SPACES.                 
           05 LB83-DTL-SNO              PIC Z9    VALUE ZEROES.                 
           05 FILLER                    PIC X(01) VALUE ')'.                    
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LB83-DTL-REGN             PIC X(08) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LB83-DTL-NRIC1            PIC X(09) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LB83-DTL-NT               PIC X(03) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LB83-DTL-FT               PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LB83-DTL-BALLOT           PIC X(07) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 LB83-DTL-ALLO-CAT         PIC X(03) VALUE SPACES.                 
           05 FILLER                    PIC X(04) VALUE SPACES.                 
           05 LB83-DTL-BK-STATUS        PIC X(02) VALUE SPACES.                 
                                                                                
      *=============================================================            
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-FILES             THRU 1000-EXIT.                  
           PERFORM 3000-PROCESS-MATCH          THRU 3000-EXIT                   
             UNTIL F595-EOF.                                                    
           PERFORM 9000-CLOSE-ROUTINE          THRU 9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-FILES.                                                         
      *=============================================================            
           OPEN INPUT BP13F595                                                  
                      BP13K500                                                  
                      BP13K595                                                  
                      BP13K813                                                  
                      BP13K828                                                  
               OUTPUT BP13LB83.                                                 
                                                                                
           IF K500-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING ERROR K500 STATUS ' K500-STATUS                  
              MOVE K500-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           IF K595-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING ERROR K595 STATUS ' K595-STATUS                  
              MOVE K595-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           IF K813-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING ERROR K813 STATUS ' K813-STATUS                  
              MOVE K813-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           IF K828-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING ERROR K828 STATUS ' K828-STATUS                  
              MOVE K828-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)          TO WS-DATE.                 
                                                                                
           STRING  WS-DATE(7:2) '/'                                             
                   WS-DATE(5:2) '/'                                             
                   WS-DATE(1:4)                                                 
                   DELIMITED BY SIZE INTO LB83-DATE                             
           END-STRING.                                                          
                                                                                
                                                                                
           PERFORM 2000-READ-BP13F595  THRU 2000-EXIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       2000-READ-BP13F595.                                                      
      *=============================================================            
                                                                                
            READ BP13F595                                                       
              AT END                                                            
                 MOVE 'Y'         TO WS-EOF-F595                                
                 MOVE HIGH-VALUES TO F595-NUM-REGN                              
                 GO               TO 2000-EXIT                                  
              NOT AT END                                                        
                 ADD 1            TO CNT-F595-READ                              
            END-READ.                                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3000-PROCESS-MATCH.                                                      
      *=============================================================            
                                                                                
           PERFORM 6000-CHECK-PURE-BTO           THRU 6000-EXIT.                
                                                                                
           PERFORM 5100-OUTPUT-SUMMARY           THRU 5100-EXIT.                
                                                                                
           PERFORM 4100-START-BP13K500           THRU 4100-EXIT.                
                                                                                
           IF NOT K500-EOF                                                      
              PERFORM UNTIL K500-EOF                                            
                 PERFORM 4200-READ-NEXT-BP13K500 THRU 4200-EXIT                 
                 IF NOT K500-EOF                                                
                    MOVE SPACES               TO WS-K813-CDE-NT                 
                                                 WS-NUM-MATURE-EST-TAG          
                                                                                
                    IF K500-NUM-ALLO-CAT = 'BTO'                                
                       INITIALIZE BP13K813-REC                                  
                                                                                
                       MOVE K500-CDE-NT1       TO K813-NUM-ZONE                 
                                                  WS-NUM-ZONE                   
      *                MOVE K500-CDE-FLAT-TYPE TO K813-NUM-FLAT-TYPE            
      *                MOVE K500-DTE-BALLOT    TO K813-DTE-BALLOT               
                                                                                
                       PERFORM 8007-READ-BP13K813    THRU 8007-EXIT             
                                                                                
                       IF WS-NUM-MATURE-EST-TAG NOT = 'Y'                       
                          PERFORM 5200-OUTPUT-DETAIL THRU 5200-EXIT             
                       END-IF                                                   
                    ELSE                                                        
                       PERFORM 5200-OUTPUT-DETAIL    THRU 5200-EXIT             
                    END-IF                                                      
                 END-IF                                                         
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F595            THRU 2000-EXIT.                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       4100-START-BP13K500.                                                     
      **************************************************************            
                                                                                
           MOVE SPACES                             TO BP13K500-REC.             
           INITIALIZE                                 BP13K500-REC.             
           MOVE 'N'                                TO WS-EOF-K500.              
                                                                                
           MOVE F595-NUM-NRIC1                     TO K500-NUM-NRIC1            
                                                                                
           START BP13K500 KEY >= K500-NUM-NRIC1                                 
                                                                                
           EVALUATE K500-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'Y'               TO WS-EOF-K500                       
               WHEN OTHER                                                       
                    DISPLAY 'ERROR START BP13K500 , STATUS = '                  
                            K500-STATUS                                         
                    GO TO 9000-CLOSE-ROUTINE                                    
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       4200-READ-NEXT-BP13K500.                                                 
      **************************************************************            
                                                                                
           READ BP13K500 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                   TO WS-EOF-K500                       
           END-READ.                                                            
                                                                                
           EVALUATE K500-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K500-NUM-NRIC1 NOT = F595-NUM-NRIC1                      
                       MOVE 'Y'            TO WS-EOF-K500                       
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'Y'               TO WS-EOF-K500                       
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING NEXT BP13K500 , STATUS = '           
                             K500-STATUS                                        
                    GO TO 9000-CLOSE-ROUTINE                                    
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       5000-OUTPUT-HEADER.                                                      
      **************************************************************            
                                                                                
           ADD 1                           TO CNT-LB83-PAGE                     
           MOVE CNT-LB83-PAGE              TO LB83-PAGE.                        
                                                                                
           MOVE SPACES                     TO BP13LB83-REC                      
           WRITE BP13LB83-REC AFTER PAGE.                                       
           WRITE BP13LB83-REC FROM LB83-TITLE01 AFTER 3.                        
           WRITE BP13LB83-REC FROM LB83-TITLE02.                                
           WRITE BP13LB83-REC FROM LB83-TITLE03 AFTER 2.                        
           WRITE BP13LB83-REC FROM LB83-TITLE04.                                
                                                                                
           MOVE 8                          TO CNT-LB83-PAGE-REC.                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       5100-OUTPUT-SUMMARY.                                                     
      **************************************************************            
                                                                                
           ADD 1                           TO CNT-LB83-PAGE-REC.                
           IF CNT-LB83-PAGE-REC > 60                                            
              PERFORM 5000-OUTPUT-HEADER THRU 5000-EXIT                         
              ADD 1                        TO CNT-LB83-PAGE-REC                 
           END-IF.                                                              
                                                                                
           INITIALIZE                         LB83-SUMMARY.                     
           MOVE 0                          TO CNT-LB83-DTL-SNO.                 
           ADD 1                           TO CNT-LB83-SUM-SNO.                 
           MOVE CNT-LB83-SUM-SNO           TO LB83-SUM-SNO.                     
           MOVE F595-NUM-REGN              TO LB83-SUM-REGN.                    
           MOVE F595-NUM-NRIC1             TO LB83-SUM-NRIC1.                   
                                                                                
           INITIALIZE BP13K813-REC                                              
                                                                                
           MOVE F595-CDE-NT1               TO K813-NUM-ZONE                     
                                              WS-NUM-ZONE.                      
      *    MOVE F595-NUM-FLAT-TYPE         TO K813-NUM-FLAT-TYPE.               
      *    MOVE F595-DTE-BALLOT            TO K813-DTE-BALLOT.                  
                                                                                
           PERFORM 8007-READ-BP13K813 THRU 8007-EXIT                            
           PERFORM 6800-BROWSE-BP13K828  THRU 6800-EXIT.                        
           IF K813-CDE-NT NOT = SPACES                                          
              MOVE K813-CDE-NT             TO LB83-SUM-NT                       
           ELSE                                                                 
              MOVE F595-CDE-NT1            TO LB83-SUM-NT                       
           END-IF.                                                              
           MOVE F595-NUM-FLAT-TYPE         TO LB83-SUM-FT.                      
           STRING F595-DTE-BALLOT(1:4) '-' F595-DTE-BALLOT(5:2)                 
                  DELIMITED BY SIZE INTO LB83-SUM-BALLOT.                       
           MOVE F595-ALLO-CAT              TO LB83-SUM-ALLO-CAT.                
           MOVE F595-NUM-ALLOC-TAG         TO LB83-SUM-ALLOC-TAG.               
           MOVE F595-NUM-BK-TAG            TO LB83-SUM-BK-TAG.                  
           MOVE F595-NUM-BE-CNT            TO LB83-SUM-BE-CNT.                  
           MOVE F595-NUM-BTO-CNT           TO LB83-SUM-BTO-CNT.                 
           MOVE F595-NUM-BK-CNT            TO LB83-SUM-TOT-CNT.                 
           MOVE F595-NUM-BOOK-STATUS       TO LB83-SUM-BK-STATUS.               
           MOVE F595-NUM-SC                TO LB83-SUM-SC.                      
           IF NOT-PURE-BTO                                                      
              MOVE SPACE                   TO LB83-SUM-PURE-BTO                 
              MOVE 'Y'                     TO LB83-SUM-MIXED-MODE               
           ELSE                                                                 
              MOVE 'Y'                     TO LB83-SUM-PURE-BTO                 
              MOVE SPACE                   TO LB83-SUM-MIXED-MODE               
           END-IF.                                                              
                                                                                
           WRITE BP13LB83-REC FROM LB83-SUMMARY.                                
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       5200-OUTPUT-DETAIL.                                                      
      **************************************************************            
                                                                                
           ADD 1                           TO CNT-LB83-PAGE-REC                 
           IF CNT-LB83-PAGE-REC > 60                                            
              PERFORM 5000-OUTPUT-HEADER THRU 5000-EXIT                         
              ADD 1                        TO CNT-LB83-PAGE-REC                 
           END-IF.                                                              
                                                                                
           INITIALIZE                         LB83-DETAIL.                      
           ADD 1                           TO CNT-LB83-DTL-SNO                  
           MOVE CNT-LB83-DTL-SNO           TO LB83-DTL-SNO.                     
           MOVE K500-NUM-REGN              TO LB83-DTL-REGN                     
           MOVE K500-NUM-NRIC1             TO LB83-DTL-NRIC1                    
                                                                                
           IF WS-K813-CDE-NT NOT = SPACES                                       
              MOVE WS-K813-CDE-NT          TO LB83-DTL-NT                       
           ELSE                                                                 
              MOVE K500-CDE-NT1            TO LB83-DTL-NT                       
           END-IF                                                               
                                                                                
           MOVE K500-CDE-FLAT-TYPE         TO LB83-DTL-FT                       
           STRING K500-DTE-BALLOT(1:4) '-' K500-DTE-BALLOT(5:2)                 
                  DELIMITED BY SIZE INTO LB83-DTL-BALLOT.                       
           MOVE K500-NUM-ALLO-CAT          TO LB83-DTL-ALLO-CAT                 
           MOVE K500-NUM-BOOK-STATUS       TO LB83-DTL-BK-STATUS.               
                                                                                
           WRITE BP13LB83-REC FROM LB83-DETAIL.                                 
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       6000-CHECK-PURE-BTO.                                                     
      **************************************************************            
                                                                                
           MOVE 'N'                                  TO WS-NOT-PURE-BTO.        
                                                                                
      ** START BROWSE BY NRIC1                                                  
           PERFORM 6100-START-BP13K595-NRIC1       THRU 6100-EXIT.              
                                                                                
           IF NOT K595-EOF                                                      
              PERFORM UNTIL K595-EOF OR NOT-PURE-BTO                            
                 PERFORM 6500-READ-NEXT-BP13K595   THRU 6500-EXIT               
                 IF NOT K595-EOF                                                
                    IF K595-NUM-NRIC1 = F595-NUM-NRIC1                          
                       PERFORM 6600-CHECK-ALLO-CAT THRU 6600-EXIT               
                    ELSE                                                        
                       MOVE 'Y'                      TO WS-EOF-K595             
                    END-IF                                                      
                 END-IF                                                         
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
      ** START BROWSE BY NRIC2                                                  
           PERFORM 6200-START-BP13K595-NRIC2       THRU 6200-EXIT.              
                                                                                
           IF NOT K595-EOF                                                      
              PERFORM UNTIL K595-EOF OR NOT-PURE-BTO                            
                 PERFORM 6500-READ-NEXT-BP13K595   THRU 6500-EXIT               
                 IF NOT K595-EOF                                                
                    IF K595-NUM-NRIC2 = F595-NUM-NRIC1                          
                       PERFORM 6600-CHECK-ALLO-CAT THRU 6600-EXIT               
                    ELSE                                                        
                       MOVE 'Y'                      TO WS-EOF-K595             
                    END-IF                                                      
                 END-IF                                                         
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
      ** START BROWSE BY NRIC3                                                  
           PERFORM 6300-START-BP13K595-NRIC3       THRU 6300-EXIT.              
                                                                                
           IF NOT K595-EOF                                                      
              PERFORM UNTIL K595-EOF OR NOT-PURE-BTO                            
                 PERFORM 6500-READ-NEXT-BP13K595   THRU 6500-EXIT               
                 IF NOT K595-EOF                                                
                    IF K595-NUM-NRIC3 = F595-NUM-NRIC1                          
                       PERFORM 6600-CHECK-ALLO-CAT THRU 6600-EXIT               
                    ELSE                                                        
                       MOVE 'Y'                      TO WS-EOF-K595             
                    END-IF                                                      
                 END-IF                                                         
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
      ** START BROWSE BY NRIC4                                                  
           PERFORM 6400-START-BP13K595-NRIC4       THRU 6400-EXIT.              
                                                                                
           IF NOT K595-EOF                                                      
              PERFORM UNTIL K595-EOF OR NOT-PURE-BTO                            
                 PERFORM 6500-READ-NEXT-BP13K595   THRU 6500-EXIT               
                 IF NOT K595-EOF                                                
                    IF K595-NUM-NRIC4 = F595-NUM-NRIC1                          
                       PERFORM 6600-CHECK-ALLO-CAT THRU 6600-EXIT               
                    ELSE                                                        
                       MOVE 'Y'                      TO WS-EOF-K595             
                    END-IF                                                      
                 END-IF                                                         
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       6100-START-BP13K595-NRIC1.                                               
      **************************************************************            
                                                                                
           MOVE SPACES                             TO BP13K595-REC.             
           INITIALIZE                                 BP13K595-REC.             
           MOVE 'N'                                TO WS-EOF-K595.              
                                                                                
           MOVE F595-NUM-NRIC1                     TO K595-NUM-NRIC1            
                                                                                
           START BP13K595 KEY >= K595-NUM-NRIC1                                 
                                                                                
           EVALUATE K595-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'Y'               TO WS-EOF-K595                       
               WHEN OTHER                                                       
                    DISPLAY 'ERROR START BP13K595 , STATUS = '                  
                            K595-STATUS                                         
                    GO TO 9000-CLOSE-ROUTINE                                    
           END-EVALUATE.                                                        
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       6200-START-BP13K595-NRIC2.                                               
      **************************************************************            
                                                                                
           MOVE SPACES                             TO BP13K595-REC.             
           INITIALIZE                                 BP13K595-REC.             
           MOVE 'N'                                TO WS-EOF-K595.              
                                                                                
           MOVE F595-NUM-NRIC1                     TO K595-NUM-NRIC2            
                                                                                
           START BP13K595 KEY >= K595-NUM-NRIC2                                 
                                                                                
           EVALUATE K595-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'Y'               TO WS-EOF-K595                       
               WHEN OTHER                                                       
                    DISPLAY 'ERROR START BP13K595 , STATUS = '                  
                            K595-STATUS                                         
                    GO TO 9000-CLOSE-ROUTINE                                    
           END-EVALUATE.                                                        
                                                                                
       6200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       6300-START-BP13K595-NRIC3.                                               
      **************************************************************            
                                                                                
           MOVE SPACES                             TO BP13K595-REC.             
           INITIALIZE                                 BP13K595-REC.             
           MOVE 'N'                                TO WS-EOF-K595.              
                                                                                
           MOVE F595-NUM-NRIC1                     TO K595-NUM-NRIC3            
                                                                                
           START BP13K595 KEY >= K595-NUM-NRIC3                                 
                                                                                
           EVALUATE K595-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'Y'               TO WS-EOF-K595                       
               WHEN OTHER                                                       
                    DISPLAY 'ERROR START BP13K595 , STATUS = '                  
                            K595-STATUS                                         
                    GO TO 9000-CLOSE-ROUTINE                                    
           END-EVALUATE.                                                        
                                                                                
       6300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       6400-START-BP13K595-NRIC4.                                               
      **************************************************************            
                                                                                
           MOVE SPACES                             TO BP13K595-REC.             
           INITIALIZE                                 BP13K595-REC.             
           MOVE 'N'                                TO WS-EOF-K595.              
                                                                                
           MOVE F595-NUM-NRIC1                     TO K595-NUM-NRIC4            
                                                                                
           START BP13K595 KEY >= K595-NUM-NRIC4                                 
                                                                                
           EVALUATE K595-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'Y'               TO WS-EOF-K595                       
               WHEN OTHER                                                       
                    DISPLAY 'ERROR START BP13K595 , STATUS = '                  
                            K595-STATUS                                         
                    GO TO 9000-CLOSE-ROUTINE                                    
           END-EVALUATE.                                                        
                                                                                
       6400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       6500-READ-NEXT-BP13K595.                                                 
      **************************************************************            
                                                                                
           READ BP13K595 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                   TO WS-EOF-K595                       
           END-READ.                                                            
                                                                                
           EVALUATE K595-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    CONTINUE                                                    
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'Y'               TO WS-EOF-K595                       
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING NEXT BP13K595 , STATUS = '           
                             K595-STATUS                                        
                    GO TO 9000-CLOSE-ROUTINE                                    
           END-EVALUATE.                                                        
                                                                                
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       6600-CHECK-ALLO-CAT.                                                     
      **************************************************************            
                                                                                
           IF K595-DTE-BALLOT > '200703'                                        
              IF K595-NUM-ALLO-CAT NOT = 'BTO'                                  
                 MOVE 'Y'                 TO WS-NOT-PURE-BTO                    
              ELSE                                                              
                 INITIALIZE BP13K813-REC                                        
                                                                                
                 MOVE K595-CDE-NT1              TO K813-NUM-ZONE                
                                                   WS-NUM-ZONE                  
      *          MOVE K595-CDE-FLAT-TYPE        TO K813-NUM-FLAT-TYPE           
      *          MOVE K595-DTE-BALLOT           TO K813-DTE-BALLOT              
                                                                                
                 PERFORM 8007-READ-BP13K813 THRU 8007-EXIT                      
                 IF K813-NUM-MATURE-EST-TAG = 'Y'                               
                    MOVE 'Y'              TO WS-NOT-PURE-BTO                    
                 END-IF                                                         
                 IF K813-CDE-NT NOT = SPACES                                    
                    MOVE K813-CDE-NT             TO LB83-DTL-NT                 
                                                    LB83-SUM-NT                 
                 ELSE                                                           
                    MOVE K595-CDE-NT1            TO LB83-DTL-NT                 
                                                    LB83-SUM-NT                 
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       6600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       6700-READ-BP13K813.                                                      
      **************************************************************            
                                                                                
           READ BP13K813.                                                       
                                                                                
           IF K813-STATUS = 00                                                  
              MOVE K813-NUM-MATURE-EST-TAG TO WS-NUM-MATURE-EST-TAG             
              MOVE K813-CDE-NT             TO WS-K813-CDE-NT                    
           ELSE                                                                 
              MOVE SPACES                  TO K813-CDE-NT                       
                                              K813-NUM-MATURE-EST-TAG           
              DISPLAY 'BP13K813 - ERROR READING, STATUS=' K813-STATUS           
           END-IF.                                                              
                                                                                
       6700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6800-BROWSE-BP13K828.                                                    
      ******************************************************************        
                                                                                
           MOVE SPACES                     TO BP13K828-REC.                     
           INITIALIZE                         BP13K828-REC.                     
                                                                                
           MOVE 'N'                        TO WS-EOF-K828.                      
           MOVE ZEROES                     TO WS-NUM-INCOME.                    
                                                                                
           MOVE F595-NUM-REGN              TO K828-NUM-REGN.                    
                                                                                
           START BP13K828 KEY >= K828-NUM-REGN.                                 
                                                                                
           EVALUATE K828-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 6810-READNEXT-BP13K828 THRU 6810-EXIT               
                    PERFORM 6820-PROCESS-BP13K828  THRU 6820-EXIT               
                            UNTIL WS-EOF-K828 = 'Y'                             
                                                                                
                    MOVE WS-NUM-INCOME             TO LB83-SUM-INCOME           
               WHEN 20                                                          
               WHEN 23                                                          
                    MOVE ZEROES                    TO LB83-SUM-INCOME           
                    MOVE SPACES                    TO LB83-SUM-NAME1            
               WHEN OTHER                                                       
                  DISPLAY 'START ERROR BP13K828 : ' K828-STATUS                 
                          ' REGN : ' K828-NUM-REGN                              
           END-EVALUATE.                                                        
                                                                                
       6800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6810-READNEXT-BP13K828.                                                  
      ******************************************************************        
                                                                                
           READ BP13K828 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-EOF-K828.                        
                                                                                
       6810-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6820-PROCESS-BP13K828.                                                   
      ******************************************************************        
                                                                                
           IF F595-NUM-REGN = K828-NUM-REGN                                     
              IF F595-NUM-NRIC1 = K828-NUM-NRIC                                 
                 MOVE K828-NME-OCCP                  TO LB83-SUM-NAME1          
              END-IF                                                            
                                                                                
              IF K828-AMT-INCOME IS NOT NUMERIC                                 
                 MOVE ZEROES                         TO K828-AMT-INCOME         
              END-IF                                                            
                                                                                
              ADD K828-AMT-INCOME                    TO WS-NUM-INCOME           
              PERFORM 6810-READNEXT-BP13K828       THRU 6810-EXIT               
           ELSE                                                                 
              MOVE 'Y'                               TO WS-EOF-K828             
           END-IF.                                                              
                                                                                
       6820-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       8007-READ-BP13K813.                                                      
      *===============================================================*         
                                                                                
           START BP13K813 KEY >= K813-KEY-FLD                                   
                                                                                
           IF K813-STATUS = 00 OR 02                                            
              READ BP13K813 NEXT RECORD                                         
              IF K813-NUM-ZONE = WS-NUM-ZONE                                    
                 MOVE K813-NUM-MATURE-EST-TAG TO WS-NUM-MATURE-EST-TAG          
                 MOVE K813-CDE-NT             TO WS-K813-CDE-NT                 
              ELSE                                                              
                 DISPLAY 'BP13K813 - NOT FND: ' K813-KEY-FLD                    
                 INITIALIZE BP13K813-REC                                        
              END-IF                                                            
           ELSE                                                                 
              IF K813-STATUS = 23                                               
                 DISPLAY 'BP13K813 - NOT FND: ' K813-KEY-FLD                    
              ELSE                                                              
                 DISPLAY 'BP13K813 - ERROR READING , '                          
                         ' STATUS : ' K813-STATUS                               
                         ' KEY : ' K813-KEY-FLD                                 
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8007-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **************************************************************            
       9000-CLOSE-ROUTINE.                                                      
      **************************************************************            
           DISPLAY ' '.                                                         
           DISPLAY ' ====  BP13CB83 CONTROL TOTALS  ======'.                    
           DISPLAY ' '.                                                         
           DISPLAY ' F595 RECORDS READ          : ' CNT-F595-READ.              
           DISPLAY ' '.                                                         
           DISPLAY ' ====================================='.                    
                                                                                
           CLOSE BP13F595                                                       
                 BP13K500                                                       
                 BP13K595                                                       
                 BP13K813                                                       
                 BP13K828                                                       
                 BP13LB83.                                                      
                                                                                
           IF K500-STATUS NOT = 00                                              
              DISPLAY 'CLOSING ERROR BP13K500-STAT ' K500-STATUS.               
           IF K595-STATUS NOT = 00                                              
              DISPLAY 'CLOSING ERROR BP13K595-STAT ' K595-STATUS.               
           IF K813-STATUS NOT = 00                                              
              DISPLAY 'CLOSING ERROR BP13K813-STAT ' K813-STATUS.               
           IF K828-STATUS NOT = 00                                              
              DISPLAY 'CLOSING ERROR BP13K828-STAT ' K828-STATUS.               
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
