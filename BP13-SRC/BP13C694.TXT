      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13C694.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  29/07/08.                                                 
      *****************************************************************         
      *OBJECTIVE -  TO SHOW ERROR REPORT FOR GROUP VOUCHER            *         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. AG07F235                                  *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP13     29/07/2008 JB8  NEW PROGRAM.                         *         
      * BP135832 29/06/2015 JGO1 CHANGE LENGTH OF F235 TO 700         *         
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  SY02F001  ASSIGN        TO SY02F001.                         
           SELECT  AG07F235  ASSIGN        TO AG07F235.                         
           SELECT  BP13L694 ASSIGN         TO BP13L694.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   AG07F235                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 700 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY AG07F235.                                                           
                                                                                
       FD   BP13L694                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   BP13L694-REC               PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-F001-EOF             PIC X(1)  VALUE 'N'.                     
               88 EOF-F001                       VALUE 'Y'.                     
           05  WS-EOF-F235             PIC X(1)  VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F235-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-L694-WRITE           PIC 9(7)  VALUE ZEROES.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-PAGE-CTR              PIC 9(04) VALUE 0.                       
           05 WS-SNO-CTR               PIC 9(04) VALUE 0.                       
           05 WS-CNT                   PIC 9(04) VALUE 0.                       
                                                                                
      *---> DETAIL LINES FOR REPORT                                             
       01  L694-HEADER01.                                                       
           05  WS-DOCID            PIC X(8)      VALUE 'BP13L694'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  L694-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L694-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  L694-HEADER02.                                                       
           05  FILLER              PIC X(44)     VALUE SPACES.                  
           05  FILLER              PIC X(40)     VALUE                          
               'REPORT ON GROUP VOUCHER REJECTED BY EWPC'.                      
                                                                                
       01  L694-HEADER03.                                                       
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'S/N'.                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE 'VOUCHER NO'.            
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'TYPE'.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE 'HDB REF '.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(40)     VALUE 'ERROR MESSAGE'.         
                                                                                
       01  L694-DETAIL01.                                                       
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L694-SNO            PIC ZZ9       VALUE ZEROES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  L694-VOUCHER        PIC X(11)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  L694-TYPE           PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  L694-HDB-REF        PIC X(11)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  L694-ERRMSG         PIC X(40)     VALUE SPACES.                  
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-AG07F235      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F235 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  AG07F235                                                 
                       SY02F001                                                 
              OUTPUT   BP13L694.                                                
                                                                                
           READ SY02F001 AT END MOVE 'Y'  TO WS-F001-EOF.                       
           IF EOF-F001                                                          
              DISPLAY 'RECORD NOT FOUND IN SY01F001 '                           
              DISPLAY 'PROCESSING TERMINATED '                                  
              GO TO 9999-CLOSE-ROUTINE                                          
           ELSE                                                                 
              MOVE F001-DTE-CURRENT      TO   WS-SYSTEM-DATE                    
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-AG07F235.                                                      
      ******************************************************************        
                                                                                
           READ AG07F235           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F235                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO   WS-F235-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF F235-NUM-REC-TYP = 00                                             
              IF F235-NUM-ACTION-TYP = 'RI' OR 'DV'                             
                 PERFORM 4000-WRITE-AG07LD47    THRU 4000-EXIT                  
              END-IF                                                            
           ELSE                                                                 
              IF F235-NUM-REC-TYP = 99                                          
                 IF F235-TXT-REJECT-TRL(1:1) NOT = SPACE AND LOW-VALUES         
                                                         AND ZERO               
                    PERFORM 4000-WRITE-AG07LD47 THRU 4000-EXIT                  
                 END-IF                                                         
              ELSE                                                              
                 IF F235-TXT-REJECT-DTL NOT = SPACES AND LOW-VALUES             
                    PERFORM 4000-WRITE-AG07LD47 THRU 4000-EXIT                  
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-AG07F235           THRU 2000-EXIT.                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-WRITE-AG07LD47.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                       TO L694-DETAIL01.                  
                                                                                
           ADD 1                             TO WS-SNO-CTR.                     
           ADD 1                             TO WS-CNT.                         
                                                                                
           MOVE WS-SNO-CTR                   TO L694-SNO.                       
                                                                                
           IF F235-NUM-REC-TYP = 00                                             
              MOVE F235-NUM-GRP-VR           TO L694-VOUCHER                    
              MOVE F235-NUM-REC-TYP          TO L694-TYPE                       
              MOVE F235-TXT-REJECT-HDR       TO L694-ERRMSG                     
           ELSE                                                                 
              IF F235-NUM-REC-TYP = 99                                          
                 MOVE F235-NUM-GRP-VR-R      TO L694-VOUCHER                    
                 MOVE F235-NUM-TRANS-SERIAL  TO L694-TYPE                       
                 MOVE F235-TXT-REJECT-TRL    TO L694-ERRMSG                     
              ELSE                                                              
                 MOVE F235-NUM-GRP-VR-T      TO L694-VOUCHER                    
                 MOVE F235-NUM-TRAILER-SERIAL TO L694-TYPE                      
                 MOVE F235-NUM-HDB-REF       TO L694-HDB-REF                    
                 MOVE F235-TXT-REJECT-DTL    TO L694-ERRMSG                     
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-CNT > 55 OR WS-PAGE-CTR = 0                                    
              PERFORM 5000-NEW-PAGE        THRU 5000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE L694-DETAIL01                TO BP13L694-REC.                   
           WRITE BP13L694-REC.                                                  
           ADD 1                             TO WS-L694-WRITE.                  
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-NEW-PAGE.                                                           
      ******************************************************************        
                                                                                
           ADD 1                             TO WS-PAGE-CTR.                    
                                                                                
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
              DELIMITED BY SIZE INTO L694-DATE-HDR.                             
           MOVE WS-PAGE-CTR                  TO L694-PAGE-HDR.                  
                                                                                
           WRITE BP13L694-REC           FROM L694-HEADER01 AFTER PAGE.          
           WRITE BP13L694-REC           FROM L694-HEADER02 AFTER 1.             
           WRITE BP13L694-REC           FROM L694-HEADER03 AFTER 3.             
                                                                                
           MOVE 0                            TO WS-CNT.                         
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C694'.                                     
           DISPLAY 'RUN DATE   : ' WS-SYSTEM-DATE(7:2)                          
                               '/' WS-SYSTEM-DATE(5:2)                          
                               '/' WS-SYSTEM-DATE(1:4)                          
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF AG07F235 RECORDS READ ............ : '            
                    WS-F235-READ.                                               
           DISPLAY '(2) NO OF AG07LD47 RECORDS WRITTEN ......... : '            
                    WS-L694-WRITE.                                              
                                                                                
           CLOSE SY02F001                                                       
                 AG07F235                                                       
                 BP13L694.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
