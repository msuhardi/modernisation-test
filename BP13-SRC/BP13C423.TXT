       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C423.                                                 
       AUTHOR.        K.P.V.SANTOS.                                             
      *DATE-WRITTEN.  04/08/93.                                                 
      *DATE-COMPILED.                                                           
      * ======================================================= *               
      *                                                         *               
      *    SYSTEM NAME : SYSTEM OF COMMITMENT(TAKE POSSESSION)  *               
      *    SYSTEM ID : BP13                                     *               
      *                                                         *               
      *    OBJECTIVE  :                                         *               
      *        1.  TO MATCH RECORDS IN BP13F410 FILE WITH       *               
      *            RECORDS IN APPLICANT MASTER (BP13K800) FILE  *               
      *            IF MATCHED  AND AMT-BK-PIAD = ZEROS          *               
      *            WRITE RECORDS FROM BP13K800 TO BP13K893.     *               
      *            DELETE RECORDS FROM BP13K800 FILE.           *               
      *                                                         *               
      *        2.  TO MATCH RECORDS IN BP13F410 FILE WITH       *               
      *            OCCUPIER(BP13K820) FILE.                     *               
      *            IF MATCHED :                                 *               
      *               WRITE RECORDS FROM BP13K820 TO BP13K895.  *               
      *               DELETE RECORDS FROM BP13K820.             *               
      *                                                         *               
      *        3.  WRITE RECORDS TO APPLICANT UPDATED(BP13F470) *               
      *            FILE TO PASS THE INFORMATION TO MAF SYSTEM.  *               
      *                                                         *               
      *        4.  UPDATE SOC APPLN HISTORY FILE.               *               
      *                                                         *               
      *        5.  PRINT OUT CONTROL LIST FOR SALES ORDER       *               
      *            WITH ORIG NUMBER AND CONTROL LIST FOR        *               
      *            RECORDS DELETED.                             *               
      *                                                         *               
      *     INPUT/OUTPUT FILES:                                 *               
      *           1. BP13F410 (FINAL SALES ORDER FILE)          *               
      *           2. BP13K800 (APPLICANTS MASTER)               *               
      *           3. BP13K893 (APPLICANTS HISTORY MASTER)       *               
      *           4. BP13K820 (OCCUPIER MASTER)                 *               
      *           5. BP13K830 (ADDRESS MASTER FILE)             *               
      *           6. BP13K840 (OCCUPIER HISTORY MASTER)         *               
      *           7. BP13F470 (OCCUPIER HISTORY MASTER)         *               
      *           8. BP13K895 (OCCUPIER HISTORY FILE)           *               
      *                                                         *               
      *     LISTING:                                            *               
      *           1. P13L423A (CONTROL LIST FOR CASES DELETED)  *               
      *                                                         *               
      *CHG-NO  BY    ON      DESCRIPTION                        *               
      *------  --    --      -----------                        *               
      *SOC-PH8 KPS   280893  TO EXPAND REGN & NRIC AS WELL AS   *               
      *                      SIMPLIFY THE PROGRAM               *               
      *AIR8288 LSB   110794  DELETE DISPLAY STATEMENT           *               
      *SOC-PH9 LSB   051094  REPLACE DTE-ISSUE-REGN WITH        *               
      *                      DTE-REGN                           *               
      *BP130418 MPA  110698  Y2000 CHANGES                      *               
      *BP130477 NOR  090798  TAKE OUT BP13F470 OUTPUT, MOVE TO  *               
      *                      DAILY JOB BP13C410                 *               
      *BP130418 CLT  280898  CATER FOR Y2K.                     *               
      *BP130519 SR   181198  CHK REGN-DEPOSIT-PD BF DELETE K800 *               
      *BP130534 LSB  090499  TO UPDATE 2ND LEVEL DATA/ALLOCA-   *               
      *                      TION SCHEME/FLAT TYPE              *               
      *BP130790 SCK  020200  TO ADD IN IN-SOURCE DOCUMENTATION  *               
      *BP132199 ANC  160802  MOVE SYSTEM DATE TO DTE UPDATE IN  *               
      *                      K800 & K820 BEFORE WRITING.        *               
      *BP132627 GAN1 161104  CHG F800/F820 TO K893/K895         *               
      *BP132722 JF4  250705  CATER FOR CD REFUND                *               
      *BP134485 RJE1 220312  CHANGE CANCEL DATE INTO CURRENT DTE*               
      *BP134872 ESD1 040913  REMOVE UPDATING OF CANCEL DATE TO  *               
      *                      CURRENT DATE.                      *               
      *BP136098 KSJ3 150216  ADD CHECK TO K800-AMT-SOC-DEBTOR   *               
      * ======================================================= *               
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410 ASSIGN TO BP13F410.                                  
                                                                                
           SELECT BP13K893 ASSIGN TO BP13K893                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE IS DYNAMIC                                          
                RECORD KEY IS K893-KEY-FLD                                      
                FILE STATUS IS K893-STATUS.                                     
                                                                                
           SELECT BP13K895 ASSIGN TO BP13K895                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE IS DYNAMIC                                          
                RECORD KEY IS K895-KEY-FLD                                      
                FILE STATUS IS K895-STATUS.                                     
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE IS DYNAMIC                                          
                RECORD KEY IS K800-NUM-REGN                                     
                FILE STATUS IS K800-STATUS.                                     
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE IS DYNAMIC                                          
                RECORD KEY IS K820-KEY-FLD                                      
                FILE STATUS IS K820-STATUS.                                     
                                                                                
           SELECT BP13K830 ASSIGN TO BP13K830                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE IS RANDOM                                           
                RECORD KEY IS K830-KEY-FLD                                      
                ALTERNATE RECORD KEY IS K830-NUM-REGN                           
                FILE STATUS IS K830-STATUS.                                     
                                                                                
           SELECT BP13K840 ASSIGN TO BP13K840                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE IS RANDOM                                           
                RECORD KEY IS K840-KEY-FLD                                      
                FILE STATUS IS K840-STATUS.                                     
                                                                                
           SELECT P13L423A ASSIGN TO P13L423A.                                  
                                                                                
           SELECT P13L423B ASSIGN TO P13L423B.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F410           BLOCK CONTAINS 0 RECORDS                          
                              RECORD CONTAINS 2000 CHARACTERS                   
                              LABEL RECORDS ARE STANDARD                        
                              RECORDING MODE IS F.                              
       COPY BP13F410.                                                           
                                                                                
       FD  BP13K893           RECORD CONTAINS 2050 CHARACTERS.                  
       COPY BP13K893.                                                           
                                                                                
       FD  BP13K895           RECORD CONTAINS 450 CHARACTERS.                   
       COPY BP13K895.                                                           
                                                                                
       FD  BP13K800           RECORD CONTAINS 2000 CHARACTERS.                  
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820           RECORD CONTAINS 400 CHARACTERS.                   
       COPY BP13K820.                                                           
                                                                                
       FD  BP13K830           RECORD CONTAINS 250 CHARACTERS.                   
       COPY BP13K830.                                                           
                                                                                
       FD  BP13K840           RECORD CONTAINS 500 CHARACTERS.                   
       COPY BP13K840.                                                           
                                                                                
       FD  P13L423A                                                             
                              RECORD CONTAINS 133 CHARACTERS                    
                              LABEL RECORDS ARE OMITTED                         
                              RECORDING MODE IS F.                              
       01  PRT-LINE1          PIC X(133).                                       
                                                                                
       FD  P13L423B                                                             
                              RECORD CONTAINS 133 CHARACTERS                    
                              LABEL RECORDS ARE OMITTED                         
                              RECORDING MODE IS F.                              
       01  PRT-REC            PIC X(133).                                       
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-DTE-UPDATE            PIC X(8).                                   
                                                                                
       01  WS-MARITAL-STATUS.                                                   
           05  WS-MARITAL-STATUS1   PIC X.                                      
           05  WS-MARITAL-STATUS2   PIC X.                                      
                                                                                
       01  WS-ADDR-TYPE             PIC X       VALUE SPACE.                    
       01  WS-STREET-CODE           PIC X(6)    VALUE SPACES.                   
       01  WS-K830-NME-STREET       PIC X(20)   VALUE SPACES.                   
       01  WS-NUM-NEW-TOWN          PIC X(3)    VALUE SPACES.                   
       01  WS-NUM-FLAT-TYPE         PIC X(2)    VALUE SPACES.                   
       01  WS-NUM-ALLOC-SCH         PIC X(3)    VALUE SPACES.                   
       01  WS-NUM-IDP-SCH           PIC X(3)    VALUE SPACES.                   
       01  WS-NUM-SCH-ACC           PIC X(11)   VALUE SPACES.                   
       01  WS-DTE-REGN              PIC X(8)    VALUE SPACES.                   
       01  WS-AMT-BK-PAID           PIC S9(4)V99 VALUE +0.                      
       01  WS-SOC-AMOUNT            PIC S9(8)V99 VALUE +0.                      
                                                                                
       01  WS-NRIC.                                                             
           05  WS-NRIC1             PIC X(9)  VALUE SPACES.                     
           05  WS-NRIC2             PIC X(9)  VALUE SPACES.                     
           05  WS-NRIC3             PIC X(9)  VALUE SPACES.                     
           05  WS-NRIC4             PIC X(9)  VALUE SPACES.                     
                                                                                
       01  WS-NAMES.                                                            
           05  WS-NAME1             PIC X(66) VALUE SPACES.                     
           05  WS-NAME2             PIC X(66) VALUE SPACES.                     
           05  WS-NAME3             PIC X(66) VALUE SPACES.                     
           05  WS-NAME4             PIC X(66) VALUE SPACES.                     
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  K800-STATUS          PIC 9(2)  VALUE ZEROS.                      
           05  K893-STATUS          PIC 9(2)  VALUE ZEROS.                      
           05  K820-STATUS          PIC 9(2)  VALUE ZEROS.                      
           05  K895-STATUS          PIC 9(2)  VALUE ZEROS.                      
           05  K830-STATUS          PIC 9(2)  VALUE ZEROS.                      
           05  K840-STATUS          PIC 9(2)  VALUE ZEROS.                      
                                                                                
       01  WS-PROGRAM-SWITCH.                                                   
           05  WS-EOF-F410          PIC X     VALUE 'N'.                        
           05  WS-EOF-K820          PIC X     VALUE 'N'.                        
                                                                                
       01  WS-COUNTS.                                                           
           05  WS-F410-READ-CNT     PIC 9(6)  VALUE ZEROS.                      
           05  WS-K800-READ-CNT     PIC 9(6)  VALUE ZEROS.                      
           05  WS-BK-PAID-CNT       PIC 9(6)  VALUE ZEROS.                      
           05  WS-K893-WRITE-CNT    PIC 9(6)  VALUE ZEROS.                      
           05  WS-K800-DELETE-CNT   PIC 9(6)  VALUE ZEROS.                      
           05  WS-K820-READ-CNT     PIC 9(6)  VALUE ZEROS.                      
           05  WS-K895-WRITE-CNT    PIC 9(6)  VALUE ZEROS.                      
           05  WS-K820-DELETE-CNT   PIC 9(6)  VALUE ZEROS.                      
           05  WS-K830-READ-CNT     PIC 9(6)  VALUE ZEROS.                      
           05  WS-K840-UPDATE-CNT   PIC 9(6)  VALUE ZEROS.                      
           05  WS-K840-WRITE-CNT    PIC 9(6)  VALUE ZEROS.                      
           05  WS-PAGE-CNT1         PIC 9(4)  VALUE ZEROS.                      
           05  WS-LINE-CNT1         PIC 9(2)  VALUE ZEROS.                      
           05  WS-CNT-NOTFND        PIC 9(6)  VALUE ZEROS.                      
           05  WS-OCC-REGN          PIC 9(6)  VALUE ZEROS.                      
           05  WS-PREV-REGN         PIC X(8)  VALUE SPACES.                     
           05  WS-L423B-WRITE-CTR   PIC 9(4)  VALUE ZEROS.                      
           05  WS-PAGE-CTR          PIC 9(4)  VALUE ZEROS.                      
           05  WS-LINE-CTR          PIC 9(2)  VALUE ZEROS.                      
                                                                                
      * ====================================================== *                
      *  LISTING LAYOUT                                                         
      * ====================================================== *                
       01  L423-HDG1.                                                           
           05 FILLER                PIC X(4) VALUE SPACES.                      
           05 FILLER                PIC X(13) VALUE 'LISTING ID :'.             
           05 LIST-ID               PIC X(12).                                  
           05 FILLER                PIC X(20) VALUE SPACES.                     
           05 FILLER                PIC X(61) VALUE                             
              'S Y S T E M   O F   C O M M I T M E N T'.                        
           05 FILLER                PIC X(11) VALUE 'RUN DATE :'.               
           05 RUN-DATE              PIC X(10).                                  
                                                                                
       01  L423-HDG3.                                                           
           05  FILLER               PIC X(15) VALUE SPACES.                     
           05  FILLER               PIC X(28) VALUE SPACES.                     
           05  FILLER               PIC X(62) VALUE                             
               'UPDATE OF SALES ORDER TO RDB, DELETE SOC FILES'.                
           05  FILLER               PIC X(7)  VALUE 'PAGE : '.                  
           05  PAGE-NO              PIC ZZ9.                                    
           05  FILLER               PIC X(9)  VALUE SPACES.                     
                                                                                
       01  SUMMARY1.                                                            
           05  FILLER               PIC X(40) VALUE SPACES.                     
           05  FILLER               PIC X(58) VALUE                             
           '1 ) TOTAL BP13F410 RECORDS READ (SALES ORDER REC)      = '.         
           05  TOTAL-F410-READ      PIC ZZZZZ9.                                 
           05  FILLER               PIC X(34) VALUE SPACES.                     
                                                                                
       01  SUMMARY2.                                                            
           05  FILLER               PIC X(40) VALUE SPACES.                     
           05  FILLER               PIC X(58) VALUE                             
           '2 ) TOTAL BP13K800 RECORDS READ (SOC MASTER REC)       = '.         
           05  TOTAL-K800-READ      PIC ZZZZZ9.                                 
           05  FILLER               PIC X(34) VALUE SPACES.                     
                                                                                
       01  SUMMARY2A.                                                           
           05  FILLER               PIC X(40) VALUE SPACES.                     
           05  FILLER               PIC X(58) VALUE                             
           '3 ) TOTAL SOC REC WITH BOOKING FEE PAID = ZEROS        = '.         
           05  TOTAL-BK-PAID        PIC ZZZZZ9.                                 
           05  FILLER               PIC X(34) VALUE SPACES.                     
                                                                                
       01  SUMMARY3.                                                            
           05  FILLER               PIC X(40) VALUE SPACES.                     
           05  FILLER               PIC X(58) VALUE                             
           '4 ) TOTAL BP13K893 REC WRITTEN (SOC MASTER KEY ISSUED) = '.         
           05  TOTAL-K893-WRITE     PIC ZZZZZ9.                                 
           05  FILLER               PIC X(34) VALUE SPACES.                     
                                                                                
       01  SUMMARY4.                                                            
           05  FILLER               PIC X(40) VALUE SPACES.                     
           05  FILLER               PIC X(58) VALUE                             
           '5 ) TOTAL BP13K800 RECORDS DELETED (SOC MASTER REC)    = '.         
           05  TOTAL-K800-DELETE    PIC ZZZZZ9.                                 
           05  FILLER               PIC X(34) VALUE SPACES.                     
                                                                                
       01  SUMMARY5.                                                            
           05  FILLER               PIC X(40) VALUE SPACES.                     
           05  FILLER               PIC X(58) VALUE                             
           '6 ) TOTAL BP13K820 RECORDS READ (SOC OCCUPIER)         = '.         
           05  TOTAL-K820-READ      PIC ZZZZZ9.                                 
           05  FILLER               PIC X(34) VALUE SPACES.                     
                                                                                
       01  SUMMARY5A.                                                           
           05  FILLER               PIC X(40) VALUE SPACES.                     
           05  FILLER               PIC X(58) VALUE                             
           '7 ) TOTAL REGN-NO CASES IN BP13K820 (SOC OCCUPIER)     = '.         
           05  TOTAL-K820-REGN      PIC ZZZZZ9.                                 
           05  FILLER               PIC X(34) VALUE SPACES.                     
                                                                                
       01  SUMMARY6.                                                            
           05  FILLER               PIC X(40) VALUE SPACES.                     
           05  FILLER               PIC X(58) VALUE                             
           '8 ) TOTAL BP13K895 REC WRITTEN (SOC OCCUP KEY ISSUED)  = '.         
           05  TOTAL-K895-WRITE     PIC ZZZZZ9.                                 
           05  FILLER               PIC X(34) VALUE SPACES.                     
                                                                                
       01  SUMMARY7.                                                            
           05  FILLER               PIC X(40) VALUE SPACES.                     
           05  FILLER               PIC X(58) VALUE                             
           '9 ) TOTAL BP13K820 RECORDS DELETED (SOC OCCUPIER)      = '.         
           05  TOTAL-K820-DELETE    PIC ZZZZZ9.                                 
           05  FILLER               PIC X(34) VALUE SPACES.                     
                                                                                
       01  SUMMARY9.                                                            
           05  FILLER               PIC X(40) VALUE SPACES.                     
           05  FILLER               PIC X(58) VALUE                             
           '10) TOTAL BP13K830 RECORDS READ (SOC CORR ADDRESS)     = '.         
           05  TOTAL-K830-READ      PIC ZZZZZ9.                                 
           05  FILLER               PIC X(34) VALUE SPACES.                     
                                                                                
       01  SUMMARY10.                                                           
           05  FILLER               PIC X(40) VALUE SPACES.                     
           05  FILLER               PIC X(58) VALUE                             
           '11) TOTAL BP13K840 RECORDS UPDATED (SOC APPLN HISTORY) = '.         
           05  TOTAL-K840-UPDATE    PIC ZZZZZ9.                                 
           05  FILLER               PIC X(34) VALUE SPACES.                     
                                                                                
       01  SUMMARY11.                                                           
           05  FILLER               PIC X(40) VALUE SPACES.                     
           05  FILLER               PIC X(58) VALUE                             
           '12) TOTAL BP13K840 RECORDS CREATED (SOC APPLN HISTORY) = '.         
           05  TOTAL-K840-WRITE     PIC ZZZZZ9.                                 
           05  FILLER               PIC X(34) VALUE SPACES.                     
                                                                                
       01  SUMMARY12.                                                           
           05  FILLER               PIC X(40) VALUE SPACES.                     
           05  FILLER               PIC X(58) VALUE                             
           '13) TOTAL NO OCCUPIER RECORD NOT FOUND IN STREET TABLE = '.         
           05  TOTAL-ST-NOT-FND-CNT PIC ZZZZZ9.                                 
           05  FILLER               PIC X(34) VALUE SPACES.                     
                                                                                
       01  SUMMARY13.                                                           
           05  FILLER               PIC X(40) VALUE SPACES.                     
           05  FILLER               PIC X(58) VALUE                             
           'PLEASE CHECK : 1) = 2) = 7) = 11)+12)                   '.          
                                                                                
       01  SUMMARY14.                                                           
           05  FILLER               PIC X(40) VALUE SPACES.                     
           05  FILLER               PIC X(58) VALUE                             
           '               3) = 4) = 5                               '.         
                                                                                
       01  SUMMARY15.                                                           
           05  FILLER               PIC X(40) VALUE SPACES.                     
           05  FILLER               PIC X(58) VALUE                             
           '               6) = 10)                                  '.         
                                                                                
       01  SUMMARY16.                                                           
           05  FILLER               PIC X(40) VALUE SPACES.                     
           05  FILLER               PIC X(58) VALUE                             
           '               8) = 9)                                   '.         
                                                                                
       01  L423B-HDG2.                                                          
           05  FILLER               PIC X(37) VALUE SPACES.                     
           05  FILLER               PIC X(59) VALUE 'REPORT ON REGISTRAT        
      -        'ION DEPOSIT NOT REFUNDED UPON KEY ISSUED'.                      
           05  FILLER               PIC X(18) VALUE SPACES.                     
           05  FILLER               PIC X(7)  VALUE 'PAGE : '.                  
           05  PAGE-NUM             PIC ZZZ9.                                   
           05  FILLER               PIC X(8)  VALUE SPACES.                     
                                                                                
       01  L423B-LINE               PIC X(133) VALUE ALL '_'.                   
                                                                                
       01  L423B-HDG3.                                                          
           05  FILLER               PIC X(5)  VALUE SPACES.                     
           05  FILLER               PIC X(4)  VALUE 'S/N'.                      
           05  FILLER               PIC X(5)  VALUE SPACES.                     
           05  FILLER               PIC X(8)  VALUE 'REGN-NO'.                  
           05  FILLER               PIC X(5)  VALUE SPACES.                     
           05  FILLER               PIC X(12) VALUE 'ALLOC-SCHEME'.             
           05  FILLER               PIC X(5)  VALUE SPACES.                     
           05  FILLER               PIC X(7)  VALUE 'AMT-BK'.                   
           05  FILLER               PIC X(4)  VALUE SPACES.                     
           05  FILLER               PIC X(12) VALUE 'DEPOSIT-CASH'.             
           05  FILLER               PIC X(5)  VALUE SPACES.                     
           05  FILLER               PIC X(10) VALUE 'AMT-CPF1'.                 
           05  FILLER               PIC X(5)  VALUE SPACES.                     
           05  FILLER               PIC X(10) VALUE 'AMT-CPF2'.                 
           05  FILLER               PIC X(5)  VALUE SPACES.                     
           05  FILLER               PIC X(10) VALUE 'AMT-CPF3'.                 
           05  FILLER               PIC X(5)  VALUE SPACES.                     
           05  FILLER               PIC X(10) VALUE 'AMT-CPF4'.                 
                                                                                
       01 L423B-DETAIL-LINE.                                                    
          05 FILLER                 PIC X(5)  VALUE SPACES.                     
          05 L423B-SNO              PIC ZZZ9.                                   
          05 FILLER                 PIC X(5)  VALUE SPACES.                     
          05 L423B-REGNO            PIC X(8).                                   
          05 FILLER                 PIC X(5)  VALUE SPACES.                     
          05 FILLER                 PIC X(5)  VALUE SPACES.                     
          05 L423B-ALLOSCH          PIC X(3).                                   
          05 FILLER                 PIC X(4)  VALUE SPACES.                     
          05 FILLER                 PIC X(5)  VALUE SPACES.                     
          05 L423B-AMTBK            PIC ZZZ9.99.                                
          05 FILLER                 PIC X(5)  VALUE SPACES.                     
          05 L423B-AMTDEPOS         PIC ZZZZZZ9.99.                             
          05 FILLER                 PIC X(5)  VALUE SPACES.                     
          05 L423B-CPF1             PIC ZZZZZZ9.99.                             
          05 FILLER                 PIC X(5)  VALUE SPACES.                     
          05 L423B-CPF2             PIC ZZZZZZ9.99.                             
          05 FILLER                 PIC X(5)  VALUE SPACES.                     
          05 L423B-CPF3             PIC ZZZZZZ9.99.                             
          05 FILLER                 PIC X(5)  VALUE SPACES.                     
          05 L423B-CPF4             PIC ZZZZZZ9.99.                             
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN.                                                               
                                                                                
           PERFORM 1000-START-PROCESS  THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F410  THRU 2000-EXIT.                          
           PERFORM 3000-UPDATE-PROCESS THRU 3000-EXIT                           
                   UNTIL WS-EOF-F410 = 'Y'.                                     
           PERFORM 4000-CONTROL-LIST   THRU 4000-EXIT.                          
           PERFORM 9999-END-PROCESS    THRU 9999-EXIT.                          
                                                                                
                                                                                
       1000-START-PROCESS.                                                      
                                                                                
           OPEN INPUT BP13F410                                                  
                      BP13K830                                                  
                I-O   BP13K800                                                  
                      BP13K820                                                  
                      BP13K840                                                  
                      BP13K893                                                  
                      BP13K895                                                  
               OUTPUT P13L423A                                                  
                      P13L423B.                                                 
                                                                                
           IF K800-STATUS NOT = 0 AND 97                                        
              DISPLAY 'OPENING ERROR K800-STATUS IS ' K800-STATUS               
              PERFORM 9999-END-PROCESS THRU 9999-EXIT.                          
                                                                                
           IF K820-STATUS NOT = 0 AND 97                                        
              DISPLAY 'OPENING ERROR K820-STATUS IS ' K820-STATUS               
              PERFORM 9999-END-PROCESS THRU 9999-EXIT.                          
                                                                                
           IF K893-STATUS NOT = 0 AND 97                                        
              DISPLAY 'OPENING ERROR K893-STATUS IS ' K893-STATUS               
              PERFORM 9999-END-PROCESS THRU 9999-EXIT.                          
                                                                                
           IF K895-STATUS NOT = 0 AND 97                                        
              DISPLAY 'OPENING ERROR K895-STATUS IS ' K895-STATUS               
              PERFORM 9999-END-PROCESS THRU 9999-EXIT.                          
                                                                                
           IF K830-STATUS NOT = 0 AND 97                                        
              DISPLAY 'OPENING ERROR K830-STATUS IS ' K830-STATUS               
              PERFORM 9999-END-PROCESS THRU 9999-EXIT.                          
                                                                                
           IF K840-STATUS NOT = 0 AND 97                                        
              DISPLAY 'OPENING ERROR K840-STATUS IS ' K840-STATUS               
              PERFORM 9999-END-PROCESS THRU 9999-EXIT.                          
                                                                                
           MOVE ZEROS               TO WS-COUNTS.                               
           MOVE SPACES              TO WS-PROGRAM-SWITCH.                       
           MOVE 60                  TO WS-LINE-CNT1.                            
                                                                                
           STRING FUNCTION CURRENT-DATE(7:2) '/'                                
                  FUNCTION CURRENT-DATE(5:2) '/'                                
                  FUNCTION CURRENT-DATE(1:4)                                    
           DELIMITED BY SIZE INTO RUN-DATE.                                     
                                                                                
                                                                                
           MOVE  FUNCTION CURRENT-DATE TO WS-DTE-UPDATE.                        
                                                                                
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
      *             INITIAL READING OF BP13F410 FILE                   *        
      ******************************************************************        
                                                                                
       2000-READ-BP13F410.                                                      
                                                                                
           READ BP13F410 AT END                                                 
                         MOVE 'Y' TO WS-EOF-F410                                
                         GO TO 2000-EXIT.                                       
                                                                                
           ADD 1 TO WS-F410-READ-CNT.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *                                                                         
      * DELETE SOC MASTER/OCCUPIER ONLY IF AMT-BK-PAID = ZEROS                  
      * OUTPUT OCCUPIER REC FOR ALL SALES ORDER CASES                           
      * UPDATE KEY-ISSUE-DATE TO SOC HISTORY FILE BUT UPDATE CLOSE-DATE         
      * TO CASES ONLY  AMT-BK-PAID = ZEROS                                      
      *                                                                         
       3000-UPDATE-PROCESS.                                                     
                                                                                
           PERFORM 3100-READ-BP13K800             THRU 3100-EXIT.               
           IF K800-STATUS  NOT = ZEROS                                          
              PERFORM 2000-READ-BP13F410          THRU 2000-EXIT                
              GO TO 3000-EXIT.                                                  
                                                                                
           MOVE ZEROES             TO  WS-SOC-AMOUNT.                           
                                                                                
           COMPUTE WS-SOC-AMOUNT =   K800-AMT-SOC-CASH                          
                                   + K800-AMT-CPF1-PAID                         
                                   + K800-AMT-CPF2-PAID                         
                                   + K800-AMT-CPF3-PAID                         
                                   + K800-AMT-CPF4-PAID.                        
                                                                                
           IF K800-STATUS = ZEROS                                               
              PERFORM 3160-READ-BP13K830     THRU 3160-EXIT                     
              IF K800-AMT-BK-PAID = ZEROS           AND                         
                 K800-AMT-REGN-DEPOSIT-CASH = ZEROS AND                         
                 K800-AMT-REGN-DEPOSIT-CPF1 = ZEROS AND                         
                 K800-AMT-REGN-DEPOSIT-CPF2 = ZEROS AND                         
                 K800-AMT-REGN-DEPOSIT-CPF3 = ZEROS AND                         
                 K800-AMT-REGN-DEPOSIT-CPF4 = ZEROS AND                         
                 K800-AMT-CD                = ZEROS AND                         
                 WS-SOC-AMOUNT              = ZEROS AND                         
                 K800-AMT-SOC-DEBTOR        = ZEROS                             
                                                                                
                 ADD 1 TO WS-BK-PAID-CNT                                        
                 PERFORM 3200-WRITE-BP13K893      THRU 3200-EXIT                
                 PERFORM 3300-DELETE-BP13K800     THRU 3300-EXIT                
                 PERFORM 3400-START-BP13K820      THRU 3400-EXIT                
                 PERFORM 3500-BROWSE-BP13K820     THRU 3500-EXIT                
                         UNTIL WS-EOF-K820 = 'Y'                                
                         OR F410-NUM-REGN NOT = K820-NUM-REGN                   
                 PERFORM 3600-UPDATE-BP13K840        THRU 3600-EXIT             
              ELSE                                                              
                 PERFORM 5000-PRINT-REGN-REPORT      THRU 5000-EXIT             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F410          THRU 2000-EXIT.                  
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3100-READ-BP13K800.                                                      
                                                                                
           MOVE F410-NUM-REGN  TO K800-NUM-REGN.                                
           READ BP13K800.                                                       
                                                                                
           IF K800-STATUS = ZEROS OR 02                                         
              ADD 1 TO WS-K800-READ-CNT                                         
              PERFORM 3150-MOVE-BP13K820-INFO THRU 3150-EXIT                    
           ELSE                                                                 
           IF K800-STATUS  = 23                                                 
              DISPLAY 'RECORD NOT FOUND IN BP13K800'                            
              DISPLAY 'K800-REG-NO = ' K800-NUM-REGN                            
           ELSE                                                                 
              DISPLAY 'READ ERROR, BP13K800 STATUS = ' K800-STATUS              
              MOVE K800-STATUS TO RETURN-CODE                                   
              PERFORM 9999-END-PROCESS THRU 9999-EXIT.                          
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3130-INITIALISE-INFO.                                                    
           MOVE SPACES            TO WS-NRIC1,                                  
                                     WS-NRIC2,                                  
                                     WS-NRIC3,                                  
                                     WS-NRIC4,                                  
                                     WS-NAME1,                                  
                                     WS-NAME2,                                  
                                     WS-NAME3,                                  
                                     WS-NAME4,                                  
                                     WS-NUM-NEW-TOWN,                           
                                     WS-NUM-FLAT-TYPE,                          
                                     WS-NUM-ALLOC-SCH,                          
                                     WS-NUM-IDP-SCH,                            
                                     WS-NUM-SCH-ACC,                            
                                     WS-DTE-REGN.                               
            MOVE ZEROS           TO  WS-AMT-BK-PAID.                            
                                                                                
       3130-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3150-MOVE-BP13K820-INFO.                                                 
           PERFORM 3130-INITIALISE-INFO THRU 3130-EXIT.                         
           MOVE K800-NUM-NRIC1       TO WS-NRIC1.                               
           MOVE K800-NUM-NRIC2       TO WS-NRIC2.                               
           MOVE K800-NUM-NRIC3       TO WS-NRIC3.                               
           MOVE K800-NUM-NRIC4       TO WS-NRIC4.                               
           MOVE K800-NUM-NEW-TOWN    TO WS-NUM-NEW-TOWN.                        
           MOVE K800-NUM-FLAT-TYPE   TO WS-NUM-FLAT-TYPE.                       
           MOVE K800-NUM-ALLO-SCHEME TO WS-NUM-ALLOC-SCH.                       
           MOVE K800-NUM-IDP-SCHEME  TO WS-NUM-IDP-SCH.                         
           MOVE K800-NUM-SCH-ACC     TO WS-NUM-SCH-ACC.                         
           MOVE K800-DTE-REGN        TO WS-DTE-REGN.                            
           IF  K800-AMT-BK-PAID IS NOT NUMERIC                                  
               MOVE ZEROS TO K800-AMT-BK-PAID                                   
           END-IF.                                                              
                                                                                
           MOVE K800-AMT-BK-PAID     TO WS-AMT-BK-PAID.                         
                                                                                
           IF K800-AMT-REGN-DEPOSIT-CASH IS NOT NUMERIC                         
               MOVE ZEROS TO K800-AMT-REGN-DEPOSIT-CASH                         
           END-IF.                                                              
                                                                                
           IF K800-AMT-REGN-DEPOSIT-CPF1 IS NOT NUMERIC                         
               MOVE ZEROS TO K800-AMT-REGN-DEPOSIT-CPF1                         
           END-IF.                                                              
                                                                                
           IF K800-AMT-REGN-DEPOSIT-CPF2 IS NOT NUMERIC                         
               MOVE ZEROS TO K800-AMT-REGN-DEPOSIT-CPF2                         
           END-IF.                                                              
                                                                                
           IF K800-AMT-REGN-DEPOSIT-CPF3 IS NOT NUMERIC                         
               MOVE ZEROS TO K800-AMT-REGN-DEPOSIT-CPF3                         
           END-IF.                                                              
                                                                                
           IF K800-AMT-REGN-DEPOSIT-CPF4 IS NOT NUMERIC                         
               MOVE ZEROS TO K800-AMT-REGN-DEPOSIT-CPF4                         
           END-IF.                                                              
                                                                                
           IF K800-AMT-SOC-DEBTOR IS NOT NUMERIC                                
               MOVE ZEROS TO K800-AMT-SOC-DEBTOR                                
           END-IF.                                                              
                                                                                
           IF K800-AMT-SOC-CASH          IS NOT NUMERIC                         
               MOVE ZEROS TO K800-AMT-SOC-CASH                                  
           END-IF.                                                              
                                                                                
           IF K800-AMT-CPF1-PAID         IS NOT NUMERIC                         
               MOVE ZEROS TO K800-AMT-CPF1-PAID                                 
           END-IF.                                                              
                                                                                
           IF K800-AMT-CPF2-PAID         IS NOT NUMERIC                         
               MOVE ZEROS TO K800-AMT-CPF2-PAID                                 
           END-IF.                                                              
                                                                                
           IF K800-AMT-CPF3-PAID         IS NOT NUMERIC                         
               MOVE ZEROS TO K800-AMT-CPF3-PAID                                 
           END-IF.                                                              
                                                                                
           IF K800-AMT-CPF4-PAID         IS NOT NUMERIC                         
               MOVE ZEROS TO K800-AMT-CPF4-PAID                                 
           END-IF.                                                              
                                                                                
           IF K800-AMT-CD                IS NOT NUMERIC                         
               MOVE ZEROS TO K800-AMT-CD                                        
           END-IF.                                                              
                                                                                
       3150-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3160-READ-BP13K830.                                                      
                                                                                
           MOVE K800-NUM-REGN TO K830-NUM-REGN.                                 
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           IF K830-STATUS = 00                                                  
              ADD 1                      TO WS-K830-READ-CNT                    
              MOVE K830-NUM-ADDR-TYPE    TO WS-ADDR-TYPE                        
              MOVE K830-NME-STREET       TO WS-K830-NME-STREET                  
           ELSE                                                                 
           IF K830-STATUS  = 23                                                 
              DISPLAY 'RECORD NOT FOUND IN BP13K830'                            
              DISPLAY 'K830-NUM-NRIC = ' K830-KEY-FLD                           
              MOVE SPACES TO WS-ADDR-TYPE                                       
              MOVE SPACES TO WS-STREET-CODE                                     
           ELSE                                                                 
              DISPLAY 'READ ERROR, BP13K830 STATUS = ' K830-STATUS.             
                                                                                
       3160-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ** BASED ON STRRET NAME TO GET STREET-CODE FROM PBF                       
                                                                                
                                                                                
       3200-WRITE-BP13K893.                                                     
                                                                                
           MOVE WS-DTE-UPDATE   TO K800-DTE-UPDATE.                             
           MOVE BP13K800-MASTER TO BP13K893-MASTER.                             
           MOVE K800-NUM-REGN   TO K893-NUM-REGN-HIST.                          
           MOVE WS-DTE-UPDATE   TO K893-DTE-CREATE-HIST.                        
           ACCEPT K893-TME-CREATE-HIST FROM TIME.                               
           WRITE BP13K893-MASTER.                                               
                                                                                
           IF K893-STATUS = 00 OR 02                                            
              ADD 1 TO WS-K893-WRITE-CNT                                        
           ELSE                                                                 
              DISPLAY 'WRITE ERROR, BP13K893 STATUS = ' K893-STATUS             
              MOVE K893-STATUS TO RETURN-CODE                                   
              PERFORM 9999-END-PROCESS THRU 9999-EXIT.                          
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3300-DELETE-BP13K800.                                                    
                                                                                
           DELETE BP13K800.                                                     
                                                                                
           IF K800-STATUS = ZEROS                                               
              ADD 1 TO WS-K800-DELETE-CNT                                       
           ELSE                                                                 
           IF K800-STATUS  = 23                                                 
              DISPLAY 'DELETE ERROR REC NOT FOUND IN BP13K800'                  
              DISPLAY 'K800-REG-NO = ' K800-NUM-REGN                            
           ELSE                                                                 
              DISPLAY 'DELETE ERROR, BP13K800 STATUS = ' K800-STATUS            
              MOVE K800-STATUS TO RETURN-CODE                                   
              PERFORM 9999-END-PROCESS THRU 9999-EXIT.                          
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3400-START-BP13K820.                                                     
                                                                                
           MOVE 'N' TO  WS-EOF-K820.                                            
           MOVE F410-NUM-REGN  TO K820-NUM-REGN.                                
                                                                                
           START BP13K820 KEY NOT < K820-NUM-REGN                               
           IF K820-STATUS = ZEROS OR 02                                         
               PERFORM 3450-READ-BP13K820 THRU 3450-EXIT                        
           ELSE                                                                 
               DISPLAY 'START ERROR, K820-STATUS IS ' K820-STATUS               
               MOVE K820-STATUS TO RETURN-CODE                                  
               PERFORM 9999-END-PROCESS THRU 9999-EXIT.                         
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3450-READ-BP13K820.                                                      
                                                                                
           READ BP13K820 NEXT AT END                                            
                MOVE 'Y' TO WS-EOF-K820                                         
                GO TO 3450-EXIT.                                                
                                                                                
           IF WS-PREV-REGN NOT = F410-NUM-REGN                                  
               MOVE F410-NUM-REGN TO WS-PREV-REGN                               
               ADD 1 TO WS-OCC-REGN.                                            
                                                                                
                                                                                
       3450-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3500-BROWSE-BP13K820.                                                    
                                                                                
           IF F410-NUM-REGN = K820-NUM-REGN                                     
              ADD 1 TO WS-K820-READ-CNT                                         
              PERFORM 3510-WRITE-BP13K895   THRU 3510-EXIT                      
              PERFORM 3530-DELETE-BP13K820  THRU 3530-EXIT.                     
                                                                                
           PERFORM 3450-READ-BP13K820 THRU 3450-EXIT.                           
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3510-WRITE-BP13K895.                                                     
                                                                                
           MOVE SPACES        TO BP13K895-REC.                                  
           MOVE WS-DTE-UPDATE TO K820-DTE-UPDATE.                               
           MOVE BP13K820-REC  TO BP13K895-REC.                                  
           MOVE K820-NUM-REGN TO K895-NUM-REGN-HIST.                            
           MOVE K820-NUM-NRIC TO K895-NUM-NRIC-HIST.                            
           MOVE WS-DTE-UPDATE TO K895-DTE-CREATE-HIST.                          
           ACCEPT K895-TME-CREATE-HIST FROM TIME.                               
           WRITE BP13K895-REC.                                                  
                                                                                
           IF K895-STATUS = 00 OR 02                                            
              ADD 1 TO WS-K895-WRITE-CNT                                        
           ELSE                                                                 
              DISPLAY 'WRITE ERROR, BP13K895 STATUS = ' K895-STATUS             
              MOVE K895-STATUS TO RETURN-CODE                                   
              PERFORM 9999-END-PROCESS THRU 9999-EXIT.                          
                                                                                
       3510-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3530-DELETE-BP13K820.                                                    
                                                                                
           IF K820-NUM-NRIC = WS-NRIC1                                          
              MOVE K820-NME-OCCP TO WS-NAME1                                    
           ELSE                                                                 
           IF K820-NUM-NRIC = WS-NRIC2                                          
              MOVE K820-NME-OCCP TO WS-NAME2                                    
           ELSE                                                                 
           IF K820-NUM-NRIC = WS-NRIC3                                          
              MOVE K820-NME-OCCP TO WS-NAME3                                    
           ELSE                                                                 
           IF K820-NUM-NRIC = WS-NRIC4                                          
              MOVE K820-NME-OCCP TO WS-NAME4.                                   
                                                                                
           DELETE BP13K820.                                                     
                                                                                
           IF K820-STATUS = 00 OR 02                                            
              ADD 1 TO WS-K820-DELETE-CNT                                       
           ELSE                                                                 
           IF K820-STATUS  = 23                                                 
              DISPLAY 'RECORD NOT FOUND IN BP13K820'                            
              DISPLAY 'K820-REG-NO = ' K820-NUM-REGN                            
           ELSE                                                                 
              DISPLAY 'DELETE ERROR, BP13K820 STATUS = ' K820-STATUS            
              DISPLAY 'K820-REG-NO = ' K820-NUM-REGN.                           
                                                                                
       3530-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3600-UPDATE-BP13K840.                                                    
                                                                                
           MOVE F410-NUM-REGN TO K840-NUM-REGN.                                 
           READ BP13K840.                                                       
                                                                                
           IF K840-STATUS = ZEROS OR 02                                         
               PERFORM 3620-REWRITE-BP13K840 THRU 3620-EXIT                     
           ELSE                                                                 
           IF K840-STATUS = 23                                                  
               PERFORM 3640-CREATE-BP13K840 THRU 3640-EXIT                      
           ELSE                                                                 
              DISPLAY 'ERROR READ BP13K840  ' K840-NUM-REGN ' '                 
                                              K840-STATUS.                      
                                                                                
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3620-REWRITE-BP13K840.                                                   
           MOVE F410-DTE-KEY-ISSUE      TO K840-DTE-TP.                         
           MOVE WS-DTE-UPDATE          TO K840-DTE-UPDATE.                      
           IF WS-NAME1 NOT = SPACES                                             
              MOVE WS-NRIC1             TO K840-NUM-NRIC1                       
              MOVE WS-NAME1             TO K840-NME-APPL1.                      
           IF WS-NAME2 NOT = SPACES                                             
              MOVE WS-NRIC2             TO K840-NUM-NRIC2                       
              MOVE WS-NAME2             TO K840-NME-APPL2.                      
           IF WS-NAME3 NOT = SPACES                                             
              MOVE WS-NRIC3             TO K840-NUM-NRIC3                       
              MOVE WS-NAME3             TO K840-NME-APPL3.                      
           IF WS-NAME4 NOT = SPACES                                             
              MOVE WS-NRIC4             TO K840-NUM-NRIC4                       
              MOVE WS-NAME4             TO K840-NME-APPL4.                      
           IF WS-AMT-BK-PAID = ZEROS                                            
              MOVE WS-DTE-UPDATE        TO K840-DTE-CLOSE.                      
                                                                                
           REWRITE BP13K840-REC.                                                
                                                                                
           IF K840-STATUS = ZEROS OR 02                                         
              ADD 1 TO WS-K840-UPDATE-CNT                                       
           ELSE                                                                 
              DISPLAY 'REWRITE ERROR = ' K840-STATUS ' '                        
                                         K840-NUM-REGN.                         
                                                                                
       3620-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3640-CREATE-BP13K840.                                                    
                                                                                
           MOVE SPACES TO BP13K840-REC.                                         
           MOVE F410-NUM-REGN TO  K840-NUM-REGN.                                
           MOVE WS-NUM-NEW-TOWN TO  K840-NUM-NEW-TOWN.                          
           MOVE WS-NUM-FLAT-TYPE TO K840-NUM-FLAT-TYPE.                         
           MOVE WS-NUM-ALLOC-SCH TO K840-NUM-ALLOC-SCH.                         
           MOVE WS-NUM-IDP-SCH TO K840-NUM-IDP-SCH.                             
           MOVE WS-NUM-SCH-ACC TO K840-NUM-SCH-ACC.                             
           MOVE WS-DTE-REGN    TO K840-DTE-REGN.                                
           MOVE F410-DTE-KEY-ISSUE      TO K840-DTE-TP.                         
           MOVE WS-DTE-UPDATE          TO K840-DTE-UPDATE.                      
           IF WS-NAME1 NOT = SPACES                                             
              MOVE WS-NRIC1             TO K840-NUM-NRIC1                       
              MOVE WS-NAME1             TO K840-NME-APPL1.                      
           IF WS-NAME2 NOT = SPACES                                             
              MOVE WS-NRIC2             TO K840-NUM-NRIC2                       
              MOVE WS-NAME2             TO K840-NME-APPL2.                      
           IF WS-NAME3 NOT = SPACES                                             
              MOVE WS-NRIC3             TO K840-NUM-NRIC3                       
              MOVE WS-NAME3             TO K840-NME-APPL3.                      
           IF WS-NAME4 NOT = SPACES                                             
              MOVE WS-NRIC4             TO K840-NUM-NRIC4                       
              MOVE WS-NAME4             TO K840-NME-APPL4.                      
           IF WS-AMT-BK-PAID = ZEROS                                            
              MOVE WS-DTE-UPDATE        TO K840-DTE-CLOSE.                      
                                                                                
           WRITE BP13K840-REC.                                                  
                                                                                
           IF K840-STATUS = ZEROS OR 02                                         
              ADD 1 TO WS-K840-WRITE-CNT                                        
           ELSE                                                                 
              DISPLAY 'WRITE ERROR = ' K840-STATUS ' '                          
                                       K840-NUM-REGN.                           
                                                                                
       3640-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-CONTROL-LIST.                                                       
                                                                                
           MOVE 'BP13L423'             TO LIST-ID.                              
           MOVE 1                      TO PAGE-NO.                              
           MOVE SPACES                 TO PRT-LINE1.                            
                                                                                
           STRING FUNCTION CURRENT-DATE(7:2) '/'                                
                  FUNCTION CURRENT-DATE(5:2) '/'                                
                  FUNCTION CURRENT-DATE(1:4)                                    
           DELIMITED BY SIZE INTO RUN-DATE.                                     
                                                                                
           WRITE PRT-LINE1 FROM L423-HDG1 AFTER PAGE.                           
           WRITE PRT-LINE1 FROM L423-HDG3 AFTER 1.                              
           MOVE WS-F410-READ-CNT       TO TOTAL-F410-READ.                      
           MOVE WS-K800-READ-CNT       TO TOTAL-K800-READ.                      
           MOVE WS-BK-PAID-CNT         TO TOTAL-BK-PAID.                        
           MOVE WS-K893-WRITE-CNT      TO TOTAL-K893-WRITE.                     
           MOVE WS-K800-DELETE-CNT     TO TOTAL-K800-DELETE.                    
           MOVE WS-K820-READ-CNT       TO TOTAL-K820-READ.                      
           MOVE WS-K895-WRITE-CNT      TO TOTAL-K895-WRITE.                     
           MOVE WS-K830-READ-CNT       TO TOTAL-K830-READ.                      
           MOVE WS-K820-DELETE-CNT     TO TOTAL-K820-DELETE.                    
           MOVE WS-K840-UPDATE-CNT     TO TOTAL-K840-UPDATE.                    
           MOVE WS-K840-WRITE-CNT      TO TOTAL-K840-WRITE.                     
           MOVE WS-CNT-NOTFND          TO TOTAL-ST-NOT-FND-CNT.                 
           MOVE WS-OCC-REGN            TO TOTAL-K820-REGN.                      
           MOVE SPACES TO PRT-LINE1.                                            
           WRITE PRT-LINE1 FROM SUMMARY1  AFTER 4.                              
           MOVE SPACES TO PRT-LINE1.                                            
           WRITE PRT-LINE1 FROM SUMMARY2  AFTER 2.                              
           MOVE SPACES TO PRT-LINE1.                                            
           WRITE PRT-LINE1 FROM SUMMARY2A  AFTER 2.                             
           MOVE SPACES TO PRT-LINE1.                                            
           WRITE PRT-LINE1 FROM SUMMARY3  AFTER 2.                              
           MOVE SPACES TO PRT-LINE1.                                            
           WRITE PRT-LINE1 FROM SUMMARY4  AFTER 2.                              
           MOVE SPACES TO PRT-LINE1.                                            
           WRITE PRT-LINE1 FROM SUMMARY5  AFTER 2.                              
           MOVE SPACES TO PRT-LINE1.                                            
           WRITE PRT-LINE1 FROM SUMMARY5A  AFTER 2.                             
           MOVE SPACES TO PRT-LINE1.                                            
           WRITE PRT-LINE1 FROM SUMMARY6  AFTER 2.                              
           MOVE SPACES TO PRT-LINE1.                                            
           WRITE PRT-LINE1 FROM SUMMARY7  AFTER 2.                              
           MOVE SPACES TO PRT-LINE1.                                            
           WRITE PRT-LINE1 FROM SUMMARY9  AFTER 2.                              
           MOVE SPACES TO PRT-LINE1.                                            
           WRITE PRT-LINE1 FROM SUMMARY10 AFTER 2.                              
           MOVE SPACES TO PRT-LINE1.                                            
           WRITE PRT-LINE1 FROM SUMMARY11 AFTER 2.                              
           MOVE SPACES TO PRT-LINE1.                                            
           WRITE PRT-LINE1 FROM SUMMARY12 AFTER 2.                              
           WRITE PRT-LINE1 FROM SUMMARY13 AFTER 1.                              
           WRITE PRT-LINE1 FROM SUMMARY14 AFTER 1.                              
           WRITE PRT-LINE1 FROM SUMMARY15 AFTER 1.                              
           WRITE PRT-LINE1 FROM SUMMARY16 AFTER 1.                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-PRINT-REGN-REPORT.                                                  
            IF WS-PAGE-CTR = 0                                                  
               PERFORM 5100-WRITE-HEADER        THRU 5100-EXIT                  
            END-IF.                                                             
            ADD 1                         TO WS-L423B-WRITE-CTR                 
                                             WS-LINE-CTR                        
            IF WS-LINE-CTR > 51                                                 
               MOVE 0                     TO WS-LINE-CTR                        
               MOVE SPACES                TO PRT-REC                            
               WRITE PRT-REC              BEFORE PAGE                           
               PERFORM 5100-WRITE-HEADER  THRU 5100-EXIT                        
            END-IF.                                                             
                                                                                
            MOVE WS-L423B-WRITE-CTR            TO L423B-SNO.                    
            MOVE K800-NUM-REGN                 TO L423B-REGNO.                  
            MOVE K800-NUM-ALLO-SCHEME          TO L423B-ALLOSCH.                
            MOVE K800-AMT-BK-PAID              TO L423B-AMTBK.                  
            MOVE K800-AMT-REGN-DEPOSIT-CASH    TO L423B-AMTDEPOS.               
            MOVE K800-AMT-REGN-DEPOSIT-CPF1    TO L423B-CPF1.                   
            MOVE K800-AMT-REGN-DEPOSIT-CPF2    TO L423B-CPF2.                   
            MOVE K800-AMT-REGN-DEPOSIT-CPF3    TO L423B-CPF3.                   
            MOVE K800-AMT-REGN-DEPOSIT-CPF4    TO L423B-CPF4.                   
            WRITE PRT-REC                 FROM L423B-DETAIL-LINE.               
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5100-WRITE-HEADER.                                                       
            ADD 1                          TO   WS-PAGE-CTR.                    
            MOVE 'P13L423B'                TO LIST-ID.                          
            WRITE PRT-REC                  FROM L423-HDG1.                      
            MOVE WS-PAGE-CTR               TO   PAGE-NUM                        
            WRITE PRT-REC                  FROM L423B-HDG2 AFTER 2.             
            WRITE PRT-REC                  FROM L423B-HDG3 AFTER 2.             
            WRITE PRT-REC                  FROM L423B-LINE.                     
            MOVE SPACES                    TO   PRT-REC.                        
            WRITE PRT-REC                  AFTER 2.                             
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9999-END-PROCESS.                                                        
      ****************************************************************          
                                                                                
           CLOSE BP13F410                                                       
                 BP13K893                                                       
                 BP13K895                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 BP13K840                                                       
                 P13L423A                                                       
                 P13L423B.                                                      
                                                                                
           IF K800-STATUS NOT = 0 AND 97                                        
              DISPLAY 'CLOSING ERROR, K800-STATUS IS ' K800-STATUS.             
                                                                                
           IF K820-STATUS NOT = 0 AND 97                                        
              DISPLAY 'CLOSING ERROR, K820-STATUS IS ' K820-STATUS.             
                                                                                
           IF K893-STATUS NOT = 0 AND 97                                        
              DISPLAY 'CLOSING ERROR, K893-STATUS IS ' K893-STATUS.             
                                                                                
           IF K895-STATUS NOT = 0 AND 97                                        
              DISPLAY 'CLOSING ERROR, K895-STATUS IS ' K895-STATUS.             
                                                                                
           IF K830-STATUS NOT = 0 AND 97                                        
              DISPLAY 'CLOSING ERROR, K830-STATUS IS ' K830-STATUS.             
                                                                                
           IF K840-STATUS NOT = 0 AND 97                                        
              DISPLAY 'CLOSING ERROR, K840-STATUS IS ' K840-STATUS.             
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *======================== END OF PROGRAM ======================*          
