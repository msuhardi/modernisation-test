       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CCAM.                                                 
      *AUTHOR.        RJB1.                                                     
      *DATE-WRITTEN.  10/12/2015.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE : TO CREATE A REPORT FOR CASES WITH NO TOL SIGN DATE *        
      *                                                                *        
      * INPUT  FILE  : BP13F462                                        *        
      * OUTPUT FILE  : BP13MAIL                                        *        
      *                                                                *        
      * CHG-NO    BY    ON    - DESCRIPTION                            *        
      * ---------------------------------                              *        
      * BP136020 RJB1 20151210  NEW PROGRAM                            *        
      * BP138831 KV8  20210817  TO REPLACE LEE_LI_PENG TO              *        
      *                         BERNICE_JH_GAN                         *        
      * BP139175 kac1 20240427  TO REPLACE BERNICE_JH_GAN with         *        
      *                         Iris_LL_KOR@hdb.gov.sg                 *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F462 ASSIGN        TO BP13F462.                           
           SELECT SY02F001 ASSIGN        TO SY02F001.                           
           SELECT BP13MAIL ASSIGN        TO BP13MAIL.                           
           SELECT P13MAIL2 ASSIGN        TO P13MAIL2.                           
           SELECT BP13K800 ASSIGN        TO BP13K800                            
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K800-NUM-REGN                       
                           FILE STATUS   IS WS-K800-STATUS.                     
           SELECT BP13K022 ASSIGN        TO BP13K022                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K022-KEY-FLD                        
                           ALTERNATE KEY IS K022-NUM-REGN                       
                           FILE STATUS   IS WS-K022-STATUS.                     
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13F462                                                            
            RECORD CONTAINS 1000 CHARACTERS                                     
            RECORDING MODE IS F.                                                
       COPY BP13F462.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13MAIL                                                            
            RECORD CONTAINS 80  CHARACTERS                                      
            RECORDING MODE IS F.                                                
       01   MAIL-PRTREC             PIC X(80).                                  
                                                                                
       FD   P13MAIL2                                                            
            RECORD CONTAINS 80  CHARACTERS                                      
            RECORDING MODE IS F.                                                
       01   MAIL2-PRTREC            PIC X(80).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-VARIABLES.                                                        
           05  WS-K462-STATUS               PIC 9(2)  VALUE ZEROS.              
           05  WS-K800-STATUS               PIC 9(2)  VALUE ZEROS.              
           05  WS-K022-STATUS               PIC 9(2)  VALUE ZEROS.              
           05  CNT-F462-READ                PIC 9(07) VALUE ZEROS.              
           05  WS-CNT-UPDATE                PIC 9(07) VALUE ZEROS.              
           05  CNT-SNO                      PIC 9(07) VALUE ZEROS.              
           05  WS-F462-EOF                  PIC X(01) VALUE SPACE.              
               88  F462-EOF                           VALUE 'Y'.                
           05  WS-DTE-TEMP-NUM              PIC 9(8).                           
           05  WS-DTE-TEMP REDEFINES WS-DTE-TEMP-NUM.                           
               10 WS-DTE-TEMP-CCYY          PIC 9(04).                          
               10 WS-DTE-TEMP-MM            PIC 9(02).                          
               10 WS-DTE-TEMP-DD            PIC 9(02).                          
           05  WS-DTE-NUM                   PIC 9(7)  VALUE ZEROS.              
           05  WS-DTE-3DD-AFTER             PIC 9(8).                           
           05  WS-OIC                       PIC X(5)  VALUE SPACES.             
           05  WS-OIC-NME                   PIC X(30) VALUE SPACES.             
           05  WS-EMAIL                     PIC X(50) VALUE SPACES.             
                                                                                
      *-------------------------------------------------------------            
      * LAYOUT BP13MAIL  -  DATASET FOR EMAIL                                   
      *-------------------------------------------------------------            
       01  MAIL-HDR1.                                                           
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  MAIL-HDR2.                                                           
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  MAIL-SENDID     PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1A-RCPT.                                                     
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(60)  VALUE                                 
      *                        'LEE_LI_PENG@HDB.GOV.SG>'.                       
      *                        'BERNICE_JH_GAN@HDB.GOV.SG>'.                    
                               'Iris_LL_KOR@hdb.gov.sg>'.                       
                                                                                
       01  MAIL-DTL3-DATA.                                                      
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4-FROM.                                                      
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  MAIL-SENDMAILID PIC X(26)  VALUE                                 
               'SOC System - Email Alert'.                                      
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5A-TO1.                                                      
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(60)  VALUE                                 
      *                        'LEE_LI_PENG@HDB.GOV.SG>'.                       
      *                        'BERNICE_JH_GAN@HDB.GOV.SG>'.                    
                               'Iris_LL_KOR@hdb.gov.sg>'.                       
                                                                                
       01  MAIL-DTL6-SUBJECT.                                                   
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  FILLER          PIC X(35)  VALUE                                 
               'No TOL Sign Date'.                                              
                                                                                
       01  MAIL-DTL7-DATE.                                                      
           05 FILLER                   PIC X(6)   VALUE 'DATE: '.               
           05 MAIL-DATE                PIC X(10)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY1-TITLE-01.                                                 
           05 FILLER                   PIC X(26)  VALUE                         
              'TP appointment date is on '.                                     
           05 MAIL-DATE-TP             PIC X(10)  VALUE SPACES.                 
           05 FILLER                   PIC X(47)  VALUE                         
              ' but TOL sign date has not been updated. '.                      
                                                                                
       01  MAIL-BODY2-TITLE-02.                                                 
           05 FILLER                   PIC X(80)  VALUE                         
              'Please check.'.                                                  
                                                                                
       01  MAIL-BODY7-HEADER.                                                   
           05 FILLER                   PIC X(80)  VALUE                         
              'S/N   Regn No '.                                                 
                                                                                
       01  MAIL-BODY8-DASH.                                                     
           05 FILLER                   PIC X(80)  VALUE                         
              '---   -------- '.                                                
                                                                                
       01  MAIL-BODY9-DETAIL.                                                   
           05  MAIL-SNO                PIC ZZ9    VALUE ZEROES.                 
           05  FILLER                  PIC X(03)  VALUE SPACES.                 
           05  MAIL-REGN-NO            PIC X(08)  VALUE SPACES.                 
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                   PIC X      VALUE '.'.                    
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(80)  VALUE SPACES.                 
                                                                                
       01  MAIL-NIL.                                                            
           05  FILLER                  PIC X(17)  VALUE                         
                                          'NO RECORDS FOUND.'.                  
                                                                                
      *----------------------------------------------------------------*        
      *        LINKAGE FOR SUB-PROGRAM BP13CCNT                        *        
      *----------------------------------------------------------------*        
       01  WS-BP13CCNT-LINK.                                                    
           05  LINK-CNTCT-TYPE         PIC X(3).                                
           05  LINK-STAFF-NICK         PIC X(6).                                
           05  LINK-STAFF-NUM          PIC X(6).                                
           05  LINK-STAFF-CNTCT        PIC X(50).                               
           05  LINK-SYSERR             PIC X(4).                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 8000-OPEN-FILES            THRU 8000-EXIT.                   
                                                                                
           PERFORM 8001-READ-BP13F462         THRU 8001-EXIT                    
           PERFORM UNTIL F462-EOF                                               
              PERFORM 1000-PROCESS-RECORDS    THRU 1000-EXIT                    
              PERFORM 8001-READ-BP13F462      THRU 8001-EXIT                    
           END-PERFORM.                                                         
                                                                                
           IF CNT-SNO > 0                                                       
              WRITE MAIL-PRTREC FROM MAIL-END                                   
           END-IF.                                                              
                                                                                
           PERFORM 9000-CLOSE-FILES           THRU 9000-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-PROCESS-RECORDS.                                                    
      *-------------------------------------------------------------            
                                                                                
           PERFORM 8002-READ-BP13K800                                           
              THRU 8002-EXIT.                                                   
                                                                                
           IF WS-DTE-3DD-AFTER = K800-DTE-SO-APPT                               
              IF F462-DTE-TOL-REQ NOT = SPACES AND LOW-VALUES                   
                 IF F462-DTE-TOL-SIGNED = SPACES OR LOW-VALUES                  
                    CONTINUE                                                    
                 ELSE                                                           
                    GO TO 1000-EXIT                                             
                 END-IF                                                         
              ELSE                                                              
                 GO TO 1000-EXIT                                                
              END-IF                                                            
           ELSE                                                                 
              GO TO 1000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF CNT-SNO < 1                                                       
              PERFORM 1100-PRINT-MAIL-HEADER THRU 1100-EXIT                     
           END-IF.                                                              
                                                                                
           MOVE SPACES                  TO MAIL-BODY9-DETAIL                    
           INITIALIZE                      MAIL-BODY9-DETAIL.                   
                                                                                
           ADD 1                        TO CNT-SNO                              
           MOVE CNT-SNO                 TO MAIL-SNO.                            
           MOVE F462-NUM-REGN           TO MAIL-REGN-NO.                        
           WRITE MAIL-PRTREC FROM MAIL-BODY9-DETAIL.                            
                                                                                
           PERFORM 8100-GET-OIC                                                 
              THRU 8100-EXIT.                                                   
                                                                                
           IF WS-OIC NOT = SPACES AND LOW-VALUES                                
              PERFORM 8900-CALL-BP13CCNT                                        
                 THRU 8900-EXIT                                                 
              IF WS-EMAIL NOT = SPACES AND LOW-VALUES                           
                 PERFORM 8200-INDV-REPORT                                       
                    THRU 8200-EXIT                                              
              END-IF                                                            
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1100-PRINT-MAIL-HEADER.                                                  
      *-------------------------------------------------------------            
                                                                                
           STRING F001-DTE-CURRENT(7:2) '/'                                     
                  F001-DTE-CURRENT(5:2) '/'                                     
                  F001-DTE-CURRENT(1:4)                                         
           DELIMITED BY SIZE INTO MAIL-DATE                                     
                                                                                
           STRING K800-DTE-SO-APPT(7:2) '/'                                     
                  K800-DTE-SO-APPT(5:2) '/'                                     
                  K800-DTE-SO-APPT(1:4)                                         
           DELIMITED BY SIZE   INTO MAIL-DATE-TP.                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-HDR1                                     
           WRITE MAIL-PRTREC FROM MAIL-HDR2                                     
                                                                                
      * RCPT TO                                                                 
           WRITE MAIL-PRTREC FROM MAIL-DTL1A-RCPT                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL3-DATA.                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL4-FROM.                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL5A-TO1.                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL6-SUBJECT.                            
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL7-DATE.                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-BODY1-TITLE-01.                          
           WRITE MAIL-PRTREC FROM MAIL-BODY2-TITLE-02.                          
           WRITE MAIL-PRTREC FROM MAIL-BODY7-HEADER.                            
           WRITE MAIL-PRTREC FROM MAIL-BODY8-DASH.                              
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT BP13F462                                                  
                      BP13K800                                                  
                      BP13K022                                                  
                      SY02F001                                                  
               OUTPUT BP13MAIL                                                  
                      P13MAIL2.                                                 
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K022 ERROR ' WS-K022-STATUS                  
              MOVE     WS-K022-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
           MOVE F001-DTE-CURRENT(1:4)      TO WS-DTE-TEMP-CCYY.                 
           MOVE F001-DTE-CURRENT(5:2)      TO WS-DTE-TEMP-MM.                   
           MOVE F001-DTE-CURRENT(7:2)      TO WS-DTE-TEMP-DD.                   
                                                                                
           COMPUTE WS-DTE-NUM =                                                 
              FUNCTION INTEGER-OF-DATE(WS-DTE-TEMP-NUM).                        
                                                                                
           ADD 3 TO WS-DTE-NUM                                                  
                                                                                
           COMPUTE WS-DTE-3DD-AFTER =                                           
              FUNCTION DATE-OF-INTEGER(WS-DTE-NUM).                             
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8001-READ-BP13F462.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F462                                                        
             AT END                                                             
                MOVE 'Y'             TO WS-F462-EOF                             
                GO                   TO 8001-EXIT.                              
                                                                                
           ADD  1                    TO CNT-F462-READ.                          
                                                                                
       8001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8002-READ-BP13K800.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE F462-NUM-REGN        TO K800-NUM-REGN.                          
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
              CONTINUE                                                          
           WHEN 23                                                              
              DISPLAY 'REGN NOT FOUND ON BP13K800 = ' K800-NUM-REGN             
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K800 , '                               
                   'STATUS = ' WS-K800-STATUS                                   
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       8002-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       8100-GET-OIC.                                                            
      *================================================================*        
           MOVE SPACES                     TO K022-KEY-FLD.                     
           MOVE F462-NUM-REGN              TO K022-NUM-REGN.                    
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN ZEROS                                                        
                 MOVE K022-NUM-OIC            TO WS-OIC                         
              WHEN 23                                                           
                 MOVE SPACES                  TO WS-OIC                         
              WHEN OTHER                                                        
                MOVE  WS-K022-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BP13K022 FILE : ' WS-K022-STATUS         
                DISPLAY 'K022-NUM-REGN = ' K022-NUM-REGN                        
                PERFORM 9000-CLOSE-FILES                                        
                   THRU 9000-EXIT                                               
           END-EVALUATE.                                                        
                                                                                
       8100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       8200-INDV-REPORT.                                                        
      *================================================================*        
                                                                                
           STRING F001-DTE-CURRENT(7:2) '/'                                     
                  F001-DTE-CURRENT(5:2) '/'                                     
                  F001-DTE-CURRENT(1:4)                                         
           DELIMITED BY SIZE INTO MAIL-DATE                                     
                                                                                
           STRING K800-DTE-SO-APPT(7:2) '/'                                     
                  K800-DTE-SO-APPT(5:2) '/'                                     
                  K800-DTE-SO-APPT(1:4)                                         
           DELIMITED BY SIZE   INTO MAIL-DATE-TP.                               
                                                                                
           WRITE MAIL2-PRTREC FROM MAIL-HDR1                                    
           WRITE MAIL2-PRTREC FROM MAIL-HDR2                                    
                                                                                
      * RCPT TO                                                                 
           MOVE SPACES  TO MAIL2-PRTREC                                         
           STRING 'RCPT TO:<' DELIMITED BY SIZE                                 
                  WS-EMAIL '>'                                                  
                  DELIMITED BY SPACES INTO MAIL2-PRTREC                         
           WRITE MAIL2-PRTREC                                                   
                                                                                
           WRITE MAIL2-PRTREC FROM MAIL-DTL3-DATA.                              
                                                                                
           WRITE MAIL2-PRTREC FROM MAIL-DTL4-FROM.                              
                                                                                
           MOVE SPACES  TO MAIL2-PRTREC                                         
           STRING 'TO:<' DELIMITED BY SIZE                                      
                  WS-EMAIL '>'                                                  
                  DELIMITED BY SPACES                                           
                  INTO MAIL2-PRTREC                                             
           WRITE MAIL2-PRTREC                                                   
                                                                                
           WRITE MAIL2-PRTREC FROM MAIL-DTL6-SUBJECT.                           
                                                                                
           WRITE MAIL2-PRTREC FROM MAIL-DTL7-DATE.                              
                                                                                
           WRITE MAIL2-PRTREC FROM MAIL-SPACES.                                 
           WRITE MAIL2-PRTREC FROM MAIL-BODY1-TITLE-01.                         
           WRITE MAIL2-PRTREC FROM MAIL-BODY2-TITLE-02.                         
           WRITE MAIL2-PRTREC FROM MAIL-BODY7-HEADER.                           
           WRITE MAIL2-PRTREC FROM MAIL-BODY8-DASH.                             
                                                                                
           MOVE SPACES                  TO MAIL-BODY9-DETAIL                    
           INITIALIZE                      MAIL-BODY9-DETAIL.                   
                                                                                
           MOVE 1                       TO MAIL-SNO.                            
           MOVE F462-NUM-REGN           TO MAIL-REGN-NO.                        
           WRITE MAIL2-PRTREC FROM MAIL-BODY9-DETAIL.                           
                                                                                
       8200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       8900-CALL-BP13CCNT.                                                      
      *================================================================*        
           MOVE 'EML'                      TO LINK-CNTCT-TYPE.                  
           MOVE WS-OIC                     TO LINK-STAFF-NICK.                  
           MOVE SPACES                     TO LINK-STAFF-NUM.                   
                                                                                
           CALL 'BP13CCNT' USING WS-BP13CCNT-LINK.                              
                                                                                
           EVALUATE LINK-SYSERR                                                 
           WHEN ZEROS                                                           
              MOVE LINK-STAFF-CNTCT        TO WS-EMAIL                          
           WHEN 100                                                             
              DISPLAY 'OIC NOT FOUND IN OFFICE_CNTCT TABLE - '                  
                         WS-OIC  ', REGN : ' F462-NUM-REGN                      
              MOVE  SPACES                 TO WS-EMAIL                          
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING OFFICE_CNTCT TABLE = '                     
                       LINK-SYSERR                                              
              MOVE  SPACES                 TO WS-EMAIL                          
           END-EVALUATE.                                                        
                                                                                
       8900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8910-WRITE-NIL.                                                          
      *-------------------------------------------------------------            
           STRING F001-DTE-CURRENT(7:2) '/'                                     
                  F001-DTE-CURRENT(5:2) '/'                                     
                  F001-DTE-CURRENT(1:4)                                         
           DELIMITED BY SIZE INTO MAIL-DATE                                     
                                                                                
           STRING K800-DTE-SO-APPT(7:2) '/'                                     
                  K800-DTE-SO-APPT(5:2) '/'                                     
                  K800-DTE-SO-APPT(1:4)                                         
           DELIMITED BY SIZE   INTO MAIL-DATE-TP.                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-HDR1                                     
           WRITE MAIL-PRTREC FROM MAIL-HDR2                                     
                                                                                
      * RCPT TO                                                                 
           WRITE MAIL-PRTREC FROM MAIL-DTL1A-RCPT                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL3-DATA.                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL4-FROM.                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL5A-TO1.                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL6-SUBJECT.                            
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL7-DATE.                               
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-NIL.                                     
                                                                                
       8910-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           IF CNT-SNO < 1                                                       
              PERFORM 8910-WRITE-NIL         THRU 8910-EXIT                     
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CCAM'.                                
           DISPLAY '***************************************'.                   
           DISPLAY 'NO. OF BP13F462 READ     : ' CNT-F462-READ.                 
           DISPLAY 'CURRENT DATE   : ' MAIL-DATE.                               
                                                                                
           CLOSE BP13F462                                                       
                 BP13K800                                                       
                 BP13K022                                                       
                 SY02F001                                                       
                 P13MAIL2                                                       
                 BP13MAIL.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K022 ERROR ' WS-K022-STATUS                  
              MOVE     WS-K022-STATUS      TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
