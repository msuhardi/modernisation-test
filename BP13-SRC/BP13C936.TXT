       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C936.                                                 
      *DATE-WRITTEN.  20/11/2001.                                               
      **********************************************************                
      * OBJECTIVE :                                            *                
      *                                                        *                
      * TO CREATE EC-OWNER AND OCCUPIER FILES SEPERATELY       *                
      * FOR PRINTING REPORT BP13L073. L006, L011               *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *========================================================*                
      * CHG-NO   BY   ON     DESCRIPTION                       *                
      * --------------------------------                       *                
      * BP131046 ANC1 201101 NEW PROGRAM                       *                
      * BP131046 ANC1 160302 BYPASS ADMINISTRATOR              *                
      **********************************************************                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F716  ASSIGN       TO BP13F716.                           
                                                                                
           SELECT P13F716A  ASSIGN       TO P13F716A.                           
                                                                                
           SELECT P13F716B  ASSIGN       TO P13F716B.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F716                                                            
            RECORDING  MODE IS  F                                               
            RECORD CONTAINS 350 CHARACTERS.                                     
       COPY BP13F716.                                                           
                                                                                
       FD   P13F716A                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 350 CHARACTERS.                                     
       01   P13F716A-REC.                                                       
            05  FILLER                  PIC X(295).                             
            05  OWNER-FLAG              PIC X.                                  
            05  FILLER                  PIC X(54).                              
                                                                                
       FD   P13F716B                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 350 CHARACTERS.                                     
       01   P13F716B-REC.                                                       
            05  FILLER                  PIC X(295).                             
            05  OCCUP-FLAG              PIC X.                                  
            05  FILLER                  PIC X(54).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F716-REGN             PIC X(8)  VALUE SPACES.                 
           05  WS-EOF-BP13F716          PIC X     VALUE 'N'.                    
           05  WS-DATE                  PIC X(8)  VALUE SPACES.                 
       01  WS-COUNTERS.                                                         
           05  BP13F716-READ            PIC 9(6)  VALUE ZEROS.                  
           05  P13F716A-WRITE           PIC 9(6)  VALUE ZEROS.                  
           05  P13F716B-WRITE           PIC 9(6)  VALUE ZEROS.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-ROUTINE         THRU  1000-EXIT.                  
            PERFORM 2000-READ-BP13F716        THRU  2000-EXIT.                  
            PERFORM 3000-PROCESS-PARA         THRU  3000-EXIT                   
                         UNTIL WS-EOF-BP13F716 = 'Y'.                           
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            OPEN INPUT  BP13F716                                                
                 OUTPUT P13F716A                                                
                        P13F716B.                                               
                                                                                
            MOVE FUNCTION CURRENT-DATE TO WS-DATE.                              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2000-READ-BP13F716.                                                      
      *-------------------------------------------------------------            
            READ BP13F716                                                       
                 AT END MOVE 'Y' TO WS-EOF-BP13F716                             
                 GO TO  2000-EXIT.                                              
                                                                                
            ADD 1 TO BP13F716-READ.                                             
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3000-PROCESS-PARA.                                                       
      *-------------------------------------------------------------            
            MOVE SPACES           TO P13F716A-REC                               
                                     P13F716B-REC.                              
            MOVE F716-NUM-REGN    TO WS-F716-REGN.                              
                                                                                
            PERFORM 4000-WRITE-TO-FILE THRU 4000-EXIT                           
               UNTIL F716-NUM-REGN NOT = WS-F716-REGN                           
                  OR WS-EOF-BP13F716 = 'Y'.                                     
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4000-WRITE-TO-FILE.                                                      
      *-------------------------------------------------------------            
            IF (F716-NUM-RELATION-APPLN = 'H1' OR 'H2' OR 'H3' OR 'H4')         
                AND (F716-CDE-OCCUPIER = '0' OR '1')                            
               MOVE BP13F716-REC        TO P13F716A-REC                         
               MOVE 'L'                 TO OWNER-FLAG                           
               WRITE                       P13F716A-REC                         
               ADD 1                    TO P13F716A-WRITE                       
                                                                                
               MOVE BP13F716-REC        TO P13F716B-REC                         
               MOVE 'K'                 TO OCCUP-FLAG                           
               WRITE                       P13F716B-REC                         
               ADD 1                    TO P13F716B-WRITE                       
            ELSE                                                                
               IF (F716-CDE-OCCUPIER NOT = '4' AND '5' AND '6' AND              
                                           '8' AND '3')                         
                  MOVE BP13F716-REC        TO P13F716B-REC                      
                  MOVE 'O'                 TO OCCUP-FLAG                        
                  WRITE                       P13F716B-REC                      
                  ADD 1                    TO P13F716B-WRITE                    
               END-IF                                                           
            END-IF.                                                             
                                                                                
            PERFORM 2000-READ-BP13F716  THRU  2000-EXIT.                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
            DISPLAY '              PROGRAM-ID :  BP13C936'.                     
            DISPLAY '              ~~~~~~~~~~             '.                    
            DISPLAY '                                  '                        
                                        'CURRENT-DATE : ' WS-DATE.              
            DISPLAY ' '.                                                        
            DISPLAY '1. NO OF RECS READ FROM BP13F716  : '                      
                                                      BP13F716-READ.            
            DISPLAY '2. NO OF RECS WRITTEN TO P13F716A : '                      
                                                      P13F716A-WRITE.           
            DISPLAY '3. NO OF RECS WRITTEN TO P13F716B : '                      
                                                      P13F716B-WRITE.           
            DISPLAY '  '.                                                       
                                                                                
            CLOSE BP13F716                                                      
                  P13F716A                                                      
                  P13F716B.                                                     
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
