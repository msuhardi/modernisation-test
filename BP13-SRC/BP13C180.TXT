       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C180                                                  
       AUTHOR.        LSB.                                                      
       DATE-WRITTEN.  18/08/98.                                                 
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : FOR NRIC FOUN DIN DEBARMENT AS ENJOYED*                
      *                  SUBSIDISED LOAN, MATCH BACK BP13F558  *                
      *                  TO EXTRACT THE WHOLE ESENTIAL FAMILY  *                
      *    DEBARMENT CODE = CA, CB, CC, CD, CE, CF, CH, CI, CJ *                
      *                                                        *                
      *    INPUT FILE  : BP13F558  - COMPLETE REQ FILE         *                
      *    INPUT FILE  : P13F558A - TAG AS 'C' IN DEBARMENT    *                
      *  OUTPUT  FILE  : P13F558O - ALL ESSENTIAL FAMILY       *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY   DATE     DETAILS                          *                
      *BP130645 SRA 19990422  TO CHANGE F558-NUM-DEBARMENT TO  *                
      *                       DEBARMENT-2                      *                
      *BP130693 SRA 19990715  CHECK IF DTE-DEBAR-EFF FALLS WITH*                
      *                       IN SAME QUARTER OR PREVIOUS QTR, *                
      *                       THEN CREATE OUTPUT FILE(P13F558O)*                
      *BP130897 AKT 20000731  EXPAND F558 RECL FROM 400 TO 600 *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
           SELECT P13F558O ASSIGN TO P13F558O.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F558                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F558.                                                           
                                                                                
       FD  P13F558O                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  P13F558O-REC                   PIC X(600).                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F558-READ                PIC 9(8)    VALUE ZEROS.              
           05 WS-F558O-WRITE              PIC 9(8)    VALUE ZEROS.              
           05 WS-PREV-REGN                PIC X(8)    VALUE SPACES.             
           05 WS-F558-SUB                 PIC 9(2)    VALUE 0.                  
           05 WS-OCCUP-STORED             PIC 9(2)    VALUE 0.                  
           05 WS-DEBAR-FD                 PIC X(1)    VALUE SPACES.             
                                                                                
       COPY WSF558.                                                             
                                                                                
       01  WS-EOF-F558                    PIC X.                                
           88 EOF-F558                                VALUE 'Y'.                
                                                                                
       01  WS-QTR                         PIC S9.                               
       01  WS-QTR-MATCH                   PIC X.                                
       01  WS-CURR-MM                     PIC 9(2).                             
       01  WS-CURR-CCYY                   PIC 9(4).                             
       01  WS-CHECK-YR                    PIC S9999.                            
                                                                                
       01  WS-CURRENT-QTR                 PIC 9.                                
           88 I-QTR                                   VALUE 1.                  
           88 II-QTR                                  VALUE 2.                  
           88 III-QTR                                 VALUE 3.                  
           88 IV-QTR                                  VALUE 4.                  
                                                                                
       01  WS-EFF-DEBAR-QTR               PIC 9.                                
           88 DEBAR-I-QTR                             VALUE 1.                  
           88 DEBAR-II-QTR                            VALUE 2.                  
           88 DEBAR-III-QTR                           VALUE 3.                  
           88 DEBAR-IV-QTR                            VALUE 4.                  
                                                                                
       01  WS-DTE-DEBAR-EFF.                                                    
           05 WS-DEBAR-CCYY               PIC 9(4).                             
           05 WS-DEBAR-MM                 PIC 9(2).                             
           05 WS-DEBAR-DD                 PIC 9(2).                             
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       000-MAIN.                                                                
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F558                                                 
                OUTPUT P13F558O.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:4) TO WS-CURR-CCYY.                     
           MOVE FUNCTION CURRENT-DATE(5:2) TO WS-CURR-MM.                       
                                                                                
           PERFORM 100-READ-F558-REC THRU 100-EXIT.                             
           PERFORM 200-PROCESS-REC   THRU 200-EXIT                              
                   UNTIL EOF-F558.                                              
           PERFORM 900-CLOSE-ROUTINE.                                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-READ-F558-REC.                                                       
      *-------------------------------------------------------------            
                                                                                
           READ BP13F558 AT END MOVE 'Y'  TO WS-EOF-F558                        
                GO TO 100-EXIT.                                                 
           ADD 1 TO WS-F558-READ.                                               
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-PROCESS-REC.                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE F558-NUM-REGN TO WS-PREV-REGN.                                  
           MOVE SPACES        TO WS-DEBAR-FD.                                   
                                                                                
           PERFORM 250-INITIALIZE THRU 250-EXIT.                                
           PERFORM 300-STORE-F558 THRU 300-EXIT                                 
                          UNTIL F558-NUM-REGN NOT = WS-PREV-REGN                
                          OR    EOF-F558.                                       
                                                                                
           IF WS-QTR-MATCH = 'Y' AND WS-DEBAR-FD = 'Y'                          
              PERFORM 400-WRITE-OUTPUT THRU 400-EXIT                            
                          VARYING WS-F558-SUB FROM 1 BY 1                       
                          UNTIL WS-F558-SUB > WS-OCCUP-STORED.                  
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       250-INITIALIZE.                                                          
      *-------------------------------------------------------------            
                                                                                
           MOVE 1      TO WS-F558-SUB.                                          
           MOVE ZEROES TO WS-OCCUP-STORED.                                      
           INITIALIZE WS-F558-RECORD.                                           
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-STORE-F558.                                                          
      *-------------------------------------------------------------            
                                                                                
           IF F558-NUM-DEBARMENT-2 = 'CA' OR 'CB' OR 'CC' OR 'CD' OR            
                                   'CE' OR 'CF' OR 'CH' OR 'CI' OR 'CJ'         
              MOVE 'Y'               TO   WS-DEBAR-FD                           
              MOVE 'N'               TO   WS-QTR-MATCH                          
              PERFORM 350-CHECK-QTR  THRU 350-EXIT                              
           END-IF.                                                              
                                                                                
           MOVE BP13F558-REC TO WS-F558-REC (WS-F558-SUB).                      
           ADD 1 TO WS-F558-SUB.                                                
           ADD 1 TO WS-OCCUP-STORED.                                            
                                                                                
           PERFORM 100-READ-F558-REC THRU 100-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       350-CHECK-QTR.                                                           
      *-------------------------------------------------------------            
                                                                                
           MOVE F558-DTE-DEBAR-EFF(1:4)  TO WS-DEBAR-CCYY.                      
           MOVE F558-DTE-DEBAR-EFF(5:2)  TO WS-DEBAR-MM.                        
                                                                                
           IF WS-CURR-MM <= 03                                                  
              MOVE 1 TO WS-CURRENT-QTR                                          
           ELSE                                                                 
              IF WS-CURR-MM <= 06                                               
                 MOVE 2 TO WS-CURRENT-QTR                                       
              ELSE                                                              
                 IF WS-CURR-MM <= 09                                            
                    MOVE 3 TO WS-CURRENT-QTR                                    
                 ELSE                                                           
                    MOVE 4 TO WS-CURRENT-QTR                                    
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-CURRENT-QTR = 1                                                
              IF (WS-DEBAR-MM >= 10) AND                                        
                 (WS-CURR-CCYY - WS-DEBAR-CCYY = 1)                             
                 MOVE 'Y'  TO WS-QTR-MATCH                                      
              ELSE                                                              
                 IF (WS-DEBAR-MM <= WS-CURR-MM) AND                             
                    (WS-CURR-CCYY = WS-DEBAR-CCYY)                              
                    MOVE 'Y'  TO WS-QTR-MATCH                                   
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              IF WS-CURRENT-QTR = 2                                             
                 IF (WS-DEBAR-MM >= 01) AND                                     
                    (WS-CURR-CCYY = WS-DEBAR-CCYY)                              
                    MOVE 'Y' TO WS-QTR-MATCH                                    
                 END-IF                                                         
              ELSE                                                              
                 IF WS-CURRENT-QTR = 3                                          
                    IF (WS-DEBAR-MM >= 04) AND                                  
                       (WS-CURR-CCYY = WS-DEBAR-CCYY)                           
                       MOVE 'Y' TO WS-QTR-MATCH                                 
                    END-IF                                                      
                 ELSE                                                           
                    IF WS-CURRENT-QTR = 4                                       
                       IF (WS-DEBAR-MM >= 07) AND                               
                          (WS-CURR-CCYY = WS-DEBAR-CCYY)                        
                          MOVE 'Y' TO WS-QTR-MATCH                              
                        END-IF                                                  
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-WRITE-OUTPUT.                                                        
      *-------------------------------------------------------------            
                                                                                
            WRITE P13F558O-REC FROM WS-F558-REC(WS-F558-SUB)                    
            ADD 1 TO WS-F558O-WRITE.                                            
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           DISPLAY 'PROGRAM-ID : BP13C180 '.                                    
           DISPLAY 'UPDATE  HOUSEHOLD TYPE TO REQUEST FILE'.                    
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM F558 ' WS-F558-READ.                
           DISPLAY 'NO OF REORDS WRITE OUT       ' WS-F558O-WRITE.              
                                                                                
           CLOSE BP13F558                                                       
                 P13F558O.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
