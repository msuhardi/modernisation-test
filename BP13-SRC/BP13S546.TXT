  /*******************************************************************/         
  /*  SYSTEM OF COMMITMENT  (SOC)                                    */         
  /*=================================================================*/         
  /*-----------------------------------------------------------------*/         
  /* REPORT BY THE RANDOM NUMBER SEQUENCE                            */         
  /*-----------------------------------------------------------------*/         
  /* CHGE     DATE       BY     DESCRIPTION                          */         
  /* -------- --------   ----   -----------                          */         
  /* BP132500 30032004   PGS1   NEW PROGRAM                          */         
  /* BP132826 02/03/2006 YCH2   EXPAND REFNO FROM 6 TO 8             */         
  /*******************************************************************/         
  OPTIONS  NODATE NONUMBER;                                                     
   DATA BP13F510;                                                               
      INFILE BP13F510;                                                          
      INPUT @1   NUM_RANDOM $CHAR28.                                            
            @33  NUM_REF    $CHAR8.                                             
            @33  NUM_REFNO  $CHAR6.                                             
            @211 FLAT_TYPE  $CHAR2.;                                            
                                                                                
   PROC SORT;                                                                   
        BY NUM_REFNO;                                                           
                                                                                
   DATA BP17F000;                                                               
      INFILE BP17F000;                                                          
      INPUT @1   NUM_SCH_ACC $CHAR9.                                            
            @12  NUM_REFNO $CHAR6.;                                             
                                                                                
   PROC SORT;                                                                   
        BY NUM_REFNO;                                                           
                                                                                
   DATA BP13F825;                                                               
      INFILE BP13F825;                                                          
      INPUT @1   NUM_REFNO $CHAR6.                                              
            @9   NUM_NRIC  $CHAR9.                                              
            @28  NME_OCCP  $CHAR66.                                             
            @95  NUM_SEX   $CHAR1.                                              
            @96  NUM_MAR   $CHAR1.                                              
            @109 AMT_INC   $CHAR5.;                                             
                                                                                
   PROC SORT;                                                                   
        BY NUM_REFNO;                                                           
                                                                                
   DATA BP13F830;                                                               
      INFILE BP13F830;                                                          
      INPUT @1   NUM_REFNO  $CHAR6.                                             
            @33  NUM_BLK    $CHAR6.                                             
            @45  NUM_FLAT   $CHAR8.                                             
            @53  NUM_HSE_S  $CHAR3.                                             
            @56  ST_NME     $CHAR65.;                                           
                                                                                
   PROC SORT;                                                                   
        BY NUM_REFNO;                                                           
                                                                                
   DATA F510F000;                                                               
   MERGE BP13F510(IN=A) BP17F000(IN=B) BP13F825(IN=C) BP13F830(IN=D);           
      BY NUM_REFNO;                                                             
                                                                                
      IF A AND B AND C AND D;                                                   
                                                                                
   PROC SORT; BY NUM_SCH_ACC;                                                   
                                                                                
   DATA TEMP;                                                                   
   SET F510F000;                                                                
                                                                                
      /*--> READ BP17K200 TO GET HH-INCOME <-- */                               
      LENGTH K200_KEY $9.;                                                      
      K200_KEY=NUM_SCH_ACC;                                                     
      INFILE BP17K200 VSAM KEY=K200_KEY;                                        
      INPUT @;                                                                  
      RC=_IORC_; FBC=_FDBK_;                                                    
      IF RC=0 THEN                                                              
         DO;                                                                    
            INPUT @63  K200_NUM_NT     $CHAR3.                                  
                  @119 K200_HSE_INCOME $CHAR5.;                                 
            WS_HH_IC = K200_HSE_INCOME;                                         
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            WS_HH_IC = '   ';                                                   
            FILE LOG;                                                           
            PUT 'HH-INCOME NOT FOUND  ' K200_KEY;                               
            _ERROR_ = 0;                                                        
         END;                                                                   
                                                                                
      /*--> END - READ BP17K200     <-- */                                      
                                                                                
   DATA TEMP1;                                                                  
   SET TEMP;                                                                    
                                                                                
      /*--> READ BP13K060 TO DECODE SEX  <-- */                                 
      LENGTH K060_KEY $5.;                                                      
      K060_KEY='43' || NUM_SEX;                                                 
      INFILE BP13K060 VSAM KEY=K060_KEY;                                        
      INPUT @;                                                                  
      RC=_IORC_; FBC=_FDBK_;                                                    
      IF RC=0 THEN                                                              
         DO;                                                                    
            INPUT @6  K060_DESC1 $CHAR3.;                                       
            WS_SEX = K060_DESC1;                                                
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            WS_SEX = '   ';                                                     
            FILE LOG;                                                           
            PUT 'WS-SEX NOT DECODED  ' K060_KEY;                                
            _ERROR_ = 0;                                                        
         END;                                                                   
                                                                                
      /*--> END - READ BP13K060     <-- */                                      
                                                                                
   DATA TEMP2;                                                                  
   SET TEMP1;                                                                   
                                                                                
      /*--> READ BP13K060 TO DECODE MART STATUS <-- */                          
      LENGTH K060_KEY $5.;                                                      
      K060_KEY='44' || NUM_MAR;                                                 
      INFILE BP13K060 VSAM KEY=K060_KEY;                                        
      INPUT @;                                                                  
      RC=_IORC_; FBC=_FDBK_;                                                    
      IF RC=0 THEN                                                              
         DO;                                                                    
            INPUT @6  K060_DESC1 $CHAR3.;                                       
            WS_MAR = K060_DESC1;                                                
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            WS_MAR = '   ';                                                     
            FILE LOG;                                                           
            PUT 'WS-MAR NOT DECODED  ' K060_KEY;                                
            _ERROR_ = 0;                                                        
         END;                                                                   
                                                                                
      /*--> END - READ BP13K060     <-- */                                      
                                                                                
      PROC SORT;                                                                
           BY K200_NUM_NT NUM_RANDOM;                                           
                                                                                
   DATA _NULL_;                                                                 
   SET TEMP2 END=EOF;                                                           
       BY K200_NUM_NT NUM_RANDOM;                                               
                                                                                
      FILE P13L546A HEADER=H NOTITLES;                                          
                                                                                
      IF FIRST.K200_NUM_NT THEN                                                 
         DO;                                                                    
             PUT _PAGE_;                                                        
                 SNO=0;                                                         
         END;                                                                   
                                                                                
      IF NUM_HSE_S > '   ' THEN                                                 
         DO;                                                                    
           WS_LVL=NUM_FLAT || '-' || NUM_HSE_S;                                 
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
           WS_LVL=NUM_FLAT;                                                     
         END;                                                                   
                                                                                
      IF FIRST.NUM_RANDOM THEN                                                  
         DO;                                                                    
         SNO+1;                                                                 
         PUT @1 ' ';                                                            
         PUT @1  SNO                                                            
             @6  NUM_RANDOM  @35  FLAT_TYPE  @38 NUM_BLK   @45 WS_LVL           
             @58 ST_NME      @123 WS_HH_IC;                                     
         PUT @25  NUM_NRIC   @35 NME_OCCP   @110 WS_SEX                         
             @116 WS_MAR     @123  AMT_INC;                                     
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
         PUT @25  NUM_NRIC   @35 NME_OCCP   @110 WS_SEX                         
             @116 WS_MAR     @123  AMT_INC;                                     
         END;                                                                   
   RETURN;                                                                      
                                                                                
 H:                                                                             
   PAGE+1;                                                                      
   CUR_DATE=TODAY();                                                            
                                                                                
   PUT @1   'P13L546A      HDB3'                                                
       @41  'S Y S T E M  O F  C O M M I T M E N T'                             
       @113 'DATE : ' CUR_DATE DDMMYY10.;                                       
   PUT @34  'REPORT ON SELECTION OF RENTAL FLAT BY RANDOM NUMBER'               
       @113 'PAGE : ' PAGE//;                                                   
   PUT @1   'ESTATE CODE :'                                                     
       @15   K200_NUM_NT/;                                                      
   PUT @1   'S/NO'                                                              
       @6   'RANDOM NO'                                                         
       @35  'FT'                                                                
       @39  'ADDRESS/NAME'                                                      
       @122 'HH INC/';                                                          
   PUT @25  'NRIC NO'                                                           
       @38  'BLK '                                                              
       @45  'UNIT-NO'                                                           
       @58  'STREET NAME'                                                       
       @109 'SEX'                                                               
       @116 'MS'                                                                
       @123 'INCOME';                                                           
   PUT @1  '-----------------------------------------------------------'        
       @60 '-----------------------------------------------------------'        
       @119'------------';                                                      
   RETURN;                                                                      
                                                                                
   DATA FLAT;                                                                   
   SET TEMP2;                                                                   
   PROC SORT; BY K200_NUM_NT FLAT_TYPE NUM_RANDOM;                              
                                                                                
   DATA _NULL_;                                                                 
   SET FLAT END=EOF;                                                            
       BY K200_NUM_NT FLAT_TYPE NUM_RANDOM;                                     
                                                                                
      FILE P13L546B HEADER=R NOTITLES;                                          
                                                                                
      IF FIRST.K200_NUM_NT THEN                                                 
         DO;                                                                    
             PUT _PAGE_;                                                        
                 SNO=0;                                                         
         END;                                                                   
                                                                                
      IF NUM_HSE_S > '   ' THEN                                                 
         DO;                                                                    
           WS_LVL1=NUM_FLAT || '-' || NUM_HSE_S;                                
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
           WS_LVL1=NUM_FLAT;                                                    
         END;                                                                   
                                                                                
      IF FIRST.NUM_RANDOM THEN                                                  
         DO;                                                                    
         SNO+1;                                                                 
         PUT @1 ' ';                                                            
         PUT @1  SNO                                                            
             @6  NUM_RANDOM  @35  FLAT_TYPE  @38 NUM_BLK  @45 WS_LVL1           
             @58 ST_NME      @123 WS_HH_IC;                                     
         PUT @25  NUM_NRIC      @35 NME_OCCP      @110 WS_SEX                   
             @116 WS_MAR        @123  AMT_INC;                                  
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
         PUT @25  NUM_NRIC      @35 NME_OCCP      @110 WS_SEX                   
             @116 WS_MAR        @123  AMT_INC;                                  
         END;                                                                   
   RETURN;                                                                      
                                                                                
 R:                                                                             
   PAGE+1;                                                                      
   CUR_DATE=TODAY();                                                            
                                                                                
   PUT @1   'P13L546B      HDB3'                                                
       @41  'S Y S T E M  O F  C O M M I T M E N T'                             
       @113 'DATE : ' CUR_DATE DDMMYY10.;                                       
   PUT @28  'REPORT ON SELECTION OF RENTAL FLAT BY FLAT-TYPE & RANDOM '         
       @85  'NUMBER'                                                            
       @113 'PAGE : ' PAGE//;                                                   
   PUT @1   'ESTATE CODE : '                                                    
       @15   K200_NUM_NT/;                                                      
   PUT @1   'S/NO'                                                              
       @6   'RANDOM NO'                                                         
       @35  'FT'                                                                
       @39  'ADDRESS/NAME'                                                      
       @122 'HH INC/ ';                                                         
   PUT @25  'NRIC NO'                                                           
       @38  'BLK '                                                              
       @45  'UNIT-NO'                                                           
       @58  'STREET NAME'                                                       
       @109 'SEX'                                                               
       @116 'MS'                                                                
       @123 'INCOME';                                                           
   PUT @1  '-----------------------------------------------------------'        
       @60 '-----------------------------------------------------------'        
       @119'------------';                                                      
   RETURN;                                                                      
