       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C22C.                                                 
       AUTHOR.        SENTHILVEL.                                               
       DATE-WRITTEN.  09/03/2004.                                               
      * ========================================================== *            
      *                    BP13                                    *            
      * ========================================================== *            
      * OBJECTIVE  : TO OUTPUT THE RECORDS TO BP13K435 AND GENERATE*            
      *              A REPORT FOR THE WRITTEN CASES.               *            
      * ---------------------------------------------------------- *            
      * CHG REF   DATE     BY    DESCRIPTIONS                      *            
      * --------- ----     --    ------------                      *            
      * BP132486  09032004 PGS1  NEW PROGRAM                       *            
      * BP132658  07022005 LMS1  TO CHANGE TO CREATE RECORDS FOR   *            
      *                          VACANT INSTEAD OF ACCEPTED FLATS. *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
            SELECT BM06F120 ASSIGN TO BM06F120.                                 
                                                                                
            SELECT BP13K435 ASSIGN TO BP13K435                                  
                   ACCESS MODE     IS RANDOM                                    
                   ORGANIZATION    IS INDEXED                                   
                   RECORD KEY      IS K435-KEY-FLD                              
                   FILE STATUS     IS BP13K435-STATUS.                          
                                                                                
            SELECT BP13K431 ASSIGN TO BP13K431                                  
                   ACCESS MODE     IS RANDOM                                    
                   ORGANIZATION    IS INDEXED                                   
                   RECORD KEY      IS K431-KEY-FLD                              
                   FILE STATUS     IS BP13K431-STATUS.                          
                                                                                
            SELECT BP13L22C  ASSIGN TO BP13L22C.                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BM06F120                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 700 CHARACTERS                                      
            RECORDING MODE IS F.                                                
                                                                                
       COPY BM06F120.                                                           
                                                                                
       FD   BP13K435.                                                           
       COPY BP13K435.                                                           
                                                                                
       FD   BP13K431.                                                           
       COPY BP13K431.                                                           
                                                                                
       FD  BP13L22C                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS OMITTED                                                
           RECORDING MODE IS F.                                                 
       01  BP13L22C-REC                      PIC X(132).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01 BP13K435-STATUS                    PIC 9(02)  VALUE ZEROS.            
       01 BP13K431-STATUS                    PIC 9(02)  VALUE ZEROS.            
       01 WS-BM06F120-READ                   PIC 9(08)  VALUE ZEROS.            
       01 WS-BP13K435-WRITE                  PIC 9(08)  VALUE ZEROS.            
       01 WS-BP13K435-DUPKEY                 PIC 9(08)  VALUE ZEROS.            
       01 WS-PRT-CNT                         PIC 9(08)  VALUE ZEROS.            
       01 WS-PAGE-CNT                        PIC 9(04)  VALUE ZEROS.            
       01 WS-SNO-CNT                         PIC 9(04)  VALUE ZEROS.            
       01 WS-LINE-CNT                        PIC 9(02)  VALUE 60.               
                                                                                
       01 WS-TEL-BORROWER                    PIC X(08)  VALUE SPACES.           
       01 WS-DATE                            PIC X(08)  VALUE SPACES.           
       01 WS-TIME                            PIC X(08)  VALUE SPACES.           
       01 WS-EDIT-DATE                       PIC X(10)  VALUE SPACES.           
                                                                                
      *-------------------------------------------------------------            
      * REPORT LAYOUT                                                           
      *-------------------------------------------------------------            
       01  L22C-HEAD-01.                                                        
           05 FILLER                   PIC X(55)  VALUE 'BP13L22C'.             
           05 FILLER                   PIC X(20)  VALUE                         
                                            'SYSTEM OF COMMITMENT'.             
           05 FILLER                   PIC X(39)  VALUE SPACES.                 
           05 FILLER                   PIC X(7)   VALUE 'DATE : '.              
           05 L22C-DATE                PIC X(10)  VALUE SPACES.                 
                                                                                
       01  L22C-HEAD-02.                                                        
           05 FILLER                   PIC X(37)  VALUE SPACES.                 
           05 FILLER                   PIC X(56)  VALUE                         
              '     REPORT ON KEY BORROWED RECORDS ADDED FOR RP, '.             
           05 FILLER                   PIC X(21)  VALUE SPACES.                 
           05 FILLER                   PIC X(7)   VALUE 'PAGE : '.              
           05 L22C-PAGE                PIC ZZZ9   VALUE ZEROS.                  
                                                                                
       01  L22C-HEAD-02-A.                                                      
           05 FILLER                   PIC X(37)  VALUE SPACES.                 
           05 FILLER                   PIC X(56)  VALUE                         
              '                 ST AND CV VACANT FLATS           '.             
                                                                                
       01  L22C-HEAD-03.                                                        
           05 FILLER                   PIC X(55)  VALUE                         
              'S/NO KEY  FLAT ADDRESS                   BORROWER NAME '.        
           05 FILLER                   PIC X(27)  VALUE SPACES.                 
           05 FILLER                   PIC X(30)  VALUE                         
                                      'TEL NO              DTE-KEY   '.         
           05 FILLER                   PIC X(20)  VALUE '  DTE-BRWD'.           
                                                                                
       01  L22C-DTL-LINE.                                                       
           05  L22C-SNO                 PIC ZZZ9.                               
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  L22C-NUM-KEY-LOCN        PIC X(04).                              
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  L22C-ADDRESS             PIC X(30).                              
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  L22C-NME-BORROWER        PIC X(40).                              
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  L22C-TEL-BORROWER        PIC X(08).                              
           05  FILLER                   PIC X(12) VALUE SPACES.                 
           05  L22C-DTE-ACCEPTANCE      PIC X(10).                              
           05  FILLER                   PIC X(02) VALUE SPACES.                 
           05  L22C-DTE-BORROWED        PIC X(10).                              
           05  FILLER                   PIC X(02) VALUE SPACES.                 
                                                                                
       01  L22C-TOTAL-LINE.                                                     
           05  FILLER                   PIC X(18) VALUE SPACES.                 
           05  FILLER                   PIC X(30) VALUE                         
                                      'TOTAL NUMBER OF CASES: '.                
            05  L22C-TOTAL-CASE         PIC Z(08).                              
                                                                                
       01  L22C-LINE                    PIC X(132) VALUE ALL '-'.               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
            PERFORM 1000-OPEN-ROUTINE     THRU 1000-EXIT.                       
            PERFORM 2000-READ-BM06F120    THRU 2000-EXIT.                       
            PERFORM 2100-READ-BP13K431    THRU 2100-EXIT.                       
            PERFORM 3000-PROCESS-DETAIL   THRU 3000-EXIT                        
                    UNTIL F120-SCH-ACC-NO = HIGH-VALUES.                        
            PERFORM 5000-PRINT-TOTALS     THRU 5000-EXIT.                       
            PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT.                       
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
            OPEN  INPUT  BM06F120                                               
                         BP13K431                                               
                  I-O    BP13K435                                               
                  OUTPUT BP13L22C.                                              
                                                                                
            IF BP13K435-STATUS NOT = 00 AND 97                                  
               DISPLAY 'OPENING ERROR K435-STATUS IS ' BP13K435-STATUS          
               MOVE BP13K435-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                        
            END-IF.                                                             
                                                                                
            IF BP13K431-STATUS NOT = 00 AND 97                                  
               DISPLAY 'OPENING ERROR K431-STATUS IS ' BP13K431-STATUS          
               MOVE BP13K431-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                        
            END-IF.                                                             
                                                                                
            MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DATE.                         
            STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)               
                   DELIMITED BY SIZE INTO WS-EDIT-DATE.                         
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       2000-READ-BM06F120.                                                      
      ******************************************************************        
            READ BM06F120 AT END MOVE HIGH-VALUES TO F120-SCH-ACC-NO            
                 GO TO 2000-EXIT.                                               
            ADD 1   TO WS-BM06F120-READ.                                        
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       2100-READ-BP13K431.                                                      
      ******************************************************************        
            MOVE SPACES       TO K431-KEY-FLD.                                  
            MOVE 'HAD001'     TO K431-NUM-BORROWER.                             
            READ BP13K431.                                                      
            EVALUATE BP13K431-STATUS                                            
              WHEN 00                                                           
                   PERFORM 2200-GET-TEL-NUMBER THRU 2200-EXIT                   
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BP13K431, KEY: '                
                            K431-KEY-FLD                                        
                   MOVE SPACES  TO BP13K431-REC  WS-TEL-BORROWER                
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BP13K431,STATU ' BP13K431-STATUS         
                   MOVE BP13K431-STATUS       TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                    
            END-EVALUATE.                                                       
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       2200-GET-TEL-NUMBER.                                                     
      ******************************************************************        
            MOVE  SPACES   TO  WS-TEL-BORROWER.                                 
            IF K431-NUM-TEL-OFFICE  NOT = SPACES AND LOW-VALUES                 
               MOVE K431-NUM-TEL-OFFICE  TO  WS-TEL-BORROWER                    
            ELSE                                                                
               IF K431-NUM-TEL-HP  NOT = SPACES AND LOW-VALUES                  
                  MOVE K431-NUM-TEL-HP  TO  WS-TEL-BORROWER                     
               ELSE                                                             
                  IF K431-NUM-TEL-PAGER  NOT = SPACES AND LOW-VALUES            
                     MOVE K431-NUM-TEL-PAGER  TO  WS-TEL-BORROWER               
                  END-IF                                                        
               END-IF                                                           
            END-IF.                                                             
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3000-PROCESS-DETAIL.                                                     
      ******************************************************************        
            PERFORM 3100-MOVE-VALETO-K435  THRU 3100-EXIT                       
                                                                                
            WRITE BP13K435-REC                                                  
            EVALUATE BP13K435-STATUS                                            
               WHEN 00                                                          
               WHEN 02                                                          
                    ADD 1 TO WS-BP13K435-WRITE                                  
               WHEN 22                                                          
                    DISPLAY 'DUPLICATE-KEY  : ' F120-SCH-ACC-NO                 
                    ADD  1 TO WS-BP13K435-DUPKEY                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K435 WRITE ERROR : ' BP13K435-STATUS           
                    DISPLAY 'BP13K435 KEY-FLD     : ' K435-KEY-FLD              
                    MOVE BP13K435-STATUS         TO   RETURN-CODE               
                    PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                 
            END-EVALUATE.                                                       
            PERFORM 4000-PRINT-REPORT   THRU 4000-EXIT.                         
            ADD    1                    TO   WS-PRT-CNT.                        
            PERFORM 2000-READ-BM06F120  THRU 2000-EXIT.                         
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       3100-MOVE-VALETO-K435.                                                   
      ******************************************************************        
            MOVE SPACES               TO BP13K435-REC.                          
            INITIALIZE                   BP13K435-REC.                          
            MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DATE.                         
            MOVE FUNCTION CURRENT-DATE(9:8) TO WS-TIME.                         
                                                                                
            MOVE F120-SCH-ACC-NO      TO K435-NUM-HDB-REF.                      
            MOVE 'HAD001'             TO K435-NUM-BORROWER                      
                                         K435-ALT-NUM-BORROWER.                 
            MOVE WS-DATE              TO K435-DTE-KEY-BORROW.                   
            MOVE F120-BO-CODE         TO K435-NUM-KEY-LOCN                      
                                         K435-NUM-KEY-LOCN2.                    
            MOVE F120-BLK-NO          TO K435-NUM-BLK                           
                                         K435-NUM-BLK2.                         
            MOVE F120-CDE-TYP-ACTUSE  TO K435-NUM-ROOM-TYPE.                    
            MOVE F120-MAIN-UNIT       TO K435-NUM-UNIT-MAIN.                    
            MOVE F120-SUB-UNIT        TO K435-NUM-UNIT-SUB.                     
            MOVE F120-LEVEL-NO        TO K435-NUM-LEVEL.                        
            MOVE F120-STREET-NAME     TO K435-NME-STREET.                       
            MOVE 'ALL'                TO K435-NUM-KEY-BORROW.                   
            MOVE F120-ESTATE          TO K435-CDE-ESTATE.                       
            MOVE F120-NEIGHBOURHOOD   TO K435-NUM-NEIGH.                        
            MOVE F120-CONTRACT-NO     TO K435-NUM-CNTRCT.                       
            MOVE 'BP13C22C'           TO K435-NUM-USERID.                       
            MOVE WS-DATE              TO K435-DTE-UPDATE.                       
            MOVE WS-TIME              TO K435-TME-UPDATE.                       
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       4000-PRINT-REPORT.                                                       
      ******************************************************************        
            PERFORM 4100-CHECK-LINE-CNT  THRU 4100-EXIT.                        
            MOVE SPACES         TO   L22C-DTL-LINE.                             
            WRITE BP13L22C-REC  FROM L22C-DTL-LINE.                             
            ADD 1               TO   WS-LINE-CNT.                               
                                                                                
            PERFORM 4100-CHECK-LINE-CNT  THRU 4100-EXIT.                        
            ADD 1                      TO WS-SNO-CNT.                           
            MOVE WS-SNO-CNT            TO L22C-SNO.                             
            MOVE F120-BO-CODE          TO L22C-NUM-KEY-LOCN.                    
            STRING 'BLK ' F120-BLK-NO  ' #'   F120-LEVEL-NO                     
                   '-'    F120-MAIN-UNIT  '-' F120-SUB-UNIT                     
                   DELIMITED BY SIZE INTO L22C-ADDRESS                          
            END-STRING.                                                         
                                                                                
            MOVE K431-NME-BORROWER     TO L22C-NME-BORROWER.                    
            MOVE WS-TEL-BORROWER       TO L22C-TEL-BORROWER.                    
                                                                                
            STRING F120-DTE-KEY-AVAIL (7:2) '/'                                 
                   F120-DTE-KEY-AVAIL (5:2) '/'                                 
                   F120-DTE-KEY-AVAIL (1:4)                                     
                   DELIMITED BY SIZE   INTO L22C-DTE-ACCEPTANCE                 
            END-STRING.                                                         
                                                                                
            STRING WS-DATE (7:2) '/' WS-DATE (5:2) '/'                          
                   WS-DATE (1:4)                                                
                   DELIMITED BY SIZE   INTO L22C-DTE-BORROWED                   
            END-STRING.                                                         
                                                                                
            WRITE BP13L22C-REC  FROM L22C-DTL-LINE.                             
            ADD 1          TO WS-LINE-CNT.                                      
                                                                                
      ***-- PRINT STREET NAME (NEXT LINE)                                       
            MOVE SPACES                TO L22C-DTL-LINE.                        
            PERFORM 4100-CHECK-LINE-CNT          THRU 4100-EXIT.                
            MOVE F120-STREET-NAME      TO L22C-ADDRESS.                         
                                                                                
            WRITE BP13L22C-REC  FROM L22C-DTL-LINE.                             
            ADD 1          TO WS-LINE-CNT.                                      
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       4100-CHECK-LINE-CNT.                                                     
      ******************************************************************        
            IF WS-LINE-CNT > 55                                                 
               ADD   1             TO   WS-PAGE-CNT                             
               MOVE WS-PAGE-CNT    TO   L22C-PAGE                               
               MOVE WS-EDIT-DATE   TO   L22C-DATE                               
               WRITE BP13L22C-REC  FROM L22C-HEAD-01 AFTER PAGE                 
               WRITE BP13L22C-REC  FROM L22C-HEAD-02                            
               WRITE BP13L22C-REC  FROM L22C-HEAD-02-A                          
               WRITE BP13L22C-REC  FROM L22C-LINE    AFTER 2                    
               WRITE BP13L22C-REC  FROM L22C-HEAD-03                            
               WRITE BP13L22C-REC  FROM L22C-LINE    AFTER 2                    
               MOVE 8              TO   WS-LINE-CNT                             
            END-IF.                                                             
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       5000-PRINT-TOTALS.                                                       
      ******************************************************************        
            PERFORM 4100-CHECK-LINE-CNT   THRU 4100-EXIT.                       
            MOVE SPACES         TO   L22C-DTL-LINE.                             
            WRITE BP13L22C-REC  FROM L22C-DTL-LINE.                             
            ADD 1               TO   WS-LINE-CNT.                               
                                                                                
            PERFORM 4100-CHECK-LINE-CNT   THRU 4100-EXIT.                       
            MOVE WS-PRT-CNT     TO   L22C-TOTAL-CASE.                           
            WRITE BP13L22C-REC  FROM L22C-TOTAL-LINE.                           
            ADD 1               TO   WS-LINE-CNT.                               
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       9000-CLOSE-ROUTINE.                                                      
      ******************************************************************        
            DISPLAY ' '.                                                        
            DISPLAY '-----------------------------------'.                      
            DISPLAY ' CONTROL LIST : BP13C22C '.                                
            DISPLAY ' RUN DATE     : ' WS-EDIT-DATE.                            
            DISPLAY '-----------------------------------'.                      
            DISPLAY ' 1) TOT BM06F120 READ      : ' WS-BM06F120-READ.           
            DISPLAY ' 2) TOT BP13K435 WRITE     : ' WS-BP13K435-WRITE.          
            DISPLAY ' 3) TOT BP13K435 DUP KEY   : ' WS-BP13K435-DUPKEY.         
            DISPLAY ' 4) TOT RECORDS PRINTED    : ' WS-PRT-CNT.                 
            DISPLAY ' '.                                                        
            DISPLAY 'PLS CHECK '.                                               
            DISPLAY '1) = 2) + 3) '.                                            
                                                                                
            CLOSE  BM06F120                                                     
                   BP13K435                                                     
                   BP13K431                                                     
                   BP13L22C.                                                    
                                                                                
            IF BP13K435-STATUS NOT = 00 AND 97                                  
               DISPLAY 'CLOSING ERROR, BP13K435   ' BP13K435-STATUS             
               MOVE BP13K435-STATUS TO RETURN-CODE                              
            END-IF.                                                             
                                                                                
            IF BP13K431-STATUS NOT = 00 AND 97                                  
               DISPLAY 'CLOSING ERROR, BP13K431   ' BP13K431-STATUS             
               MOVE BP13K431-STATUS TO RETURN-CODE                              
            END-IF.                                                             
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
