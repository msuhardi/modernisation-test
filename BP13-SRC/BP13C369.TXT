      *----------------------------------------------------------------*        
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------------*        
       PROGRAM-ID.    BP13C369.                                                 
       AUTHOR.        SK.                                                       
       DATE-WRITTEN.  03/02/2004.                                               
                                                                                
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  PRINT REPORT OF SURRENDER OF STUDIO APPARTMENT*        
      *                  COMPLETIONS RECEIVED                          *        
      *                                                                *        
      *   INPUT FILES :  1.  BP13F690  -  FIS/SLS INTERFACE FILE       *        
      *                                                                *        
      *   OUPUT FILES :  1.  BP13L369                                  *        
      *----------------------------------------------------------------*        
      *                                                                *        
      *   CHGE-NO   BY    DATE      DESCRIPTION                        *        
      *   --------  ---  ---------- -----------                        *        
      *   BP132292  SK   03/02/2004 NEW PGM                            *        
      *   BP132571  SK   27/07/2004 PRINT NULL REPORT                  *        
      *   BP135903  FNP1 04/02/2016 ADD LEASE TENURE                   *        
      *   BP136398  FNP1 17/08/2016 EXPAND F690 FROM 250 TO 600        *        
      *   BP136965  FNP1 25/09/2017 CHANGE OUTPUT FILE TO SEMICOLON    *        
      *                             DELIMITED FILE                     *        
      *   BP138300  KR13 28/05/2020 ADD REPORT ID FOR OP FILE          *        
      *   BP138944  FP6  14/10/2021 EXPAND F690 FROM 600 TO 800        *        
      ******************************************************************        
                                                                                
      *----------------------------------------------------------------*        
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------------*        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F690  ASSIGN        TO BP13F690.                          
           SELECT SY02F001  ASSIGN        TO SY02F001.                          
           SELECT BP13L369  ASSIGN        TO BP13L369.                          
                                                                                
      *----------------------------------------------------------------*        
       DATA DIVISION.                                                           
      *----------------------------------------------------------------*        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F690                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 800 CHARACTERS                                       
           RECORDING MODE F                                                     
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F690.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13L369                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS  250 CHARACTERS.                                     
                                                                                
       01  BP13L369-REC         PIC X(250).                                     
                                                                                
      *----------------------------------------------------------------*        
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------------*        
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F690-READ        PIC 9(8)  VALUE 0.                        
                                                                                
       01  WS-SWITCHES.                                                         
           05 WS-F690-EOF             PIC X(1)  VALUE 'N'.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-SEQ                  PIC 9(3)  VALUE 0.                        
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY            PIC 9(4).                                 
           05  WS-SYS-MM              PIC 9(2).                                 
           05  WS-SYS-DD              PIC 9(2).                                 
                                                                                
       01  WS-TP-DATE.                                                          
           05  WS-TP-CCYY               PIC X(4)    VALUE SPACES.               
           05  WS-TP-MM                 PIC X(2)    VALUE SPACES.               
           05  WS-TP-DD                 PIC X(2)    VALUE SPACES.               
                                                                                
       01  WS-TP-DATE1                  REDEFINES WS-TP-DATE.                   
           05  WS-TP-CCYY1              PIC 9(4).                               
           05  WS-TP-MM1                PIC 9(2).                               
           05  WS-TP-DD1                PIC 9(2).                               
                                                                                
       01  WS-EFF-DATE.                                                         
           05  WS-EFF-CCYY              PIC 9(4)    VALUE ZEROS.                
           05  WS-EFF-MM                PIC 9(2)    VALUE ZEROS.                
           05  WS-EFF-DD                PIC 9(2)    VALUE ZEROS.                
                                                                                
       01  WS-UNIT.                                                             
           05  WS-HSELVL                PIC X(2)    VALUE SPACES.               
           05  FILLER                   PIC X(1)    VALUE '-'.                  
           05  WS-HSEMAIN               PIC X(4)    VALUE SPACES.               
           05  FILLER                   PIC X(1)    VALUE '-'.                  
           05  WS-HSESUB                PIC X(3)    VALUE SPACES.               
                                                                                
       01  WS-DTE-SURR.                                                         
           05  WS-SURR-CCYY             PIC 9(4)    VALUE ZEROS.                
           05  WS-SURR-MM               PIC 9(2)    VALUE ZEROS.                
           05  WS-SURR-DD               PIC 9(2)    VALUE ZEROS.                
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-CNT-LINE-ON-PAGE       PIC 9(8)    VALUE 66.                   
           05 WS-CNT-PRINT-L369         PIC 9(8)    VALUE 1.                    
           05 WS-CNT-WRITE-L369         PIC 9(8)    VALUE 0.                    
           05 WS-NO-OF-PAGE             PIC 9(8)    VALUE 0.                    
           05 WS-DIFF-MTH               PIC 9(2)    VALUE ZEROS.                
           05 WS-TINC                   PIC 9(6)V99 VALUE ZEROS.                
           05 WS-YTD-INC                PIC 9(6)V99 VALUE ZEROS.                
           05 WS-BAL-DEF                PIC 9(6)V99 VALUE ZEROS.                
           05 WS-DIF-BET-YTD            PIC 9(6)V99 VALUE ZEROS.                
           05 WS-INC-SURR-AMT           PIC 9(8)V99 VALUE ZEROS.                
           05 WS-DIF-BET-BAL            PIC S9(8)V99 VALUE ZEROS.               
           05 WS-DIF-SUR-AMT            PIC S9(6)V99 VALUE ZEROS.               
           05 WS-COMP-AMT-TOT           PIC S9(8)V99 VALUE ZEROS.               
           05 WS-TOTAL                  PIC S9(8)V99 VALUE ZEROS.               
           05 WS-FT                     PIC X(2)    VALUE SPACES.               
           05 WS-FT-PREV                PIC X(2)    VALUE SPACES.               
           05 WS-FLAG                   PIC X(1)    VALUE 'Y'.                  
           05 WS-FLAG1                  PIC X(1)    VALUE 'N'.                  
           05 WS-MATCH                  PIC X(1)    VALUE ' '.                  
           05 WS-LAST-SCH-ACC           PIC X(11)   VALUE SPACES.               
           05 WS-CNT                    PIC 9(01)   VALUE ZEROS.                
           05 WS-DIFF-LINE              PIC 9(02)   VALUE ZEROS.                
           05 WS-SURR-AMT-ED            PIC $,$$$,$$9.99.                       
           05 WS-SN-NO                  PIC 9(4)    VALUE 0.                    
                                                                                
       01  WS-RECORD.                                                           
           05  FILLER                   PIC X(30) VALUE SPACES.                 
           05  WS-F690-NUM-BLOCK        PIC X(05)  VALUE SPACES.                
           05  FILLER                   PIC X(71)  VALUE SPACES.                
           05  WS-F690-NME-STREET       PIC X(32).                              
           05  WS-F690-NUM-UNIT.                                                
               10  WS-F690-NUM-HSELVL   PIC XX.                                 
               10  WS-F690-NUM-HSEMAIN  PIC X(4).                               
               10  WS-F690-NUM-HSESUB   PIC X(3).                               
           05  WS-F690-NUM-POSTAL       PIC X(6).                               
           05  FILLER                   PIC X(97)  VALUE SPACES.                
                                                                                
       01  WS-FOOTER-VAR.                                                       
           05  WS-REQ-SURR-FT           PIC 9(8)V99 VALUE ZEROS.                
           05  WS-COMPN-FT              PIC 9(8)V99 VALUE ZEROS.                
           05  WS-SURR-BAL-FT           PIC 9(8)V99 VALUE ZEROS.                
                                                                                
       01  WS-SUBTOT-VAR.                                                       
           05  WS-REQ-SURR-ST           PIC 9(8)V99 VALUE ZEROS.                
           05  WS-COMPN-ST              PIC 9(8)V99 VALUE ZEROS.                
           05  WS-SURR-BAL-ST           PIC 9(8)V99 VALUE ZEROS.                
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-EDITED-DD           PIC 9(2).                                 
           05  FILLER                 PIC X(1)  VALUE '/'.                      
           05  WS-EDITED-MM           PIC 9(2).                                 
           05  FILLER                 PIC X(1)  VALUE '/'.                      
           05  WS-EDITED-CCYY         PIC 9(4).                                 
                                                                                
      *-----------------------------------------------------------*             
      *    WS FOR CONTROL LISTING                                 *             
      *-----------------------------------------------------------*             
                                                                                
       01  L369-PR-HEAD-01.                                                     
           05  FILLER                   PIC X(14) VALUE 'BP13L369'.             
           05  FILLER                   PIC X(8)  VALUE 'HDBCAT 3'.             
           05  FILLER                   PIC X(25) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(13) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  L369-PRINT-DATE-1        PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'PAGE: '.               
           05  L369-PRINT-PAGE          PIC ZZZ9.                               
                                                                                
       01  L369-PR-HEAD-02.                                                     
           05  FILLER                   PIC X(54) VALUE SPACES.                 
           05  FILLER                   PIC X(50) VALUE                         
                'SURRENDER OF STUDIO APPARMENT'.                                
                                                                                
       01  L369-PR-HEAD-02-B.                                                   
           05  FILLER                   PIC X(35) VALUE SPACES.                 
           05  FILLER                   PIC X(70) VALUE                         
                'COMPENSATION AMOUNT RECEIVED FROM PAYABLES & RECEIVABLE        
      -    'S SYSTEM'.                                                          
                                                                                
       01  L369-PR-HEAD-03.                                                     
           05  FILLER          PIC X(4)  VALUE ' SN'.                           
           05  FILLER          PIC X(1)  VALUE ';'.                             
           05  FILLER          PIC X(11) VALUE 'SCHEME A/C'.                    
           05  FILLER          PIC X(1)  VALUE ';'.                             
           05  FILLER          PIC X(2)  VALUE 'FT'.                            
           05  FILLER          PIC X(1)  VALUE ';'.                             
           05  FILLER          PIC X(10) VALUE 'TP DTE'.                        
           05  FILLER          PIC X(1)  VALUE ';'.                             
           05  FILLER          PIC X(13) VALUE 'SALES EFF DTE'.                 
           05  FILLER          PIC X(1)  VALUE ';'.                             
           05  FILLER          PIC X(12) VALUE 'REQ SURR DTE'.                  
           05  FILLER          PIC X(1)  VALUE ';'.                             
           05  FILLER          PIC X(13) VALUE 'REQ SURR AMT'.                  
           05  FILLER          PIC X(1)  VALUE ';'.                             
           05  FILLER          PIC X(10) VALUE 'VCHR DTE'.                      
           05  FILLER          PIC X(1)  VALUE ';'.                             
           05  FILLER          PIC X(20) VALUE 'PRS REF'.                       
           05  FILLER          PIC X(1)  VALUE ';'.                             
           05  FILLER          PIC X(14) VALUE 'VCHR AMT'.                      
           05  FILLER          PIC X(1)  VALUE ';'.                             
           05  FILLER          PIC X(14) VALUE 'SURR COMPL DTE'.                
           05  FILLER          PIC X(1)  VALUE ';'.                             
           05  FILLER          PIC X(06) VALUE 'TENURE'.                        
           05  FILLER          PIC X(1)  VALUE ';'.                             
           05  FILLER          PIC X(70) VALUE 'ADDRESS'.                       
           05  FILLER          PIC X(1)  VALUE ';'.                             
                                                                                
       01  L369-PR-DETAILS-1.                                                   
           05  L369-NUMBER           PIC ZZZ9  BLANK WHEN ZERO.                 
           05  FIL01                 PIC X(1)  VALUE SPACES.                    
           05  L369-SCH-ACC          PIC X(11) VALUE SPACES.                    
           05  FIL02                 PIC X(1)  VALUE SPACES.                    
           05  L369-PRT-FLAT         PIC X(02) VALUE SPACES.                    
           05  FIL03                 PIC X(1)  VALUE SPACES.                    
           05  L369-TP-DTE           PIC X(10) VALUE SPACES.                    
           05  FIL04                 PIC X(1)  VALUE SPACES.                    
           05  L369-EFF-DTE          PIC X(13) VALUE SPACES.                    
           05  FIL05                 PIC X(1)  VALUE SPACES.                    
           05  L369-REQ-SURR-DTE     PIC X(12) VALUE SPACES.                    
           05  FIL06                 PIC X(1)  VALUE SPACES.                    
           05  L369-SURR-AMT         PIC X(13) VALUE SPACES.                    
           05  FIL07                 PIC X(1)  VALUE SPACES.                    
           05  L369-DTE-COMP         PIC X(10) VALUE SPACES.                    
           05  FIL08                 PIC X(1)  VALUE SPACES.                    
           05  L369-PRS-REF          PIC X(20) VALUE SPACES.                    
           05  FIL09                 PIC X(1)  VALUE SPACES.                    
           05  L369-COMP-AMT         PIC $,$$$,$$9.99.                          
           05  L369-COMP-AMT-SIGN    PIC X(02) VALUE SPACES.                    
           05  FIL10                 PIC X(1)  VALUE SPACES.                    
           05  L369-COMPL-DTE        PIC X(14) VALUE SPACES.                    
           05  FIL12                 PIC X(1)  VALUE SPACES.                    
           05  L369-TENURE           PIC X(02) VALUE SPACES.                    
           05  FILLER                PIC X(04) VALUE SPACES.                    
           05  FIL13                 PIC X(1)  VALUE SPACES.                    
           05  L369-ADDRESS          PIC X(70) VALUE SPACES.                    
           05  FIL14                 PIC X(1)  VALUE SPACES.                    
                                                                                
       01  L369-PR-BLANK             PIC X(132) VALUE SPACES.                   
                                                                                
       01  L369-PR-NO-RECORD.                                                   
           05  FILLER                   PIC X(05) VALUE SPACES.                 
           05  FILLER                   PIC X(75) VALUE                         
               'NO RECORDS TO BE PRINTED!!!!'.                                  
                                                                                
      *----------------------------------------------------------------*        
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------------*        
                                                                                
      *----------------------------------------------------------------*        
       0000-MAIN-ROUTINE.                                                       
      *----------------------------------------------------------------*        
                                                                                
           PERFORM  1000-OPEN-FILES      THRU    1000-EXIT.                     
           PERFORM  1100-READ-SY02F001   THRU    1100-EXIT.                     
           PERFORM  2000-READ-BP13F690   THRU    2000-EXIT.                     
           PERFORM  3000-PROCESS         THRU    3000-EXIT                      
                    UNTIL WS-F690-EOF = 'Y'.                                    
           IF WS-CNT-F690-READ < 1                                              
              WRITE BP13L369-REC  FROM  L369-PR-NO-RECORD AFTER 5               
           END-IF.                                                              
           PERFORM  9000-CLOSE-ROUTINE   THRU    9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------*        
                                                                                
           OPEN INPUT  BP13F690                                                 
                       SY02F001                                                 
                OUTPUT BP13L369.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
           MOVE WS-SYS-DD               TO WS-EDITED-DD.                        
           MOVE WS-SYS-MM               TO WS-EDITED-MM.                        
           MOVE WS-SYS-CCYY             TO WS-EDITED-CCYY.                      
                                                                                
           WRITE BP13L369-REC    FROM   L369-PR-HEAD-01   AFTER PAGE.           
           WRITE BP13L369-REC    FROM   L369-PR-HEAD-02   AFTER 1.              
           WRITE BP13L369-REC    FROM   L369-PR-HEAD-02-B AFTER 1.              
           WRITE BP13L369-REC    FROM   L369-PR-HEAD-03.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1100-READ-SY02F001.                                                      
      *----------------------------------------------------------------*        
                                                                                
           READ SY02F001 AT END                                                 
                GO TO 1100-EXIT                                                 
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-READ-BP13F690.                                                      
      *----------------------------------------------------------------*        
                                                                                
           READ BP13F690                                                        
                AT END                                                          
                   MOVE 'Y' TO WS-F690-EOF                                      
                NOT AT END                                                      
                   ADD 1 TO WS-CNT-F690-READ                                    
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
      *----------------------------------------------------------------*        
       3000-PROCESS.                                                            
      *----------------------------------------------------------------*        
                                                                                
           PERFORM 3100-PROCESS1      THRU 3100-EXIT.                           
                                                                                
           PERFORM 2000-READ-BP13F690 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3100-PROCESS1.                                                           
      *----------------------------------------------------------------*        
                                                                                
           PERFORM 3500-COMPUTE-PARA THRU 3500-EXIT.                            
                                                                                
           PERFORM 4000-MOVE-PARA      THRU 4000-EXIT.                          
           PERFORM 4100-WRITE-BP13L369 THRU 4100-EXIT.                          
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3500-COMPUTE-PARA.                                                       
      *----------------------------------------------------------------*        
                                                                                
           IF F690-AMT-COMPENSATION NOT NUMERIC                                 
              MOVE ZEROS TO F690-AMT-COMPENSATION                               
           END-IF.                                                              
                                                                                
           IF F690-AMT-SURRENDER-BAL NOT NUMERIC                                
              MOVE ZEROS TO F690-AMT-SURRENDER-BAL                              
           END-IF.                                                              
                                                                                
           IF F690-AMT-SURRENDER-COMPUTED NOT NUMERIC                           
              MOVE ZEROS TO F690-AMT-SURRENDER-COMPUTED                         
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4000-MOVE-PARA.                                                          
      *----------------------------------------------------------------*        
                                                                                
           PERFORM 4300-GET-EFF-DTE          THRU 4300-EXIT.                    
                                                                                
           MOVE SPACES                       TO BP13L369-REC.                   
                                                                                
           ADD 1 TO WS-SN-NO.                                                   
           MOVE WS-SN-NO                     TO L369-NUMBER.                    
                                                                                
           MOVE  F690-NUM-SCH-ACC            TO L369-SCH-ACC.                   
           MOVE F690-CDE-TYP-ACTUSE          TO L369-PRT-FLAT.                  
                                                                                
           IF F690-DTE-KEY-ISSUE NOT = SPACES AND LOW-VALUES                    
              STRING F690-DTE-KEY-ISSUE(7:2) '/'                                
                     F690-DTE-KEY-ISSUE(5:2) '/'                                
                     F690-DTE-KEY-ISSUE                                         
                     DELIMITED BY SIZE                                          
                     INTO  L369-TP-DTE                                          
              END-STRING                                                        
           ELSE                                                                 
              MOVE SPACES                    TO L369-TP-DTE                     
           END-IF.                                                              
                                                                                
           IF F690-DTE-SURRENDER-REQUEST NOT = SPACES AND LOW-VALUES            
              STRING F690-DTE-SURRENDER-REQUEST(7:2) '/'                        
                     F690-DTE-SURRENDER-REQUEST(5:2) '/'                        
                     F690-DTE-SURRENDER-REQUEST(1:4)                            
                     DELIMITED BY SIZE                                          
                     INTO  L369-REQ-SURR-DTE                                    
              END-STRING                                                        
           ELSE                                                                 
              MOVE SPACES                    TO L369-REQ-SURR-DTE               
           END-IF.                                                              
                                                                                
           STRING WS-EFF-DATE(7:2) '/'                                          
                  WS-EFF-DATE(5:2) '/'                                          
                  WS-EFF-DATE(1:4)                                              
                  DELIMITED BY SIZE                                             
                  INTO  L369-EFF-DTE                                            
           END-STRING.                                                          
                                                                                
           MOVE F690-AMT-SURRENDER-COMPUTED  TO WS-SURR-AMT-ED                  
           MOVE WS-SURR-AMT-ED               TO L369-SURR-AMT                   
                                                                                
           MOVE F690-AMT-COMPENSATION        TO L369-COMP-AMT                   
      *    MOVE F690-NUM-VOUCHER-DR-CR       TO L369-COMP-AMT-SIGN              
                                                                                
           IF F690-DTE-COMPENSATION-PD NOT = SPACES AND LOW-VALUES              
              STRING F690-DTE-COMPENSATION-PD(7:2) '/'                          
                     F690-DTE-COMPENSATION-PD(5:2) '/'                          
                     F690-DTE-COMPENSATION-PD(1:4)                              
                     DELIMITED BY SIZE                                          
                     INTO L369-DTE-COMP                                         
              END-STRING                                                        
           ELSE                                                                 
              MOVE SPACES                    TO L369-DTE-COMP                   
           END-IF.                                                              
                                                                                
           MOVE F690-NUM-PRS-REF             TO L369-PRS-REF                    
                                                                                
           IF F690-DTE-SURRENDER-COMPLETED NOT = SPACES AND LOW-VALUES          
              STRING F690-DTE-SURRENDER-COMPLETED(7:2) '/'                      
                     F690-DTE-SURRENDER-COMPLETED(5:2) '/'                      
                     F690-DTE-SURRENDER-COMPLETED(1:4)                          
                     DELIMITED BY SIZE                                          
                     INTO L369-COMPL-DTE                                        
              END-STRING                                                        
           ELSE                                                                 
              MOVE SPACES                    TO L369-COMPL-DTE                  
           END-IF.                                                              
                                                                                
           MOVE F690-NUM-LEASE-TENURE         TO L369-TENURE.                   
                                                                                
           MOVE F690-NUM-HSELVL               TO WS-HSELVL.                     
           MOVE F690-NUM-HSEMAIN              TO WS-HSEMAIN.                    
           MOVE F690-NUM-HSESUB               TO WS-HSESUB.                     
                                                                                
           STRING 'BLK-' F690-NUM-BLOCK ' #' WS-UNIT                            
                  ' ' F690-NME-STREET                                           
                  ' S(' F690-NUM-POSTAL ')'                                     
                  DELIMITED BY SIZE                                             
                  INTO  L369-ADDRESS                                            
           END-STRING.                                                          
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4100-WRITE-BP13L369.                                                     
      *----------------------------------------------------------------*        
                                                                                
           MOVE ';' TO   FIL01  FIL02  FIL03  FIL04                             
                         FIL05  FIL06  FIL07  FIL08                             
                         FIL09  FIL10  FIL12                                    
                         FIL13  FIL14.                                          
                                                                                
           WRITE BP13L369-REC      FROM  L369-PR-DETAILS-1.                     
                                                                                
           ADD   1                 TO    WS-CNT-WRITE-L369.                     
                                                                                
           MOVE  SPACES            TO    L369-PR-DETAILS-1.                     
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       4300-GET-EFF-DTE.                                                        
      *----------------------------------------------------------------*        
                                                                                
           MOVE F690-DTE-KEY-ISSUE TO WS-TP-DATE.                               
                                                                                
           IF WS-TP-DD1 < 16                                                    
              MOVE WS-TP-CCYY1  TO WS-EFF-CCYY                                  
              MOVE WS-TP-MM1    TO WS-EFF-MM                                    
              MOVE 1            TO WS-EFF-DD                                    
           ELSE                                                                 
           IF WS-TP-DD1 > 15                                                    
              COMPUTE WS-TP-MM1 =  WS-TP-MM1 + 1                                
              IF WS-TP-MM1 > 12                                                 
                 COMPUTE WS-TP-CCYY1 = WS-TP-CCYY1 + 1                          
                 MOVE 1  TO WS-TP-MM1                                           
              END-IF                                                            
                                                                                
              MOVE WS-TP-CCYY1  TO WS-EFF-CCYY                                  
              MOVE WS-TP-MM1    TO WS-EFF-MM                                    
              MOVE 1            TO WS-EFF-DD                                    
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       9000-CLOSE-ROUTINE.                                                      
      *----------------------------------------------------------------*        
                                                                                
           CLOSE BP13F690                                                       
                 SY02F001                                                       
                 BP13L369.                                                      
                                                                                
           DISPLAY '                                  '.                        
           DISPLAY '************ BP13C369 ************'.                        
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY '                                    '.                      
           DISPLAY 'NO OF RECORDS READ       (BP13F690) = '                     
                    WS-CNT-F690-READ.                                           
           DISPLAY 'NO OF RECORD PRINTED     (BP13L369) = '                     
                    WS-CNT-WRITE-L369.                                          
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
