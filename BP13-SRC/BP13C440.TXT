      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C440                                                  
       AUTHOR.        ROSALYN M. LUMANGLAS.                                     
       DATE-WRITTEN.  03/09/93.                                                 
                                                                                
      *================================================================*        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  1.  TO CREATE A FILE FOR R002.                *        
      *                                                                *        
      *   INPUT FILES :  1.  BP13F430  -                               *        
      *                  2.  BP13K060  -  CODE TRANSCRIPTION FILE      *        
      *                  3.  BP13K820  -  SOC OCCUPIER FILE            *        
      *                  4.  BP13K830  -  CORR ADDRESS FILE            *        
      *                                                                *        
      *   OUTPUT FILE :  1.  BP13F480  -  NEW TENANTS/LESSEES FILE     *        
      *                                                                *        
      *   CHGE-NO  BY    DATE   DESCRIPTION                            *        
      *   -------  ---  ------  ------------------------------------   *        
      * SOC-PH8  JEP  181193  TO CHANGE P10K060 TO BP13K060 AND        *        
      *                       P10K040 TO BP13K820                      *        
      * BP130418 CLT  280898  TO CATER FOR Y2K.                        *        
      * BP130901 AKT  100800  TO CALL 'BP13C904', PD, SEX, NATIONALITY *        
      * BP130883 RASM 250800  TO CHANGE BP13C904 TO BP13C913           *        
      * BP132301 CLT  131203  EXPAND BP13F430 TO 1000 BYTES.           *        
      * BP132313 MJ16 200603  INCLUDE SUBROUTINE BP13SR20 TO CHECK     *        
      *                       NATIONALITY & REMOVE BB14K090            *        
      *================================================================*        
                                                                                
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
            SELECT BP13F430  ASSIGN        TO BP13F430.                         
                                                                                
            SELECT BP13K060  ASSIGN        TO BP13K060                          
                             ORGANIZATION  IS INDEXED                           
                             ACCESS MODE   IS RANDOM                            
                             RECORD KEY    IS K060-KEY-FLD                      
                             FILE STATUS   IS WS-K060-STATUS.                   
                                                                                
      *     SELECT BB14K090  ASSIGN        TO BB14K090                          
      *                      ORGANIZATION  IS INDEXED                           
      *                      ACCESS MODE   IS RANDOM                            
      *                      RECORD KEY    IS K090-KEY-FLD                      
      *                      FILE STATUS   IS WS-K090-STATUS.                   
                                                                                
            SELECT BP13K820  ASSIGN        TO BP13K820                          
                             ORGANIZATION  IS INDEXED                           
                             ACCESS MODE   IS RANDOM                            
                             RECORD KEY    IS K820-KEY-FLD                      
                             FILE STATUS   IS WS-K820-STATUS.                   
                                                                                
            SELECT BP13K830  ASSIGN        TO BP13K830                          
                             ORGANIZATION  IS INDEXED                           
                             ACCESS MODE   IS RANDOM                            
                             RECORD KEY    IS K830-KEY-FLD                      
                             ALTERNATE RECORD KEY IS K830-NUM-REGN              
                             FILE STATUS   IS WS-K830-STATUS.                   
                                                                                
            SELECT BP13F480  ASSIGN        TO BP13F480.                         
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F430                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13F430.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS 25  CHARACTERS.                                     
       COPY BP13K060.                                                           
                                                                                
      *FD   BB14K090                                                            
      *     RECORD CONTAINS 100  CHARACTERS.                                    
      *COPY BB14K090.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   BP13F480                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 350 CHARACTERS.                                     
       COPY BP13F480.                                                           
                                                                                
                                                                                
      ***************************                                               
       WORKING-STORAGE SECTION.                                                 
      ***************************                                               
                                                                                
       COPY P13COMM8.                                                           
                                                                                
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH          PIC X(4).                           
               10  WS-LINK-NUM-SCH          PIC X(5).                           
                                                                                
       01  WS-SQL-CODE                      PIC 9(4)   VALUE 0.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F430-EOF                  PIC X     VALUE 'N'.                
           05  WS-F430-CNT                  PIC 9(7)  VALUE ZEROES.             
           05  WS-F480-CNT                  PIC 9(7)  VALUE ZEROES.             
                                                                                
       01  WS-STATUS.                                                           
           05  WS-K060-STATUS               PIC 99    VALUE 00.                 
           05  WS-K090-STATUS               PIC 99    VALUE 00.                 
           05  WS-K820-STATUS               PIC 99    VALUE 00.                 
           05  WS-K830-STATUS               PIC 99    VALUE 00.                 
                                                                                
       01  WS-K820-KEY-FLD.                                                     
           05  WS-K820-REGN                 PIC X(8)  VALUE SPACES.             
           05  WS-K820-NRIC                 PIC X(9)  VALUE SPACES.             
                                                                                
       01  WS-K060-KEY-FLD.                                                     
           05  WS-K060-SERIAL-NO            PIC 9(2)  VALUE ZEROES.             
           05  WS-K060-CODE                 PIC X(3)  VALUE SPACES.             
                                                                                
       01  WS-NUM-NTNLTY                    PIC X(2)  VALUE SPACES.             
                                                                                
            EXEC SQL INCLUDE SQLCA     END-EXEC.                                
            EXEC SQL INCLUDE P13VNATL  END-EXEC.                                
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
                                                                                
      ******************************************************************        
       0000-MAIN-RTN.                                                           
      ******************************************************************        
            PERFORM 1000-START-UP          THRU    1000-EXIT.                   
            PERFORM 2000-READ-BP13F430     THRU    2000-EXIT.                   
            PERFORM 3000-MOVE-DATA         THRU    3000-EXIT                    
                         UNTIL WS-F430-EOF = 'Y'.                               
            PERFORM 9999-WRAP-UP           THRU    9999-EXIT.                   
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       1000-START-UP.                                                           
      *****************************************************************         
            OPEN INPUT   BP13F430                                               
                         BP13K060                                               
                         BP13K820                                               
                         BP13K830                                               
                 OUTPUT  BP13F480.                                              
                                                                                
            IF WS-K060-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING BP13K060 FILE ERROR = ' WS-K060-STATUS          
               MOVE     WS-K060-STATUS   TO   RETURN-CODE                       
               PERFORM 9999-WRAP-UP.                                            
                                                                                
      *     IF WS-K090-STATUS NOT = 00 AND 97                                   
      *        DISPLAY 'OPENING BB14K090 FILE ERROR = ' WS-K090-STATUS          
      *        MOVE     WS-K090-STATUS   TO   RETURN-CODE                       
      *        PERFORM 9999-WRAP-UP.                                            
                                                                                
            IF WS-K820-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING BP13K820 FILE ERROR = ' WS-K820-STATUS          
               MOVE     WS-K820-STATUS   TO   RETURN-CODE                       
               PERFORM 9999-WRAP-UP.                                            
                                                                                
            IF WS-K830-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING BP13K830 FILE ERROR = ' WS-K830-STATUS          
               MOVE     WS-K830-STATUS   TO   RETURN-CODE                       
               PERFORM 9999-WRAP-UP.                                            
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13F430.                                                      
      *****************************************************************         
            READ BP13F430                                                       
                 AT END MOVE 'Y'   TO   WS-F430-EOF                             
                        GO TO 2000-EXIT.                                        
                                                                                
            ADD  1  TO  WS-F430-CNT.                                            
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       3000-MOVE-DATA.                                                          
      *****************************************************************         
           MOVE    SPACES               TO    BP13F480-REC.                     
           INITIALIZE BP13F480-REC.                                             
                                                                                
           MOVE    F430-CDE-SEX1        TO    F480-NUM-SEX                      
           MOVE    F430-NUM-HDB-REF     TO    F480-NUM-HDB-REF.                 
           MOVE    F430-CDE-SALES-TYP   TO    F480-CDE-SALES-TYPE.              
           MOVE    F430-DTE-SALE-EFF    TO    F480-DTE-SOLD.                    
                                                                                
           IF F430-CDE-SEX1 = 'M'                                               
              MOVE    'MR  '            TO    F480-CDE-TITLE                    
           ELSE                                                                 
           IF F430-CDE-SEX1 = 'F'                                               
              MOVE    'MS  '            TO    F480-CDE-TITLE                    
           END-IF.                                                              
                                                                                
           MOVE    F430-NME-LESSEE1     TO    F480-NME-LESSEE.                  
           MOVE    F430-NUM-REG         TO    F480-NUM-REGN.                    
           MOVE    F430-NUM-UIN1        TO    F480-NUM-NRIC.                    
                                                                                
           IF F430-CDE-NAT1 NOT = SPACES AND LOW-VALUES                         
      *       PERFORM 5500-READ-BB14K090    THRU  5500-EXIT                     
              PERFORM 5500-READ-NATIONALITY THRU  5500-EXIT                     
           ELSE                                                                 
              DISPLAY 'F430-CDE-NAT1 = SPACES'                                  
           END-IF.                                                              
                                                                                
           PERFORM 4000-READ-BP13K820   THRU  4000-EXIT.                        
           PERFORM 6000-GET-PREV-ADDR   THRU  6000-EXIT.                        
           PERFORM 7000-CALL-BP13C913   THRU  7000-EXIT.                        
                                                                                
           WRITE   BP13F480-REC.                                                
                                                                                
           ADD     1                    TO    WS-F480-CNT.                      
                                                                                
           PERFORM 2000-READ-BP13F430   THRU  2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       4000-READ-BP13K820.                                                      
      *****************************************************************         
            MOVE F430-NUM-REG     TO  WS-K820-REGN.                             
            MOVE F430-NUM-UIN1    TO  WS-K820-NRIC.                             
            MOVE WS-K820-KEY-FLD  TO  K820-KEY-FLD.                             
                                                                                
            READ BP13K820.                                                      
                                                                                
            IF WS-K820-STATUS = 00                                              
               MOVE K820-DTE-BIRTH         TO F480-DTE-BIRTH                    
               PERFORM 5000-READ-BP13K060  THRU  5000-EXIT                      
            ELSE                                                                
            IF WS-K820-STATUS = 23                                              
               MOVE 'CITIZEN DATA NOT AVAILABLE' TO F480-CITIZEN                
               DISPLAY 'BP13K820 REC NOT FOUND, STAT = ' WS-K820-STATUS         
               DISPLAY '    K820-KEY-FLD     =  ' K820-KEY-FLD                  
            ELSE                                                                
               DISPLAY 'ERROR READING BP13K820, STAT = ' WS-K820-STATUS         
               DISPLAY '      K820-KEY-FLD   =  ' K820-KEY-FLD.                 
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       5000-READ-BP13K060.                                                      
      *****************************************************************         
            MOVE 25                    TO  WS-K060-SERIAL-NO.                   
            MOVE K820-NUM-CITIZENSHIP  TO  WS-K060-CODE.                        
            MOVE WS-K060-KEY-FLD       TO  K060-KEY-FLD.                        
                                                                                
            READ BP13K060.                                                      
                                                                                
            IF WS-K060-STATUS = 00                                              
               MOVE    K060-DESC     TO  F480-CITIZEN                           
            ELSE                                                                
            IF WS-K060-STATUS = 23                                              
               MOVE    'CITIZEN DATA NOT AVAILABLE' TO F480-CITIZEN             
               DISPLAY 'BP13K060 REC NOT FOUND, STAT = ' WS-K060-STATUS         
               DISPLAY '    K060-KEY-FLD     =  ' K060-KEY-FLD                  
            ELSE                                                                
               DISPLAY 'ERROR READING BP13K060, STAT = ' WS-K060-STATUS         
               DISPLAY '      K060-KEY-FLD   =  ' K060-KEY-FLD.                 
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       5500-READ-NATIONALITY.                                                   
      *****************************************************************         
      *     MOVE SPACES                TO  K090-KEY-FLD.                        
      *     MOVE 'NA'                  TO  K090-CDE-SERIAL                      
      *     MOVE F430-CDE-NAT1         TO  K090-CDE-VALID.                      
                                                                                
      *     READ BB14K090.                                                      
                                                                                
      *     IF WS-K090-STATUS = 00                                              
      *        MOVE    K090-TXT-DESC     TO  F480-NUM-NATIONALITY               
      *     ELSE                                                                
      *     IF WS-K090-STATUS = 23                                              
      *        DISPLAY 'BB14K090 REC NOT FOUND, STAT = ' WS-K090-STATUS         
      *        DISPLAY '    K090-KEY-FLD     =  ' K090-KEY-FLD                  
      *     ELSE                                                                
      *        DISPLAY 'ERROR READING BB14K090, STAT = ' WS-K090-STATUS         
      *        DISPLAY '      K090-KEY-FLD   =  ' K090-KEY-FLD.                 
                                                                                
            MOVE F430-CDE-NAT1         TO  WS-NUM-NTNLTY.                       
            EXEC SQL                                                            
                 SELECT NME_NTNLTY                                              
                  INTO :DCLNATIONALITY.NME-NTNLTY                               
                  FROM  NATIONALITY                                             
                  WHERE NUM_NTNLTY  = :WS-NUM-NTNLTY                            
                  AND NUM_CODE_STATUS = 'A'                                     
            END-EXEC.                                                           
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 000                                                         
                    MOVE  NME-NTNLTY OF DCLNATIONALITY     TO                   
                                        F480-NUM-NATIONALITY                    
               WHEN 100                                                         
                    DISPLAY  "REC. NOT FOUND IN NATIONALITY TABLE :"            
                              WS-NUM-NTNLTY                                     
               WHEN OTHER                                                       
                    DISPLAY 'SQL ERROR IN NATIONALITY TABLE    '                
                    DISPLAY 'WS-NUM-NTNLTY    :  ' WS-NUM-NTNLTY                
                    DISPLAY 'SQL ERROR CODE  :  '  SQLCODE                      
                    DISPLAY SPACES                                              
                    PERFORM 9000-SQL-ERROR  THRU 9000-EXIT                      
           END-EVALUATE.                                                        
       5500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       6000-GET-PREV-ADDR.                                                      
      *****************************************************************         
            MOVE F430-NUM-REG TO K830-NUM-REGN.                                 
                                                                                
            READ BP13K830 KEY IS K830-NUM-REGN.                                 
                                                                                
            IF WS-K830-STATUS = 00                                              
               MOVE    K830-NUM-BLK         TO  F480-BLK-PREV                   
               MOVE    K830-NUM-FLAT        TO  F480-UNIT-PREV                  
               MOVE    K830-NME-STREET-BLDG TO  F480-STNAME-PREV                
               MOVE    K830-NUM-POSTAL      TO  F480-PD-PREV                    
            ELSE                                                                
            IF WS-K830-STATUS = 23                                              
               DISPLAY 'BP13K830 REC NOT FOUND, STAT = ' WS-K830-STATUS         
               DISPLAY '    K830-NUM-REGN    =  ' K830-NUM-REGN                 
            ELSE                                                                
               DISPLAY 'ERROR READING BP13K830, STAT = ' WS-K830-STATUS         
               DISPLAY '      K830-NUM-REGN  =  ' K830-NUM-REGN.                
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       7000-CALL-BP13C913.                                                      
      *****************************************************************         
      *----------------------------------------------------------------*        
      *    CALL BP13C913 USING F430-NUM-HDB-REF  VALUES AS A KEY TO    *        
      *                  GET FLAT INFO FROM PBF TABLE                  *        
      *----------------------------------------------------------------*        
                                                                                
           IF F430-NUM-HDB-REF   = SPACES OR LOW-VALUE                          
              DISPLAY 'SCH-ACCNO SPACES!'                                       
              GO TO 7000-EXIT.                                                  
                                                                                
           MOVE  SPACES                   TO BP13COMM8-REC.                     
           INITIALIZE                        BP13COMM8-REC.                     
                                                                                
           MOVE  F430-NUM-HDB-REF         TO WS-LINK-REC.                       
                                                                                
           CALL 'BP13C913'         USING  WS-LINK-REC,                          
                                          BP13COMM8-REC.                        
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              MOVE COMM8-CDE-SYSERR      TO WS-SQL-CODE                         
              DISPLAY 'F430-NUM-HDB-REF    = ' F430-NUM-HDB-REF                 
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE   ' WS-SQL-CODE        
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE   ' WS-SQL-CODE               
           ELSE                                                                 
              MOVE COMM8-NUM-POSTAL-CODE TO F480-PD.                            
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       9000-SQL-ERROR.                                                          
      *****************************************************************         
                                                                                
           DISPLAY ' *****   BP13C440 ABEND   *****'.                           
                                                                                
           CALL 'DBATIAR'  USING SQLCA.                                         
           EXEC SQL   ROLLBACK   END-EXEC.                                      
           PERFORM SQL-INITIAL UNTIL SQL-INIT-DONE                              
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ROLLBACK FAILED, SQLCODE IS : ' SQLCODE            
           END-EVALUATE.                                                        
                                                                                
           MOVE 12                        TO   RETURN-CODE.                     
           PERFORM 9999-WRAP-UP           THRU 9999-EXIT.                       
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       9999-WRAP-UP.                                                            
      *****************************************************************         
            DISPLAY '                                    '.                     
            DISPLAY '******* CONTROL   TOTALS *******    '.                     
            DISPLAY 'PROGRAM-ID : BP13C440               '.                     
            DISPLAY '                                    '.                     
            DISPLAY 'NUMBER OF BP13F430 RECORDS READ    : ' WS-F430-CNT         
            DISPLAY 'NUMBER OF BP13F480 RECORDS WRITTEN : ' WS-F480-CNT         
                                                                                
            CLOSE BP13F430                                                      
                  BP13K060                                                      
                  BP13K820                                                      
                  BP13K830                                                      
                  BP13F480.                                                     
                                                                                
            IF WS-K060-STATUS NOT = 00                                          
               DISPLAY 'CLOSING BP13K060 FILE ERROR = ' WS-K060-STATUS.         
                                                                                
            IF WS-K090-STATUS NOT = 00                                          
               DISPLAY 'CLOSING BB14K090 FILE ERROR = ' WS-K090-STATUS.         
                                                                                
            IF WS-K820-STATUS NOT = 00                                          
               DISPLAY 'CLOSING BP13K820 FILE ERROR = ' WS-K820-STATUS.         
                                                                                
            IF WS-K830-STATUS NOT = 00                                          
               DISPLAY 'CLOSING BP13K830 FILE ERROR = ' WS-K830-STATUS.         
                                                                                
            STOP RUN.                                                           
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
