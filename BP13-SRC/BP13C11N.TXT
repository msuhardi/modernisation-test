       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C11N.                                                 
      *AUTHOR.        ZS13.                                                     
      *DATE-WRITTEN.  01/11/2018.                                               
      * ========================================================== *            
      * OBJECTIVE    :  CREATE BP13FC55 RECORDS FOR ONLINE UPDATE  *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13F800                               *            
      *                 2.) BM06K110                               *            
      *                 3.) BP13K022                               *            
      *                 4.) BP13K024                               *            
      *                 5.) BP13K410                               *            
      *                 6.) BP13KC55                               *            
      * OUTPUT FILES :  1.) BP13FC55                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP137466 ZS13 01/11/2018 NEW PROGRAM                       *            
      * BP137491 ZS13 12/02/2019 CHECK FOR ASSIGNNING OF OIC       *            
      * BP137786  KV6 18/04/2019 CATER FOR 'OBF'                                
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800  ASSIGN        TO BP13F800.                          
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            FILE STATUS   IS WS-K110-STATUS.                    
                                                                                
           SELECT BP13K022  ASSIGN        TO BP13K022                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K022-KEY-FLD                       
                            ALTERNATE KEY IS K022-NUM-REGN                      
                            FILE STATUS   IS WS-K022-STATUS.                    
                                                                                
           SELECT BP13K024  ASSIGN        TO BP13K024                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K024-KEY-FLD                       
                            FILE STATUS   IS WS-K024-STATUS.                    
                                                                                
           SELECT BP13K410  ASSIGN        TO BP13K410                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K410-KEY-FLD                       
                            FILE STATUS   IS WS-K410-STATUS.                    
                                                                                
           SELECT BP13KC55  ASSIGN        TO BP13KC55                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KC55-KEY-FLD                       
                            FILE STATUS   IS WS-KC55-STATUS.                    
                                                                                
           SELECT BP13K096  ASSIGN        TO BP13K096                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K096-NUM-KEY                       
                            FILE STATUS   IS WS-K096-STATUS.                    
                                                                                
           SELECT BP13KQ10  ASSIGN        TO BP13KQ10                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS KQ10-KEY-FLD                       
                            FILE STATUS   IS WS-KQ10-STATUS.                    
                                                                                
           SELECT BP13FC55  ASSIGN        TO BP13FC55.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F800                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 2000 CHARACTERS.                                     
           COPY BP13F800.                                                       
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS  500 CHARACTERS.                                     
           COPY BM06K110.                                                       
                                                                                
       FD  BP13K022                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
           COPY BP13K022.                                                       
                                                                                
       FD  BP13K024                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
           COPY BP13K024.                                                       
                                                                                
       FD  BP13K410                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
           COPY BP13K410.                                                       
                                                                                
       FD  BP13KC55                                                             
           RECORD CONTAINS  100 CHARACTERS.                                     
           COPY BP13KC55.                                                       
                                                                                
       FD  BP13K096                                                             
           RECORD CONTAINS  200 CHARACTERS.                                     
           COPY BP13K096.                                                       
                                                                                
       FD  BP13KQ10                                                             
           RECORD CONTAINS  400 CHARACTERS.                                     
           COPY BP13KQ10.                                                       
                                                                                
       FD  BP13FC55                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS  100 CHARACTERS.                                     
           COPY BP13FC55.                                                       
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
      *-------------------------------------------------------------            
      *    LINKAGE FOR SUB-PROGRAM BP13CCNT                                     
      *-------------------------------------------------------------            
       01  WS-BP13CCNT-LINK.                                                    
           05  LINK-CNTCT-TYPE           PIC X(3)   VALUE SPACES.               
           05  LINK-STAFF-NICK           PIC X(6)   VALUE SPACES.               
           05  LINK-STAFF-NUM            PIC X(6)   VALUE SPACES.               
           05  LINK-STAFF-CNTCT          PIC X(50)  VALUE SPACES.               
           05  LINK-SYSERR               PIC 9(4)   VALUE ZEROES.               
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K110-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K022-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K024-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K410-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-KC55-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K096-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K365-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-KQ10-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F800-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K110-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K410-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-FC55-WRITE         PIC 9(6)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F800-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K110-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K022-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K410-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-KC55-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K096-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K024-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K365-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-KQ10-FND               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY           PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY           PIC 9(4)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
           05  WS-I                 PIC 9       VALUE ZEROES.                   
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-DTE-START              PIC X(8)   VALUE SPACES.               
           05  WS-DTE-TEMP.                                                     
               10  WS-DTE-TEMP-CCYY      PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-TEMP-MM        PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-TEMP-DD        PIC 9(2)   VALUE ZEROES.               
           05  WS-NUM-SA-TP              PIC X(1)   VALUE SPACES.               
           05  WS-NUM-PRE-OIC            PIC X(8)   VALUE SPACES.               
           05  WS-NUM-POS-OIC            PIC X(8)   VALUE SPACES.               
           05  ENQY-AHG-REQ                PIC X(1)   VALUE SPACE.              
           05  ENQY-SHG-REQ                PIC X(1)   VALUE SPACE.              
           05  ENQY-AHG-STATUS             PIC X(1)   VALUE SPACE.              
           05  ENQY-SHG-STATUS             PIC X(1)   VALUE SPACE.              
                                                                                
      *-------------------------------------------------------*                 
      *    COPY BOOK FOR SUBROUTINE - BP13CB4A.               *                 
      *-------------------------------------------------------*                 
       COPY P13COM73.                                                           
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-BP13F800-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F800-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BM06K110                                                 
                       BP13K022                                                 
                       BP13K024                                                 
                       BP13K410                                                 
                       BP13KC55                                                 
                       BP13K096                                                 
                       BP13KQ10                                                 
                OUTPUT BP13FC55.                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K022 ERROR ' WS-K022-STATUS                  
              MOVE     WS-K022-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K024 ERROR ' WS-K024-STATUS                  
              MOVE     WS-K024-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K410 ERROR ' WS-K410-STATUS                  
              MOVE     WS-K410-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-KC55-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC55 ERROR ' WS-KC55-STATUS                  
              MOVE     WS-KC55-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K096-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K096 ERROR ' WS-K096-STATUS                  
              MOVE     WS-K096-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-KQ10-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KQ10 ERROR ' WS-KQ10-STATUS                  
              MOVE     WS-KQ10-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
                                                                                
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
      *-------------------------------------------------------------            
      *    WS-DTE-TEMP = DATE 4 MONTHS BEFORE                                   
      *-------------------------------------------------------------            
           MOVE WS-DTE-CURR-8              TO   WS-DTE-TEMP.                    
           IF WS-DTE-TEMP-MM < 5                                                
              SUBTRACT 1                   FROM WS-DTE-TEMP-CCYY                
              ADD 8                        TO   WS-DTE-TEMP-MM                  
           ELSE                                                                 
              SUBTRACT 4                   FROM WS-DTE-TEMP-MM                  
           END-IF.                                                              
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13F800-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13F800                                                        
              AT END MOVE 'Y'              TO   WS-F800-EOF.                    
                                                                                
           IF WS-F800-EOF NOT = 'Y'                                             
              ADD 1                        TO   WS-CNT-F800-READ                
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           DISPLAY 'T1'.                                                        
      *-------------------------------------------------------------            
      *    IF NOT BTO / SBF / SHS, SKIP RECORD                                  
      *-------------------------------------------------------------            
           IF F800-NUM-ALLO-CAT NOT = 'SBF' AND 'BTO' AND 'ROF'                 
                                  AND 'OBF'                                     
              GO TO 3000-PROCEED-READ-NEXT.                                     
                                                                                
      *-------------------------------------------------------------            
      *    IF ALREADY IN BP13K410, SKIP RECORD                                  
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13K410-REC.                   
           MOVE F800-NUM-REGN              TO   K410-NUM-REGN.                  
                                                                                
           PERFORM 3500-BP13K410-READ      THRU 3500-EXIT.                      
                                                                                
           DISPLAY 'T2'.                                                        
           IF WS-K410-FND = 'Y'                                                 
              GO TO 3000-PROCEED-READ-NEXT.                                     
                                                                                
      *-------------------------------------------------------------            
      *    GET FLAT DETAILS. SKIP RECORD IF:                                    
      *    1.) NOT FOUND IN FLAT INV FILE                                       
      *    2.) KEY HAS ALREADY BEEN ISSUED                                      
      *    3.) KEY IS NOT YET AVAILABLE                                         
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   K110-REC.                       
           MOVE F800-NUM-SCH-ACC           TO   K110-SCH-ACC-NO.                
                                                                                
           PERFORM 3100-BM06K110-READ      THRU 3100-EXIT.                      
                                                                                
           DISPLAY 'T3'.                                                        
           IF WS-K110-FND NOT = 'Y'                                             
              GO TO 3000-PROCEED-READ-NEXT.                                     
                                                                                
           IF K110-DTE-KEY-AVAIL NOT NUMERIC                                    
              MOVE ZEROES                  TO   K110-DTE-KEY-AVAIL              
           END-IF.                                                              
                                                                                
           IF K110-DTE-ACCEPTANCE NOT NUMERIC                                   
              MOVE ZEROES                  TO   K110-DTE-ACCEPTANCE             
           END-IF.                                                              
                                                                                
           DISPLAY 'T4'.                                                        
           IF K110-DTE-KEY-ISSUED NOT = SPACES AND LOW-VALUES AND ZEROES        
              GO TO 3000-PROCEED-READ-NEXT.                                     
                                                                                
      *    DISPLAY 'T5'.                                                        
      *    IF K110-DTE-KEY-AVAIL = SPACES OR LOW-VALUES OR ZEROES               
      *       GO TO 3000-PROCEED-READ-NEXT.                                     
                                                                                
           DISPLAY 'T5'.                                                        
           IF K110-DTE-KEY-AVAIL NOT  =  SPACES AND LOW-VALUES AND              
                                         ZEROES                                 
              GO TO 3000-PROCEED-READ-NEXT.                                     
                                                                                
                                                                                
      *-------------------------------------------------------------            
      *    CHECK IF PENDING FOR MORE THAN 4 MONTHS                              
      *-------------------------------------------------------------            
           IF F800-AMT-SOC-CASH NOT NUMERIC                                     
              MOVE ZEROES                  TO   F800-AMT-SOC-CASH               
           END-IF.                                                              
                                                                                
           IF F800-DTE-AGRMT-FOR-LEASE NOT NUMERIC                              
              MOVE ZEROES                  TO   F800-DTE-AGRMT-FOR-LEASE        
           END-IF.                                                              
                                                                                
           IF F800-NUM-LOAN-TAG = 'B'                                           
              IF F800-AMT-SOC-CASH > 0                                          
                 MOVE 'N'                  TO   WS-NUM-SA-TP                    
              ELSE                                                              
                 MOVE 'Y'                  TO   WS-NUM-SA-TP                    
              END-IF                                                            
           ELSE                                                                 
              IF F800-DTE-AGRMT-FOR-LEASE NOT = SPACES AND LOW-VALUES           
                                                AND ZEROES                      
                 MOVE 'N'                  TO   WS-NUM-SA-TP                    
              ELSE                                                              
                 MOVE 'Y'                  TO   WS-NUM-SA-TP                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE K110-DTE-ACCEPTANCE      TO   WS-DTE-START                      
                                                                                
      *    IF K110-DTE-KEY-AVAIL > K110-DTE-ACCEPTANCE                          
      *       IF F800-DTE-RAISE-AO NOT = SPACES AND LOW-VALUES AND              
      *                                  ZEROES                                 
      *          MOVE K110-DTE-KEY-AVAIL   TO   WS-DTE-START                    
      *       ELSE                                                              
      *          MOVE K110-DTE-ACCEPTANCE  TO   WS-DTE-START                    
      *       END-IF                                                            
      *    ELSE                                                                 
      *       MOVE K110-DTE-ACCEPTANCE     TO   WS-DTE-START                    
      *    END-IF.                                                              
                                                                                
           DISPLAY 'T6'.                                                        
           IF WS-DTE-START NOT NUMERIC OR                                       
              WS-DTE-START = SPACES OR LOW-VALUES OR ZEROES                     
              GO TO 3000-PROCEED-READ-NEXT.                                     
                                                                                
      *    DISPLAY 'T7'.                                                        
      *    IF WS-DTE-TEMP >= WS-DTE-START                                       
      *       CONTINUE                                                          
      *    ELSE                                                                 
      *       GO TO 3000-PROCEED-READ-NEXT                                      
      *    END-IF.                                                              
                                                                                
           DISPLAY 'T8'.                                                        
           IF F800-DTE-SALE-AGMT NOT  =  SPACES AND LOW-VALUES AND              
                                         ZEROES                                 
              IF F800-DTE-SALE-AGMT > WS-DTE-CURR-8                             
                 GO TO 3000-PROCEED-READ-NEXT.                                  
                                                                                
           DISPLAY 'T9'.                                                        
      *-------------------------------------------------------------            
      *    GET TEAM                                                             
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13K022-REC                    
           MOVE F800-NUM-REGN              TO   K022-NUM-REGN                   
                                                                                
           PERFORM 3300-BP13K022-READ      THRU 3300-EXIT                       
                                                                                
           MOVE SPACES                     TO   BP13K024-REC.                   
           MOVE K022-NUM-OIC               TO   K024-NUM-OIC.                   
                                                                                
           PERFORM 3400-BP13K024-READ      THRU 3400-EXIT.                      
                                                                                
      *-------------------------------------------------------------            
      *    GET APPLICATION OIC                                                  
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   WS-NUM-POS-OIC                  
                                                WS-NUM-PRE-OIC.                 
                                                                                
           MOVE SPACES                     TO   BP13KQ10-REC.                   
           MOVE F800-NUM-REGN              TO   KQ10-NUM-REGN.                  
                                                                                
           PERFORM 3700-BP13KQ10-READ      THRU 3700-EXIT.                      
                                                                                
           DISPLAY 'REGN NUM    - ' F800-NUM-REGN                               
                   ' WS-KQ10-FND - ' WS-KQ10-FND                                
                   ' CASE OWNER  - ' KQ10-NUM-CASE-OWNER                        
                   ' K022 OIC    - ' K022-NUM-OIC                               
                   ' K022 OIC APL- ' K022-NUM-OIC-APPL                          
           IF WS-KQ10-FND = 'Y'                                                 
              MOVE SPACES              TO   BP13K096-REC                        
              MOVE KQ10-NUM-CASE-OWNER TO   K096-NUM-USERID                     
                                                                                
              PERFORM 3600-BP13K096-READ  THRU 3600-EXIT                        
              IF WS-K096-FND = 'Y'                                              
                    MOVE SPACES                  TO   WS-NUM-PRE-OIC            
                    MOVE K096-NUM-STAFF-NICKNAME TO   WS-NUM-POS-OIC            
                DISPLAY 'NICK NAME    - ' K096-NUM-STAFF-NICKNAME               
                EVALUATE K096-NUM-OFFICE                                        
                  WHEN 'AGR'                                                    
                     MOVE 'A'         TO   K024-CDE-TEAM                        
                  WHEN 'RST'                                                    
                     MOVE 'R'         TO   K024-CDE-TEAM                        
                  WHEN OTHER                                                    
                    IF K024-CDE-TEAM = 0                                        
                      MOVE  '1'     TO   K024-CDE-TEAM                          
                    END-IF                                                      
                END-EVALUATE                                                    
              END-IF                                                            
           ELSE                                                                 
              DISPLAY 'WS-K022-FND - ' WS-K022-FND                              
              IF WS-K022-FND = 'Y'                                              
                 IF K022-NUM-OIC-APPL = SPACES OR LOW-VALUES OR ZEROES          
                    MOVE K022-NUM-OIC         TO   WS-NUM-PRE-OIC               
                    MOVE SPACES               TO   WS-NUM-POS-OIC               
                 ELSE                                                           
                    MOVE K022-NUM-OIC-APPL    TO   WS-NUM-PRE-OIC               
                    MOVE K022-NUM-OIC         TO   WS-NUM-POS-OIC               
                 END-IF                                                         
              ELSE                                                              
                 GO TO 3000-PROCEED-READ-NEXT                                   
              END-IF                                                            
              IF WS-K024-FND = 'Y'                                              
                 DISPLAY 'K024-CDE-TEAM - ' K024-CDE-TEAM                       
                 IF K024-CDE-TEAM = 0                                           
                    MOVE SPACES               TO   WS-BP13CCNT-LINK             
                    MOVE K022-NUM-OIC         TO   LINK-STAFF-NICK              
                                                                                
                    PERFORM 8000-CALL-BP13CCNT THRU 8000-EXIT                   
                                                                                
                    IF LINK-STAFF-NUM NOT = SPACES AND LOW-VALUES               
                       MOVE SPACES            TO   BP13K096-REC                 
                       MOVE LINK-STAFF-NUM    TO   K096-NUM-USERID              
                                                                                
                       PERFORM 3600-BP13K096-READ  THRU 3600-EXIT               
                                                                                
                       IF WS-K096-FND = 'Y'                                     
                          EVALUATE K096-NUM-OFFICE                              
                          WHEN 'AGR'                                            
                             MOVE 'A'         TO   K024-CDE-TEAM                
                          WHEN 'RST'                                            
                             MOVE 'R'         TO   K024-CDE-TEAM                
                          WHEN OTHER                                            
                             MOVE '1'         TO   K024-CDE-TEAM                
                          END-EVALUATE                                          
                       ELSE                                                     
                          MOVE '1'               TO   K024-CDE-TEAM             
                       END-IF                                                   
                    ELSE                                                        
                       MOVE '1'               TO   K024-CDE-TEAM                
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
                                                                                
              PERFORM 3010-DETERMINE-TEAM   THRU 3010-EXIT                      
           END-IF.                                                              
                                                                                
                                                                                
      *-------------------------------------------------------------            
      *    CREATE FILE                                                          
      *-------------------------------------------------------------            
                                                                                
           PERFORM 4100-CREATE-FILE        THRU 4100-EXIT.                      
                                                                                
       3000-PROCEED-READ-NEXT.                                                  
                                                                                
           PERFORM 2000-BP13F800-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3001-GET-AHG-SHG-REQ.                                                    
      *=============================================================            
                                                                                
           INITIALIZE COM73-COMMAREA.                                           
                                                                                
           MOVE 'R'                  TO  COM73-NUM-CALL-ACTION.                 
           MOVE F800-NUM-REGN        TO  COM73-NUM-RGSTRN-INPUT.                
           CALL 'BP13CB4A' USING COM73-COMMAREA.                                
                                                                                
           IF COM73-CDE-ERROR = SPACES OR ZEROES                                
              CONTINUE                                                          
           ELSE                                                                 
              MOVE SPACES            TO  COM73-COMMAREA                         
              INITIALIZE                 COM73-COMMAREA                         
           END-IF.                                                              
                                                                                
           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 5                      
              IF COM73-NUM-GRANT-TYPE(WS-I) = 'AHG'                             
                 MOVE COM73-GRANT-STATUS(WS-I)    TO F800-NUM-AHG-STATUS        
                 MOVE COM73-DTE-GRANT-RQST(WS-I)(1:4)                           
                                             TO F800-DTE-AHG-REQ(1:4)           
                 MOVE COM73-DTE-GRANT-RQST(WS-I)(6:2)                           
                                             TO F800-DTE-AHG-REQ(5:2)           
                 MOVE COM73-DTE-GRANT-RQST(WS-I)(9:2)                           
                                             TO F800-DTE-AHG-REQ(7:2)           
                 MOVE COM73-DTE-GRANT-ISSUE(WS-I)(1:4)                          
                                    TO F800-DTE-AHG-FORM-PRT(1:4)               
                 MOVE COM73-DTE-GRANT-ISSUE(WS-I)(6:2)                          
                                    TO F800-DTE-AHG-FORM-PRT(5:2)               
                 MOVE COM73-DTE-GRANT-ISSUE(WS-I)(9:2)                          
                                    TO F800-DTE-AHG-FORM-PRT(7:2)               
              END-IF                                                            
              IF COM73-NUM-GRANT-TYPE(WS-I) = 'SHG'                             
                 MOVE COM73-GRANT-STATUS(WS-I)   TO F800-NUM-SHG-STATUS         
                 MOVE COM73-DTE-GRANT-RQST(WS-I)(1:4)                           
                                             TO F800-DTE-SHG-REQ(1:4)           
                 MOVE COM73-DTE-GRANT-RQST(WS-I)(6:2)                           
                                             TO F800-DTE-SHG-REQ(5:2)           
                 MOVE COM73-DTE-GRANT-RQST(WS-I)(9:2)                           
                                             TO F800-DTE-SHG-REQ(7:2)           
                 MOVE COM73-DTE-GRANT-ISSUE(WS-I)(1:4)                          
                                    TO F800-DTE-SHG-FORM-PRT(1:4)               
                 MOVE COM73-DTE-GRANT-ISSUE(WS-I)(6:2)                          
                                    TO F800-DTE-SHG-FORM-PRT(5:2)               
                 MOVE COM73-DTE-GRANT-ISSUE(WS-I)(9:2)                          
                                    TO F800-DTE-SHG-FORM-PRT(7:2)               
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           IF F800-DTE-AHG-REQ NOT = '00010101'                                 
              IF F800-DTE-AHG-FORM-PRT NOT = '00010101'                         
                 MOVE 'F'                    TO ENQY-AHG-REQ                    
              ELSE                                                              
                 MOVE SPACES                 TO ENQY-AHG-REQ                    
              END-IF                                                            
           ELSE                                                                 
              MOVE 'Y'                    TO ENQY-AHG-REQ                       
           END-IF.                                                              
                                                                                
           MOVE F800-NUM-AHG-STATUS       TO ENQY-AHG-STATUS                    
                                                                                
           IF F800-DTE-SHG-REQ = '00010101'                                     
              IF F800-DTE-SHG-FORM-PRT NOT = '00010101'                         
                 MOVE 'F'                    TO ENQY-SHG-REQ                    
              ELSE                                                              
                 MOVE SPACES                 TO ENQY-SHG-REQ                    
              END-IF                                                            
           ELSE                                                                 
              MOVE 'Y'                    TO ENQY-SHG-REQ                       
           END-IF.                                                              
                                                                                
           MOVE F800-NUM-SHG-STATUS       TO ENQY-SHG-STATUS.                   
                                                                                
           DISPLAY 'AHG DTE-REQ - ' F800-DTE-AHG-REQ '/'                        
                   'SHG DTE-REQ - ' F800-DTE-SHG-REQ '/'                        
                   'AHG PRINT   - ' F800-DTE-AHG-FORM-PRT '/'                   
                   'SHG PRINT   - ' F800-DTE-SHG-FORM-PRT '/'.                  
                                                                                
       3001-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3010-DETERMINE-TEAM.                                                     
      *=============================================================            
                                                                                
           DISPLAY 'F800-NUM-HOUSEHOLD - ' F800-NUM-HOUSEHOLD                   
           IF F800-NUM-HOUSEHOLD = 'G'                                          
                MOVE  'A'     TO   K024-CDE-TEAM                                
           ELSE                                                                 
             PERFORM 3001-GET-AHG-SHG-REQ   THRU 3001-EXIT                      
                                                                                
             IF (ENQY-AHG-REQ = SPACES AND                                      
                 ENQY-AHG-STATUS = SPACES) OR                                   
                (ENQY-SHG-REQ = SPACES AND                                      
                 ENQY-SHG-REQ = SPACES)                                         
                    CONTINUE                                                    
             ELSE                                                               
               IF (ENQY-AHG-REQ = 'Y'    AND                                    
                   ENQY-AHG-STATUS = 'A'   ) OR                                 
                  (ENQY-SHG-REQ = 'Y'    AND                                    
                   ENQY-SHG-REQ = 'A'   )                                       
                   MOVE 'A'         TO   K024-CDE-TEAM                          
               ELSE                                                             
                   MOVE 'R'         TO   K024-CDE-TEAM                          
               END-IF                                                           
             END-IF                                                             
           END-IF.                                                              
                                                                                
       3010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3100-BM06K110-READ.                                                      
      *=============================================================            
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K110-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K110-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BM06K110 ERROR '    WS-K110-STATUS                  
              MOVE    WS-K110-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3300-BP13K022-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              MOVE 'Y'                     TO   WS-K022-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K022-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K022 ERROR '    WS-K022-STATUS                  
              MOVE    WS-K022-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3400-BP13K024-READ.                                                      
      *=============================================================            
                                                                                
                                                                                
           READ BP13K024.                                                       
                                                                                
           EVALUATE WS-K024-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K024-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K024-FND                     
              MOVE SPACES                  TO   BP13K024-REC                    
              MOVE '1'                     TO   K024-CDE-TEAM                   
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K024 ERROR '    WS-K024-STATUS                  
              MOVE    WS-K024-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3500-BP13K410-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE WS-K410-STATUS                                              
           WHEN 00                                                              
           WHEN 04                                                              
              MOVE 'Y'                     TO   WS-K410-FND                     
              ADD 1                        TO   WS-CNT-K410-READ                
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K410-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K410 ERROR '    WS-K410-STATUS                  
              MOVE    WS-K410-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3600-BP13K096-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K096.                                                       
                                                                                
           EVALUATE WS-K096-STATUS                                              
           WHEN 00                                                              
           WHEN 04                                                              
              MOVE 'Y'                     TO   WS-K096-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K096-FND                     
              MOVE SPACES                  TO   BP13K096-REC                    
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K096 ERROR '    WS-K096-STATUS                  
              MOVE    WS-K096-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3700-BP13KQ10-READ.                                                      
      *=============================================================            
                                                                                
                                                                                
           READ BP13KQ10.                                                       
                                                                                
           EVALUATE WS-KQ10-STATUS                                              
           WHEN 00                                                              
              IF KQ10-NUM-CASE-OWNER NOT = SPACES AND LOW-VALUES                
                MOVE 'Y'                     TO   WS-KQ10-FND                   
              ELSE                                                              
                MOVE 'N'                     TO   WS-KQ10-FND                   
                MOVE SPACES                  TO BP13KQ10-REC                    
              END-IF                                                            
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-KQ10-FND                     
              MOVE SPACES                  TO BP13KQ10-REC                      
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13KQ10 ERROR '    WS-KQ10-STATUS                  
              MOVE    WS-KQ10-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4100-CREATE-FILE.                                                        
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13FC55-REC.                   
                                                                                
           IF WS-NUM-POS-OIC = SPACES OR LOW-VALUES OR ZEROES                   
              MOVE WS-NUM-PRE-OIC          TO   FC55-NUM-OIC                    
           ELSE                                                                 
              MOVE WS-NUM-POS-OIC          TO   FC55-NUM-OIC                    
           END-IF.                                                              
                                                                                
           MOVE WS-DTE-START               TO   FC55-DTE-START.                 
           MOVE F800-NUM-REGN              TO   FC55-NUM-REGN.                  
           MOVE WS-NUM-SA-TP               TO   FC55-NUM-SA-TP.                 
           MOVE F800-NUM-LOAN-TAG          TO   FC55-NUM-LOAN-TAG.              
           MOVE 'BP13C11N'                 TO   FC55-NUM-USERID.                
           MOVE WS-DTE-CURR-8              TO   FC55-DTE-UPDATE.                
           MOVE FC55-NUM-REGN              TO   FC55-NUM-REGN2.                 
           MOVE FC55-NUM-OIC               TO   FC55-NUM-OIC2.                  
           MOVE K024-CDE-TEAM              TO   FC55-CDE-TEAM.                  
                                                                                
      *    IF F800-DTE-PEND-CLEAR IS NUMERIC                                    
      *       IF F800-DTE-PEND-CLEAR > WS-DTE-CURR-8                            
                 MOVE SPACES               TO   BP13KC55-REC                    
                 MOVE F800-NUM-REGN        TO   KC55-NUM-REGN                   
                                                                                
                 PERFORM 4110-BP13KC55-READ     THRU 4110-EXIT                  
                                                                                
                 IF WS-KC55-FND = 'Y'                                           
                    MOVE KC55-NUM-USERID   TO   FC55-NUM-USERID                 
                    MOVE KC55-DTE-UPDATE   TO   FC55-DTE-UPDATE                 
                    MOVE KC55-NUM-UPDATED  TO   FC55-NUM-UPDATED                
                    MOVE KC55-DTE-MSG      TO   FC55-DTE-MSG                    
                    MOVE KC55-TME-MSG      TO   FC55-TME-MSG                    
                    MOVE KC55-NUM-PEND-SOCA-RSN                                 
                                           TO   FC55-NUM-PEND-SOCA-RSN          
                    MOVE KC55-DTE-PEND-CLEAR                                    
                                           TO   FC55-DTE-PEND-CLEAR             
                    MOVE KC55-NUM-LATE-COMP                                     
                                           TO   FC55-NUM-LATE-COMP              
                 END-IF                                                         
      *       END-IF                                                            
      *    END-IF.                                                              
                                                                                
      *    IF FC55-NUM-LATE-COMP = SPACES OR LOW-VALUES OR ZEROES               
      *       MOVE SPACES                  TO   BP13K365-REC                    
      *       MOVE F800-NUM-REGN           TO   K365-NUM-REGN                   
      *                                                                         
      *       PERFORM 4130-BP13K365-READ   THRU 4130-EXIT                       
      *                                                                         
      *       IF WS-K365-FND = 'Y'                                              
      *          MOVE K365-NUM-STATUS(1:1) TO   FC55-NUM-LATE-COMP              
      *       END-IF                                                            
      *    END-IF.                                                              
                                                                                
           PERFORM 4120-BP13FC55-WRITE     THRU 4120-EXIT.                      
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4110-BP13KC55-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13KC55.                                                       
                                                                                
           EVALUATE WS-KC55-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-KC55-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-KC55-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13KC55 ERROR '    WS-KC55-STATUS                  
              MOVE    WS-KC55-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4120-BP13FC55-WRITE.                                                     
      *=============================================================            
                                                                                
           WRITE BP13FC55-REC.                                                  
           ADD 1                           TO   WS-CNT-FC55-WRITE.              
                                                                                
       4120-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8000-CALL-BP13CCNT.                                                      
      *=============================================================            
                                                                                
           CALL 'BP13CCNT' USING WS-BP13CCNT-LINK.                              
                                                                                
           EVALUATE LINK-SYSERR                                                 
           WHEN ZEROES                                                          
           WHEN 100                                                             
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'CALL BP13CCNT ERROR '                                    
                       LINK-STAFF-NICK LINK-SYSERR                              
              MOVE    '99'                 TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13F800                                                       
                 BM06K110                                                       
                 BP13K022                                                       
                 BP13K024                                                       
                 BP13K410                                                       
                 BP13KC55                                                       
                 BP13K096                                                       
                 BP13FC55.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K022 ERROR ' WS-K022-STATUS                  
              MOVE     WS-K022-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K024 ERROR ' WS-K024-STATUS                  
              MOVE     WS-K024-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K410 ERROR ' WS-K410-STATUS                  
              MOVE     WS-K410-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-KC55-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC55 ERROR ' WS-KC55-STATUS                  
              MOVE     WS-KC55-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K096-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K096 ERROR ' WS-K096-STATUS                  
              MOVE     WS-K096-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C11N CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13F800 READ      : ' WS-CNT-F800-READ.          
           DISPLAY ' 2. NO OF BP13K410 READ      : ' WS-CNT-K410-READ.          
           DISPLAY ' 3. NO OF BP13FC55 WRITE     : ' WS-CNT-FC55-WRITE.         
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
