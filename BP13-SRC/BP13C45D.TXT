       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C45D.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  01/11/2016.                                               
      * ========================================================== *            
      * OBJECTIVE    : DAILY PROGRAM TO POPULATE BP13KC70          *            
      *                (POTENTIAL REGNS FOR SELF BOOKING) BASED ON *            
      *                BP13KC60 BLOCK REQUESTS.                    *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13FC60                               *            
      *                 2.) BM06K110                               *            
      *                 3.) BP13K800                               *            
      *                 4.) BP13K820                               *            
      *                 5.) BP13KC20                               *            
      *                 6.) BP13KC25                               *            
      * I-O    FILES :  1.) BP13KC60                               *            
      *              :  2.) BP13KC70                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP136430 KSJ3 01/11/2016 NEW PROGRAM                       *            
      * BP136561 KSJ3 30/12/2016 INCL CHECK QUEUE. IF FND, SKIP    *            
      * BP136980 RJB1 07/12/2017 INCL FILTER OF SATP CASES         *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FC60  ASSIGN        TO BP13FC60.                          
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            ALTERNATE KEY IS K110-AIX1                          
                            FILE STATUS   IS WS-K110-STATUS.                    
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS WS-K800-STATUS.                    
                                                                                
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K820-KEY-FLD                       
                            FILE STATUS   IS WS-K820-STATUS.                    
                                                                                
           SELECT BP13KC20  ASSIGN        TO BP13KC20                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KC20-KEY-FLD                       
                            ALTERNATE KEY IS KC20-ALT-KEY                       
                            FILE STATUS   IS WS-KC20-STATUS.                    
                                                                                
           SELECT BP13KC25  ASSIGN        TO BP13KC25                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KC25-KEY-FLD                       
                            ALTERNATE KEY IS KC25-ALT-KEY1                      
                            FILE STATUS   IS WS-KC25-STATUS.                    
                                                                                
           SELECT BP13KC60  ASSIGN        TO BP13KC60                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KC60-KEY-FLD                       
                            FILE STATUS   IS WS-KC60-STATUS.                    
                                                                                
           SELECT BP13KC70  ASSIGN        TO BP13KC70                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KC70-KEY-FLD                       
                            FILE STATUS   IS WS-KC70-STATUS.                    
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13FC60                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 150 CHARACTERS.                                      
           COPY BP13FC60.                                                       
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
           COPY BM06K110.                                                       
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
           COPY BP13K800.                                                       
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
           COPY BP13K820.                                                       
                                                                                
       FD  BP13KC20                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
           COPY BP13KC20.                                                       
                                                                                
       FD  BP13KC25                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
           COPY BP13KC25.                                                       
                                                                                
       FD  BP13KC60                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
           COPY BP13KC60.                                                       
                                                                                
       FD  BP13KC70                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
           COPY BP13KC70.                                                       
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K110-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K800-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K820-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-KC20-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-KC25-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-KC60-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-KC70-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-IDX                    PIC 9(2)   VALUE ZEROES.               
           05  WS-CTR                    PIC 9(2)   VALUE ZEROES.               
           05  WS-CNT-FC60-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-FC60-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-KC60-UPD           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-KC70-WRITE         PIC 9(6)   VALUE ZEROES.               
           05  WS-SATP                   PIC 9(6)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-FC60-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K110-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K800-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K820-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-KC20-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-KC25-FND               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8.                                                   
               10  WS-DTE-CURR8-CCYY     PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-CURR8-MM       PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-CURR8-DD       PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
           05  WS-DTE-BIRTH.                                                    
               10  WS-DTE-BIRTH-CCYY     PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-BIRTH-MM       PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-BIRTH-DD       PIC 9(2)   VALUE ZEROES.               
           05  WS-TIME-RUN               PIC X(8)   VALUE ZEROES.               
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-NUM-AVG-AGE            PIC 9(2)   VALUE ZEROES.               
           05  WS-NUM-REM                PIC 9(2)   VALUE ZEROES.               
           05  WS-TMP-DIV                PIC 9(1)   VALUE ZEROES.               
           05  WS-TMP-AGE-TOT            PIC 9(3)   VALUE ZEROES.               
           05  FILLER OCCURS 4 TIMES.                                           
               10  WS-NUM-O-UIN       PIC X(9)    VALUE SPACES.                 
               10  WS-NUM-O-AGE       PIC 9(2)    VALUE ZEROES.                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
           PERFORM 2000-BP13FC60-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-FC60-EOF = 'Y'                                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13FC60                                                 
                       BM06K110                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13KC20                                                 
                       BP13KC25                                                 
                I-O    BP13KC60                                                 
                       BP13KC70.                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K820 ERROR ' WS-K820-STATUS                  
              MOVE     WS-K820-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-KC20-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC20 ERROR ' WS-KC20-STATUS                  
              MOVE     WS-KC20-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-KC25-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC25 ERROR ' WS-KC25-STATUS                  
              MOVE     WS-KC25-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-KC60-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC60 ERROR ' WS-KC60-STATUS                  
              MOVE     WS-KC60-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-KC70-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC70 ERROR ' WS-KC70-STATUS                  
              MOVE     WS-KC70-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    GET CURRENT DATE                                                     
      *-------------------------------------------------------------            
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13FC60-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13FC60                                                        
              AT END MOVE 'Y'              TO   WS-FC60-EOF.                    
                                                                                
           IF WS-FC60-EOF NOT = 'Y'                                             
              ADD 1                        TO   WS-CNT-FC60-READ                
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           IF FC60-DTE-PROCESS NOT = SPACES AND LOW-VALUES AND ZEROES           
              GO TO 3000-CONTINUE-READ-NEXT.                                    
                                                                                
      *-------------------------------------------------------------            
      *    GET ALL REGNS FROM FLAT INVENTORY                                    
      *-------------------------------------------------------------            
           MOVE 'N'                        TO   WS-K110-EOF.                    
                                                                                
           MOVE SPACES                     TO   K110-REC.                       
           MOVE FC60-ESTATE                TO   K110-ESTATE.                    
           MOVE FC60-NEIGHBOURHOOD         TO   K110-NEIGHBOURHOOD.             
           MOVE FC60-CONTRACT-NO           TO   K110-CONTRACT-NO.               
           MOVE FC60-BLK-NO                TO   K110-BLK-NO.                    
                                                                                
           PERFORM 3010-BM06K110-STARTBR   THRU 3010-EXIT.                      
           PERFORM 3100-PROCESS-REGNS      THRU 3100-EXIT                       
                   UNTIL WS-K110-EOF = 'Y'.                                     
                                                                                
       3000-CONTINUE-READ-NEXT.                                                 
                                                                                
           PERFORM 2000-BP13FC60-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3010-BM06K110-STARTBR.                                                   
      *=============================================================            
                                                                                
           START BM06K110 KEY >= K110-AIX1.                                     
                                                                                
           EVALUATE WS-K110-STATUS                                              
           WHEN 00                                                              
              PERFORM 3020-BM06K110-READNEXT    THRU 3020-EXIT                  
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                     TO   WS-K110-EOF                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'START BM06K110 ERROR '   WS-K110-STATUS                  
              MOVE    WS-K110-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3020-BM06K110-READNEXT.                                                  
      *=============================================================            
                                                                                
           READ BM06K110 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K110-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              IF FC60-ESTATE        NOT = K110-ESTATE        OR                 
                 FC60-NEIGHBOURHOOD NOT = K110-NEIGHBOURHOOD OR                 
                 FC60-CONTRACT-NO   NOT = K110-CONTRACT-NO   OR                 
                 FC60-BLK-NO        NOT = K110-BLK-NO                           
                 MOVE 'Y'                  TO   WS-K110-EOF                     
              END-IF                                                            
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                     TO   WS-K110-EOF                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READNEXT BM06K110 ERROR ' WS-K110-STATUS                 
              MOVE    WS-K110-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3020-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-PROCESS-REGNS.                                                      
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    SKIP RECORDS WITH NO REGNS                                           
      *-------------------------------------------------------------            
           IF K110-REGN-NO = SPACES OR LOW-VALUES OR ZEROES                     
              GO TO 3100-CONTINUE-NEXT-REGN.                                    
                                                                                
           IF K110-REGN-NO(1:1) = '#'                                           
              GO TO 3100-CONTINUE-NEXT-REGN.                                    
                                                                                
      *-------------------------------------------------------------            
      *    SKIP RECORDS WITH KEY NOT YET AVAILABLE                              
      *-------------------------------------------------------------            
           IF K110-DTE-KEY-AVAIL = SPACES OR LOW-VALUES OR ZEROES               
              GO TO 3100-CONTINUE-NEXT-REGN.                                    
                                                                                
      *-------------------------------------------------------------            
      *    SKIP RECORDS WITH KEY ALREADY ISSUED                                 
      *-------------------------------------------------------------            
           IF K110-DTE-KEY-ISSUED NOT = SPACES AND LOW-VALUES AND ZEROES        
              GO TO 3100-CONTINUE-NEXT-REGN.                                    
                                                                                
      *-------------------------------------------------------------            
      *    GET SOC DETAILS                                                      
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13K800-MASTER.                
           MOVE K110-REGN-NO               TO   K800-NUM-REGN.                  
                                                                                
           PERFORM 3110-BP13K800-READ      THRU 3110-EXIT.                      
                                                                                
           IF WS-K800-FND NOT = 'Y'                                             
              GO TO 3100-CONTINUE-NEXT-REGN.                                    
                                                                                
      *-------------------------------------------------------------            
      *    SKIP RECORDS WITH EXISTING APPOINTMENT DATES                         
      *-------------------------------------------------------------            
           IF K800-DTE-SO-APPT NOT = SPACES AND LOW-VALUES AND ZEROES           
              GO TO 3100-CONTINUE-NEXT-REGN.                                    
                                                                                
      *-------------------------------------------------------------            
      *    SKIP SATP CASES                                                      
      *-------------------------------------------------------------            
           PERFORM 3140-CHECK-SATP         THRU 3140-EXIT.                      
           IF WS-CTR = 0                                                        
              ADD 1 TO WS-SATP                                                  
              GO TO 3100-CONTINUE-NEXT-REGN.                                    
                                                                                
      *-------------------------------------------------------------            
      *    GET OCCUP DETAILS AND COMPUTE AVG AGE                                
      *-------------------------------------------------------------            
           PERFORM 3200-GET-OCCUP          THRU 3200-EXIT.                      
           PERFORM 3300-COMP-AVG-AGE       THRU 3300-EXIT.                      
                                                                                
      *-------------------------------------------------------------            
      *    CHECK RECORD IN SELF BOOKING LIST                                    
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13KC20-REC.                   
           MOVE K110-REGN-NO               TO   KC20-NUM-REGN.                  
                                                                                
           PERFORM 3120-BP13KC20-READ      THRU 3120-EXIT.                      
                                                                                
           IF WS-KC20-FND = 'Y'                                                 
              GO TO 3100-CONTINUE-NEXT-REGN.                                    
                                                                                
      *-------------------------------------------------------------            
      *    CHECK RECORD IN SELF BOOKING QUEUE                                   
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13KC25-REC.                   
           MOVE K110-REGN-NO               TO   KC25-NUM-REGN.                  
                                                                                
           PERFORM 3130-BP13KC25-READ      THRU 3130-EXIT.                      
                                                                                
           IF WS-KC25-FND = 'Y'                                                 
              GO TO 3100-CONTINUE-NEXT-REGN.                                    
                                                                                
           PERFORM 4000-CREATE-SELFREGN    THRU 4000-EXIT.                      
           PERFORM 4100-UPD-SELFBLK        THRU 4100-EXIT.                      
                                                                                
       3100-CONTINUE-NEXT-REGN.                                                 
                                                                                
           PERFORM 3020-BM06K110-READNEXT  THRU 3020-EXIT.                      
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3110-BP13K800-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K800-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K800-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K800 ERROR '    WS-K800-STATUS                  
              MOVE    WS-K800-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3120-BP13KC20-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13KC20 KEY IS KC20-ALT-KEY.                                   
                                                                                
           EVALUATE WS-KC20-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-KC20-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-KC20-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13KC20 ERROR '    WS-KC20-STATUS                  
              MOVE    WS-KC20-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3120-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3130-BP13KC25-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13KC25 KEY IS KC25-ALT-KEY1.                                  
                                                                                
           EVALUATE WS-KC25-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-KC25-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-KC25-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13KC25 ERROR '    WS-KC25-STATUS                  
              MOVE    WS-KC25-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3130-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3140-CHECK-SATP.                                                         
      *=============================================================            
      * FOR SATP CASES, SOC-DEBTOR/CASH/CPF MUST BE EQUAL TO ZEROES.            
                                                                                
           MOVE 0    TO WS-CTR.                                                 
                                                                                
           IF K800-AMT-SOC-DEBTOR > ZEROES                                      
              ADD 1                        TO WS-CTR                            
           END-IF.                                                              
                                                                                
           IF K800-AMT-SOC-CASH > ZEROES                                        
              ADD 1                        TO WS-CTR                            
           END-IF.                                                              
                                                                                
           IF K800-AMT-CPF1-PAID > ZEROES                                       
              ADD 1                        TO WS-CTR                            
           END-IF.                                                              
                                                                                
           IF K800-AMT-CPF2-PAID > ZEROES                                       
              ADD 1                        TO WS-CTR                            
           END-IF.                                                              
                                                                                
           IF K800-AMT-CPF3-PAID > ZEROES                                       
              ADD 1                        TO WS-CTR                            
           END-IF.                                                              
                                                                                
           IF K800-AMT-CPF4-PAID > ZEROES                                       
              ADD 1                        TO WS-CTR                            
           END-IF.                                                              
                                                                                
       3140-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3200-GET-OCCUP.                                                          
      *=============================================================            
                                                                                
           PERFORM VARYING WS-IDX FROM 1 BY 1 UNTIL WS-IDX > 4                  
              MOVE SPACES                  TO   WS-NUM-O-UIN(WS-IDX)            
              MOVE ZEROES                  TO   WS-NUM-O-AGE(WS-IDX)            
           END-PERFORM.                                                         
                                                                                
           MOVE ZEROES                     TO   WS-IDX.                         
                                                                                
           IF K800-NUM-NRIC1 NOT = SPACES AND LOW-VALUES AND ZEROES             
              ADD 1                        TO   WS-IDX                          
              MOVE K800-NUM-NRIC1          TO   WS-NUM-O-UIN(WS-IDX)            
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND ZEROES             
              ADD 1                        TO   WS-IDX                          
              MOVE K800-NUM-NRIC2          TO   WS-NUM-O-UIN(WS-IDX)            
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES AND ZEROES             
              ADD 1                        TO   WS-IDX                          
              MOVE K800-NUM-NRIC3          TO   WS-NUM-O-UIN(WS-IDX)            
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES AND ZEROES             
              ADD 1                        TO   WS-IDX                          
              MOVE K800-NUM-NRIC4          TO   WS-NUM-O-UIN(WS-IDX)            
           END-IF.                                                              
                                                                                
           PERFORM VARYING WS-IDX FROM 1 BY 1                                   
                     UNTIL WS-IDX > 4                                           
                        OR WS-NUM-O-UIN(WS-IDX) = SPACES                        
              MOVE SPACES                  TO   BP13K820-REC                    
              MOVE K800-NUM-REGN           TO   K820-NUM-REGN                   
              MOVE WS-NUM-O-UIN(WS-IDX)    TO   K820-NUM-NRIC                   
                                                                                
              PERFORM 3210-BP13K820-READ   THRU 3210-EXIT                       
                                                                                
              IF WS-K820-FND = 'Y'                                              
                 IF K820-DTE-BIRTH NOT NUMERIC                                  
                    MOVE ZEROES            TO   WS-NUM-O-AGE(WS-IDX)            
                 ELSE                                                           
                    MOVE K820-DTE-BIRTH    TO   WS-DTE-BIRTH                    
                    COMPUTE WS-NUM-O-AGE(WS-IDX) =                              
                            WS-DTE-CURR8-CCYY - WS-DTE-BIRTH-CCYY               
                                                                                
                    IF WS-DTE-CURR8-MM = WS-DTE-BIRTH-MM                        
                       IF WS-DTE-CURR8-DD > WS-DTE-BIRTH-DD                     
                          SUBTRACT 1       FROM WS-NUM-O-AGE(WS-IDX)            
                       END-IF                                                   
                    ELSE                                                        
                       IF WS-DTE-CURR8-MM > WS-DTE-BIRTH-MM                     
                          SUBTRACT 1       FROM WS-NUM-O-AGE(WS-IDX)            
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3210-BP13K820-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K820-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K820-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K820 ERROR '    WS-K820-STATUS                  
              MOVE    WS-K820-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3300-COMP-AVG-AGE.                                                       
      *=============================================================            
                                                                                
           MOVE ZEROES                     TO   WS-TMP-DIV                      
                                                WS-TMP-AGE-TOT.                 
                                                                                
           PERFORM VARYING WS-IDX FROM 1 BY 1 UNTIL WS-IDX > 4                  
              IF (WS-NUM-O-AGE(WS-IDX) IS NUMERIC)  AND                         
                 (WS-NUM-O-AGE(WS-IDX) NOT = SPACES AND LOW-VALUES AND          
                                              ZEROES)                           
                 ADD 1                     TO   WS-TMP-DIV                      
                 ADD WS-NUM-O-AGE(WS-IDX)  TO   WS-TMP-AGE-TOT                  
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           IF WS-TMP-AGE-TOT = 0 OR WS-TMP-DIV = 0                              
              MOVE 0                       TO   WS-NUM-AVG-AGE                  
           ELSE                                                                 
              DIVIDE WS-TMP-AGE-TOT BY WS-TMP-DIV GIVING WS-NUM-AVG-AGE         
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4000-CREATE-SELFREGN.                                                    
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13KC70-REC.                   
           MOVE FC60-ESTATE                TO   KC70-ESTATE.                    
           MOVE FC60-NEIGHBOURHOOD         TO   KC70-NEIGHBOURHOOD.             
           MOVE FC60-CONTRACT-NO           TO   KC70-CONTRACT-NO.               
           MOVE FC60-BLK-NO                TO   KC70-BLK-NO.                    
           MOVE K110-REGN-NO               TO   KC70-NUM-REGN.                  
           MOVE 'TP'                       TO   KC70-NUM-APPT-TYPE.             
           MOVE 'W'                        TO   KC70-CDE-APPT.                  
           MOVE 'A'                        TO   KC70-NUM-STATUS.                
           MOVE 'BP13C45D'                 TO   KC70-NUM-USERID.                
           MOVE WS-DTE-CURR-8              TO   KC70-DTE-UPDATE.                
           MOVE FUNCTION CURRENT-DATE(9:9) TO   KC70-TME-UPDATE.                
                                                                                
           IF (K800-NUM-HOUSEHOLD = 'H' OR 'T') AND                             
              (WS-NUM-AVG-AGE < 30)                                             
              MOVE '1'                     TO   KC70-NUM-CATEGORY               
           ELSE                                                                 
              IF K800-NUM-PPS-ELIG = 'Y' OR 'E'                                 
                 MOVE '2'                  TO   KC70-NUM-CATEGORY               
              ELSE                                                              
                 MOVE '0'                  TO   KC70-NUM-CATEGORY               
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 4010-BP13KC70-WRITE     THRU 4010-EXIT.                      
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4010-BP13KC70-WRITE.                                                     
      *=============================================================            
                                                                                
           WRITE BP13KC70-REC.                                                  
                                                                                
           EVALUATE WS-KC70-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              ADD 1                        TO   WS-CNT-KC70-WRITE               
                                                                                
           WHEN 22                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'WRITE BP13KC70 ERROR '   WS-KC70-STATUS                  
              MOVE     WS-KC70-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4100-UPD-SELFBLK.                                                        
      *=============================================================            
                                                                                
           MOVE BP13FC60-REC               TO   BP13KC60-REC.                   
           MOVE WS-DTE-CURR-8              TO   KC60-DTE-PROCESS                
                                                KC60-DTE-UPDATE.                
           MOVE FUNCTION CURRENT-DATE(9:9) TO   KC60-TME-UPDATE.                
                                                                                
           PERFORM 4110-BP13KC60-REWRITE   THRU 4110-EXIT.                      
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4110-BP13KC60-REWRITE.                                                   
      *=============================================================            
                                                                                
           REWRITE BP13KC60-REC.                                                
                                                                                
           EVALUATE WS-KC60-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              ADD 1                        TO   WS-CNT-KC60-UPD                 
                                                                                
           WHEN 10                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'REWRITE BP13KC60 ERROR '   WS-KC60-STATUS                
              MOVE     WS-KC60-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13FC60                                                       
                 BM06K110                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13KC20                                                       
                 BP13KC25                                                       
                 BP13KC60                                                       
                 BP13KC70.                                                      
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BM06K110 ERROR ' WS-K110-STATUS                  
              MOVE     WS-K110-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K820 ERROR ' WS-K820-STATUS                  
              MOVE     WS-K820-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-KC20-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC20 ERROR ' WS-KC20-STATUS                  
              MOVE     WS-KC20-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-KC25-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC25 ERROR ' WS-KC25-STATUS                  
              MOVE     WS-KC25-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-KC60-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC60 ERROR ' WS-KC60-STATUS                  
              MOVE     WS-KC60-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-KC70-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC70 ERROR ' WS-KC70-STATUS                  
              MOVE     WS-KC70-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C45D CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13FC60 READ      : ' WS-CNT-FC60-READ.          
           DISPLAY ' 2. NO OF BP13KC60 UPDATED   : ' WS-CNT-KC60-UPD.           
           DISPLAY ' 3. NO OF BP13KC70 WRITTEN   : ' WS-CNT-KC70-WRITE.         
           DISPLAY ' 4. NO OF SATP CASES         : ' WS-SATP.                   
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
