       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C94E.                                                  
      *DATE-WRITTEN. 27 APR 2017.                                               
      *AUTHOR.       KRISTINE LANTIN.                                           
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: 1) TO CREATE INTERFACING FILE TO BB24 OF ALL   *            
      *                REJECTED/ACCEPTED (APPLICANT'S DECISION)    *            
      *                CASES AND THOSE WHOSE CONTRA TAG WAS UPDATED*            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP136743 20170427 KVL1  NEW PROGRAM                        *            
      * BP137588 20180118 ZS13  ADD MULTIPLE-CONTRA-TAG            *            
      * BP137558 20190326 DN8   TO CATER FOR CONTRA-CPF1,2,3,4 AMTS*            
      * BP137558 20190328 DN8   TO CATER FOR CONTRA-CPF1,2,3,4 AMTS*            
      * BP137558 20190404 DN8   TO CATER FOR CASH PROCEED AS SUM OF*            
      *                         INTEREST AND CASH                  *            
      * BP137758 20190405 DN8   TO CATER FOR CASH PROCEED AS SUM OF*            
      *                         INTEREST AND CASH                  *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *-------------------------------------------------------------            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F48K  ASSIGN        TO BP13F48K.                          
           SELECT SY02F001  ASSIGN        TO SY02F001.                          
           SELECT BP13F94E  ASSIGN        TO BP13F94E.                          
           SELECT BP13K480  ASSIGN        TO BP13K480                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K480-KEY-FLD                       
                            FILE STATUS   IS BP13K480-STATUS.                   
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F48K                                                             
           RECORD CONTAINS 1200 CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       COPY BP13F48K.                                                           
                                                                                
       FD  BP13F94E                                                             
           RECORD CONTAINS 150  CHARACTERS                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F94E.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13K480                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K480.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-READ-CNT         PIC 9(06)  VALUE ZEROES.                         
       01  WS-WRITE-CNT        PIC 9(06)  VALUE ZEROES.                         
       01  WS-BP13F48K-EOF     PIC X      VALUE 'N'.                            
       01  WS-WRITE-TAG        PIC X      VALUE 'N'.                            
       01  BP13K480-STATUS     PIC 9(02)  VALUE ZEROES.                         
       01  WS-K480-FND         PIC X(01)  VALUE 'N'.                            
       01  WS-I                PIC 9(02)  VALUE 1.                              
       01  WS-TOTAL-INT        PIC S9(07)V99 VALUE ZEROES.                      
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2100-READ-SY02F001       THRU 2100-EXIT.                     
           PERFORM 2000-READ-BP13F48K       THRU 2000-EXIT.                     
           IF WS-BP13F48K-EOF = 'Y'                                             
              DISPLAY 'FILE BP13F48K IS  EMPTY'                                 
           ELSE                                                                 
              PERFORM 3000-PROCESS-DATA     THRU 3000-EXIT                      
                UNTIL WS-BP13F48K-EOF = 'Y'                                     
           END-IF                                                               
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F48K                                                 
                       SY02F001                                                 
                       BP13K480                                                 
                OUTPUT BP13F94E.                                                
                                                                                
           IF BP13K480-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K480 OPEN ERROR, STATUS : ' BP13K480-STATUS          
              MOVE     BP13K480-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F48K.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F48K AT END                                                 
                         MOVE 'Y'  TO WS-BP13F48K-EOF                           
                         NOT AT END                                             
                         ADD  1    TO WS-READ-CNT                               
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-SY02F001.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ SY02F001 AT END                                                 
                DISPLAY 'EMPTY FILE - SY02F001'                                 
                MOVE    10                 TO   RETURN-CODE                     
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                      
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2200-READ-BP13K480.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES  TO   WS-K480-FND WS-WRITE-TAG                           
                                                                                
           READ BP13K480.                                                       
                                                                                
           EVALUATE BP13K480-STATUS                                             
              WHEN 00                                                           
                 MOVE 'Y'             TO WS-K480-FND                            
                 MOVE 'Y'             TO WS-WRITE-TAG                           
              WHEN 10                                                           
              WHEN 23                                                           
              WHEN 46                                                           
                 MOVE 'N'             TO WS-K480-FND                            
                 MOVE 'N'             TO WS-WRITE-TAG                           
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K480. STATUS IS '                   
                            BP13K480-STATUS                                     
                 MOVE BP13K480-STATUS TO RETURN-CODE                            
                 PERFORM 9000-CLOSE-ROUTINE                                     
                    THRU 9000-EXIT                                              
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'                         TO WS-WRITE-TAG.                    
           MOVE SPACES                      TO BP13F94E-REC.                    
           IF F48K-DTE-KEY-ISS              = F001-DTE-CURRENT                  
              MOVE F48K-DTE-KEY-ISS         TO F94E-DTE-KEY-ISSUE               
              MOVE F48K-CASH-PROCEED        TO F94E-CASH-PROCEED                
              MOVE 'Y'                      TO WS-WRITE-TAG                     
           END-IF.                                                              
                                                                                
           MOVE SPACES                   TO BP13K480-REC.                       
           MOVE F48K-NUM-REGN            TO K480-NUM-REGN.                      
           PERFORM 2200-READ-BP13K480  THRU 2200-EXIT                           
                                                                                
           IF F48K-DTE-UPDATE-CONTRA = F001-DTE-CURRENT OR                      
              F48K-DTE-CRM-UPDATED   = F001-DTE-CURRENT                         
              MOVE 'Y'                      TO WS-WRITE-TAG                     
           END-IF.                                                              
                                                                                
           IF WS-WRITE-TAG = 'Y'                                                
              PERFORM 3100-MOVE-TO-OUTPUT THRU 3100-EXIT                        
           END-IF.                                                              
           PERFORM 2000-READ-BP13F48K     THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3100-MOVE-TO-OUTPUT.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE F48K-NUM-CASE            TO F94E-NUM-CASE.                      
           MOVE F48K-RES-NUM-HDB-REF     TO F94E-RES-NUM-HDB-REF.               
           MOVE F48K-CONTRA-TAG          TO F94E-CONTRA-TAG.                    
           MOVE F48K-NUM-HDB-REF         TO F94E-NUM-HDB-REF.                   
           MOVE F48K-APPN-DECISION       TO F94E-APPN-DECISION.                 
           MOVE F48K-MULTIPLE-CONTRA-ALLOWED TO                                 
                                            F94E-MULTIPLE-CONTRA-TAG            
           MOVE F001-DTE-CURRENT         TO F94E-DTE-INTERFACE.                 
                                                                                
           IF WS-K480-FND = 'Y'                                                 
            IF K480-NUM-CONTRA-TYPE = 'TM'                                      
                                                                                
               IF K480-AMT-TEMP-LOAN-CPF1 NOT NUMERIC                           
                 MOVE ZEROES                  TO F94E-AMT-CONTRA-CPF1           
                ELSE                                                            
                 MOVE K480-AMT-TEMP-LOAN-CPF1 TO F94E-AMT-CONTRA-CPF1           
               END-IF                                                           
                                                                                
               IF K480-AMT-TEMP-LOAN-CPF2 NOT NUMERIC                           
                 MOVE ZEROES                  TO F94E-AMT-CONTRA-CPF2           
                ELSE                                                            
                 MOVE K480-AMT-TEMP-LOAN-CPF2 TO F94E-AMT-CONTRA-CPF2           
               END-IF                                                           
                                                                                
               IF K480-AMT-TEMP-LOAN-CPF3 NOT NUMERIC                           
                 MOVE ZEROES                  TO F94E-AMT-CONTRA-CPF3           
                ELSE                                                            
                 MOVE K480-AMT-TEMP-LOAN-CPF3 TO F94E-AMT-CONTRA-CPF3           
               END-IF                                                           
                                                                                
               IF K480-AMT-TEMP-LOAN-CPF4 NOT NUMERIC                           
                 MOVE ZEROES                  TO F94E-AMT-CONTRA-CPF4           
                ELSE                                                            
                 MOVE K480-AMT-TEMP-LOAN-CPF4 TO F94E-AMT-CONTRA-CPF4           
               END-IF                                                           
                                                                                
               MOVE   1              TO WS-I                                    
               MOVE   ZEROES         TO WS-TOTAL-INT                            
                                                                                
               PERFORM UNTIL WS-I > 12                                          
                 IF K480-AMT-TEMP-LOAN-INT (WS-I) IS NUMERIC                    
                    COMPUTE WS-TOTAL-INT =  WS-TOTAL-INT +                      
                                            K480-AMT-TEMP-LOAN-INT(WS-I)        
                    ADD  1 TO WS-I                                              
                   ELSE                                                         
                    MOVE ZEROES TO K480-AMT-TEMP-LOAN-INT(WS-I)                 
                    ADD  1 TO WS-I                                              
                  END-IF                                                        
               END-PERFORM                                                      
                                                                                
               IF K480-AMT-TEMP-LOAN-CASH IS NUMERIC                            
                 COMPUTE F94E-CASH-PROCEED = K480-AMT-TEMP-LOAN-CASH            
                                                     + WS-TOTAL-INT             
                ELSE                                                            
                 MOVE ZEROES                TO K480-AMT-TEMP-LOAN-CASH          
                 COMPUTE F94E-CASH-PROCEED = K480-AMT-TEMP-LOAN-CASH +          
                                              WS-TOTAL-INT                      
               END-IF                                                           
                                                                                
            ELSE                                                                
             IF K480-NUM-CONTRA-TYPE = 'TU' OR 'DT'                             
                                                                                
               IF K480-AMT-CONTRA-CPF1    NOT NUMERIC                           
                 MOVE ZEROES                  TO F94E-AMT-CONTRA-CPF1           
                ELSE                                                            
                 MOVE K480-AMT-CONTRA-CPF1    TO F94E-AMT-CONTRA-CPF1           
               END-IF                                                           
                                                                                
               IF K480-AMT-CONTRA-CPF2    NOT NUMERIC                           
                 MOVE ZEROES                  TO F94E-AMT-CONTRA-CPF2           
                ELSE                                                            
                 MOVE K480-AMT-CONTRA-CPF2    TO F94E-AMT-CONTRA-CPF2           
               END-IF                                                           
                                                                                
               IF K480-AMT-CONTRA-CPF3    NOT NUMERIC                           
                 MOVE ZEROES                  TO F94E-AMT-CONTRA-CPF3           
                ELSE                                                            
                 MOVE K480-AMT-CONTRA-CPF3    TO F94E-AMT-CONTRA-CPF3           
               END-IF                                                           
                                                                                
               IF K480-AMT-CONTRA-CPF4    NOT NUMERIC                           
                 MOVE ZEROES                  TO F94E-AMT-CONTRA-CPF4           
                ELSE                                                            
                 MOVE K480-AMT-CONTRA-CPF4    TO F94E-AMT-CONTRA-CPF4           
               END-IF                                                           
                                                                                
               MOVE   1      TO WS-I                                            
               MOVE   ZEROES TO WS-TOTAL-INT                                    
                                                                                
               PERFORM UNTIL WS-I > 12                                          
                 IF K480-AMT-TEMP-LOAN-INT (WS-I) IS NUMERIC                    
                    COMPUTE WS-TOTAL-INT =  WS-TOTAL-INT +                      
                                            K480-AMT-TEMP-LOAN-INT(WS-I)        
                    ADD  1 TO WS-I                                              
                   ELSE                                                         
                    MOVE ZEROES TO K480-AMT-TEMP-LOAN-INT(WS-I)                 
                    ADD  1 TO WS-I                                              
                  END-IF                                                        
               END-PERFORM                                                      
                                                                                
               IF K480-AMT-CONTRA-CASH    IS NUMERIC                            
                 COMPUTE F94E-CASH-PROCEED = K480-AMT-CONTRA-CASH               
                                                   + WS-TOTAL-INT               
                ELSE                                                            
                 MOVE ZEROES                TO K480-AMT-CONTRA-CASH             
                 COMPUTE F94E-CASH-PROCEED = K480-AMT-CONTRA-CASH +             
                                             WS-TOTAL-INT                       
               END-IF                                                           
             END-IF                                                             
            END-IF                                                              
           END-IF.                                                              
                                                                                
           WRITE BP13F94E-REC.                                                  
           ADD 1                         TO WS-WRITE-CNT.                       
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '---- BP13C94E CONTROL TOTAL -----'.                         
           DISPLAY ' NO OF RECS READ FROM BP13F48K   = '                        
                     WS-READ-CNT.                                               
           DISPLAY ' NO OF RECS WRITTEN TO INT FILE  = '                        
                     WS-WRITE-CNT.                                              
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BP13F48K                                                 
                       SY02F001                                                 
                       BP13K480                                                 
                       BP13F94E.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
