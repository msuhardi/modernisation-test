       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C45K.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  17/01/2016.                                               
      * ========================================================== *            
      * OBJECTIVE    : DAILY PROGRAM TO GET ALL SELF BOOKING CASES *            
      *                BOOKED THRU THE DAY                         *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13F310                               *            
      *                 2.) BP13KC20                               *            
      *                 3.) BP13K454                               *            
      * OUTPUT FILES :  1.) BP13MAIL                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP136561 KSJ3 17/01/2017 NEW PROGRAM                       *            
      * BP136561 KSJ3 31/01/2017 INCL FLAT UNIT IN EMAIL REPORT    *            
      * BP136561 KSJ3 21/02/2017 ADD 02 TO SUCCESSFUL READ         *            
      * BP136980 RJB1 20/10/2017 TO CASTER FOR SATP CASE           *            
      * BP137239 PCL4 01/03/2018 TO UPDATE EMAIL RECIPIENTS        *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310  ASSIGN        TO BP13F310.                          
                                                                                
           SELECT BP13KC20  ASSIGN        TO BP13KC20                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KC20-KEY-FLD                       
                            ALTERNATE KEY IS KC20-ALT-KEY                       
                            FILE STATUS   IS WS-KC20-STATUS.                    
                                                                                
           SELECT BP13K454  ASSIGN        TO BP13K454                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K454-KEY-FLD                       
                            ALTERNATE KEY IS K454-ALT1-KEY                      
                            FILE STATUS   IS WS-K454-STATUS.                    
                                                                                
           SELECT BP13MAIL  ASSIGN        TO BP13MAIL.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F310                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 150 CHARACTERS.                                      
           COPY BP13F310.                                                       
                                                                                
       FD  BP13KC20                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
           COPY BP13KC20.                                                       
                                                                                
       FD  BP13K454                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
           COPY BP13K454.                                                       
                                                                                
       FD  BP13MAIL                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13MAIL-REC                  PIC X(132).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
      *-------------------------------------------------------------            
      *    BP13C913 COMM-AREA                                                   
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH       PIC X(4)   VALUE SPACES.               
               10  WS-LINK-NUM-SCH       PIC X(5)   VALUE SPACES.               
                                                                                
       COPY P13COMM8.                                                           
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-KC20-STATUS            PIC 9(2)   VALUE ZEROES.               
           05  WS-K454-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F310-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F310-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-MAIL-WRITE         PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-PAGE               PIC 9(4)   VALUE 1.                    
           05  WS-CNT-LINE               PIC 9(2)   VALUE 61.                   
           05  WS-CNT-SNO                PIC 9(4)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F310-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-KC20-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K454-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-PIDB-FND               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE ZEROES.               
           05  WS-DTE-ADD                PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-ADD-INT            PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-PREV-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-PREV-10            PIC X(10)  VALUE ZEROES.               
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-SQL-CODE               PIC 9(4)   VALUE 0.                    
                                                                                
      *-------------------------------------------------------------            
      *    EMAIL REPORT COMMANDS                                                
      *-------------------------------------------------------------            
       01  MAIL-COMMANDS.                                                       
           05  WS-MAIL-HELO              PIC X(132) VALUE                       
               'HELO SGPHDB1'.                                                  
           05  WS-MAIL-MAIL-FROM         PIC X(132) VALUE                       
               'MAIL FROM:<OPCP@SGPHDB1>'.                                      
           05  WS-MAIL-RCPT-TO-1         PIC X(132) VALUE                       
               'RCPT TO:<DERRICK_WK_LOH@HDB.GOV.SG>'.                           
           05  WS-MAIL-RCPT-TO-2         PIC X(132) VALUE                       
               'RCPT TO:<JAMILAH_BAHROM@HDB.GOV.SG>'.                           
           05  WS-MAIL-RCPT-TO-3         PIC X(132) VALUE                       
               'RCPT TO:<TOH_LAY_YEN@HDB.GOV.SG>'.                              
           05  WS-MAIL-RCPT-TO-4         PIC X(132) VALUE                       
               'RCPT TO:<JUARIAH_ALI@HDB.GOV.SG>'.                              
           05  WS-MAIL-RCPT-TO-5         PIC X(132) VALUE                       
               'RCPT TO:<KOGILA_RAMARAJAN@HDB.GOV.SG>'.                         
           05  WS-MAIL-RCPT-TO-6         PIC X(132) VALUE                       
               'RCPT TO:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                            
           05  WS-MAIL-DATA              PIC X(132) VALUE 'DATA'.               
           05  WS-MAIL-FROM              PIC X(132) VALUE                       
               'FROM:Soc System - Email Alert'.                                 
           05  WS-MAIL-TO-1              PIC X(132) VALUE                       
               'TO:<DERRICK_WK_LOH@HDB.GOV.SG>'.                                
           05  WS-MAIL-TO-2              PIC X(132) VALUE                       
               'TO:<JAMILAH_BAHROM@HDB.GOV.SG>'.                                
           05  WS-MAIL-TO-3              PIC X(132) VALUE                       
               'TO:<TOH_LAY_YEN@HDB.GOV.SG>'.                                   
           05  WS-MAIL-TO-4              PIC X(132) VALUE                       
               'TO:<JUARIAH_ALI@HDB.GOV.SG>'.                                   
           05  WS-MAIL-TO-5              PIC X(132) VALUE                       
               'TO:<KOGILA_RAMARAJAN@HDB.GOV.SG>'.                              
           05  WS-MAIL-TO-6              PIC X(132) VALUE                       
               'CC:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                                 
           05  WS-MAIL-SUBJECT.                                                 
               10  FILLER                PIC X(132) VALUE                       
                   'SUBJECT : List of Self Booking Cases Booked / Change        
      -            'd for the day'.                                             
           05  WS-MAIL-DATE.                                                    
               10  FILLER                PIC X(6)   VALUE                       
                   'DATE: '.                                                    
               10  WS-MAIL-DTE-CURRENT   PIC X(10)  VALUE SPACES.               
                                                                                
      *-------------------------------------------------------------            
      *    BP13L45K REPORT                                                      
      *-------------------------------------------------------------            
       01  L45K-HEADER1.                                                        
           05  FILLER                    PIC X(8)   VALUE 'BP13L45K'.           
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE 'HDBCAT 3'.           
           05  FILLER                    PIC X(28)  VALUE SPACES.               
           05  FILLER                    PIC X(39)  VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                    PIC X(17)  VALUE SPACES.               
           05  FILLER                    PIC X(6)   VALUE 'DATE: '.             
           05  L45K-DTE-CURRENT          PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE '  PAGE: '.           
           05  L45K-CNT-PAGE             PIC ZZZ9.                              
                                                                                
       01  L45K-HEADER2.                                                        
           05  FILLER                    PIC X(32)  VALUE SPACES.               
           05  FILLER                    PIC X(57)  VALUE                       
               'LIST OF SELF BOOKING CASES BOOKED / CHANGED FOR THE DAY         
      -        '('.                                                             
           05  L45K-DTE-CURRENT2         PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(1)   VALUE ')'.                  
                                                                                
       01  L45K-HEADER3.                                                        
           05  FILLER                    PIC X(132) VALUE                       
               'S/NO   REGN       APPT TYPE   APPT DATE      APPT TIME          
      -        ' BLOCK   UNIT      STREET NAME'.                                
                                                                                
       01  L45K-HEADER4.                                                        
           05  FILLER                    PIC X(132) VALUE                       
               '----   --------   ---------   ------------   ---------          
      -        ' -----   -------   --------------------------------'.           
                                                                                
       01  L45K-DETAIL.                                                         
           05  L45K-SNO                  PIC ZZZ9.                              
           05  FILLER                    PIC X(3)   VALUE SPACES.               
           05  L45K-NUM-REGN             PIC X(8)   VALUE SPACES.               
           05  FILLER                    PIC X(3)   VALUE SPACES.               
           05  L45K-NUM-APPT-TYPE        PIC X(4)   VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE SPACES.               
           05  L45K-DTE-APPT             PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  L45K-TME-APPT             PIC X(5)   VALUE SPACES.               
           05  FILLER                    PIC X(7)   VALUE SPACES.               
           05  L45K-NUM-BLK              PIC X(5)   VALUE SPACES.               
           05  FILLER                    PIC X(3)   VALUE SPACES.               
           05  L45K-NUM-UNIT             PIC X(7)   VALUE SPACES.               
           05  FILLER                    PIC X(3)   VALUE SPACES.               
           05  L45K-NME-STREET           PIC X(32)  VALUE SPACES.               
                                                                                
       01  L45K-NIL.                                                            
           05  FILLER                    PIC X(50)  VALUE SPACES.               
           05  FILLER                    PIC X(32)  VALUE                       
               '***** NO RECORDS PROCESSED *****'.                              
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
           PERFORM 1200-PRINT-HEADER       THRU 1200-EXIT.                      
           PERFORM 2000-BP13F310-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F310-EOF = 'Y'.                                     
                                                                                
           IF WS-CNT-MAIL-WRITE = ZEROES                                        
              PERFORM 1300-PRINT-NIL       THRU 1300-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13F310                                                 
                       BP13KC20                                                 
                       BP13K454                                                 
                OUTPUT BP13MAIL.                                                
                                                                                
           IF WS-KC20-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC20 ERROR ' WS-KC20-STATUS                  
              MOVE     WS-KC20-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K454-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K454 ERROR ' WS-K454-STATUS                  
              MOVE     WS-K454-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    GET CURRENT DATE                                                     
      *-------------------------------------------------------------            
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
      *-------------------------------------------------------------            
      *    GET PREVIOUS DATE                                                    
      *-------------------------------------------------------------            
           MOVE WS-DTE-CURR-8              TO   WS-DTE-ADD.                     
           COMPUTE WS-DTE-ADD-INT = FUNCTION INTEGER-OF-DATE                    
                                    (WS-DTE-ADD).                               
           SUBTRACT 1                      FROM WS-DTE-ADD-INT.                 
           COMPUTE WS-DTE-ADD     = FUNCTION DATE-OF-INTEGER                    
                                    (WS-DTE-ADD-INT).                           
           MOVE WS-DTE-ADD                 TO   WS-DTE-PREV-8.                  
           MOVE WS-DTE-PREV-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-PREV-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1200-PRINT-HEADER.                                                       
      *=============================================================            
                                                                                
           MOVE WS-DTE-CURR-10             TO   WS-MAIL-DTE-CURRENT             
                                                L45K-DTE-CURRENT.               
           MOVE WS-DTE-PREV-10             TO   L45K-DTE-CURRENT2.              
                                                                                
           MOVE WS-CNT-PAGE                TO   L45K-CNT-PAGE.                  
                                                                                
           WRITE BP13MAIL-REC              FROM WS-MAIL-HELO.                   
           WRITE BP13MAIL-REC              FROM WS-MAIL-MAIL-FROM.              
           WRITE BP13MAIL-REC              FROM WS-MAIL-RCPT-TO-1.              
           WRITE BP13MAIL-REC              FROM WS-MAIL-RCPT-TO-2.              
           WRITE BP13MAIL-REC              FROM WS-MAIL-RCPT-TO-3.              
           WRITE BP13MAIL-REC              FROM WS-MAIL-RCPT-TO-4.              
           WRITE BP13MAIL-REC              FROM WS-MAIL-RCPT-TO-5.              
           WRITE BP13MAIL-REC              FROM WS-MAIL-RCPT-TO-6.              
           WRITE BP13MAIL-REC              FROM WS-MAIL-DATA.                   
           WRITE BP13MAIL-REC              FROM WS-MAIL-FROM.                   
           WRITE BP13MAIL-REC              FROM WS-MAIL-TO-1.                   
           WRITE BP13MAIL-REC              FROM WS-MAIL-TO-2.                   
           WRITE BP13MAIL-REC              FROM WS-MAIL-TO-3.                   
           WRITE BP13MAIL-REC              FROM WS-MAIL-TO-4.                   
           WRITE BP13MAIL-REC              FROM WS-MAIL-TO-5.                   
           WRITE BP13MAIL-REC              FROM WS-MAIL-TO-6.                   
           WRITE BP13MAIL-REC              FROM WS-MAIL-SUBJECT.                
           WRITE BP13MAIL-REC              FROM WS-MAIL-DATE.                   
                                                                                
           WRITE BP13MAIL-REC              FROM L45K-HEADER1.                   
           WRITE BP13MAIL-REC              FROM L45K-HEADER2.                   
                                                                                
           MOVE SPACES                     TO   BP13MAIL-REC.                   
           WRITE BP13MAIL-REC.                                                  
                                                                                
           WRITE BP13MAIL-REC              FROM L45K-HEADER3.                   
           WRITE BP13MAIL-REC              FROM L45K-HEADER4.                   
                                                                                
           MOVE SPACES                     TO   BP13MAIL-REC.                   
           WRITE BP13MAIL-REC.                                                  
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1300-PRINT-NIL.                                                          
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13MAIL-REC.                   
           WRITE BP13MAIL-REC.                                                  
                                                                                
           WRITE BP13MAIL-REC FROM L45K-NIL.                                    
                                                                                
       1300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13F310-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13F310                                                        
              AT END MOVE 'Y'              TO   WS-F310-EOF.                    
                                                                                
           IF WS-F310-EOF NOT = 'Y'                                             
              ADD 1                        TO   WS-CNT-F310-READ                
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    SKIP RECORDS WHERE DATE UPDATE NOT = DTE PREV AND DTE-CURR           
      *-------------------------------------------------------------            
           IF F310-DTE-UPDATE NOT = WS-DTE-PREV-8 AND WS-DTE-CURR-8             
              GO TO 3000-CONTINUE-READ-NEXT.                                    
                                                                                
      *-------------------------------------------------------------            
      *    SKIP NON-TP AND NON-SATP RECORDS                                     
      *-------------------------------------------------------------            
           IF F310-CDE-TRANS NOT = 'TP' AND 'SP'                                
              GO TO 3000-CONTINUE-READ-NEXT.                                    
                                                                                
      *-------------------------------------------------------------            
      *    SKIP NON-SELF BOOKING RECORDS                                        
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13KC20-REC.                   
           MOVE F310-REGN-NO               TO   KC20-NUM-REGN.                  
                                                                                
           PERFORM 3100-BP13KC20-READ      THRU 3100-EXIT.                      
                                                                                
           IF WS-KC20-FND NOT = 'Y'                                             
              GO TO 3000-CONTINUE-READ-NEXT.                                    
                                                                                
      *-------------------------------------------------------------            
      *    GET APPT DATE AND TIME                                               
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   BP13K454-REC.                   
           MOVE F310-REGN-NO               TO   K454-NUM-REGN.                  
           MOVE F310-CDE-TRANS             TO   K454-CDE-APPT-TYPE.             
                                                                                
           PERFORM 3200-BP13K454-READ      THRU 3200-EXIT.                      
                                                                                
           IF WS-K454-FND NOT = 'Y'                                             
              GO TO 3000-CONTINUE-READ-NEXT.                                    
                                                                                
      *-------------------------------------------------------------            
      *    GET BLOCK AND STREET NAME                                            
      *-------------------------------------------------------------            
           MOVE SPACES                     TO   WS-LINK-REC                     
                                                BP13COMM8-REC.                  
           MOVE F310-SCH-ACC               TO   WS-LINK-REC.                    
                                                                                
           PERFORM 8000-CALL-BP13C913      THRU 8000-EXIT.                      
                                                                                
           IF WS-PIDB-FND NOT = 'Y'                                             
              MOVE SPACES                  TO   COMM8-NUM-BLK                   
                                                COMM8-NME-STREET                
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    WRITE MAIL DETAILS                                                   
      *-------------------------------------------------------------            
           PERFORM 4000-WRITE-MAIL         THRU 4000-EXIT.                      
                                                                                
       3000-CONTINUE-READ-NEXT.                                                 
                                                                                
           PERFORM 2000-BP13F310-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-BP13KC20-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13KC20 KEY IS KC20-ALT-KEY.                                   
                                                                                
           EVALUATE WS-KC20-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-KC20-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-KC20-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13KC20 ERROR '    WS-KC20-STATUS                  
              MOVE    WS-KC20-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3200-BP13K454-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K454 KEY IS K454-ALT1-KEY.                                  
                                                                                
           EVALUATE WS-K454-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              MOVE 'Y'                     TO   WS-K454-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K454-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K454 ERROR '    WS-K454-STATUS                  
              MOVE    WS-K454-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4000-WRITE-MAIL.                                                         
      *=============================================================            
                                                                                
           ADD  1                          TO   WS-CNT-MAIL-WRITE.              
           ADD  1                          TO   WS-CNT-SNO.                     
                                                                                
           MOVE SPACES                     TO   BP13MAIL-REC.                   
           MOVE WS-CNT-SNO                 TO   L45K-SNO.                       
           MOVE F310-REGN-NO               TO   L45K-NUM-REGN.                  
                                                                                
           IF K454-CDE-APPT = 'W'                                               
              IF KC20-NUM-APPT-TYPE = 'SP'                                      
              MOVE 'WP'                    TO   L45K-NUM-APPT-TYPE              
              ELSE                                                              
              MOVE 'WT'                    TO   L45K-NUM-APPT-TYPE              
              END-IF                                                            
           ELSE                                                                 
              IF KC20-NUM-APPT-TYPE = 'SP'                                      
              MOVE 'SP'                    TO   L45K-NUM-APPT-TYPE              
              ELSE                                                              
              MOVE 'TP'                    TO   L45K-NUM-APPT-TYPE              
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE K454-DTE-APPT              TO   L45K-DTE-APPT.                  
           MOVE K454-TME-APPT              TO   L45K-TME-APPT.                  
           MOVE COMM8-NUM-BLK              TO   L45K-NUM-BLK.                   
           MOVE COMM8-NME-STREET           TO   L45K-NME-STREET.                
           MOVE SPACES                     TO   L45K-NUM-UNIT.                  
           STRING COMM8-NUM-LEVEL '-' COMM8-NUM-UNIT-MAIN                       
                  DELIMITED BY SIZE        INTO L45K-NUM-UNIT.                  
                                                                                
           WRITE BP13MAIL-REC              FROM L45K-DETAIL.                    
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8000-CALL-BP13C913.                                                      
      *=============================================================            
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
           WHEN ZEROES                                                          
           WHEN SPACES                                                          
           WHEN LOW-VALUES                                                      
              MOVE 'Y'                     TO   WS-PIDB-FND                     
                                                                                
           WHEN 100                                                             
              MOVE 'N'                     TO   WS-PIDB-FND                     
                                                                                
           WHEN OTHER                                                           
              MOVE 99                      TO   RETURN-CODE                     
              MOVE COMM8-CDE-SYSERR        TO   WS-SQL-CODE                     
              DISPLAY 'ERROR CALLING BP13C913. CODE: ' WS-SQL-CODE              
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13F310                                                       
                 BP13KC20                                                       
                 BP13K454                                                       
                 BP13MAIL.                                                      
                                                                                
           IF WS-KC20-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC20 ERROR ' WS-KC20-STATUS                  
              MOVE     WS-KC20-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K454-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K454 ERROR ' WS-K454-STATUS                  
              MOVE     WS-K454-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C45K CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13F310 READ      : ' WS-CNT-F310-READ.          
           DISPLAY ' 3. NO OF BP13MAIL WRITTEN   : ' WS-CNT-MAIL-WRITE.         
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
