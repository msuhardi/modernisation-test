       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C10D.                                                 
       AUTHOR.        IRVIN CHING.                                              
       DATE-WRITTEN.  21/09/2012.                                               
      *****************************************************************         
      * OBJECTIVE - TO GENERATE REPORT FOR ACTIVE DBSS CASES WITH NO LOF        
      *             RAISED 7 WEEKS AFTER BOOKING DATE.                          
      *----------------------------------------------------------------         
      * REF NO   DATE    BY   AMENDMENTS/ENHANCEMENTS                           
      * -------  ----    ---  -----------------------                           
      * BP132386 210912  IMC1 NEW PGM                                           
      * BP132386 130213  JGO1 COMPLETE PROGRAM                                  
      * BP138047 251119  KAC1 RECOMPILE FOR BP27 COPYBOOK UPDATE                
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13KD00  ASSIGN       TO BP13KD00                            
                            ACCESS MODE  IS SEQUENTIAL                          
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS KD00-NUM-REGN                       
                            FILE STATUS  IS KD00-STATUS.                        
                                                                                
           SELECT BP13K800  ASSIGN TO BP13K800                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K800-NUM-REGN                             
                  FILE STATUS      IS K800-STATUS                               
                  ACCESS           IS RANDOM.                                   
                                                                                
           SELECT BP27K800  ASSIGN TO BP27K800                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K800-NUM-HLA                              
                                      OF BP27K800-MASTER                        
                  FILE STATUS      IS BP27K800-STATUS                           
                  ACCESS           IS RANDOM.                                   
                                                                                
                                                                                
           SELECT BP13K022  ASSIGN       TO BP13K022                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K022-KEY-FLD                        
                            ALTERNATE KEY IS K022-NUM-REGN                      
                            FILE STATUS  IS K022-STATUS.                        
                                                                                
           SELECT BP27K025  ASSIGN       TO BP27K025                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K025-KEY-FLD                        
                            ALTERNATE KEY IS K025-NUM-HLA                       
                            FILE STATUS  IS K025-STATUS.                        
                                                                                
           SELECT BP13K024  ASSIGN TO BP13K024                                  
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K024-KEY-FLD                              
                  FILE STATUS      IS K024-STATUS                               
                  ACCESS           IS RANDOM.                                   
                                                                                
           SELECT BP13FOUT  ASSIGN TO BP13FOUT.                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13KD00                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13KD00.                                                           
                                                                                
       FD  BP13FOUT                   RECORD CONTAINS 140 CHARACTERS            
                                      LABEL RECORD IS OMITTED.                  
       01 BP13FOUT-REC            PIC X(140).                                   
                                                                                
       FD  BP13K800                                                             
                                      RECORD CONTAINS 2000 CHARACTERS.          
       COPY BP13K800.                                                           
                                                                                
       FD  BP27K800                                                             
                                      RECORD CONTAINS 1200 CHARACTERS.          
       COPY BP27K800.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP27K025                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP27K025.                                                           
                                                                                
       FD   BP13K024                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K024.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-STATUS.                                                           
           05 KD00-STATUS              PIC 99       VALUE ZEROS.                
           05 FOUT-STATUS              PIC 99       VALUE ZEROS.                
           05 K800-STATUS              PIC 99       VALUE ZEROS.                
           05 K022-STATUS              PIC 99       VALUE ZEROS.                
           05 K025-STATUS              PIC 99       VALUE ZEROS.                
           05 K024-STATUS              PIC 99       VALUE ZEROS.                
           05 BP27K800-STATUS          PIC 99       VALUE ZEROS.                
           05 WS-KD00-END              PIC X        VALUE 'N'.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-CNT-KD00-RD           PIC 9(6) COMP VALUE 0.                   
           05 WS-CNT-FOUT-WRITE        PIC 9(6) COMP VALUE 0.                   
           05 WS-CURR-MM-DAYS          PIC 9(2) COMP VALUE 0.                   
           05 WS-SNO                   PIC 9(4) VALUE 0.                        
           05 WS-SNO-Z                 PIC ZZZ9 VALUE ZEROES.                   
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-NUM-DAYS              PIC 9(4) COMP VALUE 0.                   
           05 WS-INT-NUM-DAYS          PIC 9(9) COMP VALUE 0.                   
           05 WS-INT-DTE-ACCEPTANCE    PIC 9(9) COMP VALUE 0.                   
           05 WS-INT-CURRENT-DATE      PIC 9(9) COMP VALUE 0.                   
           05 WS-NUM-QUOTIENT          PIC 9(6) COMP VALUE 0.                   
           05 WS-NUM-REMAINDER         PIC 9(6) COMP VALUE 0.                   
                                                                                
       01  WS-CURR-DATE-NUM            PIC 9(8).                                
       01  WS-ACCEPT-DT-NUM            PIC 9(8).                                
                                                                                
       01  WS-CURRENT-DATE1.                                                    
           05  WS-DATE1.                                                        
                10  WS-DATE1-CYM.                                               
                    15  WS-DATE1-CCYY  PIC X(4).                                
                    15  WS-DATE1-MM    PIC X(2).                                
                10  WS-DATE1-DD        PIC X(2).                                
           05  WS-CURRENT-TIME         PIC X(9).                                
           05  WS-TIME REDEFINES WS-CURRENT-TIME.                               
                10  WS-HH              PIC 9(2).                                
                10  WS-MM              PIC 9(2).                                
                10  WS-SS              PIC 9(2).                                
                10  WS-MS              PIC 9(2).                                
                10  WS-GM              PIC 9(1).                                
           05  FILLER                  PIC X(4).                                
                                                                                
       01  CS-DATE-VARIABLES.                                                   
           05  WS-DATE.                                                         
               10  WS-CCYY                   PIC 9(4).                          
               10  WS-MM                     PIC 9(2).                          
               10  WS-DD                     PIC 9(2).                          
           05  WS-DTE-KEY-ISSUED.                                               
               10  WS-KI-CCYY                PIC 9(4).                          
               10  WS-KI-MM                  PIC 9(2).                          
               10  WS-KI-DD                  PIC 9(2).                          
           05  WS-FUTURE-DATE.                                                  
               10  WS-DUE-CCYY               PIC 9(4).                          
               10  WS-DUE-MM                 PIC 9(2).                          
               10  WS-DUE-DD                 PIC 9(2).                          
                                                                                
       01  FOUT-LAYOUT.                                                         
           05  FOUT-HEADER.                                                     
               10  FILLER                    PIC X(35) VALUE SPACES.            
               10  FOUT-TITLE                PIC X(76).                         
                                                                                
           05  FOUT-HEADER1.                                                    
               10  FOUT-HD1                  PIC X(5) VALUE 'DATE '.            
               10  FOUT-DATE                 PIC X(10).                         
                                                                                
           05  FOUT-HEADER2.                                                    
               10  FOUT-HD2                  PIC X(13)                          
                                 VALUE 'PROJECT CODE '.                         
               10  FOUT-PROJECT-CODE         PIC X(3).                          
                                                                                
           05  FOUT-HEADER3.                                                    
               10  FILLER           PIC X(4)   VALUE 'S/NO'.                    
               10  FILLER           PIC X(3)   VALUE SPACES.                    
               10  FILLER           PIC X(12)  VALUE 'DBSS REGN NO'.            
               10  FILLER           PIC X(11)  VALUE SPACES.                    
               10  FILLER           PIC X(7)   VALUE 'SSS CSM'.                 
      *        10  FILLER           PIC X(27)   VALUE SPACES.                   
      *        10  FILLER           PIC X(7)   VALUE 'LOF OIC'.                 
               10  FILLER           PIC X(18)   VALUE SPACES.                   
               10  FILLER           PIC X(7)   VALUE 'HLE NO.'.                 
               10  FILLER           PIC X(3)   VALUE SPACES.                    
               10  FILLER           PIC X(7)   VALUE 'HLE OIC'.                 
               10  FILLER           PIC X(3)   VALUE SPACES.                    
               10  FILLER           PIC X(10)  VALUE 'HLE STATUS'.              
               10  FILLER           PIC X(3)   VALUE SPACES.                    
               10  FILLER           PIC X(15)  VALUE 'DATE OF BOOKING'.         
               10  FILLER           PIC X(3)   VALUE SPACES.                    
               10  FILLER           PIC X(35)  VALUE                            
                    'NO. OF DAYS FR. DATE OF BOOKING'.                          
                                                                                
           05  FOUT-DETAIL.                                                     
               10  FOUT-SNO                  PIC Z,ZZ9  VALUE ZEROES.           
               10  FILLER                    PIC X(2)   VALUE SPACES.           
               10  FOUT-NUM-REGN             PIC X(8)   VALUE SPACES.           
               10  FILLER                    PIC X(7)   VALUE SPACES.           
               10  FOUT-SOU-CSM              PIC X(30)  VALUE SPACES.           
      *        10  FILLER                    PIC X(3)   VALUE SPACES.           
      *        10  FOUT-LOF-OIC              PIC X(30)  VALUE SPACES.           
               10  FILLER                    PIC X(3)   VALUE SPACES.           
               10  FOUT-HLE-NUM              PIC X(9)   VALUE SPACES.           
               10  FILLER                    PIC X(1)   VALUE SPACES.           
               10  FOUT-HLE-OIC              PIC X(4)   VALUE SPACES.           
               10  FILLER                    PIC X(9)   VALUE SPACES.           
               10  FOUT-HLE-STATUS           PIC X(3)   VALUE SPACES.           
               10  FILLER                    PIC X(10)  VALUE SPACES.           
               10  FOUT-DTE-BOOKING          PIC X(10)  VALUE SPACES.           
               10  FILLER                    PIC X(15)  VALUE SPACES.           
               10  FOUT-NUM-DAYS             PIC Z,ZZ9  VALUE ZEROES.           
               10  FILLER                    PIC X(3)   VALUE SPACES.           
                                                                                
           05  FOUT-TRAILER.                                                    
               10  FILLER                    PIC X(35) VALUE SPACES.            
               10  FOUT-END                  PIC X(76).                         
                                                                                
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-START-UP       THRU 1000-EXIT.                          
           PERFORM 2000-MAIN-PROCESS   THRU 2000-EXIT                           
                   UNTIL WS-KD00-END = 'Y' .                                    
           IF WS-CNT-KD00-RD = 0                                                
              PERFORM 3500-WRITE-HEADER THRU 3500-EXIT                          
           END-IF.                                                              
      *    MOVE SPACES         TO  FOUT-TRAILER.                                
      *    MOVE '***** END OF REPORT  ******' TO FOUT-END.                      
      *    WRITE BP13FOUT-REC FROM FOUT-TRAILER.                                
           PERFORM 9999-WRAP-UP        THRU 9999-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-START-UP.                                                           
      ******************************************************************        
                                                                                
            OPEN INPUT  BP13KD00                                                
                        BP13K800                                                
                        BP27K800                                                
                        BP27K025                                                
                        BP13K022                                                
                        BP13K024                                                
                 OUTPUT BP13FOUT.                                               
                                                                                
           IF  K800-STATUS NOT = '00' AND '97'                                  
               DISPLAY 'ERROR OPEN BP13K800 : ' K800-STATUS                     
               MOVE K800-STATUS TO RETURN-CODE                                  
               PERFORM 9999-WRAP-UP        THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF  KD00-STATUS NOT = '00' AND '97'                                  
               DISPLAY 'ERROR OPEN BP13KD00 : ' KD00-STATUS                     
               MOVE KD00-STATUS TO RETURN-CODE                                  
               PERFORM 9999-WRAP-UP        THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF  BP27K800-STATUS NOT = '00' AND '97'                              
               DISPLAY 'ERROR OPEN BP27K800 : ' BP27K800-STATUS                 
               MOVE BP27K800-STATUS TO RETURN-CODE                              
               PERFORM 9999-WRAP-UP        THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF  K025-STATUS NOT = '00' AND '97'                                  
               DISPLAY 'ERROR OPEN BP27K025 : ' K025-STATUS                     
               MOVE K025-STATUS TO RETURN-CODE                                  
               PERFORM 9999-WRAP-UP        THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF  K022-STATUS NOT = '00' AND '97'                                  
               DISPLAY 'ERROR OPEN BP13K022 : ' K022-STATUS                     
               MOVE K022-STATUS TO RETURN-CODE                                  
               PERFORM 9999-WRAP-UP        THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF  K024-STATUS NOT = '00' AND '97'                                  
               DISPLAY 'ERROR OPEN BP13K024 : ' K024-STATUS                     
               MOVE K024-STATUS TO RETURN-CODE                                  
               PERFORM 9999-WRAP-UP        THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO  WS-CURRENT-DATE1.                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-MAIN-PROCESS.                                                       
      ******************************************************************        
                                                                                
           PERFORM 3000-READ-BP13KD00       THRU 3000-EXIT.                     
           IF WS-CNT-KD00-RD = 1                                                
              PERFORM 3500-WRITE-HEADER THRU 3500-EXIT                          
           END-IF.                                                              
           PERFORM 3100-READ-BP13K800       THRU 3100-EXIT.                     
           PERFORM 3200-CHECK-DATE          THRU 3200-EXIT.                     
           IF WS-CURRENT-DATE1 >= WS-FUTURE-DATE                                
              PERFORM 3300-MOVE-DATA           THRU 3300-EXIT                   
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-READ-BP13KD00.                                                      
      ******************************************************************        
                                                                                
           READ BP13KD00.                                                       
           IF KD00-STATUS = '00'                                                
              ADD 1 TO WS-CNT-KD00-RD                                           
              IF KD00-CDE-ACCT-STAT NOT = 'Z'                                   
                AND KD00-DTE-SNP-SIGNED = SPACES OR LOW-VALUES                  
                AND  KD00-DTE-SNP-RETURNED = SPACES OR LOW-VALUES               
                AND  KD00-DTE-OFFER-PRINTED = SPACES OR LOW-VALUES              
                   CONTINUE                                                     
              ELSE                                                              
                 GO TO 2000-EXIT                                                
              END-IF                                                            
           ELSE                                                                 
              IF KD00-STATUS = '10' OR '46'                                     
                 MOVE 'Y'         TO WS-KD00-END                                
                 GO TO 2000-EXIT                                                
              ELSE                                                              
               DISPLAY 'ERROR READING, BP13KD00:' KD00-STATUS                   
               MOVE     KD00-STATUS TO RETURN-CODE                              
               PERFORM  9999-WRAP-UP THRU 9999-EXIT                             
              END-IF                                                            
            END-IF.                                                             
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-READ-BP13K800.                                                      
      ******************************************************************        
                                                                                
           MOVE  SPACES              TO     BP13K800-MASTER.                    
           INITIALIZE                       BP13K800-MASTER.                    
           MOVE  KD00-NUM-REGN       TO     K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
           IF K800-STATUS = 00 OR 02                                            
             PERFORM 3400-READ-BP27K800  THRU 3400-EXIT                         
           ELSE                                                                 
             IF K800-STATUS = 23                                                
                DISPLAY 'BP13K800-RECORD NOT FOUND: ' K800-NUM-REGN             
             ELSE                                                               
                DISPLAY 'BP13K800-READ ERROR. STATUS: '                         
                       K800-STATUS                                              
                MOVE   K800-STATUS   TO   RETURN-CODE                           
                PERFORM 9999-WRAP-UP        THRU 9999-EXIT                      
             END-IF                                                             
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3200-CHECK-DATE.                                                         
      ******************************************************************        
                                                                                
            IF KD00-DTE-OPTION     = SPACES OR LOW-VALUES                       
               MOVE '99990101'             TO  WS-FUTURE-DATE                   
            ELSE                                                                
               MOVE KD00-DTE-OPTION        TO  WS-FUTURE-DATE                   
            END-IF.                                                             
                                                                                
            ADD 49 TO WS-DUE-DD.                                                
                                                                                
            IF WS-DUE-MM = 1 OR 3 OR 5 OR 7 OR 8 OR 10 OR 12                    
               MOVE 31 TO WS-CURR-MM-DAYS                                       
            ELSE                                                                
               IF WS-DUE-MM = 4 OR 6 OR 9 OR 11                                 
                  MOVE 30 TO WS-CURR-MM-DAYS                                    
               ELSE                                                             
      *--->       CHECK IF LEAP YEAR                                            
                  DIVIDE WS-DUE-CCYY BY 4 GIVING WS-NUM-QUOTIENT                
                                        REMAINDER WS-NUM-REMAINDER              
                  IF WS-NUM-REMAINDER = ZEROES                                  
                     MOVE 29 TO WS-CURR-MM-DAYS                                 
                  ELSE                                                          
                     MOVE 28 TO WS-CURR-MM-DAYS                                 
                  END-IF                                                        
               END-IF                                                           
            END-IF.                                                             
                                                                                
            PERFORM 3250-GET-FUTURE-DATE THRU 3250-EXIT                         
                 UNTIL WS-DUE-DD < WS-CURR-MM-DAYS.                             
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3250-GET-FUTURE-DATE.                                                    
      ******************************************************************        
                                                                                
      * CHECK 7TH WEEK DATE                                                     
            IF WS-DUE-DD > WS-CURR-MM-DAYS                                      
               COMPUTE WS-DUE-DD = WS-DUE-DD - WS-CURR-MM-DAYS                  
               ADD 1 TO WS-DUE-MM                                               
               IF WS-DUE-MM > 12                                                
                  MOVE 01 TO WS-DUE-MM                                          
                  ADD 1 TO WS-DUE-CCYY                                          
               END-IF                                                           
            END-IF.                                                             
                                                                                
            IF WS-DUE-MM = 1 OR 3 OR 5 OR 7 OR 8 OR 10 OR 12                    
               MOVE 31 TO WS-CURR-MM-DAYS                                       
            ELSE                                                                
               IF WS-DUE-MM = 4 OR 6 OR 9 OR 11                                 
                  MOVE 30 TO WS-CURR-MM-DAYS                                    
               ELSE                                                             
      *--->       CHECK IF LEAP YEAR                                            
                  DIVIDE WS-DUE-CCYY BY 4 GIVING WS-NUM-QUOTIENT                
                                        REMAINDER WS-NUM-REMAINDER              
                  IF WS-NUM-REMAINDER = ZEROES                                  
                     MOVE 29 TO WS-CURR-MM-DAYS                                 
                  ELSE                                                          
                     MOVE 28 TO WS-CURR-MM-DAYS                                 
                  END-IF                                                        
               END-IF                                                           
            END-IF.                                                             
                                                                                
       3250-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3300-MOVE-DATA.                                                          
      ******************************************************************        
                                                                                
           INITIALIZE FOUT-DETAIL.                                              
           ADD 1         TO WS-SNO                                              
           ADD 1         TO WS-CNT-FOUT-WRITE                                   
           MOVE WS-SNO               TO FOUT-SNO                                
           MOVE K800-NUM-REGN        TO FOUT-NUM-REGN                           
           MOVE SPACES               TO FOUT-SOU-CSM                            
      *    MOVE SPACES               TO FOUT-LOF-OIC                            
           PERFORM 3600-GET-OIC THRU 3600-EXIT.                                 
           MOVE K800-NUM-HLA OF BP27K800-MASTER                                 
                                     TO FOUT-HLE-NUM                            
           PERFORM 3800-READ-K025 THRU 3800-EXIT                                
           MOVE K800-NUM-CASE-STATUS OF BP27K800-MASTER                         
                                     TO FOUT-HLE-STATUS                         
           STRING KD00-DTE-OPTION(7:2) '/' KD00-DTE-OPTION(5:2) '/'             
                  KD00-DTE-OPTION(1:4)                                          
                  DELIMITED BY SIZE  INTO  FOUT-DTE-BOOKING.                    
                                                                                
           MOVE WS-DATE1              TO WS-CURR-DATE-NUM.                      
           MOVE KD00-DTE-OPTION      TO WS-ACCEPT-DT-NUM.                       
           COMPUTE WS-INT-CURRENT-DATE                                          
                      = FUNCTION INTEGER-OF-DATE(WS-CURR-DATE-NUM)              
                                                                                
           COMPUTE WS-INT-DTE-ACCEPTANCE                                        
                      = FUNCTION INTEGER-OF-DATE(WS-ACCEPT-DT-NUM)              
                                                                                
           COMPUTE WS-NUM-DAYS     = WS-INT-CURRENT-DATE -                      
                                     WS-INT-DTE-ACCEPTANCE                      
           MOVE WS-NUM-DAYS              TO FOUT-NUM-DAYS.                      
           WRITE BP13FOUT-REC FROM FOUT-DETAIL.                                 
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3400-READ-BP27K800.                                                      
      ******************************************************************        
                                                                                
           MOVE  SPACES              TO     BP27K800-MASTER.                    
           INITIALIZE                       BP27K800-MASTER.                    
           MOVE K800-NUM-HLA OF BP13K800-MASTER                                 
                  TO K800-NUM-HLA OF BP27K800-MASTER                            
                                                                                
           READ BP27K800.                                                       
           IF BP27K800-STATUS = 00 OR 02                                        
             CONTINUE                                                           
           ELSE                                                                 
             IF BP27K800-STATUS = 23                                            
                DISPLAY 'BP27K800-RECORD NOT FOUND: ' K800-NUM-REGN             
             ELSE                                                               
                DISPLAY 'BP27K800-READ ERROR. STATUS: '                         
                   BP27K800-STATUS                                              
                MOVE  BP27K800-STATUS   TO   RETURN-CODE                        
                PERFORM 9999-WRAP-UP        THRU 9999-EXIT                      
             END-IF                                                             
           END-IF.                                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
      ******************************************************************        
       3500-WRITE-HEADER.                                                       
      ******************************************************************        
                                                                                
            MOVE SPACES TO BP13FOUT-REC.                                        
            STRING 'MONITORING REPORT ON OUTSTANDING DBSS LOF CASES'            
                   ' 7 WEEKS FROM DATE OF BOOKING'                              
                   DELIMITED BY SIZE  INTO  FOUT-TITLE.                         
            WRITE BP13FOUT-REC FROM FOUT-HEADER.                                
                                                                                
            MOVE SPACES TO BP13FOUT-REC.                                        
            STRING WS-DATE1-DD   '/' WS-DATE1-MM '/' WS-DATE1-CCYY              
                   DELIMITED BY SIZE  INTO  FOUT-DATE.                          
            WRITE BP13FOUT-REC FROM FOUT-HEADER1.                               
                                                                                
            MOVE SPACES TO BP13FOUT-REC.                                        
            MOVE KD00-NUM-PROJECT TO FOUT-PROJECT-CODE.                         
            WRITE BP13FOUT-REC FROM FOUT-HEADER2.                               
                                                                                
            MOVE SPACES TO BP13FOUT-REC.                                        
            WRITE BP13FOUT-REC FROM FOUT-HEADER3.                               
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3600-GET-OIC.                                                            
      ******************************************************************        
                                                                                
            MOVE KD00-NUM-REGN       TO K022-NUM-REGN.                          
            READ BP13K022 KEY IS K022-NUM-REGN.                                 
            IF K022-STATUS = '00'                                               
                 PERFORM 3700-READ-K024 THRU 3700-EXIT                          
            ELSE                                                                
                 DISPLAY 'OIC NOT FOUND: ' KD00-NUM-REGN                        
            END-IF.                                                             
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
      ******************************************************************        
       3700-READ-K024.                                                          
      ******************************************************************        
                                                                                
            IF K022-NUM-OIC-APPL = SPACES OR LOW-VALUES                         
               MOVE K022-NUM-OIC        TO K024-KEY-FLD                         
            ELSE                                                                
               MOVE K022-NUM-OIC-APPL   TO K024-KEY-FLD                         
            END-IF.                                                             
                                                                                
            READ BP13K024.                                                      
            IF K024-STATUS = 00 OR 02                                           
              MOVE K024-NME-OIC         TO FOUT-SOU-CSM                         
      *       MOVE K024-NME-OIC         TO FOUT-LOF-OIC                         
            ELSE                                                                
              IF K024-STATUS = 23                                               
                 DISPLAY 'K024-RECORD NOT FOUND: ' K022-NUM-OIC                 
              ELSE                                                              
                 DISPLAY 'K024-READ ERROR. STATUS: '                            
                    K024-STATUS                                                 
                 MOVE  K024-STATUS   TO   RETURN-CODE                           
                 PERFORM 9999-WRAP-UP        THRU 9999-EXIT                     
              END-IF                                                            
            END-IF.                                                             
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
      ******************************************************************        
       3800-READ-K025.                                                          
      ******************************************************************        
                                                                                
            MOVE K800-NUM-HLA OF BP13K800-MASTER                                
                                     TO K025-NUM-HLA.                           
            READ BP27K025 KEY IS K025-NUM-HLA.                                  
            IF K025-STATUS = '00'                                               
                 MOVE K025-NUM-OIC       TO FOUT-HLE-OIC                        
            ELSE                                                                
                 DISPLAY 'OIC NOT FOUND: ' KD00-NUM-REGN                        
                 MOVE SPACES             TO FOUT-HLE-OIC                        
            END-IF.                                                             
                                                                                
       3800-EXIT.                                                               
           EXIT.                                                                
      ******************************************************************        
       9999-WRAP-UP.                                                            
      ******************************************************************        
           DISPLAY  SPACES.                                                     
           DISPLAY '******* CONTROL   COUNTS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13C10D'                                      
           DISPLAY  SPACES.                                                     
           DISPLAY 'NO. OF BP13KD00 RECS READ    : ' WS-CNT-KD00-RD.            
           DISPLAY 'NO. OF BP13FOUT RECS WRITTEN : ' WS-CNT-FOUT-WRITE.         
                                                                                
           CLOSE    BP13KD00                                                    
                    BP13FOUT                                                    
                    BP27K800                                                    
                    BP27K025                                                    
                    BP13K022                                                    
                    BP13K024                                                    
                    BP13K800.                                                   
                                                                                
           IF  K800-STATUS NOT = '00'                                           
               DISPLAY 'ERROR CLOSE BP13K800 : ' K800-STATUS                    
               MOVE K800-STATUS TO RETURN-CODE                                  
           END-IF.                                                              
                                                                                
           IF  KD00-STATUS NOT = '00'                                           
               DISPLAY 'ERROR CLOSE BP13KD00 : ' KD00-STATUS                    
               MOVE KD00-STATUS TO RETURN-CODE                                  
           END-IF.                                                              
                                                                                
           IF  BP27K800-STATUS NOT = '00'                                       
               DISPLAY 'ERROR CLOSE BP27K800 : ' BP27K800-STATUS                
               MOVE BP27K800-STATUS TO RETURN-CODE                              
           END-IF.                                                              
                                                                                
           IF  K022-STATUS NOT = '00'                                           
               DISPLAY 'ERROR CLOSE BP13K022 : ' K022-STATUS                    
               MOVE K022-STATUS TO RETURN-CODE                                  
           END-IF.                                                              
                                                                                
           IF  K025-STATUS NOT = '00'                                           
               DISPLAY 'ERROR CLOSE BP27K025 : ' K025-STATUS                    
               MOVE K025-STATUS TO RETURN-CODE                                  
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
