      *-------------------------*                                               
       IDENTIFICATION DIVISION.                                                 
      *-------------------------*                                               
       PROGRAM-ID.    BP13C912.                                                 
      *AUTHOR.        JIJO FRANCIS.                                             
      *DATE-WRITTEN.  04/08/2003.                                               
      *===============================================================*         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      * OBJECTIVE :                                                   *         
      *  1. PRINT REPORT ON FLATLIST FOR GRO BY WARD/NT/FLAT-TYPE     *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * CHG REF  BY    DATE    DESCRIPTION                            *         
      * -------- ---  -------- -----------                            *         
      * BP132360 JF4  04/08/03 REPORT ON FLAT LIST FOR GRO.           *         
      * BP132456 JF4  04/12/03 INCLUDE NEIGH AND CNTRCT IN GROUPING.  *         
      * BP132736 CKK  28/08/05 INCLUDE BLOCK                          *         
      * BP134076 ZDD1 11/11/10 INCLUDE OUTPUT ON BP13FQ50             *         
      * BP134094 ZDD1 31/12/10 IINCLUDE NUM-WARD FOR BP13FQ50         *         
      * BP134123 ZDD1 31/12/10 FORMAT FQ50-NUM-MAIN                   *         
      * BP134367 ZDD1 16/09/11 INCLUDE FQ50-NUM-MODL-DESG             *         
      * BP134558 SMR2 28/05/12 NEW FILE BP13FNMR FOR UNSELECTED UNITS *         
      * BP134558 IL5  14/06/12 NEW FILE BP13FSLT FOR SELECTED UNITS   *         
      * BP137237 JF7  03/04/18 COMBINE 2A AND 2B FLAT TYPES           *         
      *===============================================================*         
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F110  ASSIGN       TO BP13F110.                           
           SELECT BP13F100  ASSIGN       TO BP13F100.                           
           SELECT BP13L912  ASSIGN       TO BP13L912.                           
           SELECT BP13FQ50  ASSIGN       TO BP13FQ50.                           
           SELECT BP13FNMR  ASSIGN       TO BP13FNMR.                           
           SELECT BP13FSLT  ASSIGN       TO BP13FSLT.                           
                                                                                
      *---------------*                                                         
       DATA DIVISION.                                                           
      *---------------*                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F110                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 80  CHARACTERS.                                      
       01  BP13F110-REC.                                                        
           05  F110-KEY-FLD.                                                    
               10  F110-NUM-WARD        PIC X(03).                              
               10  F110-NUM-ESTATE      PIC X(03).                              
               10  F110-NUM-TYP-ACTUSE  PIC X(02).                              
               10  F110-NUM-NEIGH       PIC X(04).                              
               10  F110-NUM-CNTRCT      PIC X(04).                              
               10  F110-NUM-BLOCK       PIC X(05).                              
           05  F110-NUM-UNIT            PIC 9(02).                              
           05  FILLER                   PIC X(57).                              
                                                                                
       FD  BP13F100                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 800 CHARACTERS.                                      
       COPY BP13F100.                                                           
                                                                                
       FD  BP13L912                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS.                                      
       01 BP13L912-REC            PIC X(132).                                   
                                                                                
       FD  BP13FQ50                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13FQ50.                                                           
                                                                                
       FD  BP13FNMR                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 800 CHARACTERS.                                      
       01 BP13FNMR-REC            PIC X(800).                                   
                                                                                
       FD  BP13FSLT                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 800 CHARACTERS.                                      
       01 BP13FSLT-REC            PIC X(800).                                   
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
      *------------------------------*                                          
      *   WORKING STORAGE VARIABLES  *                                          
      *------------------------------*                                          
       01 WS-F110-EOF             PIC X(01) VALUE 'N'.                          
          88 F110-END-OF-FILE   VALUE 'Y'.                                      
       01 WS-F100-EOF             PIC X(01) VALUE 'N'.                          
          88 F100-END-OF-FILE   VALUE 'Y'.                                      
       01 WS-F110-READ-CNT        PIC 9(08) VALUE ZEROES.                       
       01 WS-F100-READ-CNT        PIC 9(08) VALUE ZEROES.                       
       01 WS-L912-WRITE-CNT       PIC 9(08) VALUE ZEROES.                       
       01 WS-FNMR-WRITE-CNT       PIC 9(08) VALUE ZEROES.                       
       01 WS-FSLT-WRITE-CNT       PIC 9(08) VALUE ZEROES.                       
                                                                                
       01 WS-EDT-DATE             PIC X(10) VALUE SPACES.                       
       01 WS-CUR-DATE             PIC X(08) VALUE SPACES.                       
                                                                                
       01 WS-EDIT1                PIC Z(07)9.                                   
                                                                                
       01 WS-LINE-CNT             PIC 9(02) VALUE ZEROES.                       
       01 WS-PAGE-CNT             PIC 9(06) VALUE ZEROES.                       
       01 WS-PRINT-UNIT-CNT       PIC 9(03) VALUE ZEROES.                       
       01 WS-MAX-LINES            PIC 9(02) VALUE ZEROES.                       
       01 WS-SLNO                 PIC 9(05) VALUE ZEROES.                       
       01 WS-PREV-F110-KEY-FLD    PIC X(21) VALUE SPACES.                       
       01 WS-FQ50-WRT             PIC 9(06) VALUE ZEROES.                       
                                                                                
       01 WS-F100-KEY-FLD.                                                      
          05 WS-F100-WARD         PIC X(03).                                    
          05 WS-F100-NT           PIC X(03).                                    
          05 WS-F100-FT           PIC X(02).                                    
          05 WS-F100-NEIGH        PIC X(04).                                    
          05 WS-F100-CNTRCT       PIC X(04).                                    
          05 WS-F100-BLOCK        PIC X(05).                                    
                                                                                
       01 WS-F110-KEY-FLD.                                                      
          05 WS-F110-WARD         PIC X(03).                                    
          05 WS-F110-NT           PIC X(03).                                    
          05 WS-F110-FT           PIC X(02).                                    
          05 WS-F110-NEIGH        PIC X(04).                                    
          05 WS-F110-CNTRCT       PIC X(04).                                    
          05 WS-F110-BLOCK        PIC X(05).                                    
                                                                                
       01 WS-REPORT-VARS.                                                       
          05 L912-HEADER1.                                                      
             10 FILLER            PIC X(08) VALUE 'BP13L912'.                   
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(08) VALUE 'HDBCAT 3'.                   
             10 FILLER            PIC X(29) VALUE SPACES.                       
             10 FILLER            PIC X(20) VALUE                               
                'SYSTEM OF COMMITMENT'.                                         
             10 FILLER            PIC X(23) VALUE SPACES.                       
             10 FILLER            PIC X(07) VALUE 'DATE : '.                    
             10 L912-DATE         PIC X(12) VALUE SPACES.                       
             10 FILLER            PIC X(04) VALUE SPACES.                       
             10 FILLER            PIC X(07) VALUE 'PAGE : '.                    
             10 L912-PAGE         PIC Z(05)9.                                   
          05 L912-HEADER2.                                                      
             10 FILLER            PIC X(45) VALUE SPACES.                       
             10 FILLER            PIC X(30) VALUE                               
                'FLAT LIST BY WARD/NT/FLAT TYPE'.                               
             10 FILLER            PIC X(47) VALUE SPACES.                       
          05 L912-HEADER3.                                                      
             10 FILLER            PIC X(12) VALUE 'WARD      : '.               
             10 L912-WARD         PIC X(03) VALUE SPACES.                       
          05 L912-HEADER4.                                                      
             10 FILLER            PIC X(12) VALUE 'NEW TOWN  : '.               
             10 L912-NT           PIC X(03) VALUE SPACES.                       
          05 L912-HEADER5.                                                      
             10 FILLER            PIC X(12) VALUE 'FLAT TYPE : '.               
             10 L912-FLAT-TYPE    PIC X(02) VALUE SPACES.                       
          05 L912-HEADER5A.                                                     
             10 FILLER            PIC X(12) VALUE 'NEIGH     : '.               
             10 L912-NGH          PIC X(04) VALUE SPACES.                       
          05 L912-HEADER5B.                                                     
             10 FILLER            PIC X(12) VALUE 'CONTRACT  : '.               
             10 L912-CNRT         PIC X(04) VALUE SPACES.                       
          05 L912-HEADER5C.                                                     
             10 FILLER            PIC X(12) VALUE 'UNITS     : '.               
             10 L912-UNITS        PIC Z9.                                       
          05 L912-HEADER6.                                                      
             10 FILLER            PIC X(33) VALUE                               
                ' S/NO    NGH   CONT  BLOCK   UNIT'.                            
             10 FILLER            PIC X(15) VALUE                               
                '         STREET'.                                              
             10 FILLER            PIC X(28) VALUE SPACES.                       
             10 FILLER            PIC X(27) VALUE                               
                'SCHEME A/C    MODL   RANDOM'.                                  
             10 FILLER            PIC X(25) VALUE SPACES.                       
          05 L912-HEADER7.                                                      
             10 FILLER            PIC X(132) VALUE ALL '-'.                     
          05 L912-DETAILS.                                                      
             10 L912-SLNO         PIC Z(4)9.                                    
             10 FILLER            PIC X(04) VALUE SPACES.                       
             10 L912-NEIGH        PIC X(04) VALUE SPACES.                       
             10 FILLER            PIC X(02) VALUE SPACES.                       
             10 L912-CNTRCT       PIC X(04) VALUE SPACES.                       
             10 FILLER            PIC X(02) VALUE SPACES.                       
             10 L912-BLOCK        PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 L912-UNIT         PIC X(10) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 L912-STREET       PIC X(32) VALUE SPACES.                       
             10 FILLER            PIC X(02) VALUE SPACES.                       
             10 L912-SCHACC       PIC X(11) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 L912-MODL         PIC X(02) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 L912-RANDOM       PIC X(28) VALUE SPACES.                       
                                                                                
       01 WS-RIGHT-JUST.                                                        
          05 WS-UNIT                PIC X(04)    VALUE SPACES.                  
          05 WS-UNIT1               PIC X(04)    VALUE SPACES.                  
          05 WS-SUB                 PIC 9(01)    VALUE ZEROES.                  
          05 WS-SUB1                PIC 9(01)    VALUE ZEROES.                  
          05 WS-CHECK               PIC 9(01)    VALUE ZEROES.                  
          05 WS-CNT                 PIC 9(01)    VALUE ZEROES.                  
                                                                                
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F110      THRU 2000-EXIT.                      
           PERFORM 3000-READ-BP13F100      THRU 3000-EXIT.                      
           PERFORM 4000-PROCESS-DATA       THRU 4000-EXIT                       
                   UNTIL WS-F110-KEY-FLD = HIGH-VALUES AND                      
                         WS-F100-KEY-FLD = HIGH-VALUES.                         
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN  INPUT  BP13F110                                                
                        BP13F100                                                
                 OUTPUT BP13L912                                                
                        BP13FQ50                                                
                        BP13FNMR                                                
                        BP13FSLT.                                               
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
           STRING WS-CUR-DATE(7:2), '/',                                        
                  WS-CUR-DATE(5:2), '/',                                        
                  WS-CUR-DATE(1:4)  DELIMITED BY SIZE                           
                  INTO WS-EDT-DATE.                                             
                                                                                
           MOVE  '55'         TO WS-MAX-LINES.                                  
           MOVE  '60'         TO WS-LINE-CNT.                                   
           MOVE WS-EDT-DATE   TO L912-DATE.                                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F110.                                                      
      *---------------------------------------------------------------*         
           READ BP13F110 AT END                                                 
                          MOVE HIGH-VALUES TO WS-F110-KEY-FLD                   
                          MOVE 'Y' TO WS-F110-EOF                               
                          GO TO 2000-EXIT.                                      
                                                                                
           MOVE F110-NUM-WARD       TO WS-F110-WARD.                            
           MOVE F110-NUM-ESTATE     TO WS-F110-NT.                              
           MOVE F110-NUM-TYP-ACTUSE TO WS-F110-FT.                              
           MOVE F110-NUM-BLOCK      TO WS-F110-BLOCK.                           
                                                                                
           IF F110-NUM-NEIGH = SPACES OR LOW-VALUES                             
              MOVE    SPACES        TO WS-F110-NEIGH                            
           ELSE                                                                 
              MOVE F110-NUM-NEIGH   TO WS-F110-NEIGH                            
           END-IF.                                                              
                                                                                
           IF F110-NUM-CNTRCT = SPACES OR LOW-VALUES                            
              MOVE    SPACES        TO WS-F110-CNTRCT                           
           ELSE                                                                 
              MOVE F110-NUM-CNTRCT  TO WS-F110-CNTRCT                           
           END-IF.                                                              
                                                                                
           ADD    1    TO WS-F110-READ-CNT.                                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-READ-BP13F100.                                                      
      *---------------------------------------------------------------*         
           READ BP13F100 AT END                                                 
                          MOVE HIGH-VALUES TO WS-F100-KEY-FLD                   
                          MOVE 'Y' TO WS-F100-EOF                               
                          GO TO 3000-EXIT.                                      
                                                                                
           MOVE F100-NUM-WARD       TO WS-F100-WARD.                            
           MOVE F100-NUM-NT         TO WS-F100-NT.                              
           MOVE F100-NUM-TYP-ACTUSE TO WS-F100-FT.                              
           MOVE F100-NUM-BLOCK      TO WS-F100-BLOCK.                           
                                                                                
           IF F100-NUM-NGHBRD = SPACES OR LOW-VALUES                            
              MOVE    SPACES        TO WS-F100-NEIGH                            
           ELSE                                                                 
              MOVE F100-NUM-NGHBRD  TO WS-F100-NEIGH                            
           END-IF.                                                              
                                                                                
           IF F100-NUM-CNTRCT = SPACES OR LOW-VALUES                            
              MOVE    SPACES        TO WS-F100-CNTRCT                           
           ELSE                                                                 
              MOVE F100-NUM-CNTRCT  TO WS-F100-CNTRCT                           
           END-IF.                                                              
                                                                                
           ADD    1    TO WS-F100-READ-CNT.                                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-PROCESS-DATA.                                                       
      *---------------------------------------------------------------*         
           DISPLAY '110 : ' WS-F110-KEY-FLD ' 100 : ' WS-F100-KEY-FLD.          
           EVALUATE TRUE                                                        
             WHEN WS-F110-KEY-FLD  =  WS-F100-KEY-FLD                           
               MOVE  ZEROES   TO WS-PRINT-UNIT-CNT                              
               PERFORM 4100-PROCESS-F100   THRU 4100-EXIT                       
                   UNTIL (WS-F100-KEY-FLD  NOT =  WS-F110-KEY-FLD)  OR          
                           (F100-END-OF-FILE)                                   
               PERFORM 2000-READ-BP13F110  THRU 2000-EXIT                       
             WHEN WS-F110-KEY-FLD  <  WS-F100-KEY-FLD                           
               PERFORM 2000-READ-BP13F110  THRU 2000-EXIT                       
             WHEN WS-F110-KEY-FLD  >  WS-F100-KEY-FLD                           
      **       WRITE BP13FNMR-REC          FROM BP13F100-REC                    
               MOVE  BP13F100-REC            TO BP13FNMR-REC                    
               MOVE  BP13F100-REC(134:2)     TO BP13FNMR-REC(72:2)              
               MOVE  SPACES                  TO BP13FNMR-REC(134:2)             
               WRITE BP13FNMR-REC                                               
               ADD 1                       TO WS-FNMR-WRITE-CNT                 
               PERFORM 3000-READ-BP13F100  THRU 3000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4100-PROCESS-F100.                                                       
      *---------------------------------------------------------------*         
           IF WS-PRINT-UNIT-CNT < F110-NUM-UNIT                                 
              ADD 1     TO WS-PRINT-UNIT-CNT                                    
              PERFORM 5000-PRINT-REPORT THRU 5000-EXIT                          
           ELSE                                                                 
      **      WRITE BP13FNMR-REC        FROM BP13F100-REC                       
              MOVE  BP13F100-REC          TO BP13FNMR-REC                       
              MOVE  BP13F100-REC(134:2)   TO BP13FNMR-REC(72:2)                 
              MOVE  SPACES                TO BP13FNMR-REC(134:2)                
              WRITE BP13FNMR-REC                                                
              ADD 1                     TO WS-FNMR-WRITE-CNT                    
           END-IF.                                                              
           PERFORM 3000-READ-BP13F100   THRU 3000-EXIT.                         
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5000-PRINT-REPORT.                                                       
      *---------------------------------------------------------------*         
           IF (WS-LINE-CNT  >  WS-MAX-LINES)   OR                               
              (WS-F110-KEY-FLD  NOT =  WS-PREV-F110-KEY-FLD)                    
              MOVE  SPACES      TO BP13L912-REC                                 
              IF (WS-F110-KEY-FLD  NOT =  WS-PREV-F110-KEY-FLD)                 
                 MOVE WS-F110-KEY-FLD      TO WS-PREV-F110-KEY-FLD              
                 MOVE  ZEROES              TO WS-SLNO                           
                                              WS-PAGE-CNT                       
              END-IF                                                            
              ADD      1        TO WS-PAGE-CNT                                  
              MOVE     0        TO WS-LINE-CNT                                  
              MOVE WS-PAGE-CNT  TO L912-PAGE                                    
              MOVE F110-NUM-WARD        TO L912-WARD                            
              MOVE F110-NUM-ESTATE      TO L912-NT                              
              MOVE F110-NUM-TYP-ACTUSE  TO L912-FLAT-TYPE                       
              MOVE F110-NUM-NEIGH       TO L912-NGH                             
              MOVE F110-NUM-CNTRCT      TO L912-CNRT                            
              MOVE F110-NUM-UNIT        TO L912-UNITS                           
              WRITE BP13L912-REC     FROM L912-HEADER1 AFTER PAGE               
              WRITE BP13L912-REC     FROM L912-HEADER2                          
              WRITE BP13L912-REC     FROM L912-HEADER3                          
              WRITE BP13L912-REC     FROM L912-HEADER4                          
              WRITE BP13L912-REC     FROM L912-HEADER5                          
              WRITE BP13L912-REC     FROM L912-HEADER5A                         
              WRITE BP13L912-REC     FROM L912-HEADER5B                         
              WRITE BP13L912-REC     FROM L912-HEADER5C                         
              WRITE BP13L912-REC     FROM L912-HEADER6 AFTER 2                  
              WRITE BP13L912-REC     FROM L912-HEADER7                          
              ADD     10        TO WS-LINE-CNT                                  
           END-IF.                                                              
                                                                                
           MOVE   SPACES        TO L912-DETAILS                                 
                                   BP13FSLT-REC                                 
                                   BP13FQ50-REC.                                
           INITIALIZE   L912-DETAILS                                            
                        BP13FSLT-REC                                            
                        BP13FQ50-REC.                                           
           ADD    1             TO WS-SLNO.                                     
           MOVE  WS-SLNO        TO L912-SLNO.                                   
           MOVE F100-NUM-NT            TO L912-NT.                              
           MOVE F100-NUM-NGHBRD        TO L912-NEIGH                            
                                          FQ50-NUM-NGHBRD.                      
           MOVE F100-NUM-CNTRCT        TO L912-CNTRCT                           
                                          FQ50-NUM-CNTRCT.                      
           MOVE F100-NUM-BLOCK         TO L912-BLOCK                            
                                          FQ50-NUM-BLOCK.                       
           MOVE F100-NME-STREET        TO L912-STREET.                          
           MOVE F100-NUM-HDB-REF       TO L912-SCHACC.                          
           MOVE F100-NUM-MODL-DESG     TO L912-MODL.                            
           MOVE F100-NUM-RANDOM        TO L912-RANDOM.                          
           MOVE F100-NUM-ESTATE        TO FQ50-NUM-ESTATE.                      
           MOVE F100-NUM-LEVEL         TO FQ50-NUM-LEVEL.                       
      *    MOVE F100-NUM-MAIN          TO FQ50-NUM-MAIN.                        
           MOVE F100-NUM-MAIN          TO WS-UNIT.                              
           PERFORM 5001-GET-MAIN-UNIT   THRU 5001-EXIT.                         
           MOVE WS-UNIT1               TO FQ50-NUM-MAIN.                        
           MOVE F100-NUM-SUB           TO FQ50-NUM-SUB.                         
      **   MOVE F100-NUM-TYP-ACTUSE    TO FQ50-NUM-TYP-ACTUSE.                  
           MOVE BP13F100-REC(134:2)    TO FQ50-NUM-TYP-ACTUSE.                  
           MOVE F100-NUM-ALLO-CAT      TO FQ50-NUM-ALLO-CAT.                    
           MOVE F100-DTE-BALLOT        TO FQ50-DTE-BALLOT.                      
           MOVE F100-NUM-WARD          TO FQ50-NUM-WARD.                        
           MOVE F100-NUM-NT            TO FQ50-NUM-NT.                          
           MOVE F100-NUM-MODL-DESG     TO FQ50-NUM-MODL-DESG.                   
           IF F100-NUM-SUB  =  SPACES OR LOW-VALUES                             
              STRING F100-NUM-LEVEL  '-'                                        
                     F100-NUM-MAIN                                              
                     DELIMITED BY SIZE  INTO L912-UNIT                          
           ELSE                                                                 
              STRING F100-NUM-LEVEL  '-'                                        
                     F100-NUM-MAIN   '-'                                        
                     F100-NUM-SUB                                               
                     DELIMITED BY SIZE  INTO L912-UNIT                          
           END-IF.                                                              
                                                                                
           MOVE     SPACES             TO BP13L912-REC.                         
           WRITE BP13L912-REC        FROM L912-DETAILS.                         
           WRITE BP13FQ50-REC.                                                  
      **   WRITE BP13FSLT-REC          FROM BP13F100-REC                        
           MOVE  BP13F100-REC            TO BP13FSLT-REC                        
           MOVE  BP13F100-REC(134:2)     TO BP13FSLT-REC(72:2)                  
           MOVE  SPACES                  TO BP13FSLT-REC(134:2)                 
           WRITE BP13FSLT-REC.                                                  
           ADD    1             TO WS-LINE-CNT.                                 
           ADD    1             TO WS-FQ50-WRT.                                 
           ADD    1             TO WS-L912-WRITE-CNT.                           
           ADD    1             TO WS-FSLT-WRITE-CNT.                           
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       5001-GET-MAIN-UNIT.                                                      
      *---------------------------------------------------------------*         
                                                                                
           MOVE SPACES                     TO WS-UNIT1.                         
           MOVE ZEROES                     TO WS-SUB1                           
                                              WS-CHECK.                         
                                                                                
           PERFORM VARYING WS-SUB FROM 1 BY 1  UNTIL WS-SUB > 4                 
             IF WS-UNIT(WS-SUB:1) = SPACES OR LOW-VALUES                        
                ADD 1                      TO WS-SUB1                           
             END-IF                                                             
           END-PERFORM.                                                         
                                                                                
           IF WS-SUB1 > 0                                                       
              MOVE SPACES  TO WS-UNIT1(1:WS-SUB1)                               
              COMPUTE WS-CNT = 4 - WS-SUB1                                      
                                                                                
              PERFORM VARYING WS-CHECK FROM 1 BY 1 UNTIL WS-CNT = 0             
                 IF WS-UNIT(WS-CHECK:1) NOT = SPACES AND LOW-VALUES             
                    SUBTRACT  1 FROM WS-CNT                                     
                    ADD 1 TO WS-SUB1                                            
                    MOVE WS-UNIT(WS-CHECK:1) TO WS-UNIT1(WS-SUB1:1)             
                 END-IF                                                         
              END-PERFORM                                                       
           ELSE                                                                 
              MOVE WS-UNIT                 TO WS-UNIT1                          
           END-IF.                                                              
                                                                                
                                                                                
       5001-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13C912       ' WS-EDT-DATE.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-F110-READ-CNT    TO WS-EDIT1.                                
           DISPLAY 'NO OF RECS READ FROM BP13F110  : ' WS-EDIT1.                
           MOVE WS-F100-READ-CNT    TO WS-EDIT1.                                
           DISPLAY 'NO OF RECS READ FROM BP13F100  : ' WS-EDIT1.                
           MOVE WS-L912-WRITE-CNT  TO WS-EDIT1.                                 
           DISPLAY 'NO OF RECS WRITTEN TO BP13L912 : ' WS-EDIT1.                
           MOVE WS-FQ50-WRT        TO WS-EDIT1.                                 
           DISPLAY 'NO OF RECS WRITTEN TO BP13FQ50 : ' WS-EDIT1.                
           MOVE WS-FNMR-WRITE-CNT  TO WS-EDIT1.                                 
           DISPLAY 'NO OF RECS WRITTEN TO BP13FNMR : ' WS-EDIT1.                
           MOVE WS-FSLT-WRITE-CNT  TO WS-EDIT1.                                 
           DISPLAY 'NO OF RECS WRITTEN TO BP13FSTR : ' WS-EDIT1.                
           DISPLAY '*---------------------------------------------*'.           
                                                                                
           CLOSE   BP13F110                                                     
                   BP13F100                                                     
                   BP13L912                                                     
                   BP13FQ50                                                     
                   BP13FNMR                                                     
                   BP13FSLT.                                                    
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
