       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CZ2D.                                                 
      *AUTHOR.        IRVIN CHING.                                              
      *DATE-WRITTEN.  17/07/2013.                                               
      *========================================================*                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1. PROGRAM TO RETRIEVE NRIC NUMBERS FOR BP13F00K  *                
      *         FOR JOB BP13AY0K                               *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO    BY  DATE    DESCRIPTION                      *                
      * -------  ---  ------  -----------                      *                
      * BP134980 IMC1 170713  NEW PROGRAM                      *                
      * BP134991 IMC1 250713  RETRIEVE DTE-BALLOT FIELD        *                
      * BP134991 IMC1 260813  ADD BP13K893                     *                
      * BP135026 IMC1 280713  TAKE SMALLER NRIC                *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F00K ASSIGN TO BP13F00K.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-BP13K800-STATUS.                        
                                                                                
           SELECT BP13K893 ASSIGN TO BP13K893                                   
                     ORGANIZATION IS INDEXED                                    
                      ACCESS MODE IS DYNAMIC                                    
                       RECORD KEY IS K893-KEY-FLD                               
                      FILE STATUS IS WS-BP13K893-STATUS.                        
                                                                                
           SELECT BP13FOUT ASSIGN TO BP13FOUT.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F00K                                                            
            RECORD CONTAINS 50 CHARACTERS                                       
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
       COPY BP13F00K.                                                           
                                                                                
       FD   BP13K800.                                                           
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD   BP13FOUT                                                            
            RECORD CONTAINS 50 CHARACTERS                                       
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
       01   BP13FOUT-REC            PIC X(50).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-STATUS.                                                      
           05  WS-BP13K800-STATUS   PIC 9(2)    VALUE ZEROES.                   
           05  WS-BP13K893-STATUS   PIC 9(2)    VALUE ZEROES.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F00K-READ          PIC 9(8)    VALUE ZEROES.                   
           05 WS-CNT                PIC 9(8)    VALUE ZEROES.                   
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-K800-FND          PIC X(1)     VALUE SPACES.                  
           05  WS-F00K-EOF          PIC X(1)     VALUE SPACES.                  
                                                                                
       01  WS-WORKING-FIELDS.                                                   
           05  WS-NUM-REGN          PIC X(8)     VALUE SPACES.                  
                                                                                
       PROCEDURE DIVISION.                                                      
      *------------------------------------------------------------             
       0000-MAIN.                                                               
      *------------------------------------------------------------             
                                                                                
           OPEN INPUT  BP13F00K                                                 
                       BP13K800                                                 
                       BP13K893                                                 
                OUTPUT BP13FOUT.                                                
                                                                                
           IF WS-BP13K800-STATUS NOT = ZEROS AND 97                             
              DISPLAY 'FILE BP13K800 OPEN ERROR' WS-BP13K800-STATUS             
              MOVE WS-BP13K800-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-BP13K893-STATUS NOT = ZEROS AND 97                             
              DISPLAY 'FILE BP13K893 OPEN ERROR' WS-BP13K893-STATUS             
              MOVE WS-BP13K893-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 1000-READ-F00K-REC THRU 1000-EXIT.                           
           PERFORM 2000-PROCESS-REC   THRU 2000-EXIT                            
                   UNTIL WS-F00K-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE.                                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       1000-READ-F00K-REC.                                                      
      *------------------------------------------------------------             
                                                                                
           READ BP13F00K AT END                                                 
                MOVE 'Y' TO WS-F00K-EOF                                         
                GO TO 1000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F00K-READ.                                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       2000-PROCESS-REC.                                                        
      *------------------------------------------------------------             
                                                                                
           PERFORM 3000-READ-BP13K800 THRU 3000-EXIT.                           
                                                                                
           WRITE BP13FOUT-REC         FROM BP13F00K-REC.                        
                                                                                
           PERFORM 1000-READ-F00K-REC THRU 1000-EXIT.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       3000-READ-BP13K800.                                                      
      *------------------------------------------------------------             
                                                                                
           MOVE F00K-NUM-REGN    TO K800-NUM-REGN                               
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-BP13K800-STATUS                                          
              WHEN 00                                                           
      *  MOVE THE SMALLER NRIC TO NRIC1 POSITION                                
                   IF (K800-NUM-NRIC1(2:7) < K800-NUM-NRIC2(2:7)) OR            
                      (K800-NUM-NRIC2 = SPACES OR LOW-VALUES)                   
                      MOVE K800-NUM-NRIC1      TO F00K-NUM-NRIC1                
                      MOVE K800-NUM-NRIC2      TO F00K-NUM-NRIC2                
                   ELSE                                                         
                      MOVE K800-NUM-NRIC2      TO F00K-NUM-NRIC1                
                      MOVE K800-NUM-NRIC1      TO F00K-NUM-NRIC2                
                   END-IF                                                       
                   MOVE K800-DTE-BALLOT     TO F00K-DTE-BALLOT                  
              WHEN 10                                                           
              WHEN 23                                                           
                   PERFORM 5000-START-BP13K893  THRU 5000-EXIT                  
              WHEN OTHER                                                        
                   DISPLAY 'READ BP13K800 ERROR ' WS-BP13K800-STATUS            
                   MOVE WS-BP13K800-STATUS        TO RETURN-CODE                
                   PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-START-BP13K893.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                           TO BP13K893-MASTER.            
           INITIALIZE                               BP13K893-MASTER.            
                                                                                
           MOVE SPACES                           TO K893-KEY-FLD.               
           MOVE K800-NUM-REGN                    TO K893-NUM-REGN-HIST.         
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           EVALUATE WS-BP13K893-STATUS                                          
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 5100-READ-BP13K893  THRU 5100-EXIT                  
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K893: '                         
                             WS-BP13K893-STATUS                                 
                            ' REGN = ' K893-NUM-REGN-HIST                       
                    PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5100-READ-BP13K893.                                                      
      ******************************************************************        
                                                                                
            READ  BP13K893 NEXT AT END                                          
                  MOVE SPACES               TO K893-KEY-FLD.                    
                                                                                
            EVALUATE WS-BP13K893-STATUS                                         
                WHEN 00                                                         
                WHEN 02                                                         
                     IF K800-NUM-REGN  = K893-NUM-REGN-HIST                     
                       IF (K893-NUM-NRIC1(2:7) < K893-NUM-NRIC2(2:7)) OR        
                          (K893-NUM-NRIC2 = SPACES OR LOW-VALUES)               
                          MOVE K893-NUM-NRIC1      TO F00K-NUM-NRIC1            
                          MOVE K893-NUM-NRIC2      TO F00K-NUM-NRIC2            
                       ELSE                                                     
                          MOVE K893-NUM-NRIC2      TO F00K-NUM-NRIC1            
                          MOVE K893-NUM-NRIC1      TO F00K-NUM-NRIC2            
                       END-IF                                                   
                       MOVE K893-DTE-BALLOT      TO F00K-DTE-BALLOT             
                     ELSE                                                       
                        DISPLAY K800-NUM-REGN ' NOT FOUND IN K893'              
                     END-IF                                                     
                WHEN 23                                                         
                     DISPLAY K800-NUM-REGN ' NOT FOUND IN K893'                 
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K893 : '                        
                              WS-BP13K893-STATUS                                
                             ' REGN = ' K893-NUM-REGN-HIST                      
                     PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                 
            END-EVALUATE.                                                       
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------------------------------------------------             
       9000-CLOSE-ROUTINE.                                                      
      *------------------------------------------------------------             
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'BP13CZ2D                        '.                          
           DISPLAY '*-----------------------------------------------*'.         
           DISPLAY 'EXCEPTIONAL REPORT BEFORE BALLOT COMMENCE'.                 
           DISPLAY '*-----------------------------------------------*'.         
           DISPLAY 'NO OF RECORDS READ FROM F00K    ' WS-F00K-READ.             
                                                                                
                                                                                
           CLOSE BP13F00K                                                       
                 BP13FOUT                                                       
                 BP13K800                                                       
                 BP13K893.                                                      
                                                                                
           IF  WS-BP13K800-STATUS NOT = ZEROS AND 97                            
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'WS-BP13K800-STATUS ' WS-BP13K800-STATUS.                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
