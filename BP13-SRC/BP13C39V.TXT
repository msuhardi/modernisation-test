      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C39V.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  NOVEMBER 22, 2018                                         
      *===============================================================*         
      * OBJECTIVE  :  PRINT PROPOSED REVAMP OF KEYS COLLECTION        *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F40I                                     *         
      *               2. BP13K800                                     *         
      *               3. BP13K410                                     *         
      *                                                               *         
      * OUTPUT FILE:  1. BP13L39V                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP137141 22/11/2018 PCL4 NEW PROGRAM.                         *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F40I  ASSIGN        TO BP13F40I.                         
                                                                                
           SELECT  BP13K893  ASSIGN        TO BP13K893                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K893-KEY-FLD                      
                             FILE STATUS   IS WS-K893-STATUS.                   
                                                                                
           SELECT  BP13K800  ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
                                                                                
           SELECT  BP13K410  ASSIGN        TO BP13K410                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K410-KEY-FLD                      
                             FILE STATUS   IS WS-K410-STATUS.                   
                                                                                
                                                                                
           SELECT  BP13L39V  ASSIGN        TO BP13L39V.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F40I                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 100 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F40I.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD   BP13K410                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K410.                                                           
                                                                                
       FD   BP13L39V                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   BP13L39V-REC               PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F40I             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K410             PIC X(1)  VALUE 'N'.                     
           05  WS-K893-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K410-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                    PIC 9(1)  VALUE ZEROES.                  
           05  WS-HA                   PIC 9(1)  VALUE ZEROES.                  
           05  WS-F40I-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO1                 PIC 9(7)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
           05  WS-CNT-L39V-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(1)  VALUE 2.                       
           05  WS-MAX-LINE             PIC 9(2)  VALUE 55.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-AMT-POD               PIC 9(6)     VALUE ZEROES.               
           05 WS-DEFER-PREMIUM         PIC S9(7)V99 VALUE ZEROES.               
           05 WS-DEFER-RATE            PIC 9(2)V99 VALUE ZEROES.                
           05 WS-PRIOR-DTE             PIC 9(08) VALUE ZEROES.                  
           05 WS-NUM-DTE-APPT          PIC 9(07) VALUE ZEROES.                  
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-F40I-NUM-REGN         PIC X(8)  VALUE SPACES.                  
           05 WS-NUM-NRIC              PIC X(9)  VALUE SPACES.                  
           05 WS-SNO-ZZZZ9             PIC ZZZZ9 VALUE ZEROES.                  
           05 WS-AMT-FORMAT            PIC S9(7)V99 VALUE ZEROES.               
           05 WS-FIRST-WRITE           PIC X(1)  VALUE 'Y'.                     
           05 WS-CNT-HA                PIC 9(2)  VALUE ZEROES.                  
                                                                                
       01  WS-DTE-REGN.                                                         
           05 WS-DTE-CCYY                   PIC 9(4).                           
           05 WS-DTE-MM                     PIC 9(2).                           
           05 WS-DTE-DD                     PIC 9(2).                           
                                                                                
       01  WS-FIX-VARS.                                                         
           05  WS-DOB-DATE.                                                     
               10  WS-DOB-YR                 PIC  9(04).                        
               10  WS-DOB-MMDD               PIC  9(04).                        
           05  WS-SYS-DATE2.                                                    
               10  WS-SYS-YR                 PIC  9(04).                        
               10  WS-SYS-MMDD               PIC  9(04).                        
           05  WS-AGE1                       PIC  9(04)  VALUE ZEROES.          
           05  WS-HA-DOB                     PIC  X(08).                        
           05  WS-HA-AGE                     PIC  9(04).                        
                                                                                
       01 WS-PRIO-DTE.                                                          
          05  WS-PRIO-CCYY                 PIC 9(04).                           
          05  WS-PRIO-MM                   PIC 9(02).                           
          05  WS-PRIO-DD                   PIC 9(02).                           
                                                                                
       01 WS-CURR-DATE.                                                         
          05  WS-CURR-CCYY                 PIC 9(04).                           
          05  WS-CURR-MM                   PIC 9(02).                           
          05  WS-CURR-DD                   PIC 9(02).                           
                                                                                
       01  WS-DEBAR-CODE               PIC X(02).                               
           88 VALID-YR-CODE          VALUE 'YR'.                                
           88 VALID-YS-CODE          VALUE 'YS'.                                
                                                                                
       01  L39V-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13L39V'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  L39V-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L39V-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  L39V-HEADER02.                                                       
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(35)     VALUE                          
               'PROPOSED REVAMP OF KEY COLLECTION '.                            
           05  L39V-DTE-HDR        PIC X(10)     VALUE SPACES.                  
                                                                                
       01  L39V-HEADER03.                                                       
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(05)    VALUE '  S/N'.                  
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(08)    VALUE 'REGN NO '.               
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(10)    VALUE 'APPT DATE'.              
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(10)    VALUE 'APPT TIME'.              
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(10)    VALUE 'APPT TYPE'.              
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(14)    VALUE 'KEY ISSUE DATE'.         
           05  FILLER              PIC X(01)    VALUE SPACES.                   
                                                                                
       01  L39V-DETAIL01.                                                       
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L39V-SN            PIC X(05)        VALUE SPACES.                
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L39V-REGN          PIC X(08)        VALUE SPACES.                
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L39V-APPT-DATE     PIC X(10)        VALUE SPACES.                
           05  FILLER             PIC X(03)        VALUE SPACES.                
           05  L39V-APPT-TIME     PIC X(04)        VALUE SPACES.                
           05  FILLER             PIC X(09)        VALUE SPACES.                
           05  L39V-LETTER        PIC X(02)        VALUE SPACES.                
           05  FILLER             PIC X(07)        VALUE SPACES.                
           05  L39V-DTE-ISSUE     PIC X(10)        VALUE SPACES.                
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F40I      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F40I = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F40I                                                 
                       BP13K800                                                 
                       BP13K893                                                 
                       BP13K410                                                 
              OUTPUT   BP13L39V.                                                
                                                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K410 - ERROR OPENING : ' WS-K410-STATUS              
              MOVE WS-K410-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K893 - ERROR OPENING : ' WS-K893-STATUS              
              MOVE WS-K893-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 7000-PRINT-HEADER       THRU 7000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F40I.                                                      
      ******************************************************************        
                                                                                
           READ BP13F40I           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F40I                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F40I-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       2100-READ-BP13K800.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP13K800-MASTER.                    
           INITIALIZE                       BP13K800-MASTER.                    
                                                                                
           MOVE F40I-NUM-REGN            TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    PERFORM 2200-START-BP13K893  THRU 2200-EXIT                 
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    DISPLAY 'K800 KEY: ' K800-NUM-REGN                          
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       2200-START-BP13K893.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                           TO BP13K893-MASTER             
                                                    BP13K800-MASTER.            
           INITIALIZE                               BP13K893-MASTER             
                                                    BP13K800-MASTER.            
                                                                                
           MOVE SPACES                           TO K893-KEY-FLD.               
           MOVE F40I-NUM-REGN                    TO K893-NUM-REGN-HIST.         
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K893-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 2300-READ-BP13K893  THRU 2300-EXIT                  
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K893: ' WS-K893-STATUS          
                            ' REGN = ' K410-NUM-REGN                            
                    PERFORM  9999-CLOSE-ROUTINE THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2300-READ-BP13K893.                                                      
      ******************************************************************        
                                                                                
            READ  BP13K893 NEXT AT END                                          
                  MOVE SPACES               TO K893-KEY-FLD.                    
                                                                                
            EVALUATE WS-K893-STATUS                                             
                WHEN 00                                                         
                WHEN 02                                                         
                     IF K893-NUM-REGN-HIST = F40I-NUM-REGN                      
                        MOVE BP13K893-MASTER(1:2000) TO BP13K800-MASTER         
                     END-IF                                                     
                WHEN 23                                                         
                     CONTINUE                                                   
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K893 : ' WS-K893-STATUS         
                             ' REGN = ' K893-NUM-REGN-HIST                      
                     PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                
            END-EVALUATE.                                                       
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       2400-READ-BP13K410.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP13K410-REC.                       
           INITIALIZE                       BP13K410-REC.                       
                                                                                
           MOVE F40I-NUM-REGN            TO K410-KEY-FLD.                       
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE WS-K410-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES          TO BP13K410-REC                        
                    INITIALIZE              BP13K410-REC                        
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K410 READ ERROR ' WS-K410-STATUS               
                    DISPLAY 'K410 KEY: ' K410-KEY-FLD                           
                    MOVE WS-K410-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2400-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           PERFORM 2100-READ-BP13K800         THRU 2100-EXIT.                   
           PERFORM 2400-READ-BP13K410         THRU 2400-EXIT.                   
                                                                                
           PERFORM 4000-PROCESS-RECORDS       THRU 4000-EXIT.                   
                                                                                
           PERFORM 2000-READ-BP13F40I         THRU 2000-EXIT.                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       4000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE SPACES                    TO L39V-DETAIL01.                     
           INITIALIZE                        L39V-DETAIL01.                     
                                                                                
           ADD 1                          TO WS-SNO1.                           
           MOVE WS-SNO1                   TO WS-SNO-ZZZZ9.                      
           MOVE WS-SNO-ZZZZ9              TO L39V-SN.                           
           MOVE F40I-NUM-REGN             TO L39V-REGN.                         
           MOVE F40I-NUM-LETTER-TAG       TO L39V-LETTER.                       
                                                                                
           IF K800-CDE-APPT-TYPE = 'SA'                                         
              MOVE SPACES             TO L39V-APPT-DATE                         
                                         L39V-APPT-TIME                         
                                                                                
           ELSE                                                                 
              IF K800-DTE-SO-APPT NOT = SPACES AND LOW-VALUES                   
                                               AND ZEROES                       
                 STRING K800-DTE-SO-APPT(7:2) '/'                               
                        K800-DTE-SO-APPT(5:2) '/'                               
                        K800-DTE-SO-APPT(1:4)                                   
                   DELIMITED BY SIZE INTO L39V-APPT-DATE                        
              END-IF                                                            
                                                                                
              IF K800-TME-SO-APPT NOT = SPACES AND LOW-VALUES                   
                                               AND ZEROES                       
                 MOVE K800-TME-SO-APPT TO L39V-APPT-TIME                        
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
           IF K410-DTE-KEY-ISSUE NOT = SPACES AND LOW-VALUES                    
                                              AND ZEROES                        
              STRING K410-DTE-KEY-ISSUE(7:2) '/'                                
                     K410-DTE-KEY-ISSUE(5:2) '/'                                
                     K410-DTE-KEY-ISSUE(1:4)                                    
              DELIMITED BY SIZE INTO L39V-DTE-ISSUE                             
           END-IF.                                                              
                                                                                
                                                                                
           WRITE BP13L39V-REC               FROM L39V-DETAIL01.                 
           ADD 1                              TO WS-CNT-L39V-WRITE.             
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7000-PRINT-HEADER.                                                       
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)      TO WS-SYSTEM-DATE               
                                                   WS-CURR-DATE.                
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
              DELIMITED BY SIZE INTO L39V-DATE-HDR.                             
                                                                                
                                                                                
           ADD 1                               TO WS-PAGE-CTR.                  
           MOVE ZEROES                         TO L39V-PAGE-HDR.                
           MOVE WS-PAGE-CTR                    TO L39V-PAGE-HDR.                
                                                                                
           WRITE BP13L39V-REC    FROM L39V-HEADER01 AFTER PAGE.                 
           WRITE BP13L39V-REC    FROM L39V-HEADER02.                            
           WRITE BP13L39V-REC    FROM L39V-HEADER03 AFTER 2.                    
                                                                                
           INITIALIZE WS-LINE-CTR.                                              
           MOVE 5  TO WS-LINE-CTR.                                              
                                                                                
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C39V'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F40I RECORDS READ............. : '            
                    WS-F40I-READ.                                               
           DISPLAY '(2) NO OF BP13L39V RECORDS WRITTEN.......... : '            
                    WS-CNT-L39V-WRITE.                                          
                                                                                
           CLOSE    BP13F40I                                                    
                    BP13K800                                                    
                    BP13K893                                                    
                    BP13K410                                                    
                    BP13L39V.                                                   
                                                                                
                                                                                
           IF WS-K410-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K410 - ERROR CLOSING : ' WS-K410-STATUS              
              MOVE WS-K410-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR CLOSING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K893 - ERROR CLOSING : ' WS-K893-STATUS              
              MOVE WS-K893-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
