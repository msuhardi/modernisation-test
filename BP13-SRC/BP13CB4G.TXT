       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB4G.                                                 
      *AUTHOR.        ZARITA DANE SARMIENTO.                                    
      *DATE-WRITTEN.  21/04/2016.                                               
      * ========================================================== *            
      *             SYSTEM OF COMMITMENT  (BP13)                   *            
      * ========================================================== *            
      *                                                            *            
      *  DESCRIPTION :  TO GENERATE REPORT.                        *            
      *  INPUT FILE  :  AB02F935                                   *            
      *              :  BP13L935                                   *            
      * ---------------------------------------------------------- *            
      * CHG-NO.  BY     ON          DESCRIPTION                    *            
      * -------- ---    ----------  -----------                    *            
      * XXXXXXXX ZDD1   21/04/2016  NEW PROGRAM                    *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AB02F935  ASSIGN       TO AB02F935.                           
                                                                                
           SELECT BP13L935  ASSIGN       TO BP13L935.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *---------------------------------------------------------------*         
       FD  AB02F935                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY AB02F935.                                                           
                                                                                
       FD  BP13L935                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  BP13L935-REC.                                                        
           05  L935-NUM-NRIC                      PIC X(9).                     
           05  FILLER0002                         PIC X(01).                    
           05  L935-NUM-REFNO                     PIC X(12).                    
           05  FILLER0003                         PIC X(01).                    
           05  L935-AMT-GRANT                     PIC X(10).                    
           05  FILLER0004                         PIC X(01).                    
           05  L935-SIGN-GRNT-EXCS                PIC X(1).                     
           05  FILLER0005                         PIC X(01).                    
           05  L935-SYS-ID                        PIC X(4).                     
           05  FILLER0006                         PIC X(01).                    
           05  L935-NUM-REGNO                     PIC X(8).                     
           05  FILLER0007                         PIC X(01).                    
           05  L935-STATUS                        PIC X(03).                    
           05  FILLER0008                         PIC X(01).                    
           05  L935-REMARK                        PIC X(40).                    
           05  FILLER0009                         PIC X(01).                    
           05  L935-DTE-OF-RUN                    PIC X(8).                     
           05  FILLER0010                         PIC X(01).                    
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-VARIABLES.                                                        
           05  WS-F935-EOF         PIC X(01)  VALUE SPACES.                     
               88 F935-END-OF-FILE   VALUE 'Y'.                                 
           05  WS-F935-READ-CNT    PIC 9(08)  VALUE ZEROES.                     
           05  WS-WRT-L935A        PIC 9(08)  VALUE ZEROES.                     
                                                                                
       01  WS-DATE-TIME            PIC X(15)  VALUE SPACES.                     
                                                                                
       01  WS-CURR-DATE            PIC X(10)  VALUE SPACES.                     
       01  WS-CURR-TIME            PIC X(08)  VALUE SPACES.                     
                                                                                
       01  WS-EDT-CNT              PIC Z(7)9.                                   
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *---------------------------------------------------------------*         
       0000-CONTROL.                                                            
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-READ-AB02F935     THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS-F935      THRU 3000-EXIT                        
                                 UNTIL F935-END-OF-FILE.                        
           PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  AB02F935                                                 
              OUTPUT   BP13L935.                                                
           STRING                                                               
              'NUM-NRIC;'                                                       
              'NUM-REFNO;'                                                      
              'AMT-GRANT;'                                                      
              'SIGN-GRNT-EXCS;'                                                 
              'SYS-ID;'                                                         
              'NUM-REGNO;'                                                      
              'STATUS;'                                                         
              'REMARK;'                                                         
              'DTE-OF-RUN;'                                                     
              DELIMITED BY SIZE INTO BP13L935-REC.                              
                                                                                
              WRITE BP13L935-REC.                                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-AB02F935.                                                      
      *---------------------------------------------------------------*         
           READ AB02F935 AT END                                                 
                          MOVE 'Y'  TO WS-F935-EOF                              
                         NOT AT END                                             
                          ADD  1    TO WS-F935-READ-CNT                         
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2500-MOVE-FILLER.                                                        
      *---------------------------------------------------------------*         
                                                                                
           MOVE ';'                TO                                           
                                       FILLER0002                               
                                       FILLER0003                               
                                       FILLER0004                               
                                       FILLER0005                               
                                       FILLER0006                               
                                       FILLER0007                               
                                       FILLER0008                               
                                       FILLER0009                               
                                       FILLER0010.                              
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-F935.                                                       
      *---------------------------------------------------------------*         
                                                                                
           MOVE SPACES             TO  BP13L935-REC.                            
           INITIALIZE                  BP13L935-REC.                            
                                                                                
           PERFORM 2500-MOVE-FILLER     THRU 2500-EXIT.                         
                                                                                
           MOVE F935-NUM-CPF-ACCT          TO L935-NUM-NRIC                     
           MOVE F935-NUM-PREF-HDB          TO L935-NUM-REFNO                    
           STRING F935-AMT-GRNT(1:7) '.'                                        
                  F935-AMT-GRNT(8:2)                                            
           DELIMITED BY SIZE INTO  L935-AMT-GRANT.                              
      *    MOVE F935-AMT-GRNT              TO L935-AMT-GRANT                    
           MOVE F935-SIGN-GRNT-EXCS        TO L935-SIGN-GRNT-EXCS               
           MOVE 'BP13'                     TO L935-SYS-ID                       
           MOVE F935-NUM-REGN              TO L935-NUM-REGNO                    
           MOVE F935-CDE-STATUS            TO L935-STATUS                       
           MOVE F935-TXT-REMARKS           TO L935-REMARK                       
           MOVE FUNCTION CURRENT-DATE(1:8) TO L935-DTE-OF-RUN                   
                                                                                
           WRITE BP13L935-REC.                                                  
           ADD 1 TO WS-WRT-L935A.                                               
                                                                                
           PERFORM 2000-READ-AB02F935      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY ' '.                                                         
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
           DISPLAY '   PROGRAM : BP13L935 ' .                                   
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
           DISPLAY ' '.                                                         
           DISPLAY ' REC READ FROM AB02F935 - '   WS-F935-READ-CNT.             
           DISPLAY ' REC WRITTEN ON THE REPORT -' WS-WRT-L935A.                 
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
                                                                                
           CLOSE AB02F935                                                       
                 BP13L935.                                                      
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
