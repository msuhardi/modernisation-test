      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
                                                                                
       PROGRAM-ID.    BP13C240.                                                 
      *AUTHOR.        GILBERT S. DURAN.                                         
      *DATE WRITTEN.  29/10/94.                                                 
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  1.  TO EXTRACT BP13F200 RECORDS WITH MATCHING *        
      *                      BP13F205 RECORD                           *        
      *                                                                *        
      *   INPUT  FILE :  1.  BP13F200  -  BOOKING APPOINTMENT FILE     *        
      *                  2.  BP13F205  -  CONTROL RECORD      FILE     *        
      *                  3.  BP13K800  -  SOC MASTER          FILE     *        
      *                                                                *        
      *   OUTPUT FILE :  1.  P13F200O  -  BK APPOINTMENT FILE EXTRACT  *        
      *                  2.  P13F240O  -  LIST OF CANCELLED REGN NO.   *        
      *                                                                *        
      *   LISTINGS    :  1.  CONTROL LIST                              *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ---------------------------------   *        
      *   SOC-PH9   GJ    180195   ADD A REPORT ON CANCELLED REGN NO'S *        
      *   BP130418  SIA   300998   Y2K COMPLIANCE CHANGES              *        
      *   BP132095  MTD   140302   MOVE DATE AND TIME UPD LIST, DATE   *        
      *                            RESELECTION AND QUEUE PREFIX FROM   *        
      *                            BP13F205 TO BP13F200                *        
      *   BP133390  RB12  280708   FILTER BY START AND END QUEUE       *        
      *   BP134226  ESA1  290411   CATER FOR 2 BYTES QUEUE-PREFIX      *        
      *   BP134521  ESA1  030512   CATER FOR MGPS                      *        
      *   BP136836  ESA1  180817   TO PRINT 3 BYTE-ZONE IN THE REPORT  *        
      ******************************************************************        
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F200  ASSIGN       TO BP13F200.                           
           SELECT BP13F205  ASSIGN       TO BP13F205.                           
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
           SELECT P13F200O  ASSIGN       TO P13F200O.                           
           SELECT P13F240O  ASSIGN       TO P13F240O.                           
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F200                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13F200.                                                           
                                                                                
       FD   BP13F205                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 80  CHARACTERS.                                     
       COPY BP13F205.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   P13F200O                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 300 CHARACTERS.                                     
       01  P13F200O-REC     PIC X(300).                                         
                                                                                
       FD   P13F240O                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   P13F240O-REC    PIC X(132).                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILE-STATUS.                                                         
           05 WS-K800-STATUS            PIC 9(02) VALUE 00.                     
                                                                                
       01  WS-MATCH-FIELDS.                                                     
           05 WS-F200-MATCH-KEY.                                                
              10 WS-F200-NUM-NT-ZONE    PIC X(03).                              
              10 WS-F200-NUM-FLAT-TYPE  PIC X(02).                              
              10 WS-F200-DTE-BALLOT     PIC X(06).                              
           05 WS-F205-MATCH-KEY.                                                
              10 WS-F205-NUM-NT-ZONE    PIC X(03).                              
              10 WS-F205-NUM-FLAT-TYPE  PIC X(02).                              
              10 WS-F205-DTE-BALLOT     PIC X(06).                              
                                                                                
       01  WS-ACCUMULATORS.                                                     
           05 WS-F200-READ              PIC 9(08) VALUE ZEROES.                 
           05 WS-F205-READ              PIC 9(08) VALUE ZEROES.                 
           05 WS-F200-DELETE            PIC 9(08) VALUE ZEROES.                 
           05 WS-K800-READ              PIC 9(08) VALUE ZEROES.                 
           05 WS-F200-WRITE             PIC 9(08) VALUE ZEROES.                 
           05 WS-MAX-LINES              PIC 9(02) VALUE 60.                     
           05 WS-LINE-COUNT             PIC 9(02) VALUE 65.                     
           05 WS-L240-PRINTED           PIC 9(08) VALUE ZEROES.                 
           05 WS-PAGE-NO                PIC 9(05) VALUE ZEROES.                 
                                                                                
       01  WS-RUN-DATE                  PIC X(08).                              
       01  WS-PREV-F205-ZONE            PIC X(03) VALUE SPACES.                 
       01  WS-PREV-F205-FLAT-TYPE       PIC X(02) VALUE SPACES.                 
                                                                                
       01  WS-SWITCHES.                                                         
           05 WS-F200-EOF-SW            PIC X(01) VALUE SPACES.                 
              88 C-EOF-F200                 VALUE 'Y'.                          
           05 WS-F205-EOF-SW            PIC X(01) VALUE SPACES.                 
              88 C-EOF-F205                 VALUE 'Y'.                          
                                                                                
       01  WS-DTE-CANCEL.                                                       
           05 WS-CANCEL-CC              PIC X(02).                              
           05 WS-CANCEL-YY              PIC X(02).                              
           05 WS-CANCEL-MM              PIC X(02).                              
           05 WS-CANCEL-DD              PIC X(02).                              
                                                                                
       01  WS-EDIT-DTE-CANCEL.                                                  
           05 WS-CANCEL-DD              PIC X(02).                              
           05 FILLER                    PIC X(01) VALUE '/'.                    
           05 WS-CANCEL-MM              PIC X(02).                              
           05 FILLER                    PIC X(01) VALUE '/'.                    
           05 WS-CANCEL-CC              PIC X(02).                              
           05 WS-CANCEL-YY              PIC X(02).                              
                                                                                
       01  WS-DATE.                                                             
           05 WS-CCYY                   PIC 9(04).                              
           05 WS-MM                     PIC 9(02).                              
           05 WS-DD                     PIC 9(02).                              
                                                                                
       01  WS-EDIT-DATE.                                                        
           05 WS-EDIT-DD                PIC 9(02).                              
           05 FILLER                    PIC X(01) VALUE '/'.                    
           05 WS-EDIT-MM                PIC 9(02).                              
           05 FILLER                    PIC X(01) VALUE '/'.                    
           05 WS-EDIT-CCYY              PIC 9(04).                              
                                                                                
       01  L240-HDG1.                                                           
           05 FILLER                    PIC X(14) VALUE 'BP13C240'.             
           05 FILLER                    PIC X(08) VALUE 'HDB3'.                 
           05 FILLER                    PIC X(18) VALUE SPACES.                 
           05 FILLER                    PIC X(39) VALUE                         
              'S Y S T E M   O F   C O M M I T M E N T'.                        
           05 FILLER                    PIC X(16) VALUE SPACES.                 
           05 FILLER                    PIC X(07) VALUE 'DATE: '.               
           05 L240-DATE                 PIC X(10) VALUE SPACES.                 
           05 FILLER                    PIC X(03) VALUE SPACES.                 
           05 FILLER                    PIC X(07) VALUE 'PAGE: '.               
           05 L240-PAGE                 PIC ZZZZ9.                              
                                                                                
       01  L240-HDG2.                                                           
           05 FILLER                    PIC X(35) VALUE SPACES.                 
           05 FILLER                    PIC X(31) VALUE                         
             'LIST OF CANCELLATION CASES FOR '.                                 
           05 L240-ZONE                 PIC X(03) VALUE SPACES.                 
           05 FILLER                    PIC X(06) VALUE ' ZONE '.               
           05 L240-FLAT-TYPE            PIC X(06) VALUE SPACES.                 
           05 FILLER                    PIC X(06) VALUE ' FLAT '.               
           05 FILLER                    PIC X(11) VALUE SPACES.                 
           05 FILLER                    PIC X(34) VALUE                         
             'USER: SALES UNIT (ALLOCATION TEAM)'.                              
                                                                                
       01  L240-HDG3.                                                           
           05 FILLER                    PIC X(10) VALUE SPACES.                 
           05 FILLER                    PIC X(06) VALUE 'SER NO'.               
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(07) VALUE 'REGN NO'.              
           05 FILLER                    PIC X(03) VALUE SPACES.                 
           05 FILLER                    PIC X(11) VALUE 'CANCEL DATE'.          
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(13) VALUE 'CANCEL REASON'.        
                                                                                
       01  L240-HDG4.                                                           
           05 FILLER                    PIC X(10) VALUE SPACES.                 
           05 FILLER                    PIC X(06) VALUE '------'.               
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(07) VALUE '-------'.              
           05 FILLER                    PIC X(03) VALUE SPACES.                 
           05 FILLER                    PIC X(11) VALUE '-----------'.          
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 FILLER                    PIC X(13) VALUE '-------------'.        
                                                                                
       01  L240-DETAIL-REC.                                                     
           05 FILLER                    PIC X(10) VALUE SPACES.                 
           05 L240-SER-NO               PIC ZZ9   VALUE ZEROES.                 
           05 FILLER                    PIC X(05) VALUE SPACES.                 
           05 L240-NUM-REGN             PIC X(08) VALUE SPACES.                 
           05 FILLER                    PIC X(02) VALUE SPACES.                 
           05 L240-DTE-CANCEL           PIC X(10) VALUE SPACES.                 
           05 FILLER                    PIC X(03) VALUE SPACES.                 
           05 L240-NUM-RC               PIC X(02) VALUE SPACES.                 
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      ***********                                                               
       0000-MAIN.                                                               
      ***********                                                               
                                                                                
            PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                        
            PERFORM 2000-PROCESS-ROUTINE THRU 2000-EXIT.                        
            PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *******************                                                       
       1000-OPEN-ROUTINE.                                                       
      *******************                                                       
                                                                                
           OPEN INPUT BP13F200                                                  
                      BP13F205                                                  
                      BP13K800                                                  
               OUTPUT P13F200O                                                  
                      P13F240O.                                                 
                                                                                
           IF WS-K800-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR OPENING BP13K800 FILE, STATUS '                    
                       WS-K800-STATUS                                           
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           MOVE   WS-DD   TO   WS-EDIT-DD.                                      
           MOVE   WS-MM   TO   WS-EDIT-MM.                                      
           MOVE   WS-CCYY TO   WS-EDIT-CCYY.                                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       2000-PROCESS-ROUTINE.                                                    
      **********************                                                    
                                                                                
           PERFORM 2100-READ-F205      THRU  2100-EXIT.                         
           PERFORM 2200-READ-F200      THRU  2200-EXIT.                         
           PERFORM 2300-PROCESS-RECORD THRU  2300-EXIT                          
              UNTIL C-EOF-F200 OR                                               
                    C-EOF-F205.                                                 
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ****************                                                          
       2100-READ-F205.                                                          
      ****************                                                          
                                                                                
           READ BP13F205                                                        
              AT END MOVE 'Y'       TO  WS-F205-EOF-SW                          
              GO TO 2100-EXIT                                                   
              END-READ.                                                         
                                                                                
           MOVE F205-NUM-NT-ZONE    TO WS-F205-NUM-NT-ZONE.                     
           MOVE F205-NUM-FLAT-TYPE  TO WS-F205-NUM-FLAT-TYPE.                   
           MOVE F205-DTE-BALLOT     TO WS-F205-DTE-BALLOT.                      
           ADD +1                   TO WS-F205-READ.                            
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************                                                          
       2200-READ-F200.                                                          
      ****************                                                          
                                                                                
           READ BP13F200                                                        
              AT END MOVE 'Y'       TO  WS-F200-EOF-SW                          
              GO TO 2200-EXIT                                                   
              END-READ.                                                         
                                                                                
           MOVE F200-NUM-NT-ZONE    TO WS-F200-NUM-NT-ZONE.                     
           MOVE F200-NUM-FLAT-TYPE  TO WS-F200-NUM-FLAT-TYPE.                   
           MOVE F200-DTE-BALLOT     TO WS-F200-DTE-BALLOT.                      
           ADD +1                   TO WS-F200-READ.                            
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       2300-PROCESS-RECORD.                                                     
      *********************                                                     
                                                                                
           IF WS-F200-MATCH-KEY = WS-F205-MATCH-KEY                             
             IF F205-START-QUEUE NOT = SPACES AND LOW-VALUES                    
                IF (((F200-NUM-HHTY-QUEUE-SERIAL >= F205-START-QUEUE)           
                AND (F200-NUM-HHTY-QUEUE-SERIAL <= F205-END-QUEUE)) OR          
                    ((F200-NUM-MGPS-QUEUE-SERIAL >= F205-START-QUEUE)           
                AND (F200-NUM-MGPS-QUEUE-SERIAL <= F205-END-QUEUE)))            
                   PERFORM 2310-PROCESS-F200 THRU 2310-EXIT                     
                ELSE                                                            
                   CONTINUE                                                     
                END-IF                                                          
             ELSE                                                               
                PERFORM 2310-PROCESS-F200   THRU 2310-EXIT                      
             END-IF                                                             
             PERFORM 2200-READ-F200           THRU 2200-EXIT                    
           ELSE                                                                 
              IF (WS-F205-MATCH-KEY > WS-F200-MATCH-KEY)                        
                 PERFORM 2200-READ-F200        THRU 2200-EXIT                   
              ELSE                                                              
                 PERFORM 2100-READ-F205        THRU 2100-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       2310-PROCESS-F200.                                                       
      *******************                                                       
                                                                                
           MOVE F200-NUM-REGN      TO K800-NUM-REGN.                            
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN  00                                                             
                 ADD +1                   TO WS-K800-READ                       
                 IF K800-NUM-STATUS NOT = 'C'                                   
                    MOVE F205-DTE-ALLOCN TO F200-DTE-ALLOC                      
                    IF F205-NUM-OPTION = 'UN' OR 'UZ'                           
                       MOVE F205-DTE-RESELECTION                                
                         TO F200-DTE-RESELECTION                                
                       MOVE F205-DTE-REQUEST   TO F200-DTE-SER-REQUEST          
                       MOVE F205-NUM-QUEUE-PREFIX(1:1) TO                       
                            F200-NUM-HHTY-QUEUE-SERIAL (1:1)                    
                       IF F205-NUM-QUEUE-PREFIX(2:1) NOT = SPACES               
                                                     AND LOW-VALUES             
                          MOVE F205-NUM-QUEUE-PREFIX(2:1) TO                    
                               F200-NUM-HHTY-QUEUE-SERIAL (2:1)                 
                       END-IF                                                   
                    END-IF                                                      
                    WRITE P13F200O-REC  FROM BP13F200-REC                       
                    ADD +1                TO WS-F200-WRITE                      
                 ELSE                                                           
                    ADD +1                      TO WS-F200-DELETE               
                    PERFORM 3000-WRITE-REPORT THRU 3000-EXIT                    
                 END-IF                                                         
           WHEN  23                                                             
                 ADD +1                   TO WS-F200-DELETE                     
                 DISPLAY 'REGN NOT FOUND IN K800, REGN # : '                    
                          F200-NUM-REGN                                         
           WHEN  OTHER                                                          
                 DISPLAY 'ERROR READING BP13K800 FILE, STATUS : '               
                          WS-K800-STATUS                                        
           END-EVALUATE.                                                        
                                                                                
       2310-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       3000-WRITE-REPORT.                                                       
      ******************                                                        
                                                                                
           IF ( WS-LINE-COUNT          < WS-MAX-LINES         ) AND             
              ( WS-PREV-F205-ZONE      = WS-F205-NUM-NT-ZONE  ) AND             
              ( WS-PREV-F205-FLAT-TYPE = WS-F205-NUM-FLAT-TYPE)                 
                CONTINUE                                                        
           ELSE                                                                 
              PERFORM 3500-WRITE-HEADING  THRU 3500-EXIT                        
           END-IF.                                                              
                                                                                
           ADD  1                            TO WS-L240-PRINTED.                
           MOVE WS-L240-PRINTED              TO L240-SER-NO.                    
                                                                                
           MOVE    K800-DTE-CANCEL           TO WS-DTE-CANCEL.                  
           MOVE    CORR WS-DTE-CANCEL        TO WS-EDIT-DTE-CANCEL.             
           MOVE    WS-EDIT-DTE-CANCEL        TO L240-DTE-CANCEL.                
           MOVE    K800-NUM-REGN             TO L240-NUM-REGN.                  
           MOVE    K800-NUM-CANCEL-REASON    TO L240-NUM-RC.                    
                                                                                
           WRITE P13F240O-REC  FROM L240-DETAIL-REC AFTER 1.                    
           ADD   1                           TO WS-LINE-COUNT.                  
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************                                                        
       3500-WRITE-HEADING.                                                      
      ******************                                                        
                                                                                
           MOVE  WS-EDIT-DATE          TO L240-DATE.                            
           ADD   1                     TO WS-PAGE-NO.                           
           MOVE  WS-PAGE-NO            TO L240-PAGE.                            
                                                                                
           IF ( WS-PREV-F205-ZONE      = WS-F205-NUM-NT-ZONE  ) AND             
              ( WS-PREV-F205-FLAT-TYPE = WS-F205-NUM-FLAT-TYPE)                 
                CONTINUE                                                        
           ELSE                                                                 
                MOVE ZEROES            TO WS-L240-PRINTED                       
           END-IF.                                                              
                                                                                
           MOVE  WS-F205-NUM-NT-ZONE   TO WS-PREV-F205-ZONE.                    
           MOVE  WS-F205-NUM-FLAT-TYPE TO WS-PREV-F205-FLAT-TYPE.               
                                                                                
           MOVE WS-F205-NUM-NT-ZONE    TO L240-ZONE.                            
                                                                                
           EVALUATE  WS-F205-NUM-FLAT-TYPE                                      
           WHEN '3 '                                                            
                 MOVE '3-ROOM'          TO L240-FLAT-TYPE                       
           WHEN '4 '                                                            
                 MOVE '4-ROOM'          TO L240-FLAT-TYPE                       
           WHEN '4D'                                                            
                 MOVE 'TYPE-A'          TO L240-FLAT-TYPE                       
           WHEN '5 '                                                            
                 MOVE '5-ROOM'          TO L240-FLAT-TYPE                       
           WHEN '5D'                                                            
                 MOVE 'TYPE-A'          TO L240-FLAT-TYPE                       
           WHEN 'E '                                                            
                 MOVE ' EXEC '          TO L240-FLAT-TYPE                       
           WHEN 'ED'                                                            
                 MOVE 'TYPE-C'          TO L240-FLAT-TYPE                       
           WHEN OTHER                                                           
                 MOVE WS-F205-NUM-FLAT-TYPE TO L240-FLAT-TYPE                   
           END-EVALUATE.                                                        
                                                                                
           WRITE P13F240O-REC  FROM L240-HDG1 AFTER ADVANCING PAGE.             
           WRITE P13F240O-REC  FROM L240-HDG2 AFTER ADVANCING 1.                
           WRITE P13F240O-REC  FROM L240-HDG3 AFTER ADVANCING 2.                
           WRITE P13F240O-REC  FROM L240-HDG4 AFTER ADVANCING 1.                
                                                                                
           MOVE SPACES  TO P13F240O-REC.                                        
           WRITE P13F240O-REC.                                                  
           MOVE  5      TO WS-LINE-COUNT.                                       
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ********************                                                      
       9000-CLOSE-ROUTINE.                                                      
      ********************                                                      
                                                                                
           CLOSE BP13F200                                                       
                 BP13F205                                                       
                 BP13K800                                                       
                 P13F200O                                                       
                 P13F240O.                                                      
                                                                                
           IF WS-K800-STATUS NOT = ZEROS                                        
              DISPLAY 'ERROR CLOSING BP13K800 FILE, STATUS '                    
                       WS-K800-STATUS                                           
           END-IF.                                                              
                                                                                
           DISPLAY '                                       '.                   
           DISPLAY ' *****  BP13C240 CONTROL TOTALS ****** '.                   
           DISPLAY ' RUN DATE : ' WS-EDIT-DATE                                  
           DISPLAY '                                       '.                   
           DISPLAY ' RECORDS READ    FROM BP13F200  = ' WS-F200-READ.           
           DISPLAY ' RECORDS READ    FROM BP13F205  = ' WS-F205-READ.           
           DISPLAY ' RECORDS READ    FROM BP13K800  = ' WS-K800-READ.           
           DISPLAY ' RECORDS DELETED FROM BP13F200  = ' WS-F200-DELETE.         
           DISPLAY ' RECORDS WRITTEN TO   P13F200O  = ' WS-F200-WRITE.          
           DISPLAY ' RECORDS WRITTEN TO   P13F240O  = ' WS-L240-PRINTED.        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
**************************** END OF PROGRAM ****************************        
