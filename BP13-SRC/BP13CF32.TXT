       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF32.                                                 
      *AUTHOR.        ZDD1                                                      
      *DATE-WRITTEN.  25MAY2011.                                                
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1. TO CHECK IF THE CASES IS EXISTING IN K800        *                
      *    INPUT FILES  : BP13FA40                             *                
      *    I-O            BP13K800                             *                
      *    OUTPUT         P13FA40A                             *                
      *--------------------------------------------------------*                
      * CHG-NO  BY  DATE      DETAILS                          *                
      *=======  === ========= =================================*                
      *BP133893 ZDD 20100426  NEW PGM                          *                
      *BP135130 SMR 20131113  INCLUDE BP13KB78                 *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FA40 ASSIGN TO BP13FA40.                                  
                                                                                
           SELECT P13FA40A ASSIGN TO P13FA40A.                                  
           SELECT P13FA40B ASSIGN TO P13FA40B.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
           SELECT BP13KB78 ASSIGN TO BP13KB78                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KB78-KEY-FLD                               
                  FILE STATUS     IS WS-KB78-STATUS.                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13FA40                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13FA40.                                                           
                                                                                
       FD   P13FA40A                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 150 CHARACTERS.                                     
       01   P13FA40A-REC   PIC X(150).                                          
                                                                                
       FD   P13FA40B                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 150 CHARACTERS.                                     
       01   P13FA40B-REC   PIC X(150).                                          
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13KB78                                                            
            RECORD CONTAINS 3300 CHARACTERS.                                    
       COPY BP13KB78.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-FA40-EOF                    PIC X(1)  VALUE 'N'.              
           05  WS-K800-STATUS                 PIC 9(2)  VALUE ZEROES.           
           05  WS-KB78-STATUS                 PIC 9(2)  VALUE ZEROES.           
           05  WS-WRITTEN                     PIC 9(6)  VALUE ZEROS.            
           05  WS-WRITTEN-B                   PIC 9(6)  VALUE ZEROS.            
           05  WS-FND-K800                    PIC 9(6)  VALUE ZEROS.            
           05  WS-NFND-K800                   PIC 9(6)  VALUE ZEROS.            
           05  WS-FOUND                       PIC X(1)  VALUE 'N'.              
           05  WS-KB78-NTFND                  PIC 9(6)  VALUE ZEROS.            
           05  WS-KB78-FOUND                  PIC X(1)  VALUE 'N'.              
           05  WS-CNT-READ                    PIC 9(6)  VALUE ZEROS.            
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
                                                                                
           PERFORM 2000-READ-FA40         THRU 2000-EXIT.                       
                                                                                
           PERFORM 3000-PROCESS-FA40      THRU 3000-EXIT                        
                  UNTIL  WS-FA40-EOF = 'Y'.                                     
                                                                                
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13FA40                                                 
                       BP13K800                                                 
                       BP13KB78                                                 
                OUTPUT P13FA40A                                                 
                       P13FA40B.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 OPEN ERROR, STATUS IS ' WS-K800-STATUS          
              MOVE WS-K800-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-KB78-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KB78 OPEN ERROR, STATUS IS ' WS-KB78-STATUS          
              MOVE WS-KB78-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-FA40.                                                          
      ****************************************************************          
                                                                                
           READ BP13FA40                                                        
              AT END MOVE 'Y' TO WS-FA40-EOF                                    
              GO TO 2000-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD  +1                TO WS-CNT-READ.                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2200-READ-K800.                                                          
      ****************************************************************          
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
            WHEN 00                                                             
               MOVE 'Y' TO WS-FOUND                                             
               ADD 1 TO WS-FND-K800                                             
            WHEN 23                                                             
               MOVE 'N' TO WS-FOUND                                             
               ADD 1 TO WS-NFND-K800                                            
            WHEN OTHER                                                          
              DISPLAY 'ERROR READING OF BP13K800. STATUS IS '                   
                       WS-K800-STATUS                                           
              MOVE WS-K800-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-FA40.                                                       
      ****************************************************************          
                                                                                
              MOVE SPACES        TO BP13K800-MASTER                             
              MOVE FA40-REGN-NO  TO  K800-NUM-REGN                              
              PERFORM 2200-READ-K800 THRU 2200-EXIT                             
              IF WS-FOUND = 'Y'                                                 
                 WRITE P13FA40A-REC FROM BP13FA40-TRANS-REC                     
                 ADD 1 TO WS-WRITTEN                                            
              ELSE                                                              
                 PERFORM 3100-READ-BP13KB78 THRU 3100-EXIT                      
                 IF WS-KB78-FOUND = 'Y'                                         
                    WRITE P13FA40B-REC FROM BP13FA40-TRANS-REC                  
                    ADD 1 TO WS-WRITTEN-B                                       
                 END-IF                                                         
              END-IF.                                                           
                                                                                
              PERFORM 2000-READ-FA40    THRU 2000-EXIT.                         
                                                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3100-READ-BP13KB78.                                                      
      ****************************************************************          
                                                                                
           MOVE SPACES        TO BP13KB78-REC.                                  
           MOVE FA40-REGN-NO  TO KB78-NUM-REGN.                                 
                                                                                
           READ BP13KB78.                                                       
                                                                                
           EVALUATE WS-KB78-STATUS                                              
           WHEN 00                                                              
              IF KB78-NUM-STATUS NOT = 'CAN'                                    
                 MOVE 'Y' TO WS-KB78-FOUND                                      
              ELSE                                                              
                 MOVE 'N' TO WS-KB78-FOUND                                      
                 ADD 1    TO WS-KB78-NTFND                                      
              END-IF                                                            
           WHEN 23                                                              
              MOVE 'N'    TO WS-KB78-FOUND                                      
              ADD 1       TO WS-KB78-NTFND                                      
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING OF BP13KB78. STATUS IS '                   
                       WS-KB78-STATUS                                           
              MOVE WS-KB78-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13CF32 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF    RECORDS READ      = '  WS-CNT-READ.                
           DISPLAY 'NO OF REC FND IN K800      = '  WS-FND-K800.                
           DISPLAY 'NO OF REC NOT FND IN K800  = '  WS-NFND-K800.               
           DISPLAY 'NO OF REC WRT (FND IN K800)= '  WS-WRITTEN.                 
           DISPLAY 'NO OF REC WRT(NFND IN K800)= '  WS-WRITTEN-B.               
           DISPLAY 'NO OF REC NTFND/CAN IN KB78= '  WS-KB78-NTFND.              
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           CLOSE BP13FA40                                                       
                 BP13K800                                                       
                 BP13KB78                                                       
                 P13FA40A                                                       
                 P13FA40B.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 CLOSE ERROR, STATUS IS ' WS-K800-STATUS         
              MOVE WS-K800-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF WS-KB78-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KB78 CLOSE ERROR, STATUS IS ' WS-KB78-STATUS         
              MOVE WS-KB78-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
