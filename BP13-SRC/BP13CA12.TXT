      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13CA12.                                                 
       AUTHOR.        LMS.                                                      
       DATE-WRITTEN.  28/11/06.                                                 
                                                                                
      ************************************************************              
      *    SYSTEM NAME :   SYSTEM OF COMMITMENT                  *              
      ************************************************************              
      *    OBJECTIVE   :                                         *              
      *      1.  PROGRAM TO UPDATE PIDB INTO TRANSACTION FILE    *              
      *          FOR BATCH PROCESSING.                           *              
      *                                                          *              
      *    INPUT FILES :                                         *              
      *      1.  BP13KA40 - TRANSACTION FILE                     *              
      *                                                          *              
      *   OUTPUT FILES :                                         *              
      *      1.  BP13FA40 - OUTPUT TRANSACTION FLAT FILE         *              
      *                                                          *              
      *   SUB-PROGRAM :                                          *              
      *      1.  BP13C913 - CALLED SUB-ROUTINE TO RETRIEVE       *              
      *                     PIDB INFORMATION.                    *              
      ************************************************************              
      *                                                          *              
      *  CHG-REQ     DATE     BY    DESCRIPTION                  *              
      *  -------   --------   ---   ---------------------------  *              
      * BP132876   28/11/06   LMS   NEW PROGRAM.                 *              
      ************************************************************              
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
                                                                                
      *------------------------*                                                
       INPUT-OUTPUT SECTION.                                                    
      *------------------------*                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13KA40   ASSIGN        TO  BP13KA40                         
                             ACCESS MODE   IS  DYNAMIC                          
                             ORGANIZATION  IS  INDEXED                          
                             RECORD KEY    IS  KA40-KEY-FLD                     
                             FILE STATUS   IS  WS-KA40-STATUS.                  
                                                                                
           SELECT BP13FA40   ASSIGN        TO  BP13FA40.                        
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
      *------------------------*                                                
                                                                                
       FD   BP13KA40                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13KA40.                                                           
                                                                                
       FD   BP13FA40                                                            
            RECORD CONTAINS 150 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       COPY BP13FA40.                                                           
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-KA40-STATUS          PIC 9(2)    VALUE ZEROS.                 
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-EOF-KA40-IND         PIC X       VALUE SPACE.                 
               88  WS-EOF-KA40                     VALUE 'Y'.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-KA40-READ        PIC 9(5)    VALUE 0.                     
           05  WS-CNT-FA40-WRITE       PIC 9(5)    VALUE 0.                     
           05  WS-CNT-FA40-NON-PIDB    PIC 9(5)    VALUE 0.                     
           05  WS-CNT-PIDB-FND         PIC 9(5)    VALUE 0.                     
           05  WS-CNT-PIDB-NOTFND      PIC 9(5)    VALUE 0.                     
                                                                                
       05  WS-NUM-HSEMAIN.                                                      
           10  WS-NUM-HSE-MAIN         PIC X(4).                                
           10  WS-NUM-HSE-SUB.                                                  
               15  WS-NUM-HSE-SUB1     PIC X.                                   
               15  WS-NUM-HSE-SUB2     PIC XX.                                  
                                                                                
       01  SQL-CODE                    PIC 9(4).                                
                                                                                
      *----------------------------------------------------------------*        
      *        LINKAGE FOR SUB-PROGRAM BP13C913                        *        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH     PIC X(4).                                
               10  WS-LINK-NUM-ACC     PIC X(5).                                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
      *--------------------*                                                    
       PROCEDURE DIVISION.                                                      
      *--------------------*                                                    
                                                                                
      *-----------------*                                                       
       000-MAIN-ROUTINE.                                                        
      *-----------------*                                                       
      **** BP13KA40 PROCESSING ****                                             
           PERFORM  100-OPEN-ROUTINE        THRU  100-EXIT.                     
           PERFORM  200-START-BP13KA40      THRU  200-EXIT.                     
           PERFORM  300-READ-BP13KA40       THRU  300-EXIT.                     
           PERFORM  400-PROCESS-ROUTINE     THRU  400-EXIT                      
                        UNTIL WS-EOF-KA40.                                      
           PERFORM  900-CONTROL-LIST-KA40.                                      
                                                                                
      **** CLOSE ROUTINE ****                                                   
           PERFORM  990-CLOSE-ROUTINE.                                          
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       100-OPEN-ROUTINE.                                                        
      *-----------------*                                                       
           OPEN INPUT  BP13KA40                                                 
                OUTPUT BP13FA40.                                                
                                                                                
           IF WS-KA40-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, BP13KA40-STATUS = '                       
                       WS-KA40-STATUS                                           
              MOVE     WS-KA40-STATUS TO RETURN-CODE                            
              PERFORM  900-CONTROL-LIST-KA40   THRU  900-EXIT                   
              PERFORM  990-CLOSE-ROUTINE.                                       
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       200-START-BP13KA40.                                                      
      *-----------------*                                                       
           MOVE  LOW-VALUES  TO KA40-KEY-FLD.                                   
                                                                                
           START BP13KA40 KEY > KA40-KEY-FLD.                                   
                                                                                
           IF WS-KA40-STATUS NOT = 00                                           
              DISPLAY 'ERROR START BP13KA40, STATUS = ' WS-KA40-STATUS          
              MOVE     WS-KA40-STATUS TO RETURN-CODE                            
              PERFORM  900-CONTROL-LIST-KA40 THRU 900-EXIT                      
              PERFORM  990-CLOSE-ROUTINE.                                       
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       300-READ-BP13KA40.                                                       
      *-----------------*                                                       
           READ BP13KA40 NEXT AT END                                            
                MOVE 'Y' TO WS-EOF-KA40-IND                                     
                GO TO 300-EXIT.                                                 
                                                                                
           ADD 1 TO WS-CNT-KA40-READ.                                           
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       400-PROCESS-ROUTINE.                                                     
      *--------------------*                                                    
           MOVE  SPACES                TO  BP13FA40-TRANS-REC.                  
           INITIALIZE                      BP13FA40-TRANS-REC.                  
                                                                                
           MOVE  BP13KA40-TRANS-REC    TO  BP13FA40-TRANS-REC.                  
                                                                                
           IF (KA40-SCH-ACC NOT = SPACES AND ZEROES AND LOW-VALUES)             
               MOVE  SPACES               TO     BP13COMM8-REC                  
               INITIALIZE                        BP13COMM8-REC                  
               MOVE  KA40-SCH-ACC         TO     WS-LINK-REC                    
               PERFORM 500-CALL-BP13C913  THRU   500-EXIT                       
                                                                                
               IF COMM8-CDE-SYSERR = 0                                          
                  ADD 1 TO WS-CNT-PIDB-FND                                      
                  PERFORM 600-MOVE-PIDB-DETLS THRU 600-EXIT                     
               ELSE                                                             
                  ADD 1 TO WS-CNT-PIDB-NOTFND                                   
               END-IF                                                           
           ELSE                                                                 
               ADD 1 TO  WS-CNT-FA40-NON-PIDB                                   
           END-IF.                                                              
                                                                                
           IF FA40-NUM-PREMIUM-RATE IS NOT NUMERIC                              
              MOVE ZERO TO FA40-NUM-PREMIUM-RATE                                
           ELSE                                                                 
              CONTINUE                                                          
           END-IF.                                                              
                                                                                
           IF FA40-AMT-STAMP-DUTIES IS NOT NUMERIC                              
              MOVE ZERO TO FA40-AMT-STAMP-DUTIES                                
           ELSE                                                                 
              CONTINUE                                                          
           END-IF.                                                              
                                                                                
           WRITE     BP13FA40-TRANS-REC.                                        
           ADD 1 TO  WS-CNT-FA40-WRITE.                                         
                                                                                
           PERFORM 300-READ-BP13KA40 THRU 300-EXIT.                             
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       500-CALL-BP13C913.                                                       
      *------------------*                                                      
           CALL 'BP13C913'     USING  WS-LINK-REC,                              
                                      BP13COMM8-REC.                            
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       600-MOVE-PIDB-DETLS.                                                     
      *-------------------*                                                     
           MOVE COMM8-NUM-MGMT        TO  FA40-CDE-BO.                          
           MOVE COMM8-NUM-ED-GL       TO  FA40-CDE-CC.                          
           MOVE COMM8-NUM-MNTNC       TO  FA40-CDE-TC.                          
           MOVE COMM8-NUM-UNIT-MAIN   TO  WS-NUM-HSE-MAIN.                      
           MOVE COMM8-NUM-UNIT-SUB    TO  WS-NUM-HSE-SUB.                       
           MOVE COMM8-NUM-BLK         TO  FA40-NUM-BLK.                         
           MOVE COMM8-NME-STREET      TO  FA40-NME-STREET.                      
           MOVE COMM8-NUM-LEVEL       TO  FA40-NUM-LEVEL.                       
           MOVE WS-NUM-HSEMAIN        TO  FA40-NUM-MAIN.                        
           MOVE COMM8-NUM-POSTAL-CODE TO  FA40-NUM-POSTAL.                      
           MOVE COMM8-CDE-BUC         TO  FA40-CDE-BUC.                         
                                                                                
           IF   COMM8-QTY-AREA-TOTAL IS NOT NUMERIC                             
                MOVE  ZERO                 TO  FA40-QTY-AREA-STRATA             
           ELSE                                                                 
                MOVE COMM8-QTY-AREA-TOTAL  TO  FA40-QTY-AREA-STRATA             
           END-IF.                                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       900-CONTROL-LIST-KA40.                                                   
      *----------------------*                                                  
           DISPLAY '                                         '.                 
           DISPLAY '*****     BP13CA12 CONTROL TOTAL    *****'.                 
           DISPLAY '                                         '.                 
           DISPLAY '  ***    ------------------------   ***  '.                 
           DISPLAY '  ***      BP13KA40 PROCESSING      ***  '.                 
           DISPLAY '  ***    ------------------------   ***  '.                 
           DISPLAY '                                         '.                 
           DISPLAY 'NO OF KA40 REC READ        = ' WS-CNT-KA40-READ.            
           DISPLAY 'NO OF KA40 REC SKIPPED     = ' WS-CNT-FA40-NON-PIDB.        
           DISPLAY 'NO SCH-ACC FND IN PIDB     = ' WS-CNT-PIDB-FND.             
           DISPLAY 'NO SCH-ACC NOT FND IN PIDB = ' WS-CNT-PIDB-NOTFND.          
           DISPLAY 'NO OF FA40 REC UPDTE       = ' WS-CNT-FA40-WRITE.           
           DISPLAY '                                         '.                 
           DISPLAY '                                         '.                 
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       990-CLOSE-ROUTINE.                                                       
      *------------------*                                                      
           CLOSE BP13KA40                                                       
                 BP13FA40.                                                      
                                                                                
           IF WS-KA40-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13KA40, STATUS = '                       
                   WS-KA40-STATUS                                               
              MOVE WS-KA40-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
