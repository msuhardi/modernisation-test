      *-------------------------------------------------------------*           
       IDENTIFICATION DIVISION.                                                 
      *-------------------------------------------------------------*           
       PROGRAM-ID.    BP13C042.                                                 
      *AUTHOR.        SK.                                                       
      *DATE-WRITTEN.  11/02/2004.                                               
      *-------------------------------------------------------------*           
      * SYSTEM OF COMMITMENT  (BP13)                                *           
      *-------------------------------------------------------------*           
      *                                                             *           
      *  OBJECTIVE   : 1.  LETTER TO INFORM ON CHANGE OF CSM        *           
      *                                                             *           
      *  INPUT FILES : 1.  BP13F022  -  SOC REQ FILE                *           
      *                2.  BP13K820  -  OCCUPIER FILE               *           
      *                3.  BP13K830  -  ADDRESS FILE                *           
      *                4.  BP13K022  -  OIC FILE                    *           
      *                5.  BP13K024  -  OIC MASTER FILE             *           
      *                6.  BP13K800  -  SOC MASTER                  *           
      *                                                             *           
      *  LISTINGS    : 1.  BP13P042  - LETTR                        *           
      *                2.  BP13L042  - LISTING                      *           
      *                                                             *           
      *  FORM ID     : 1.  BP13UF                                   *           
      *                                                             *           
      *-------------------------------------------------------------*           
      * CHG REF  BY    DATE    DESCRIPTION                          *           
      * -------- ---- -------- -----------                          *           
      * BP132480 SK   20040211 NEW PGM                              *           
      *-------------------------------------------------------------*           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *-------------------------------------------------------------*           
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------*           
           SELECT BP13F022  ASSIGN       TO BP13F022.                           
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K820-KEY-FLD                        
                            FILE STATUS  IS WS-K820-STATUS.                     
                                                                                
           SELECT BP13K830  ASSIGN       TO BP13K830                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K830-KEY-FLD                        
                            ALTERNATE RECORD KEY IS  K830-NUM-REGN              
                            FILE STATUS  IS WS-K830-STATUS.                     
                                                                                
           SELECT BP13K022 ASSIGN TO BP13K022                                   
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K022-KEY-FLD                        
                           ALTERNATE KEY IS K022-NUM-REGN                       
                           FILE STATUS   IS WS-K022-STATUS.                     
                                                                                
           SELECT BP13K024 ASSIGN TO BP13K024                                   
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K024-KEY-FLD                        
                           FILE STATUS   IS WS-K024-STATUS.                     
                                                                                
           SELECT BP13P042  ASSIGN       TO BP13P042.                           
           SELECT BP13L042  ASSIGN       TO BP13L042.                           
                                                                                
      *-------------------------------------------------------------*           
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------*           
                                                                                
       FD   BP13F022                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 100  CHARACTERS.                                    
       COPY BP13F022.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K024                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K024.                                                           
                                                                                
       FD  BP13P042                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  P042-PRT-REC                     PIC X(90).                          
                                                                                
       FD   BP13L042                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       01   BP13L042-REC     PIC X(132).                                        
                                                                                
      *-------------------------------------------------------------*           
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------*           
                                                                                
       01  WS-STATUS.                                                           
           05  WS-K800-STATUS               PIC 99    VALUE ZEROS.              
           05  WS-K820-STATUS               PIC 99    VALUE ZEROS.              
           05  WS-K830-STATUS               PIC 99    VALUE ZEROS.              
           05  WS-K022-STATUS               PIC 99    VALUE ZEROS.              
           05  WS-K024-STATUS               PIC 99    VALUE ZEROS.              
                                                                                
       01  WS-F022-READ                     PIC 9(6)  VALUE ZEROS.              
       01  WS-P042-PRINTED                  PIC 9(6)  VALUE ZEROS.              
       01  WS-DATE                          PIC X(8)  VALUE SPACES.             
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F022-EOF                  PIC X(01) VALUE 'N'.                
           05  WS-K820-NME-OCCP             PIC X(66) VALUE SPACES.             
           05  WS-K800-NUM-REGN             PIC X(08) VALUE SPACES.             
           05  WS-OIC-NAME                  PIC X(30) VALUE SPACES.             
           05  WS-OIC-PHHP                  PIC X(08) VALUE SPACES.             
           05  WS-OIC-PHOF                  PIC X(08) VALUE SPACES.             
           05  WS-NUM-OIC                   PIC X(05) VALUE SPACES.             
                                                                                
           05  WS-CNT-LINE-ON-PAGE          PIC 9(8)   VALUE 66.                
           05  WS-CNT-PRINT-L042            PIC 9(8)   VALUE 1.                 
           05  WS-CNT-WRITE-L042            PIC 9(8)   VALUE 0.                 
           05  WS-NO-OF-PAGE                PIC 9(8)   VALUE 0.                 
                                                                                
       COPY P13COMM8.                                                           
       COPY AV02COMM.                                                           
                                                                                
      *-------------------------------------------------------------*           
      *   LINKAGE SECTION FOR BP13C913                                          
      *-------------------------------------------------------------*           
                                                                                
       01  WS-LINK-BP13C913.                                                    
           05  LINK-NUM-SCH-ACC.                                                
               10  LINK-NUM-SCH             PIC X(4).                           
               10  LINK-NUM-ACC             PIC X(5).                           
                                                                                
      *-------------------------------------------------------------*           
      *   LINKAGE SECTION FOR AV02C015                                          
      *-------------------------------------------------------------*           
                                                                                
       01  WS-LINK-AV02C015.                                                    
           05  AV02C015-IN-POSTCODE             PIC X(6).                       
           05  AV02C015-OUT-POSTCODE-CHKSUM     PIC X(9).                       
                                                                                
      *-------------------------------------------------------------*           
      *    LAYOUT FOR LETTER                                                    
      *-------------------------------------------------------------*           
                                                                                
       01  P042-LINE-01.                                                        
           05  FILLER                   PIC X(72) VALUE SPACES.                 
           05  P042-REF                 PIC X(15) VALUE SPACES.                 
                                                                                
       01  P042-LINE-02.                                                        
           05  FILLER                   PIC X(72) VALUE SPACES.                 
           05  P042-DATE                PIC X(10) VALUE SPACES.                 
                                                                                
       01  P042-LINE-03.                                                        
           05  FILLER                   PIC X(12) VALUE SPACES.                 
           05  P042-NAME01              PIC X(66) VALUE SPACES.                 
                                                                                
       01  P042-LINE-04.                                                        
           05  FILLER                   PIC X(12) VALUE SPACES.                 
           05  P042-NAME02              PIC X(66) VALUE SPACES.                 
                                                                                
       01  P042-LINE-05.                                                        
           05  FILLER                   PIC X(12) VALUE SPACES.                 
           05  P042-BLK                 PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  P042-UNIT                PIC X(11) VALUE SPACES.                 
                                                                                
       01  P042-LINE-06.                                                        
           05  FILLER                   PIC X(12) VALUE SPACES.                 
           05  P042-ADDR-L2             PIC X(47) VALUE SPACES.                 
                                                                                
       01  P042-LINE-07.                                                        
           05  FILLER                   PIC X(12) VALUE SPACES.                 
           05  P042-ADDR-L3             PIC X(20) VALUE SPACES.                 
                                                                                
       01  P042-LINE-08.                                                        
           05  FILLER                   PIC X(10) VALUE SPACES.                 
           05  P042-BAR-CODE            PIC X(9)  VALUE SPACES.                 
                                                                                
       01  P042-LINE-08B.                                                       
           05  FILLER                   PIC X(13) VALUE SPACES.                 
           05  P042-FLAT-ADDR           PIC X(49) VALUE SPACES.                 
                                                                                
       01  P042-LINE-9.                                                         
           05  FILLER                   PIC X(10) VALUE SPACES.                 
           05  P042-PREV-CSM1           PIC X(30) VALUE SPACES.                 
                                                                                
       01  P042-LINE-10.                                                        
           05  FILLER                   PIC X(58) VALUE SPACES.                 
           05  P042-PREV-CSM2           PIC X(30) VALUE SPACES.                 
                                                                                
       01  P042-LINE-12.                                                        
           05  FILLER                   PIC X(67) VALUE SPACES.                 
           05  P042-OIC-PFOFF           PIC X(8)  VALUE SPACES.                 
                                                                                
       01  P042-LINE-13.                                                        
           05  FILLER                   PIC X(14) VALUE SPACES.                 
           05  P042-OIC-PFHP            PIC X(8)  VALUE SPACES.                 
                                                                                
       01  P042-LINE-14.                                                        
           05  FILLER                   PIC X(10) VALUE SPACES.                 
           05  P042-OIC-NME             PIC X(30) VALUE SPACES.                 
                                                                                
      *-------------------------------------------------------------*           
      *    BP13L042 - LAYOUT                                        *           
      *-------------------------------------------------------------*           
                                                                                
       01  L042-PR-HEAD-01.                                                     
           05  FILLER                   PIC X(14) VALUE 'BP13L042'.             
           05  FILLER                   PIC X(8)  VALUE 'HDBCAT 3'.             
           05  FILLER                   PIC X(25) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(13) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  L042-PRINT-DATE          PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'PAGE: '.               
           05  L042-PRINT-PAGE          PIC ZZZ9.                               
                                                                                
       01  L042-PR-HEAD-02.                                                     
           05  FILLER                   PIC X(55) VALUE SPACES.                 
           05  FILLER                   PIC X(75) VALUE                         
                'RE-ASSIGNING CSM'.                                             
                                                                                
       01  L042-PR-HEAD-03.                                                     
           05  FILLER           PIC X(05)  VALUE ' S-NO'.                       
           05  FILLER           PIC X(01)  VALUE SPACES.                        
           05  FILLER           PIC X(10)  VALUE 'REGN NO'.                     
           05  FILLER           PIC X(12)  VALUE 'FROM OIC'.                    
           05  FILLER           PIC X(03)  VALUE SPACES.                        
           05  FILLER           PIC X(08)  VALUE 'TO OIC'.                      
           05  FILLER           PIC X(03)  VALUE SPACES.                        
           05  FILLER           PIC X(08)  VALUE 'NEW TWN'.                     
           05  FILLER           PIC X(03)  VALUE SPACES.                        
           05  FILLER           PIC X(09)  VALUE 'FLAT TYPE'.                   
                                                                                
       01  L042-PR-HEAD-04.                                                     
           05  FILLER                PIC X(132) VALUE ALL '-'.                  
                                                                                
       01  L042-PR-DETAILS.                                                     
           05  L042-NUMBER           PIC ZZZZ9.                                 
           05  FILLER                PIC X(01)  VALUE SPACES.                   
           05  L042-NUM-REGN         PIC X(10)  VALUE SPACES.                   
           05  L042-FROM-OIC         PIC X(12)  VALUE SPACES.                   
           05  FILLER                PIC X(06)  VALUE SPACES.                   
           05  L042-TO-OIC           PIC X(05)  VALUE SPACES.                   
           05  FILLER                PIC X(06)  VALUE SPACES.                   
           05  L042-NT               PIC X(03)  VALUE SPACES.                   
           05  FILLER                PIC X(09)  VALUE SPACES.                   
           05  L042-FT               PIC X(03)  VALUE SPACES.                   
                                                                                
      *-------------------------------------------------------------*           
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------*           
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------*           
                                                                                
           PERFORM 1000-OPEN-FILES             THRU 1000-EXIT.                  
           PERFORM 2000-READ-BP13F022          THRU 2000-EXIT                   
           PERFORM 3000-PROCESS-RECORDS        THRU 3000-EXIT                   
                       UNTIL WS-F022-EOF = 'Y'.                                 
           PERFORM 9000-CLOSE-ROUTINE          THRU 9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------*           
                                                                                
           OPEN INPUT BP13F022                                                  
                      BP13K820                                                  
                      BP13K830                                                  
                      BP13K022                                                  
                      BP13K024                                                  
                      BP13K800                                                  
               OUTPUT BP13P042                                                  
                      BP13L042.                                                 
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K800 STATUS IS ' WS-K800-STATUS            
              MOVE     WS-K800-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K820 STATUS IS ' WS-K820-STATUS            
              MOVE     WS-K820-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K830 STATUS IS ' WS-K830-STATUS            
              MOVE     WS-K830-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 OPEN ERROR, STATUS IS ' WS-K022-STATUS          
              MOVE     WS-K022-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           IF WS-K024-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K024 OPEN ERROR, STATUS IS ' WS-K024-STATUS          
              MOVE     WS-K024-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO  P042-DATE                             
           END-STRING.                                                          
                                                                                
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO  L042-PRINT-DATE                       
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       2000-READ-BP13F022.                                                      
      *-------------------------------------------------------------*           
                                                                                
           READ BP13F022                                                        
                AT END MOVE 'Y' TO WS-F022-EOF                                  
                       GO TO 2000-EXIT.                                         
                                                                                
           ADD  1  TO  WS-F022-READ.                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       3000-PROCESS-RECORDS.                                                    
      *-------------------------------------------------------------*           
                                                                                
           PERFORM 4000-READ-K800     THRU 4000-EXIT.                           
           IF K800-NUM-STATUS NOT = 'C'                                         
              PERFORM 4500-MOVE-DETAILS  THRU 4500-EXIT                         
              PERFORM 7000-GET-PIDB-INFO THRU 7000-EXIT                         
              PERFORM 8000-PRINT-LETTER  THRU 8000-EXIT                         
              PERFORM 7500-PRINT-REPORT  THRU 7500-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F022 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       4000-READ-K800.                                                          
      *-------------------------------------------------------------*           
                                                                                
           MOVE SPACES         TO K800-NUM-REGN.                                
           MOVE F022-NUM-REGN  TO K800-NUM-REGN.                                
                                                                                
           READ BP13K800.                                                       
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN 23                                                           
                 DISPLAY 'RECORD NOT FOUND IN BP13K800, KEY IS '                
                          K800-NUM-REGN                                         
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K800, STATUS '                      
                       WS-K800-STATUS                                           
                 MOVE WS-K800-STATUS TO RETURN-CODE                             
                 PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       4500-MOVE-DETAILS.                                                       
      *-------------------------------------------------------------*           
                                                                                
           MOVE SPACES                TO P042-REF                               
                                         P042-NAME01                            
                                         P042-NAME02                            
                                         P042-PREV-CSM1                         
                                         P042-PREV-CSM2                         
                                         P042-OIC-NME                           
                                         P042-OIC-PFHP                          
                                         P042-OIC-PFOFF                         
                                         P042-FLAT-ADDR                         
                                         BP13L042-REC.                          
           INITIALIZE                    BP13L042-REC.                          
                                                                                
           MOVE SPACES                TO K820-KEY-FLD.                          
           MOVE K800-NUM-REGN         TO K820-NUM-REGN L042-NUM-REGN.           
           MOVE K800-NUM-NRIC1        TO K820-NUM-NRIC.                         
           MOVE K800-NUM-NEW-TOWN     TO L042-NT.                               
           MOVE K800-NUM-FLAT-TYPE    TO L042-FT.                               
                                                                                
           PERFORM  4600-READ-K820      THRU 4600-EXIT.                         
           MOVE     WS-K820-NME-OCCP    TO P042-NAME01.                         
                                                                                
           IF K800-NUM-NRIC2 (2:1) = '#' OR SPACES OR LOW-VALUES                
              MOVE SPACES                 TO    P042-NAME02                     
           ELSE                                                                 
              MOVE SPACES                    TO K820-KEY-FLD                    
              MOVE K800-NUM-REGN             TO K820-NUM-REGN                   
              MOVE K800-NUM-NRIC2            TO K820-NUM-NRIC                   
              PERFORM  4600-READ-K820        THRU 4600-EXIT                     
              MOVE     WS-K820-NME-OCCP      TO P042-NAME02                     
           END-IF.                                                              
                                                                                
           PERFORM  5000-GET-ADDR      THRU  5000-EXIT.                         
                                                                                
           MOVE F022-NUM-OIC TO WS-NUM-OIC L042-FROM-OIC.                       
           PERFORM  6500-READ-BP13K024 THRU  6500-EXIT.                         
           MOVE WS-OIC-NAME           TO P042-PREV-CSM1                         
                                         P042-PREV-CSM2.                        
                                                                                
           PERFORM  6000-GET-NEW-CSM   THRU  6000-EXIT.                         
           MOVE     WS-NUM-OIC         TO L042-TO-OIC.                          
           MOVE     WS-OIC-NAME        TO P042-OIC-NME.                         
           MOVE     WS-OIC-PHHP        TO P042-OIC-PFHP.                        
           MOVE     WS-OIC-PHOF        TO P042-OIC-PFOFF.                       
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       4600-READ-K820.                                                          
      *-------------------------------------------------------------*           
                                                                                
           MOVE SPACES                TO  WS-K820-NME-OCCP.                     
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00 OR 02                                         
              MOVE K820-NME-OCCP      TO  WS-K820-NME-OCCP                      
           ELSE                                                                 
           IF WS-K820-STATUS = 23                                               
              DISPLAY 'K820 REC NOT FOUND : ' K820-KEY-FLD                      
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K820, STATUS ' WS-K820-STATUS          
                      ',  K820-KEY-FLD = '   K820-KEY-FLD                       
              MOVE WS-K820-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                      
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       5000-GET-ADDR.                                                           
      *-------------------------------------------------------------*           
                                                                                
           MOVE SPACES            TO P042-LINE-05                               
                                     P042-LINE-06                               
                                     P042-LINE-07                               
                                     P042-LINE-08.                              
           MOVE SPACES            TO K830-NUM-REGN.                             
                                                                                
           MOVE K800-NUM-REGN     TO K830-NUM-REGN.                             
           READ BP13K830 KEY IS K830-NUM-REGN.                                  
                                                                                
           IF WS-K830-STATUS = 00                                               
              IF ((K830-NUM-MAIL-ADDR =  SPACES OR LOW-VALUES) OR               
                 (K830-NME-MAIL-STR-BLDG(1:47) = SPACES))                       
                 PERFORM 5200-GET-RES-ADDR THRU 5200-EXIT                       
              ELSE                                                              
                 PERFORM 5400-GET-MAIL-ADDR THRU 5400-EXIT                      
              END-IF                                                            
           ELSE                                                                 
           IF WS-K830-STATUS = 23                                               
              DISPLAY 'BP13K830 RECORD NOT FOUND = ' WS-K830-STATUS             
                      ',  K830-KEY-FLD IS ' K830-KEY-FLD                        
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K830 = ' WS-K830-STATUS                
                      ',  K830-KEY-FLD IS ' K830-KEY-FLD                        
              MOVE WS-K830-STATUS TO RETURN-CODE                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       5200-GET-RES-ADDR.                                                       
      *-------------------------------------------------------------*           
                                                                                
           MOVE K830-NUM-BLK         TO P042-BLK.                               
                                                                                
           IF K830-NUM-LVL-NUM = SPACES AND                                     
              K830-NUM-HSEMAIN = SPACES                                         
              MOVE SPACES            TO P042-UNIT                               
           ELSE                                                                 
              MOVE K830-NUM-FLAT     TO P042-UNIT                               
           END-IF.                                                              
                                                                                
           MOVE K830-NME-STREET-BLDG(1:47) TO P042-ADDR-L2.                     
                                                                                
           IF K830-NUM-POSTAL = SPACES OR LOW-VALUES OR ZEROES                  
              MOVE SPACES            TO P042-ADDR-L3                            
              DISPLAY 'RES POSTAL CODE EMPTY FOR ' K830-KEY-FLD                 
           ELSE                                                                 
              STRING 'SINGAPORE '  K830-NUM-POSTAL                              
                 DELIMITED BY SIZE INTO P042-ADDR-L3                            
              MOVE SPACES                    TO WS-LINK-AV02C015                
              MOVE K830-NUM-POSTAL           TO AV02C015-IN-POSTCODE            
              PERFORM 5800-CALL-AV02C015     THRU 5800-EXIT                     
              MOVE AV02C015-OUT-POSTCODE-CHKSUM TO P042-BAR-CODE                
           END-IF.                                                              
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       5400-GET-MAIL-ADDR.                                                      
      *-------------------------------------------------------------*           
                                                                                
           MOVE K830-NUM-MAIL-BLK         TO P042-BLK.                          
                                                                                
           IF K830-NUM-MAIL-LVL-NUM = SPACES AND                                
              K830-NUM-MAIL-HSEMAIN = SPACES                                    
              MOVE SPACES                 TO P042-UNIT                          
           ELSE                                                                 
              MOVE K830-NUM-MAIL-FLAT     TO P042-UNIT                          
           END-IF.                                                              
                                                                                
           MOVE K830-NME-MAIL-STR-BLDG(1:47) TO P042-ADDR-L2.                   
                                                                                
           IF K830-NUM-MAIL-POSTAL = SPACES OR LOW-VALUES OR ZEROS              
              MOVE SPACES                TO P042-ADDR-L3                        
           ELSE                                                                 
              STRING 'SINGAPORE '      K830-NUM-MAIL-POSTAL                     
                     DELIMITED BY SIZE INTO P042-ADDR-L3                        
              MOVE SPACES                    TO WS-LINK-AV02C015                
              MOVE K830-NUM-MAIL-POSTAL      TO AV02C015-IN-POSTCODE            
              PERFORM 5800-CALL-AV02C015     THRU 5800-EXIT                     
              MOVE AV02C015-OUT-POSTCODE-CHKSUM TO P042-BAR-CODE                
           END-IF.                                                              
                                                                                
       5400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       5800-CALL-AV02C015.                                                      
      *-------------------------------------------------------------*           
                                                                                
           CALL 'AV02C015'          USING                                       
                                    AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM.               
       5800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       6000-GET-NEW-CSM.                                                        
      *-------------------------------------------------------------*           
                                                                                
           MOVE SPACES         TO K022-KEY-FLD.                                 
           MOVE F022-NUM-REGN  TO K022-NUM-REGN.                                
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 MOVE K022-NUM-OIC  TO WS-NUM-OIC                               
                 PERFORM 6500-READ-BP13K024  THRU 6500-EXIT                     
              WHEN 23                                                           
                 MOVE SPACES        TO WS-NUM-OIC                               
                 DISPLAY 'OIC NOT FOUND FOR ' K022-NUM-REGN                     
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K022. STATUS IS '                   
                       WS-K022-STATUS                                           
                 PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       6500-READ-BP13K024.                                                      
      *-------------------------------------------------------------*           
                                                                                
           MOVE SPACES        TO WS-OIC-NAME                                    
                                 WS-OIC-PHHP                                    
                                 WS-OIC-PHOF.                                   
                                                                                
           MOVE LOW-VALUES    TO K024-KEY-FLD.                                  
           MOVE WS-NUM-OIC    TO K024-KEY-FLD.                                  
                                                                                
           READ BP13K024.                                                       
                                                                                
           EVALUATE WS-K024-STATUS                                              
              WHEN 00                                                           
                   MOVE K024-NME-OIC  TO  WS-OIC-NAME                           
                   MOVE K024-NUM-HP   TO  WS-OIC-PHHP                           
                   MOVE K024-NUM-DID  TO  WS-OIC-PHOF                           
              WHEN 23                                                           
                   MOVE SPACES        TO  WS-OIC-NAME                           
                                          WS-OIC-PHHP                           
                                          WS-OIC-PHOF                           
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13K024 = ' WS-K024-STATUS             
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       7000-GET-PIDB-INFO.                                                      
      *-------------------------------------------------------------*           
                                                                                
           MOVE K800-NUM-SCH-ACC        TO LINK-NUM-SCH-ACC.                    
           CALL 'BP13C913' USING WS-LINK-BP13C913,                              
                                 BP13COMM8-REC.                                 
           IF COMM8-CDE-SYSERR NOT = 0                                          
              MOVE COMM8-CDE-SYSERR     TO RETURN-CODE                          
              DISPLAY 'ERROR READING PIDB ' COMM8-CDE-SYSERR                    
              MOVE SPACES TO   COMM8-NUM-LEVEL                                  
                               COMM8-NUM-UNIT-MAIN                              
                               COMM8-NUM-UNIT-SUB                               
                               COMM8-NME-STREET                                 
                               COMM8-NUM-POSTAL-CODE                            
                               COMM8-CDE-CLS-DESG                               
                               COMM8-CDE-TYP-DESG                               
                               COMM8-CDE-MODL-DESG                              
                               COMM8-CDE-CONFIG-DESG                            
                               COMM8-CDE-CLS-ACTLUSE                            
                               COMM8-CDE-TYP-ACTLUSE                            
           ELSE                                                                 
              STRING COMM8-NUM-BLK  '  '                                        
                    '#'  COMM8-NUM-LEVEL   '-'                                  
                    COMM8-NUM-UNIT-MAIN    '  ' DELIMITED BY SIZE               
                    COMM8-NME-STREET       DELIMITED BY '   '                   
                    INTO P042-FLAT-ADDR                                         
           END-IF.                                                              
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------*           
       7500-PRINT-REPORT.                                                       
      *-------------------------------------------------------------*           
                                                                                
           IF WS-CNT-LINE-ON-PAGE > 53                                          
              PERFORM 7600-PRINT-HEADING THRU 7600-EXIT                         
              MOVE 1 TO WS-CNT-LINE-ON-PAGE                                     
           END-IF.                                                              
                                                                                
           MOVE WS-CNT-PRINT-L042  TO    L042-NUMBER.                           
                                                                                
           WRITE BP13L042-REC      FROM  L042-PR-DETAILS   AFTER 1.             
                                                                                
           ADD   1                 TO    WS-CNT-LINE-ON-PAGE                    
                                         WS-CNT-PRINT-L042                      
                                         WS-CNT-WRITE-L042.                     
                                                                                
           MOVE  SPACES            TO    L042-PR-DETAILS.                       
                                                                                
       7500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       7600-PRINT-HEADING.                                                      
      *-------------------------------------------------------------*           
                                                                                
           ADD  1                TO     WS-NO-OF-PAGE.                          
           MOVE WS-NO-OF-PAGE    TO     L042-PRINT-PAGE.                        
                                                                                
           MOVE SPACES           TO     BP13L042-REC.                           
                                                                                
           WRITE BP13L042-REC    AFTER  PAGE.                                   
           WRITE BP13L042-REC    FROM   L042-PR-HEAD-01 AFTER 1.                
           WRITE BP13L042-REC    FROM   L042-PR-HEAD-02 AFTER 1.                
           WRITE BP13L042-REC    FROM   L042-PR-HEAD-03 AFTER 2.                
           WRITE BP13L042-REC    FROM   L042-PR-HEAD-04 AFTER 1.                
                                                                                
           MOVE 5  TO WS-CNT-LINE-ON-PAGE.                                      
                                                                                
       7600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       8000-PRINT-LETTER.                                                       
      *-------------------------------------------------------------*           
                                                                                
           STRING K800-NUM-REGN          '/'                                    
                  K800-NUM-NEW-TOWN      '/'                                    
                  K800-NUM-FLAT-TYPE                                            
                  DELIMITED BY SIZE INTO P042-REF.                              
                                                                                
           MOVE  SPACES         TO    P042-PRT-REC.                             
           WRITE P042-PRT-REC    AFTER PAGE.                                    
                                                                                
           WRITE P042-PRT-REC    FROM  P042-LINE-01  AFTER 1.                   
           WRITE P042-PRT-REC    FROM  P042-LINE-02.                            
           WRITE P042-PRT-REC    FROM  P042-LINE-03  AFTER 5.                   
           WRITE P042-PRT-REC    FROM  P042-LINE-04.                            
           WRITE P042-PRT-REC    FROM  P042-LINE-05.                            
           WRITE P042-PRT-REC    FROM  P042-LINE-06.                            
           WRITE P042-PRT-REC    FROM  P042-LINE-07.                            
           WRITE P042-PRT-REC    FROM  P042-LINE-08  AFTER 2.                   
           WRITE P042-PRT-REC    FROM  P042-LINE-08B AFTER 8.                   
           WRITE P042-PRT-REC    FROM  P042-LINE-9   AFTER 9.                   
           WRITE P042-PRT-REC    FROM  P042-LINE-10  AFTER 3.                   
           WRITE P042-PRT-REC    FROM  P042-LINE-12  AFTER 5.                   
           WRITE P042-PRT-REC    FROM  P042-LINE-13  AFTER 1.                   
           WRITE P042-PRT-REC    FROM  P042-LINE-14  AFTER 10.                  
           ADD   1               TO    WS-P042-PRINTED.                         
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------*           
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' - BP13C042 CONTROL TOTALS ------------'.                   
           DISPLAY ' '.                                                         
           DISPLAY ' F022 RECORDS READ   : ' WS-F022-READ.                      
           DISPLAY ' LETTER PRINTED      : ' WS-P042-PRINTED.                   
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F022                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 BP13K022                                                       
                 BP13K024                                                       
                 BP13P042                                                       
                 BP13L042.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR-BP13K800-STAT ' WS-K800-STATUS             
              MOVE     WS-K800-STATUS TO RETURN-CODE.                           
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR-BP13K820-STAT ' WS-K820-STATUS             
              MOVE     WS-K820-STATUS TO RETURN-CODE.                           
                                                                                
           IF WS-K830-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR-BP13K830-STAT ' WS-K830-STATUS             
              MOVE     WS-K830-STATUS TO RETURN-CODE.                           
                                                                                
           IF WS-K022-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR-BP13K022-STAT ' WS-K022-STATUS             
              MOVE     WS-K022-STATUS TO RETURN-CODE.                           
                                                                                
           IF WS-K024-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR-BP13K024-STAT ' WS-K024-STATUS             
              MOVE     WS-K024-STATUS TO RETURN-CODE.                           
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
