       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C746.                                                 
      *AUTHOR.        ED.                                                       
      *DATE-WRITTEN.  29JAN2002.                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  TO PRINT REPORT ON FLAT ALLOCATED UNDER       *                
      *          CCC SCHEME AFTER EACH PIGEONHOLING.           *                
      *                                                        *                
      *     INPUT FILES :                                      *                
      *      1. BP13K765                                       *                
      *      2. BP13F713                                       *                
      *                                                        *                
      *     OUTPUT FILES :                                     *                
      *      1. BP13L746                                       *                
      *                                                        *                
      * CHG-NO   BY  ON        DESCRIPTION                     *                
      * ======== === ========= ===============================*                 
      * BP132089 ED  28JAN2002 NEW PGM.                        *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K765 ASSIGN TO BP13K765                                   
                ACCESS MODE  IS SEQUENTIAL                                      
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K765-KEY-FLD                                    
                FILE STATUS  IS BP13K765-STATUS.                                
                                                                                
           SELECT BP13F713 ASSIGN TO BP13F713.                                  
                                                                                
           SELECT BP13L746 ASSIGN TO BP13L746.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13K765                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13K765.                                                           
                                                                                
       FD   BP13F713                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 20 CHARACTERS                                       
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F713.                                                           
                                                                                
       FD   BP13L746                                                            
            RECORD CONTAINS 133 CHARACTERS                                      
            LABEL RECORD IS OMITTED.                                            
                                                                                
       01   L746-PRINT-REC               PIC X(133).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  BP13K765-STATUS           PIC 9(02) VALUE ZEROS.                 
           05  WS-TIME.                                                         
               10  WS-TIME-HH            PIC 9(02) VALUE 0.                     
               10  WS-TIME-MM            PIC 9(02) VALUE 0.                     
               10  WS-TIME-SS            PIC 9(02) VALUE 0.                     
           05  WS-PREV-BRK               PIC X(05) VALUE SPACES.                
           05  WS-K765-BRK               PIC X(05) VALUE SPACES.                
           05  WS-CNT-K765               PIC 9(05) VALUE 0.                     
           05  WS-CNT-PAGE               PIC 9(04) VALUE 0.                     
           05  WS-CNT-SNO                PIC 9(05) VALUE 1.                     
           05  WS-MAX-LINE               PIC 9(02) VALUE 60.                    
           05  WS-CNT-LINE               PIC 9(02) VALUE 61.                    
           05  WS-CDE-FT                 PIC X(02).                             
           05  WS-CDE-NT                 PIC X(03).                             
           05  WS-K765-EOF               PIC X     VALUE SPACES.                
               88  K765-EOF                        VALUE 'Y'.                   
                                                                                
       01  WS-DTE-CURRENT.                                                      
           05  WS-YY                     PIC X(02).                             
           05  WS-MM                     PIC X(02).                             
           05  WS-DD                     PIC X(02).                             
                                                                                
       01  WS-DTE-TODATE.                                                       
           05  WS-DD                     PIC X(02).                             
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-MM                     PIC X(02).                             
           05  FILLER                    PIC X     VALUE '/'.                   
           05  WS-YY                     PIC X(02).                             
                                                                                
      ******************* REPORT LAYOUT *****************                       
                                                                                
       01  L746-HEAD1.                                                          
           05  FILLER                    PIC X(08) VALUE 'BP13L746'.            
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'HDBCAT3'.             
           05  FILLER                    PIC X(19) VALUE SPACES.                
           05  FILLER                    PIC X(42) VALUE                        
               'S Y S T E M     O F    C O M M I T M E N T'.                    
           05  FILLER                    PIC X(10) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'DATE : '.             
           05  L746-DTE-CURRENT          PIC X(08).                             
           05  FILLER                    PIC X(02) VALUE ' ('.                  
           05  L746-TME-HH               PIC 9(02).                             
           05  FILLER                    PIC X(01) VALUE ':'.                   
           05  L746-TME-MM               PIC 9(02).                             
           05  FILLER                    PIC X(02) VALUE ') '.                  
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'PAGE : '.             
           05  L746-CNT-PAGE             PIC ZZZ9.                              
                                                                                
       01  L746-HEAD2.                                                          
           05  FILLER                    PIC X(100) VALUE SPACES.               
           05  FILLER                    PIC X(23) VALUE                        
               'USER (APPLICATION TEAM)'.                                       
                                                                                
       01  L746-HEAD3.                                                          
           05  FILLER                    PIC X(39) VALUE SPACES.                
           05  FILLER                    PIC X(49) VALUE                        
               'REPORT ON FLAT ALLOTED UNDER CCC SCHEME'.                       
           05  FILLER                    PIC X(26) VALUE SPACES.                
           05  FILLER                    PIC X(10) VALUE                        
               'PROCESS : '.                                                    
           05  L746-HEAD-PROCESS         PIC X(15).                             
                                                                                
       01  L746-HEAD4.                                                          
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  FILLER                    PIC X(05) VALUE 'S/NO.'.               
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  FILLER                    PIC X(03) VALUE 'NT '.                 
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  FILLER                    PIC X(02) VALUE 'FT'.                  
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  FILLER                    PIC X(04) VALUE 'WARD'.                
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(08) VALUE '  CAT1  '.            
           05  FILLER                    PIC X(06) VALUE SPACES.                
           05  FILLER                    PIC X(08) VALUE '  CAT2  '.            
           05  FILLER                    PIC X(06) VALUE SPACES.                
           05  FILLER                    PIC X(09) VALUE '  CAT3   '.           
           05  FILLER                    PIC X(06) VALUE SPACES.                
           05  FILLER                    PIC X(07) VALUE 'SERVING'.             
           05  FILLER                    PIC X(06) VALUE SPACES.                
           05  FILLER                    PIC X(08) VALUE 'NON-SRV '.            
                                                                                
       01  L746-HEAD4B.                                                         
           05  FILLER                    PIC X(31) VALUE SPACES.                
           05  FILLER                    PIC X(09) VALUE 'FLAT  REG'.           
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(09) VALUE 'FLAT  REG'.           
           05  FILLER                    PIC X(06) VALUE SPACES.                
           05  FILLER                    PIC X(09) VALUE 'FLAT  REG'.           
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  FILLER                    PIC X(09) VALUE 'FLAT  REG'.           
           05  FILLER                    PIC X(05) VALUE SPACES.                
           05  FILLER                    PIC X(09) VALUE 'FLAT  REG'.           
                                                                                
       01  L746-DETAIL1.                                                        
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L746-SNO                  PIC X(05).                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L746-NT                   PIC X(03).                             
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  L746-FT                   PIC X(02).                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L746-WARD                 PIC X(03).                             
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  L746-CAT1-FLAT            PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L746-CAT1-REG             PIC ZZZZ9.                             
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  L746-CAT2-FLAT            PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L746-CAT2-REG             PIC ZZZZ9.                             
           05  FILLER                    PIC X(04) VALUE SPACES.                
           05  L746-CAT3-FLAT            PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L746-CAT3-REG             PIC ZZZZ9.                             
           05  FILLER                    PIC X(02) VALUE SPACES.                
           05  L746-SERVE-FLAT           PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L746-SERVE-REG            PIC ZZZZ9.                             
           05  FILLER                    PIC X(03) VALUE SPACES.                
           05  L746-NON-SERVE-FLAT       PIC ZZZZ9.                             
           05  FILLER                    PIC X(01) VALUE SPACES.                
           05  L746-NON-SERVE-REG        PIC ZZZZ9.                             
                                                                                
       01  WS-BLANK-LINE                 PIC X(133) VALUE SPACES.               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-ROUTINE.                                                       
                                                                                
           PERFORM 1000-OPEN-FILES                                              
              THRU 1000-EXIT.                                                   
                                                                                
           PERFORM 2000-READ-BP13F713                                           
              THRU 2000-EXIT.                                                   
                                                                                
           PERFORM 3000-READ-BP13K765                                           
              THRU 3000-EXIT.                                                   
                                                                                
           PERFORM 4000-MAIN-ROUTINE                                            
              THRU 4000-EXIT                                                    
             UNTIL K765-EOF.                                                    
                                                                                
           PERFORM 9000-CLOSE-ROUTINE                                           
              THRU 9000-EXIT.                                                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
                                                                                
           OPEN INPUT  BP13K765                                                 
                       BP13F713                                                 
                OUTPUT BP13L746.                                                
                                                                                
           IF BP13K765-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K765,STATUS : '                        
                        BP13K765-STATUS                                         
              MOVE BP13K765-STATUS       TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE                                        
                 THRU 9000-EXIT.                                                
                                                                                
           ACCEPT WS-DTE-CURRENT         FROM DATE.                             
           MOVE CORR WS-DTE-CURRENT        TO WS-DTE-TODATE.                    
           MOVE WS-DTE-TODATE              TO L746-DTE-CURRENT.                 
                                                                                
           ACCEPT  WS-TIME                 FROM TIME.                           
           MOVE WS-TIME-HH                 TO L746-TME-HH.                      
           MOVE WS-TIME-MM                 TO L746-TME-MM.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F713.                                                      
                                                                                
           READ BP13F713                AT END                                  
           GO TO 2000-EXIT.                                                     
                                                                                
            EVALUATE  F713-CDE-COHORT                                           
            WHEN 'C1'   MOVE 'ROUND 1'            TO L746-HEAD-PROCESS          
            WHEN 'C2'   MOVE 'ROUND 2'            TO L746-HEAD-PROCESS          
            WHEN OTHER  MOVE '               '    TO L746-HEAD-PROCESS          
            END-EVALUATE.                                                       
                                                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-BP13K765.                                                      
                                                                                
            READ BP13K765 NEXT           AT END                                 
            MOVE 'Y'                     TO WS-K765-EOF                         
            GO TO 3000-EXIT.                                                    
                                                                                
            ADD 1                        TO WS-CNT-K765.                        
            MOVE K765-KEY-FLD(1:5)       TO WS-K765-BRK.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-MAIN-ROUTINE.                                                       
                                                                                
           IF  WS-CNT-LINE  >=  WS-MAX-LINE                                     
               ADD 1                     TO WS-CNT-PAGE                         
               PERFORM 5000-PRINT-HEADING                                       
                  THRU 5000-EXIT                                                
               MOVE 7                    TO WS-CNT-LINE                         
           END-IF.                                                              
                                                                                
                                                                                
           PERFORM 4100-MOVE-VALUES      THRU 4100-EXIT.                        
                                                                                
           IF WS-K765-BRK = WS-PREV-BRK                                         
             MOVE SPACES            TO L746-SNO  L746-NT  L746-FT               
           ELSE                                                                 
             ADD 1                     TO WS-CNT-SNO                            
             MOVE WS-K765-BRK          TO WS-PREV-BRK                           
             WRITE L746-PRINT-REC    FROM WS-BLANK-LINE                         
             ADD 1                     TO WS-CNT-LINE                           
           END-IF.                                                              
                                                                                
           WRITE L746-PRINT-REC        FROM L746-DETAIL1.                       
           ADD 1                       TO WS-CNT-LINE.                          
                                                                                
           PERFORM 3000-READ-BP13K765                                           
              THRU 3000-EXIT.                                                   
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4100-MOVE-VALUES.                                                        
                                                                                
           MOVE WS-CNT-SNO                     TO L746-SNO.                     
           MOVE K765-CDE-NT                    TO L746-NT.                      
           MOVE K765-CDE-FLAT-TY               TO L746-FT.                      
           MOVE K765-CDE-WARD                  TO L746-WARD.                    
           MOVE K765-NUM-FLAT-CAT1             TO L746-CAT1-FLAT.               
           MOVE K765-NUM-REG-CAT1              TO L746-CAT1-REG.                
           MOVE K765-NUM-FLAT-CAT2             TO L746-CAT2-FLAT.               
           MOVE K765-NUM-REG-CAT2              TO L746-CAT2-REG.                
           MOVE K765-NUM-FLAT-CAT3             TO L746-CAT3-FLAT.               
           MOVE K765-NUM-REG-CAT3              TO L746-CAT3-REG.                
           MOVE K765-NUM-FLAT-SERVE            TO L746-SERVE-FLAT.              
           MOVE K765-NUM-REG-SERVE             TO L746-SERVE-REG.               
           MOVE K765-NUM-FLAT-NONSERVE         TO L746-NON-SERVE-FLAT.          
           MOVE K765-NUM-REG-NONSERVE          TO L746-NON-SERVE-REG.           
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-PRINT-HEADING.                                                      
                                                                                
           MOVE WS-CNT-PAGE              TO L746-CNT-PAGE.                      
                                                                                
           WRITE L746-PRINT-REC        FROM L746-HEAD1 AFTER PAGE.              
           WRITE L746-PRINT-REC        FROM L746-HEAD2.                         
           WRITE L746-PRINT-REC        FROM L746-HEAD3.                         
           WRITE L746-PRINT-REC        FROM L746-HEAD4 AFTER 1.                 
           WRITE L746-PRINT-REC        FROM L746-HEAD4B.                        
           MOVE  SPACES                  TO L746-PRINT-REC.                     
           WRITE L746-PRINT-REC.                                                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
                                                                                
           CLOSE BP13K765                                                       
                 BP13F713                                                       
                 BP13L746.                                                      
                                                                                
           DISPLAY '*************** BP13C746 ***************'.                  
           DISPLAY ' '.                                                         
           DISPLAY 'NO. OF RECORDS READ FROM BP13K765 : ' WS-CNT-K765.          
           DISPLAY ' '.                                                         
                                                                                
           IF BP13K765-STATUS NOT = 0                                           
              DISPLAY '***  CLOSING ERROR, BP13K765-STATUS '                    
              BP13K765-STATUS.                                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
