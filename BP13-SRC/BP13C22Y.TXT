       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C22Y.                                                 
       AUTHOR.        MTDULLONA.                                                
      *DATE-WRITTEN.  NOV2003.                                                  
      *COPY OF.       BP13C298.                                                 
      *****************************************************************         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *****************************************************************         
      * OBJECTIVES:  TO CALL SUBROUTINES BP13C29A, BP13C29B, BP13C29C,*         
      *              BP13C29D TO PRINT ADJUSTED FLATLIST              *         
      *---------------------------------------------------------------*         
      * CHECK F208-NUM-FORM-TYPE:                                     *         
      *    - 3 (CALLS BP13C29E-FLATLIST FOR WIS - BP134W)             *         
      *    - 6 (CALLS BP13C29E-FLATLIST FOR CASES WITH                *         
      *         F999-NUM-POD-DUE = 'N' AND SPACES)                    *         
      *    - 8 (CALLS BP13C29E-FLATLIST FOR WIS - USING FORM BP13UA)  *         
      *                                                               *         
      * CHG REQ#  DATE        BY    DESCRIPTION                       *         
      * --------  ----------  ----  --------------------------------- *         
      * BP132562  07/10/2005  GAN1  REPLACE F205 WITH F208            *         
      *                             ORIGINAL COPY IN BP13C298         *         
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F208       ASSIGN TO BP13F208.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F208                                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       COPY BP13F208.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-WORK-VARIABLES.                                                   
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-DATE             PIC X(08) VALUE SPACES.              
               10  FILLER                  PIC X(13) VALUE SPACES.              
           05  WS-DISP-DATE.                                                    
               10  WS-DISP-DD              PIC X(02) VALUE SPACES.              
               10  FILLER                  PIC X(01) VALUE '/'.                 
               10  WS-DISP-MM              PIC X(02) VALUE SPACES.              
               10  FILLER                  PIC X(01) VALUE '/'.                 
               10  WS-DISP-CCYY            PIC X(04) VALUE SPACES.              
           05  WS-END-OF-F208-SW           PIC X(01) VALUE SPACE.               
               88  C-END-OF-F208           VALUE 'Y'.                           
           05  WS-READ-CTR                 PIC 9(08) VALUE ZEROES.              
           05  WS-SUBROUTINE               PIC X(08) VALUE SPACES.              
                                                                                
       01  WS-C22Y-PARAMETERS.                                                  
           05  WS-C22Y-FORM-TYPE       PIC X(01) VALUE SPACES.                  
           05  WS-C22Y-NT              PIC X(20) VALUE SPACES.                  
           05  WS-C22Y-FT              PIC X(02) VALUE SPACES.                  
           05  WS-C22Y-RETURN-CODE     PIC 9(04) VALUE ZEROES.                  
                                                                                
       PROCEDURE DIVISION.                                                      
      *--------------------------------*                                        
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
           PERFORM 2000-PROCESS-RECORDS   THRU 2000-EXIT                        
             UNTIL C-END-OF-F208.                                               
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       1000-INITIALIZATION.                                                     
      *--------------------------------*                                        
                                                                                
           OPEN INPUT    BP13F208.                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
           MOVE WS-SYS-DATE(1:4)       TO WS-DISP-CCYY.                         
           MOVE WS-SYS-DATE(5:2)       TO WS-DISP-MM.                           
           MOVE WS-SYS-DATE(7:2)       TO WS-DISP-DD.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2000-PROCESS-RECORDS.                                                    
      *--------------------------------*                                        
                                                                                
           READ BP13F208  AT END                                                
                MOVE 'Y'   TO  WS-END-OF-F208-SW                                
                GO TO 2000-EXIT.                                                
                                                                                
           ADD  1  TO WS-READ-CTR.                                              
                                                                                
           MOVE F208-NUM-NT-ZONE     TO WS-C22Y-NT.                             
           MOVE F208-NUM-FLAT-TYPE   TO WS-C22Y-FT.                             
           MOVE F208-NUM-FORM-TYPE   TO WS-C22Y-FORM-TYPE.                      
           MOVE ZEROES               TO WS-C22Y-RETURN-CODE.                    
           EVALUATE WS-C22Y-FORM-TYPE                                           
              WHEN '3'                                                          
              WHEN '6'                                                          
              WHEN '8'                                                          
                   MOVE 'BP13C29E'   TO WS-SUBROUTINE                           
                   CALL 'BP13C29E'   USING  WS-C22Y-PARAMETERS                  
           END-EVALUATE.                                                        
                                                                                
           IF WS-C22Y-RETURN-CODE NOT = ZEROES                                  
              DISPLAY 'ERROR IN CALLING SUBROUTINE: ' WS-SUBROUTINE             
              MOVE WS-C22Y-RETURN-CODE       TO RETURN-CODE                     
              PERFORM 9000-CLOSE-FILES          THRU 9000-EXIT                  
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
      *--------------------------------*                                        
       9000-CLOSE-FILES.                                                        
      *--------------------------------*                                        
                                                                                
           DISPLAY 'PROGRAM ID   : BP13C22Y'.                                   
           DISPLAY 'CURRENT DATE : ' WS-DISP-DATE.                              
           DISPLAY SPACES.                                                      
           DISPLAY '*=============== COUNTERS ===============*'.                
           DISPLAY 'BP13F208 RECORDS READ    : ' WS-READ-CTR.                   
                                                                                
           CLOSE BP13F208.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
