       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF09.                                                 
      *AUTHOR.        ZARITA DANE SARMIENTO.                                    
      *DATE-WRITTEN.  03/05/10.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  TO CHECK FOR CASES THAT MC SUMB DATE IS     *            
      *              BLANK AND DEFER DUE DATE IS = CURRENT DATE    *            
      *  INPUT FILE :  1. BP13F399                                 *            
      *  OUTPUT FILE:  1. BP13OUT1                                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * BP133681 ZDD 20100429   NEW PROGRAM                        *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F399 ASSIGN TO BP13F399.                                  
                                                                                
            SELECT BP13K022 ASSIGN TO BP13K022                                  
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K022-KEY-FLD                               
                  ALTERNATE KEY   IS K022-NUM-REGN                              
                  FILE STATUS     IS WS-K022-STATUS.                            
                                                                                
                                                                                
            SELECT BP13K820 ASSIGN TO BP13K820                                  
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS WS-K820-STATUS.                            
                                                                                
           SELECT BP13OUT1 ASSIGN TO BP13OUT1.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F399                                                             
           RECORD CONTAINS 800 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F399.                                                           
                                                                                
       FD  BP13OUT1                                                             
           RECORD CONTAINS 60  CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01  BP13OUT1-REC.                                                        
           05  OUT1-CSM        PIC X(05).                                       
           05  OUT1-REGN-NUM   PIC X(08).                                       
           05  OUT1-HA1-UIN    PIC X(09).                                       
           05  OUT1-HA1-NAME   PIC X(30).                                       
           05  OUT1-DEFER-DTE  PIC X(08).                                       
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  NO-F399-READ        PIC 9(5)    VALUE ZEROS.                     
           05  NO-F399-WRITTEN     PIC 9(5)    VALUE ZEROS.                     
           05  WS-BYPASS           PIC 9(5)    VALUE ZEROS.                     
           05  WS-DATE             PIC X(8)    VALUE SPACES.                    
           05  WS-EOF              PIC X(1)    VALUE 'N'.                       
           05  WS-K022-STATUS      PIC 9(2)    VALUE ZEROS.                     
           05  WS-K820-STATUS      PIC 9(2)    VALUE ZEROS.                     
           05  WS-FOUND            PIC X(1)    VALUE 'N'.                       
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
           PERFORM 1000-OPENING-ROUTINE  THRU 1000-EXIT.                        
           PERFORM 2000-READ-F399        THRU 2000-EXIT.                        
           PERFORM 4000-PROCESS-FILES    THRU 4000-EXIT                         
                   UNTIL WS-EOF = 'Y'.                                          
           PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F399                                                 
                I-O    BP13K022                                                 
                       BP13K820                                                 
                OUTPUT BP13OUT1.                                                
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 OPEN ERROR, STATUS IS ' WS-K022-STATUS          
              MOVE WS-K022-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 OPEN ERROR, STATUS IS ' WS-K820-STATUS          
              MOVE WS-K820-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-F399.                                                          
      *-------------------------------------------------------------            
            READ BP13F399 AT END                                                
                 MOVE 'Y' TO WS-EOF                                             
                 GO TO 2000-EXIT.                                               
            ADD 1 TO NO-F399-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
           PERFORM 4200-PROCESS-RECORD  THRU 4200-EXIT                          
           PERFORM 2000-READ-F399 THRU 2000-EXIT.                               
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4200-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
                                                                                
            MOVE SPACES       TO BP13K022-REC                                   
                                 BP13OUT1-REC                                   
                                 BP13K820-REC.                                  
                                                                                
            IF F399-DTE-MC-DEFER <= WS-DATE                                     
               MOVE F399-NUM-REGN            TO OUT1-REGN-NUM                   
                                                K022-NUM-REGN                   
                                                K820-NUM-REGN                   
               MOVE F399-NUM-NRIC            TO OUT1-HA1-UIN                    
                                                K820-NUM-NRIC                   
               PERFORM 4300-READ-BP13K022    THRU 4300-EXIT                     
                 IF WS-FOUND = 'Y'                                              
                    MOVE K022-NUM-OIC        TO OUT1-CSM                        
                 END-IF                                                         
               PERFORM 4400-READ-BP13K820    THRU 4400-EXIT                     
                 IF WS-FOUND = 'Y'                                              
                    MOVE K820-NME-OCCP       TO OUT1-HA1-NAME                   
                 END-IF                                                         
               MOVE F399-DTE-MC-DEFER        TO OUT1-DEFER-DTE                  
               WRITE BP13OUT1-REC                                               
               ADD 1 TO NO-F399-WRITTEN                                         
            ELSE                                                                
               ADD 1 TO WS-BYPASS                                               
            END-IF.                                                             
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4300-READ-BP13K022.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K022 KEY IS K022-NUM-REGN                                   
                                                                                
           EVALUATE WS-K022-STATUS                                              
            WHEN 00                                                             
               MOVE 'Y' TO WS-FOUND                                             
            WHEN 23                                                             
               MOVE 'N' TO WS-FOUND                                             
            WHEN OTHER                                                          
              DISPLAY 'ERROR READING OF BP13K022. STATUS IS '                   
                       WS-K022-STATUS                                           
              MOVE WS-K022-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-EVALUATE.                                                        
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4400-READ-BP13K820.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
            WHEN 00                                                             
               MOVE 'Y' TO WS-FOUND                                             
            WHEN 23                                                             
               MOVE 'N' TO WS-FOUND                                             
            WHEN OTHER                                                          
              DISPLAY 'ERROR READING OF BP13K820. STATUS IS '                   
                       WS-K820-STATUS                                           
              MOVE WS-K820-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           CLOSE BP13OUT1                                                       
                 BP13F399                                                       
                 BP13K022                                                       
                 BP13K820.                                                      
                                                                                
           DISPLAY 'NO. OF F399 REC READ   > ' NO-F399-READ.                    
           DISPLAY 'NO. OF F399 REC WRITTEN> ' NO-F399-WRITTEN.                 
           DISPLAY 'NO. OF F399 REC BYPASS > ' WS-BYPASS.                       
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 CLOSE ERROR, STATUS IS ' WS-K022-STATUS         
              MOVE WS-K022-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 CLOSE ERROR, STATUS IS ' WS-K820-STATUS         
              MOVE WS-K820-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
