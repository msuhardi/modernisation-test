      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13CD3D.                                                 
       AUTHOR.        MARGE ROSALES.                                            
       DATE-WRITTEN.  NOV 03, 2023.                                             
      *===============================================================*         
      * OBJECTIVE  :  PREPARE EMAIL FILES FOR APPT TYPE L3 AND L4     *         
      *               F757-NUM-SALES-MODE BTO -> L3                   *         
      *               F757-NUM-SALES-MODE SBF -> L4                   *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F757                                     *         
      *               2. BP13K857                                     *         
      *                                                               *         
      * OUTPUT FILE:  1. BP13F23A                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP139857 27/07/2023 MRR5 NEW PROGRAM.                         *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F757  ASSIGN        TO BP13F757.                         
                                                                                
           SELECT  BP13K857  ASSIGN        TO BP13K857                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K857-KEY-FLD                      
                             FILE STATUS   IS WS-K857-STATUS.                   
                                                                                
           SELECT  BP13F23A  ASSIGN        TO BP13F23A.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F757                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 4000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F757.                                                           
                                                                                
       FD   BP13K857                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K857.                                                           
                                                                                
       FD   BP13F23A                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       COPY BP13F23A.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
                                                                                
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F757             PIC X(1)  VALUE 'N'.                     
           05  WS-K857-FOUND           PIC X(1)  VALUE 'N'.                     
           05  WS-K857-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F757-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-K857-FND             PIC 9(7)  VALUE ZEROES.                  
           05  WS-K857-NFND            PIC 9(7)  VALUE ZEROES.                  
           05  WS-K857-EMAIL           PIC 9(7)  VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
           05  WS-F23A-WRITE           PIC 9(8)  VALUE ZEROS.                   
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-SYSTEM-DATE          PIC 9(8)  VALUE ZEROES.                  
           05  WS-TYPE-APPMT           PIC X(2)  VALUE SPACES.                  
           05  WS-A                    PIC 9(2)  VALUE ZEROS.                   
                                                                                
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F757      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F757 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F757                                                 
                       BP13K857                                                 
               OUTPUT  BP13F23A.                                                
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K857 - ERROR OPENING : ' WS-K857-STATUS              
              MOVE WS-K857-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE         THRU 9999-EXIT                
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)          TO WS-SYSTEM-DATE.          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F757.                                                      
      ******************************************************************        
                                                                                
           READ BP13F757           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F757                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                    TO WS-F757-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           PERFORM 4000-READ-BP13K857          THRU 4000-EXIT.                  
           IF WS-K857-FOUND = 'Y'                                               
              PERFORM 6000-WRITE-BP13F23A      THRU 6000-EXIT                   
           ELSE                                                                 
              DISPLAY 'NO EMAIL ADDRESS FOUND : ' F757-NUM-REGN                 
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F757                   THRU 2000-EXIT.         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-READ-BP13K857.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                                TO BP13K857-REC.          
           INITIALIZE                                    BP13K857-REC.          
                                                                                
           MOVE 'N'                                   TO WS-K857-FOUND.         
           MOVE F757-NUM-REGN                         TO K857-NUM-REGN.         
                                                                                
           READ BP13K857.                                                       
                                                                                
           EVALUATE WS-K857-STATUS                                              
               WHEN 00                                                          
                    IF K857-NUM-EMAIL NOT = SPACES AND LOW-VALUES               
                       MOVE 'Y'                       TO WS-K857-FOUND          
                    ELSE                                                        
                       MOVE 'N'                       TO WS-K857-FOUND          
                       ADD 1                          TO WS-K857-EMAIL          
                    END-IF                                                      
                                                                                
                    ADD 1                             TO WS-K857-FND            
               WHEN 23                                                          
                    ADD 1                             TO WS-K857-NFND           
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K857 : ' WS-K857-STATUS          
                            ' REGN = ' K857-NUM-REGN                            
                    PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6000-WRITE-BP13F23A.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                        TO BP13F23A-REC.                  
           INITIALIZE                            BP13F23A-REC.                  
                                                                                
           MOVE F757-NUM-REGN                 TO F23A-NUM-REGN.                 
                                                                                
           IF F757-NUM-SALES-MODE = 'BTO'                                       
              MOVE 'L3'                       TO WS-TYPE-APPMT                  
           END-IF.                                                              
           IF F757-NUM-SALES-MODE = 'SBF'                                       
              MOVE 'L4'                       TO WS-TYPE-APPMT                  
           END-IF.                                                              
                                                                                
           MOVE WS-TYPE-APPMT                 TO F23A-TYPE-APPMT.               
           MOVE F757-DTE-BALLOT               TO F23A-DTE-BALLOT.               
           PERFORM VARYING WS-A FROM 1 BY 1 UNTIL WS-A > 50 OR                  
                          K857-NUM-EMAIL(WS-A:1) = LOW-VALUES                   
               MOVE K857-NUM-EMAIL(WS-A:1) TO F23A-NUM-EMAIL(WS-A:1)            
           END-PERFORM                                                          
                                                                                
           WRITE BP13F23A-REC.                                                  
           ADD   1               TO WS-F23A-WRITE.                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CD3D'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY ' NO OF BP13F757 RECORDS READ............. : '               
                    WS-F757-READ.                                               
           DISPLAY ' NO OF BP13K857 RECORDS FOUND............ : '               
                    WS-K857-FND.                                                
           DISPLAY ' NO OF BP13K857 RECORDS NOT FOUND........ : '               
                    WS-K857-NFND.                                               
           DISPLAY ' NO OF BP13K857 RECORDS W/ BLANK EMAIL... : '               
                    WS-K857-EMAIL.                                              
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF BP13F23A REC WRITTEN......... : '                     
                    WS-F23A-WRITE.                                              
                                                                                
           CLOSE    BP13F757                                                    
                    BP13K857                                                    
                    BP13F23A.                                                   
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K857 - ERROR CLOSING : ' WS-K857-STATUS              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
