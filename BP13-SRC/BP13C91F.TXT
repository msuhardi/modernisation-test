       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C91F.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  19/03/2012.                                               
      *===============================================================*         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      *  OBJECTIVE  :  SELECT CHILD UNIT BASED ON PARENT FLAT TYPE    *         
      *                                                               *         
      *  INPUT      :  BP13F10P  BP13.F100.MGPUNIT.PARENT             *         
      *  I-O        :  BP13K115  BP13.K115.MGPSUNIT                   *         
      *                BP13K125  BP13.K125.MGPSUNIT.CHILD             *         
      *  OUTPUT     :  BP13F10C  BP13.F100.MGPUNIT.CHILD              *         
      *                BP13FQ50  BP13.FQ50.GROUNIT                    *         
      *                                                               *         
      *===============================================================*         
      * CHG-REF#  BY  DDMMCCYY DESCRIPTION                            *         
      * -------- ---- -------- -------------------------------------- *         
      * BP134482 SMR2 19032012 NEW PROGRAM                            *         
      * BP134558 SMR2 25052012 WRITE BP13FREM IF BP13K115 & BP13FLE3  *         
      *                        (NT, NEIGH, CNTRCT) IS EQUAL           *         
      * BP134558 SMR2 13062012 FORMAT FQ50-NUM-UNIT                   *         
      * BP134627 SMR2 30082012 ROUND UP SELECTED UNITS FOR CHILD      *         
      * BP135080 SMR2 03102013 CHANGE LOGIC                           *         
      * BP135140 SMR2 11112013 CATER FOR FT COUNT                     *         
      * BP135140 SMR2 30102013 WRITE CHILD UNITS INTO BP13K120        *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F10P  ASSIGN       TO BP13F10P.                           
           SELECT BP13K115  ASSIGN       TO BP13K115                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K115-KEY-FLD                        
                            FILE STATUS  IS WS-K115-STATUS.                     
           SELECT BP13K125  ASSIGN       TO BP13K125                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K125-KEY-FLD                        
                            FILE STATUS  IS WS-K125-STATUS.                     
           SELECT BP13K120  ASSIGN       TO BP13K120                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K120-KEY-FLD                        
                            FILE STATUS  IS WS-K120-STATUS.                     
           SELECT BP13F10C  ASSIGN       TO BP13F10C.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F10P                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 800 CHARACTERS.                                     
       COPY BP13F100.                                                           
                                                                                
       FD   BP13K115                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K115.                                                           
                                                                                
       FD   BP13K125                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K125.                                                           
                                                                                
       FD   BP13K120                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K120.                                                           
                                                                                
       FD   BP13F10C                                                            
            RECORDING MODE   IS F                                               
            RECORD CONTAINS 800 CHARACTERS.                                     
       01   BP13F10C-REC.                                                       
            05  F10C-NUM-HDB-REF           PIC X(11).                           
            05  FILLER                     PIC X(07).                           
            05  F10C-NUM-RANDOM            PIC X(28).                           
            05  F10C-NUM-PRIO              PIC X(01).                           
            05  F10C-NUM-FT-BALLOT         PIC X(02).                           
            05  F10C-NUM-PRIO-UPDATE       PIC X(01).                           
            05  FILLER                     PIC X(05).                           
            05  F10C-NUM-ESTATE            PIC X(03).                           
            05  F10C-NUM-NGHBRD            PIC X(04).                           
            05  F10C-NUM-CNTRCT            PIC X(04).                           
            05  F10C-NUM-BLOCK             PIC X(05).                           
            05  F10C-NUM-TYP-ACTUSE        PIC X(02).                           
            05  F10C-NUM-MODL-DESG         PIC X(02).                           
            05  F10C-NME-STREET            PIC X(32).                           
            05  F10C-NUM-UNIT.                                                  
                10  F10C-NUM-LEVEL         PIC X(02).                           
                10  F10C-NUM-MAIN          PIC X(04).                           
                10  F10C-NUM-SUB           PIC X(03).                           
            05  F10C-NUM-WARD              PIC X(03).                           
            05  F10C-NUM-NT                PIC X(03).                           
            05  F10C-NUM-ALLO-CAT          PIC X(03).                           
            05  F10C-DTE-BALLOT            PIC X(06).                           
            05  F10C-NUM-PARENT-FT         PIC X(02).                           
            05  F10C-NUM-MGPS              PIC X(01).                           
            05  FILLER                     PIC X(666).                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K115-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K125-STATUS              PIC 9(02)   VALUE ZEROES.            
           05  WS-K120-STATUS              PIC 9(02)   VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F10P-READ                PIC 9(08)   VALUE ZEROES.            
           05  WS-K115-UPDATE              PIC 9(08)   VALUE ZEROES.            
           05  WS-K125-UPDATE              PIC 9(08)   VALUE ZEROES.            
           05  WS-K120-WRITE               PIC 9(08)   VALUE ZEROES.            
           05  WS-K120-FND                 PIC 9(08)   VALUE ZEROES.            
           05  WS-F10C-WRITE               PIC 9(08)   VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F10P-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-K125-EOF                 PIC X(01)   VALUE 'N'.               
           05  WS-FLAG-FIRST               PIC X(01)   VALUE 'Y'.               
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-EDT-DATE                 PIC X(10)   VALUE SPACES.            
           05  WS-CUR-DATE                 PIC X(08)   VALUE SPACES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-FIRST-FT                 PIC 9(01)   VALUE ZEROES.            
           05  WS-NUM-UNIT-REQ             PIC 9(02)   VALUE ZEROES.            
           05  WS-EDIT1                    PIC Z(07)9.                          
                                                                                
       01  WS-RIGHT-JUST.                                                       
           05  WS-UNIT                     PIC X(04)   VALUE SPACES.            
           05  WS-UNIT1                    PIC X(04)   VALUE SPACES.            
           05  WS-SUB                      PIC 9(01)   VALUE ZEROES.            
           05  WS-SUB1                     PIC 9(01)   VALUE ZEROES.            
           05  WS-CHECK                    PIC 9(01)   VALUE ZEROES.            
           05  WS-CNT                      PIC 9(01)   VALUE ZEROES.            
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F10P      THRU 2000-EXIT                       
             UNTIL WS-F10P-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  BP13F10P                                                 
                I-O    BP13K115                                                 
                       BP13K125                                                 
                       BP13K120                                                 
                OUTPUT BP13F10C.                                                
                                                                                
           IF WS-K115-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPEN ERROR, BP13K115 STATUS ' WS-K115-STATUS             
              MOVE WS-K115-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K125-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPEN ERROR, BP13K125 STATUS ' WS-K125-STATUS             
              MOVE WS-K125-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K120-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPEN ERROR, BP13K120 STATUS ' WS-K120-STATUS             
              MOVE WS-K120-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CUR-DATE.                      
           STRING WS-CUR-DATE(7:2), '/',                                        
                  WS-CUR-DATE(5:2), '/',                                        
                  WS-CUR-DATE(1:4)  DELIMITED BY SIZE                           
                  INTO WS-EDT-DATE.                                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F10P.                                                      
      *---------------------------------------------------------------*         
           READ BP13F10P AT END                                                 
                MOVE 'Y'                   TO WS-F10P-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F10P-READ.                     
           PERFORM 3000-STARTBR-K125       THRU 3000-EXIT.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-STARTBR-K125.                                                       
      *---------------------------------------------------------------*         
           MOVE SPACES                     TO BP13K125-REC.                     
           INITIALIZE                         BP13K125-REC.                     
                                                                                
           MOVE 'N'                        TO WS-K125-EOF.                      
           MOVE 'Y'                        TO WS-FLAG-FIRST.                    
                                                                                
           MOVE F100-NUM-NT                TO K125-NUM-NT.                      
           MOVE F100-NUM-NGHBRD            TO K125-NUM-NGHBRD.                  
           MOVE F100-NUM-CNTRCT            TO K125-NUM-CNTRCT.                  
           MOVE F100-NUM-BLOCK             TO K125-NUM-BLOCK.                   
           MOVE F100-NUM-LEVEL             TO K125-NUM-LVL.                     
                                                                                
           START BP13K125 KEY >= K125-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K125-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              PERFORM 3100-READNEXT-K125   THRU 3100-EXIT                       
                UNTIL WS-K125-EOF = 'Y'                                         
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'START ERROR BP13K125 : ' WS-K125-STATUS                  
                      ' KEY-FLD : ' K125-KEY-FLD                                
              MOVE WS-K125-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3100-READNEXT-K125.                                                      
      *---------------------------------------------------------------*         
                                                                                
           READ BP13K125 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K125-STATUS                                              
           WHEN ZEROES                                                          
              IF K125-NUM-NT     = F100-NUM-NT AND                              
                 K125-NUM-NGHBRD = F100-NUM-NGHBRD AND                          
                 K125-NUM-CNTRCT = F100-NUM-CNTRCT AND                          
                 K125-NUM-BLOCK  = F100-NUM-BLOCK AND                           
                 K125-NUM-LVL    = F100-NUM-LEVEL                               
                 IF WS-FLAG-FIRST = 'Y'                                         
                    MOVE K125-NUM-FT       TO WS-FIRST-FT                       
                    MOVE 'N'               TO WS-FLAG-FIRST                     
                 END-IF                                                         
                 IF K125-NUM-FT > WS-FIRST-FT                                   
                    MOVE K125-NUM-FT       TO WS-FIRST-FT                       
                 END-IF                                                         
                 IF (K125-NUM-FT = WS-FIRST-FT) AND                             
                    (K125-NUM-PARENT-FT = SPACES OR LOW-VALUES)                 
                    PERFORM 3200-READ-BP13K115 THRU 3200-EXIT                   
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'Y'                  TO WS-K125-EOF                       
              END-IF                                                            
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K125 : ' K125-KEY-FLD            
              MOVE 'Y'                     TO WS-K125-EOF                       
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K125. STATUS IS - '                 
                      WS-K125-STATUS                                            
              MOVE WS-K125-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3200-READ-BP13K115.                                                      
      *---------------------------------------------------------------*         
           MOVE SPACES                     TO BP13K115-REC.                     
           INITIALIZE                         BP13K115-REC.                     
                                                                                
           MOVE K125-NUM-NT                TO K115-NUM-ESTATE.                  
           MOVE K125-NUM-TYP-ACTUSE        TO K115-NUM-TYP-ACTUSE.              
           MOVE K125-NUM-NGHBRD            TO K115-NUM-NGHBRD.                  
           MOVE K125-NUM-CNTRCT            TO K115-NUM-CNTRCT.                  
           MOVE K125-NUM-BLOCK             TO K115-NUM-BLOCK.                   
           MOVE 'C'                        TO K115-NUM-UNIT-TAG.                
           MOVE F100-NUM-TYP-ACTUSE(1:1)   TO K115-NUM-PARENT-FT.               
                                                                                
           READ BP13K115.                                                       
                                                                                
           EVALUATE WS-K115-STATUS                                              
           WHEN ZEROES                                                          
              IF K115-NUM-UNIT-SLCT IS NOT NUMERIC                              
                 MOVE ZEROES               TO K115-NUM-UNIT-SLCT                
              END-IF                                                            
              COMPUTE WS-NUM-UNIT-REQ = ((K115-NUM-UNIT-REQ + 1) / 2)           
              IF WS-NUM-UNIT-REQ > K115-NUM-UNIT-SLCT                           
                 ADD 1                     TO WS-FIRST-FT                       
                 PERFORM 3300-UPD-BP13K115 THRU 3300-EXIT                       
                 PERFORM 3400-UPD-BP13K125 THRU 3400-EXIT                       
                 PERFORM 3500-WRITE-F10C   THRU 3500-EXIT                       
                 PERFORM 3600-WRITE-K120   THRU 3600-EXIT                       
              END-IF                                                            
                                                                                
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K115 : ' K115-KEY-FLD            
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K115 : ' WS-K115-STATUS                
              MOVE WS-K115-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3300-UPD-BP13K115.                                                       
      *---------------------------------------------------------------*         
           ADD 1                           TO K115-NUM-UNIT-SLCT.               
                                                                                
           REWRITE BP13K115-REC.                                                
                                                                                
           EVALUATE WS-K115-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                        TO WS-K115-UPDATE                    
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR REWRITING TO BP13K115. STATUS IS - '               
                      WS-K115-STATUS                                            
              MOVE WS-K115-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3400-UPD-BP13K125.                                                       
      *---------------------------------------------------------------*         
           MOVE F100-NUM-TYP-ACTUSE        TO K125-NUM-PARENT-FT.               
                                                                                
           REWRITE BP13K125-REC.                                                
                                                                                
           EVALUATE WS-K125-STATUS                                              
           WHEN ZEROES                                                          
              ADD 1                        TO WS-K125-UPDATE                    
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR REWRITING TO BP13K125. STATUS IS - '               
                      WS-K125-STATUS                                            
              MOVE WS-K125-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3500-WRITE-F10C.                                                         
      *---------------------------------------------------------------*         
           MOVE SPACES                     TO BP13F10C-REC.                     
           MOVE K125-NUM-HDB-REF           TO F10C-NUM-HDB-REF.                 
           MOVE K125-NUM-RANDOM            TO F10C-NUM-RANDOM.                  
           MOVE K125-NUM-PRIO              TO F10C-NUM-PRIO.                    
           MOVE K125-NUM-FT-BALLOT         TO F10C-NUM-FT-BALLOT.               
           MOVE K125-NUM-PRIO-UPDATE       TO F10C-NUM-PRIO-UPDATE.             
           MOVE K125-NUM-ESTATE            TO F10C-NUM-ESTATE.                  
           MOVE K125-NUM-NGHBRD            TO F10C-NUM-NGHBRD.                  
           MOVE K125-NUM-CNTRCT            TO F10C-NUM-CNTRCT.                  
           MOVE K125-NUM-BLOCK             TO F10C-NUM-BLOCK.                   
           MOVE K125-NUM-TYP-ACTUSE        TO F10C-NUM-TYP-ACTUSE.              
           MOVE K125-NUM-MODL-DESG         TO F10C-NUM-MODL-DESG.               
           MOVE K125-NME-STREET            TO F10C-NME-STREET.                  
           MOVE K125-NUM-UNIT              TO F10C-NUM-UNIT.                    
           MOVE K125-NUM-WARD              TO F10C-NUM-WARD.                    
           MOVE K125-NUM-NT                TO F10C-NUM-NT.                      
           MOVE K125-NUM-ALLO-CAT          TO F10C-NUM-ALLO-CAT.                
           MOVE K125-DTE-BALLOT            TO F10C-DTE-BALLOT.                  
           MOVE K125-NUM-PARENT-FT         TO F10C-NUM-PARENT-FT.               
           MOVE K125-NUM-MGPS              TO F10C-NUM-MGPS.                    
           WRITE BP13F10C-REC.                                                  
           ADD 1                           TO WS-F10C-WRITE.                    
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3600-WRITE-K120.                                                         
      *---------------------------------------------------------------*         
           MOVE SPACE                      TO BP13K120-REC.                     
           MOVE K125-NUM-NT                TO K120-NUM-NT.                      
           MOVE K125-NUM-NGHBRD            TO K120-NUM-NGHBRD.                  
           MOVE K125-NUM-CNTRCT            TO K120-NUM-CNTRCT.                  
           MOVE K125-NUM-BLOCK             TO K120-NUM-BLOCK.                   
           MOVE K125-NUM-FT                TO K120-NUM-FT.                      
           MOVE K125-NUM-RANDOM            TO K120-NUM-RANDOM.                  
           MOVE K125-NUM-LEVEL             TO K120-NUM-LEVEL.                   
           MOVE K125-NUM-MAIN              TO WS-UNIT.                          
           PERFORM 3610-GET-MAIN-UNIT      THRU 3610-EXIT.                      
           MOVE WS-UNIT1                   TO K120-NUM-MAIN.                    
           MOVE K125-NUM-SUB               TO K120-NUM-SUB.                     
           MOVE 'C'                        TO K120-NUM-UNIT-TAG.                
           MOVE K125-NUM-MODL-DESG         TO K120-NUM-MODL-DESG.               
           MOVE K125-NME-STREET            TO K120-NME-STREET.                  
           MOVE K125-NUM-ESTATE            TO K120-NUM-ESTATE.                  
           MOVE K125-NUM-WARD              TO K120-NUM-WARD.                    
           MOVE K125-NUM-ALLO-CAT          TO K120-NUM-ALLO-CAT.                
           MOVE K125-DTE-BALLOT            TO K120-DTE-BALLOT.                  
           MOVE K125-NUM-PARENT-FT         TO K120-NUM-PARENT-FT.               
           MOVE K125-NUM-TYP-ACTUSE        TO K120-NUM-TYP-ACTUSE.              
           MOVE K125-NUM-HDB-REF           TO K120-NUM-HDB-REF.                 
           MOVE K125-NUM-PRIO              TO K120-NUM-PRIO.                    
           MOVE K125-NUM-FT-BALLOT         TO K120-NUM-FT-BALLOT.               
           MOVE K125-NUM-PRIO-UPDATE       TO K120-NUM-PRIO-UPDATE.             
           MOVE WS-CUR-DATE                TO K120-DTE-UPDATE.                  
           ACCEPT K120-TME-UPDATE          FROM TIME.                           
           MOVE 'BP13C91F'                 TO K120-NUM-USERID.                  
                                                                                
           WRITE BP13K120-REC.                                                  
                                                                                
           EVALUATE WS-K120-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              ADD 1                        TO WS-K120-WRITE                     
                                                                                
           WHEN 22                                                              
              ADD 1                        TO WS-K120-FND                       
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR WRITING - BP13K120 : ' WS-K120-STATUS              
              MOVE WS-K120-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3610-GET-MAIN-UNIT.                                                      
      *---------------------------------------------------------------*         
           MOVE SPACES                     TO WS-UNIT1.                         
           MOVE ZEROES                     TO WS-SUB1                           
                                              WS-CHECK.                         
                                                                                
           PERFORM VARYING WS-SUB FROM 1 BY 1                                   
             UNTIL WS-SUB > 4                                                   
              IF WS-UNIT(WS-SUB:1) = SPACES OR LOW-VALUES                       
                 ADD 1                     TO WS-SUB1                           
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           IF WS-SUB1 > 0                                                       
              MOVE SPACES                  TO WS-UNIT1(1:WS-SUB1)               
              COMPUTE WS-CNT = 4 - WS-SUB1                                      
                                                                                
              PERFORM VARYING WS-CHECK FROM 1 BY 1                              
                UNTIL WS-CNT = 0                                                
                 IF WS-UNIT(WS-CHECK:1) NOT = SPACES AND LOW-VALUES             
                    SUBTRACT 1               FROM WS-CNT                        
                    ADD 1                    TO WS-SUB1                         
                    MOVE WS-UNIT(WS-CHECK:1) TO WS-UNIT1(WS-SUB1:1)             
                 END-IF                                                         
              END-PERFORM                                                       
           ELSE                                                                 
              MOVE WS-UNIT                 TO WS-UNIT1                          
           END-IF.                                                              
                                                                                
       3610-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13C91F       ' WS-EDT-DATE.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-F10P-READ               TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS READ FROM BP13F10P  : ' WS-EDIT1.                
           MOVE WS-K115-UPDATE             TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS UPDATED ON BP13K115 : ' WS-EDIT1.                
           MOVE WS-K125-UPDATE             TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS UPDATED ON BP13K125 : ' WS-EDIT1.                
           MOVE WS-K120-WRITE              TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS WRITTEN TO BP13K120 : ' WS-EDIT1.                
           MOVE WS-F10C-WRITE              TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS WRITTEN TO BP13F10C : ' WS-EDIT1.                
           MOVE WS-K120-FND                TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS FOUND FROM BP13K120 : ' WS-EDIT1.                
           DISPLAY '*---------------------------------------------*'.           
                                                                                
           CLOSE   BP13F10P                                                     
                   BP13K115                                                     
                   BP13K125                                                     
                   BP13K120                                                     
                   BP13F10C.                                                    
                                                                                
           IF WS-K115-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSE ERROR, BP13K115 STATUS ' WS-K115-STATUS            
              MOVE WS-K115-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K125-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSE ERROR, BP13K125 STATUS ' WS-K125-STATUS            
              MOVE WS-K125-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF WS-K120-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'CLOSE ERROR, BP13K120 STATUS ' WS-K120-STATUS            
              MOVE WS-K120-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
