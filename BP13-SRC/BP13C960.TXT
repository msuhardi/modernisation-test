       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C960.                                                 
       AUTHOR.        ANABEL N CABOTAJE.                                        
       DATE-WRITTEN.  14/02/2002.                                               
      * ====================================================== *                
      *    SYSTEM NAME : SOC                                   *                
      *    SYSTEM ID   : BP13                                  *                
      *--------------------------------------------------------*                
      * CHG-NO    BY   DATE     DETAILS                        *                
      * BP132255 JF4  20021227  TO GET WARD FROM PIDB_INTERFACE*                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BM06F120 ASSIGN          TO BM06F120.                         
           SELECT BP13F100 ASSIGN          TO BP13F100.                         
           SELECT P13F100A ASSIGN          TO P13F100A.                         
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BM06F120                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 700 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BM06F120.                                                           
                                                                                
       FD  BP13F100                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 800 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  BP13F100-REC.                                                        
           05  WS-F100-KEY.                                                     
               10  WS-F100-SCH-ACCT    PIC X(11).                               
               10  FILLER              PIC X(07).                               
           05  WS-F100-RANDOM-NO       PIC X(28).                               
           05  FILLER                  PIC X(754).                              
                                                                                
       FD  P13F100A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 800 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  P13F100A-REC.                                                        
           05  F100A-SCH-ACCT          PIC X(11).                               
           05  FILLER                  PIC X(07).                               
           05  F100A-RANDOM-NO         PIC X(28).                               
           05  FILLER                  PIC X(09).                               
           05  F100A-ESTATE            PIC X(03).                               
           05  F100A-NEIGHBOURHOOD     PIC X(04).                               
           05  F100A-CONTRACT-NO       PIC X(04).                               
           05  F100A-BLK-NO            PIC X(05).                               
           05  F100A-CDE-TYP-ACTUSE    PIC X(02).                               
           05  F100A-CDE-MODL-DESG     PIC X(02).                               
           05  F100A-STREET-NAME       PIC X(32).                               
           05  F100A-UNIT-NO.                                                   
               10  F100A-LEVEL-NO      PIC X(02).                               
               10  F100A-MAIN-UNIT     PIC X(04).                               
               10  F100A-SUB-UNIT      PIC X(03).                               
           05  F100A-CDE-WARD          PIC X(03).                               
           05  FILLER                  PIC X(681).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-F100-STATUS                 PIC XX    VALUE '00'.                 
       01  WS-READ-F120                   PIC 9(8)  VALUE ZEROES.               
       01  WS-READ-F100                   PIC 9(8)  VALUE ZEROES.               
       01  WS-MATCH                       PIC 9(8)  VALUE ZEROES.               
       01  WS-UNMATCH                     PIC 9(8)  VALUE ZEROES.               
       01  WS-WRITE-F100A                 PIC 9(8)  VALUE ZEROES.               
                                                                                
       01  WS-EOF-F120                    PIC X     VALUE SPACES.               
       01  WS-EOF-F100                    PIC X     VALUE SPACES.               
                                                                                
       01  WS-SCH-ACC-NO                  PIC X(09) VALUE SPACES.               
                                                                                
       01  WS-SQL-CODES.                                                        
         05  WS-SQL-FOUND                 PIC S9(4) COMP VALUE +000.            
         05  WS-SQL-NOT-FOUND             PIC S9(4) COMP VALUE +100.            
                                                                                
           EXEC SQL INCLUDE SQLCA    END-EXEC.                                  
           EXEC SQL INCLUDE P13VINTF END-EXEC.                                  
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       000-MAIN-ROUTINE.                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 150-READ-F100 THRU 150-EXIT.                                 
           PERFORM 175-READ-F120 THRU 175-EXIT.                                 
           PERFORM 200-PROCESS-F100   THRU 200-EXIT                             
                   UNTIL WS-EOF-F100 = 'Y'.                                     
           PERFORM 999-CLOSE-FILE     THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
           OPEN INPUT  BM06F120                                                 
                       BP13F100                                                 
                OUTPUT P13F100A.                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       150-READ-F100.                                                           
           READ BP13F100 AT END                                                 
           MOVE 'Y'                     TO WS-EOF-F100                          
           GO TO 150-EXIT.                                                      
                                                                                
           ADD 1                        TO WS-READ-F100.                        
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
       175-READ-F120.                                                           
           READ BM06F120 AT END                                                 
           MOVE 'Y'                     TO WS-EOF-F120                          
           GO TO 175-EXIT.                                                      
                                                                                
           ADD 1                        TO WS-READ-F120.                        
                                                                                
       175-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-PROCESS-F100.                                                        
           IF WS-F100-SCH-ACCT = F120-SCH-ACC-NO                                
              ADD 1 TO  WS-MATCH                                                
              PERFORM 300-WRITE-P13F100A THRU 300-EXIT                          
              PERFORM 150-READ-F100 THRU 150-EXIT                               
              PERFORM 175-READ-F120 THRU 175-EXIT                               
           ELSE                                                                 
              IF WS-F100-SCH-ACCT < F120-SCH-ACC-NO                             
                 ADD 1 TO WS-UNMATCH                                            
                 PERFORM 150-READ-F100 THRU 150-EXIT                            
              ELSE                                                              
                 ADD 1 TO WS-UNMATCH                                            
                 PERFORM 175-READ-F120 THRU 175-EXIT                            
              END-IF                                                            
           END-IF.                                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-WRITE-P13F100A.                                                      
           MOVE SPACES                   TO P13F100A-REC.                       
           PERFORM 350-READ-PIDB       THRU 350-EXIT.                           
                                                                                
           MOVE WS-F100-SCH-ACCT         TO F100A-SCH-ACCT.                     
           MOVE WS-F100-RANDOM-NO        TO F100A-RANDOM-NO.                    
           MOVE F120-ESTATE              TO F100A-ESTATE.                       
           MOVE F120-NEIGHBOURHOOD       TO F100A-NEIGHBOURHOOD.                
           MOVE F120-CONTRACT-NO         TO F100A-CONTRACT-NO.                  
           MOVE F120-BLK-NO              TO F100A-BLK-NO.                       
           MOVE F120-CDE-TYP-ACTUSE      TO F100A-CDE-TYP-ACTUSE.               
           MOVE F120-CDE-MODL-DESG       TO F100A-CDE-MODL-DESG.                
           MOVE F120-STREET-NAME         TO F100A-STREET-NAME.                  
           MOVE F120-UNIT-NO             TO F100A-UNIT-NO.                      
                                                                                
           WRITE P13F100A-REC.                                                  
           ADD   1                       TO WS-WRITE-F100A.                     
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       350-READ-PIDB.                                                           
           MOVE WS-F100-SCH-ACCT(1:9)    TO WS-SCH-ACC-NO.                      
           EXEC SQL SELECT NUM_CCC                                              
               INTO :DCLPIDB-INTERFACE.NUM-CCC                                  
                    FROM PIDB_INTERFACE                                         
                    WHERE NUM_SCHM_ACNT = :WS-SCH-ACC-NO                        
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
           WHEN 000                                                             
             MOVE NUM-CCC    TO F100A-CDE-WARD                                  
           WHEN 100                                                             
             DISPLAY 'RECORD NOT FOUND IN PIDB TABLE : ' WS-SCH-ACC-NO          
           WHEN OTHER                                                           
             DISPLAY 'ERROR IN READING PIDB INTERFACE TABLE'                    
             GO TO 9900-ABORT-PROCESS                                           
           END-EVALUATE.                                                        
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
       999-CLOSE-FILE.                                                          
           CLOSE BM06F120                                                       
                 BP13F100                                                       
                 P13F100A.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '* ------------BP13C960------------- * '.                    
           DISPLAY 'NO OF REC READ    BM06F120 = ' WS-READ-F120.                
           DISPLAY 'NO OF REC READ    BP13F100 = ' WS-READ-F100.                
           DISPLAY 'NO OF REC MATCHED          = ' WS-MATCH.                    
           DISPLAY 'NO OF REC UNMATCHED        = ' WS-UNMATCH.                  
           DISPLAY 'NO OF REC WRITTEN P13F100A = ' WS-WRITE-F100A.              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *************************************************************             
       9900-ABORT-PROCESS.                                                      
      *************************************************************             
           CALL 'DBATIAR' USING SQLCA.                                          
           MOVE SQLCODE       TO RETURN-CODE.                                   
           GOBACK.                                                              
                                                                                
