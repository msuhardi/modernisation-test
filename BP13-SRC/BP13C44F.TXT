       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C44F.                                                 
       AUTHOR.        ELGER DE GUZMAN                                           
      *DATE-WRITTEN.  17/05/2014.                                               
      * ===============================================================*        
      * SYSTEM NAME (SYSTEM ID)                                        *        
      * ===============================================================*        
      * OBJECTIVE   : CREATE MAIL FILE FOR COURT ORDER UPDATE          *        
      * BP135249  ESD1  170514  NEW PROGRAM                            *        
      * BP135537  RJB1  030315  CHANGE THE EMAIL RECIPIENT FROM        *        
      *                         NICKNAME INTO COMPLETE NAME            *        
      * BP136082  KVL1  110216  REPLACE TEEU_HUI@HDB.GOV.SG TO         *        
      *                         KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG AND*        
      *                         REPLACE JESSICA_PC_LIM@HDB.GOV.SG TO   *        
      *                         SERENA_JM_TEH@HDB.GOV.SG               *        
      * BP136477  RJB1  181016  TO REPLACE KIM HUAT TO YEE MIEN        *        
      * BP138757  KV8   230721  TO REPLACE KATHIJAH TO SUZANNE         *        
      * BP139030  ZAR7  070122  REPLACE CHUA YEE MIEN TO LIM KOK CHUN           
      * BP139175  KAC1  120922  ADDED JIALING HE                                
      * BP139276  KAC1  050424  REPLACE LIM KOK CHUN BY                *        
      *                                 KATHIJAH_BEE_ALI_MOHAMED       *        
      *================================================================*        
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310  ASSIGN        TO BP13F310.                          
                                                                                
           SELECT MAILFILE  ASSIGN        TO MAILFILE.                          
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F310                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 150 CHARACTERS.                                      
       COPY BP13F310.                                                           
                                                                                
       FD  MAILFILE                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  MAILFILE-REC      PIC X(132).                                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  VARIABLES.                                                           
           05  WS-F310-EOF                  PIC X      VALUE 'N'.               
       01  WS-F310RPT-COUNT                 PIC 9(7)   VALUE ZERO.              
       01  WS-CTR                           PIC 9(2)   VALUE ZERO.              
       01  WS-FIRST-REPORT                  PIC X      VALUE 'Y'.               
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY                  PIC 9(04).                          
           05  WS-CUR-MM                    PIC 9(02).                          
           05  WS-CUR-DD                    PIC 9(02).                          
                                                                                
       01  WS-REPORT-DTL.                                                       
                                                                                
           05  WS-HEADER1-RPT.                                                  
               10 FILLER                  PIC X(05) VALUE 'S/NO.'.              
               10 FILLER                  PIC X(05) VALUE SPACES.               
               10 FILLER                  PIC X(18)                             
                                          VALUE 'REGISTRATION NO.'.             
               10 FILLER                  PIC X(05) VALUE SPACES.               
               10 FILLER                  PIC X(11)                             
                                          VALUE 'UPDATED BY:'.                  
               10 FILLER                  PIC X(05) VALUE SPACES.               
               10 FILLER                  PIC X(15)                             
                                          VALUE 'DATE UPDATED'.                 
                                                                                
           05  WS-DETAIL-RPT.                                                   
               10 WS-SNO-DTL                PIC  99   VALUE ZERO.               
               10 FILLER                    PIC X(8)  VALUE SPACES.             
               10 WS-REGNO-DTL              PIC X(08) VALUE SPACES.             
               10 FILLER                    PIC X(15) VALUE SPACES.             
               10 WS-UPDATE-BY              PIC X(11) VALUE SPACES.             
               10 FILLER                    PIC X(05) VALUE SPACES.             
               10 WS-DATE-DTL               PIC X(15) VALUE SPACES.             
                                                                                
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT3             PIC X(60)                               
      *        VALUE 'RCPT TO:<JESSICA_PC_LIM@HDB.GOV.SG>'.                     
               VALUE 'RCPT TO:<SERENA_JM_TEH@HDB.GOV.SG>'.                      
           05  WS-MAIL-RPT4             PIC X(60)                               
      *        VALUE 'RCPT TO:<TEEU_HUI@HDB.GOV.SG>'.                           
      *        VALUE 'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG>'.           
               VALUE 'RCPT TO:<SUZANNE_SC_HENG@HDB.GOV.SG>'.                    
           05  WS-MAIL-RPT5             PIC X(60)                               
               VALUE 'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.           
           05  WS-MAIL-RPT6             PIC X(60)                               
               VALUE 'RCPT TO:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                      
           05  WS-MAIL-RPT7             PIC X(60)                               
               VALUE 'RCPT TO:<TENG_SIEW_HONG@HDB.GOV.SG>'.                     
           05  WS-MAIL-RPT7A            PIC X(60)                               
               VALUE 'RCPT TO:<HE_Jialing@hdb.gov.sg>'.                         
           05  WS-MAIL-RPT8             PIC X(4)                                
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT9             PIC X(29)                               
               VALUE 'FROM:SOC_SYSTEM@HDB.GOV.SG   '.                           
           05  WS-MAIL-RPT10            PIC X(60)                               
      *        VALUE 'TO:<JESSICA_PC_LIM@HDB.GOV.SG>'.                          
               VALUE 'TO:<SERENA_JM_TEH@HDB.GOV.SG>'.                           
           05  WS-MAIL-RPT11            PIC X(60)                               
      *        VALUE 'TO:<TEEU_HUI@HDB.GOV.SG>'.                                
      *        VALUE 'TO:<KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG>'.                
               VALUE 'TO:<SUZANNE_SC_HENG@HDB.GOV.SG>'.                         
           05  WS-MAIL-RPT12            PIC X(60)                               
               VALUE 'TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                
           05  WS-MAIL-RPT13            PIC X(60)                               
               VALUE 'TO:<LOW_SOO_CHIEW@HDB.GOV.SG>'.                           
           05  WS-MAIL-RPT14            PIC X(60)                               
               VALUE 'TO:<TENG_SIEW_HONG@HDB.GOV.SG>'.                          
           05  WS-MAIL-RPT14A           PIC X(60)                               
               VALUE 'TO:<HE_Jialing@hdb.gov.sg>'.                              
           05  WS-MAIL-RPT15            PIC X(50)                               
               VALUE  'SUBJECT : Deleted Court Order Cases'.                    
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================*           
       MAIN.                                                                    
      *=============================================================*           
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-PROCESS-INDATA  THRU 2000-EXIT.                         
           PERFORM 3000-GET-ALL-TRANS   THRU 3000-EXIT                          
           UNTIL WS-F310-EOF = 'Y'.                                             
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
      *=============================================================*           
       1000-OPEN-ROUTINE.                                                       
      *=============================================================*           
                                                                                
           OPEN INPUT  BP13F310                                                 
                OUTPUT MAILFILE.                                                
                                                                                
           INITIALIZE  BP13F310-TRANS-REC                                       
                       MAILFILE-REC.                                            
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2000-PROCESS-INDATA.                                                     
      *=============================================================*           
                                                                                
            READ BP13F310                                                       
                 AT END MOVE 'Y'  TO WS-F310-EOF                                
            END-READ.                                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       3000-GET-ALL-TRANS.                                                      
      *=============================================================*           
                                                                                
            PERFORM 4000-PROCESS-REPORT  THRU  4000-EXIT                        
                                                                                
            PERFORM 2000-PROCESS-INDATA THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       4000-PROCESS-REPORT.                                                     
      *================================================================*        
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CUR-DATE.                      
                                                                                
           ADD   1   TO   WS-F310RPT-COUNT.                                     
                                                                                
           IF WS-F310RPT-COUNT = 1                                              
              PERFORM 4500-EMAIL-HEAD      THRU  4500-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 4600-EMAIL-BODY THRU 4600-EXIT.                              
                                                                                
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4500-EMAIL-HEAD.                                                         
      *================================================================*        
                                                                                
            WRITE MAILFILE-REC FROM WS-MAIL-RPT1                                
            WRITE MAILFILE-REC FROM WS-MAIL-RPT2                                
            WRITE MAILFILE-REC FROM WS-MAIL-RPT3                                
            WRITE MAILFILE-REC FROM WS-MAIL-RPT4                                
            WRITE MAILFILE-REC FROM WS-MAIL-RPT5                                
            WRITE MAILFILE-REC FROM WS-MAIL-RPT6                                
            WRITE MAILFILE-REC FROM WS-MAIL-RPT7                                
            WRITE MAILFILE-REC FROM WS-MAIL-RPT7A                               
            WRITE MAILFILE-REC FROM WS-MAIL-RPT8                                
            WRITE MAILFILE-REC FROM WS-MAIL-RPT9                                
            WRITE MAILFILE-REC FROM WS-MAIL-RPT10                               
            WRITE MAILFILE-REC FROM WS-MAIL-RPT11                               
            WRITE MAILFILE-REC FROM WS-MAIL-RPT12                               
            WRITE MAILFILE-REC FROM WS-MAIL-RPT13                               
            WRITE MAILFILE-REC FROM WS-MAIL-RPT14                               
            WRITE MAILFILE-REC FROM WS-MAIL-RPT14A                              
            WRITE MAILFILE-REC FROM WS-MAIL-RPT15                               
            STRING WS-CUR-DD '/' WS-CUR-MM '/' WS-CUR-CCYY                      
                   DELIMITED BY SIZE INTO WS-DATE-RPT                           
            WRITE MAILFILE-REC FROM WS-DATE-RPT-DTL                             
            MOVE SPACES TO  MAILFILE-REC                                        
            WRITE   MAILFILE-REC                                                
                                                                                
            WRITE   MAILFILE-REC FROM WS-HEADER1-RPT.                           
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4600-EMAIL-BODY.                                                         
      *================================================================*        
                                                                                
            ADD      1           TO WS-SNO-DTL                                  
            MOVE F310-REGN-NO    TO WS-REGNO-DTL                                
            MOVE F310-USERID     TO WS-UPDATE-BY                                
            STRING F310-DTE-UPDATE(7:2) '/' F310-DTE-UPDATE(5:2) '/'            
                   F310-DTE-UPDATE(1:4)                                         
                   DELIMITED BY SIZE INTO WS-DATE-DTL                           
            WRITE MAILFILE-REC FROM WS-DETAIL-RPT.                              
                                                                                
       4600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       9000-CLOSE-ROUTINE.                                                      
      *================================================================*        
           DISPLAY 'TOTAL EMAIL CREATED: '                                      
                   WS-F310RPT-COUNT.                                            
                                                                                
           CLOSE BP13F310                                                       
                 MAILFILE.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
