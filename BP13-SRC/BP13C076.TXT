       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C076.                                                 
       AUTHOR.        MCC.                                                      
      ***********************************************************               
      *                                                         *               
      *    SYSTEM NAME : SYSTEM OF COMMENCEMENT                 *               
      *                                                         *               
      *    SYSTEM ID : BP13                                     *               
      *                                                         *               
      *    OBJECTIVE  :                                         *               
      *        1.  TO MATCH SURVEY AND RESERVE LIST AND OUTPUT  *               
      *            TO BALLOT FILE FOR BOOKING SYSTEM.           *               
      *                                                         *               
      *     OUTPUT FILES :                                      *               
      *       1. BP13F700  : BALLOT FILE FOR BOOKING SYSTEM     *               
      *                                                         *               
      *     INPUT FILES :                                       *               
      *       1. BP13F789  : SURVEY FILE                        *               
      *       2. BP13K705  : SOC RESERVE LIST                   *               
      * --------------------------------------------------------*               
      * CHG REF  OIC  DATE    DESCRIPTION                       *               
      * -------- ---  ------  -----------                       *               
      * BP130837 MCC  100500  NEW PROGRAM                       *               
      ***********************************************************               
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3900.                                               
       OBJECT-COMPUTER. IBM-3900.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F789 ASSIGN  TO BP13F789.                                 
                                                                                
           SELECT BP13K705  ASSIGN TO BP13K705                                  
                  ACCESS MODE      IS DYNAMIC                                   
                  ORGANIZATION     IS INDEXED                                   
                  RECORD KEY       IS K705-KEY-FLD                              
                  FILE STATUS      IS WS-K705-FILE-STATUS.                      
                                                                                
           SELECT BP13F700 ASSIGN  TO BP13F700.                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F789                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 100 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F789.                                                           
                                                                                
       FD  BP13K705                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K705.                                                           
                                                                                
       FD  BP13F700                                                             
           BLOCK  CONTAINS   0 RECORDS                                          
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE  IS F                                                 
           LABEL RECORDS   ARE STANDARD.                                        
       COPY BP13F700.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-K705-FILE-STATUS         PIC 9(2)    VALUE ZEROES.                
       01  WS-F789-READ                PIC 9(8)    VALUE ZEROES.                
       01  WS-K705-READ                PIC 9(8)    VALUE ZEROES.                
       01  WS-F700-WRITE               PIC 9(8)    VALUE ZEROES.                
       01  WS-BP13F789-EOF             PIC X       VALUE SPACES.                
                                                                                
      ************************                                                  
       PROCEDURE DIVISION.                                                      
      ************************                                                  
       0000-MAIN.                                                               
                                                                                
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F789   THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-ROUTINE THRU 3000-EXIT                          
                   UNTIL WS-BP13F789-EOF = 'Y'                                  
           PERFORM 9000-CLOSE-FILES.                                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
           OPEN INPUT  BP13F789                                                 
                       BP13K705                                                 
                OUTPUT BP13F700.                                                
                                                                                
           IF WS-K705-FILE-STATUS NOT = ZEROES AND 97                           
              DISPLAY ' OPENING BP13K705 ERROR ' WS-K705-FILE-STATUS            
              MOVE WS-K705-FILE-STATUS TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES.                                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       2000-READ-BP13F789.                                                      
      *-----------------*                                                       
           READ BP13F789 AT END MOVE 'Y' TO WS-BP13F789-EOF                     
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F789-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3000-PROCESS-ROUTINE.                                                    
      *-------------------*                                                     
           MOVE F789-RESERVE-KEY     TO K705-KEY-FLD.                           
                                                                                
           READ BP13K705.                                                       
                                                                                
           EVALUATE WS-K705-FILE-STATUS                                         
               WHEN 00                                                          
               WHEN 02                                                          
                  IF (K705-CDE-SUCC-NT NOT EQUAL SPACES AND LOW-VALUES)         
                      AND (K705-CDE-UNSUCC EQUAL SPACE)                         
                     ADD 01 TO WS-K705-READ                                     
                     PERFORM 4000-WRITE-BP13F700 THRU 4000-EXIT                 
                  END-IF                                                        
               WHEN 10                                                          
               WHEN 23                                                          
                  CONTINUE                                                      
               WHEN OTHER                                                       
                  MOVE WS-K705-FILE-STATUS TO RETURN-CODE                       
                  PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2000-READ-BP13F789 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4000-WRITE-BP13F700.                                                     
      *-------------------*                                                     
           MOVE SPACES              TO BP13F700-REC.                            
           MOVE BP13K705-REC        TO BP13F700-REC.                            
                                                                                
           WRITE   BP13F700-REC.                                                
           ADD 01  TO WS-F700-WRITE.                                            
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
           DISPLAY '    --BP13C076--        '.                                  
           DISPLAY 'TOTAL F789 REC READ     = ' WS-F789-READ.                   
           DISPLAY 'TOTAL K705 REC READ     = ' WS-K705-READ.                   
           DISPLAY 'TOTAL F700 REC WRITTEN  = ' WS-F700-WRITE.                  
                                                                                
           CLOSE BP13F789                                                       
                 BP13K705                                                       
                 BP13F700.                                                      
                                                                                
           IF WS-K705-FILE-STATUS NOT = ZEROES AND 97                           
              DISPLAY ' CLOSING BP13K705 ERROR ' WS-K705-FILE-STATUS.           
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
