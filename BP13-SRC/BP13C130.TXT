      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
       PROGRAM-ID.    BP13C130.                                                 
      *AUTHOR.        EG1.                                                      
      *DATE-WRITTEN.  22/03/96.                                                 
                                                                                
      ******************************************************************        
      *  PROGRAM NAME : BP13C130                                       *        
      *  OBJECTIVE    : 1. TO EXTRACT VALID K885 RECORDS THAT HAVE     *        
      *                    CDE-VOUCHERS = 'Y'                          *        
      *  INPUT/OUTPUT FILES :                                          *        
      *  ====================                                          *        
      *  TYPE  FILE NAME                   DD NAME   ACCESS    KEYLEN  *        
      *  ----  ---------                   -------   ------    ------  *        
      *  I     BP13F885                    BP13F885   SEQ.             *        
      *  I     BP13K885                    BP13K885   KSDS             *        
      *  O     BP13FOUT                    BP13K885   SEQ.             *        
      *================================================================*        
      * MODIFICATION HISTORY:                                          *        
      * ---------------------                                          *        
      * CHG REQ. AUTHOR  DATE     DESCRIPTIONS                         *        
      * =======  ====== =======   =====================================*        
      * BP130094 EG1    22031996  TO CATER FOR SUB-TRANSFER MODULE.    *        
      * BP130418 RHB    05061998  Y2K                                  *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
      *================================================================*        
       CONFIGURATION SECTION.                                                   
      *================================================================*        
       SOURCE-COMPUTER.  IBM-4341.                                              
       OBJECT-COMPUTER.  IBM-4341.                                              
                                                                                
      *================================================================*        
       INPUT-OUTPUT SECTION.                                                    
      *================================================================*        
      *----------------------------------------------------------------*        
       FILE-CONTROL.                                                            
      *----------------------------------------------------------------*        
           SELECT BP13F885 ASSIGN TO BP13F885.                                  
                                                                                
           SELECT BP13K885 ASSIGN TO BP13K885                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS          IS DYNAMIC                                    
                  RECORD KEY      IS K885-KEY-FLD                               
                  FILE STATUS     IS K885-STATUS.                               
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS          IS DYNAMIC                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS K800-STATUS.                               
                                                                                
           SELECT BP13FOUT ASSIGN TO BP13FOUT.                                  
           SELECT BP13L130 ASSIGN TO BP13L130.                                  
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      *================================================================*        
       FILE SECTION.                                                            
      *================================================================*        
       FD   BP13F885                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F885.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K885                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K885.                                                           
                                                                                
       FD   BP13FOUT                                                            
            LABEL RECORDS ARE OMITTED                                           
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            RECORDING MODE IS F.                                                
                                                                                
       01   FOUT-REC                    PIC X(400).                             
                                                                                
       FD   BP13L130                                                            
            LABEL RECORDS ARE OMITTED                                           
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE IS F.                                                
                                                                                
       01   L130-REC                    PIC X(132).                             
                                                                                
      *================================================================*        
       WORKING-STORAGE SECTION.                                                 
      *================================================================*        
      *----------------------------------------------------------------*        
      *    ERROR HANDLING VARIABLES                                    *        
      *----------------------------------------------------------------*        
       01 WS-ERR-MSG                     PIC X(80) VALUE SPACES.                
       01 WS-ERR-CDE                     PIC X(2)  VALUE SPACES.                
                                                                                
      *----------------------------------------------------------------*        
      *    FLAG VARIABLES                                              *        
      *----------------------------------------------------------------*        
       01 WS-FLAGS.                                                             
          05 WS-K885-VALID-FL            PIC X.                                 
                                                                                
      *----------------------------------------------------------------*        
      *    FILE STATUS                                                 *        
      *----------------------------------------------------------------*        
       01 WS-FILE-STATUS-VAR.                                                   
          05 K800-STATUS                 PIC X(2).                              
          05 K885-STATUS                 PIC X(2).                              
                                                                                
       01 WS-EOF-STATUS-VAR.                                                    
          05 WS-K885-EOF                 PIC X(1) VALUE 'N'.                    
          05 WS-F885-EOF                 PIC X(1) VALUE 'N'.                    
                                                                                
      *----------------------------------------------------------------*        
      *    WORKING STORAGE AREA FOR COUNTER                            *        
      *----------------------------------------------------------------*        
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F885-READ           PIC 9(07) VALUE 0.                     
           05 WS-CNT-WRITE-FOUT          PIC 9(07) VALUE 0.                     
                                                                                
      *----------------------------------------------------------------*        
      *    WORKING STORAGE AREA FOR DATE VARIABLES                     *        
      *----------------------------------------------------------------*        
       01  WS-CURRENT-DATE.                                                     
           05 WS-CURR-CC                 PIC X(02) VALUE SPACES.                
           05 WS-CURR-YY                 PIC X(02) VALUE SPACES.                
           05 WS-CURR-MM                 PIC X(02) VALUE SPACES.                
           05 WS-CURR-DD                 PIC X(02) VALUE SPACES.                
                                                                                
      *----------------------------------------------------------------*        
      *    WORKING STORAGE AREA FOR COMPUTE VARIABLES.                 *        
      *----------------------------------------------------------------*        
       01  WS-COMPUTE-VARIABLES.                                                
           05 WS-TOT-SUSPENSE            PIC S9(7)V99 VALUE +0.                 
           05 WS-TOT-K885-INTEREST       PIC S9(7)V99 VALUE +0.                 
           05 WS-TOT-K800-INTEREST       PIC S9(7)V99 VALUE +0.                 
                                                                                
      *----------------------------------------------------------------*        
      *    WORKING STORAGE AREA FOR REPORT VARIABLES                   *        
      *----------------------------------------------------------------*        
       01  WS-ERR-SEQ-CNT                PIC 9(5) VALUE 0.                      
       01  WS-LINE-CNT                   PIC 99   VALUE 0.                      
       01  WS-PAGE-CNT                   PIC 9(5) VALUE 0.                      
                                                                                
      *----------------------------------------------------------------*        
      *    WORKING STORAGE AREA FOR REPORT LAYOUT                      *        
      *----------------------------------------------------------------*        
       01  L130-RPT-HDR-1.                                                      
           05 L130-RPT-ID                PIC X(08) VALUE 'BP13L130'.            
           05 FILLER                     PIC X(06) VALUE SPACES.                
           05 FILLER                     PIC X(04) VALUE 'HDB3'.                
           05 FILLER                     PIC X(28) VALUE SPACES.                
           05 FILLER                     PIC X(39) VALUE                        
             'S Y S T E M   O F   C O M M I T M E N T'.                         
           05 FILLER                     PIC X(17) VALUE SPACES.                
           05 FILLER                     PIC X(07) VALUE 'DATE : '.             
           05 L130-PRINT-DATE            PIC X(10).                             
           05 FILLER                     PIC X(03) VALUE SPACES.                
           05 FILLER                     PIC X(07) VALUE 'PAGE : '.             
           05 L130-PAGE-CNT              PIC ZZZZ9.                             
                                                                                
       01  L130-RPT-HDR-2.                                                      
           05 FILLER                     PIC X(40) VALUE SPACES.                
           05 FILLER                     PIC X(51) VALUE                        
             '       ERROR LIST ON BP13K885 EX-APPLT FILE        '.             
                                                                                
       01  L130-RPT-HDR-3.                                                      
           05 FILLER                     PIC X(49) VALUE SPACES.                
           05 FILLER                     PIC X(51) VALUE                        
             '      SUB-TRANSFER CASES                  '.                      
                                                                                
       01  L130-RPT-HDR-4.                                                      
           05 FILLER                     PIC X(02) VALUE SPACES.                
           05 FILLER                     PIC X(03) VALUE 'NO.'.                 
           05 FILLER                     PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(08) VALUE 'REGN NO.'.            
           05 FILLER                     PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(08) VALUE 'CPF  NO.'.            
           05 FILLER                     PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(09) VALUE '  NRIC   '.           
           05 FILLER                     PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(40) VALUE 'REMARKS'.             
                                                                                
       01  L130-RPT-HDR-5.                                                      
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(04) VALUE '===='.                
           05 FILLER                     PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(08) VALUE '========'.            
           05 FILLER                     PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(09) VALUE '========='.           
           05 FILLER                     PIC X(09) VALUE SPACES.                
           05 FILLER                     PIC X(09) VALUE '========='.           
           05 FILLER                     PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(40) VALUE ALL '='.               
                                                                                
       01  L130-DETAIL-LINE.                                                    
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 L130-SEQ-NO                PIC ZZZ9.                              
           05 FILLER                     PIC X(10) VALUE SPACES.                
           05 L130-REGN-NO               PIC X(08).                             
           05 FILLER                     PIC X(10) VALUE SPACES.                
           05 L130-CPF-NO                PIC X(09).                             
           05 FILLER                     PIC X(09) VALUE SPACES.                
           05 L130-NRIC                  PIC X(09).                             
           05 FILLER                     PIC X(10) VALUE SPACES.                
           05 L130-REMARKS               PIC X(40).                             
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *================================================================*        
       0000-MAIN-ROUTINE.                                                       
      *================================================================*        
                                                                                
           PERFORM 1000-INITIALIZATION      THRU 1000-EXIT.                     
           PERFORM 2000-PROCESS-ROUTINE     THRU 2000-EXIT                      
                   UNTIL WS-F885-EOF = 'Y'.                                     
           PERFORM 9999-CLOSE-FILES         THRU 9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       1000-INITIALIZATION.                                                     
      *================================================================*        
           OPEN INPUT  BP13F885                                                 
                       BP13K800                                                 
                       BP13K885                                                 
                OUTPUT BP13FOUT                                                 
                       BP13L130.                                                
                                                                                
           IF K885-STATUS NOT EQUAL 00 AND 97                                   
              STRING 'BP13K885 OPENING ERROR (' K885-STATUS ')'                 
                     DELIMITED BY SIZE INTO WS-ERR-MSG                          
              PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                          
           END-IF.                                                              
                                                                                
           IF K800-STATUS NOT EQUAL 00 AND 97                                   
              STRING 'BP13K800 OPENING ERROR (' K800-STATUS ')'                 
                     DELIMITED BY SIZE INTO WS-ERR-MSG                          
              PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                          
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
                                                                                
           READ  BP13F885 AT END                                                
                 MOVE 'Y'          TO  WS-F885-EOF.                             
                                                                                
           IF WS-F885-EOF = 'Y'                                                 
              STRING 'NO INPUT F885 RECORDS FOUND'                              
                     DELIMITED BY SIZE INTO WS-ERR-MSG                          
              PERFORM 9999-CLOSE-FILES         THRU 9999-EXIT                   
           ELSE                                                                 
              ADD 1                TO WS-CNT-F885-READ                          
           END-IF.                                                              
           MOVE 60                 TO WS-LINE-CNT.                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       2000-PROCESS-ROUTINE.                                                    
      *================================================================*        
           PERFORM  3000-VALIDATE-RECS THRU 3000-EXIT.                          
           IF WS-K885-VALID-FL = 'Y'                                            
              PERFORM 4000-WRITE-FOUT           THRU 4000-EXIT                  
           ELSE                                                                 
              PERFORM 6000-ERROR-LIST-PRINT     THRU 6000-EXIT                  
           END-IF.                                                              
                                                                                
           READ  BP13F885 AT END                                                
                 MOVE 'Y'        TO WS-F885-EOF                                 
                 GO  TO 2000-EXIT                                               
                 END-READ.                                                      
                                                                                
           ADD   1   TO  WS-CNT-F885-READ.                                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3000-VALIDATE-RECS.                                                      
      *================================================================*        
           MOVE SPACES                     TO WS-K885-VALID-FL                  
                                              WS-ERR-CDE.                       
           MOVE ZEROES                     TO WS-TOT-K885-INTEREST              
                                              WS-TOT-K800-INTEREST.             
           MOVE LOW-VALUES                 TO K885-KEY-FLD.                     
           MOVE F885-NUM-REGN              TO K885-NUM-REGN.                    
           PERFORM  8100-START-BP13K885    THRU 8100-EXIT.                      
           PERFORM UNTIL WS-K885-EOF = 'Y' OR                                   
                         WS-K885-VALID-FL = 'N'                                 
              PERFORM  8150-READNEXT-BP13K885 THRU 8150-EXIT                    
              IF WS-K885-EOF NOT = 'Y'                                          
                 IF K885-AMT-INTEREST NOT NUMERIC                               
                    MOVE ZEROES TO K885-AMT-INTEREST                            
                 END-IF                                                         
                 ADD K885-AMT-INTEREST        TO WS-TOT-K885-INTEREST           
                 IF K885-CDE-VOUCHER  = 'Y' AND                                 
                    K885-NUM-PRINT-STATUS NOT = 'Y'                             
                    MOVE 'Y'                  TO WS-K885-VALID-FL               
                    PERFORM  3100-CHECK-SUMS  THRU 3100-EXIT                    
                 ELSE                                                           
                    IF K885-NUM-PRINT-STATUS = 'Y'                              
                       MOVE 'N'               TO WS-K885-VALID-FL               
                       MOVE '03'              TO WS-ERR-CDE                     
                    ELSE                                                        
                       MOVE 'N'               TO WS-K885-VALID-FL               
                       MOVE '01'              TO WS-ERR-CDE                     
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           IF WS-K885-VALID-FL = 'Y'                                            
              MOVE F885-NUM-REGN              TO K800-NUM-REGN                  
              PERFORM 8200-READ-BP13K800      THRU 8200-EXIT                    
              IF WS-K885-VALID-FL = 'Y'                                         
                 COMPUTE WS-TOT-K800-INTEREST = K800-AMT-SOC-CASH +             
                                             K800-AMT-CPF1-PAID +               
                                             K800-AMT-CPF2-PAID +               
                                             K800-AMT-CPF3-PAID +               
                                             K800-AMT-CPF4-PAID -               
                                             K800-AMT-SOC-CASH-REQ -            
                                             K800-AMT-SOC-CPF1-REQ -            
                                             K800-AMT-SOC-CPF2-REQ -            
                                             K800-AMT-SOC-CPF3-REQ -            
                                             K800-AMT-SOC-CPF4-REQ              
                 IF WS-TOT-K800-INTEREST NOT = WS-TOT-K885-INTEREST             
                    MOVE 'N'                  TO WS-K885-VALID-FL               
                    MOVE '05'                 TO WS-ERR-CDE                     
                 ELSE                                                           
                    IF K800-AMT-SOC-CASH-REQ > K800-AMT-SOC-CASH  OR            
                       K800-AMT-SOC-CPF1-REQ > K800-AMT-CPF1-PAID OR            
                       K800-AMT-SOC-CPF2-REQ > K800-AMT-CPF2-PAID OR            
                       K800-AMT-SOC-CPF3-REQ > K800-AMT-CPF3-PAID OR            
                       K800-AMT-SOC-CPF4-REQ > K800-AMT-CPF4-PAID               
                       MOVE 'N'               TO WS-K885-VALID-FL               
                       MOVE '06'              TO WS-ERR-CDE                     
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3100-CHECK-SUMS.                                                         
      *================================================================*        
           COMPUTE WS-TOT-SUSPENSE =                                            
                        K885-AMT-REFUND + K885-AMT-INTEREST.                    
           IF K885-AMT-SUSPENSE NOT = WS-TOT-SUSPENSE                           
              MOVE 'N'                     TO WS-K885-VALID-FL                  
              MOVE '02'                    TO WS-ERR-CDE                        
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       4000-WRITE-FOUT.                                                         
      *================================================================*        
           WRITE FOUT-REC              FROM BP13F885-REC.                       
           ADD   1                     TO   WS-CNT-WRITE-FOUT.                  
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       6000-ERROR-LIST-PRINT.                                                   
      *================================================================*        
           PERFORM 6100-FORMAT-ERR-DETAIL  THRU 6100-EXIT.                      
           IF WS-LINE-CNT > 46                                                  
              PERFORM 6050-WRITE-HDR   THRU 6050-EXIT                           
           END-IF.                                                              
           WRITE L130-REC      FROM  L130-DETAIL-LINE AFTER 2.                  
           ADD   2             TO    WS-LINE-CNT.                               
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       6050-WRITE-HDR.                                                          
      *================================================================*        
                                                                                
           MOVE 'BP13L130'     TO  L130-RPT-ID.                                 
           ADD  1              TO  WS-PAGE-CNT.                                 
           MOVE WS-PAGE-CNT    TO  L130-PAGE-CNT.                               
           STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CC WS-CURR-YY           
                  DELIMITED BY SIZE INTO L130-PRINT-DATE.                       
                                                                                
           WRITE L130-REC    FROM L130-RPT-HDR-1   AFTER PAGE.                  
           WRITE L130-REC    FROM L130-RPT-HDR-2   AFTER 1.                     
           WRITE L130-REC    FROM L130-RPT-HDR-3   AFTER 1.                     
           WRITE L130-REC    FROM L130-RPT-HDR-4   AFTER 3.                     
           WRITE L130-REC    FROM L130-RPT-HDR-5   AFTER 1.                     
                                                                                
           MOVE 8               TO WS-LINE-CNT.                                 
                                                                                
       6050-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       6100-FORMAT-ERR-DETAIL.                                                  
      *================================================================*        
           ADD 1                TO WS-ERR-SEQ-CNT.                              
           MOVE WS-ERR-SEQ-CNT  TO L130-SEQ-NO.                                 
           MOVE F885-NUM-REGN   TO L130-REGN-NO.                                
           MOVE F885-NUM-NRIC   TO L130-NRIC.                                   
           MOVE F885-NUM-CPF    TO L130-CPF-NO.                                 
           EVALUATE WS-ERR-CDE                                                  
           WHEN '  '                                                            
                MOVE 'BP13K885 NOT FND!'   TO L130-REMARKS                      
           WHEN '01'                                                            
                MOVE 'INCOMPLETE CASE!'    TO L130-REMARKS                      
           WHEN '02'                                                            
                MOVE 'SUSPENSE AMOUNT NOT TALLIED!'                             
                                           TO L130-REMARKS                      
           WHEN '03'                                                            
                MOVE 'VOUCHER ALREADY ISSUED!     '                             
                                           TO L130-REMARKS                      
           WHEN '04'                                                            
                MOVE 'MISSING K800 FILE!'  TO L130-REMARKS                      
           WHEN '05'                                                            
                MOVE 'K885 & K800 FILE UNTALLIED!'                              
                                           TO L130-REMARKS                      
           WHEN '06'                                                            
                MOVE 'K800 SOC REQUIRED FIELD CORRUPTED!'                       
                                           TO L130-REMARKS                      
           END-EVALUATE.                                                        
                                                                                
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       8100-START-BP13K885.                                                     
      *================================================================*        
            MOVE 'N'                 TO WS-K885-EOF.                            
            START BP13K885 KEY >= K885-KEY-FLD                                  
                  INVALID KEY  MOVE 'Y'    TO WS-K885-EOF                       
                               GO TO 8100-EXIT.                                 
                                                                                
            IF K885-STATUS NOT = '00'                                           
               STRING 'BP13K885 START ERROR CODE(' K885-STATUS ')'              
                      DELIMITED BY SIZE INTO WS-ERR-MSG                         
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
       8100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       8150-READNEXT-BP13K885.                                                  
      *================================================================*        
            READ  BP13K885 NEXT                                                 
                  AT END   MOVE 'Y'        TO WS-K885-EOF                       
                           GO TO 8150-EXIT.                                     
                                                                                
            IF K885-STATUS NOT = '00'                                           
               STRING 'BP13K885 READ NEXT ERROR CODE(' K885-STATUS ')'          
                      DELIMITED BY SIZE INTO WS-ERR-MSG                         
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
            IF K885-NUM-REGN NOT = F885-NUM-REGN                                
               MOVE 'Y'            TO WS-K885-EOF                               
            END-IF.                                                             
                                                                                
       8150-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       8200-READ-BP13K800.                                                      
      *================================================================*        
            READ  BP13K800                                                      
            INVALID KEY   MOVE 'N'        TO WS-K885-VALID-FL                   
                          MOVE '04'       TO WS-ERR-CDE                         
                          GO TO 8200-EXIT.                                      
                                                                                
            IF K800-STATUS NOT = '00'                                           
               STRING 'BP13K800 READ ERROR CODE(' K800-STATUS ')'               
                      DELIMITED BY SIZE INTO WS-ERR-MSG                         
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
       8200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       9900-HANDLE-ABEND.                                                       
      *================================================================*        
            CLOSE   BP13F885                                                    
                    BP13K800                                                    
                    BP13K885                                                    
                    BP13FOUT                                                    
                    BP13L130.                                                   
                                                                                
            DISPLAY '***************************************'                   
            DISPLAY '*    BP13C130  ABEND TOTAL            *'                   
            DISPLAY '***************************************'                   
            DISPLAY '* NO. OF F885 READ: ' WS-CNT-F885-READ.                    
            DISPLAY '* NO. OF VALID RECS: ' WS-CNT-WRITE-FOUT.                  
            DISPLAY '* NO. OF ERR CASES: ' WS-ERR-SEQ-CNT.                      
            DISPLAY '***************************************'                   
            DISPLAY '* ABEND DESC: ' WS-ERR-MSG.                                
            MOVE '4095'               TO RETURN-CODE.                           
                                                                                
            STOP RUN.                                                           
       9900-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       9999-CLOSE-FILES.                                                        
      *================================================================*        
            CLOSE   BP13F885                                                    
                    BP13K885                                                    
                    BP13K800                                                    
                    BP13FOUT                                                    
                    BP13L130.                                                   
                                                                                
            DISPLAY '***************************************'                   
            DISPLAY '*    BP13C130 CONTROL TOTAL           *'                   
            DISPLAY '***************************************'                   
            DISPLAY '* NO. OF F885 READ: ' WS-CNT-F885-READ.                    
            DISPLAY '* NO. OF VALID RECS: ' WS-CNT-WRITE-FOUT.                  
            DISPLAY '* NO. OF ERR CASES: ' WS-ERR-SEQ-CNT.                      
            DISPLAY '* ABEND DESC: ' WS-ERR-MSG.                                
            DISPLAY '***************************************'                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
