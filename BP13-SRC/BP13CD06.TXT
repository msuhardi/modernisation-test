       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD06.                                                 
      *AUTHOR.        SHARON DUMDUM.                                            
      *DATE-WRITTEN.  23/02/2005.                                               
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES  : TO TAG F825-NUM-TAG2                  *                
      *                                                        *                
      *    1.  TO TAG F825-NUM-TAG2, DEPENDING ON HOUSEHOLD    *                
      *        AND RELATIONSHIP TO APPLICANT                   *                
      *                                                        *                
      *        NOTE: SPOUSES, CHILD, SIBLING WILL BE TAGGED    *                
      *              ONLY IF SINGAPORE CITIZENS AND AGE > 20   *                
      *              YRS. OLD                                  *                
      *                                                        *                
      *        (A) MARRIED APPLICANTS:                         *                
      *            - ALL APPLICANTS AND RESPECTIVE SPOUSES     *                
      *                                                        *                
      *        (B) SINGLE PARENT APPLICANTS:                   *                
      *            - ALL APPLICANTS AND 1 CHILD                *                
      *                                                        *                
      *        (C) MULTI-NUCLEI HOUSEHOLD:                     *                
      *            - ONLY 1 MARRIED COUPLE WILL BE TAGGED      *                
      *                                                        *                
      *        (D) RELATED ORPHANS AND SIBLINGS SCHEME:        *                
      *            - APPLICANT WITH 1 SIBLING WILL BE TAGGED   *                
      *                                                        *                
      *        (E) SINGLE APPLICANTS WITH PARENTS:             *                
      *            - IF BOTH PARENTS ARE LESSEES, BOTH PARENTS *                
      *              WILL BE TAGGED AND APPLICANT WILL NOT BE  *                
      *              TAGGED                                    *                
      *            - IF DIV/WID/SEP PARENT IS ALSO A LESSEE,   *                
      *              APPLICANT AND PARENT WILL BE TAGGED       *                
      *                                                        *                
      *        (F) SPECIAL CASES:                              *                
      *            - FOR CASES WHERE SPOUSE, CHILD, SIBLING    *                
      *              CANNOT BE TAGGED DUE TO CITIZENSHIP OR    *                
      *              AGE, ONLY APPLICANT WILL BE TAGGED        *                
      *                                                        *                
      *    2.  SYSTEM WILL NOT TAG ALL OCCUPIERS               *                
      *                                                        *                
      *========================================================*                
      * TAGS FOR SUBSIDIZED FLATS BOUGHT DIRECT FROM HDB       *                
      *--------------------------------------------------------*                
      * A. FAMILY BASED SCHEMES                                *                
      *                                                        *                
      *    I. MARRIED COUPLES                                  *                
      *                                                        *                
      *       1. SMCHH - APPLICANT (MARRIED/HUSBAND) WHO HAS   *                
      *                  BOUGHT A SUBSIDIZED FLAT DIRECT FROM  *                
      *                  HDB AND LIABLE FOR 1/2 RESALE LEVY    *                
      *                                                        *                
      *       2. SMCWH - APPLICANT (MARRIED/WIFE) WHO HAS      *                
      *                  BOUGHT A SUBSIDIZED FLAT DIRECT FROM  *                
      *                  HDB AND LIABLE FOR 1/2 RESALE LEVY    *                
      *                                                        *                
      *       3. SMCHF - APPLICANT (MARRIED/HUSBAND) WHO HAS   *                
      *                  BOUGHT A SUBSIDIZED FLAT DIRECT FROM  *                
      *                  HDB AND LIABLE FOR FULL RESALE LEVY   *                
      *                                                        *                
      *       4. SMCWF - APPLICANT (MARRIED/WIFE) WHO HAS      *                
      *                  BOUGHT A SUBSIDIZED FLAT DIRECT FROM  *                
      *                  HDB AND LIABLE FOR FULL RESALE LEVY   *                
      *                                                        *                
      *       5. SOSHN - HUSBAND (NON-CITIZEN/MINOR) OF        *                
      *                  APPLICANT AND LISTED AS AN OCCUPIER   *                
      *                  AND NOT LIABLE FOR RESALE LEVY        *                
      *                                                        *                
      *       6. SOSWN - WIFE (NON-CITIZEN/MINOR) OF APPLICANT *                
      *                  AND LISTED AS AN OCCUPIER AND NOT     *                
      *                  LIABLE FOR RESALE LEVY                *                
      *                                                        *                
      *    II. SINGLE PARENT HOUSEHOLDS (DIVORCED, WIDOWED,    *                
      *        SEPARATED)                                      *                
      *                                                        *                
      *       7. SPCPH - APPLICANT (PARENT) WHO HAS BOUGHT A   *                
      *                  SUBSIDIZED FLAT DIRECT FROM HDB WITH  *                
      *                  ADULT CHILD AND LIABLE FOR 1/2 RESALE *                
      *                  LEVY                                  *                
      *                                                        *                
      *       8. SPCCH - APPLICANT (CHILD) WHO HAS BOUGHT A    *                
      *                  SUBSIDIZED FLAT DIRECT FROM HDB WITH  *                
      *                  PARENT AND LIABLE FOR 1/2 RESALE LEVY *                
      *                                                        *                
      *       9. SPCPF  - APPLICANT (PARENT) WHO HAS BOUGHT A  *                
      *                   SUBSIDIZED FLAT DIRECT FROM HDB WITH *                
      *                   MINOR CHILD AND LIABLE FOR FULL      *                
      *                   RESALE LEVY                          *                
      *                                                        *                
      *    III. MULTI-NUCLEI HOUSEHOLDS                        *                
      *                                                        *                
      *      10. SAME AS (1) TO (6)                            *                
      *                                                        *                
      * B. SPECIAL ELIGIBILITY SCHEMES                         *                
      *                                                        *                
      *    I. RELATED ORPHANS SCHEME (OR)                      *                
      *                                                        *                
      *      11. SORSH - APPLICANT (RELATED ORPHANS) WHO HAS   *                
      *                  BOUGHT A SUBSIDIZED FLAT DIRECT FROM  *                
      *                  HDB AND LIABLE FOR 1/2 RESALE LEVY    *                
      *                                                        *                
      *      12. SORSF - APPLICANT (RELATED ORPHANS) WHO HAS   *                
      *                  BOUGHT A SUBSIDIZED FLAT DIRECT FROM  *                
      *                  HDB WITH MINOR SIBLING(S) AND LIABLE  *                
      *                  FOR FULL RESALE LEVY                  *                
      *                                                        *                
      *    II. SIBLING SCHEME                                  *                
      *                                                        *                
      *      13. NOT APPLICABLE FOR PURCHASE OF SUBSIZED FLATS *                
      *          DIRECT FROM HDB                               *                
      *                                                        *                
      *    III. UNMARRIED PARENT HOUSEHOLDS/SPLIT FAMILIES     *                
      *                                                        *                
      *      14. SAME AS (7) TO (9)                            *                
      *                                                        *                
      * C. DEVIATIONS FROM TAG2                                *                
      *                                                        *                
      *      15. SAME AS (3), (4), (9) OR (12)                 *                
      *                                                        *                
      *========================================================*                
      *                                                        *                
      *    INPUT  FILE  : BP13F730 - REQUEST DATA ENTRY FILE   *                
      *                   BP13F825 - REQUEST OCCUPIER FILE     *                
      *                   BP13K060 - CODE TRANSCRIPTION FILE   *                
      *                                                        *                
      *    I/O    FILE  : BP13K825 - REQUEST OCCUPIER FILE     *                
      *                                                        *                
      *    OUTPUT FILES : NIL                                  *                
      *                                                        *                
      *    LISTINGS     : BP13LD06 - REPORT ON OCCUPIERS AFTER *                
      *                              TAGGING NUM-TAG2          *                
      *                                                        *                
      *========================================================*                
      *  CHG-NO   BY   DATE   DETAILS                          *                
      * ======== ==== ====== ================================= *                
      * BP132978 SD10 020205 NEW PROGRAM                       *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN TO BP13F730.                                  
                                                                                
           SELECT BP13F825 ASSIGN TO BP13F825.                                  
                                                                                
           SELECT BP13K060 ASSIGN TO BP13K060                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K060-KEY-FLD                                  
                  FILE STATUS  IS WS-K060-STATUS.                               
                                                                                
           SELECT BP13K825 ASSIGN TO BP13K825                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K825-KEY-FLD                                  
                  FILE STATUS  IS WS-K825-STATUS.                               
                                                                                
           SELECT BP13LD06 ASSIGN TO BP13LD06.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F730.                                                           
                                                                                
       FD   BP13F825                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13F825.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K825.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS  25 CHARACTERS.                                     
       COPY BP13K060.                                                           
                                                                                
       FD   BP13LD06                                                            
            RECORDING MODE    F.                                                
       01   BP13LD06-REC   PIC X(132).                                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-MIN-CNT               PIC 9(01)  VALUE 1.                     
           05  WS-MAX-CNT               PIC 9(01)  VALUE 2.                     
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F730-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-F825-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-K060-STATUS                 PIC 9(2)  VALUE ZEROS.            
           05  WS-K825-STATUS                 PIC 9(2)  VALUE ZEROS.            
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-CNT-READ-F730               PIC 9(5)  VALUE ZEROS.            
           05  WS-CNT-READ-F825               PIC 9(5)  VALUE ZEROS.            
           05  WS-CNT-UPD-K825                PIC 9(5)  VALUE ZEROS.            
                                                                                
       01  WS-LD06-CONTROLS.                                                    
           05  WS-LD06-LINECTR                PIC 9(2)  VALUE 50.               
           05  WS-LD06-PAGECTR                PIC 9(5)  VALUE ZEROS.            
           05  WS-LD06-SNO                    PIC 9(5)  VALUE ZEROS.            
                                                                                
       01  WS-F825-ARRAY-VARS.                                                  
           05  WS-BP13F825-REC                OCCURS    16 TIMES                
                                              PIC X(200).                       
           05  WS-NUM-FAMILY                  PIC 9(2).                         
           05  WS-OCCUP-TAG                   PIC 9(2).                         
                                                                                
       01  WS-NUCLEUS-TAGS.                                                     
           05  WS-HA-GRAND                    PIC X(1) VALUE 'N'.               
           05  WS-HA-PARENT                   PIC X(1) VALUE 'N'.               
           05  WS-HA-SIBLING                  PIC X(1) VALUE 'N'.               
           05  WS-HA-CHILD                    PIC X(1) VALUE 'N'.               
           05  WS-HA-CHILD-MARRIED            PIC X(1) VALUE 'N'.               
           05  WS-HA1-MARRIED                 PIC X(1) VALUE 'N'.               
           05  WS-SC-SP-CHILD                 PIC X(1) VALUE 'N'.               
                                                                                
       01  WS-MISCELLANEOUS-VARS.                                               
           05  WS-SAVE-BP13F825               PIC X(200) VALUE SPACES.          
           05  WS-AGE                         PIC S9(3).                        
           05  WS-REMARKS                     PIC X(10).                        
           05  WS-BIRTH-YY                    PIC 9(4).                         
       01  WS-COUNTERS.                                                         
           05  WS-HA-GRANDPR-CNT              PIC 9(01)  VALUE ZEROES.          
           05  WS-HA-PARENT-CNT               PIC 9(01)  VALUE ZEROES.          
           05  WS-HA-SIBLING-CNT              PIC 9(01)  VALUE ZEROES.          
           05  WS-SIBLING-CNT                 PIC 9(01)  VALUE ZEROES.          
           05  WS-OCCP-AC-SIBLING-CNT         PIC 9(01)  VALUE ZEROES.          
           05  WS-OCCP-MC-SIBLING-CNT         PIC 9(01)  VALUE ZEROES.          
           05  WS-OCCP-PARENT-CNT             PIC 9(01)  VALUE ZEROES.          
           05  WS-OCCP-GRANDPR-CNT            PIC 9(01)  VALUE ZEROES.          
           05  WS-HA-CHILD-CNT                PIC 9(01)  VALUE ZEROES.          
           05  WS-CHILD-CNT                   PIC 9(01)  VALUE ZEROES.          
           05  WS-ADULT-CHILD-CNT             PIC 9(01)  VALUE ZEROES.          
           05  WS-SPR-GRANDPR-CNT             PIC 9(01)  VALUE ZEROES.          
           05  WS-SPR-PARENT-CNT              PIC 9(01)  VALUE ZEROES.          
           05  WS-LESSEE-TAGGED               PIC 9(01)  VALUE ZEROES.          
                                                                                
       01  WS-ALL-SWITCHES.                                                     
           05  WS-MINOR-SPOUSE                PIC X(04)  VALUE 'N'.             
           05  WS-SC-SPOUSE                   PIC X(01)  VALUE 'N'.             
           05  WS-OCCP-SPOUSE                 PIC X(01)  VALUE 'N'.             
           05  WS-SINGLE-APPL                 PIC X(01)  VALUE 'N'.             
           05  WS-FIANCE-APL                  PIC X(01)  VALUE 'N'.             
           05  WS-MARRIED-APPL                PIC X(01)  VALUE 'N'.             
           05  WS-SINGLE-PARENT-APPL          PIC X(01)  VALUE 'N'.             
                                                                                
           05  WS-RELATIONSHIP-SW             PIC X(02).                        
               88 APPLICANT                   VALUE '00'.                       
               88 PARENT                      VALUE '02' '12' '19' '22'         
                                                    '32' '97' '98'.             
               88 SIBLING                     VALUE '06' '07' '26' '27'.        
               88 NEPHEW-NIECE                VALUE '08'.                       
               88 SPOUSE                      VALUE '20' '10'.                  
               88 FIANCEE                     VALUE '09'.                       
               88 ADP-CHILD                   VALUE '39'.                       
               88 CHILD                       VALUE '41'.                       
               88 SP-CHILD                    VALUE '42'.                       
               88 GRANDCHILD                  VALUE '43'.                       
               88 GRANDPARENT                 VALUE '01' '11' '21' '31'.        
                                                                                
           05  WS-CITIZENSHIP-SW              PIC X(02).                        
               88 SC                          VALUE '10'.                       
               88 SPR                         VALUE '20'.                       
                                                                                
           05  WS-AGE-SW                      PIC 9(02).                        
               88 WS-AGE-20                   VALUE 1 THRU 20.                  
                                                                                
           05  WS-MARITAL-STAT-SW             PIC X(01).                        
               88 SINGLE                      VALUE '1' 'S'.                    
               88 MARRIED                     VALUE '2' 'M'.                    
               88 DIV-WID-SEP                 VALUE '3' '4' '5'                 
                                                    'W' 'D' 'P'.                
           05  WS-SEX-SW                      PIC X(01).                        
               88 MALE                        VALUE '1' 'M'.                    
               88 FEMALE                      VALUE '2' 'F'.                    
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC              PIC 9(2).                         
                   15  WS-SYS-YY              PIC 9(2).                         
               10  WS-SYS-MM                  PIC 9(2).                         
               10  WS-SYS-DD                  PIC 9(2).                         
           05  WS-CUR-CCYY                    PIC 9(4).                         
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-MM               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-YY               PIC 9(4).                         
                                                                                
      *============================================================*            
      *            BP13LD06 - R E P O R T    L A Y O U T           *            
      *============================================================*            
                                                                                
       01  LD06-HEADER-01.                                                      
           05  FILLER              PIC X(08)     VALUE 'BP13LD06'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(41)     VALUE                          
               'S Y S T E M    O F    C O M M I T M E N T'.                     
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  LD06-RUN-DATE       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LD06-PAGE-NO        PIC ZZZZ9 .                                  
                                                                                
       01  LD06-HEADER-02.                                                      
           05  FILLER              PIC X(44)     VALUE SPACES.                  
           05  FILLER              PIC X(46)     VALUE                          
           'REPORT ON OCCUPIERS AFTER TAGGING NUM-TAG2'.                        
                                                                                
       01  LD06-HEADER-03.                                                      
           05  FILLER              PIC X(05)     VALUE ' S/N '.                 
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'REF NO'.                
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'ALC SCH'.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'ELG SCH'.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE 'NT'.                    
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE 'FT'.                    
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'CAT'.                   
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE 'APPL/'.                 
           05  FILLER              PIC X(41)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'REMARKS'.               
                                                                                
       01  LD06-HEADER-04.                                                      
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'NRIC'.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(49)     VALUE 'NAME'.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'DOB'.                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'OCC '.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'REL'.                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE 'RELATIONSHIP'.          
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE 'MS'.                    
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'CIT'.                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'SEX'.                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'TAG2'.                  
                                                                                
       01  LD06-HEADER-05.                                                      
           05  FILLER              PIC X(130)    VALUE ALL '-'.                 
                                                                                
       01  LD06-DETAIL-01.                                                      
           05  LD06-SNO            PIC ZZZZ9.                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LD06-REF-NO         PIC X(08).                                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LD06-ALLOC-SCH      PIC X(03).                                   
           05  FILLER              PIC X(07)     VALUE SPACES.                  
           05  LD06-ELIG-SCH       PIC X(03).                                   
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  LD06-NT             PIC X(02).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LD06-FT             PIC X(02).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  LD06-CAT            PIC X(01).                                   
           05  FILLER              PIC X(68)     VALUE SPACES.                  
           05  LD06-REMARKS        PIC X(10).                                   
                                                                                
       01  LD06-DETAIL-02.                                                      
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LD06-NRIC           PIC X(09).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LD06-NAME           PIC X(46).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LD06-DOB            PIC X(08).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LD06-HA-OCCP        PIC X(04).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LD06-REL-CODE       PIC X(03).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LD06-REL-DESC       PIC X(20).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  LD06-MARITAL-STAT   PIC X(01).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LD06-CITIZENSHIP    PIC X(02).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  LD06-SEX            PIC X(01).                                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LD06-TAG2           PIC X(05).                                   
                                                                                
                                                                                
      *==================*                                                      
       PROCEDURE DIVISION.                                                      
      *==================*                                                      
       0000-MAIN-ROUTINE.                                                       
      *==================*                                                      
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
           PERFORM 2000-READ-F730         THRU 2000-EXIT.                       
           PERFORM 2500-READ-F825         THRU 2500-EXIT.                       
           MOVE BP13F825-REC              TO WS-SAVE-BP13F825.                  
                                                                                
           PERFORM 3000-PROCESS-F730                                            
              THRU 3000-EXIT                                                    
             UNTIL (WS-F730-EOF-SW = 'Y')  OR                                   
                   (WS-F825-EOF-SW = 'Y').                                      
                                                                                
           PERFORM 9999-CLOSE-ROUTINE.                                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==================*                                                      
       1000-OPEN-ROUTINE.                                                       
      *==================*                                                      
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13K060                                                 
                I-O    BP13F825                                                 
                       BP13K825                                                 
                OUTPUT BP13LD06.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYSTEM-DATE.                 
                                                                                
           MOVE   WS-SYS-DD           TO WS-EDITED-DD.                          
           MOVE   WS-SYS-MM           TO WS-EDITED-MM.                          
           MOVE   WS-SYS-CCYY         TO WS-EDITED-YY                           
                                         WS-CUR-CCYY.                           
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K060 OPEN ERROR, STATUS IS ' WS-K060-STATUS          
              GO TO   9999-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K825 OPEN ERROR, STATUS IS ' WS-K825-STATUS          
              GO TO   9999-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           MOVE   WS-EDITED-DATE      TO LD06-RUN-DATE.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *===============*                                                         
       2000-READ-F730.                                                          
      *===============*                                                         
                                                                                
           READ BP13F730                                                        
              AT END MOVE 'Y'   TO WS-F730-EOF-SW.                              
                                                                                
           ADD  +1                 TO WS-CNT-READ-F730.                         
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *===============*                                                         
       2500-READ-F825.                                                          
      *===============*                                                         
                                                                                
           READ BP13F825                                                        
              AT END     MOVE 'Y'  TO WS-F825-EOF-SW.                           
                                                                                
           ADD +1                  TO WS-CNT-READ-F825.                         
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==================*                                                      
       3000-PROCESS-F730.                                                       
      *==================*                                                      
                                                                                
           MOVE ZEROS              TO WS-NUM-FAMILY.                            
           MOVE SPACES             TO WS-NUCLEUS-TAGS.                          
           INITIALIZE                 WS-F825-ARRAY-VARS.                       
           MOVE WS-SAVE-BP13F825   TO BP13F825-REC.                             
                                                                                
           MOVE ZEROES             TO WS-COUNTERS.                              
           INITIALIZE                 WS-ALL-SWITCHES                           
                                      WS-REMARKS.                               
                                                                                
           IF F730-NUM-REGN = F825-NUM-REGN                                     
              PERFORM 4000-PROCESS-NUCLEUS THRU 4000-EXIT                       
                UNTIL (F825-NUM-REGN > F730-NUM-REGN) OR                        
                      (WS-NUM-FAMILY > 15) OR                                   
                      (WS-F825-EOF-SW = 'Y')                                    
                                                                                
              MOVE BP13F825-REC            TO WS-SAVE-BP13F825                  
                                                                                
              PERFORM 5000-TAG-AND-PRINT   THRU 5000-EXIT                       
              VARYING WS-OCCUP-TAG FROM 1 BY 1                                  
                UNTIL (WS-OCCUP-TAG > WS-NUM-FAMILY)                            
                                                                                
              MOVE ZEROES             TO WS-COUNTERS                            
              INITIALIZE                 WS-ALL-SWITCHES                        
              PERFORM 2000-READ-F730       THRU 2000-EXIT                       
                                                                                
           ELSE                                                                 
           IF F730-NUM-REGN > F825-NUM-REGN                                     
              INITIALIZE                 WS-ALL-SWITCHES                        
              PERFORM 2500-READ-F825    THRU 2500-EXIT                          
                UNTIL (F825-NUM-REGN  = F730-NUM-REGN) OR                       
                      (F825-NUM-REGN  > F730-NUM-REGN) OR                       
                      (WS-F825-EOF-SW = 'Y')                                    
                                                                                
              MOVE BP13F825-REC    TO WS-SAVE-BP13F825                          
                                                                                
           ELSE                                                                 
              INITIALIZE                  WS-ALL-SWITCHES                       
                                          WS-REMARKS                            
              PERFORM 2000-READ-F730      THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=====================*                                                   
       4000-PROCESS-NUCLEUS.                                                    
      *=====================*                                                   
                                                                                
           ADD 1                   TO WS-NUM-FAMILY.                            
           MOVE BP13F825-REC       TO WS-BP13F825-REC (WS-NUM-FAMILY).          
                                                                                
           PERFORM 5110-COMPUTE-AGE     THRU 5110-EXIT.                         
           MOVE F825-NUM-RELATIONSHIP   TO WS-RELATIONSHIP-SW.                  
           MOVE F825-NUM-CITIZENSHIP    TO WS-CITIZENSHIP-SW.                   
           MOVE F825-NUM-MARITAL-STATUS TO WS-MARITAL-STAT-SW.                  
           MOVE WS-AGE             TO WS-AGE-SW.                                
                                                                                
      * DETERMINE IF SC/SP CHILD                                                
           IF CHILD AND (SC OR SPR)                                             
              IF (WS-AGE >= +21 ) AND (WS-AGE <  +999)                          
                 ADD  1            TO WS-ADULT-CHILD-CNT                        
              END-IF                                                            
              ADD  1            TO WS-CHILD-CNT                                 
              IF (F825-NUM-NRIC = F730-NUM-NRIC1 OR F730-NUM-NRIC2 OR           
                                  F730-NUM-NRIC3 OR F730-NUM-NRIC4)             
                  ADD 1         TO WS-HA-CHILD-CNT                              
              END-IF                                                            
           END-IF                                                               
                                                                                
      * DETERMINE MARRIED HA1                                                   
           IF SPOUSE OR FIANCEE                                                 
              MOVE 'Y'             TO WS-HA1-MARRIED                            
              IF (WS-AGE <= +20 )                                               
                  MOVE 'Y'         TO WS-MINOR-SPOUSE                           
              END-IF                                                            
                                                                                
      * DETERMINE WHETHER SPOUSE IS SC/SPR OR NOT:                              
      * FOR DBSS ONLY SC SPOUSE CAN BE TAGGED                                   
      * FOR NON-DBSS EITHER SC/SPR SPOUSE CAN BE TAGGED                         
              IF (SC OR SPR)                                                    
                  MOVE 'Y'         TO WS-SC-SPOUSE                              
              ELSE                                                              
                  MOVE 'N'         TO WS-SC-SPOUSE                              
              END-IF                                                            
                                                                                
              IF (F825-NUM-NRIC = F730-NUM-NRIC1 OR F730-NUM-NRIC2 OR           
                                  F730-NUM-NRIC3 OR F730-NUM-NRIC4)             
                  CONTINUE                                                      
              ELSE                                                              
                  MOVE 'Y'         TO WS-OCCP-SPOUSE                            
              END-IF                                                            
                                                                                
              IF FIANCEE                                                        
                 MOVE 'Y'          TO WS-MARRIED-APPL                           
                 MOVE 'N'          TO WS-SINGLE-APPL                            
                                      WS-SINGLE-PARENT-APPL                     
                 MOVE 'Y'          TO WS-FIANCE-APL                             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF APPLICANT                                                         
              IF (F825-NUM-NRIC = F730-NUM-NRIC1 OR F730-NUM-NRIC2 OR           
                                  F730-NUM-NRIC3 OR F730-NUM-NRIC4)             
                  CONTINUE                                                      
              ELSE                                                              
                 MOVE 'Y'          TO WS-OCCP-SPOUSE                            
              END-IF                                                            
              IF DIV-WID-SEP                                                    
                 MOVE 'Y'          TO WS-SINGLE-PARENT-APPL                     
              ELSE                                                              
                 IF SINGLE                                                      
                    IF WS-FIANCE-APL = 'Y'                                      
                       MOVE 'N'    TO WS-SINGLE-APPL                            
                       MOVE 'Y'    TO WS-MARRIED-APPL                           
                    ELSE                                                        
                       MOVE 'Y'    TO WS-SINGLE-APPL                            
                    END-IF                                                      
                 ELSE                                                           
                    MOVE 'Y'       TO WS-MARRIED-APPL                           
                    MOVE 'N'       TO WS-SINGLE-APPL                            
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
      * PROCESS OCCUPIER RECORD IF HA                                           
           IF F825-NUM-NRIC = F730-NUM-NRIC1 OR F730-NUM-NRIC2 OR               
                              F730-NUM-NRIC3 OR F730-NUM-NRIC4                  
              NEXT SENTENCE                                                     
           ELSE                                                                 
              IF GRANDPARENT                                                    
                 ADD  1            TO WS-OCCP-GRANDPR-CNT                       
              ELSE                                                              
                 IF PARENT                                                      
                    ADD  1         TO WS-OCCP-PARENT-CNT                        
                 ELSE                                                           
                    IF SIBLING                                                  
                       ADD  1          TO WS-SIBLING-CNT                        
                       IF (SC OR SPR) AND (WS-AGE > +20 )                       
                           ADD  1      TO WS-OCCP-AC-SIBLING-CNT                
                       ELSE                                                     
                           ADD  1      TO WS-OCCP-MC-SIBLING-CNT                
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
              GO TO 4000-READNEXT                                               
           END-IF.                                                              
                                                                                
      * DETERMINE APPLN BY HA RELATIONSHIP                                      
           IF GRANDPARENT                                                       
              MOVE 'Y'             TO WS-HA-GRAND                               
              ADD  1               TO WS-HA-GRANDPR-CNT                         
              IF SPR                                                            
                 ADD  1            TO WS-SPR-GRANDPR-CNT                        
              END-IF                                                            
           ELSE                                                                 
           IF PARENT                                                            
              MOVE 'Y'             TO WS-HA-PARENT                              
              ADD  1               TO WS-HA-PARENT-CNT                          
              IF SPR                                                            
                 ADD  1            TO WS-SPR-PARENT-CNT                         
              END-IF                                                            
           ELSE                                                                 
           IF SIBLING AND (SC OR SPR) AND                                       
              (WS-AGE > +20 )                                                   
              MOVE 'Y'             TO WS-HA-SIBLING                             
              ADD  1               TO WS-HA-SIBLING-CNT                         
           ELSE                                                                 
           IF CHILD OR SP-CHILD                                                 
              MOVE 'Y'             TO WS-HA-CHILD                               
              IF MARRIED                                                        
                 MOVE 'Y'          TO WS-HA-CHILD-MARRIED                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-READNEXT.                                                           
           PERFORM 2500-READ-F825  THRU 2500-EXIT.                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *===================*                                                     
       5000-TAG-AND-PRINT.                                                      
      *===================*                                                     
                                                                                
           PERFORM 5100-TAG-NUM-TAG2       THRU 5100-EXIT.                      
                                                                                
           PERFORM 5200-UPDATE-K825        THRU 5200-EXIT.                      
                                                                                
           PERFORM 5300-PRINT-LD06         THRU 5300-EXIT.                      
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==================*                                                      
       5100-TAG-NUM-TAG2.                                                       
      *==================*                                                      
                                                                                
           MOVE WS-BP13F825-REC (WS-OCCUP-TAG) TO BP13F825-REC.                 
                                                                                
           MOVE F825-NUM-RELATIONSHIP   TO WS-RELATIONSHIP-SW.                  
           MOVE F825-NUM-MARITAL-STATUS TO WS-MARITAL-STAT-SW.                  
           MOVE F825-NUM-SEX            TO WS-SEX-SW.                           
                                                                                
           IF F730-CDE-ELIG-SCH = 'NCS'                                         
              MOVE 'ERROR-SCH'       TO WS-REMARKS                              
              GO TO 5100-EXIT.                                                  
                                                                                
           IF SPOUSE OR FIANCEE                                                 
              CONTINUE                                                          
           ELSE                                                                 
              IF (F825-NUM-NRIC NOT = F730-NUM-NRIC1 AND F730-NUM-NRIC2         
                                  AND F730-NUM-NRIC3 AND F730-NUM-NRIC4)        
                  PERFORM 5105-CHECK-OCCP    THRU 5105-EXIT                     
                  GO TO 5100-EXIT                                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *  COMPULSORY  TAG                                                        
                                                                                
           IF APPLICANT OR SPOUSE OR FIANCEE                                    
              PERFORM 5120-UPDATE-TAG   THRU 5120-EXIT                          
              GO  TO  5100-EXIT                                                 
           END-IF.                                                              
                                                                                
      *  GRANDPARENT AS HA                                                      
           IF WS-HA1-MARRIED NOT = 'Y'                                          
              IF WS-HA-GRAND = 'Y'                                              
                 IF GRANDPARENT                                                 
                    PERFORM 5120-UPDATE-TAG   THRU 5120-EXIT                    
                    GO TO 5100-EXIT                                             
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *  (PARENT AS HA) OR (HA1 SINGLE)                                         
           IF (WS-HA-PARENT        = 'Y') OR                                    
              (WS-HA1-MARRIED  NOT = 'Y') AND                                   
              (WS-SC-SP-CHILD  NOT = 'Y')                                       
              IF PARENT OR SIBLING                                              
                 PERFORM 5120-UPDATE-TAG   THRU 5120-EXIT                       
                 GO TO 5100-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *  SON/DAUGHTER, DAU/SON-IN-LAW, G-CHILD AS HA                            
           IF WS-HA-CHILD = 'Y'                                                 
              IF (CHILD OR ADP-CHILD OR GRANDCHILD) AND                         
                 (WS-AGE                > +20)                                  
                  PERFORM 5120-UPDATE-TAG   THRU 5120-EXIT                      
                  GO TO 5100-EXIT                                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *  BRO/SIS, NEPHEW/NIECE AS HA                                            
           IF WS-HA-SIBLING = 'Y'                                               
              IF SIBLING OR NEPHEW-NIECE                                        
                 PERFORM 5120-UPDATE-TAG   THRU 5120-EXIT                       
                 GO TO 5100-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
      *  CHILD ABOVE 21                                                         
           IF CHILD OR ADP-CHILD                                                
              PERFORM 5120-UPDATE-TAG   THRU 5120-EXIT                          
           END-IF.                                                              
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================*                                                        
       5105-CHECK-OCCP.                                                         
      *================*                                                        
                                                                                
           IF WS-MARRIED-APPL = 'Y'                                             
              IF (WS-SC-SPOUSE = 'Y') AND                                       
                 (WS-MINOR-SPOUSE NOT = 'Y') AND                                
                 (WS-OCCP-SPOUSE = 'Y')                                         
                 MOVE 'ERROR1'          TO WS-REMARKS                           
                 GO TO 5105-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-SINGLE-APPL = 'Y' OR WS-SINGLE-PARENT-APPL = 'Y'               
              IF WS-OCCP-PARENT-CNT > 0                                         
                 MOVE 'ERROR2'          TO WS-REMARKS                           
                 GO TO 5105-EXIT                                                
              END-IF                                                            
                                                                                
              IF WS-HA-CHILD-CNT = 0 AND WS-ADULT-CHILD-CNT > 0                 
                 MOVE 'ERROR3'          TO WS-REMARKS                           
                 GO TO 5105-EXIT                                                
              END-IF                                                            
                                                                                
              IF WS-HA-SIBLING-CNT > 0 AND WS-OCCP-PARENT-CNT = 1               
                 MOVE 'ERROR4'          TO WS-REMARKS                           
                 GO TO 5105-EXIT                                                
              END-IF                                                            
                                                                                
              IF WS-HA-PARENT-CNT = 0 AND                                       
                 WS-HA-SIBLING-CNT = 0 AND                                      
                 WS-OCCP-AC-SIBLING-CNT > 0                                     
                 MOVE 'ERROR5'          TO WS-REMARKS                           
                 GO TO 5105-EXIT                                                
              END-IF                                                            
                                                                                
              IF WS-HA-PARENT-CNT = 0 AND WS-OCCP-PARENT-CNT = 0                
                 IF WS-OCCP-GRANDPR-CNT > 0                                     
                    MOVE 'ERROR6'          TO WS-REMARKS                        
                    GO TO 5105-EXIT                                             
                 END-IF                                                         
                                                                                
                 IF WS-HA-SIBLING-CNT > 0 AND WS-OCCP-GRANDPR-CNT = 1           
                    MOVE 'ERROR7'          TO WS-REMARKS                        
                    GO TO 5105-EXIT                                             
                 END-IF                                                         
                                                                                
                 IF WS-HA-GRANDPR-CNT = 0 AND                                   
                    WS-HA-SIBLING-CNT = 0 AND                                   
                    WS-OCCP-AC-SIBLING-CNT > 0                                  
                    MOVE 'ERROR8'          TO WS-REMARKS                        
                    GO TO 5105-EXIT                                             
                 END-IF                                                         
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
       5105-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=================*                                                       
       5110-COMPUTE-AGE.                                                        
      *=================*                                                       
                                                                                
           MOVE ZEROS                  TO WS-AGE.                               
                                                                                
      * USE +999 AS AGE FOR SPECIAL CASES                                       
                                                                                
           IF F825-NUM-NRIC (1:1) = 'F' OR 'X'                                  
              MOVE +999                TO WS-AGE                                
              GO TO 5110-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F825-NUM-NRIC (2:2) < '68'                                        
              MOVE +999                TO WS-AGE                                
              IF F825-NUM-RELATIONSHIP = '41'                                   
                 MOVE F825-DTE-BIRTH(1:4) TO WS-BIRTH-YY                        
                 COMPUTE WS-AGE = WS-CUR-CCYY - WS-BIRTH-YY                     
              END-IF                                                            
              GO TO 5110-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE F825-NUM-NRIC (2:2)     TO WS-BIRTH-YY.                         
                                                                                
           COMPUTE WS-AGE = WS-SYS-YY - WS-BIRTH-YY.                            
                                                                                
      * FOR WS-SYS-YY IN 21ST CENTURY                                           
           IF WS-AGE IS NEGATIVE                                                
              ADD +100   TO WS-AGE                                              
           END-IF.                                                              
                                                                                
       5110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================*                                                        
       5120-UPDATE-TAG.                                                         
      *================*                                                        
                                                                                
           MOVE SPACES        TO F825-NUM-TAG2.                                 
                                                                                
      *  FOR MARRIED APPLICANTS                                                 
           IF WS-MARRIED-APPL = 'Y'                                             
              PERFORM 6500-PROCESS-MARRIED-APL       THRU 6500-EXIT             
              GO TO 5120-EXIT                                                   
           END-IF.                                                              
                                                                                
      *  FOR SINGLE/SINGLE PARENT APPLICANTS                                    
           IF WS-SINGLE-PARENT-APPL = 'Y' OR                                    
              WS-SINGLE-APPL = 'Y'                                              
              IF WS-CHILD-CNT > 0                                               
                 PERFORM 6600-PROCESS-SNGLE-PARENT-APL THRU 6600-EXIT           
              ELSE                                                              
                 PERFORM 6800-PROCESS-SINGLE-APL       THRU 6800-EXIT           
              END-IF                                                            
              GO TO 5120-EXIT                                                   
           END-IF.                                                              
                                                                                
       5120-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       5200-UPDATE-K825.                                                        
      *************************************************************             
                                                                                
           MOVE WS-SYSTEM-DATE         TO F825-DTE-UPDATE.                      
           MOVE 'P13CD06'              TO F825-NUM-USERID.                      
           MOVE BP13F825-REC           TO BP13K825-REC.                         
                                                                                
           REWRITE BP13K825-REC.                                                
                                                                                
           IF WS-K825-STATUS = ZEROS                                            
              ADD +1  TO WS-CNT-UPD-K825                                        
           ELSE                                                                 
              DISPLAY 'BP13K825 UPDATE ERROR, STATUS IS ' WS-K825-STATUS        
              DISPLAY 'BP13K825 RECORD KEY IS ' F825-NUM-REGN                   
           END-IF.                                                              
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       5300-PRINT-LD06.                                                         
      *************************************************************             
                                                                                
           IF WS-LD06-LINECTR > 49                                              
              PERFORM 5310-PRINT-HEADING   THRU 5310-EXIT                       
           END-IF.                                                              
                                                                                
           ADD +1        TO WS-LD06-LINECTR.                                    
                                                                                
           IF WS-OCCUP-TAG = 1                                                  
              ADD +1                       TO WS-LD06-SNO                       
              MOVE WS-LD06-SNO             TO LD06-SNO                          
              MOVE F730-NUM-REGN           TO LD06-REF-NO                       
              MOVE F730-CDE-ALLOC-SCH      TO LD06-ALLOC-SCH                    
              MOVE F730-CDE-ELIG-SCH       TO LD06-ELIG-SCH                     
              MOVE F730-CDE-NT1            TO LD06-NT                           
              MOVE F730-CDE-FLAT-TYPE      TO LD06-FT                           
              MOVE F730-CDE-CATEGORY       TO LD06-CAT                          
              MOVE WS-REMARKS              TO LD06-REMARKS                      
                                                                                
              WRITE BP13LD06-REC         FROM LD06-DETAIL-01                    
                                        AFTER ADVANCING 2 LINES                 
              ADD   +2                     TO WS-LD06-LINECTR                   
           END-IF.                                                              
                                                                                
           PERFORM 5320-GET-REL-DESC     THRU 5320-EXIT.                        
           PERFORM 5325-GET-CITIZENSHIP  THRU 5325-EXIT.                        
           PERFORM 5330-GET-MARITAL-STAT THRU 5330-EXIT.                        
           PERFORM 5335-GET-SEX          THRU 5335-EXIT.                        
                                                                                
           EVALUATE TRUE                                                        
           WHEN F825-NUM-NRIC = F730-NUM-NRIC1                                  
              MOVE 'HA1'                   TO LD06-HA-OCCP                      
           WHEN F825-NUM-NRIC = F730-NUM-NRIC2                                  
              MOVE 'HA2'                   TO LD06-HA-OCCP                      
           WHEN F825-NUM-NRIC = F730-NUM-NRIC3                                  
              MOVE 'HA3'                   TO LD06-HA-OCCP                      
           WHEN F825-NUM-NRIC = F730-NUM-NRIC4                                  
              MOVE 'HA4'                   TO LD06-HA-OCCP                      
           WHEN OTHER                                                           
              MOVE 'OCCP'                  TO LD06-HA-OCCP                      
           END-EVALUATE.                                                        
                                                                                
           MOVE F825-NUM-NRIC              TO LD06-NRIC.                        
           MOVE F825-NME-OCCP              TO LD06-NAME.                        
           MOVE F825-DTE-BIRTH             TO LD06-DOB.                         
           MOVE F825-NUM-RELATIONSHIP      TO LD06-REL-CODE.                    
           MOVE F825-NUM-TAG2              TO LD06-TAG2.                        
                                                                                
           WRITE BP13LD06-REC            FROM LD06-DETAIL-02                    
                                        AFTER ADVANCING 1 LINE.                 
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       5310-PRINT-HEADING.                                                      
      *************************************************************             
                                                                                
           ADD +1                  TO WS-LD06-PAGECTR.                          
           MOVE ZERO               TO WS-LD06-LINECTR.                          
                                                                                
           MOVE WS-LD06-PAGECTR    TO LD06-PAGE-NO.                             
           MOVE WS-EDITED-DATE     TO LD06-RUN-DATE.                            
                                                                                
           WRITE BP13LD06-REC    FROM LD06-HEADER-01                            
                                AFTER ADVANCING PAGE.                           
                                                                                
           WRITE BP13LD06-REC    FROM LD06-HEADER-02                            
                                AFTER ADVANCING 1 LINE.                         
                                                                                
           WRITE BP13LD06-REC    FROM LD06-HEADER-03                            
                                AFTER ADVANCING 2 LINES.                        
                                                                                
           WRITE BP13LD06-REC    FROM LD06-HEADER-04                            
                                AFTER ADVANCING 1 LINE.                         
                                                                                
           WRITE BP13LD06-REC    FROM LD06-HEADER-05                            
                                AFTER ADVANCING 1 LINE.                         
                                                                                
       5310-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       5320-GET-REL-DESC.                                                       
      ****************************************************************          
                                                                                
           MOVE SPACES                TO BP13K060-REC.                          
           MOVE '35'                  TO K060-SERIAL-NO.                        
           MOVE F825-NUM-RELATIONSHIP TO K060-CODE.                             
           PERFORM 5400-READ-K060     THRU 5400-EXIT.                           
           MOVE K060-DESC             TO LD06-REL-DESC.                         
                                                                                
       5320-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5325-GET-CITIZENSHIP.                                                    
      ****************************************************************          
                                                                                
           MOVE SPACES                TO BP13K060-REC.                          
           MOVE '41'                  TO K060-SERIAL-NO.                        
           MOVE K825-NUM-CITIZENSHIP  TO K060-CODE.                             
           PERFORM 5400-READ-K060     THRU 5400-EXIT.                           
           MOVE K060-DESC             TO LD06-CITIZENSHIP.                      
                                                                                
       5325-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5330-GET-MARITAL-STAT.                                                   
      ****************************************************************          
                                                                                
           IF F825-NUM-MARITAL-STATUS = 'P' OR '5'                              
              MOVE 'P'                  TO LD06-MARITAL-STAT                    
              GO TO 5330-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE SPACES                  TO BP13K060-REC.                        
           MOVE '21'                    TO K060-SERIAL-NO.                      
           MOVE F825-NUM-MARITAL-STATUS TO K060-CODE.                           
           PERFORM 5400-READ-K060       THRU 5400-EXIT.                         
           MOVE K060-DESC               TO LD06-MARITAL-STAT.                   
                                                                                
       5330-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5335-GET-SEX.                                                            
      ****************************************************************          
                                                                                
           MOVE SPACES                  TO BP13K060-REC.                        
           MOVE '22'                    TO K060-SERIAL-NO.                      
           MOVE F825-NUM-SEX            TO K060-CODE.                           
           PERFORM 5400-READ-K060       THRU 5400-EXIT.                         
           MOVE K060-DESC               TO LD06-SEX.                            
                                                                                
       5335-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5400-READ-K060.                                                          
      ****************************************************************          
           READ BP13K060 KEY IS K060-KEY-FLD.                                   
                                                                                
           IF WS-K060-STATUS = 0                                                
              CONTINUE                                                          
           ELSE                                                                 
           IF WS-K060-STATUS = 23                                               
              DISPLAY 'BP13K060 RECORD NOT FOUND, KEY IS ' K060-KEY-FLD         
              DISPLAY 'F730-KEY-FLD:' F730-KEY-FLD                              
           ELSE                                                                 
              DISPLAY 'BP13K060 READ ERROR, STATUS IS ' WS-K060-STATUS          
              DISPLAY 'K060-KEY-FLD:' K060-KEY-FLD                              
                    '& F730-KEY-FLD:' F730-KEY-FLD                              
           END-IF.                                                              
                                                                                
       5400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       6500-PROCESS-MARRIED-APL.                                                
      *************************************************************             
      *  FOR MARRIED APPLICANTS                                                 
                                                                                
           IF ((WS-HA-GRANDPR-CNT = WS-MAX-CNT)   AND                           
               (WS-SPR-GRANDPR-CNT < WS-MAX-CNT)) OR                            
              ((WS-HA-PARENT-CNT = WS-MAX-CNT)    AND                           
               (WS-SPR-PARENT-CNT < WS-MAX-CNT))  OR                            
              ((WS-HA-PARENT-CNT = WS-MIN-CNT)    AND                           
               (WS-HA-SIBLING-CNT = WS-MIN-CNT))                                
                MOVE SPACES          TO F825-NUM-TAG2                           
                MOVE 'OPTION-MA'     TO WS-REMARKS                              
                GO TO 6500-EXIT                                                 
           END-IF.                                                              
                                                                                
      **** IF ONLY ONE COUPLE IS LISTED AS HA'S ****                            
           IF (WS-SC-SPOUSE = 'Y') AND                                          
              (WS-MINOR-SPOUSE NOT = 'Y')                                       
               IF WS-OCCP-SPOUSE NOT = 'Y'                                      
                  IF APPLICANT OR SPOUSE OR FIANCEE                             
                     IF MALE                                                    
                        MOVE 'SMCHH'     TO F825-NUM-TAG2                       
                        MOVE SPACES      TO WS-REMARKS                          
                        GO TO 6500-EXIT                                         
                     ELSE                                                       
                        IF FEMALE                                               
                           MOVE 'SMCWH'     TO F825-NUM-TAG2                    
                           MOVE SPACES      TO WS-REMARKS                       
                           GO TO 6500-EXIT                                      
                        END-IF                                                  
                     END-IF                                                     
                  END-IF                                                        
               ELSE                                                             
                  MOVE SPACES      TO F825-NUM-TAG2                             
                  MOVE 'ERROR-SP'  TO WS-REMARKS                                
                  GO TO 6500-EXIT                                               
               END-IF                                                           
           ELSE                                                                 
               IF APPLICANT                                                     
                  IF MALE                                                       
                     MOVE 'SMCHF'     TO F825-NUM-TAG2                          
                     MOVE SPACES      TO WS-REMARKS                             
                     GO TO 6500-EXIT                                            
                  ELSE                                                          
                     IF FEMALE                                                  
                        MOVE 'SMCWF'     TO F825-NUM-TAG2                       
                        MOVE SPACES      TO WS-REMARKS                          
                        GO TO 6500-EXIT                                         
                     END-IF                                                     
                  END-IF                                                        
               END-IF                                                           
               IF SPOUSE OR FIANCEE                                             
                  IF MALE                                                       
                     MOVE 'SOSHN'     TO F825-NUM-TAG2                          
                     MOVE SPACES      TO WS-REMARKS                             
                     GO TO 6500-EXIT                                            
                  ELSE                                                          
                     IF FEMALE                                                  
                        MOVE 'SOSWN'     TO F825-NUM-TAG2                       
                        MOVE SPACES      TO WS-REMARKS                          
                        GO TO 6500-EXIT                                         
                     END-IF                                                     
                  END-IF                                                        
               END-IF                                                           
           END-IF.                                                              
                                                                                
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       6600-PROCESS-SNGLE-PARENT-APL.                                           
      *************************************************************             
      *  FOR SINGLE PARENT APPLICANTS                                           
                                                                                
           IF WS-HA-CHILD = 'Y' AND WS-HA-PARENT = 'Y'                          
              MOVE SPACES                  TO F825-NUM-TAG2                     
              MOVE 'OPTION-SPR'            TO WS-REMARKS                        
              GO TO 6600-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF WS-HA-CHILD = 'Y' AND WS-HA-PARENT NOT = 'Y'                      
              IF WS-HA-CHILD-MARRIED = 'Y'                                      
                 MOVE SPACES         TO F825-NUM-TAG2                           
                 MOVE 'MAR-CHILD'    TO WS-REMARKS                              
                 GO TO 6600-EXIT                                                
              END-IF                                                            
                                                                                
              IF WS-OCCP-PARENT-CNT > 0                                         
                 MOVE 'ERROR-HAPR'      TO WS-REMARKS                           
                 MOVE SPACES            TO F825-NUM-TAG2                        
                 GO TO 6600-EXIT                                                
              END-IF                                                            
                                                                                
              IF WS-HA-CHILD-CNT < WS-MAX-CNT                                   
                 IF WS-LESSEE-TAGGED < WS-MAX-CNT                               
                    IF APPLICANT                                                
                       MOVE 'SPCPH'        TO F825-NUM-TAG2                     
                       ADD 1               TO WS-LESSEE-TAGGED                  
                       GO TO 6600-EXIT                                          
                    ELSE                                                        
                       IF CHILD                                                 
                          MOVE 'SPCCH'        TO F825-NUM-TAG2                  
                          ADD 1               TO WS-LESSEE-TAGGED               
                          GO TO 6600-EXIT                                       
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF WS-HA-CHILD-CNT >= WS-MAX-CNT                                  
                 MOVE 'OPT-HACH'           TO WS-REMARKS                        
                 MOVE SPACES               TO F825-NUM-TAG2                     
                 GO TO 6600-EXIT                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-HA-CHILD NOT = 'Y'                                             
              IF WS-HA-PARENT = 'Y'                                             
                 MOVE SPACES               TO WS-REMARKS                        
                 IF WS-OCCP-PARENT-CNT > 0                                      
                    MOVE 'ERROR-HAPR'      TO WS-REMARKS                        
                    MOVE SPACES            TO F825-NUM-TAG2                     
                    GO TO 6600-EXIT                                             
                 END-IF                                                         
                 IF APPLICANT                                                   
                    MOVE 'SPCPF'           TO F825-NUM-TAG2                     
                    MOVE SPACES            TO WS-REMARKS                        
                    GO TO 6600-EXIT                                             
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF WS-HA-PARENT NOT = 'Y'                                         
                 IF WS-OCCP-PARENT-CNT > 0                                      
                    MOVE 'ERROR-OPR'       TO WS-REMARKS                        
                    GO TO 6600-EXIT                                             
                 END-IF                                                         
                                                                                
                 IF WS-ADULT-CHILD-CNT > 0                                      
                    MOVE 'ERROR-ADCH'      TO WS-REMARKS                        
                    GO TO 6600-EXIT                                             
                 END-IF                                                         
                                                                                
                 IF APPLICANT                                                   
                    MOVE 'SPCPF'           TO F825-NUM-TAG2                     
                    MOVE SPACES            TO WS-REMARKS                        
                    GO TO 6600-EXIT                                             
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF (WS-HA-PARENT-CNT = 0 AND WS-OCCP-PARENT-CNT = 0) AND             
              (WS-HA-GRANDPR-CNT > 0 OR WS-OCCP-GRANDPR-CNT > 0)                
              PERFORM 6700-CHK-SNGLE-PARENT-APL-GRPR THRU 6700-EXIT             
           ELSE                                                                 
              MOVE SPACES      TO F825-NUM-TAG2                                 
              MOVE 'NO-TAG2'   TO WS-REMARKS                                    
           END-IF.                                                              
                                                                                
       6600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       6700-CHK-SNGLE-PARENT-APL-GRPR.                                          
      *************************************************************             
                                                                                
           IF WS-HA-CHILD = 'Y' AND WS-HA-GRAND = 'Y'                           
              MOVE SPACES                  TO F825-NUM-TAG2                     
              MOVE 'OPTION-SPR'            TO WS-REMARKS                        
              GO TO 6700-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF WS-HA-CHILD = 'Y' AND WS-HA-GRAND NOT = 'Y'                       
              IF WS-HA-CHILD-MARRIED = 'Y'                                      
                 MOVE SPACES         TO F825-NUM-TAG2                           
                 MOVE 'MAR-CHILDG'   TO WS-REMARKS                              
                 GO TO 6700-EXIT                                                
              END-IF                                                            
                                                                                
              IF WS-HA-CHILD-CNT < WS-MAX-CNT                                   
                 IF WS-LESSEE-TAGGED < WS-MAX-CNT                               
                    IF APPLICANT                                                
                       MOVE 'SPCPH'        TO F825-NUM-TAG2                     
                       MOVE SPACES         TO WS-REMARKS                        
                       ADD 1               TO WS-LESSEE-TAGGED                  
                       GO TO 6700-EXIT                                          
                    ELSE                                                        
                       IF CHILD                                                 
                          MOVE 'SPCCH'        TO F825-NUM-TAG2                  
                          MOVE SPACES         TO WS-REMARKS                     
                          ADD 1               TO WS-LESSEE-TAGGED               
                          GO TO 6700-EXIT                                       
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF WS-HA-CHILD-CNT >= WS-MAX-CNT                                  
                 MOVE 'OPTION'             TO WS-REMARKS                        
                 IF APPLICANT                                                   
                    MOVE 'SPCPH'           TO F825-NUM-TAG2                     
                    GO TO 6700-EXIT                                             
                 END-IF                                                         
                                                                                
                 IF CHILD                                                       
                    MOVE SPACES            TO F825-NUM-TAG2                     
                    GO TO 6700-EXIT                                             
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-HA-CHILD NOT = 'Y'                                             
              IF WS-HA-GRAND = 'Y'                                              
                 MOVE SPACES               TO WS-REMARKS                        
                 IF APPLICANT                                                   
                    MOVE 'SPCPF'           TO F825-NUM-TAG2                     
                    GO TO 6700-EXIT                                             
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF WS-HA-GRAND NOT = 'Y'                                          
                 IF WS-OCCP-GRANDPR-CNT > 0                                     
                    MOVE 'ERROR-OPR'       TO WS-REMARKS                        
                    GO TO 6700-EXIT                                             
                 END-IF                                                         
                                                                                
                 IF WS-ADULT-CHILD-CNT > 0                                      
                    MOVE 'ERROR-ADCH'      TO WS-REMARKS                        
                    GO TO 6700-EXIT                                             
                 END-IF                                                         
                                                                                
                 IF APPLICANT                                                   
                    MOVE 'SPCPF'           TO F825-NUM-TAG2                     
                    MOVE SPACES            TO WS-REMARKS                        
                    GO TO 6700-EXIT                                             
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       6700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       6800-PROCESS-SINGLE-APL.                                                 
      *************************************************************             
      *  FOR SINGLE APPLICANTS                                                  
                                                                                
           IF WS-HA-PARENT-CNT > 0                                              
              IF WS-HA-PARENT-CNT = WS-MAX-CNT                                  
                 IF WS-SPR-PARENT-CNT >= WS-MAX-CNT                             
                    MOVE 'ERROR-SPR'     TO WS-REMARKS                          
                    GO TO 6800-EXIT                                             
                 END-IF                                                         
                                                                                
                 IF APPLICANT                                                   
                    MOVE SPACES       TO F825-NUM-TAG2                          
                    GO TO 6800-EXIT                                             
                 END-IF                                                         
                                                                                
                 IF PARENT                                                      
                    IF F825-NUM-MARITAL-STATUS = 'M' OR '2'                     
                       IF F825-NUM-SEX = 'M' OR '1'                             
                          MOVE 'SMCHH'      TO F825-NUM-TAG2                    
                          MOVE SPACES       TO WS-REMARKS                       
                          GO TO 6800-EXIT                                       
                       ELSE                                                     
                          IF F825-NUM-SEX = 'F' OR '2'                          
                             MOVE 'SMCWH'      TO F825-NUM-TAG2                 
                             MOVE SPACES       TO WS-REMARKS                    
                             GO TO 6800-EXIT                                    
                          END-IF                                                
                       END-IF                                                   
                    ELSE                                                        
                       MOVE 'SPCPH'      TO F825-NUM-TAG2                       
                       MOVE SPACES       TO WS-REMARKS                          
                       GO TO 6800-EXIT                                          
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF WS-HA-PARENT-CNT = WS-MIN-CNT                                  
                 IF WS-HA-SIBLING-CNT = 0                                       
                    IF WS-OCCP-PARENT-CNT = 0                                   
                       IF PARENT                                                
                          MOVE 'SPCPH'   TO F825-NUM-TAG2                       
                          MOVE SPACES    TO WS-REMARKS                          
                          GO TO 6800-EXIT                                       
                       ELSE                                                     
                          IF APPLICANT                                          
                             MOVE 'SPCCH'   TO F825-NUM-TAG2                    
                             MOVE SPACES    TO WS-REMARKS                       
                             GO TO 6800-EXIT                                    
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                                                                                
                    IF WS-OCCP-PARENT-CNT > 0                                   
                       MOVE 'ERROR-PR1'    TO WS-REMARKS                        
                       GO TO 6800-EXIT                                          
                    END-IF                                                      
                 END-IF                                                         
                                                                                
                 IF WS-HA-SIBLING-CNT > 0                                       
                    IF WS-OCCP-PARENT-CNT = WS-MIN-CNT                          
                       MOVE SPACES       TO F825-NUM-TAG2                       
                       MOVE 'ERROR-PR2'  TO WS-REMARKS                          
                       GO TO 6800-EXIT                                          
                    ELSE                                                        
                       MOVE SPACES       TO F825-NUM-TAG2                       
                       MOVE 'OPTION-PR'  TO WS-REMARKS                          
                       GO TO 6800-EXIT                                          
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-HA-PARENT-CNT = 0                                              
              IF WS-OCCP-PARENT-CNT > 0                                         
                 MOVE SPACES           TO F825-NUM-TAG2                         
                 MOVE 'ERROR-PR3'      TO WS-REMARKS                            
                 GO TO 6800-EXIT                                                
              END-IF                                                            
                                                                                
              IF WS-OCCP-AC-SIBLING-CNT > 0                                     
                 IF WS-HA-SIBLING-CNT = 0                                       
                    MOVE 'ERROR-SIB'   TO WS-REMARKS                            
                    MOVE SPACES        TO F825-NUM-TAG2                         
                    GO TO 6800-EXIT                                             
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF WS-OCCP-AC-SIBLING-CNT = 0 AND                                 
                 WS-OCCP-MC-SIBLING-CNT > 0                                     
                 IF APPLICANT                                                   
                    MOVE 'SORSF'    TO F825-NUM-TAG2                            
                    GO TO 6800-EXIT                                             
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF WS-HA-SIBLING-CNT >= WS-MAX-CNT                                
                 MOVE 'OPTION'      TO WS-REMARKS                               
                 MOVE SPACES        TO F825-NUM-TAG2                            
                 GO TO 6800-EXIT                                                
              END-IF                                                            
                                                                                
              IF WS-HA-SIBLING-CNT = WS-MIN-CNT                                 
                 IF APPLICANT OR SIBLING                                        
                    MOVE 'SORSH'  TO F825-NUM-TAG2                              
                    MOVE SPACES   TO WS-REMARKS                                 
                    GO TO 6800-EXIT                                             
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF (WS-HA-PARENT-CNT = 0 AND WS-OCCP-PARENT-CNT = 0) AND             
              (WS-HA-GRANDPR-CNT > 0 OR WS-OCCP-GRANDPR-CNT > 0)                
              PERFORM 6900-PROCESS-SINGLE-APL-GRPR  THRU 6900-EXIT              
           ELSE                                                                 
              MOVE SPACES      TO F825-NUM-TAG2                                 
              MOVE 'NO-TAG3'   TO WS-REMARKS                                    
           END-IF.                                                              
                                                                                
       6800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       6900-PROCESS-SINGLE-APL-GRPR.                                            
      *************************************************************             
                                                                                
           IF WS-HA-GRANDPR-CNT > 0                                             
              IF WS-HA-GRANDPR-CNT = WS-MAX-CNT                                 
                 IF WS-SPR-GRANDPR-CNT >= WS-MAX-CNT                            
                    MOVE 'ERR-GRSPR'   TO WS-REMARKS                            
                    GO TO 6900-EXIT                                             
                 END-IF                                                         
                                                                                
                 IF APPLICANT                                                   
                    MOVE SPACES       TO F825-NUM-TAG2                          
                    GO TO 6900-EXIT                                             
                 END-IF                                                         
                                                                                
                 IF GRANDPARENT                                                 
                    MOVE 'SPCPH'      TO F825-NUM-TAG2                          
                    MOVE SPACES       TO WS-REMARKS                             
                    GO TO 6900-EXIT                                             
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF WS-HA-GRANDPR-CNT = WS-MIN-CNT                                 
                 IF WS-HA-SIBLING-CNT = 0                                       
                    IF WS-OCCP-GRANDPR-CNT = 0                                  
                       IF GRANDPARENT                                           
                          MOVE 'SPCPH'      TO F825-NUM-TAG2                    
                          MOVE SPACES       TO WS-REMARKS                       
                          GO TO 6900-EXIT                                       
                       ELSE                                                     
                          IF APPLICANT                                          
                             MOVE 'SPCCH'      TO F825-NUM-TAG2                 
                             MOVE SPACES       TO WS-REMARKS                    
                             GO TO 6900-EXIT                                    
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                                                                                
                    IF WS-OCCP-GRANDPR-CNT > 0                                  
                       MOVE 'ERR-GRPR1'  TO WS-REMARKS                          
                       GO TO 6900-EXIT                                          
                    END-IF                                                      
                 END-IF                                                         
                                                                                
                 IF WS-HA-SIBLING-CNT > 0                                       
                    IF WS-OCCP-GRANDPR-CNT = WS-MIN-CNT                         
                       MOVE SPACES       TO F825-NUM-TAG2                       
                       MOVE 'ERR-GRPR2'   TO WS-REMARKS                         
                       GO TO 6900-EXIT                                          
                    ELSE                                                        
                       MOVE SPACES       TO F825-NUM-TAG2                       
                       MOVE 'OPTION-GRPR'   TO WS-REMARKS                       
                       GO TO 6900-EXIT                                          
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-HA-GRANDPR-CNT = 0                                             
              IF WS-OCCP-GRANDPR-CNT > 0                                        
                 MOVE SPACES           TO F825-NUM-TAG2                         
                 MOVE 'ERR-GRPR3'    TO WS-REMARKS                              
                 GO TO 6900-EXIT                                                
              END-IF                                                            
                                                                                
              IF WS-OCCP-AC-SIBLING-CNT > 0                                     
                 IF WS-HA-SIBLING-CNT = 0                                       
                    MOVE 'ERROR-GRSIB' TO WS-REMARKS                            
                    MOVE SPACES        TO F825-NUM-TAG2                         
                    GO TO 6900-EXIT                                             
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF WS-OCCP-AC-SIBLING-CNT = 0 AND                                 
                 WS-OCCP-MC-SIBLING-CNT > 0                                     
                 IF APPLICANT                                                   
                    MOVE 'SORSF'    TO F825-NUM-TAG2                            
                    MOVE SPACES     TO WS-REMARKS                               
                    GO TO 6900-EXIT                                             
                 END-IF                                                         
              END-IF                                                            
                                                                                
              IF WS-HA-SIBLING-CNT >= WS-MAX-CNT                                
                 MOVE 'OPTION'      TO WS-REMARKS                               
                 MOVE SPACES        TO F825-NUM-TAG2                            
                 GO TO 6900-EXIT                                                
              END-IF                                                            
                                                                                
              IF WS-HA-SIBLING-CNT = WS-MIN-CNT                                 
                 IF APPLICANT OR SIBLING                                        
                    MOVE 'SORSH'     TO F825-NUM-TAG2                           
                    MOVE SPACES      TO WS-REMARKS                              
                    GO TO 6900-EXIT                                             
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE SPACES      TO F825-NUM-TAG2.                                   
           MOVE 'NO-TAG4'   TO WS-REMARKS.                                      
                                                                                
       6900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           CLOSE BP13F730                                                       
                 BP13F825                                                       
                 BP13K825                                                       
                 BP13K060                                                       
                 BP13LD06.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : ' WS-EDITED-DATE.                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13CD06 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F730 RECORDS READ    = ' WS-CNT-READ-F730.              
           DISPLAY 'BP13F825 RECORDS READ    = ' WS-CNT-READ-F825.              
           DISPLAY 'BP13K825 RECORDS UPDATED = ' WS-CNT-UPD-K825.               
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           STOP RUN.                                                            
                                                                                
