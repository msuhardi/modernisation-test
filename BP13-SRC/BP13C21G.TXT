       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C21G.                                                 
       AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
       DATE-WRITTEN.  03/08/2007.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT         (BP13)                        *            
      * ========================================================== *            
      *                                                            *            
      *  DESCRIPTION :  TO DELETE RECORDS BASED ON THE             *            
      *                 FOLLOWING TRANSACTION FILES:               *            
      *                                                            *            
      *                   DELETE                                   *            
      *  INPUT TRANS  :  BP13F240                                  *            
      *  I-O FILES    :  BP13K249                                  *            
      * ---------------------------------------------------------- *            
      * CHG-NO.  BY   ON       DESCRIPTION                         *            
      * -------- ---  -------- -----------                         *            
      * BP133094 RB12 20070803 NEW PROGRAM                         *            
      * BP133112 RB12 20070919 ADD DTE BALLOT AS KEY               *            
      * BP133367 RB12 20080425 INCREASE K249 LENGTH                *            
      * BP135135 ESA1 20131114 TO CATER FOR BONUS FLATS            *            
      * BP135415 ESA1 20140813 EXTEND BP13K249 TO 450              *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F240   ASSIGN TO BP13F240.                                
                                                                                
           SELECT BP13K249  ASSIGN       TO BP13K249                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K249-KEY-FLD                        
                            FILE STATUS  IS BP13K249-STATUS.                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BP13F240                                                            
            RECORDING MODE  F                                                   
            LABEL RECORDS   ARE STANDARD                                        
            BLOCK CONTAINS  0   RECORDS                                         
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K240.                                                           
                                                                                
       FD   BP13K249                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K249.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  BP13K249-STATUS           PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-PREV-NEW-TOWN          PIC X(20)  VALUE SPACES.               
           05  WS-PREV-FLAT-TYPE         PIC X(09)  VALUE SPACES.               
           05  WS-PREV-SELECTION         PIC X(03)  VALUE SPACES.               
           05  WS-PREV-DTE-BALLOT        PIC X(06)  VALUE SPACES.               
           05  WS-PREV-NUM-BONUS         PIC X(01)  VALUE SPACES.               
           05  WS-F240-READ-CTR          PIC 9(8)   VALUE ZEROES.               
           05  WS-K249-DELETE-CTR        PIC 9(8)   VALUE ZEROES.               
           05  WS-K249-DELETE-ERR        PIC 9(8)   VALUE ZEROES.               
           05  WS-F240-FLAG              PIC X(1)   VALUE 'N'.                  
               88 WS-F240-EOF            VALUE 'Y'.                             
                                                                                
       01  WS-EDT-DATE.                                                         
           05  WS-EDT-DD           PIC 99.                                      
           05  FILLER              PIC X      VALUE '/'.                        
           05  WS-EDT-MM           PIC 99.                                      
           05  FILLER              PIC X      VALUE '/'.                        
           05  WS-EDT-CCYY         PIC 9999.                                    
                                                                                
       01  WS-DTE-TMP              PIC X(10).                                   
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY         PIC 9999.                                    
           05  WS-CUR-MM           PIC 99.                                      
           05  WS-CUR-DD           PIC 99.                                      
                                                                                
       01  WS-LINER           PIC X(96) VALUE ALL '~'.                          
                                                                                
       01  WS-LINER1.                                                           
           10  FILLER         PIC X(90) VALUE ALL "-".                          
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE SECTION.                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 3000-READ-BP13F240      THRU 3000-EXIT                       
           PERFORM 2000-PROCESS-BP13F240   THRU 2000-EXIT                       
                   UNTIL WS-F240-EOF.                                           
           PERFORM 9000-CLOSE-RTN          THRU 9000-EXIT.                      
                                                                                
       0000-MAIN-EXIT.                                                          
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F240                                                 
                I-O    BP13K249.                                                
                                                                                
           IF BP13K249-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING BP13K249 : ' BP13K249-STATUS               
              MOVE BP13K249-STATUS             TO RETURN-CODE                   
              PERFORM 9000-CLOSE-RTN           THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
           MOVE WS-CUR-CCYY           TO WS-EDT-CCYY.                           
           MOVE WS-CUR-MM             TO WS-EDT-MM.                             
           MOVE WS-CUR-DD             TO WS-EDT-DD.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-PROCESS-BP13F240.                                                   
      *-------------------------------------------------------------            
                                                                                
           IF (WS-PREV-NEW-TOWN  NOT EQUAL K240-NUM-NEW-TOWN5) OR               
              (WS-PREV-FLAT-TYPE NOT EQUAL K240-NUM-FLAT-TYPE5) OR              
              (WS-PREV-SELECTION NOT EQUAL K240-NUM-SELECTION5) OR              
              (WS-PREV-DTE-BALLOT NOT EQUAL K240-DTE-BALLOT5) OR                
              (WS-PREV-NUM-BONUS  NOT EQUAL K240-NUM-BONUS5)                    
              MOVE SPACES              TO K249-KEY-FLD                          
              MOVE K240-NUM-NEW-TOWN5  TO K249-NUM-NEW-TOWN                     
                                          WS-PREV-NEW-TOWN                      
              MOVE K240-NUM-FLAT-TYPE5 TO K249-NUM-FLAT-TYPE                    
                                          WS-PREV-FLAT-TYPE                     
              MOVE K240-NUM-SELECTION5 TO K249-NUM-SELECTION                    
                                          WS-PREV-SELECTION                     
              MOVE K240-DTE-BALLOT5    TO K249-DTE-BALLOT                       
                                          WS-PREV-DTE-BALLOT                    
              MOVE K240-NUM-BONUS5     TO K249-NUM-BONUS                        
                                          WS-PREV-NUM-BONUS                     
              PERFORM 2100-DELETE-BP13K249 THRU 2100-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-BP13F240   THRU 3000-EXIT.                         
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2100-DELETE-BP13K249.                                                    
                                                                                
           READ BP13K249 KEY IS K249-KEY-FLD.                                   
                                                                                
      *    IF K249-DTE-BALLOT NOT EQUAL K240-DTE-BALLOT                         
      *       GO TO 2100-EXIT                                                   
      *    END-IF.                                                              
                                                                                
           EVALUATE BP13K249-STATUS                                             
           WHEN 00                                                              
                DELETE BP13K249                                                 
                IF BP13K249-STATUS = 00                                         
                   DISPLAY 'RECORD DELETED: ' K249-KEY-FLD                      
                   ADD 1 TO WS-K249-DELETE-CTR                                  
                ELSE                                                            
                   DISPLAY 'ERROR DELETE BP13K249 KEY: '                        
                           K249-KEY-FLD                                         
                   ADD 1 TO WS-K249-DELETE-ERR                                  
                END-IF                                                          
           WHEN OTHER                                                           
                DISPLAY 'RECORD NOT FOUND: ' K249-KEY-FLD                       
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3000-READ-BP13F240.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F240 AT END                                                 
                MOVE 'Y' TO WS-F240-FLAG                                        
                GO TO 3000-EXIT.                                                
           ADD 1 TO WS-F240-READ-CTR.                                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-RTN.                                                          
      *-------------------------------------------------------------            
           DISPLAY WS-LINER.                                                    
           DISPLAY ' PROGRAM : BP13C21G        '.                               
           DISPLAY ' PURPOSE : DELETE RECORDS FROM BP13K249 '.                  
           DISPLAY ' RUN DATE: ' WS-EDT-DATE.                                   
           DISPLAY WS-LINER.                                                    
           DISPLAY ' '.                                                         
           DISPLAY WS-LINER1.                                                   
           DISPLAY 'BP13F240 RECORDS READ    => ' WS-F240-READ-CTR.             
           DISPLAY 'BP13K249 RECORDS DELETED => ' WS-K249-DELETE-CTR.           
           DISPLAY 'BP13K249 DELETE ERRORS   => ' WS-K249-DELETE-ERR.           
           DISPLAY WS-LINER1.                                                   
                                                                                
           CLOSE BP13F240,                                                      
                 BP13K249.                                                      
                                                                                
           IF BP13K249-STATUS NOT = 0 AND 97                                    
              MOVE BP13K249-STATUS       TO RETURN-CODE                         
              DISPLAY 'BP13K249 CLOSE ERROR : ' BP13K249-STATUS                 
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
