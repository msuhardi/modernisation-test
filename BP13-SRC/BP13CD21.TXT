       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD21.                                                 
      *AUTHOR.        ED1.                                                      
      *DATE-WRITTEN.  09/04/2008.                                               
      ***************************************************************           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      ***************************************************************           
      * OBJECTIVE : CREATE F125 FOR CASES WITH ARREARS IN           *           
      *             BEGINNING OF THE MONTH                          *           
      *             CREATE TRAN INTRADJ IN OPTION 27                *           
      * ----------------------------------------------------------- *           
      * CHG REF  DATE       BY   DESCRIPTION                        *           
      * -------- ---------- ---- -----------                        *           
      * BP133209 09/04/2008 ED1  NEW PRGM                           *           
      * BP133638 20/07/2009 JB8  OMIT ZEROES INTEREST               *           
      ***************************************************************           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13FD60          ASSIGN BP13FD60.                            
           SELECT BP13F125          ASSIGN BP13F125.                            
           SELECT BP13LD21          ASSIGN BP13LD21.                            
           SELECT SY02F001          ASSIGN SY02F001.                            
                                                                                
           SELECT BP13KD00 ASSIGN TO BP13KD00                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS DYNAMIC                                         
                RECORD KEY   IS KD00-KEY-FLD                                    
                FILE STATUS  IS KD00-STATUS.                                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BP13FD60                RECORD CONTAINS 250 CHARACTERS              
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BP13FD60.                                                           
                                                                                
       FD   BP13F125                RECORD CONTAINS 150 CHARACTERS              
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BP13F125.                                                           
                                                                                
       FD   BP13KD00                RECORD CONTAINS 1600 CHARACTERS.            
       COPY BP13KD00.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13LD21                                                            
            RECORD CONTAINS   132 CHARACTERS                                    
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01   PRINT-REC             PIC X(132).                                   
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  CNT-LINE-ON-PAGE       PIC 9(02) VALUE 66.                           
       01  CNT-F125-WRITE         PIC 9(05) VALUE ZEROES.                       
       01  CNT-PRT-CASES          PIC 9(06) VALUE ZEROES.                       
       01  CNT-PAGE               PIC 9(03) VALUE ZEROES.                       
                                                                                
       01  WS-COUNTERS.                                                         
           05  KD00-STATUS                  PIC 9(02)  VALUE 00.                
           05  WS-FD60-READ                 PIC 9(06)  VALUE ZEROS.             
           05  WS-F125-WRITE                PIC 9(06)  VALUE ZEROS.             
           05  NO-KD00-REWRITE              PIC 9(06)  VALUE ZEROS.             
           05  WS-CNT-NOTFD                 PIC 9(06)  VALUE ZEROS.             
           05  WS-CNT-FOUND                 PIC 9(06)  VALUE ZEROS.             
           05  WS-KD00-EOF                  PIC X      VALUE 'N'.               
           05  WS-KD00-FND                  PIC X      VALUE 'N'.               
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-YY                    PIC X(04)  VALUE SPACES.            
           05  WS-SYS-MM                    PIC X(02)  VALUE SPACES.            
           05  WS-SYS-DD                    PIC X(02)  VALUE SPACES.            
                                                                                
       01  WS-TRANS-DTE                     PIC X(08)  VALUE SPACES.            
       01  WS-TRANS-DTE-NUM REDEFINES WS-TRANS-DTE.                             
           05  WS-TRANS-CCYY                PIC 9(04).                          
           05  WS-TRANS-MM                  PIC 9(02).                          
           05  WS-TRANS-DD                  PIC 9(02).                          
                                                                                
       01 WS-POS-AMT-OPEN-ARR-INST          PIC 9(6)V99.                        
       01 WS-POS-AMT-ARR-INTR               PIC 9(6)V99.                        
                                                                                
       01 WS-MONTH-NAME.                                                        
          05 WS-STRING PIC X(36) VALUE                                          
                'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'.                         
          05 WS-MONTH  REDEFINES WS-STRING OCCURS 12 TIMES PIC XXX.             
                                                                                
       01  LD21-PR-HEAD-01.                                                     
           05  FILLER             PIC X(08) VALUE 'BP13LD21'.                   
           05  FILLER             PIC X(05) VALUE SPACES.                       
           05  FILLER             PIC X(08) VALUE 'HDBCAT 3'.                   
           05  FILLER             PIC X(24) VALUE SPACES.                       
           05  FILLER             PIC X(39) VALUE                               
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER             PIC X(17) VALUE SPACES.                       
           05  FILLER             PIC X(06) VALUE 'DATE: '.                     
           05  LD21-DATE          PIC X(10).                                    
           05  FILLER             PIC X(03) VALUE SPACES.                       
           05  FILLER             PIC X(06) VALUE 'PAGE :'.                     
           05  LD21-PAGE          PIC ZZ9 .                                     
                                                                                
       01  LD21-PR-HEAD-02.                                                     
           05  FILLER             PIC X(47) VALUE SPACES.                       
           05  FILLER             PIC X(55) VALUE                               
               'REPORT ON ARREAR INTEREST ADJUSTMENT'.                          
                                                                                
       01  LD21-PR-HEAD-03.                                                     
           05  FILLER             PIC X(35)                                     
                  VALUE '  S/NO   REGN NO.   SCH ACCT       '.                  
           05  FILLER             PIC X(36)                                     
                  VALUE 'INTEREST($)     TOT PYMT TODTE($)'.                    
                                                                                
       01  LD21-PR-DETAILS.                                                     
           05  FILLER             PIC X(01) VALUE SPACES.                       
           05  LD21-SNO           PIC ZZZZZ9 .                                  
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LD21-REG-NO        PIC X(08).                                    
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LD21-SCH-ACC       PIC X(11).                                    
           05  FILLER             PIC X(01) VALUE SPACES.                       
           05  LD21-AMOUNT        PIC    $$$$,$$9.99DB.                         
           05  FILLER             PIC X(07) VALUE SPACES.                       
           05  LD21-AMT-TODTE     PIC    $$$$,$$9.99DB.                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      ***************************                                               
       0000-MAIN.                                                               
      ***************************                                               
           PERFORM 1000-OPEN-FILES           THRU  1000-EXIT.                   
           PERFORM 2000-READ-BP13FD60        THRU  2000-EXIT.                   
           PERFORM 3000-PROCESS              THRU  3000-EXIT                    
             UNTIL FD60-NUM-REGN = HIGH-VALUES.                                 
           PERFORM 9000-CLOSE-FILES          THRU  9000-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       1000-OPEN-FILES.                                                         
      ***************************                                               
           OPEN INPUT  BP13FD60                                                 
                I-O    BP13KD00                                                 
                       SY02F001                                                 
                OUTPUT BP13F125                                                 
                       BP13LD21.                                                
                                                                                
           READ SY02F001.                                                       
                                                                                
           STRING F001-CURR-DD '/'                                              
                  F001-CURR-MM '/'                                              
                  F001-CURR-CC F001-CURR-YY                                     
           DELIMITED BY SIZE INTO LD21-DATE.                                    
                                                                                
           MOVE F001-DTE-CURRENT  TO WS-SYSTEM-DATE.                            
           MOVE F001-DTE-CURRENT  TO WS-TRANS-DTE.                              
           MOVE    01             TO WS-TRANS-DD.                               
                                                                                
           IF KD00-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING BP13KD00 FILE ERROR ' KD00-STATUS                
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       2000-READ-BP13FD60.                                                      
      ***************************                                               
           READ BP13FD60                                                        
             AT END                                                             
                MOVE HIGH-VALUES            TO  FD60-NUM-REGN                   
            NOT AT END                                                          
                ADD   1                     TO  WS-FD60-READ                    
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       3000-PROCESS.                                                            
      ***************************                                               
           IF FD60-AMT-OPEN-ARR-INST IS NOT NUMERIC                             
              MOVE ZEROS TO FD60-AMT-OPEN-ARR-INST                              
           END-IF.                                                              
                                                                                
           MOVE ZEROS                  TO WS-POS-AMT-OPEN-ARR-INST.             
           MOVE FD60-AMT-OPEN-ARR-INST TO WS-POS-AMT-OPEN-ARR-INST.             
                                                                                
           IF FD60-AMT-OPEN-ARR-INST < ZEROS                                    
              PERFORM 4000-READ-BP13KD00     THRU 4000-EXIT                     
              IF WS-KD00-FND = 'Y'                                              
                 PERFORM 5000-PROCESS-BP13F125 THRU 5000-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FD60     THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       4000-READ-BP13KD00.                                                      
      ***************************                                               
           MOVE 'N'             TO WS-KD00-FND.                                 
           MOVE SPACES          TO KD00-KEY-FLD.                                
           INITIALIZE              KD00-KEY-FLD.                                
           MOVE FD60-NUM-REGN   TO KD00-KEY-FLD.                                
                                                                                
           READ BP13KD00.                                                       
                                                                                
           IF KD00-STATUS = 00 OR 02                                            
              MOVE 'Y' TO WS-KD00-FND                                           
              ADD   1  TO WS-CNT-FOUND                                          
           ELSE                                                                 
              IF KD00-STATUS = 23                                               
                 DISPLAY 'BP13KD00 RECORD NOT FOUND ' KD00-KEY-FLD              
                 ADD 1 TO WS-CNT-NOTFD                                          
              ELSE                                                              
                 DISPLAY 'BP13KD00 ERROR IN READ ' KD00-KEY-FLD                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       5000-PROCESS-BP13F125.                                                   
      ***************************                                               
           MOVE SPACES TO BP13F125-CASHTRAN.                                    
           INITIALIZE     BP13F125-CASHTRAN.                                    
                                                                                
           IF KD00-AMT-ARR-INTR      IS NOT NUMERIC                             
              MOVE ZEROS TO KD00-AMT-ARR-INTR                                   
           END-IF.                                                              
           MOVE KD00-AMT-ARR-INTR TO WS-POS-AMT-ARR-INTR.                       
                                                                                
           IF WS-POS-AMT-OPEN-ARR-INST < WS-POS-AMT-ARR-INTR                    
              MOVE WS-POS-AMT-OPEN-ARR-INST TO F125-AMT-INTEREST                
           ELSE                                                                 
              MOVE WS-POS-AMT-ARR-INTR      TO F125-AMT-INTEREST                
           END-IF.                                                              
                                                                                
           IF F125-AMT-INTEREST = 0                                             
              GO TO 5000-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE '69'                TO F125-CDE-TRANS-TYPE.                     
           MOVE FD60-NUM-REGN       TO F125-NUM-REGN.                           
           MOVE FD60-NUM-SCH-ACCNT  TO F125-NUM-SCH-ACCT.                       
           MOVE WS-TRANS-DTE        TO F125-DTE-TRANS.                          
           MOVE WS-TRANS-DTE        TO F125-DTE-POST.                           
           MOVE 'INTEREST'          TO F125-NUM-RECPT-JRNO.                     
           MOVE 'JVA'               TO F125-CDE-PAYMENT-TYPE.                   
           MOVE 'C'                 TO F125-CDE-CRDR.                           
                                                                                
           PERFORM 6000-UPDATE-KD00  THRU 6000-EXIT                             
           PERFORM 7000-WRITE-REPORT THRU 7000-EXIT                             
                                                                                
           WRITE      BP13F125-CASHTRAN.                                        
           ADD  1  TO WS-F125-WRITE.                                            
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       6000-UPDATE-KD00.                                                        
      ***************************                                               
           IF KD00-AMT-ARR-INTR       IS NOT NUMERIC                            
              MOVE ZEROS TO KD00-AMT-ARR-INTR                                   
           END-IF.                                                              
           IF KD00-AMT-TOT-INTR-TODTE IS NOT NUMERIC                            
              MOVE ZEROS TO KD00-AMT-TOT-INTR-TODTE                             
           END-IF.                                                              
                                                                                
           COMPUTE KD00-AMT-ARR-INTR =                                          
                   KD00-AMT-ARR-INTR - F125-AMT-INTEREST.                       
                                                                                
           COMPUTE KD00-AMT-TOT-INTR-TODTE =                                    
                   KD00-AMT-TOT-INTR-TODTE + F125-AMT-INTEREST.                 
                                                                                
           PERFORM 6100-REWRITE-KD00  THRU 6100-EXIT.                           
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       6100-REWRITE-KD00.                                                       
      ***************************                                               
            REWRITE BP13KD00-REC.                                               
            EVALUATE KD00-STATUS                                                
            WHEN 00                                                             
              ADD 1 TO NO-KD00-REWRITE                                          
                                                                                
            WHEN OTHER                                                          
                 DISPLAY 'BP13KD00 - REWRITE ERROR, STATUS: '                   
                          KD00-STATUS                                           
                          ' KEY IS ' KD00-KEY-FLD                               
            END-EVALUATE.                                                       
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       7000-WRITE-REPORT.                                                       
      ***************************                                               
           MOVE SPACES TO LD21-PR-DETAILS.                                      
           INITIALIZE     LD21-PR-DETAILS.                                      
                                                                                
           IF CNT-LINE-ON-PAGE > 46                                             
              MOVE 1 TO CNT-LINE-ON-PAGE                                        
              PERFORM 7100-REPORT-HEADING THRU 7100-EXIT                        
           ELSE                                                                 
              ADD 1 TO CNT-LINE-ON-PAGE                                         
           END-IF.                                                              
                                                                                
           ADD 1 TO CNT-PRT-CASES.                                              
                                                                                
           MOVE CNT-PRT-CASES       TO LD21-SNO.                                
           MOVE F125-NUM-REGN       TO LD21-REG-NO.                             
           MOVE F125-NUM-SCH-ACCT   TO LD21-SCH-ACC.                            
                                                                                
           IF F125-AMT-INTEREST IS NOT NUMERIC                                  
              MOVE ZEROES           TO F125-AMT-INTEREST                        
           END-IF.                                                              
           MOVE F125-AMT-INTEREST        TO LD21-AMOUNT.                        
           MOVE KD00-AMT-TOT-INTR-TODTE  TO LD21-AMT-TODTE.                     
                                                                                
           WRITE PRINT-REC          FROM LD21-PR-DETAILS.                       
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------*                                                    
       7100-REPORT-HEADING.                                                     
      *--------------------*                                                    
            ADD 1           TO CNT-PAGE.                                        
            MOVE CNT-PAGE   TO LD21-PAGE.                                       
            MOVE SPACES     TO PRINT-REC.                                       
            WRITE PRINT-REC AFTER PAGE.                                         
            WRITE PRINT-REC FROM LD21-PR-HEAD-01 AFTER 2.                       
            WRITE PRINT-REC FROM LD21-PR-HEAD-02 AFTER 3.                       
            WRITE PRINT-REC FROM LD21-PR-HEAD-03 AFTER 3.                       
            MOVE ALL '-'    TO PRINT-REC.                                       
            WRITE PRINT-REC.                                                    
            MOVE SPACES     TO PRINT-REC.                                       
            WRITE PRINT-REC AFTER 1.                                            
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ***************************                                               
       9000-CLOSE-FILES.                                                        
      ***************************                                               
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : ' WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-YY.         
           DISPLAY '**************** BP13CD21 **********************'.          
           DISPLAY ' BP13FD60 RECS READ     : ' WS-FD60-READ.                   
           DISPLAY ' BP13KD00 RECS UPDATED  : ' NO-KD00-REWRITE.                
           DISPLAY ' CASES NOT FND IN KD00  : ' WS-CNT-NOTFD.                   
           DISPLAY ' CASES FOUND   IN KD00  : ' WS-CNT-FOUND.                   
           DISPLAY ' BP13F125 RECS WRITTEN  : ' WS-F125-WRITE.                  
           DISPLAY '**************** -------- ***********************'.         
                                                                                
           CLOSE BP13FD60                                                       
                 BP13F125                                                       
                 SY02F001                                                       
                 BP13KD00                                                       
                 BP13LD21.                                                      
                                                                                
           IF KD00-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING BP13KD00 FILE ERROR ' KD00-STATUS                
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
      ********************** END OF PROGRAM ***************************         
