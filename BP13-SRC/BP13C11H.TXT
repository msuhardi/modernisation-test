      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C11H.                                                 
      *AUTHOR.        DEEPIKA NALLAM.                                           
      *DATE-WRITTEN.  23/04/2019.                                               
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : THIS PROGRAM WILL UPDATE THE SUMMARY COUNT  *           
      *                 FILE USE BY PORTAL.                         *           
      *                 PROGRAM WILL UPDATE THE SUMMARY REGISTRATION*           
      *                 NUMBER LIST FOR SOCA SUMMARY PORTAL.        *           
      *               : TP SIGNED CASES.                            *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13F410   -- TEMP FILE CONTAINS ALL OUTSTANDING CASES*           
      *                                                             *           
      *   OUTPUT FILES:                                             *           
      *   1.  BP13FB40 -- SUMMARY COUNT FILE                        *           
      *                                                             *           
      * REF      DATE        BY   DESCRIPTIONS                      *           
      * -------  --------    ---- ------------                      *           
      * BP136733 23/04/2019  DN8  NEW PROGRAM (CLONE OF BP13C11B)   *           
      * BP137741 26/04/2019  DN8  TO CORRECT THE WRITE STATEMENT    *           
      * =========================================================== *           
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
      *------------------------*                                                
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *------------------------*                                                
       INPUT-OUTPUT SECTION.                                                    
      *------------------------*                                                
       FILE-CONTROL.                                                            
      *------------------------*                                                
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            FILE STATUS   IS BM06K110-STATUS.                   
                                                                                
           SELECT BP13K410  ASSIGN        TO BP13K410                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K410-KEY-FLD                       
                            FILE STATUS   IS BP13K410-STATUS.                   
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS BP13K800-STATUS.                   
                                                                                
           SELECT BP13F410 ASSIGN TO BP13F410.                                  
                                                                                
           SELECT BP13FB40 ASSIGN TO BP13FB40.                                  
                                                                                
           SELECT BP13L11B ASSIGN TO BP13L11B.                                  
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
      *------------------------*                                                
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD BP13F410                                                              
            RECORD CONTAINS  2000  CHARACTERS                                   
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F410.                                                   00150000
                                                                                
       FD BP13K410                                                              
            RECORD CONTAINS  2000  CHARACTERS.                                  
       COPY BP13K410.                                                   00150000
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD BP13FB40                                                              
            RECORD CONTAINS 2000  CHARACTERS                                    
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13FB40.                                                   00150000
                                                                                
       FD BP13L11B                                                              
            RECORD CONTAINS 132  CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       01 PRINT-REC          PIC X(132).                                00150000
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  BM06K110-STATUS            PIC 9(2)       VALUE ZEROES.          
           05  BP13K410-STATUS            PIC 9(2)       VALUE ZEROES.          
           05  BP13K800-STATUS            PIC 9(2)       VALUE ZEROES.          
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FB40-WRITE              PIC 9(5)       VALUE ZEROES.          
           05  WS-CNT-F410-READ           PIC 9(5)       VALUE ZEROES.          
           05  WS-K110-NOTFND             PIC 9(5)       VALUE ZEROES.          
           05  WS-K410-NOTFND             PIC 9(5)       VALUE ZEROES.          
           05  WS-K800-NOTFND             PIC 9(5)       VALUE ZEROES.          
           05  WS-SKIP-COUNT              PIC 9(5)       VALUE ZEROES.          
           05  WS-PAGE-CNT                PIC 9(5)       VALUE ZERO.            
           05  WS-LINE-CNT                PIC 9(5)       VALUE ZERO.            
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F410-EOF                PIC X          VALUE 'N'.             
           05  WS-K110-REC-FND            PIC X          VALUE SPACES.          
           05  WS-K410-REC-FND            PIC X          VALUE SPACES.          
           05  WS-K800-REC-FND            PIC X          VALUE SPACES.          
           05  WS-SKIP-REC                PIC X          VALUE 'N'.             
           05  WS-ERROR-REPORT            PIC X          VALUE 'N'.             
                                                                                
       01  WS-TODAY-DTE                   PIC X(08) VALUE SPACES.               
       01  WS-ACCPT-DTE                   PIC X(08) VALUE SPACES.               
       01  WS-DTE-CUR.                                                          
           05  WS-CC                      PIC 99.                               
           05  WS-YY                      PIC 99.                               
           05  WS-MM                      PIC 99.                               
           05  WS-DD                      PIC 99.                               
                                                                                
       01  WS-SAVE-AREA.                                                        
           05  WS-REC-TYPE                PIC X.                                
           05  WS-LOAN-TAG                PIC X.                                
           05  WS-MONTH-OUT               PIC 9(2) VALUE ZEROES.                
           05  WS-ROOM-TYPE               PIC X(2) VALUE SPACES.                
           05  WS-REGN                    PIC X(8) VALUE SPACES.                
           05  WS-DATE-TEMP               PIC X(8) VALUE SPACES.                
           05  WS-CNT-F410.                                                     
               10 WS-F410-SA               PIC 9(5) VALUE ZEROES.               
               10 WS-F410-2R               PIC 9(5) VALUE ZEROES.               
               10 WS-F410-3R               PIC 9(5) VALUE ZEROES.               
               10 WS-F410-4R               PIC 9(5) VALUE ZEROES.               
               10 WS-F410-5R               PIC 9(5) VALUE ZEROES.               
               10 WS-F410-ER               PIC 9(5) VALUE ZEROES.               
                                                                                
       01  WS-SNO                    PIC 9(6)   VALUE ZEROES.                   
       01  WS-REMARK                 PIC X(50)  VALUE SPACES.                   
                                                                                
       01  L11B-ERR-HEAD-01.                                                    
           05  FILLER              PIC X(8)      VALUE 'BP13L11B'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE '        '.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(19)     VALUE                          
                                    'C O M M I T M E N T'.                      
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  L11B-DATE           PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  L11B-PAGENO         PIC ZZZZ9     VALUE ZEROES.                  
                                                                                
       01  L11B-ERR-HEAD-02.                                                    
           05  FILLER              PIC X(39)     VALUE SPACES .                 
           05  FILLER              PIC X(93)     VALUE                          
               ' ERROR LIST OF OUTSTANDING CASES (SOCA - APPLICATION)'.         
                                                                                
       01  L11B-ERR-HEAD-03.                                                    
           05  FILLER              PIC X(28)                                    
                  VALUE '   S/NO     SCH-ACCNT NO    '.                         
           05  FILLER              PIC X(34)                                    
                  VALUE 'REGN NO       ERROR DESCRIPTION'.                      
           05  FILLER              PIC X(25)     VALUE SPACES.                  
                                                                                
       01  L11B-ERR-DETAILS.                                                    
           05  LL1A-SNO            PIC ZZZZZ9    VALUE ZEROES.                  
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  LL1A-NUM-SCHACCNT   PIC X(11).                                   
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  LL1A-REG-NO         PIC X(8).                                    
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  LL1A-REMARK         PIC X(50)     VALUE SPACES.                  
           05  FILLER              PIC X(3)      VALUE SPACES.                  
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
                                                                                
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM 100-OPEN-FILES       THRU 100-EXIT.                          
           PERFORM 250-READ-BP13F410    THRU 250-EXIT                           
                   UNTIL WS-F410-EOF = 'Y'.                                     
                                                                                
           IF WS-F410-EOF = 'Y'                                                 
              PERFORM 450-WRITE-SA-AO THRU 450-EXIT                             
           END-IF.                                                              
                                                                                
           PERFORM 999-CLOSE-FILES      THRU 999-EXIT.                          
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN   INPUT  BM06K110                                               
                         BP13K410                                               
                         BP13K800                                               
                         BP13F410                                               
                  OUTPUT BP13FB40                                               
                         BP13L11B.                                              
                                                                                
           IF BM06K110-STATUS NOT = ZEROS   AND  97                             
               DISPLAY 'ERROR OPENING - BM06K110 : ' BM06K110-STATUS            
               MOVE BM06K110-STATUS TO RETURN-CODE                              
               PERFORM 999-CLOSE-FILES THRU 999-EXIT.                           
                                                                                
           IF BP13K410-STATUS NOT = ZEROS   AND  97                             
               DISPLAY 'ERROR OPENING - BP13K410 : ' BP13K410-STATUS            
               MOVE BP13K410-STATUS TO RETURN-CODE                              
               PERFORM 999-CLOSE-FILES THRU 999-EXIT.                           
                                                                                
           IF BP13K800-STATUS NOT = ZEROS   AND  97                             
               DISPLAY 'ERROR OPENING - BP13K800 : ' BP13K800-STATUS            
               MOVE BP13K800-STATUS TO RETURN-CODE                              
               PERFORM 999-CLOSE-FILES THRU 999-EXIT.                           
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-TODAY-DTE WS-DTE-CUR.               
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       250-READ-BP13F410.                                                       
      *-------------------------------------------------------------            
           READ BP13F410                                                        
                AT END                                                          
                MOVE HIGH-VALUES TO BP13F410-REC                                
                MOVE 'Y' TO  WS-F410-EOF                                        
                GO TO 250-EXIT.                                                 
           ADD 1 TO WS-CNT-F410-READ.                                           
           PERFORM 300-PROCESS-F410     THRU 300-EXIT.                          
                                                                                
       250-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       300-PROCESS-F410.                                                        
      *-------------------------------------------------------------            
                                                                                
           PERFORM 305-READ-BM06K110 THRU 305-EXIT.                             
           PERFORM 306-READ-BP13K410 THRU 306-EXIT.                             
           PERFORM 307-VALIDATE-REC  THRU 307-EXIT.                             
                                                                                
           PERFORM 515-EVALUATE-FLAT THRU 515-EXIT.                             
           PERFORM 550-SAVE-RMCNT    THRU 550-EXIT.                             
                                                                                
           IF WS-DATE-TEMP = SPACES OR LOW-VALUES OR ZEROES                     
              MOVE F410-DTE-SO-EFFECTIVE TO WS-DATE-TEMP                        
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       305-READ-BM06K110.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                    TO K110-REC.                          
           MOVE F410-NUM-SCH-ACC          TO K110-KEY-FLD.                      
           READ BM06K110.                                                       
                                                                                
           EVALUATE BM06K110-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   MOVE 'SCHEME ACCOUNT NOT FOUND ON FIS ' TO WS-REMARK         
                   ADD 1 TO WS-K110-NOTFND                                      
                            WS-SKIP-COUNT                                       
                   MOVE 'Y' TO WS-SKIP-REC                                      
                   PERFORM 800-ERROR-REPORT THRU 800-EXIT                       
              WHEN OTHER                                                        
                   MOVE BM06K110-STATUS    TO RETURN-CODE                       
                   DISPLAY 'ERROR READING BM06K110, STATUS = '                  
                                          BM06K110-STATUS                       
                   PERFORM 999-CLOSE-FILES THRU 999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       305-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       306-READ-BP13K410.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                    TO BP13K410-REC.                      
           MOVE F410-NUM-REGN             TO K410-KEY-FLD.                      
           READ BP13K410.                                                       
                                                                                
           EVALUATE BP13K410-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                   MOVE 'Y'  TO WS-K410-REC-FND                                 
                   CONTINUE                                                     
              WHEN 23                                                           
                   MOVE 'REGN NOT FOUND IN K410' TO WS-REMARK                   
                   ADD 1     TO WS-K410-NOTFND WS-SKIP-COUNT                    
                   MOVE 'Y'  TO WS-SKIP-REC                                     
                   PERFORM 800-ERROR-REPORT THRU 800-EXIT                       
              WHEN OTHER                                                        
                   MOVE BP13K410-STATUS    TO RETURN-CODE                       
                   DISPLAY 'ERROR READING BP13K410, STATUS = '                  
                                          BP13K410-STATUS                       
                   PERFORM 999-CLOSE-FILES THRU 999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       306-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       307-VALIDATE-REC.                                                        
      *-------------------------------------------------------------            
           IF F410-NUM-ALLO-CAT = 'BTO'                                         
              MOVE 'U'  TO WS-REC-TYPE                                          
           ELSE                                                                 
              MOVE 'V'  TO WS-REC-TYPE                                          
           END-IF                                                               
                                                                                
           IF WS-K410-REC-FND   = 'Y'                                           
              MOVE 'T'  TO WS-REC-TYPE                                          
           END-IF                                                               
                                                                                
           IF (K110-DTE-ACCEPTANCE = SPACES OR LOW-VALUES OR ZEROES) AND        
              (WS-SKIP-REC NOT = 'Y')                                           
              STRING 'E777: INVALID ACCPT DTE = '                               
                      K110-DTE-ACCEPTANCE DELIMITED BY SIZE                     
                      INTO WS-REMARK                                            
              MOVE 'Y' TO WS-SKIP-REC                                           
              PERFORM 800-ERROR-REPORT THRU 800-EXIT                            
           ELSE                                                                 
              MOVE  K110-DTE-ACCEPTANCE   TO  WS-ACCPT-DTE                      
           END-IF.                                                              
                                                                                
           IF F410-NUM-SCH-ACC = SPACES OR LOW-VALUES OR ZEROES                 
              MOVE 'NO SCHEME ACCOUNT ' TO WS-REMARK                            
              MOVE 'Y' TO WS-SKIP-REC                                           
              PERFORM 800-ERROR-REPORT THRU 800-EXIT                            
           END-IF.                                                              
                                                                                
       307-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       450-WRITE-SA-AO.                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES TO BP13FB40-REC.                                         
           INITIALIZE BP13FB40-REC.                                             
           MOVE WS-DATE-TEMP        TO FB40-RUN-DATE                            
                                      FB40-DTE-UPDATE                           
           MOVE 'T'                TO FB40-NUM-RECTYPE                          
           MOVE ' '                TO FB40-NUM-LOAN-TAG                         
           MOVE WS-F410-SA         TO FB40-SA-COUNT(1)                          
           MOVE WS-F410-2R         TO FB40-2ROOM-COUNT(1)                       
           MOVE WS-F410-3R         TO FB40-3ROOM-COUNT(1)                       
           MOVE WS-F410-4R         TO FB40-4ROOM-COUNT(1)                       
           MOVE WS-F410-5R         TO FB40-5ROOM-COUNT(1)                       
           MOVE WS-F410-ER         TO FB40-EROOM-COUNT(1)                       
           MOVE 'BP13C11H'         TO FB40-NUM-USERID                           
                                                                                
           WRITE BP13FB40-REC.                                                  
           ADD 1  TO WS-FB40-WRITE.                                             
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       515-EVALUATE-FLAT.                                                       
      *-------------------------------------------------------------            
           MOVE SPACES             TO BP13K800-MASTER                           
           MOVE F410-NUM-REGN      TO WS-REGN K800-NUM-REGN                     
           READ BP13K800                                                        
                                                                                
           EVALUATE BP13K800-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                   MOVE 'Y'  TO WS-K800-REC-FND                                 
              WHEN 23                                                           
                   MOVE 'REGN NOT FOUND IN K800' TO WS-REMARK                   
                   ADD   1   TO WS-K800-NOTFND                                  
                   MOVE 'Y'  TO WS-SKIP-REC                                     
                   PERFORM 800-ERROR-REPORT THRU 800-EXIT                       
              WHEN OTHER                                                        
                   MOVE BP13K800-STATUS    TO RETURN-CODE                       
                   DISPLAY 'ERROR READING BP13K800, STATUS = '                  
                                          BP13K800-STATUS                       
                   PERFORM 999-CLOSE-FILES THRU 999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
           IF K800-NUM-FLAT-TYPE = '1A' OR '2A'                                 
              MOVE 'SA'    TO   WS-ROOM-TYPE                                    
           ELSE                                                                 
              MOVE K800-NUM-FLAT-TYPE  TO WS-ROOM-TYPE                          
           END-IF.                                                              
                                                                                
       515-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       550-SAVE-RMCNT.                                                          
      *-------------------------------------------------------------            
                                                                                
           EVALUATE TRUE                                                        
              WHEN WS-ROOM-TYPE = 'SA'                                          
                   ADD  1      TO   WS-F410-SA                                  
                                                                                
              WHEN WS-ROOM-TYPE(1:1)= '2'                                       
                   ADD  1      TO   WS-F410-2R                                  
                   MOVE '2 ' TO WS-ROOM-TYPE                                    
              WHEN WS-ROOM-TYPE(1:1)= '3'                                       
                   ADD  1      TO   WS-F410-3R                                  
                   MOVE '3 ' TO WS-ROOM-TYPE                                    
              WHEN WS-ROOM-TYPE(1:1)= '4'                                       
                   ADD  1      TO   WS-F410-4R                                  
                   MOVE '4 ' TO WS-ROOM-TYPE                                    
              WHEN WS-ROOM-TYPE(1:1)= '5'                                       
                   ADD  1      TO   WS-F410-5R                                  
                   MOVE '5 ' TO WS-ROOM-TYPE                                    
              WHEN WS-ROOM-TYPE(1:1)= 'E'                                       
                   ADD  1      TO   WS-F410-ER                                  
                   MOVE 'E ' TO WS-ROOM-TYPE                                    
              WHEN OTHER                                                        
                   MOVE 12                 TO RETURN-CODE                       
                   DISPLAY 'ROOM TYPE ERROR FOR: ' F410-NUM-REGN                
                   DISPLAY 'ROOM TYPE = ' WS-ROOM-TYPE                          
                   PERFORM 999-CLOSE-FILES THRU 999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       550-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       800-ERROR-REPORT.                                                        
      *-------------------------------------------------------------            
           IF WS-LINE-CNT > 45                                                  
              MOVE 'N' TO WS-ERROR-REPORT                                       
              MOVE ZEROES TO WS-LINE-CNT                                        
           END-IF.                                                              
                                                                                
           IF WS-ERROR-REPORT = 'Y'                                             
              CONTINUE                                                          
           ELSE                                                                 
              PERFORM 810-ERROR-HEADING THRU 810-EXIT                           
           END-IF.                                                              
                                                                                
           ADD  1   TO   WS-SNO WS-LINE-CNT                                     
           MOVE WS-SNO            TO LL1A-SNO                                   
           MOVE F410-NUM-SCH-ACC  TO LL1A-NUM-SCHACCNT                          
           MOVE F410-NUM-REGN     TO LL1A-REG-NO                                
           MOVE WS-REMARK         TO LL1A-REMARK                                
           WRITE PRINT-REC FROM L11B-ERR-DETAILS.                               
      *    PERFORM 250-READ-BP13F410 THRU 250-EXIT.                             
                                                                                
       800-EXIT.                                                                
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       810-ERROR-HEADING.                                                       
      *-------------------------------------------------------------            
                                                                                
            ADD 1               TO WS-PAGE-CNT.                                 
            MOVE WS-PAGE-CNT    TO L11B-PAGENO.                                 
            STRING WS-DD '/' WS-MM '/' WS-CC WS-YY                              
                   DELIMITED BY SIZE INTO L11B-DATE.                            
            MOVE SPACES         TO PRINT-REC                                    
            WRITE PRINT-REC FROM L11B-ERR-HEAD-01 AFTER PAGE.                   
            WRITE PRINT-REC FROM L11B-ERR-HEAD-02 AFTER 2.                      
            WRITE PRINT-REC FROM L11B-ERR-HEAD-03 AFTER 2.                      
            INSPECT PRINT-REC REPLACING ALL SPACES BY LOW-VALUES.               
            INSPECT PRINT-REC REPLACING CHARACTERS BY '-'.                      
            INSPECT PRINT-REC REPLACING ALL LOW-VALUES BY SPACES.               
            WRITE PRINT-REC .                                                   
            MOVE SPACES TO PRINT-REC.                                           
            WRITE PRINT-REC AFTER 2.                                            
            MOVE 'Y'  TO WS-ERROR-REPORT.                                       
                                                                                
       810-EXIT.                                                                
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       999-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY '******  BP13C11H *************'.                            
           DISPLAY 'NO OF F410 READ      (F410) : ' WS-CNT-F410-READ.           
           DISPLAY 'NO OF K110 NTFOUND(SKIP)(K110) : ' WS-K110-NOTFND.          
           DISPLAY 'NO OF K410 NTFOUND(SKIP)(K410) : ' WS-K410-NOTFND.          
           DISPLAY 'NO OF K800 NTFOUND(SKIP)(K800) : ' WS-K800-NOTFND.          
           DISPLAY 'NO OF TOTAL REC SKIPPED     : ' WS-SKIP-COUNT.              
           DISPLAY 'NO OF FB40 WRITE     (FB40) : ' WS-FB40-WRITE.              
                                                                                
           CLOSE BM06K110                                                       
                 BP13K410                                                       
                 BP13K800                                                       
                 BP13F410                                                       
                 BP13FB40                                                       
                 BP13L11B.                                                      
                                                                                
           IF BM06K110-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BM06K110-STATUS ' BM06K110-STATUS          
            MOVE BM06K110-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13K410-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K410-STATUS ' BP13K410-STATUS          
            MOVE BP13K410-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K800-STATUS ' BP13K800-STATUS          
            MOVE BP13K800-STATUS TO RETURN-CODE.                                
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
            EXIT.                                                               
