       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CE02.                                                 
      *AUTHOR.        LSB1.                                                     
      *DATE-WRITTEN.  08/06/2009.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE :                                                *            
      *      COMPUTE AGE OF NRIC1 , OUTPUT AGE TO F730(468:4)      *            
      *      BASE ON BP13K800 , READ BP13K820                      *            
      *                                                            *            
      * INPUT  : BP13F800                                          *            
      *          BP13K820                                          *            
      * OUTPUT : P13F800A                                          *            
      *                                                            *            
      * CHG REF  BY   ON        DESCRIPTION                        *            
      * ======== ==== ========  ================================== *            
      * BP133792 LSB1 20070709  NEW PGM                            *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13F800   ASSIGN TO BP13F800.                                
                                                                                
           SELECT BP13K820   ASSIGN TO BP13K820                                 
                  ACCESS MODE       IS RANDOM                                   
                  ORGANIZATION      IS INDEXED                                  
                  RECORD KEY        IS K820-KEY-FLD                             
                  FILE STATUS       IS WS-K820-STATUS.                          
                                                                                
           SELECT P13F800A   ASSIGN TO P13F800A.                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   P13F800A                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   P13F800A-REC         PIC X(2000).                                   
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01 WS-F800-READ              PIC 9(8)    VALUE ZEROES.                   
       01 WS-K820-READ              PIC 9(8)    VALUE ZEROES.                   
       01 WS-FOUT-WROTE             PIC 9(8)    VALUE ZEROES.                   
       01 WS-FD                     PIC 9(8)    VALUE ZEROES.                   
       01 WS-NOTFD                  PIC 9(8)    VALUE ZEROES.                   
                                                                                
       01 WS-F800-EOF               PIC X       VALUE 'N'.                      
       01 WS-K820-EOF               PIC X       VALUE 'N'.                      
       01 WS-K820-STATUS            PIC 9(2)    VALUE 0.                        
                                                                                
       01 WS-TEMP-VAL               PIC Z(7)9.                                  
       01  WS-DTE-REQUEST.                                                      
           05  WS-REQUEST-CYM       PIC 9(6)  VALUE ZEROES.                     
           05  WS-REQUEST-DD        PIC 9(2)  VALUE ZEROES.                     
       01  WS-DTE-BIRTH.                                                        
           05  WS-BIRTH-CYM         PIC 9(6)  VALUE ZEROES.                     
           05  WS-BIRTH-DD          PIC 9(2)  VALUE ZEROES.                     
       01  WS-AGE                   PIC 9(6)  VALUE ZEROES.                     
       01  WS-NUM-AGE REDEFINES WS-AGE.                                         
           05 WS-APPLN-AGE          PIC 9(4).                                   
           05 WS-NUM-REMNG          PIC 9(2).                                   
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F800   THRU 2000-EXIT.                         
           PERFORM 4000-PROCESS-DATA    THRU 4000-EXIT                          
                   UNTIL WS-F800-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13K820                                                 
               OUTPUT  P13F800A.                                                
                                                                                
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   MOVE WS-K820-STATUS             TO RETURN-CODE               
                   DISPLAY 'OPEN BP13K820 FAIL. STATUS ' WS-K820-STATUS         
                   PERFORM 9000-CLOSE-ROUTINE                                   
           END-EVALUATE.                                                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F800                                                        
                AT END                                                          
                   MOVE 'Y'     TO WS-F800-EOF                                  
            NOT AT END                                                          
                   ADD 1        TO WS-F800-READ                                 
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE LOW-VALUES                         TO K820-KEY-FLD.             
           MOVE F800-NUM-REGN                      TO K820-NUM-REGN.            
           MOVE F800-NUM-NRIC1                     TO K820-NUM-NRIC.            
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
                   ADD 1                           TO WS-FD                     
                   PERFORM 5000-COMPUTE-AGE      THRU 5000-EXIT                 
              WHEN 23                                                           
                   ADD 1                           TO WS-NOTFD                  
                   MOVE 'Y'                        TO WS-K820-EOF               
                   MOVE ZEROES                     TO WS-AGE                    
                   PERFORM 6000-WRITE-RECORD     THRU 6000-EXIT                 
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING  BP13K820, STATUS '                   
                            WS-K820-STATUS                                      
                   PERFORM 9000-CLOSE-ROUTINE                                   
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2000-READ-BP13F800            THRU 2000-EXIT.                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-COMPUTE-AGE.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE ZEROS                              TO WS-AGE.                   
                                                                                
           IF K820-DTE-BIRTH NOT NUMERIC                                        
              MOVE ZEROES                          TO K820-DTE-BIRTH            
           END-IF                                                               
                                                                                
           MOVE K820-DTE-BIRTH                     TO WS-DTE-BIRTH.             
           MOVE F800-DTE-REGN                      TO WS-DTE-REQUEST.           
                                                                                
           COMPUTE WS-AGE = WS-REQUEST-CYM - WS-BIRTH-CYM.                      
                                                                                
           PERFORM 6000-WRITE-RECORD             THRU 6000-EXIT.                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-WRITE-RECORD.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-APPLN-AGE                 TO BP13F800-MASTER(37:4)           
           MOVE BP13F800-MASTER              TO P13F800A-REC.                   
           WRITE P13F800A-REC.                                                  
           ADD 1                             TO WS-FOUT-WROTE.                  
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY 'BP13CE02 CONTROL TOTAL        '.                            
           DISPLAY '-----------------------------------------'.                 
           DISPLAY ' '.                                                         
           MOVE WS-F800-READ       TO WS-TEMP-VAL.                              
           DISPLAY 'NO OF RECS READ FROM BP13F800  : ' WS-TEMP-VAL.             
           MOVE WS-FOUT-WROTE TO WS-TEMP-VAL.                                   
           DISPLAY 'NO OF RECS WRITTEN TO P13F800A : ' WS-TEMP-VAL.             
           DISPLAY 'NO OF FOUND                    : ' WS-FD.                   
           DISPLAY 'NO OF NOTFD                    : ' WS-NOTFD.                
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F800                                                       
                 BP13K820                                                       
                 P13F800A.                                                      
                                                                                
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   MOVE WS-K820-STATUS             TO RETURN-CODE               
                   DISPLAY 'OPEN BP13K820 FAIL. STATUS ' WS-K820-STATUS         
                   PERFORM 9000-CLOSE-ROUTINE                                   
           END-EVALUATE.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
