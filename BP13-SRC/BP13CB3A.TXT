      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13CB3A.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  24 MARCH 2016.                                            
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : THIS PROGRAM CREATES HOUSING GRANT CREDITING*           
      *                 FILE                                        *           
      *                                                             *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13FD10 -- VOUCHER TRANSACTION FILE                  *           
      *   2.  BP13KD05 -- SOC GRANT FILE                            *           
      *                                                             *           
      *   OUTPUT FILES:                                             *           
      *   1.  AB02F930 -- HDB GRANT FILE                            *           
      *   2.  B02F930A -- HDB GRANT FILE - DAILY                    *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136182 24/03/2016 FNP1 NEW PROGRAM                        *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13KD05 ASSIGN TO BP13KD05                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD05-KEY-FLD                               
                  FILE STATUS     IS BP13KD05-STATUS.                           
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS BP13K800-STATUS.                           
                                                                                
           SELECT BP13FD10   ASSIGN       TO BP13FD10.                          
                                                                                
           SELECT AB02F930   ASSIGN       TO AB02F930.                          
           SELECT B02F930A   ASSIGN       TO B02F930A.                          
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13KD05                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13KD05.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13FD10                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS 1400 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13FD10.                                                           
                                                                                
       FD   AB02F930                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY AB02F930.                                                   00055007
                                                                                
       FD   B02F930A                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  150 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       01  B02F930A-REC         PIC X(150).                             00055007
                                                                                
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-FD10-EOF                PIC X       VALUE SPACE.              
           05  WS-KD05-EOF                PIC X       VALUE SPACE.              
           05  WS-KD05-FND                PIC X       VALUE SPACE.              
           05  BP13KD05-STATUS            PIC 9(2)    VALUE ZEROES.             
           05  BP13K800-STATUS            PIC 9(2)    VALUE ZEROES.             
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT                     PIC 9       VALUE ZEROES.             
           05  WS-CTR                     PIC 9       VALUE ZEROES.             
           05  CNT-FD10-READ              PIC 9(5)    VALUE ZEROES.             
           05  CNT-KD05-FND               PIC 9(5)    VALUE ZEROES.             
           05  CNT-K800-FND               PIC 9(5)    VALUE ZEROES.             
           05  WS-WRITE-CNT-F930A         PIC 9(5)    VALUE ZEROS.              
           05  WS-WRITE-CNT-DETAIL-F930   PIC 9(5)    VALUE ZEROS.              
           05  WS-WRITE-CNT-TRAILER-F930  PIC 9(5)    VALUE ZEROS.              
           05  WS-CNT-REC                 PIC 9(5)    VALUE ZEROS.              
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-SCH-ACC                 PIC X(11).                            
           05  WS-AMT-GRNT                PIC 9(7)V99.                          
           05  WS-TAG-CHANNEL             PIC X(1).                             
           05  WS-AMT-THRESHOLD           PIC 9(7)V99.                          
                                                                                
       01  WS-DATE                        PIC X(08)   VALUE SPACES.             
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-FD10    THRU 2000-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-FD10-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13FD10                                                 
                       BP13KD05                                                 
                       BP13K800                                                 
                OUTPUT AB02F930                                                 
                       B02F930A.                                                
                                                                                
           IF BP13KD05-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13KD05 : ' BP13KD05-STATUS             
              MOVE BP13KD05-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING - BP13K800 : ' BP13K800-STATUS             
              MOVE BP13K800-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-FD10.                                                          
      *---------------*                                                         
                                                                                
           READ BP13FD10 AT END                                                 
                MOVE HIGH-VALUES TO BP13FD10-REC                                
                MOVE 'Y' TO WS-FD10-EOF                                         
                GO TO 2000-EXIT.                                                
           ADD 1 TO CNT-FD10-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
           MOVE ZEROES       TO WS-CNT   WS-AMT-GRNT.                           
           MOVE SPACES       TO WS-SCH-ACC  WS-TAG-CHANNEL.                     
                                                                                
           IF FD10-NUM-REGN(1:1) = '5' OR '6'                                   
              PERFORM 3100-PROCESS-REC     THRU 3100-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-FD10    THRU 2000-EXIT.                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3100-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           IF FD10-NUM-REGN(1:1) NOT = '6'                                      
              PERFORM 3500-READ-BP13K800   THRU 3500-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 4                  
                 OR  FD10-NUM-CPF-PAYEE(WS-CTR) = SPACES                        
              PERFORM 3200-READ-BP13KD05      THRU 3200-EXIT                    
              PERFORM 4000-CHECK-GRANT        THRU 4000-EXIT                    
              PERFORM 7000-WRITE-DETAILS      THRU 7000-EXIT                    
           END-PERFORM.                                                         
                                                                                
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3200-READ-BP13KD05.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                       TO KD05-KEY-FLD.                   
           MOVE FD10-NUM-REGN                TO KD05-NUM-REGN.                  
           MOVE FD10-NUM-CPF-PAYEE(WS-CTR)   TO KD05-NUM-NRIC.                  
                                                                                
           READ BP13KD05.                                                       
                                                                                
           IF BP13KD05-STATUS = 00 OR 02                                        
              IF KD05-AMT-THRESHOLD NOT NUMERIC                                 
                 MOVE ZEROES TO KD05-AMT-THRESHOLD                              
              END-IF                                                            
              MOVE KD05-AMT-THRESHOLD    TO WS-AMT-THRESHOLD                    
              ADD 1 TO CNT-KD05-FND                                             
           ELSE                                                                 
              IF BP13KD05-STATUS = 23                                           
                 DISPLAY 'REGNO NOT FOUND IN BP13KD05 : ' KD05-NUM-REGN         
                 MOVE ZEROES  TO WS-AMT-THRESHOLD                               
              ELSE                                                              
                 DISPLAY 'ERROR STARTING- BP13KD05 : ' BP13KD05-STATUS          
                 MOVE BP13KD05-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3500-READ-BP13K800.                                                      
      *---------------------*                                                   
                                                                                
           MOVE SPACES           TO BP13K800-MASTER.                            
           MOVE FD10-NUM-REGN    TO K800-NUM-REGN.                              
                                                                                
           READ BP13K800.                                                       
           IF BP13K800-STATUS = 0                                               
              MOVE K800-NUM-SCH-ACC TO WS-SCH-ACC                               
              ADD 1 TO CNT-K800-FND                                             
           ELSE                                                                 
              IF BP13K800-STATUS = 23                                           
                 MOVE SPACES    TO WS-SCH-ACC                                   
                 DISPLAY 'RECORD NOT FOUND IN BP13K800: ' K800-NUM-REGN         
              ELSE                                                              
                 DISPLAY 'ERROR READING - BP13K800 : ' BP13K800-STATUS          
                 MOVE BP13K800-STATUS     TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       4000-CHECK-GRANT.                                                        
      *-----------------*                                                       
           IF FD10-AMT-CPF-GRANT-PAYEE(WS-CTR) NOT NUMERIC                      
              MOVE ZEROES   TO FD10-AMT-CPF-GRANT-PAYEE(WS-CTR)                 
           END-IF.                                                              
                                                                                
           MOVE FD10-AMT-CPF-GRANT-PAYEE(WS-CTR) TO WS-AMT-GRNT.                
                                                                                
           IF FD10-NUM-GRANT(WS-CTR) = 'HGT'                                    
              MOVE 'N'       TO WS-TAG-CHANNEL                                  
           ELSE                                                                 
              IF FD10-NUM-GRANT(WS-CTR) = 'AHG' OR 'PHG'                        
                 MOVE 'Y'    TO WS-TAG-CHANNEL                                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       7000-WRITE-DETAILS.                                                      
      *-----------------*                                                       
                                                                                
           MOVE SPACES           TO AB02F930-DETAIL-REC.                        
           INITIALIZE               AB02F930-DETAIL-REC.                        
                                                                                
           MOVE '1'                    TO F930-REC-TYPE-D.                      
           MOVE FD10-NUM-CPF-PAYEE(WS-CTR) TO F930-NUM-CPF-ACCT.                
                                                                                
           IF WS-SCH-ACC = SPACES                                               
              MOVE SPACES              TO F930-NUM-PREFIX                       
           ELSE                                                                 
              MOVE '0'                 TO F930-NUM-PREFIX                       
           END-IF.                                                              
                                                                                
           MOVE WS-SCH-ACC             TO F930-NUM-HDB-REF.                     
           MOVE WS-TAG-CHANNEL         TO F930-TAG-CHANNEL.                     
           MOVE WS-AMT-GRNT            TO F930-AMT-GRNT.                        
           MOVE WS-AMT-THRESHOLD       TO F930-AMT-THRSHOLD.                    
           MOVE '+'                    TO F930-SIGN-GRNT                        
                                          F930-SIGN-GRNT-EXCS.                  
           MOVE 'BP13'                 TO F930-CDE-SYSID.                       
           MOVE FD10-NUM-REGN          TO F930-NUM-REGN.                        
                                                                                
           ADD 1 TO WS-CNT-REC.                                                 
                                                                                
           MOVE AB02F930-DETAIL-REC  TO B02F930A-REC.                           
           WRITE AB02F930-DETAIL-REC.                                           
           WRITE B02F930A-REC.                                                  
           ADD 1 TO WS-WRITE-CNT-DETAIL-F930.                                   
           ADD 1 TO WS-WRITE-CNT-F930A.                                         
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13CB3A *************'.                            
           DISPLAY 'NO OF RECS READ FROM  BP13FD10  : ' CNT-FD10-READ.          
           DISPLAY 'NO OF RECS FOUND IN   BP13KD05  : ' CNT-KD05-FND.           
           DISPLAY 'NO OF RECS FOUND IN   BP13K800  : ' CNT-K800-FND.           
           DISPLAY 'NO OF RECS WRITTEN TO AB02F930  : '                         
                                               WS-WRITE-CNT-DETAIL-F930.        
           DISPLAY 'NO OF RECS WRITTEN TO B02F930A  : '                         
                                               WS-WRITE-CNT-F930A.              
           CLOSE BP13K800                                                       
                 BP13KD05                                                       
                 BP13FD10                                                       
                 AB02F930                                                       
                 B02F930A.                                                      
                                                                                
           IF BP13K800-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13K800-STATUS ' BP13K800-STATUS          
            MOVE BP13K800-STATUS TO RETURN-CODE.                                
                                                                                
           IF BP13KD05-STATUS NOT = 0 AND 97                                    
            DISPLAY ' CLOSING ERROR, BP13KD05-STATUS ' BP13KD05-STATUS          
            MOVE BP13KD05-STATUS TO RETURN-CODE.                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
