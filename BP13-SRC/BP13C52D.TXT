       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C52D.                                                  
       AUTHOR.       AM25.                                                      
       DATE-WRITTEN. 23/11/2023.                                                
      *****************************************************************         
      *                SOC - SYSTEM OF COMMITMENT (BP13)              *         
      *===============================================================*         
      *  OBJECTIVES : UPDATE K745-NUM-NT-CLASSIFY BASED ON MATURE     *         
      *               ESTATE TAG IN BP13K813                          *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                                                               *         
      *                ACCESS     REC                                 *         
      * DD NAME  I/O/E MODE       LEN  KEY FIELDS    REMARKS          *         
      * -------- ----- ---------- ---- ------------- ---------------- *         
      * BP13K745 I/O   SEQUENTIAL 500  K745-KEY-FLD                   *         
      * BP13K813 I     RANDOM     1000 K813-KEY-FLD                   *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      *                                                               *         
      * CHG-REQ#  DDMMCCYY   BY   REMARKS                             *         
      * --------  --------  ----  ----------------------------------- *         
      * BP139670  23112023  AM25  NEW PROGRAM                         *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K745 ASSIGN      TO BP13K745                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS SEQUENTIAL                                       
              RECORD KEY    IS K745-KEY-FLD                                     
              FILE STATUS   IS WS-K745-STATUS.                                  
                                                                                
           SELECT BP13K813 ASSIGN      TO BP13K813                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K813-KEY-FLD                                     
              FILE STATUS   IS WS-K813-STATUS.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K745                                                             
           RECORD CONTAINS 500  CHARACTERS.                                     
       COPY BP13K745.                                                           
                                                                                
       FD  BP13K813                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K813.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K745-STATUS          PIC 9(2)        VALUE ZEROES.            
           05  WS-K813-STATUS          PIC 9(2)        VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-K745-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-K813-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-K745-UPDATE          PIC 9(6)        VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES-FLAGS.                                                   
           05  WS-K745-EOF             PIC X           VALUE 'N'.               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13K745    THRU 2000-EXIT                         
              UNTIL WS-K745-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN I-O    BP13K745                                                 
                INPUT  BP13K813.                                                
                                                                                
           IF (WS-K745-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K745 FAIL STATUS=' WS-K745-STATUS               
              MOVE WS-K745-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF (WS-K813-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K813 FAIL STATUS=' WS-K813-STATUS               
              MOVE WS-K813-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13K745.                                                      
      *****************************************************************         
                                                                                
           READ BP13K745.                                                       
                                                                                
           EVALUATE WS-K745-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K745-READ                          
                                                                                
                 IF (K745-KEY-FLD NOT = SPACES AND LOW-VALUES)                  
                    PERFORM 3000-READ-BP13K813   THRU 3000-EXIT                 
                    PERFORM 4000-UPDATE-BP13K745 THRU 4000-EXIT                 
                 END-IF                                                         
              WHEN 10                                                           
                 MOVE 'Y'              TO WS-K745-EOF                           
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K745 FAIL STATUS=' WS-K745-STATUS            
                 MOVE WS-K745-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-READ-BP13K813.                                                      
      *****************************************************************         
                                                                                
           MOVE K745-CDE-NT1           TO K813-NUM-ZONE.                        
           MOVE K745-CDE-FLAT-TYPE     TO K813-NUM-FLAT-TYPE.                   
           MOVE K745-DTE-BALLOT        TO K813-DTE-BALLOT.                      
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K813-READ                          
              WHEN 23                                                           
                 DISPLAY 'REC NOT FOUND IN BP13K813: ' K813-KEY-FLD             
                 MOVE SPACES           TO K813-NUM-MATURE-EST-TAG               
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K813 FAIL STATUS=' WS-K813-STATUS            
                 MOVE WS-K813-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4000-UPDATE-BP13K745.                                                    
      *****************************************************************         
                                                                                
           EVALUATE K813-NUM-MATURE-EST-TAG                                     
              WHEN 'Y'                                                          
                 MOVE 'M'              TO K745-NUM-NT-CLASSIFY                  
              WHEN 'N'                                                          
                 MOVE 'N'              TO K745-NUM-NT-CLASSIFY                  
              WHEN OTHER                                                        
                 MOVE SPACES           TO K745-NUM-NT-CLASSIFY                  
           END-EVALUATE.                                                        
                                                                                
           REWRITE BP13K745-REC.                                                
                                                                                
           IF WS-K745-STATUS = ZEROES                                           
              ADD 1                    TO WS-K745-UPDATE                        
           ELSE                                                                 
              DISPLAY 'REWRITE BP13K745 FAIL STATUS=' WS-K745-STATUS            
              MOVE WS-K745-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BP13K745                                                       
                 BP13K813.                                                      
                                                                                
           IF WS-K745-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K745 FAIL STATUS=' WS-K745-STATUS              
              MOVE WS-K745-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K813 FAIL STATUS=' WS-K813-STATUS              
              MOVE WS-K813-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           DISPLAY '******************************'.                            
           DISPLAY '* PROGRAM BP13C52D           *'.                            
           DISPLAY '* CONTROL COUNTS             *'.                            
           DISPLAY '*----------------------------*'.                            
           DISPLAY '* REC READ K745     : ' WS-K745-READ ' *'.                  
           DISPLAY '* REC READ K813     : ' WS-K813-READ ' *'.                  
           DISPLAY '* REC UPDATE K745   : ' WS-K745-UPDATE ' *'.                
           DISPLAY '******************************'.                            
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
