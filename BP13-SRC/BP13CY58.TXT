       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY58.                                                 
       AUTHOR.        JIANG BO.                                                 
       DATE-WRITTEN.  07/07/2009.                                               
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVE   : MATCH F555 & F716                     *                
      *                  TO GET CURRENT FLAT STATUS            *                
      *                                                        *                
      *    INPUT FILE  : BP13F555                              *                
      *                  BP13F716                              *                
      *                                                        *                
      *   OUTPUT FILE  : P13F555A                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE     DETAILS                         *                
      * -------- ---- -------- ------------------------------- *                
      * BP133653 JB8  11/06/09 NEW PROGRAM                     *                
      *========================================================*                
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F716  ASSIGN       TO BP13F716.                           
           SELECT BP13F555  ASSIGN       TO BP13F555.                           
           SELECT P13F555A  ASSIGN       TO P13F555A.                           
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F716                                                            
            RECORD CONTAINS 350 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       COPY BP13F716.                                                           
                                                                                
       FD   BP13F555                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       COPY BP13F555.                                                           
                                                                                
       FD   P13F555A                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       01   P13F555A-REC            PIC X(600).                                 
                                                                                
      ***************************                                               
       WORKING-STORAGE SECTION.                                                 
      ***************************                                               
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-SYS-YY             PIC X(04) .                                 
           05 WS-SYS-MM             PIC X(02) .                                 
           05 WS-SYS-DD             PIC X(02) .                                 
                                                                                
       01  WS-EDIT-DATE.                                                        
           05 WS-EDIT-DAY           PIC X(02) .                                 
           05 FILLER                PIC X(01) VALUE '/'.                        
           05 WS-EDIT-MONTH         PIC X(02) .                                 
           05 FILLER                PIC X(01) VALUE '/'.                        
           05 WS-EDIT-YEAR          PIC X(04) .                                 
                                                                                
       01  WS-ACCUMULATORS.                                                     
           05 WS-F555-READ               PIC 9(8)  VALUE ZEROES.                
           05 WS-F555-MATCH              PIC 9(8)  VALUE ZEROES.                
           05 WS-F555-NOMATCH            PIC 9(8)  VALUE ZEROES.                
           05 WS-F716-NOMATCH            PIC 9(8)  VALUE ZEROES.                
           05 WS-F555A-WRITE             PIC 9(8)  VALUE ZEROES.                
           05 WS-F716-READ               PIC 9(8)  VALUE ZEROES.                
                                                                                
       01  WS-ROLE                       PIC X(02) VALUE SPACES.                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      *--------*                                                                
       000-MAIN.                                                                
      *--------*                                                                
                                                                                
           PERFORM 100-OPEN-FILES      THRU 100-EXIT.                           
           PERFORM 200-READ-BP13F555   THRU 200-EXIT.                           
           PERFORM 300-READ-BP13F716   THRU 300-EXIT.                           
           PERFORM 400-PROCESS-INDATA  THRU 400-EXIT                            
             UNTIL F555-LATEST-NUM-REGN = HIGH-VALUES                           
               AND F716-NUM-REGN = HIGH-VALUES.                                 
           PERFORM 900-CLOSE-FILES     THRU 900-EXIT.                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------                                                       
       100-OPEN-FILES.                                                          
      *------------------                                                       
                                                                                
           OPEN INPUT  BP13F716                                                 
                       BP13F555                                                 
                OUTPUT P13F555A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)   TO  WS-SYS-DATE.                   
           MOVE   WS-SYS-DD       TO WS-EDIT-DAY                                
           MOVE   WS-SYS-MM       TO WS-EDIT-MONTH                              
           MOVE   WS-SYS-YY       TO WS-EDIT-YEAR.                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       200-READ-BP13F555.                                                       
      *-----------------*                                                       
                                                                                
           READ BP13F555  AT END                                                
                MOVE HIGH-VALUES TO F555-LATEST-NUM-REGN                        
                GO TO 200-EXIT                                                  
           END-READ                                                             
                                                                                
           ADD  1  TO WS-F555-READ.                                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       300-READ-BP13F716.                                                       
      *-----------------*                                                       
                                                                                
           READ BP13F716  AT END                                                
                MOVE HIGH-VALUES TO F716-NUM-REGN                               
                GO TO 300-EXIT                                                  
           END-READ                                                             
                                                                                
           ADD  1  TO WS-F716-READ.                                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------                                                       
       400-PROCESS-INDATA.                                                      
      *------------------*                                                      
                                                                                
           IF F555-LATEST-NUM-REGN = F716-NUM-REGN                              
              ADD 1                              TO WS-F555-MATCH               
              PERFORM 500-WRITE-P13F555A       THRU 500-EXIT                    
                UNTIL F716-NUM-REGN NOT = F555-LATEST-NUM-REGN                  
                   OR F716-NUM-REGN = HIGH-VALUES                               
                                                                                
              WRITE P13F555A-REC               FROM BP13F555-REC                
              ADD 1                              TO WS-F555A-WRITE              
                                                                                
              PERFORM 200-READ-BP13F555        THRU 200-EXIT                    
           ELSE                                                                 
              IF F555-LATEST-NUM-REGN < F716-NUM-REGN                           
                                                                                
                 ADD 1                           TO WS-F555-NOMATCH             
                 WRITE P13F555A-REC            FROM BP13F555-REC                
                 ADD 1                           TO WS-F555A-WRITE              
                                                                                
                 PERFORM 200-READ-BP13F555     THRU 200-EXIT                    
              ELSE                                                              
                 ADD  1                          TO WS-F716-NOMATCH             
                 PERFORM 300-READ-BP13F716     THRU 300-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-WRITE-P13F555A.                                                      
                                                                                
           MOVE SPACES                     TO WS-ROLE.                          
                                                                                
           EVALUATE F716-NUM-ROLE                                               
               WHEN '003'                                                       
                    MOVE 'RT'              TO WS-ROLE                           
               WHEN '023'                                                       
                    MOVE 'L '              TO WS-ROLE                           
               WHEN '024'                                                       
                    MOVE 'CO'              TO WS-ROLE                           
               WHEN '004'                                                       
               WHEN '025'                                                       
                    MOVE 'OC'              TO WS-ROLE                           
               WHEN OTHER                                                       
                    MOVE 'OT'              TO WS-ROLE                           
           END-EVALUATE                                                         
                                                                                
           EVALUATE F716-NUM-NRIC                                               
               WHEN F555-LATEST-HA1-NRIC                                        
                    MOVE WS-ROLE           TO F555-LATEST-HA1-SMS-ROLE          
               WHEN F555-LATEST-HA2-NRIC                                        
                    MOVE WS-ROLE           TO F555-LATEST-HA2-SMS-ROLE          
               WHEN F555-LATEST-HA3-NRIC                                        
                    MOVE WS-ROLE           TO F555-LATEST-HA3-SMS-ROLE          
               WHEN F555-LATEST-HA4-NRIC                                        
                    MOVE WS-ROLE           TO F555-LATEST-HA4-SMS-ROLE          
           END-EVALUATE.                                                        
                                                                                
           PERFORM 300-READ-BP13F716     THRU 300-EXIT.                         
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *----------------                                                         
       900-CLOSE-FILES.                                                         
      *----------------                                                         
                                                                                
           CLOSE BP13F555                                                       
                 P13F555A                                                       
                 BP13F716.                                                      
                                                                                
           DISPLAY 'PROGRAM-ID : BP13CY51                 '.                    
           DISPLAY '                                      '.                    
           DISPLAY 'DATE : ' WS-EDIT-DATE.                                      
           DISPLAY '                                      '.                    
           DISPLAY 'NO. OF RECORDS READ FROM F555   = ' WS-F555-READ.           
           DISPLAY 'NO. OF RECORDS READ FROM F716   = ' WS-F716-READ.           
           DISPLAY 'NO. OF RECORDS OF F555 MATCHD   = ' WS-F555-MATCH.          
           DISPLAY 'NO. OF RECORDS OF F555 NOT MATCHED ='                       
                   WS-F555-NOMATCH.                                             
           DISPLAY 'NO. OF RECORDS OF F716 NOT MATCHED ='                       
                   WS-F716-NOMATCH.                                             
           DISPLAY 'NO. OF RECORDS WRITTEN TO F555A = ' WS-F555A-WRITE.         
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
