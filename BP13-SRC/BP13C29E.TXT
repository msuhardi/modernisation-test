       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C29E.                                                 
      *AUTHOR.        BHASKAR RAJU G.                                           
      *DATE-WRITTEN.  20/07/2000.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *  (USED BY PRINTING OF ADJUSTED FLAT LIST (BY BATCH)).      *            
      *  OBJECTIVE  :                                              *            
      *    1.  PRINT LIST OF UNIT NO'S IN DIFFERENT BLOCKS AND     *            
      *        STREETS FOR A GIVEN ZONE OR NEW TOWN.               *            
      *    2.  SUBROUTINE TO PRINT ADJUSTED FLATLIST FOR WIS       *            
      *        (FORM BP134W)                                       *            
      *                                                            *            
      *  INPUT FILES:                                              *            
      *    1. BP13F999                                             *            
      *    2. BP13K060                                             *            
      *                                                            *            
      *  OUTPUT FILES:                                             *            
      *    1. BP13L29E                                             *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY   DESCRIPTION                         *            
      * -------- -------- ---- ----------------------------------- *            
      * BP132562 20051013 LMS1 NEW PROGRAM.                        *            
      * BP132967 20070110 SSS2 CHG 'KEY AVAILABLE' TO              *            
      *                            'FLAT COMPLETED'                *            
      * BP133390 20080731 RB12 EXPAND F999 FROM 200 TO 300 AND     *            
      *                        PRINT ECD                           *            
      * BP133610 20090605 ESA1 TO CATER FOR SELECTION TYPE = 'BTO' *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F999 ASSIGN TO BP13F999.                                  
                                                                                
           SELECT BP13K060 ASSIGN TO BP13K060                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K060-KEY-FLD                                    
                FILE STATUS  IS BP13K060-STATUS.                                
                                                                                
           SELECT BP13L29E ASSIGN TO BP13L29E.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F999                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 300 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F999.                                                           
                                                                                
       FD  BP13L29E                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  L29E-PRTREC                      PIC X(100).                         
                                                                                
       FD   BP13K060                 RECORD CONTAINS 25 CHARACTERS.             
       COPY BP13K060.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-VARIABLES.                                                        
           05 WS-F999-READ                  PIC 9(08)  VALUE ZEROES.            
           05 WS-PRT-LINE-CNT               PIC 9(08)  VALUE ZEROES.            
           05 BP13K060-STATUS               PIC 9(02)  VALUE ZEROES.            
           05 WS-K060-FND                   PIC X(01)  VALUE SPACES.            
           05 WS-EDT-TMP                    PIC Z(7)9.                          
           05 WS-ALLOC-DATE.                                                    
              10  WS-ALLOC-DATE-MONTH       PIC X(03).                          
              10  FILLER                    PIC X(01).                          
              10  WS-ALLOC-DATE-YEAR        PIC X(04).                          
           05 WS-FLAT-TYPE                  PIC X(06) VALUE SPACES.             
           05 WS-TXT-QTR                    PIC X(07) VALUE SPACES.             
                                                                                
       01  WS-TABLES.                                                           
           05  WS-MONTH-TABLE.                                                  
               10  FILLER        PIC X(05) VALUE '01JAN'.                       
               10  FILLER        PIC X(05) VALUE '02FEB'.                       
               10  FILLER        PIC X(05) VALUE '03MAR'.                       
               10  FILLER        PIC X(05) VALUE '04APR'.                       
               10  FILLER        PIC X(05) VALUE '05MAY'.                       
               10  FILLER        PIC X(05) VALUE '06JUN'.                       
               10  FILLER        PIC X(05) VALUE '07JUL'.                       
               10  FILLER        PIC X(05) VALUE '08AUG'.                       
               10  FILLER        PIC X(05) VALUE '09SEP'.                       
               10  FILLER        PIC X(05) VALUE '10OCT'.                       
               10  FILLER        PIC X(05) VALUE '11NOV'.                       
               10  FILLER        PIC X(05) VALUE '12DEC'.                       
           05  FILLER  REDEFINES WS-MONTH-TABLE.                                
               10  WS-MONTH-TBL  OCCURS 12 INDEXED BY WS-MNT-NDX.               
                   15  WS-MONTH-NUM      PIC X(02).                             
                   15  WS-MONTH-NAME     PIC X(03).                             
                                                                                
       01  WS-REPORT-COUNTERS.                                                  
           05 WS-CNT                        PIC 9(01)  VALUE ZEROES.            
           05 WS-CNT1                       PIC 9(01)  VALUE ZEROES.            
           05 WS-CNT2                       PIC 9(01)  VALUE ZEROES.            
           05 WS-COUNTER                    PIC 9(01)  VALUE ZEROES.            
           05 WS-NO-CNTRCT-LINE             PIC 9(01)  VALUE ZEROES.            
           05 WS-TOTAL-LINES                PIC 9(02)  VALUE ZEROES.            
           05 WS-BAL-LINES                  PIC 9(02)  VALUE ZEROES.            
           05 WS-CTR                        PIC 9(02)  VALUE ZEROES.            
           05 WS-UNITS-CNT                  PIC 9(02)  VALUE ZEROES.            
           05 WS-DTL-CNT                    PIC 9(02)  VALUE ZEROES.            
           05 WS-LINE-CNT                   PIC 9(02)  VALUE ZEROES.            
           05 WS-PAGE-CNT                   PIC 9(05)  VALUE ZEROES.            
           05 WS-COUNT                      PIC 9(05)  VALUE ZEROES.            
                                                                                
       01  WS-REPORT-VARIABLES.                                                 
           05 WS-HEADING                    PIC X(01)  VALUE SPACES.            
           05 WS-UNIT-FLAG                  PIC X(01)  VALUE SPACES.            
           05 WS-BLK-CHNGED                 PIC X(01)  VALUE SPACES.            
           05 WS-PAGE-BREAK                 PIC X(01)  VALUE SPACES.            
           05 WS-810-PARA-BRK               PIC X(01)  VALUE SPACES.            
           05 WS-CHK-810-PARA               PIC X(01)  VALUE SPACES.            
           05 WS-819-PARA-BRK               PIC X(01)  VALUE SPACES.            
           05 WS-CHK-819-PARA               PIC X(01)  VALUE SPACES.            
           05 WS-CNTRCT-CHNG                PIC X(01)  VALUE SPACES.            
           05 WS-LAST-LINE-NPRT             PIC X(01)  VALUE SPACES.            
           05 WS-CHK-UNT-PARA               PIC X(01)  VALUE SPACES.            
           05 WS-CHK-PAGE-BRK               PIC X(01)  VALUE SPACES.            
           05 WS-NEW-ESTATE                 PIC X(01)  VALUE SPACES.            
           05 WS-TMP-DATE                   PIC X(02)  VALUE SPACES.            
           05 WS-JUL-MNTH                   PIC X(03)  VALUE SPACES.            
           05 WS-HEADING-LINE               PIC X(78)  VALUE SPACES.            
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05  WS-F999-FLAG                 PIC X(01)  VALUE 'N'.               
               88 WS-F999-EOF                          VALUE 'Y'.               
                                                                                
       01  WS-DISP-DATE                     PIC X(10) VALUE SPACES.             
       01  WS-DATE                          PIC X(8)  VALUE SPACES.             
                                                                                
       01  WS-MAX-CAT1                      PIC ZZZ9.                           
       01  WS-MAX-CAT2                      PIC ZZZ9.                           
       01  WS-MAX-CAT3                      PIC ZZZ9.                           
                                                                                
       01  WS-TMP-VARIABLES.                                                    
           05  WS-PREV-CNTRCT-TYP           PIC X(04) VALUE SPACES.             
           05  WS-PREV-CNTRCT-NO-BLK.                                           
               10  WS-PREV-NEIGHBOURHOOD    PIC X(04) VALUE SPACES.             
               10  WS-PREV-CONTRACT-NO      PIC X(04) VALUE SPACES.             
               10  WS-PREV-BLOCK-NO         PIC X(05) VALUE SPACES.             
           05  WS-TMP-CNTRCT-TYP1           PIC X(20) VALUE SPACES.             
           05  WS-TMP-CNTRCT-TYP2           PIC X(20) VALUE SPACES.             
           05  WS-TMP-CNTRCT-TYP3           PIC X(20) VALUE SPACES.             
           05  WS-TMP-CNTRCT-TYP4           PIC X(20) VALUE SPACES.             
           05  WS-TMP-REMARKS.                                                  
               10  WS-REMARKS-INFO OCCURS 5 TIMES.                              
                   15  WS-REMARKS           PIC X(21) VALUE SPACES.             
           05  WS-PGE-CNTRCT-DTLS.                                              
               10  WS-PGE-CNTRCT-INFO OCCURS 9 TIMES.                           
                   15  WS-CNTRCT-DTLS       PIC X(20) VALUE SPACES.             
           05  WS-PGE-BLK-DTLS.                                                 
               10  WS-PGE-BLK-INFO OCCURS 4 TIMES.                              
                   15  WS-BLK-DTLS          PIC X(30) VALUE SPACES.             
                                                                                
       01  WS-TMP-REC-INFO.                                                     
           05  WS-TMP-NEIGHBOUR             PIC X(04) VALUE SPACES.             
           05  WS-TMP-CNTRCT-NO             PIC X(04) VALUE SPACES.             
           05  WS-TMP-BLK-NO                PIC X(05) VALUE SPACES.             
           05  WS-TMP-STREET                PIC X(30) VALUE SPACES.             
           05  WS-TMP-CAT1                  PIC X(04) VALUE SPACES.             
           05  WS-TMP-CAT2                  PIC X(04) VALUE SPACES.             
           05  WS-TMP-CAT3                  PIC X(04) VALUE SPACES.             
           05  WS-TMP-OCS-TAG               PIC X(01) VALUE SPACES.             
           05  WS-TMP-UNIT-INFO.                                                
               10  WS-UNIT-INFO OCCURS 4 TIMES.                                 
                   15  WS-TMP-LEVEL         PIC X(02) VALUE SPACES.             
                   15  WS-TMP-UNIT          PIC X(04) VALUE SPACES.             
                   15  WS-TMP-FLAT-CDE      PIC X(02) VALUE SPACES.             
                   15  WS-TMP-AREA          PIC 9(03) VALUE ZEROES.             
                   15  WS-TMP-SELL-PR       PIC 9(08) VALUE ZEROES.             
                                                                                
       01  WS-LINK-REC.                                                         
           05  WS-LINK-ZONE           PIC X(02).                                
       01  WS-ZONE-FND-FLAG           PIC X(01).                                
       01  WS-RETURN-CODE             PIC 9(02).                                
                                                                                
      *-----------------------------------------------------------              
      *LAY-OUT FOR SALE OF FLATS                                                
      *-----------------------------------------------------------              
       01  L29E-LINE-FLAT-TYP1.                                                 
           05  FILLER              PIC X(10) VALUE SPACES.                      
           05  FILLER              PIC X(27) VALUE                              
                                   'SALE OF 3-ROOM FLATS UNDER '.               
           05  L29E-ALLOC-DATE     PIC X(08) VALUE SPACES.                      
           05  FILLER              PIC X(27) VALUE                              
                                   ' WALK-IN SELECTION EXERCISE'.               
       01  L29E-LINE-FLAT-TYP2.                                                 
           05  FILLER              PIC X(10) VALUE SPACES.                      
           05  FILLER              PIC X(08) VALUE 'SALE OF '.                  
           05  L29E-FLAT-TYP2      PIC X(06) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE ' FLATS IN '.                
           05  L29E-ZONE           PIC X(20) VALUE SPACES.                      
           05  FILLER              PIC X(02) VALUE SPACES.                      
                                                                                
       01  L29E-LINE-FLAT-TYP3.                                                 
           05  FILLER              PIC X(10) VALUE SPACES.                      
           05  FILLER              PIC X(08) VALUE 'SALE OF '.                  
           05  L29E-FLAT-TYP3      PIC X(06) VALUE SPACES.                      
           05  FILLER              PIC X(07) VALUE ' FLATS '.                   
                                                                                
       01  L29E-ESTATE-PAGE.                                                    
           05  FILLER              PIC X(10) VALUE SPACES.                      
           05  L29E-NME-ESTATE     PIC X(30) VALUE SPACES.                      
           05  FILLER              PIC X(37) VALUE SPACES.                      
           05  FILLER              PIC X(07) VALUE 'PAGE : '.                   
           05  L29E-PAGE-CNT       PIC ZZZZZ.                                   
                                                                                
       01  L29E-LINE-CNTRCT-TYP1.                                               
           05  FILLER              PIC X(11) VALUE SPACES.                      
           05  L29E-BLK-NO         PIC X(08) VALUE SPACES.                      
           05  FILLER              PIC X(23) VALUE SPACES.                      
           05  L29E-UNIT1          PIC X(08) VALUE SPACES.                      
           05  L29E-FLAT-CODE1     PIC X(04) VALUE SPACES.                      
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  L29E-FLOOR-AREA1    PIC ZZZ.                                     
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  L29E-SELLING-PR1    PIC Z,ZZZ,ZZZ.                               
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  L29E-CNTRCT-TYP1    PIC X(20) VALUE SPACES.                      
                                                                                
       01  L29E-LINE-CNTRCT-TYP2.                                               
           05  FILLER              PIC X(11) VALUE SPACES.                      
           05  L29E-NME-STREET     PIC X(30) VALUE SPACES.                      
           05  FILLER              PIC X(01) VALUE SPACES.                      
           05  L29E-UNIT2          PIC X(08) VALUE SPACES.                      
           05  L29E-FLAT-CODE2     PIC X(04) VALUE SPACES.                      
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  L29E-FLOOR-AREA2    PIC ZZZ.                                     
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  L29E-SELLING-PR2    PIC Z,ZZZ,ZZZ.                               
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  L29E-CNTRCT-TYP2    PIC X(20) VALUE SPACES.                      
                                                                                
       01  L29E-LINE-CNTRCT-TYP3.                                               
           05  FILLER              PIC X(11) VALUE SPACES.                      
           05  L29E-MAX-UNITS      PIC X(30) VALUE SPACES.                      
           05  FILLER              PIC X(01) VALUE SPACES.                      
           05  L29E-UNIT3          PIC X(08) VALUE SPACES.                      
           05  L29E-FLAT-CODE3     PIC X(04) VALUE SPACES.                      
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  L29E-FLOOR-AREA3    PIC ZZZ.                                     
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  L29E-SELLING-PR3    PIC Z,ZZZ,ZZZ.                               
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  L29E-CNTRCT-TYP3    PIC X(20) VALUE SPACES.                      
                                                                                
       01  L29E-LINE-CNTRCT-TYP4.                                               
           05  FILLER              PIC X(11) VALUE SPACES.                      
           05  L29E-MAX-H1         PIC X(03) VALUE SPACES.                      
           05  L29E-MAX-CAT1       PIC X(04).                                   
           05  L29E-MAX-H2         PIC X(05) VALUE SPACES.                      
           05  L29E-MAX-CAT2       PIC X(04).                                   
           05  L29E-MAX-H3         PIC X(07) VALUE SPACES.                      
           05  L29E-MAX-CAT3       PIC X(04).                                   
           05  FILLER              PIC X(04) VALUE SPACES.                      
           05  L29E-UNIT4          PIC X(08) VALUE SPACES.                      
           05  L29E-FLAT-CODE4     PIC X(04) VALUE SPACES.                      
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  L29E-FLOOR-AREA4    PIC ZZZ.                                     
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  L29E-SELLING-PR4    PIC Z,ZZZ,ZZZ.                               
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  L29E-CNTRCT-TYP4    PIC X(20) VALUE SPACES.                      
                                                                                
       01  L29E-LINE-CNTRCT-TYP.                                                
           05  FILLER              PIC X(11) VALUE SPACES.                      
           05  L29E-BLK-DTLS       PIC X(30) VALUE SPACES.                      
           05  FILLER              PIC X(01) VALUE SPACES.                      
           05  L29E-UNIT           PIC X(08) VALUE SPACES.                      
           05  L29E-FLAT-CODE      PIC X(04) VALUE SPACES.                      
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  L29E-FLOOR-AREA     PIC ZZZ.                                     
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  L29E-SELLING-PR     PIC Z,ZZZ,ZZZ.                               
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  L29E-CNTRCT-TYP     PIC X(20) VALUE SPACES.                      
                                                                                
       01  L29E-LINE-FULL.                                                      
           05  FILLER              PIC X(10) VALUE SPACES.                      
           05  FILLER              PIC X(83) VALUE ALL '_'.                     
                                                                                
       01  L29E-DJDE-LINE              PIC X(80) VALUE                          
           '$DJDE$ FORMS=BP134W,END;'.                                          
                                                                                
      *-------------------------------                                          
      *  DJDE FOR FORM TYPE = '6'    *                                          
      *-------------------------------                                          
       01  DJDE-FORM-BP13UH              PIC X(80)  VALUE                       
           '$DJDE$ FORMS=BP13UH,END;'.                                          
                                                                                
      *-------------------------------                                          
      *  DJDE FOR FORM TYPE = '8'    *                                          
      *-------------------------------                                          
       01  DJDE-FORM-BP13UA              PIC X(80)  VALUE                       
           '$DJDE$ FORMS=BP13UA,END;'.                                          
                                                                                
      *-----------------------------------------------------------              
       LINKAGE SECTION.                                                         
      *-----------------------------------------------------------              
       01  WS-C22Y-PARAMETERS.                                                  
           05  WS-C22Y-FORM-TYPE       PIC X(01).                               
           05  WS-C22Y-NT              PIC X(20).                               
           05  WS-C22Y-FT              PIC X(02).                               
           05  WS-C22Y-RETURN-CODE     PIC 9(04).                               
                                                                                
                                                                                
       PROCEDURE DIVISION USING WS-C22Y-PARAMETERS.                             
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM 100-OPEN-FILES  THRU 100-EXIT.                               
           PERFORM 200-F999-READ   THRU 200-EXIT.                               
           PERFORM 300-PROCESS-REC THRU 300-EXIT                                
                   UNTIL WS-F999-EOF.                                           
                                                                                
           PERFORM 819-PRINT-REMARKS   THRU 819-EXIT.                           
                                                                                
           IF WS-PRT-LINE-CNT > 0                                               
              IF WS-LINE-CNT < 51                                               
                 MOVE SPACES              TO L29E-PRTREC                        
                 WRITE L29E-PRTREC        FROM L29E-LINE-FULL AFTER 1           
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                             
                                                                                
       000-EXIT.                                                                
           GOBACK.                                                              
                                                                                
       100-OPEN-FILES.                                                          
      *-----------------------------------------------------------              
           OPEN INPUT  BP13F999                                                 
                       BP13K060                                                 
                OUTPUT BP13L29E.                                                
                                                                                
           IF BP13K060-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BP13K060 STATUS ' BP13K060-STATUS            
              MOVE BP13K060-STATUS TO WS-RETURN-CODE                            
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           STRING WS-DATE(7:2) '/'                                              
                  WS-DATE(5:2) '/'                                              
                  WS-DATE(1:4)                                                  
                  DELIMITED BY SIZE INTO WS-DISP-DATE.                          
                                                                                
           MOVE 'N' TO WS-F999-FLAG.                                            
                                                                                
           MOVE ZEROES TO WS-F999-READ                                          
                          WS-PRT-LINE-CNT                                       
                          WS-REPORT-COUNTERS.                                   
                                                                                
           MOVE SPACES TO WS-HEADING                                            
                          WS-UNIT-FLAG                                          
                          WS-BLK-CHNGED                                         
                          WS-PAGE-BREAK                                         
                          WS-810-PARA-BRK                                       
                          WS-CHK-810-PARA                                       
                          WS-819-PARA-BRK                                       
                          WS-CHK-819-PARA                                       
                          WS-CNTRCT-CHNG                                        
                          WS-LAST-LINE-NPRT                                     
                          WS-CHK-UNT-PARA                                       
                          WS-CHK-PAGE-BRK                                       
                          WS-NEW-ESTATE.                                        
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-F999-READ.                                                           
      *-----------------------------------------------------------              
           READ BP13F999 AT END                                                 
                MOVE 'Y' TO WS-F999-FLAG                                        
                GO TO 200-EXIT.                                                 
                                                                                
           IF WS-F999-READ = 0                                                  
              MOVE 'Y'                TO WS-HEADING                             
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-F999-READ.                                               
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-REC.                                                         
      *-----------------------------------------------------------              
                                                                                
           IF (F999-CDE-NT = WS-C22Y-NT) AND                                    
              (F999-NUM-FLAT-TYPE = WS-C22Y-FT)                                 
               PERFORM 800-PRINT-REPORT THRU 800-EXIT                           
           END-IF.                                                              
                                                                                
           PERFORM 200-F999-READ        THRU 200-EXIT.                          
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-BP13K060-READ.                                                       
      *-----------------------------------------------------------              
                                                                                
           READ BP13K060.                                                       
           EVALUATE BP13K060-STATUS                                             
           WHEN 00                                                              
              MOVE 'Y'       TO WS-K060-FND                                     
           WHEN 23                                                              
              MOVE 'N'       TO WS-K060-FND                                     
              DISPLAY 'RECORD NOT FOUND BP13K060 : ' K060-KEY-FLD               
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K060 : ' BP13K060-STATUS               
              MOVE BP13K060-STATUS            TO  WS-RETURN-CODE                
              PERFORM 999-CLOSE-ROUTINE       THRU 999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       800-PRINT-REPORT.                                                        
      *-----------------------------------------------------------              
           ADD 1 TO WS-COUNT.                                                   
                                                                                
           IF WS-HEADING = 'Y'                                                  
              MOVE SPACES               TO L29E-PRTREC                          
                                           WS-HEADING                           
              MOVE ZEROES               TO WS-LINE-CNT                          
              MOVE 'Y'                  TO WS-NEW-ESTATE                        
                                           WS-CNTRCT-CHNG                       
              PERFORM 802-DECODE-ROOM-ZONE THRU 802-EXIT                        
              MOVE F999-NME-ESTATE      TO L29E-NME-ESTATE                      
              ADD 1                     TO WS-PAGE-CNT                          
              MOVE WS-PAGE-CNT          TO L29E-PAGE-CNT                        
              MOVE SPACES               TO L29E-PRTREC                          
              WRITE L29E-PRTREC FROM L29E-ESTATE-PAGE AFTER 1                   
              MOVE SPACES               TO L29E-PRTREC                          
              WRITE L29E-PRTREC AFTER 4                                         
              ADD 6      TO WS-LINE-CNT                                         
                                                                                
              MOVE F999-NEIGHBOURHOOD        TO WS-PREV-NEIGHBOURHOOD           
              MOVE F999-CONTRACT-NO          TO WS-PREV-CONTRACT-NO             
              MOVE F999-BLOCK-NO             TO WS-PREV-BLOCK-NO                
              PERFORM 801-CHANGE-CNTRCT-TYP  THRU 801-EXIT                      
           END-IF.                                                              
                                                                                
           IF ((F999-NUM-CNTRCT-TYP = WS-PREV-CNTRCT-TYP) AND                   
              (F999-NEIGHBOURHOOD = WS-PREV-NEIGHBOURHOOD) AND                  
              (F999-CONTRACT-NO = WS-PREV-CONTRACT-NO) AND                      
              (F999-BLOCK-NO = WS-PREV-BLOCK-NO)) AND (WS-COUNT < 5)            
                                                                                
              PERFORM 805-MOVE-TO-TEMP   THRU 805-EXIT                          
           ELSE                                                                 
              IF (F999-NUM-CNTRCT-TYP NOT = WS-PREV-CNTRCT-TYP)                 
                 PERFORM 819-PRINT-REMARKS   THRU 819-EXIT                      
                 MOVE 1                 TO WS-COUNT                             
                                                                                
                 MOVE SPACES       TO WS-BLK-CHNGED                             
                 MOVE 'Y'          TO WS-CNTRCT-CHNG                            
                                                                                
                 PERFORM 801-CHANGE-CNTRCT-TYP    THRU 801-EXIT                 
                 PERFORM 805-MOVE-TO-TEMP         THRU 805-EXIT                 
              ELSE                                                              
                 IF (F999-NEIGHBOURHOOD NOT = WS-PREV-NEIGHBOURHOOD) OR         
                    (F999-CONTRACT-NO NOT = WS-PREV-CONTRACT-NO) OR             
                    (F999-BLOCK-NO NOT = WS-PREV-BLOCK-NO)                      
                                                                                
                     PERFORM 819-PRINT-REMARKS   THRU 819-EXIT                  
                     MOVE  1                  TO WS-COUNT                       
                                                                                
                     IF F999-BLOCK-NO NOT = WS-PREV-BLOCK-NO                    
                        MOVE 'Y'          TO WS-BLK-CHNGED                      
                     ELSE                                                       
                        MOVE SPACES       TO WS-BLK-CHNGED                      
                     END-IF                                                     
                                                                                
                     IF (F999-NEIGHBOURHOOD NOT = WS-PREV-NEIGHBOURHOOD)        
                        OR (F999-CONTRACT-NO NOT = WS-PREV-CONTRACT-NO)         
                        MOVE 'Y'          TO WS-CNTRCT-CHNG                     
                     ELSE                                                       
                        MOVE 'N'          TO WS-CNTRCT-CHNG                     
                     END-IF                                                     
                                                                                
                    MOVE SPACES              TO WS-PREV-CNTRCT-NO-BLK           
                    MOVE F999-NEIGHBOURHOOD  TO WS-PREV-NEIGHBOURHOOD           
                    MOVE F999-CONTRACT-NO    TO WS-PREV-CONTRACT-NO             
                    MOVE F999-BLOCK-NO       TO WS-PREV-BLOCK-NO                
                                                                                
                    PERFORM 805-MOVE-TO-TEMP THRU 805-EXIT                      
                 ELSE                                                           
                   IF WS-COUNT = 5                                              
                      PERFORM 810-PRINT-ROW-1  THRU 810-EXIT                    
                   END-IF                                                       
                   PERFORM 818-PRINT-UNIT      THRU 818-EXIT                    
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
       801-CHANGE-CNTRCT-TYP.                                                   
      *-----------------------------------------------------------              
           MOVE SPACES               TO WS-PREV-CNTRCT-NO-BLK                   
                                        WS-PREV-CNTRCT-TYP.                     
           MOVE F999-NUM-CNTRCT-TYP  TO WS-PREV-CNTRCT-TYP.                     
           MOVE F999-NEIGHBOURHOOD   TO WS-PREV-NEIGHBOURHOOD.                  
           MOVE F999-CONTRACT-NO     TO WS-PREV-CONTRACT-NO.                    
           MOVE F999-BLOCK-NO        TO WS-PREV-BLOCK-NO.                       
                                                                                
           MOVE SPACES               TO WS-TMP-CNTRCT-TYP1                      
                                        WS-TMP-CNTRCT-TYP2                      
                                        WS-TMP-CNTRCT-TYP3                      
                                        WS-TMP-CNTRCT-TYP4.                     
                                                                                
           EVALUATE F999-NUM-CNTRCT-TYP                                         
             WHEN '2DBP'                                                        
                MOVE 'DESIGN & BUILD/'   TO WS-TMP-CNTRCT-TYP1                  
                MOVE 'DESIGN PLUS'       TO WS-TMP-CNTRCT-TYP2                  
                MOVE 2                   TO WS-NO-CNTRCT-LINE                   
             WHEN '1PA '                                                        
                MOVE 'PREMIUM'           TO WS-TMP-CNTRCT-TYP1                  
                MOVE 'APARTMENTS'        TO WS-TMP-CNTRCT-TYP2                  
                MOVE 2                   TO WS-NO-CNTRCT-LINE                   
             WHEN '3STD'                                                        
                MOVE 'STANDARD'          TO WS-TMP-CNTRCT-TYP1                  
                MOVE 1                   TO WS-NO-CNTRCT-LINE                   
             WHEN '3STB'                                                        
                MOVE 'STANDARD (BUDGET)' TO WS-TMP-CNTRCT-TYP1                  
                MOVE 1                   TO WS-NO-CNTRCT-LINE                   
             END-EVALUATE.                                                      
                                                                                
       801-EXIT.                                                                
           EXIT.                                                                
                                                                                
       802-DECODE-ROOM-ZONE.                                                    
      *-----------------------------------------------------------              
           EVALUATE F999-NUM-FLAT-TYPE                                          
           WHEN '1 '                                                            
              MOVE '1-ROOM'       TO WS-FLAT-TYPE                               
           WHEN '1A'                                                            
              MOVE '1RM-SA'       TO WS-FLAT-TYPE                               
           WHEN '2 '                                                            
              MOVE '2-ROOM'       TO WS-FLAT-TYPE                               
           WHEN '2A'                                                            
              MOVE '2RM-SA'       TO WS-FLAT-TYPE                               
           WHEN '3 '                                                            
              MOVE '3-ROOM'       TO WS-FLAT-TYPE                               
           WHEN '4 '                                                            
              MOVE '4-ROOM'       TO WS-FLAT-TYPE                               
           WHEN '4D'                                                            
              MOVE 'TYPE-A'       TO WS-FLAT-TYPE                               
           WHEN '5 '                                                            
              MOVE '5-ROOM'       TO WS-FLAT-TYPE                               
           WHEN '5D'                                                            
              MOVE 'TYPE-B'       TO WS-FLAT-TYPE                               
           WHEN 'E '                                                            
              MOVE 'EXEC  '       TO WS-FLAT-TYPE                               
           WHEN 'ED'                                                            
              MOVE 'TYPE-C'       TO WS-FLAT-TYPE                               
           END-EVALUATE.                                                        
                                                                                
           SET WS-MNT-NDX TO 1.                                                 
           SEARCH WS-MONTH-TBL                                                  
              AT END                                                            
                 MOVE SPACES       TO WS-ALLOC-DATE                             
              WHEN F999-DTE-ALLOC (5:2) = WS-MONTH-NUM (WS-MNT-NDX)             
                 MOVE WS-MONTH-NAME (WS-MNT-NDX) TO WS-ALLOC-DATE-MONTH         
                 MOVE F999-DTE-ALLOC (1:4)       TO WS-ALLOC-DATE-YEAR          
           END-SEARCH.                                                          
                                                                                
           MOVE SPACES            TO WS-HEADING-LINE.                           
                                                                                
           IF F999-NUM-SELTY = '3R'                                             
              MOVE WS-ALLOC-DATE         TO L29E-ALLOC-DATE                     
              MOVE L29E-LINE-FLAT-TYP1   TO WS-HEADING-LINE                     
              IF WS-C22Y-FORM-TYPE = '6'                                        
                 WRITE L29E-PRTREC FROM DJDE-FORM-BP13UH AFTER PAGE             
              ELSE                                                              
              IF WS-C22Y-FORM-TYPE = '8'                                        
                 WRITE L29E-PRTREC FROM DJDE-FORM-BP13UA AFTER PAGE             
              ELSE                                                              
                 WRITE L29E-PRTREC FROM L29E-DJDE-LINE AFTER PAGE               
              END-IF                                                            
              END-IF                                                            
              WRITE L29E-PRTREC FROM L29E-LINE-FLAT-TYP1 AFTER 1                
           ELSE                                                                 
              IF F999-NUM-ZONE NOT = SPACES AND LOW-VALUES                      
                 IF F999-NUM-SELTY = 'BTO'                                      
                    MOVE WS-FLAT-TYPE           TO L29E-FLAT-TYP3               
                    MOVE L29E-LINE-FLAT-TYP3    TO WS-HEADING-LINE              
                    IF WS-C22Y-FORM-TYPE = '6'                                  
                       WRITE L29E-PRTREC FROM DJDE-FORM-BP13UH                  
                                         AFTER PAGE                             
                    ELSE                                                        
                    IF WS-C22Y-FORM-TYPE = '8'                                  
                       WRITE L29E-PRTREC FROM DJDE-FORM-BP13UA                  
                                         AFTER PAGE                             
                    ELSE                                                        
                       WRITE L29E-PRTREC FROM L29E-DJDE-LINE AFTER PAGE         
                    END-IF                                                      
                    END-IF                                                      
                    WRITE L29E-PRTREC FROM L29E-LINE-FLAT-TYP3 AFTER 1          
                 ELSE                                                           
                    MOVE SPACES                 TO K060-KEY-FLD                 
                    MOVE '45'                   TO K060-SERIAL-NO               
                    MOVE F999-NUM-ZONE          TO K060-CODE                    
                    PERFORM 600-BP13K060-READ   THRU 600-EXIT                   
                    MOVE K060-DESC              TO L29E-ZONE                    
                    MOVE WS-FLAT-TYPE           TO L29E-FLAT-TYP2               
                    MOVE L29E-LINE-FLAT-TYP2    TO WS-HEADING-LINE              
                    IF WS-C22Y-FORM-TYPE = '6'                                  
                       WRITE L29E-PRTREC FROM DJDE-FORM-BP13UH                  
                                         AFTER PAGE                             
                    ELSE                                                        
                    IF WS-C22Y-FORM-TYPE = '8'                                  
                       WRITE L29E-PRTREC FROM DJDE-FORM-BP13UA                  
                                         AFTER PAGE                             
                    ELSE                                                        
                       WRITE L29E-PRTREC FROM L29E-DJDE-LINE AFTER PAGE         
                    END-IF                                                      
                    END-IF                                                      
                    WRITE L29E-PRTREC FROM L29E-LINE-FLAT-TYP2 AFTER 1          
                 END-IF                                                         
              ELSE                                                              
                 MOVE WS-FLAT-TYPE           TO L29E-FLAT-TYP3                  
                 MOVE L29E-LINE-FLAT-TYP3    TO WS-HEADING-LINE                 
                 IF WS-C22Y-FORM-TYPE = '6'                                     
                    WRITE L29E-PRTREC FROM DJDE-FORM-BP13UH AFTER PAGE          
                 ELSE                                                           
                 IF WS-C22Y-FORM-TYPE = '8'                                     
                    WRITE L29E-PRTREC FROM DJDE-FORM-BP13UA AFTER PAGE          
                 ELSE                                                           
                    WRITE L29E-PRTREC FROM L29E-DJDE-LINE AFTER PAGE            
                 END-IF                                                         
                 END-IF                                                         
                 WRITE L29E-PRTREC FROM L29E-LINE-FLAT-TYP3 AFTER 1             
              END-IF                                                            
           END-IF.                                                              
                                                                                
       802-EXIT.                                                                
           EXIT.                                                                
                                                                                
       805-MOVE-TO-TEMP.                                                        
      *-----------------------------------------------------------              
           IF WS-COUNT = 1                                                      
                                                                                
              MOVE 'Y'                    TO WS-LAST-LINE-NPRT                  
                                                                                
              MOVE SPACES                 TO WS-TMP-REC-INFO                    
              INITIALIZE WS-TMP-REC-INFO                                        
                                                                                
              MOVE F999-NEIGHBOURHOOD     TO WS-TMP-NEIGHBOUR                   
              MOVE F999-CONTRACT-NO       TO WS-TMP-CNTRCT-NO                   
                                                                                
              MOVE F999-BLOCK-NO          TO WS-TMP-BLK-NO                      
              MOVE F999-NME-STREET        TO WS-TMP-STREET                      
      ***      IF F999-NUM-CAT1-FULL = 'F'                                      
               IF F999-NUM-MAX-CAT1  = ZEROES AND                               
                  F999-NUM-CAT1-FULL = SPACES                                   
                  MOVE F999-NUM-MAX-CAT1  TO WS-MAX-CAT1                        
                  MOVE WS-MAX-CAT1        TO WS-TMP-CAT1                        
               ELSE                                                             
                  MOVE F999-NUM-ADJ-CAT1  TO WS-MAX-CAT1                        
                  MOVE WS-MAX-CAT1        TO WS-TMP-CAT1                        
               END-IF                                                           
      ***      IF F999-NUM-CAT2-FULL = 'F'                                      
               IF F999-NUM-MAX-CAT2  = ZEROES AND                               
                  F999-NUM-CAT2-FULL = SPACES                                   
                  MOVE F999-NUM-MAX-CAT2  TO WS-MAX-CAT2                        
                  MOVE WS-MAX-CAT2        TO WS-TMP-CAT2                        
               ELSE                                                             
                  MOVE F999-NUM-ADJ-CAT2  TO WS-MAX-CAT2                        
                  MOVE WS-MAX-CAT2        TO WS-TMP-CAT2                        
               END-IF                                                           
      ***      IF F999-NUM-CAT3-FULL = 'F'                                      
               IF F999-NUM-MAX-CAT3  = ZEROES AND                               
                  F999-NUM-CAT3-FULL = SPACES                                   
                  MOVE F999-NUM-MAX-CAT3  TO WS-MAX-CAT3                        
                  MOVE WS-MAX-CAT3        TO WS-TMP-CAT3                        
               ELSE                                                             
                  MOVE F999-NUM-ADJ-CAT3  TO WS-MAX-CAT3                        
                  MOVE WS-MAX-CAT3        TO WS-TMP-CAT3                        
               END-IF                                                           
              MOVE F999-CDE-OCS-TAG       TO WS-TMP-OCS-TAG                     
                                                                                
              PERFORM 830-MOVE-REMARKS    THRU 830-EXIT                         
           END-IF.                                                              
                                                                                
              MOVE F999-LEVEL-NO          TO WS-TMP-LEVEL(WS-COUNT).            
              MOVE F999-MAIN-UNIT         TO WS-TMP-UNIT(WS-COUNT).             
              MOVE F999-NUM-FLAT-CODE     TO WS-TMP-FLAT-CDE(WS-COUNT).         
              MOVE F999-QTY-AREA-TOTAL    TO WS-TMP-AREA(WS-COUNT).             
              MOVE F999-AMT-SELLING-PRICE TO WS-TMP-SELL-PR(WS-COUNT).          
                                                                                
       805-EXIT.                                                                
           EXIT.                                                                
                                                                                
       810-PRINT-ROW-1.                                                         
      *-----------------------------------------------------------              
                                                                                
           MOVE 'Y'                    TO WS-CHK-810-PARA.                      
                                                                                
           PERFORM 820-CHK-PAGE-BREAK  THRU 820-EXIT.                           
                                                                                
           MOVE SPACES                 TO L29E-LINE-CNTRCT-TYP1                 
                                          WS-LAST-LINE-NPRT                     
                                          WS-UNIT-FLAG                          
                                          WS-CHK-810-PARA.                      
                                                                                
           PERFORM 850-CHK-END-OF-PAGE    THRU 850-EXIT                         
                                                                                
           INITIALIZE L29E-LINE-CNTRCT-TYP1.                                    
           MOVE ZEROES                 TO WS-CNT2.                              
                                                                                
           MOVE WS-TMP-CNTRCT-TYP1     TO L29E-CNTRCT-TYP1.                     
           STRING 'BLK' WS-TMP-BLK-NO                                           
                         DELIMITED BY SIZE INTO L29E-BLK-NO.                    
           IF WS-TMP-LEVEL(1) NOT = SPACES AND LOW-VALUES AND ZEROES            
              STRING '#' WS-TMP-LEVEL(1) '-' WS-TMP-UNIT(1)                     
                         DELIMITED BY SIZE INTO L29E-UNIT1                      
           MOVE 'Y'                    TO WS-UNIT-FLAG                          
           END-IF.                                                              
                                                                                
           EVALUATE WS-TMP-FLAT-CDE(1)                                          
           WHEN 'SF'                                                            
              MOVE '*'                 TO L29E-FLAT-CODE1                       
           WHEN SPACES                                                          
              MOVE SPACES              TO L29E-FLAT-CODE1                       
           WHEN OTHER                                                           
              STRING '(' WS-TMP-FLAT-CDE(1) ')'                                 
                   DELIMITED BY SIZE INTO L29E-FLAT-CODE1                       
           END-EVALUATE.                                                        
                                                                                
           MOVE WS-TMP-AREA(1)         TO L29E-FLOOR-AREA1.                     
           MOVE WS-TMP-SELL-PR(1)      TO L29E-SELLING-PR1.                     
                                                                                
           MOVE SPACES       TO L29E-PRTREC.                                    
           WRITE L29E-PRTREC FROM L29E-LINE-CNTRCT-TYP1.                        
           ADD 1             TO WS-LINE-CNT.                                    
                                                                                
           IF WS-UNIT-FLAG = 'Y'                                                
              ADD 1          TO WS-PRT-LINE-CNT                                 
           END-IF.                                                              
                                                                                
                                                                                
           PERFORM 815-PRINT-ROW-2     THRU 815-EXIT.                           
                                                                                
       810-EXIT.                                                                
           EXIT.                                                                
                                                                                
       815-PRINT-ROW-2.                                                         
      *-----------------------------------------------------------              
                                                                                
           PERFORM 820-CHK-PAGE-BREAK   THRU 820-EXIT.                          
                                                                                
           MOVE SPACES                 TO WS-UNIT-FLAG.                         
           MOVE SPACES                 TO L29E-LINE-CNTRCT-TYP2.                
           INITIALIZE L29E-LINE-CNTRCT-TYP2.                                    
                                                                                
           IF WS-NO-CNTRCT-LINE = 2                                             
              MOVE WS-TMP-CNTRCT-TYP2     TO L29E-CNTRCT-TYP2                   
           ELSE                                                                 
              STRING WS-TMP-NEIGHBOUR ' ' WS-TMP-CNTRCT-NO                      
                      DELIMITED BY SIZE INTO L29E-CNTRCT-TYP2                   
           END-IF.                                                              
                                                                                
           MOVE WS-TMP-STREET          TO L29E-NME-STREET.                      
                                                                                
           IF WS-TMP-LEVEL(2) NOT = SPACES AND LOW-VALUES AND ZEROES            
              STRING '#' WS-TMP-LEVEL(2) '-' WS-TMP-UNIT(2)                     
                         DELIMITED BY SIZE INTO L29E-UNIT2                      
           MOVE 'Y'                    TO WS-UNIT-FLAG                          
                                                                                
           END-IF.                                                              
                                                                                
           EVALUATE WS-TMP-FLAT-CDE(2)                                          
           WHEN 'SF'                                                            
              MOVE '*'                 TO L29E-FLAT-CODE2                       
           WHEN SPACES                                                          
              MOVE SPACES              TO L29E-FLAT-CODE2                       
           WHEN OTHER                                                           
              STRING '(' WS-TMP-FLAT-CDE(2) ')'                                 
                   DELIMITED BY SIZE INTO L29E-FLAT-CODE2                       
           END-EVALUATE.                                                        
                                                                                
           MOVE WS-TMP-AREA(2)         TO L29E-FLOOR-AREA2.                     
           MOVE WS-TMP-SELL-PR(2)      TO L29E-SELLING-PR2.                     
                                                                                
           MOVE SPACES       TO L29E-PRTREC.                                    
           WRITE L29E-PRTREC FROM L29E-LINE-CNTRCT-TYP2.                        
           ADD 1             TO WS-LINE-CNT.                                    
                                                                                
           IF WS-UNIT-FLAG = 'Y'                                                
              ADD 1          TO WS-PRT-LINE-CNT                                 
           END-IF.                                                              
                                                                                
           PERFORM 816-PRINT-ROW-3     THRU 816-EXIT.                           
                                                                                
       815-EXIT.                                                                
           EXIT.                                                                
                                                                                
       816-PRINT-ROW-3.                                                         
      *-----------------------------------------------------------              
                                                                                
           PERFORM 820-CHK-PAGE-BREAK   THRU 820-EXIT.                          
                                                                                
           MOVE SPACES                 TO WS-UNIT-FLAG.                         
           MOVE SPACES                 TO L29E-LINE-CNTRCT-TYP3.                
           INITIALIZE L29E-LINE-CNTRCT-TYP3.                                    
                                                                                
           IF WS-NO-CNTRCT-LINE = 2                                             
              STRING WS-TMP-NEIGHBOUR ' ' WS-TMP-CNTRCT-NO                      
                      DELIMITED BY SIZE INTO L29E-CNTRCT-TYP3                   
           ELSE                                                                 
              MOVE SPACES                 TO L29E-CNTRCT-TYP3                   
           END-IF.                                                              
                                                                                
           MOVE 'MAXIMUM UNITS ALLOWED FOR:' TO L29E-MAX-UNITS.                 
                                                                                
           IF WS-TMP-LEVEL(3) NOT = SPACES AND LOW-VALUES AND ZEROES            
              STRING '#' WS-TMP-LEVEL(3) '-' WS-TMP-UNIT(3)                     
                         DELIMITED BY SIZE INTO L29E-UNIT3                      
           MOVE 'Y'                    TO WS-UNIT-FLAG                          
           END-IF.                                                              
                                                                                
           EVALUATE WS-TMP-FLAT-CDE(3)                                          
           WHEN 'SF'                                                            
              MOVE '*'                 TO L29E-FLAT-CODE3                       
           WHEN SPACES                                                          
              MOVE SPACES              TO L29E-FLAT-CODE3                       
           WHEN OTHER                                                           
              STRING '(' WS-TMP-FLAT-CDE(3) ')'                                 
                   DELIMITED BY SIZE INTO L29E-FLAT-CODE3                       
           END-EVALUATE.                                                        
                                                                                
           MOVE WS-TMP-AREA(3)         TO L29E-FLOOR-AREA3.                     
           MOVE WS-TMP-SELL-PR(3)      TO L29E-SELLING-PR3.                     
                                                                                
           MOVE SPACES       TO L29E-PRTREC.                                    
           WRITE L29E-PRTREC FROM L29E-LINE-CNTRCT-TYP3.                        
           ADD 1             TO WS-LINE-CNT.                                    
                                                                                
           IF WS-UNIT-FLAG = 'Y'                                                
              ADD 1          TO WS-PRT-LINE-CNT                                 
           END-IF.                                                              
                                                                                
           PERFORM 817-PRINT-ROW-4     THRU 817-EXIT.                           
       816-EXIT.                                                                
           EXIT.                                                                
                                                                                
       817-PRINT-ROW-4.                                                         
      *-----------------------------------------------------------              
                                                                                
           PERFORM 820-CHK-PAGE-BREAK   THRU 820-EXIT.                          
                                                                                
           MOVE SPACES                 TO WS-UNIT-FLAG.                         
           MOVE SPACES                 TO L29E-LINE-CNTRCT-TYP4.                
           INITIALIZE L29E-LINE-CNTRCT-TYP4.                                    
                                                                                
           IF WS-NO-CNTRCT-LINE = 2                                             
              MOVE SPACES              TO L29E-CNTRCT-TYP4                      
           ELSE                                                                 
              ADD   1                  TO WS-CNT2                               
              MOVE WS-REMARKS(WS-CNT2) TO L29E-CNTRCT-TYP4                      
           END-IF.                                                              
                                                                                
           MOVE 'M -'                  TO L29E-MAX-H1.                          
           MOVE WS-TMP-CAT1            TO L29E-MAX-CAT1.                        
           MOVE ', C -'                TO L29E-MAX-H2.                          
           MOVE WS-TMP-CAT2            TO L29E-MAX-CAT2.                        
           MOVE ', I/O -'              TO L29E-MAX-H3.                          
           MOVE WS-TMP-CAT3            TO L29E-MAX-CAT3.                        
           IF WS-TMP-LEVEL(4) NOT = SPACES AND LOW-VALUES AND ZEROES            
              STRING '#' WS-TMP-LEVEL(4) '-' WS-TMP-UNIT(4)                     
                         DELIMITED BY SIZE INTO L29E-UNIT4                      
           MOVE 'Y'                    TO WS-UNIT-FLAG                          
           END-IF.                                                              
                                                                                
           EVALUATE WS-TMP-FLAT-CDE(4)                                          
           WHEN 'SF'                                                            
              MOVE '*'                 TO L29E-FLAT-CODE4                       
           WHEN SPACES                                                          
              MOVE SPACES              TO L29E-FLAT-CODE4                       
           WHEN OTHER                                                           
              STRING '(' WS-TMP-FLAT-CDE(4) ')'                                 
                   DELIMITED BY SIZE INTO L29E-FLAT-CODE4                       
           END-EVALUATE.                                                        
                                                                                
           MOVE WS-TMP-AREA(4)         TO L29E-FLOOR-AREA4.                     
           MOVE WS-TMP-SELL-PR(4)      TO L29E-SELLING-PR4.                     
                                                                                
           MOVE SPACES       TO L29E-PRTREC.                                    
           WRITE L29E-PRTREC FROM L29E-LINE-CNTRCT-TYP4.                        
           ADD 1             TO WS-LINE-CNT.                                    
                                                                                
           IF WS-UNIT-FLAG = 'Y'                                                
              ADD 1          TO WS-PRT-LINE-CNT                                 
           END-IF.                                                              
                                                                                
           MOVE 4                      TO WS-UNITS-CNT.                         
                                                                                
           PERFORM 840-MOVE-CNTRCT-BLK-DTLS THRU 840-EXIT.                      
                                                                                
           MOVE SPACES            TO WS-810-PARA-BRK                            
                                     WS-NEW-ESTATE.                             
                                                                                
       817-EXIT.                                                                
           EXIT.                                                                
                                                                                
       818-PRINT-UNIT.                                                          
      *-----------------------------------------------------------              
                                                                                
           MOVE 'Y'                    TO WS-CHK-UNT-PARA.                      
           PERFORM 820-CHK-PAGE-BREAK  THRU 820-EXIT.                           
                                                                                
           MOVE SPACES                 TO WS-UNIT-FLAG                          
                                          WS-CHK-UNT-PARA                       
                                          L29E-LINE-CNTRCT-TYP.                 
           INITIALIZE L29E-LINE-CNTRCT-TYP.                                     
                                                                                
           IF F999-LEVEL-NO NOT = SPACES AND LOW-VALUES AND ZEROES              
              STRING '#' F999-LEVEL-NO '-' F999-MAIN-UNIT                       
                         DELIMITED BY SIZE INTO L29E-UNIT                       
           MOVE 'Y'                    TO WS-UNIT-FLAG                          
           END-IF.                                                              
                                                                                
           EVALUATE F999-NUM-FLAT-CODE                                          
           WHEN 'SF'                                                            
              MOVE '*'                 TO L29E-FLAT-CODE                        
           WHEN SPACES                                                          
              MOVE SPACES              TO L29E-FLAT-CODE                        
           WHEN OTHER                                                           
              STRING '(' F999-NUM-FLAT-CODE ')'                                 
                   DELIMITED BY SIZE INTO L29E-FLAT-CODE                        
           END-EVALUATE.                                                        
                                                                                
           MOVE F999-QTY-AREA-TOTAL    TO L29E-FLOOR-AREA.                      
           MOVE F999-AMT-SELLING-PRICE TO L29E-SELLING-PR.                      
           IF (WS-CNT2 NOT > WS-CTR)                                            
              ADD 1                      TO WS-CNT2                             
              MOVE WS-REMARKS(WS-CNT2)   TO L29E-CNTRCT-TYP                     
              MOVE SPACES                TO L29E-BLK-DTLS                       
              IF WS-CHK-PAGE-BRK = 'Y'                                          
                 MOVE SPACES             TO WS-CHK-PAGE-BRK                     
              END-IF                                                            
           END-IF                                                               
                                                                                
           IF (WS-CHK-PAGE-BRK = 'Y') AND (WS-DTL-CNT NOT > WS-COUNTER)         
              MOVE WS-CNTRCT-DTLS(WS-DTL-CNT) TO L29E-CNTRCT-TYP                
              IF WS-DTL-CNT < 5                                                 
                 MOVE WS-BLK-DTLS(WS-DTL-CNT) TO L29E-BLK-DTLS                  
              ELSE                                                              
                 MOVE SPACES                  TO L29E-BLK-DTLS                  
              END-IF                                                            
              ADD 1                           TO WS-DTL-CNT                     
           ELSE                                                                 
              IF WS-DTL-CNT > WS-COUNTER                                        
                 MOVE SPACES               TO WS-CHK-PAGE-BRK                   
                 MOVE ZEROES               TO WS-DTL-CNT                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE SPACES                 TO L29E-PRTREC.                          
           WRITE L29E-PRTREC FROM L29E-LINE-CNTRCT-TYP.                         
           ADD 1             TO WS-LINE-CNT                                     
                                WS-UNITS-CNT.                                   
                                                                                
           IF WS-UNIT-FLAG = 'Y'                                                
              ADD 1          TO WS-PRT-LINE-CNT                                 
           END-IF.                                                              
                                                                                
       818-EXIT.                                                                
           EXIT.                                                                
                                                                                
       819-PRINT-REMARKS.                                                       
      *-----------------------------------------------------------              
                                                                                
           MOVE ZEROES       TO WS-UNITS-CNT.                                   
           MOVE 'Y'          TO WS-CHK-819-PARA.                                
                                                                                
           IF WS-CHK-PAGE-BRK = 'Y' AND WS-DTL-CNT NOT > WS-COUNTER             
              PERFORM 845-PRINT-PGBRK-DTLS   THRU 845-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 820-CHK-PAGE-BREAK   THRU 820-EXIT.                          
                                                                                
           IF WS-COUNT < 6                                                      
              IF WS-LAST-LINE-NPRT = 'Y'                                        
                 PERFORM 810-PRINT-ROW-1     THRU 810-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
           ADD  1                         TO WS-CNT2                            
                                                                                
           PERFORM VARYING WS-CNT FROM WS-CNT2 BY 1                             
               UNTIL (WS-CNT > WS-CTR) OR (WS-REMARKS(WS-CNT) = SPACES)         
              MOVE SPACES              TO L29E-LINE-CNTRCT-TYP                  
              INITIALIZE L29E-LINE-CNTRCT-TYP                                   
              MOVE WS-REMARKS(WS-CNT)  TO L29E-CNTRCT-TYP                       
              MOVE SPACES              TO L29E-BLK-DTLS                         
                                                                                
              MOVE SPACES              TO L29E-PRTREC                           
              WRITE L29E-PRTREC FROM L29E-LINE-CNTRCT-TYP                       
              ADD 1             TO WS-LINE-CNT                                  
           END-PERFORM.                                                         
                                                                                
           MOVE SPACES          TO WS-CHK-819-PARA                              
                                   WS-819-PARA-BRK.                             
                                                                                
       819-EXIT.                                                                
           EXIT.                                                                
                                                                                
       820-CHK-PAGE-BREAK.                                                      
      *-----------------------------------------------------------              
                                                                                
           IF WS-LINE-CNT > 51                                                  
              MOVE SPACES               TO L29E-PRTREC                          
              IF WS-C22Y-FORM-TYPE = '6'                                        
                 WRITE L29E-PRTREC FROM DJDE-FORM-BP13UH AFTER PAGE             
              ELSE                                                              
              IF WS-C22Y-FORM-TYPE = '8'                                        
                 WRITE L29E-PRTREC FROM DJDE-FORM-BP13UA AFTER PAGE             
              ELSE                                                              
                 WRITE L29E-PRTREC FROM L29E-DJDE-LINE AFTER PAGE               
              END-IF                                                            
              END-IF                                                            
              WRITE L29E-PRTREC FROM WS-HEADING-LINE AFTER 1                    
              MOVE ZEROES               TO WS-LINE-CNT                          
              MOVE SPACES               TO L29E-PRTREC                          
      **      WRITE L29E-PRTREC AFTER 1                                         
              ADD 1                     TO WS-PAGE-CNT                          
              MOVE WS-PAGE-CNT          TO L29E-PAGE-CNT                        
              MOVE SPACES               TO L29E-PRTREC                          
              WRITE L29E-PRTREC FROM L29E-ESTATE-PAGE                           
              MOVE SPACES               TO L29E-PRTREC                          
              WRITE L29E-PRTREC AFTER 4                                         
              ADD   6                   TO WS-LINE-CNT                          
              MOVE ZEROES               TO WS-UNITS-CNT                         
              IF WS-CHK-UNT-PARA = 'Y'                                          
                 MOVE 'Y'               TO WS-CHK-PAGE-BRK                      
                 MOVE ZEROES            TO WS-DTL-CNT                           
                 ADD 1                  TO WS-DTL-CNT                           
              END-IF                                                            
              IF WS-CHK-810-PARA = 'Y'                                          
                 MOVE 'Y'               TO WS-810-PARA-BRK                      
              END-IF                                                            
              IF WS-CHK-819-PARA = 'Y'                                          
                 MOVE 'Y'               TO WS-819-PARA-BRK                      
              END-IF                                                            
           ELSE                                                                 
              IF WS-UNITS-CNT = 5                                               
                 MOVE SPACES               TO L29E-PRTREC                       
                 WRITE L29E-PRTREC AFTER 1                                      
                 ADD   1                   TO WS-LINE-CNT                       
                 MOVE ZEROES               TO WS-UNITS-CNT                      
              END-IF                                                            
              IF WS-CHK-810-PARA = 'Y'                                          
                 MOVE SPACES            TO WS-810-PARA-BRK                      
              END-IF                                                            
              IF WS-CHK-819-PARA = 'Y'                                          
                 MOVE SPACES            TO WS-819-PARA-BRK                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       820-EXIT.                                                                
           EXIT.                                                                
                                                                                
       830-MOVE-REMARKS.                                                        
      *-----------------------------------------------------------              
           MOVE ZEROES       TO WS-CTR.                                         
           MOVE SPACES       TO WS-TMP-REMARKS.                                 
                                                                                
           IF (F999-CDE-KEY-AVAIL = 'Y') AND                                    
              (F999-DTE-LEASE-COMM NOT = SPACES AND LOW-VALUES)                 
              ADD 1 TO WS-CTR                                                   
              MOVE 'FLAT COMPLETED,'      TO WS-REMARKS(WS-CTR)                 
                                                                                
           ELSE                                                                 
              IF (F999-CDE-KEY-AVAIL = 'Y') AND                                 
                 (F999-DTE-LEASE-COMM = SPACES OR LOW-VALUES)                   
                 ADD 1 TO WS-CTR                                                
                 MOVE 'FLAT COMPLETED'    TO WS-REMARKS(WS-CTR)                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F999-DTE-ECD NOT = SPACES AND LOW-VALUES                          
              ADD 1 TO WS-CTR                                                   
              MOVE F999-DTE-ECD(5:2) TO WS-TMP-DATE                             
              PERFORM 836-DECODE-QTR THRU 836-EXIT                              
              STRING 'ECD: '                                                    
                      WS-TXT-QTR ' '                                            
                      F999-DTE-ECD(1:4)                                         
                      DELIMITED BY SIZE   INTO WS-REMARKS(WS-CTR)               
           END-IF.                                                              
                                                                                
           IF F999-DTE-DLVRY-POSSESSION NOT = SPACES AND LOW-VALUES             
              ADD 1 TO WS-CTR                                                   
                                                                                
              MOVE F999-DTE-DLVRY-POSSESSION(5:2)  TO WS-TMP-DATE               
              PERFORM 835-DECODE-JUL-DATE   THRU 835-EXIT                       
                                                                                
              STRING 'DPD: '                                                    
                     F999-DTE-DLVRY-POSSESSION(7:2) ' '                         
                     WS-JUL-MNTH ' '                                            
                     F999-DTE-DLVRY-POSSESSION(1:4)                             
                     DELIMITED BY SIZE INTO WS-REMARKS(WS-CTR)                  
           END-IF.                                                              
                                                                                
           IF (F999-CDE-OCS-TAG = 'Y') AND                                      
              (F999-DTE-EXPIRY NOT = SPACES AND LOW-VALUES AND ZEROES)          
              ADD 1 TO WS-CTR                                                   
              MOVE 'WITH OCS OPTION'     TO WS-REMARKS(WS-CTR)                  
           END-IF.                                                              
                                                                                
           IF F999-DTE-LEASE-COMM NOT = SPACES AND LOW-VALUES                   
                                                                                
              ADD 1 TO WS-CTR                                                   
              MOVE F999-DTE-LEASE-COMM(5:2) TO WS-TMP-DATE                      
              PERFORM 835-DECODE-JUL-DATE   THRU 835-EXIT                       
                                                                                
              STRING 'LCD: ' F999-DTE-LEASE-COMM(7:2) ' ' WS-JUL-MNTH           
                              ' ' F999-DTE-LEASE-COMM(1:4)                      
                        DELIMITED BY SIZE INTO WS-REMARKS(WS-CTR)               
           END-IF.                                                              
                                                                                
       830-EXIT.                                                                
           EXIT.                                                                
                                                                                
       835-DECODE-JUL-DATE.                                                     
      *-----------------------------------------------------------              
           EVALUATE WS-TMP-DATE                                                 
           WHEN '01'                                                            
              MOVE 'JAN'       TO WS-JUL-MNTH                                   
           WHEN '02'                                                            
              MOVE 'FEB'       TO WS-JUL-MNTH                                   
           WHEN '03'                                                            
              MOVE 'MAR'       TO WS-JUL-MNTH                                   
           WHEN '04'                                                            
              MOVE 'APR'       TO WS-JUL-MNTH                                   
           WHEN '05'                                                            
              MOVE 'MAY'       TO WS-JUL-MNTH                                   
           WHEN '06'                                                            
              MOVE 'JUN'       TO WS-JUL-MNTH                                   
           WHEN '07'                                                            
              MOVE 'JUL'       TO WS-JUL-MNTH                                   
           WHEN '08'                                                            
              MOVE 'AUG'       TO WS-JUL-MNTH                                   
           WHEN '09'                                                            
              MOVE 'SEP'       TO WS-JUL-MNTH                                   
           WHEN '10'                                                            
              MOVE 'OCT'       TO WS-JUL-MNTH                                   
           WHEN '11'                                                            
              MOVE 'NOV'       TO WS-JUL-MNTH                                   
           WHEN '12'                                                            
              MOVE 'DEC'       TO WS-JUL-MNTH                                   
           END-EVALUATE.                                                        
                                                                                
       835-EXIT.                                                                
           EXIT.                                                                
                                                                                
       836-DECODE-QTR.                                                          
      *-----------------------------------------------------------              
           EVALUATE WS-TMP-DATE                                                 
           WHEN '01'                                                            
           WHEN '02'                                                            
           WHEN '03'                                                            
              MOVE '1ST QTR' TO WS-TXT-QTR                                      
           WHEN '04'                                                            
           WHEN '05'                                                            
           WHEN '06'                                                            
              MOVE '2ND QTR' TO WS-TXT-QTR                                      
           WHEN '07'                                                            
           WHEN '08'                                                            
           WHEN '09'                                                            
              MOVE '3RD QTR' TO WS-TXT-QTR                                      
           WHEN '10'                                                            
           WHEN '11'                                                            
           WHEN '12'                                                            
              MOVE '4TH QTR' TO WS-TXT-QTR                                      
           END-EVALUATE.                                                        
                                                                                
       836-EXIT.                                                                
           EXIT.                                                                
                                                                                
       840-MOVE-CNTRCT-BLK-DTLS.                                                
      *-----------------------------------------------------------              
                                                                                
           IF WS-CNTRCT-CHNG = 'Y'                                              
              MOVE ZEROES               TO WS-COUNTER                           
                                           WS-CNT1                              
              MOVE SPACES               TO WS-PGE-CNTRCT-DTLS                   
              MOVE L29E-CNTRCT-TYP1     TO WS-CNTRCT-DTLS(1)                    
              MOVE L29E-CNTRCT-TYP2     TO WS-CNTRCT-DTLS(2)                    
              MOVE L29E-CNTRCT-TYP3     TO WS-CNTRCT-DTLS(3)                    
                                                                                
              IF WS-NO-CNTRCT-LINE = 2                                          
                 MOVE L29E-CNTRCT-TYP4     TO WS-CNTRCT-DTLS(4)                 
                 MOVE 5                    TO WS-COUNTER                        
                 PERFORM VARYING WS-CNT1 FROM 1 BY 1 UNTIL WS-CNT1 > 5          
                                   OR WS-REMARKS(WS-CNT1) = SPACES              
                 MOVE WS-REMARKS(WS-CNT1)  TO WS-CNTRCT-DTLS(WS-COUNTER)        
                 ADD 1                     TO WS-COUNTER                        
                 END-PERFORM                                                    
              ELSE                                                              
                 MOVE WS-REMARKS(1)        TO WS-CNTRCT-DTLS(4)                 
                 MOVE 5                    TO WS-COUNTER                        
                 PERFORM VARYING WS-CNT1 FROM 2 BY 1 UNTIL WS-CNT1 > 5          
                                   OR WS-REMARKS(WS-CNT1) = SPACES              
                 MOVE WS-REMARKS(WS-CNT1)  TO WS-CNTRCT-DTLS(WS-COUNTER)        
                 ADD 1                     TO WS-COUNTER                        
                 END-PERFORM                                                    
              END-IF                                                            
                                                                                
      **--> STORE THE BLOCK INFORMATION                                         
              MOVE SPACES               TO WS-PGE-BLK-DTLS                      
              MOVE L29E-BLK-NO          TO WS-BLK-DTLS(1)                       
              MOVE L29E-NME-STREET      TO WS-BLK-DTLS(2)                       
              MOVE L29E-MAX-UNITS       TO WS-BLK-DTLS(3)                       
              STRING   L29E-MAX-H1   L29E-MAX-CAT1                              
                       L29E-MAX-H2   L29E-MAX-CAT2                              
                       L29E-MAX-H3   L29E-MAX-CAT3                              
                       DELIMITED BY SIZE INTO WS-BLK-DTLS(4)                    
           ELSE                                                                 
             IF WS-BLK-CHNGED = 'Y'                                             
              IF WS-NO-CNTRCT-LINE = 2                                          
                 MOVE 5                    TO WS-COUNTER                        
                 PERFORM VARYING WS-CNT1 FROM 1 BY 1 UNTIL WS-CNT1 > 5          
                                   OR WS-REMARKS(WS-CNT1) = SPACES              
                 MOVE SPACES               TO WS-CNTRCT-DTLS(WS-COUNTER)        
                 MOVE WS-REMARKS(WS-CNT1)  TO WS-CNTRCT-DTLS(WS-COUNTER)        
                 ADD 1                     TO WS-COUNTER                        
                 END-PERFORM                                                    
              ELSE                                                              
                 MOVE SPACES               TO WS-CNTRCT-DTLS(4)                 
                 MOVE WS-REMARKS(1)        TO WS-CNTRCT-DTLS(4)                 
                 MOVE 5                    TO WS-COUNTER                        
                                                                                
                 PERFORM VARYING WS-CNT1 FROM 2 BY 1 UNTIL WS-CNT1 > 5          
                                   OR WS-REMARKS(WS-CNT1) = SPACES              
                 MOVE SPACES               TO WS-CNTRCT-DTLS(WS-COUNTER)        
                 MOVE WS-REMARKS(WS-CNT1)  TO WS-CNTRCT-DTLS(WS-COUNTER)        
                 ADD 1                     TO WS-COUNTER                        
                 END-PERFORM                                                    
              END-IF                                                            
                                                                                
      **--> STORE THE BLOCK INFORMATION                                         
              MOVE SPACES               TO WS-PGE-BLK-DTLS                      
              MOVE L29E-BLK-NO          TO WS-BLK-DTLS(1)                       
              MOVE L29E-NME-STREET      TO WS-BLK-DTLS(2)                       
              MOVE L29E-MAX-UNITS       TO WS-BLK-DTLS(3)                       
              STRING   L29E-MAX-H1   L29E-MAX-CAT1                              
                       L29E-MAX-H2   L29E-MAX-CAT2                              
                       L29E-MAX-H3   L29E-MAX-CAT3                              
                       DELIMITED BY SIZE INTO WS-BLK-DTLS(4)                    
             END-IF                                                             
           END-IF.                                                              
                                                                                
       840-EXIT.                                                                
           EXIT.                                                                
                                                                                
       845-PRINT-PGBRK-DTLS.                                                    
      *-----------------------------------------------------------              
                                                                                
           PERFORM VARYING WS-CNT1 FROM WS-DTL-CNT BY 1 UNTIL                   
            (WS-CNT1 > WS-COUNTER) OR (WS-CNTRCT-DTLS(WS-CNT1) = SPACES         
                       AND WS-CNT1 > 4)                                         
                                                                                
              MOVE SPACES               TO L29E-LINE-CNTRCT-TYP                 
              INITIALIZE L29E-LINE-CNTRCT-TYP                                   
                                                                                
              MOVE WS-CNTRCT-DTLS(WS-CNT1) TO L29E-CNTRCT-TYP                   
              IF WS-CNT1 < 5                                                    
                 MOVE WS-BLK-DTLS(WS-CNT1)    TO L29E-BLK-DTLS                  
              END-IF                                                            
                                                                                
              IF L29E-LINE-CNTRCT-TYP NOT = SPACES                              
                 MOVE SPACES               TO L29E-PRTREC                       
                 WRITE L29E-PRTREC FROM L29E-LINE-CNTRCT-TYP                    
                ADD 1             TO WS-LINE-CNT                                
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           MOVE SPACES             TO WS-CHK-PAGE-BRK.                          
           MOVE ZEROES             TO WS-DTL-CNT.                               
                                                                                
       845-EXIT.                                                                
           EXIT.                                                                
                                                                                
       850-CHK-END-OF-PAGE.                                                     
      *-----------------------------------------------------------              
                                                                                
           MOVE ZEROES            TO WS-TOTAL-LINES                             
                                     WS-BAL-LINES.                              
                                                                                
           IF WS-NO-CNTRCT-LINE = 2                                             
              COMPUTE WS-BAL-LINES = WS-CTR + 5                                 
              COMPUTE WS-TOTAL-LINES = WS-BAL-LINES + WS-LINE-CNT               
           ELSE                                                                 
              IF WS-NO-CNTRCT-LINE = 1                                          
                 COMPUTE WS-BAL-LINES = WS-CTR + 4                              
                 COMPUTE WS-TOTAL-LINES = WS-BAL-LINES + WS-LINE-CNT            
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-TOTAL-LINES > 51                                               
              ADD WS-BAL-LINES            TO WS-LINE-CNT                        
              PERFORM 820-CHK-PAGE-BREAK  THRU 820-EXIT                         
           ELSE                                                                 
              IF WS-NEW-ESTATE NOT = 'Y' AND WS-LINE-CNT < 51                   
                        AND WS-LINE-CNT > 9                                     
                 IF WS-CNTRCT-CHNG = 'Y' OR WS-CNTRCT-CHNG = 'N'                
                    WRITE L29E-PRTREC  FROM L29E-LINE-FULL AFTER 1              
                    ADD 1 TO WS-LINE-CNT                                        
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       850-EXIT.                                                                
           EXIT.                                                                
                                                                                
       999-CLOSE-ROUTINE.                                                       
      *-----------------------------------------------------------              
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' BP13C29E CONTROL TOTAL        ' WS-DISP-DATE.              
           DISPLAY '*---------------------------------------------*'.           
           MOVE WS-F999-READ    TO WS-EDT-TMP.                                  
           DISPLAY ' NO OF RECS READ FROM BP13F999: ' WS-EDT-TMP.               
           MOVE WS-PRT-LINE-CNT TO WS-EDT-TMP.                                  
           DISPLAY ' NO OF RECS PRINTED IN REPORT : ' WS-EDT-TMP.               
           DISPLAY '*---------------------------------------------*'.           
                                                                                
           IF BP13K060-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BP13K060-STATUS ' BP13K060-STATUS           
              MOVE BP13K060-STATUS     TO WS-RETURN-CODE                        
           END-IF.                                                              
                                                                                
           MOVE WS-RETURN-CODE      TO WS-C22Y-RETURN-CODE.                     
                                                                                
           CLOSE BP13F999                                                       
                 BP13K060                                                       
                 BP13L29E.                                                      
                                                                                
           DISPLAY 'CLOSE ROUTINE'.                                             
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
