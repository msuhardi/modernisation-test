       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CL10.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  20/10/2015.                                               
      *============================================================*            
      * OBJECTIVE    :  THIS PROGRAM COUNTS RECORDS FROM THE SOC   *            
      *                 TRANS FILE AND WRITES SUMMARY TO BP13FH70  *            
      *============================================================*            
      * NOTES        :  PROGRAM ASSUMES RECORDS ARE SORTED BY      *            
      *                 DOC EXECUTION DATE, TRANS CODE AND REGN NO *            
      *============================================================*            
      * INPUT FILES  :  1.) BP13F315                               *            
      *                                                            *            
      * OUTPUT FILES :  1.) BP13FH70                               *            
      *                                                            *            
      * LISTING      :  1.) BP13LL10                               *            
      *============================================================*            
      * CHG-NO    BY    DATE    DESCRIPTION                        *            
      * BP135965  KSJ3  201015  NEW PROGRAM                        *            
      *                         (NOTE: DOES NOT HANDLE SUA YET)    *            
      *============================================================*            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F315  ASSIGN        TO BP13F315.                          
                                                                                
           SELECT B05K006   ASSIGN        TO B05K006                            
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K006-HDB-REF-SF                    
                            FILE STATUS   IS WS-K006-STATUS.                    
                                                                                
           SELECT BP13FH70  ASSIGN        TO BP13FH70.                          
                                                                                
           SELECT BP13LL10  ASSIGN        TO BP13LL10.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13F315                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 500  CHARACTERS.                                     
           COPY BP13F315.                                                       
                                                                                
       FD  B05K006                                                              
           RECORD CONTAINS 800  CHARACTERS.                                     
           COPY B05K006.                                                        
                                                                                
       FD  BP13FH70                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 400  CHARACTERS.                                     
           COPY BP13FH70.                                                       
                                                                                
       FD  BP13LL10                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13LL10-REC                  PIC X(132).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K006-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F315-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F315-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-FH70-WRT-1         PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-FH70-WRT-2         PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-PAGE               PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-LINE               PIC 9(2)   VALUE 60.                   
           05  WS-CNT-SNO                PIC 9(4)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F315-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K006-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-SKIP-REC               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-FMT8-CCYY      PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-FMT8-MM        PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-FMT8-DD        PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-FMT10-DD       PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-FMT10-MM       PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-FMT10-CCYY.                                           
                   15  WS-DTE-FMT10-CC   PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-FMT10-YY   PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-TMP                PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-INT                PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-PREV-DTE-DOC-EXEC      PIC X(8)   VALUE ZEROES.               
           05  WS-NUM-SA-CNT-1           PIC 9(5)   VALUE ZEROES.               
           05  WS-NUM-SF-CNT-1           PIC 9(5)   VALUE ZEROES.               
           05  WS-NUM-TP-CNT-1           PIC 9(5)   VALUE ZEROES.               
           05  WS-NUM-SATP-CNT-1         PIC 9(5)   VALUE ZEROES.               
           05  WS-NUM-TOL-CNT-1          PIC 9(5)   VALUE ZEROES.               
           05  WS-NUM-SUA-CNT-1          PIC 9(5)   VALUE ZEROES.               
           05  WS-NUM-SA-CNT-2           PIC 9(5)   VALUE ZEROES.               
           05  WS-NUM-SF-CNT-2           PIC 9(5)   VALUE ZEROES.               
           05  WS-NUM-TP-CNT-2           PIC 9(5)   VALUE ZEROES.               
           05  WS-NUM-SATP-CNT-2         PIC 9(5)   VALUE ZEROES.               
           05  WS-NUM-TOL-CNT-2          PIC 9(5)   VALUE ZEROES.               
           05  WS-NUM-SUA-CNT-2          PIC 9(5)   VALUE ZEROES.               
                                                                                
      *--- BP13LL10 REPORT -----------------------------------------            
       01  LL10-HEADER1.                                                        
           05  FILLER                    PIC X(8)   VALUE 'BP13LL10'.           
           05  FILLER                    PIC X(4)   VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE 'HDBCAT 3'.           
           05  FILLER                    PIC X(28)  VALUE SPACES.               
           05  FILLER                    PIC X(39)  VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                    PIC X(17)  VALUE SPACES.               
           05  FILLER                    PIC X(6)   VALUE 'DATE: '.             
           05  LL10-DTE-CURRENT          PIC X(10)  VALUE SPACES.               
           05  FILLER                    PIC X(8)   VALUE '  PAGE: '.           
           05  LL10-CNT-PAGE             PIC ZZZ9.                              
                                                                                
       01  LL10-HEADER2.                                                        
           05  FILLER                    PIC X(44)  VALUE SPACES.               
           05  FILLER                    PIC X(44)  VALUE                       
               'EXCEPTION REPORT ON TRANS CODE SUMMARY COUNT'.                  
                                                                                
       01  LL10-HEADER3.                                                        
           05  FILLER                    PIC X(132) VALUE                       
               '     S/NO     SCH ACCNT NO     REGN NO      ERROR DESCRI        
      -        'PTION'.                                                         
                                                                                
       01  LL10-HEADER4.                                                        
           05  FILLER                    PIC X(132) VALUE                       
               '     ----     ------------     --------     ------------        
      -        '--------------------------------------'.                        
                                                                                
       01  LL10-DETAIL.                                                         
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  LL10-SNO                  PIC ZZZ9.                              
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  LL10-NUM-HDB-REF          PIC X(11)  VALUE SPACES.               
           05  FILLER                    PIC X(6)   VALUE SPACES.               
           05  LL10-NUM-REGN             PIC X(8)   VALUE SPACES.               
           05  FILLER                    PIC X(5)   VALUE SPACES.               
           05  LL10-NUM-DESC             PIC X(50)  VALUE SPACES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-BP13F315-READ      THRU 2000-EXIT.                      
           PERFORM 2100-INIT-LOOP          THRU 2100-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F315-EOF = 'Y'.                                     
           PERFORM 5000-PRINT-LAST-CNT     THRU 5000-EXIT.                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13F315                                                 
                       B05K006                                                  
                OUTPUT BP13FH70                                                 
                       BP13LL10.                                                
                                                                                
           IF WS-K006-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING B05K006 ERROR ' WS-K006-STATUS                   
              MOVE     WS-K006-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
                                                                                
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           PERFORM 8100-CONV-DATE-TO-10    THRU 8100-EXIT.                      
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13F315-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13F315 AT END                                                 
                MOVE 'Y'                   TO   WS-F315-EOF                     
                GO                         TO   2000-EXIT.                      
                                                                                
           ADD 1                           TO   WS-CNT-F315-READ.               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2100-INIT-LOOP.                                                          
      *=============================================================            
                                                                                
           MOVE F315-DTE-DOC-EXEC-ALTX1    TO   WS-PREV-DTE-DOC-EXEC.           
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    WRITE ACCUMULATED TOTAL TO FH70                                      
      *-------------------------------------------------------------            
           IF (F315-DTE-DOC-EXEC-ALTX1 NOT = WS-PREV-DTE-DOC-EXEC)              
              PERFORM 4000-WRITE-BP13FH70-LVL2                                  
                                           THRU 4000-EXIT                       
                                                                                
              ADD WS-NUM-SA-CNT-2          TO   WS-NUM-SA-CNT-1                 
              ADD WS-NUM-SF-CNT-2          TO   WS-NUM-SF-CNT-1                 
              ADD WS-NUM-TP-CNT-2          TO   WS-NUM-TP-CNT-1                 
              ADD WS-NUM-SATP-CNT-2        TO   WS-NUM-SATP-CNT-1               
              ADD WS-NUM-TOL-CNT-2         TO   WS-NUM-TOL-CNT-1                
              ADD WS-NUM-SUA-CNT-2         TO   WS-NUM-SUA-CNT-1                
                                                                                
              IF F315-DTE-DOC-EXEC-ALTX1(1:6) NOT =                             
                 WS-PREV-DTE-DOC-EXEC(1:6)                                      
                 PERFORM 4100-WRITE-BP13FH70-LVL1                               
                                           THRU 4100-EXIT                       
              END-IF                                                            
                                                                                
              MOVE ZEROES                  TO   WS-NUM-SA-CNT-2                 
                                                WS-NUM-SF-CNT-2                 
                                                WS-NUM-TP-CNT-2                 
                                                WS-NUM-SATP-CNT-2               
                                                WS-NUM-TOL-CNT-2                
                                                WS-NUM-SUA-CNT-2                
                                                                                
              MOVE F315-DTE-DOC-EXEC-ALTX1 TO   WS-PREV-DTE-DOC-EXEC            
           END-IF.                                                              
                                                                                
      *-------------------------------------------------------------            
      *    ACCUMULATE COUNT                                                     
      *-------------------------------------------------------------            
           PERFORM 3100-CHECK-NUMERIC-FIELDS THRU 3100-EXIT.                    
                                                                                
           EVALUATE F315-CDE-TRANS                                              
           WHEN 'SA'                                                            
              IF F315-AMT-LEASE-FEE        NOT > 0 AND                          
                 F315-AMT-LEASE-FEE-DUP    NOT > 0 AND                          
                 F315-AMT-SUB-TRF-DOA      NOT > 0 AND                          
                 F315-AMT-SUB-TRF-DOA-DUP  NOT > 0 AND                          
                 F315-AMT-MORT-SF          NOT > 0 AND                          
                 F315-AMT-MORT-SF-DUP      NOT > 0 AND                          
                 F315-AMT-SUPP-AGRM-SF     NOT > 0 AND                          
                 F315-AMT-SUPP-AGRM-SF-DUP NOT > 0                              
                                                                                
                 MOVE F315-SCH-ACC         TO   K006-HDB-REF-SF                 
                 PERFORM 3200-READ-B05K006 THRU 3200-EXIT                       
                                                                                
                 IF WS-K006-FND = 'Y'                                           
                    ADD 1                  TO   WS-NUM-SA-CNT-2                 
                 ELSE                                                           
                    ADD 1                  TO   WS-CNT-F315-SKIP                
                 END-IF                                                         
              ELSE                                                              
                 ADD 1                     TO   WS-CNT-F315-SKIP                
              END-IF                                                            
                                                                                
           WHEN 'SF'                                                            
              IF F315-CDE-SUBTRANSFER-DOC = 'TRA'                               
                 MOVE F315-SCH-ACC         TO   K006-HDB-REF-SF                 
                 PERFORM 3200-READ-B05K006 THRU 3200-EXIT                       
                                                                                
                 IF WS-K006-FND = 'Y'                                           
                    ADD 1                  TO   WS-NUM-SF-CNT-2                 
                 ELSE                                                           
                    ADD 1                  TO   WS-CNT-F315-SKIP                
                 END-IF                                                         
              ELSE                                                              
                 ADD 1                     TO   WS-NUM-SF-CNT-2                 
              END-IF                                                            
                                                                                
           WHEN 'ST'                                                            
              IF F315-AMT-LEASE-FEE        NOT > 0 AND                          
                 F315-AMT-LEASE-FEE-DUP    NOT > 0 AND                          
                 F315-AMT-SUB-TRF-DOA      NOT > 0 AND                          
                 F315-AMT-SUB-TRF-DOA-DUP  NOT > 0 AND                          
                 F315-AMT-MORT-SF          NOT > 0 AND                          
                 F315-AMT-MORT-SF-DUP      NOT > 0 AND                          
                 F315-AMT-SUPP-AGRM-SF     NOT > 0 AND                          
                 F315-AMT-SUPP-AGRM-SF-DUP NOT > 0                              
                                                                                
                 MOVE F315-SCH-ACC         TO   K006-HDB-REF-SF                 
                 PERFORM 3200-READ-B05K006 THRU 3200-EXIT                       
                                                                                
                 IF WS-K006-FND = 'Y'                                           
                    IF F315-CDE-SA-TP-TAG = 'Y'                                 
                       ADD 1               TO   WS-NUM-SATP-CNT-2               
                    ELSE                                                        
                       ADD 1               TO   WS-NUM-TP-CNT-2                 
                    END-IF                                                      
                 ELSE                                                           
                    ADD 1                  TO   WS-CNT-F315-SKIP                
                 END-IF                                                         
              ELSE                                                              
                 ADD 1                     TO   WS-CNT-F315-SKIP                
              END-IF                                                            
                                                                                
           WHEN 'TO'                                                            
              IF F315-AMT-STAMP-DUTIES         > 0                              
                 IF F315-CDE-SUBTRANSFER-DOC NOT = 'TOL'                        
                    ADD 1                  TO   WS-CNT-F315-SKIP                
                 ELSE                                                           
                                                                                
                    MOVE F315-SCH-ACC      TO   K006-HDB-REF-SF                 
                    PERFORM 3200-READ-B05K006 THRU 3200-EXIT                    
                                                                                
                    IF WS-K006-FND = 'Y'                                        
                       IF F315-CDE-SUBTRANSFER-DOC = 'TOL'                      
                          ADD 1            TO   WS-NUM-TOL-CNT-2                
                       ELSE                                                     
                          ADD 1            TO   WS-NUM-SUA-CNT-2                
                       END-IF                                                   
                    ELSE                                                        
                       ADD 1               TO   WS-CNT-F315-SKIP                
                    END-IF                                                      
                 END-IF                                                         
              ELSE                                                              
                 ADD 1                     TO   WS-CNT-F315-SKIP                
              END-IF                                                            
                                                                                
           WHEN OTHER                                                           
              ADD 1                        TO   WS-CNT-F315-SKIP                
                                                                                
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2000-BP13F315-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-CHECK-NUMERIC-FIELDS.                                               
      *=============================================================            
                                                                                
           IF F315-AMT-STAMP-DUTIES NOT NUMERIC                                 
              MOVE ZEROES          TO   F315-AMT-STAMP-DUTIES                   
           END-IF.                                                              
                                                                                
           IF F315-AMT-LEASE-FEE NOT NUMERIC                                    
              MOVE ZEROES          TO   F315-AMT-LEASE-FEE                      
           END-IF.                                                              
                                                                                
           IF F315-AMT-LEASE-FEE-DUP NOT NUMERIC                                
              MOVE ZEROES          TO   F315-AMT-LEASE-FEE-DUP                  
           END-IF.                                                              
                                                                                
           IF F315-AMT-SUB-TRF-DOA NOT NUMERIC                                  
              MOVE ZEROES          TO   F315-AMT-SUB-TRF-DOA                    
           END-IF.                                                              
                                                                                
           IF F315-AMT-SUB-TRF-DOA-DUP NOT NUMERIC                              
              MOVE ZEROES          TO   F315-AMT-SUB-TRF-DOA-DUP                
           END-IF.                                                              
                                                                                
           IF F315-AMT-SELLER-STAMP-DUTY NOT NUMERIC                            
              MOVE ZEROES          TO   F315-AMT-SELLER-STAMP-DUTY              
           END-IF.                                                              
                                                                                
           IF F315-AMT-MORT-SF NOT NUMERIC                                      
              MOVE ZEROES          TO   F315-AMT-MORT-SF                        
           END-IF.                                                              
                                                                                
           IF F315-AMT-MORT-SF-DUP NOT NUMERIC                                  
              MOVE ZEROES          TO   F315-AMT-MORT-SF-DUP                    
           END-IF.                                                              
                                                                                
           IF F315-AMT-SUPP-AGRM-SF NOT NUMERIC                                 
              MOVE ZEROES          TO   F315-AMT-SUPP-AGRM-SF                   
           END-IF.                                                              
                                                                                
           IF F315-AMT-SUPP-AGRM-SF-DUP NOT NUMERIC                             
              MOVE ZEROES          TO   F315-AMT-SUPP-AGRM-SF-DUP               
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *=============================================================            
       3200-READ-B05K006.                                                       
      *=============================================================            
                                                                                
           READ B05K006.                                                        
                                                                                
           EVALUATE WS-K006-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              MOVE 'Y'                     TO   WS-K006-FND                     
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K006-FND                     
           WHEN OTHER                                                           
              MOVE WS-K006-STATUS          TO   RETURN-CODE                     
              DISPLAY 'ERROR READING B05K006, STATUS = '                        
                       WS-K006-STATUS                                           
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *=============================================================            
       4000-WRITE-BP13FH70-LVL2.                                                
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13FH70-REC.                   
           MOVE '2'                        TO   FH70-NUM-LEVEL.                 
           MOVE WS-PREV-DTE-DOC-EXEC       TO   FH70-DTE-DOC-EXEC.              
           MOVE WS-NUM-SA-CNT-2            TO   FH70-NUM-SA-CNT.                
           MOVE WS-NUM-SF-CNT-2            TO   FH70-NUM-SF-CNT.                
           MOVE WS-NUM-TP-CNT-2            TO   FH70-NUM-TP-CNT.                
           MOVE WS-NUM-SATP-CNT-2          TO   FH70-NUM-SATP-CNT.              
           MOVE WS-NUM-TOL-CNT-2           TO   FH70-NUM-TOL-CNT.               
           MOVE WS-NUM-SUA-CNT-2           TO   FH70-NUM-SUA-CNT.               
                                                                                
           MOVE WS-DTE-CURR-8              TO   FH70-DTE-UPDATE.                
           MOVE 'BP13CL10'                 TO   FH70-NUM-USERID.                
                                                                                
           WRITE BP13FH70-REC.                                                  
           ADD 1                           TO   WS-CNT-FH70-WRT-2.              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4100-WRITE-BP13FH70-LVL1.                                                
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   BP13FH70-REC.                   
           MOVE '1'                        TO   FH70-NUM-LEVEL.                 
           MOVE WS-PREV-DTE-DOC-EXEC(1:6)  TO   FH70-DTE-DOC-EXEC.              
           MOVE WS-NUM-SA-CNT-1            TO   FH70-NUM-SA-CNT.                
           MOVE WS-NUM-SF-CNT-1            TO   FH70-NUM-SF-CNT.                
           MOVE WS-NUM-TP-CNT-1            TO   FH70-NUM-TP-CNT.                
           MOVE WS-NUM-SATP-CNT-1          TO   FH70-NUM-SATP-CNT.              
           MOVE WS-NUM-TOL-CNT-1           TO   FH70-NUM-TOL-CNT.               
           MOVE WS-NUM-SUA-CNT-1           TO   FH70-NUM-SUA-CNT.               
                                                                                
           MOVE WS-DTE-CURR-8              TO   FH70-DTE-UPDATE.                
           MOVE 'BP13CL10'                 TO   FH70-NUM-USERID.                
                                                                                
           WRITE BP13FH70-REC.                                                  
           ADD 1                           TO   WS-CNT-FH70-WRT-1.              
                                                                                
           MOVE ZEROES                     TO   WS-NUM-SA-CNT-1                 
                                                WS-NUM-SF-CNT-1                 
                                                WS-NUM-TP-CNT-1                 
                                                WS-NUM-SATP-CNT-1               
                                                WS-NUM-TOL-CNT-1                
                                                WS-NUM-SUA-CNT-1.               
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5000-PRINT-LAST-CNT.                                                     
      *=============================================================            
                                                                                
           PERFORM 4000-WRITE-BP13FH70-LVL2  THRU 4000-EXIT.                    
           ADD WS-NUM-SA-CNT-2               TO   WS-NUM-SA-CNT-1               
           ADD WS-NUM-SF-CNT-2               TO   WS-NUM-SF-CNT-1               
           ADD WS-NUM-TP-CNT-2               TO   WS-NUM-TP-CNT-1               
           ADD WS-NUM-SATP-CNT-2             TO   WS-NUM-SATP-CNT-1             
           ADD WS-NUM-TOL-CNT-2              TO   WS-NUM-TOL-CNT-1              
           ADD WS-NUM-SUA-CNT-2              TO   WS-NUM-SUA-CNT-1              
           PERFORM 4100-WRITE-BP13FH70-LVL1  THRU 4100-EXIT.                    
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8000-CONV-DATE-TO-8.                                                     
      *=============================================================            
      *    INPUT  - WS-DTE-FMT10                                                
      *    OUTPUT - WS-DTE-FMT8                                                 
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-DTE-FMT10-CCYY  TO  WS-DTE-FMT8-CCYY.                        
           MOVE WS-DTE-FMT10-MM    TO  WS-DTE-FMT8-MM.                          
           MOVE WS-DTE-FMT10-DD    TO  WS-DTE-FMT8-DD.                          
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8100-CONV-DATE-TO-10.                                                    
      *=============================================================            
      *    INPUT  - WS-DTE-FMT8                                                 
      *    OUTPUT - WS-DTE-FMT10                                                
      *-------------------------------------------------------------            
                                                                                
           MOVE WS-DTE-FMT8-CCYY   TO  WS-DTE-FMT10-CCYY.                       
           MOVE WS-DTE-FMT8-MM     TO  WS-DTE-FMT10-MM.                         
           MOVE WS-DTE-FMT8-DD     TO  WS-DTE-FMT10-DD.                         
                                                                                
       8100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8900-PRINT-ERROR.                                                        
      *=============================================================            
                                                                                
           IF WS-CNT-LINE > 50                                                  
              PERFORM 8910-PRINT-ERROR-HDR THRU 8910-EXIT                       
           END-IF.                                                              
                                                                                
           ADD  1                          TO   WS-CNT-SNO                      
                                                WS-CNT-LINE.                    
           MOVE WS-CNT-SNO                 TO   LL10-SNO.                       
           MOVE F315-SCH-ACC               TO   LL10-NUM-HDB-REF.               
           MOVE F315-REGN-NO               TO   LL10-NUM-REGN.                  
                                                                                
           WRITE BP13LL10-REC              FROM LL10-DETAIL.                    
                                                                                
       8900-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8910-PRINT-ERROR-HDR.                                                    
      *=============================================================            
                                                                                
           ADD 1                           TO   WS-CNT-PAGE.                    
           MOVE WS-CNT-PAGE                TO   LL10-CNT-PAGE.                  
           MOVE WS-DTE-CURR-10             TO   LL10-DTE-CURRENT.               
                                                                                
           WRITE BP13LL10-REC FROM LL10-HEADER1 AFTER PAGE.                     
           WRITE BP13LL10-REC FROM LL10-HEADER2.                                
           WRITE BP13LL10-REC FROM LL10-HEADER3 AFTER 2.                        
           WRITE BP13LL10-REC FROM LL10-HEADER4.                                
                                                                                
           MOVE SPACES                     TO   BP13LL10-REC.                   
           WRITE BP13LL10-REC.                                                  
                                                                                
           MOVE 6                          TO   WS-CNT-LINE.                    
                                                                                
       8910-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*------- BP13CL10 CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13F315 READ      : ' WS-CNT-F315-READ.          
           DISPLAY ' 2. NO OF BP13F315 SKIPPED   : ' WS-CNT-F315-SKIP.          
           DISPLAY ' 3. NO OF BP13FH70 HEADERS   : ' WS-CNT-FH70-WRT-1.         
           DISPLAY ' 4. NO OF BP13FH70 WRITTEN   : ' WS-CNT-FH70-WRT-2.         
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F315                                                       
                 B05K006                                                        
                 BP13FH70                                                       
                 BP13LL10.                                                      
                                                                                
           IF WS-K006-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING B05K006 ERROR ' WS-K006-STATUS                   
              MOVE     WS-K006-STATUS      TO   RETURN-CODE.                    
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
