       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C254.                                                    
       AUTHOR. JAI.                                                             
       DATE-WRITTEN. 03/02/95.                                                  
                                                                                
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1.  TO CHECK THAT THE ZONE/NT AND FLAT-TYPE FOR   *                
      *          SUBMISSION IS OVER THE BOOKING DATE.          *                
      *========================================================*                
      * CHG REQ   DATE   OIC   DESCRIPTION                     *                
      * -------  ------  ---   -----------                     *                
      * BP130067 191295  LMS   CHANGE IN FILE LAYOUT OF        *                
      *                        BP13K222.                       *                
      * BP130677 210699  LMS   TO CHECK BALLOT DATE FROM F205  *                
      *                        AND K222.                       *                
      * BP137206 030418  ESA1  TO BYPASS CHECKING FOR BP13K222 *                
      *                        FOR ROF                         *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205   ASSIGN       TO BP13F205.                          
           SELECT BP13K222   ASSIGN       TO BP13K222                           
                             ACCESS       IS DYNAMIC                            
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K222-KEY-FLD                       
                             FILE STATUS  IS WS-K222-STATUS.                    
           SELECT P13F205A   ASSIGN       TO P13F205A.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F205                                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F205.                                                           
                                                                                
       FD   BP13K222                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K222.                                                           
                                                                                
       FD   P13F205A                                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       01   P13F205A-REC              PIC X(80).                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-F205-READ               PIC 9(08) VALUE 0.                        
       01  WS-F205A-WRITE             PIC 9(08) VALUE 0.                        
       01  WS-K222-READ               PIC 9(08) VALUE 0.                        
       01  WS-K222-STATUS             PIC 9(02) VALUE 0.                        
                                                                                
       01  WS-SYS-DATE.                                                         
           05 WS-CC                   PIC X(02).                                
           05 WS-CUR-DATE.                                                      
              10 WS-YY                PIC X(02).                                
              10 WS-MM                PIC X(02).                                
              10 WS-DD                PIC X(02).                                
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      **********                                                                
       000-MAIN.                                                                
      **********                                                                
                                                                                
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
           PERFORM 200-READ-F205-REC THRU 200-EXIT.                             
                                                                                
           PERFORM 300-PROCESS-REC   THRU 300-EXIT                              
                   UNTIL F205-NUM-NT-ZONE = HIGH-VALUE.                         
                                                                                
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ****************                                                          
       100-OPEN-FILES.                                                          
      ****************                                                          
                                                                                
           OPEN INPUT  BP13F205                                                 
                       BP13K222.                                                
           OPEN OUTPUT P13F205A.                                                
                                                                                
           IF WS-K222-STATUS  NOT EQUAL 00 AND 97                               
              DISPLAY 'OPEN ERROR IN K222 FILE : ' WS-K222-STATUS               
              DISPLAY 'ABORTING PROGRAM BP13C254'                               
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                           
           END-IF.                                                              
                                                                                
           ACCEPT WS-CUR-DATE FROM DATE.                                        
                                                                                
           IF WS-YY > '94'                                                      
              MOVE '19'     TO WS-CC                                            
           ELSE                                                                 
              MOVE '20'     TO WS-CC                                            
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *******************                                                       
       200-READ-F205-REC.                                                       
      *******************                                                       
                                                                                
           READ BP13F205 AT END                                                 
                MOVE HIGH-VALUE TO F205-NUM-NT-ZONE                             
                GO TO 200-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F205-READ.                                               
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *==============================================*                          
      *  READ K222 FILE USING NT-ZONE AND FLAT TYPE. *                          
      *  IF THE BOOK APPMT END DATE IS LESS THAN THE *                          
      *  CURRENT SYSTEM DATE THEN WRITE THE F205 REC *                          
      *  TO AN OUTPUT FILE.                          *                          
      *==============================================*                          
                                                                                
      *****************                                                         
       300-PROCESS-REC.                                                         
      *****************                                                         
                                                                                
           INITIALIZE  BP13K222-REC.                                            
           MOVE F205-DTE-ALLOCN    TO K222-DTE-ALLOC.                           
           MOVE F205-NUM-NT-ZONE   TO K222-NUM-NT-ZONE.                         
           MOVE F205-NUM-FLAT-TYPE TO K222-NUM-FLAT-TYPE.                       
                                                                                
           READ BP13K222.                                                       
                                                                                
           EVALUATE WS-K222-STATUS                                              
               WHEN 00                                                          
                    IF K222-DTE-END-BKAPPMT < WS-SYS-DATE AND                   
                            K222-DTE-BALLOT = F205-DTE-BALLOT                   
                       MOVE  F205-REC      TO P13F205A-REC                      
                       WRITE P13F205A-REC                                       
                       ADD  1              TO WS-F205A-WRITE                    
                    ELSE                                                        
                       IF F205-NUM-SELECTION = 'ROF'                            
                          MOVE  F205-REC      TO P13F205A-REC                   
                          WRITE P13F205A-REC                                    
                          ADD  1              TO WS-F205A-WRITE                 
                       END-IF                                                   
                    END-IF                                                      
                    ADD     1              TO WS-K222-READ                      
               WHEN 23                                                          
                    IF F205-NUM-SELECTION = 'ROF'                               
                       MOVE  F205-REC      TO P13F205A-REC                      
                       WRITE P13F205A-REC                                       
                       ADD  1              TO WS-F205A-WRITE                    
                    END-IF                                                      
                    DISPLAY 'RECORD NOT IN K222 FILE ' K222-KEY-FLD             
               WHEN OTHER                                                       
                    DISPLAY 'ERROR IN READING K222 FILE ' WS-K222-STATUS        
                    DISPLAY ' K222 KEY FLD IS ' K222-KEY-FLD                    
                    PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                     
           END-EVALUATE.                                                        
                                                                                
           PERFORM 200-READ-F205-REC          THRU 200-EXIT.                    
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************                                                        
       900-CLOSE-ROUTINE.                                                       
      ******************                                                        
                                                                                
           DISPLAY 'PROGRAM-ID : BP13C254 '.                                    
           DISPLAY '                                      '.                    
           DISPLAY 'CURR-DATE  : ' WS-SYS-DATE.                                 
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ  FROM  F205 ' WS-F205-READ.              
           DISPLAY 'NO OF RECORDS READ  FROM  K222 ' WS-K222-READ.              
           DISPLAY 'NO OF RECORDS WRITTEN TO F205A ' WS-F205A-WRITE.            
                                                                                
           CLOSE BP13F205                                                       
                 P13F205A                                                       
                 BP13K222.                                                      
                                                                                
           IF WS-K222-STATUS  NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR IN K222 FILE : ' WS-K222-STATUS              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
