       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C928.                                                 
      *AUTHOR.        MSD1.                                                     
      *DATE-WRITTEN.  09/11/2007.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE:  MATCH F410 WITH K893 AND K895 USING REGN-NO.  *            
      *              AND WRITE TO F800 AND F820, RESPECTIVELY      *            
      *                                                            *            
      *  INPUT FILE :  1. BP13F410 - MONTH SALES ORDER             *            
      *                2. BP13K893 - APPLICATION HISTORY           *            
      *                3. BP13K895 - OCCUPIER HISTORY              *            
      *  OUTPUT FILE:  1. BP13F800 - APPLICATOIN MASTER            *            
      *                2. BP13F820 - OCCUPIER FILE                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP133145  MSD1  20071109  NEW PGM                          *            
      *                                                            *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F410  ASSIGN        TO BP13F410.                          
                                                                                
           SELECT BP13K893  ASSIGN        TO BP13K893                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K893-KEY-FLD                       
                            FILE STATUS   IS BP13K893-STATUS.                   
                                                                                
           SELECT BP13K895  ASSIGN        TO BP13K895                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K895-KEY-FLD                       
                            FILE STATUS   IS BP13K895-STATUS.                   
                                                                                
           SELECT BP13F800  ASSIGN TO BP13F800.                                 
           SELECT BP13F820  ASSIGN TO BP13F820.                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F410                                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       COPY BP13F410.                                                           
                                                                                
       FD   BP13K893                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2050 CHARACTERS                                     
            LABEL RECORD IS STANDARD.                                           
       COPY BP13K893.                                                           
                                                                                
       FD   BP13K895                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS  450 CHARACTERS                                     
            LABEL RECORD IS STANDARD.                                           
       COPY BP13K895.                                                           
                                                                                
       FD   BP13F800                                                            
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13F820                                                            
            RECORDING MODE IS F.                                                
       COPY BP13F820.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-COUNTERS.                                                          
          05 WS-F410-READ         PIC 9(08) VALUE ZEROES.                       
          05 WS-F800-WRITE        PIC 9(08) VALUE ZEROES.                       
          05 WS-F820-WRITE        PIC 9(08) VALUE ZEROES.                       
                                                                                
       01 WS-FILE-STATUS.                                                       
          05 BP13K893-STATUS      PIC 9(02) VALUE ZEROES.                       
          05 BP13K895-STATUS      PIC 9(02) VALUE ZEROES.                       
                                                                                
       01 WS-EOF-SWITCH.                                                        
          05  WS-K893-EOF         PIC X(01) VALUE 'N'.                          
          05  WS-K895-EOF         PIC X(01) VALUE 'N'.                          
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F410  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-DATA   THRU 3000-EXIT                           
              UNTIL F410-NUM-REGN = HIGH-VALUES.                                
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F410                                                 
                       BP13K893                                                 
                       BP13K895                                                 
               OUTPUT  BP13F800                                                 
                       BP13F820.                                                
                                                                                
           IF BP13K893-STATUS  NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING - BP13K893 : ' BP13K893-STATUS             
              MOVE     BP13K893-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP13K895-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13K895 : ' BP13K895-STATUS             
              MOVE     BP13K895-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F410.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F410                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F410-NUM-REGN                            
                NOT AT END                                                      
                   ADD 1    TO WS-F410-READ                                     
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 4000-START-BP13K893   THRU 4000-EXIT                         
           PERFORM 5000-START-BP13K895   THRU 5000-EXIT                         
                                                                                
           PERFORM 2000-READ-BP13F410    THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-START-BP13K893.                                                     
      *-------------------------------------------------------------            
           MOVE SPACES        TO WS-K893-EOF                                    
                                 K893-KEY-FLD.                                  
           MOVE F410-NUM-REGN TO K893-NUM-REGN-HIST.                            
           START BP13K893 KEY >= K893-NUM-REGN-HIST.                            
                                                                                
           IF BP13K893-STATUS = 00                                              
              PERFORM  4010-READNXT-K893  THRU 4010-EXIT                        
              PERFORM  4020-WRITE-F800    THRU 4020-EXIT                        
                 UNTIL K893-NUM-REGN-HIST NOT = F410-NUM-REGN                   
                    OR WS-K893-EOF = 'Y'                                        
           ELSE                                                                 
               DISPLAY 'ERROR START BP13K893'  BP13K893-STATUS.                 
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4010-READNXT-K893.                                                       
      *-------------------------------------------------------------            
           READ BP13K893 NEXT AT END                                            
                MOVE 'Y' TO WS-K893-EOF.                                        
                                                                                
       4010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4020-WRITE-F800.                                                         
      *-------------------------------------------------------------            
                                                                                
           IF  K893-NUM-REGN-HIST = F410-NUM-REGN                               
           AND K893-NUM-STATUS NOT = 'C'                                        
              MOVE BP13K893-MASTER(1:2000) TO BP13F800-MASTER                   
              WRITE BP13F800-MASTER                                             
                                                                                
              ADD 1 TO WS-F800-WRITE                                            
           END-IF.                                                              
                                                                                
           PERFORM 4010-READNXT-K893  THRU 4010-EXIT.                           
                                                                                
       4020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-START-BP13K895.                                                     
      *-------------------------------------------------------------            
           MOVE SPACES        TO WS-K895-EOF                                    
                                 K895-KEY-FLD.                                  
           MOVE F410-NUM-REGN TO K895-NUM-REGN-HIST.                            
           START BP13K895 KEY >= K895-NUM-REGN-HIST.                            
                                                                                
           IF BP13K895-STATUS = 00                                              
              PERFORM  5010-READNXT-K895  THRU 5010-EXIT                        
              PERFORM  5020-WRITE-F820    THRU 5020-EXIT                        
                 UNTIL K895-NUM-REGN-HIST NOT = F410-NUM-REGN                   
                    OR WS-K895-EOF = 'Y'                                        
           ELSE                                                                 
               DISPLAY 'ERROR START BP13K895'  BP13K895-STATUS.                 
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5010-READNXT-K895.                                                       
      *-------------------------------------------------------------            
           READ BP13K895 NEXT AT END                                            
                MOVE 'Y' TO WS-K895-EOF.                                        
                                                                                
       5010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5020-WRITE-F820.                                                         
      *-------------------------------------------------------------            
                                                                                
           IF K895-NUM-REGN-HIST = F410-NUM-REGN                                
               MOVE BP13K895-REC(1:400) TO BP13F820-REC                         
               WRITE BP13F820-REC                                               
                                                                                
               ADD 1 TO WS-F820-WRITE                                           
           END-IF.                                                              
                                                                                
           PERFORM 5010-READNXT-K895  THRU 5010-EXIT.                           
                                                                                
       5020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13C928 CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ FROM BP13F410  : ' WS-F410-READ.            
           DISPLAY 'NO OF RECS WRITTEN TO BP13F800 : ' WS-F800-WRITE.           
           DISPLAY 'NO OF RECS WRITTEN TO BP13F820 : ' WS-F820-WRITE.           
                                                                                
           CLOSE BP13F410                                                       
                 BP13K893                                                       
                 BP13K895                                                       
                 BP13F800                                                       
                 BP13F820.                                                      
                                                                                
           IF BP13K893-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K893 : ' BP13K893-STATUS             
              MOVE BP13K893-STATUS TO RETURN-CODE.                              
                                                                                
           IF BP13K895-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING - BP13K895 : ' BP13K895-STATUS             
              MOVE BP13K895-STATUS TO RETURN-CODE.                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
