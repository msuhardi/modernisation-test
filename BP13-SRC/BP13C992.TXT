      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C992.                                                 
      *AUTHOR.        SATHAPPAN.                                                
      *DATE-WRITTEN.  19/12/2001.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *   OBJECTIVE   :  THIS PROGRAM IS USED TO TAKE THE BACK UP  *            
      *                  OF BP13F915 FOR SUCCESSFULLY COMMITED CASE*            
      *                  S IN CDB.                                 *            
      *                                                            *            
      *   INPUT  FILES:  1.P13F915A - SOC CDB TRANSACTION FILE     *            
      *                  2.P13F915B - SOC CDB TRANSACTION FILE     *            
      *   OUTPUT FILES:  1.BP13F915 - SOC CDB TRANSACTION FILE     *            
      *                                                            *            
      * CHG REF  DATE      OIC   REMARKS                           *            
      * -------- --------  ----  --------------------------------- *            
      * BP130850 20011218  AS1   NEW PROGRAM                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *---------------------*                                                   
       INPUT-OUTPUT SECTION.                                                    
      *---------------------*                                                   
      *-------------*                                                           
       FILE-CONTROL.                                                            
      *-------------*                                                           
                                                                                
           SELECT   P13F915A   ASSIGN       TO P13F915A.                        
                                                                                
           SELECT   P13F915B   ASSIGN       TO P13F915B.                        
                                                                                
           SELECT   BP13F915   ASSIGN       TO BP13F915.                        
      *--------------*                                                          
       DATA DIVISION.                                                           
      *--------------*                                                          
       FILE SECTION.                                                            
      *-------------*                                                           
                                                                                
       FD   P13F915A                    BLOCK CONTAINS 0 RECORDS                
                                        RECORD CONTAINS 800 CHARACTERS          
                                        LABEL RECORD IS STANDARD                
                                        RECORDING MODE  IS  F.                  
       01   P13F915A-REC.                                                       
            05 FILLER                   PIC X(2).                               
            05 F915A-NUM-REGN           PIC X(15).                              
            05 FILLER                   PIC X(783).                             
                                                                                
       FD   P13F915B                    BLOCK CONTAINS 0 RECORDS                
                                        RECORD CONTAINS 800 CHARACTERS          
                                        LABEL RECORD IS STANDARD                
                                        RECORDING MODE  IS  F.                  
       01   P13F915B-REC.                                                       
            05 FILLER                   PIC X(2).                               
            05 F915B-NUM-REGN           PIC X(15).                              
            05 FILLER                   PIC X(783).                             
                                                                                
       FD   BP13F915                    BLOCK CONTAINS 0 RECORDS                
                                        RECORD CONTAINS 800 CHARACTERS          
                                        LABEL RECORD IS STANDARD                
                                        RECORDING MODE  IS  F.                  
       COPY BP13F915.                                                           
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F915A-READ            PIC 9(6)    VALUE ZEROES.               
           05  WS-F915B-READ            PIC 9(6)    VALUE ZEROES.               
           05  WS-F915-WRT              PIC 9(6)    VALUE ZEROES.               
           05  WS-EDT-F915A-READ        PIC ZZZ,ZZ9 VALUE ZEROES.               
           05  WS-EDT-F915B-READ        PIC ZZZ,ZZ9 VALUE ZEROES.               
           05  WS-EDT-F915-WRT          PIC ZZZ,ZZ9 VALUE ZEROES.               
                                                                                
       01  WS-NUM-REGN                  PIC X(8)    VALUE SPACES.               
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
       0000-CONTROL.                                                            
      *-------------*                                                           
                                                                                
           PERFORM  1000-OPEN-FILES     THRU 1000-EXIT.                         
           PERFORM  2000-READ-P13F915A  THRU 2000-EXIT.                         
           PERFORM  3000-READ-P13F915B  THRU 3000-EXIT.                         
           PERFORM  4000-MAIN-ROUTINE   THRU 4000-EXIT                          
                        UNTIL F915A-NUM-REGN = HIGH-VALUE AND                   
                              F915B-NUM-REGN = HIGH-VALUE                       
           PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
           OPEN INPUT  P13F915A                                                 
                       P13F915B                                                 
                OUTPUT BP13F915.                                                
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------*                                                      
       2000-READ-P13F915A.                                                      
      *------------------*                                                      
                                                                                
           READ P13F915A AT END MOVE HIGH-VALUE TO F915A-NUM-REGN               
                            GO TO 2000-EXIT.                                    
                                                                                
           ADD  1                         TO WS-F915A-READ.                     
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------*                                                      
       3000-READ-P13F915B.                                                      
      *------------------*                                                      
                                                                                
           READ P13F915B AT END MOVE HIGH-VALUE TO F915B-NUM-REGN               
                            GO TO 3000-EXIT.                                    
                                                                                
           ADD  1                         TO WS-F915B-READ.                     
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------*                                                      
       4000-MAIN-ROUTINE.                                                       
      *------------------*                                                      
                                                                                
           IF F915A-NUM-REGN = F915B-NUM-REGN                                   
              MOVE F915B-NUM-REGN                TO   WS-NUM-REGN               
              PERFORM 3000-READ-P13F915B         THRU 3000-EXIT                 
                 UNTIL (F915B-NUM-REGN NOT = WS-NUM-REGN) OR                    
                       (F915B-NUM-REGN = HIGH-VALUE)                            
              PERFORM   2000-READ-P13F915A       THRU 2000-EXIT                 
           ELSE                                                                 
              IF F915A-NUM-REGN < F915B-NUM-REGN                                
                 PERFORM  2000-READ-P13F915A     THRU 2000-EXIT                 
              ELSE                                                              
                 IF F915A-NUM-REGN > F915B-NUM-REGN                             
                    PERFORM 5000-WRITE-BP13F915  THRU 5000-EXIT                 
                    PERFORM 3000-READ-P13F915B   THRU 3000-EXIT                 
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------*                                                     
       5000-WRITE-BP13F915.                                                     
      *-------------------*                                                     
                                                                                
            MOVE SPACES  TO BP13F915-REC.                                       
            INITIALIZE BP13F915-REC.                                            
                                                                                
            WRITE BP13F915-REC FROM P13F915B-REC.                               
            ADD   1            TO   WS-F915-WRT.                                
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------*                                                        
       9000-CLOSE-FILES.                                                        
      *----------------*                                                        
                                                                                
           MOVE WS-F915A-READ                    TO  WS-EDT-F915A-READ.         
           MOVE WS-F915B-READ                    TO  WS-EDT-F915B-READ.         
           MOVE WS-F915-WRT                      TO  WS-EDT-F915-WRT.           
                                                                                
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13C992 CONTROL '.                                         
           DISPLAY '-----------------------------------'.                       
           DISPLAY 'TOTAL P13F915A REC READ      = ' WS-EDT-F915A-READ.         
           DISPLAY 'TOTAL P13F915B REC READ      = ' WS-EDT-F915B-READ.         
           DISPLAY 'TOTAL BP13F915 REC WRITTEN   = ' WS-EDT-F915-WRT.           
                                                                                
           CLOSE      BP13F915                                                  
                      P13F915A                                                  
                      P13F915B.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
