       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C31K.                                                 
       AUTHOR.        KV5.                                                      
      *DATE-WRITTEN.  22/06/2020.                                               
      * ===============================================================*        
      * BP13 SYSTEM OF COMMITMENT                                      *        
      * ===============================================================*        
      * OBJECTIVE   : TO UPDATE KEY ISSUE DATE IF RECORD FOUND BP13F410*        
      *               TO BP13KH50                                      *        
      *                                                                *        
      *   INPUT FILES :  1. BP13F410                                   *        
      *                  2. BP13KH50                                   *        
      *                                                                *        
      *   I-O         :  1. BP13KH50                                   *        
      *                                                                *        
      *   CHGE-NO   BY     DATE    DESCRIPTION                         *        
      *   -------   ---   ------   ------------------------------------*        
      *   BP138336  KV5   22/06/20 NEW PROGRAM                         *        
      *================================================================*        
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410  ASSIGN           TO BP13F410.                       
                                                                                
           SELECT BP13KH50  ASSIGN           TO BP13KH50                        
                            ACCESS MODE      IS DYNAMIC                         
                            ORGANIZATION     IS INDEXED                         
                            RECORD KEY       IS KH50-KEY-FLD                    
                            FILE STATUS      IS WS-KH50-STATUS.                 
                                                                                
      ****************                                                          
       DATA DIVISION.                                                           
      ****************                                                          
                                                                                
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BP13F410                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F410.                                                           
                                                                                
       FD   BP13KH50                                                            
            RECORD CONTAINS 3000 CHARACTERS.                                    
       COPY BP13KH50.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-COUNT.                                                            
           05  WS-F410-READ            PIC 9(08) VALUE 0.                       
           05  WS-KH50-READ            PIC 9(08) VALUE 0.                       
           05  WS-KH50-NOTFND          PIC 9(08) VALUE 0.                       
           05  WS-KH50-UPD             PIC 9(08) VALUE 0.                       
                                                                                
       01  WS-F410-EOF                 PIC X(01) VALUE SPACES.                  
       01  WS-FILE-STATUS.                                                      
           05 WS-KH50-STATUS           PIC 99    VALUE ZEROS.                   
                                                                                
      *=============================================================*           
       PROCEDURE DIVISION.                                                      
      *=============================================================*           
       MAIN.                                                                    
      *=============================================================*           
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 2000-READ-INPUT      THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-DATA    THRU 3000-EXIT                          
           UNTIL WS-F410-EOF = 'Y'.                                             
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
      *=============================================================*           
       1000-OPEN-ROUTINE.                                                       
      *=============================================================*           
                                                                                
           OPEN INPUT  BP13F410                                                 
                I-O    BP13KH50.                                                
                                                                                
           IF WS-KH50-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KH50 ERROR ' WS-KH50-STATUS                  
              MOVE     WS-KH50-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       2000-READ-INPUT.                                                         
      *=============================================================*           
                                                                                
            READ BP13F410                                                       
                 AT END MOVE 'Y'  TO WS-F410-EOF                                
            END-READ.                                                           
                                                                                
            IF WS-F410-EOF NOT = 'Y'                                            
               ADD 1 TO WS-F410-READ                                            
            END-IF.                                                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================*           
       3000-PROCESS-DATA.                                                       
      *=============================================================*           
                                                                                
           PERFORM 3100-READ-BP13KH50   THRU 3100-EXIT                          
           PERFORM 2000-READ-INPUT      THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3100-READ-BP13KH50.                                                      
      *================================================================*        
           MOVE SPACES                     TO KH50-KEY-FLD.                     
           MOVE F410-NUM-REGN              TO KH50-NUM-REGN.                    
                                                                                
           READ BP13KH50.                                                       
                                                                                
           EVALUATE WS-KH50-STATUS                                              
              WHEN ZEROS                                                        
                 ADD 1                 TO WS-KH50-READ                          
                 MOVE F410-DTE-KEY-ISSUE   TO KH50-DTE-KEY-ISSUED               
                 PERFORM 3200-UPDATE-BP13KH50  THRU 3200-EXIT                   
              WHEN 23                                                           
                 ADD 1                 TO WS-KH50-NOTFND                        
                 MOVE SPACES           TO BP13KH50-REC                          
              WHEN OTHER                                                        
                MOVE  WS-KH50-STATUS TO RETURN-CODE                             
                DISPLAY 'ERROR READING BP13KH50 FILE : ' WS-KH50-STATUS         
                DISPLAY 'KH50-NUM-REGN = ' KH50-NUM-REGN                        
                PERFORM 9000-CLOSE-ROUTINE                                      
                   THRU 9000-EXIT                                               
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3200-UPDATE-BP13KH50.                                                    
      *================================================================*        
                                                                                
            REWRITE BP13KH50-REC.                                               
                                                                                
            EVALUATE WS-KH50-STATUS                                             
              WHEN 00                                                           
                   ADD 1 TO WS-KH50-UPD                                         
                                                                                
              WHEN OTHER                                                        
                   DISPLAY 'REWRITE BP13KH50 ERROR ' WS-KH50-STATUS             
                   MOVE WS-KH50-STATUS              TO RETURN-CODE              
                   PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       9000-CLOSE-ROUTINE.                                                      
      *================================================================*        
                                                                                
           DISPLAY 'PROGRAM: BP13C31K'.                                         
           DISPLAY 'TOTAL F410 INPUT RECORDS   : ' WS-F410-READ.                
           DISPLAY 'TOTAL KH50 INPUT RECORDS   : ' WS-KH50-READ.                
           DISPLAY 'TOTAL KH50 RECORDS NOTFND  : ' WS-KH50-NOTFND.              
           DISPLAY 'TOTAL RECORD UPD KH50      : ' WS-KH50-UPD.                 
           DISPLAY '        '                                                   
                                                                                
           CLOSE BP13F410                                                       
                 BP13KH50                                                       
                                                                                
           IF WS-KH50-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KH50 ERROR ' WS-KH50-STATUS                  
              MOVE     WS-KH50-STATUS      TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
