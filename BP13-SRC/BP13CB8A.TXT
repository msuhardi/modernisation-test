       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB8A.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  04/02/2015.                                               
      *===============================================================*         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      *  OBJECTIVE  :  EXTRACT CASES WITH MATCHING RECORDS OF ITS OWN *         
      *                                                               *         
      *  INPUT      :  BP13F595 - NRIC1 ON ITS OWN (NO DUPLICATES)    *         
      *                BP13F500 - EXTRACTED RECORDS FROM BP13CB80     *         
      *  OUTPUT     :  BP13F50A - OUTPUT FILE                         *         
      *                                                               *         
      *===============================================================*         
      * CHG-NO   DATE     BY   DESCRIPTION                            *         
      * -------- -------- ---- -------------------------------------- *         
      * BP135635 04022015 SMR2 NEW PROGRAM                            *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F595  ASSIGN       TO BP13F595.                           
           SELECT BP13F500  ASSIGN       TO BP13F500.                           
           SELECT BP13F50A  ASSIGN       TO BP13F50A.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F595                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13F595.                                                           
                                                                                
       FD  BP13F500                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13F500.                                                           
                                                                                
       FD  BP13F50A                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 500 CHARACTERS.                                      
       01  BP13F50A-REC           PIC X(500).                                   
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F595-EOF             PIC X(01) VALUE 'N'.                     
           05  WS-F500-EOF             PIC X(01) VALUE 'N'.                     
                                                                                
       01  WS-FILE-COUNTERS.                                                    
           05  WS-F595-READ            PIC 9(08) VALUE ZEROES.                  
           05  WS-F500-READ            PIC 9(08) VALUE ZEROES.                  
           05  WS-F50A-WRITE           PIC 9(08) VALUE ZEROES.                  
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-EDT-DATE             PIC X(10) VALUE SPACES.                  
           05  WS-CUR-DATE             PIC X(08) VALUE SPACES.                  
           05  WS-EDIT1                PIC Z(07)9.                              
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F595      THRU 2000-EXIT.                      
           PERFORM 3000-READ-BP13F500      THRU 3000-EXIT.                      
           PERFORM 4000-PROCESS-DATA       THRU 4000-EXIT                       
             UNTIL WS-F595-EOF = 'Y'                                            
                OR WS-F500-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  BP13F595                                                 
                       BP13F500                                                 
                OUTPUT BP13F50A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CUR-DATE.                      
           STRING WS-CUR-DATE(7:2), '/',                                        
                  WS-CUR-DATE(5:2), '/',                                        
                  WS-CUR-DATE(1:4)  DELIMITED BY SIZE                           
                  INTO WS-EDT-DATE.                                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F595.                                                      
      *---------------------------------------------------------------*         
           READ BP13F595                                                        
             AT END                                                             
                MOVE 'Y'                   TO WS-F595-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F595-READ.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-READ-BP13F500.                                                      
      *---------------------------------------------------------------*         
           READ BP13F500                                                        
             AT END                                                             
                MOVE 'Y'                   TO WS-F500-EOF                       
                GO TO 3000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F500-READ.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-PROCESS-DATA.                                                       
      *---------------------------------------------------------------*         
           IF F595-NUM-NRIC1 = F500-NUM-NRIC1                                   
              WRITE BP13F50A-REC            FROM BP13F500-REC                   
              ADD 1                         TO WS-F50A-WRITE                    
              PERFORM 3000-READ-BP13F500    THRU 3000-EXIT                      
           ELSE                                                                 
              IF F595-NUM-NRIC1 < F500-NUM-NRIC1                                
                 PERFORM 2000-READ-BP13F595 THRU 2000-EXIT                      
              ELSE                                                              
                 PERFORM 3000-READ-BP13F500 THRU 3000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13CB8A       ' WS-EDT-DATE.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-F595-READ               TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS READ FROM BP13F595  : ' WS-EDIT1.                
           MOVE WS-F500-READ               TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS READ FROM BP13F500  : ' WS-EDIT1.                
           MOVE WS-F50A-WRITE              TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS WRITTEN TO BP13F50A : ' WS-EDIT1.                
           DISPLAY ' '.                                                         
           DISPLAY '*---------------------------------------------*'.           
                                                                                
           CLOSE BP13F595                                                       
                 BP13F500                                                       
                 BP13F50A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
