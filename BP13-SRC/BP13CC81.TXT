      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13CC81.                                                 
      *AUTHOR.        EDEN ROSE DINGAL.                                         
      *DATE-WRITTEN.  14/05/2007.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT  (BP13)                                *           
      * =========================================================== *           
      *  OBJECTIVES : 1) TO CREATE AB02F820 FOR THE CESSATION       *           
      *                  OF CPF DEDUCTION                           *           
      *                                                             *           
      * INPUT FILES  :  1.  BP13FD92                                *           
      *                 2.  BP13K800                                *           
      *                 3.  BP13K820                                *           
      *                 4.  SY02F001                                *           
      *                 5.  PIDB TABLE - FLAT ADDR                  *           
      *                                                             *           
      * OUTPUT       :  1.  AB02F820                                *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG REF  DATE     BY   DESCRIPTION                          *           
      * -------- ------   ---  -----------                          *           
      * BP133052 20070514 ED1  NEW PROGRAM                          *           
      *          20070523 ED1  ADDED REGNO IN REPORT                *           
      *          20070611 BA3  MOVE SPACES TO F820-DTE-SEND-CPFB    *           
      * BP133171 20071102 ED1  REMOVED UPD OF KD92-DTE&TME UPDATE   *           
      * =========================================================== *           
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
                                                                                
         SOURCE-COMPUTER.   IBM-3090.                                           
         OBJECT-COMPUTER.   IBM-3090.                                           
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
         FILE-CONTROL.                                                          
           SELECT BP13FD92 ASSIGN TO BP13FD92.                                  
           SELECT AB02F820 ASSIGN TO AB02F820.                                  
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13KD92 ASSIGN TO BP13KD92                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS KD92-KEY-FLD                                    
                FILE STATUS  IS WS-KD92-STATUS.                                 
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K800-NUM-REGN                                   
                FILE STATUS  IS WS-K800-STATUS.                                 
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS RANDOM                                          
                RECORD KEY   IS K820-KEY-FLD                                    
                FILE STATUS  IS WS-K820-STATUS.                                 
                                                                                
           SELECT BP13LC81  ASSIGN TO BP13LC81.                                 
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
                                                                                
       FD   BP13FD92    BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 1000 CHARACTERS                         
                        LABEL RECORDS ARE STANDARD                              
                        RECORDING MODE IS F.                                    
       COPY BP13FD92.                                                           
                                                                                
       FD   AB02F820    BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 1000 CHARACTERS                         
                        LABEL RECORDS ARE STANDARD                              
                        RECORDING MODE IS F.                                    
       COPY AB02F820.                                                           
                                                                                
       FD   BP13KD92    RECORD CONTAINS 1000 CHARACTERS.                        
       COPY BP13KD92.                                                           
                                                                                
       FD   BP13K800    RECORD CONTAINS 2000 CHARACTERS.                        
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820    RECORD CONTAINS 400 CHARACTERS.                         
       COPY BP13K820.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13LC81    BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 132 CHARACTERS                          
                        LABEL RECORD IS OMITTED                                 
                        RECORDING MODE IS F.                                    
       01  LC81-PRTREC  PIC X(132).                                             
                                                                                
      *--------------------------------------------------------------           
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------           
       01  WS-COUNTERS.                                                         
           05 WS-CNT-FD92-READ       PIC 9(7) VALUE ZEROES.                     
           05 WS-CNT-KD92-UPD        PIC 9(7) VALUE ZEROES.                     
           05 WS-CNT-F820-WRITE      PIC 9(7) VALUE ZEROES.                     
           05 WS-CNT-LC81-WRITE      PIC 9(7) VALUE ZEROES.                     
           05 WS-SNO                 PIC 9(5) VALUE ZEROES.                     
           05 WS-PAGE-CTR            PIC 9(4) VALUE ZEROES.                     
           05 WS-LINE-CTR            PIC 9(2) VALUE ZEROES.                     
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 WS-KD92-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K800-STATUS         PIC 9(2) VALUE ZEROES.                     
           05 WS-K820-STATUS         PIC 9(2) VALUE ZEROES.                     
                                                                                
       01  WS-DATE-HDR               PIC X(10) VALUE SPACES.                    
       01  WS-MAX-LINE               PIC 99    VALUE 56.                        
                                                                                
       01  WS-SYSTEM-DATE-TIME.                                                 
           05  WS-SYS-DATE               PIC X(08) VALUE SPACES.                
           05  WS-SYS-TIME               PIC X(08) VALUE SPACES.                
           05  FILLER                    PIC X(05) VALUE SPACES.                
                                                                                
       01  WS-DATE.                                                             
           05  WS-CCYY               PIC X(4).                                  
           05  WS-MM                 PIC X(2).                                  
           05  WS-MM-R REDEFINES WS-MM.                                         
               10 WS-MTH             PIC 9(2).                                  
           05  WS-DD                 PIC X(2).                                  
                                                                                
       01  WS-TOTAL              PIC      9(7)V99 VALUE ZEROES.                 
                                                                                
       01 WS-MONTH-NAME.                                                        
          05 WS-STRING PIC X(36) VALUE                                          
                'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'.                         
          05 WS-MONTH  REDEFINES WS-STRING OCCURS 12 TIMES PIC XXX.             
                                                                        01010000
       01  WS-BLANK-LINE  PIC X(132)             VALUE SPACES.                  
                                                                                
       01  WS-DASH-LINE   PIC X(132)             VALUE ALL '-'.                 
                                                                                
       01  LC81-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13LC81'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LC81-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LC81-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
                                                                                
       01  LC81-HEADER02.                                                       
           05  FILLER              PIC X(39)     VALUE SPACES.                  
           05  FILLER              PIC X(49)     VALUE                          
               'REPORT ON CESSATION OF CPF DEDUCTION SENT TO CPFB'.             
                                                                                
       01  LC81-HEADER03.                                                       
           05  FILLER              PIC X(03)     VALUE 'S/N'.                   
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'REGN NO'.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'SCH-ACC'.               
           05  FILLER              PIC X(09)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'CPF NO.'.               
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'NAME'.                  
           05  FILLER              PIC X(64)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'CEASE CODE'.            
                                                                                
       01 LC81-DETAILS.                                                         
          05 LC81-DTL-SERIAL      PIC ZZZZ9     VALUE ZEROES.                   
          05 FILLER               PIC X(01)     VALUE SPACES.                   
          05 LC81-REGNO           PIC X(08)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LC81-SCH-ACC         PIC X(14)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LC81-CPFNO1          PIC X(09)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LC81-NAME1           PIC X(66)     VALUE SPACES.                   
          05 FILLER               PIC X(07)     VALUE SPACES.                   
          05 LC81-CEASE-CDE1      PIC X(01)     VALUE SPACES.                   
                                                                                
       01 LC81-DETAIL2.                                                         
          05 FILLER               PIC X(32)     VALUE SPACES.                   
          05 LC81-CPFNO2          PIC X(09)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LC81-NAME2           PIC X(66)     VALUE SPACES.                   
          05 FILLER               PIC X(07)     VALUE SPACES.                   
          05 LC81-CEASE-CDE2      PIC X(01)     VALUE SPACES.                   
                                                                                
       01 LC81-DETAIL3.                                                         
          05 FILLER               PIC X(32)     VALUE SPACES.                   
          05 LC81-CPFNO3          PIC X(09)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LC81-NAME3           PIC X(66)     VALUE SPACES.                   
          05 FILLER               PIC X(07)     VALUE SPACES.                   
          05 LC81-CEASE-CDE3      PIC X(01)     VALUE SPACES.                   
                                                                                
       01 LC81-DETAIL4.                                                         
          05 FILLER               PIC X(32)     VALUE SPACES.                   
          05 LC81-CPFNO4          PIC X(09)     VALUE SPACES.                   
          05 FILLER               PIC X(02)     VALUE SPACES.                   
          05 LC81-NAME4           PIC X(66)     VALUE SPACES.                   
          05 FILLER               PIC X(07)     VALUE SPACES.                   
          05 LC81-CEASE-CDE4      PIC X(01)     VALUE SPACES.                   
                                                                                
      *-------------------------------------------------------------            
      *  LINKAGE FOR BP13C913                                                   
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH            PIC X(04).                             
           05 WS-LINK-NUM-ACC            PIC X(04).                             
           05 WS-LINK-NUM-CHK            PIC X(01).                             
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
           PERFORM 1000-START-UP          THRU 1000-EXIT.                       
           PERFORM 2000-READ-BP13FD92     THRU 2000-EXIT.                       
           PERFORM 3000-PROCESS-RECORDS   THRU 3000-EXIT                        
                   UNTIL FD92-NUM-REGN = HIGH-VALUES.                           
           PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       1000-START-UP.                                                           
      *--------------------------------------------------------------           
           OPEN INPUT  BP13FD92                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       SY02F001                                                 
               I-O     BP13KD92                                                 
               OUTPUT  AB02F820                                                 
                       BP13LC81.                                                
                                                                                
           IF WS-KD92-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KD92 FILE ERROR ' WS-KD92-STATUS             
              MOVE     WS-KD92-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 FILE ERROR ' WS-K800-STATUS             
              MOVE     WS-K800-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K820 FILE ERROR ' WS-K820-STATUS             
              MOVE     WS-K820-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT TO WS-DATE.                                    
                                                                                
           STRING WS-DD '/' WS-MM '/' WS-CCYY                                   
           DELIMITED BY SIZE                   INTO WS-DATE-HDR.                
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       2000-READ-BP13FD92.                                                      
      *--------------------------------------------------------------           
           READ BP13FD92                                                        
              AT END MOVE HIGH-VALUES TO FD92-NUM-REGN                          
              GO TO 2000-EXIT.                                                  
                                                                                
           ADD 1 TO WS-CNT-FD92-READ.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3000-PROCESS-RECORDS.                                                    
      *--------------------------------------------------------------           
           IF (FD92-CDE-CEASE-LSE(1) = '1'   OR                                 
               FD92-CDE-CEASE-LSE(2) = '1'   OR                                 
               FD92-CDE-CEASE-LSE(3) = '1'   OR                                 
               FD92-CDE-CEASE-LSE(4) = '1' ) AND                                
             ( FD92-DTE-SEND-CPF     = SPACES OR LOW-VALUES OR ZEROS )          
                                                                                
              PERFORM 3100-READ-BP13K800     THRU 3100-EXIT                     
                                                                                
              IF WS-K800-STATUS = 00                                            
                                                                                
                 MOVE SPACES       TO AB02F820-REC                              
                 INITIALIZE           AB02F820-REC                              
                                                                                
                 PERFORM 3200-READ-BP13KD92     THRU 3200-EXIT                  
                 PERFORM 3300-REWRITE-BP13KD92  THRU 3300-EXIT                  
                 PERFORM 4000-FILL-AB02F820     THRU 4000-EXIT                  
                                                                                
                 WRITE                AB02F820-REC                              
                 ADD 1             TO WS-CNT-F820-WRITE                         
                                                                                
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FD92        THRU 2000-EXIT.                    
                                                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3100-READ-BP13K800.                                                      
      *--------------------------------------------------------------           
           MOVE FD92-NUM-REGN TO K800-NUM-REGN.                                 
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = 00                                               
              CONTINUE                                                          
                                                                                
           ELSE                                                                 
             IF WS-K800-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN K800, STATUS ' WS-K800-STATUS           
                        ', REGN IS ' K800-NUM-REGN                              
             ELSE                                                               
                DISPLAY 'ERROR READING K800, STATUS ' WS-K800-STATUS            
                        ', REGN IS ' K800-NUM-REGN                              
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3200-READ-BP13KD92.                                                      
      *--------------------------------------------------------------           
           MOVE SPACES        TO BP13KD92-REC.                                  
           MOVE 'STOP'        TO KD92-NUM-TRANS.                                
           MOVE FD92-NUM-REGN TO KD92-NUM-REGN.                                 
           READ BP13KD92.                                                       
                                                                                
           IF WS-KD92-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
             IF WS-KD92-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN KD92, STATUS ' WS-KD92-STATUS           
                        ', REGN IS ' KD92-NUM-REGN                              
             ELSE                                                               
                DISPLAY 'ERROR READING KD92, STATUS ' WS-KD92-STATUS            
                        ', REGN IS ' KD92-NUM-REGN                              
             END-IF                                                             
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       3300-REWRITE-BP13KD92.                                                   
      *--------------------------------------------------------------           
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE-TIME.                   
           MOVE  'BP13CC81'           TO KD92-NUM-USERID.                       
           MOVE  F001-DTE-CURRENT     TO KD92-DTE-SEND-CPF.                     
                                                                                
           REWRITE BP13KD92-REC.                                                
                                                                                
           EVALUATE WS-KD92-STATUS                                              
           WHEN 00                                                              
                ADD 1 TO WS-CNT-KD92-UPD                                        
                                                                                
           WHEN OTHER                                                           
               DISPLAY 'BP13KD92 - ERROR IN REWRITE, STATUS: '                  
                        WS-KD92-STATUS ' KEY ' KD92-KEY-FLD                     
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       4000-FILL-AB02F820.                                                      
      *--------------------------------------------------------------           
           MOVE SPACES                   TO   LC81-DETAILS.                     
           MOVE SPACES                   TO   LC81-DETAIL2.                     
           MOVE SPACES                   TO   LC81-DETAIL3.                     
           MOVE SPACES                   TO   LC81-DETAIL4.                     
           MOVE 'D'                      TO   F820-CDE-TRANS.                   
           MOVE 'C'                      TO   F820-CDE-ACCT-STAT.               
           MOVE  1                       TO   F820-NUM-SEQ.                     
           MOVE '0'                      TO   F820-NUM-HDB-REF-PREFIX.          
           MOVE 'SS'                     TO   F820-CDE-OFFICE.                  
           MOVE 'BP13CC81'               TO   F820-NUM-USERID.                  
           MOVE FD92-NUM-SCH-ACC         TO   F820-NUM-SCH-ACCT-LSE.            
           MOVE FD92-DTE-UPDATE          TO   F820-DTE-CREATE.                  
           MOVE SPACES                   TO   F820-DTE-SEND-CPFB.               
           MOVE 'N'                      TO   F820-CDE-NET-PROCEED              
           MOVE 'N'                      TO   F820-CDE-VOUCHER.                 
           MOVE FD92-DTE-REVSD-INSTAL-EFF                                       
                                         TO  F820-DTE-INT-COMPUTE.              
                                                                                
           IF FD92-CDE-CEASE-LSE(1) = '1' AND                                   
              K800-NUM-NRIC1 NOT    = SPACES AND LOW-VALUES AND ZEROES          
              MOVE SPACES                TO   BP13K820-REC                      
              MOVE K800-NUM-NRIC1        TO   K820-NUM-NRIC                     
              MOVE FD92-NUM-REGN         TO   K820-NUM-REGN                     
              PERFORM 4100-READ-BP13K820 THRU 4100-EXIT                         
              MOVE K820-NME-OCCP         TO   F820-NME-LSE1                     
                                              LC81-NAME1                        
              MOVE FD92-NUM-CPF(1)       TO   F820-NUM-CPF-ACCT1                
                                              LC81-CPFNO1                       
              MOVE '1'                   TO   F820-CDE-CEASE-LSE1               
                                              LC81-CEASE-CDE1                   
           END-IF.                                                              
                                                                                
           IF FD92-CDE-CEASE-LSE(2) = '1' AND                                   
              K800-NUM-NRIC2 NOT    = SPACES AND LOW-VALUES AND ZEROES          
              MOVE SPACES                TO   BP13K820-REC                      
              MOVE K800-NUM-NRIC2        TO   K820-NUM-NRIC                     
              MOVE FD92-NUM-REGN         TO   K820-NUM-REGN                     
              PERFORM 4100-READ-BP13K820 THRU 4100-EXIT                         
              MOVE K820-NME-OCCP         TO   F820-NME-LSE2                     
                                              LC81-NAME2                        
              MOVE FD92-NUM-CPF(2)       TO   F820-NUM-CPF-ACCT2                
                                              LC81-CPFNO2                       
              MOVE '1'                   TO   F820-CDE-CEASE-LSE2               
                                              LC81-CEASE-CDE2                   
           END-IF.                                                              
                                                                                
           IF FD92-CDE-CEASE-LSE(3) = '1' AND                                   
              K800-NUM-NRIC2 NOT    = SPACES AND LOW-VALUES AND ZEROES          
              MOVE SPACES                TO   BP13K820-REC                      
              MOVE K800-NUM-NRIC3        TO   K820-NUM-NRIC                     
              MOVE FD92-NUM-REGN         TO   K820-NUM-REGN                     
              PERFORM 4100-READ-BP13K820 THRU 4100-EXIT                         
              MOVE K820-NME-OCCP         TO   F820-NME-LSE3                     
                                              LC81-NAME3                        
              MOVE FD92-NUM-CPF(3)       TO   F820-NUM-CPF-ACCT3                
                                              LC81-CPFNO3                       
              MOVE '1'                   TO   F820-CDE-CEASE-LSE3               
                                              LC81-CEASE-CDE3                   
           END-IF.                                                              
                                                                                
           IF FD92-CDE-CEASE-LSE(4) = '1' AND                                   
              K800-NUM-NRIC2 NOT    = SPACES AND LOW-VALUES AND ZEROES          
              MOVE SPACES                TO   BP13K820-REC                      
              MOVE K800-NUM-NRIC4        TO   K820-NUM-NRIC                     
              MOVE FD92-NUM-REGN         TO   K820-NUM-REGN                     
              PERFORM 4100-READ-BP13K820 THRU 4100-EXIT                         
              MOVE K820-NME-OCCP         TO   F820-NME-LSE4                     
                                              LC81-NAME4                        
              MOVE FD92-NUM-CPF(4)       TO   F820-NUM-CPF-ACCT4                
                                              LC81-CPFNO4                       
              MOVE '1'                   TO   F820-CDE-CEASE-LSE4               
                                              LC81-CEASE-CDE4                   
           END-IF.                                                              
                                                                                
           PERFORM 4200-GET-FLAT-ADDR         THRU 4200-EXIT.                   
           PERFORM 4300-MOVE-REPORT-DTLS      THRU 4300-EXIT.                   
           PERFORM 4400-PRINT-REPORT          THRU 4400-EXIT.                   
                                                                                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       4100-READ-BP13K820.                                                      
      *--------------------------------------------------------------           
                                                                                
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
             IF WS-K820-STATUS = 23                                             
                DISPLAY 'REC NOT FND IN K820, STATUS ' WS-K820-STATUS           
                        ', KEY IS ' FD92-NUM-REGN ' ' K820-NUM-NRIC             
             ELSE                                                               
                DISPLAY 'ERROR READING K820, STATUS ' WS-K820-STATUS            
                        ', KEY IS ' FD92-NUM-REGN ' ' K820-NUM-NRIC             
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------*           
       4200-GET-FLAT-ADDR.                                                      
      *-------------------------------------------------------------*           
           MOVE SPACES                TO BP13COMM8-REC.                         
           MOVE K800-NUM-SCH-ACC(1:9) TO WS-LINK-REC.                           
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
             WHEN 000                                                           
                MOVE COMM8-NUM-BLK          TO  F820-NUM-BLK                    
                MOVE COMM8-NME-STREET       TO  F820-NME-STREET                 
                MOVE COMM8-NUM-LEVEL        TO  F820-NUM-HSE-LVL                
                MOVE COMM8-NUM-POSTAL-CODE  TO  F820-NUM-POST-CDE               
                                                                                
                STRING COMM8-NUM-UNIT-MAIN COMM8-NUM-UNIT-SUB                   
                     DELIMITED BY SIZE    INTO  F820-NUM-HSE-UNIT               
                END-STRING                                                      
                                                                                
             WHEN 100                                                           
                DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE - '                     
                         K800-NUM-SCH-ACC ', REGN : ' FD92-NUM-REGN             
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR              
                        ', AT KEY ' K800-NUM-SCH-ACC                            
                        ', REGN : ' FD92-NUM-REGN                               
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *--------------------------------------------------------------           
       4300-MOVE-REPORT-DTLS.                                                   
      *--------------------------------------------------------------           
           MOVE WS-DATE-HDR          TO  LC81-DATE-HDR.                         
           MOVE FD92-NUM-REGN        TO  LC81-REGNO.                            
                                                                                
           STRING FD92-NUM-SCH-ACC(1:4) '-'                                     
                  FD92-NUM-SCH-ACC(5:4) '-'                                     
                  FD92-NUM-SCH-ACC(9:1) '-'                                     
                  FD92-NUM-SCH-ACC(10:2)                                        
                  DELIMITED BY SIZE INTO LC81-SCH-ACC.                          
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       4400-PRINT-REPORT.                                                       
      *--------------------------------------------------------------           
           ADD   1                      TO  WS-SNO.                             
           MOVE  WS-SNO                 TO  LC81-DTL-SERIAL.                    
                                                                                
           IF WS-LINE-CTR > WS-MAX-LINE OR                                      
              WS-SNO       = 1                                                  
              ADD            1   TO WS-PAGE-CTR                                 
              MOVE ZEROES        TO LC81-PAGE-HDR                               
              MOVE WS-PAGE-CTR   TO LC81-PAGE-HDR                               
              PERFORM 4500-PRINT-HEADING THRU 4500-EXIT                         
           END-IF.                                                              
                                                                                
           WRITE LC81-PRTREC FROM LC81-DETAILS.                                 
           WRITE LC81-PRTREC FROM LC81-DETAIL2.                                 
           WRITE LC81-PRTREC FROM LC81-DETAIL3.                                 
           WRITE LC81-PRTREC FROM LC81-DETAIL4.                                 
           WRITE LC81-PRTREC FROM WS-BLANK-LINE.                                
                                                                                
           ADD 5  TO  WS-LINE-CTR.                                              
           ADD 1  TO  WS-CNT-LC81-WRITE.                                        
                                                                                
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------           
       4500-PRINT-HEADING.                                                      
      *--------------------------------------------------------------           
            WRITE LC81-PRTREC FROM WS-BLANK-LINE AFTER PAGE.                    
            WRITE LC81-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC81-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC81-PRTREC FROM LC81-HEADER01.                               
            WRITE LC81-PRTREC FROM LC81-HEADER02.                               
            WRITE LC81-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LC81-PRTREC FROM LC81-HEADER03.                               
            WRITE LC81-PRTREC FROM WS-DASH-LINE.                                
                                                                                
            INITIALIZE WS-LINE-CTR.                                             
            MOVE  8 TO WS-LINE-CTR.                                             
                                                                                
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------------           
       9000-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------------           
                                                                                
           CLOSE  BP13FD92                                                      
                  BP13KD92                                                      
                  BP13K800                                                      
                  BP13K820                                                      
                  AB02F820                                                      
                  SY02F001                                                      
                  BP13LC81.                                                     
                                                                                
                                                                                
           DISPLAY ' RUN   DATE :  ' WS-DATE-HDR.                               
           DISPLAY ' *------------ BP13CC81 AUDIT TRAIL ------------*'.         
           DISPLAY '    '.                                                      
           DISPLAY 'NO. OF RECORDS READ FROM DAILY TRANS (BP13FD92) = '         
                   WS-CNT-FD92-READ.                                            
           DISPLAY 'NO. OF RECORDS UPDATED IN DAILY TRANS(BP13KD92) = '         
                   WS-CNT-KD92-UPD.                                             
           DISPLAY 'NO. OF RECORDS CREATED FOR CPF REFUND(AB02F820) = '         
                   WS-CNT-F820-WRITE.                                           
           DISPLAY 'NO. OF RECORDS WRITTEN TO REPORT     (BP13LC81) = '         
                   WS-CNT-LC81-WRITE.                                           
                                                                                
           IF WS-KD92-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13KD92 FILE ERROR ' WS-KD92-STATUS             
              MOVE     WS-KD92-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13K800 FILE ERROR ' WS-K800-STATUS             
              MOVE     WS-K800-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'CLOSING BP13K820 FILE ERROR ' WS-K820-STATUS             
              MOVE     WS-K820-STATUS   TO  RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
