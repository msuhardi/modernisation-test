       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC69.                                                 
      *AUTHOR.        ED1.                                                      
      *DATE-WRITTEN.  01/03/2007.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE : TO MATCH FD38 WITH F120 TO GET FLAT-TYPE           *        
      *             AND UPDATE BP13FD38                                *        
      *                                                                *        
      * INPUT  FILES : BM06F120                                        *        
      * I-O    FILE  : BP13FD38                                        *        
      *                                                                *        
      * CHG-NO    BY    ON    - DESCRIPTION                            *        
      * ---------------------------------                              *        
      * BP132888 ED1 01/03/07   NEW PROGRAM                            *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT   BM06F120      ASSIGN TO BM06F120.                           
           SELECT   BP13FD38      ASSIGN TO BP13FD38.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BM06F120                RECORD CONTAINS 700 CHARACTERS              
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BM06F120.                                                           
                                                                                
       FD   BP13FD38                RECORD CONTAINS 400 CHARACTERS              
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BP13FD38.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  WS-F120-READ                 PIC 9(07) VALUE ZEROS.              
           05  WS-FD38-READ                 PIC 9(07) VALUE ZEROS.              
           05  NO-FD38-REWRITE              PIC 9(07) VALUE ZEROS.              
           05  WS-UNMATCH-FD38              PIC 9(07) VALUE ZEROS.              
           05  WS-UNMATCH-F120              PIC 9(07) VALUE ZEROS.              
           05  WS-MATCH-REGN                PIC 9(07) VALUE ZEROS.              
                                                                                
       01  WS-SYSTEM-DATE-TIME.                                                 
           05  WS-SYS-DATE                  PIC X(08) VALUE SPACES.             
           05  WS-SYS-TIME                  PIC X(08) VALUE SPACES.             
           05  FILLER                       PIC X(05) VALUE SPACES.             
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CUR-DD                   PIC 9(02) VALUE ZEROES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-MM                   PIC 9(02) VALUE ZEROES.              
           05  FILLER                      PIC X(01) VALUE '/'.                 
           05  WS-CUR-CCYY                 PIC 9(04) VALUE ZEROES.              
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BM06F120       THRU 2000-EXIT.                     
           PERFORM 2100-READ-BP13FD38       THRU 2100-EXIT.                     
           PERFORM 3000-PROCESS-RECORD      THRU 3000-EXIT                      
             UNTIL FD38-NUM-REGN = HIGH-VALUES AND                              
                   F120-REGN-NO  = HIGH-VALUES                                  
           PERFORM 9000-CLOSING-ROUTINE     THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT BM06F120                                                  
                I-O   BP13FD38.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE-TIME.                   
           MOVE WS-SYS-DATE(1:4)      TO WS-CUR-CCYY.                           
           MOVE WS-SYS-DATE(5:2)      TO WS-CUR-MM.                             
           MOVE WS-SYS-DATE(7:2)      TO WS-CUR-DD.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BM06F120.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BM06F120                                                        
             AT END MOVE HIGH-VALUES TO F120-REGN-NO                            
             GO TO 2000-EXIT.                                                   
                                                                                
           ADD  1                    TO WS-F120-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2100-READ-BP13FD38.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13FD38                                                        
             AT END MOVE HIGH-VALUES TO  FD38-NUM-REGN                          
             GO TO 2100-EXIT.                                                   
                                                                                
           ADD  1                    TO  WS-FD38-READ.                          
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
                                                                                
           IF FD38-NUM-REGN = F120-REGN-NO                                      
              PERFORM 3100-UPD-FD38          THRU 3100-EXIT                     
              PERFORM 2100-READ-BP13FD38     THRU 2100-EXIT                     
                                                                                
           ELSE                                                                 
              IF FD38-NUM-REGN > F120-REGN-NO                                   
                 ADD   1                     TO   WS-UNMATCH-F120               
                 PERFORM 2000-READ-BM06F120  THRU 2000-EXIT                     
              ELSE                                                              
                 ADD 1                       TO   WS-UNMATCH-FD38               
                 PERFORM 2100-READ-BP13FD38  THRU 2100-EXIT                     
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3100-UPD-FD38.                                                           
      *-------------------------------------------------------------            
           MOVE F120-CDE-TYP-ACTUSE  TO FD38-NUM-FLAT-TYPE.                     
                                                                                
           REWRITE BP13FD38-TRANS-REC.                                          
           ADD 1   TO  NO-FD38-REWRITE.                                         
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CC69'.                                
           DISPLAY 'CURRENT DATE   :  ' WS-CURRENT-DATE.                        
           DISPLAY '***************************************'.                   
           DISPLAY 'NO. OF BP13FD38 READ     : ' WS-FD38-READ.                  
           DISPLAY 'NO. OF BP13FD38 UPDATED  : ' NO-FD38-REWRITE.               
           DISPLAY 'NO. OF FD38 UNMATCHED    : ' WS-UNMATCH-FD38.               
                                                                                
           CLOSE BM06F120                                                       
                 BP13FD38.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
