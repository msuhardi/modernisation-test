      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C485.                                                 
      *AUTHOR.        MCC.                                                      
      *DATE-WRITTEN.  29/05/00.                                                 
                                                                                
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      * ====================================================== *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1. TO PRINT A WEEKLY REPORT FOR DEFERRED INCOME     *                
      *       NOT EQUAL TO SELLING PRICE.                      *                
      *                                                        *                
      *    INPUT  FILE  : BP13F410 - SALES ORDER FILE          *                
      *                   BP13F120 - GL TRANSACTION FILE       *                
      *                   BP13K830 - ADDRESS FILE              *                
      *                                                        *                
      *    LISTING      : BP13L485 - DAILY REPORT ON REFUND    *                
      *                              OF BKFEE/CD TO STDAPT     *                
      *                              LESSEES                   *                
      *--------------------------------------------------------*                
      * CHG REF  BY  DATE       DESCRIPTION                    *                
      * -------- --- ---------- -----------                    *                
      * BP130998 MCC 29/05/2000 NEW PROGRAM                    *                
      * BP130998 MCC 15/06/2000 CATER FOR BOOKING FEE FOR SATP *                
      **********************************************************                
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
      *------------------------*                                                
                                                                                
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
      *------------------------*                                                
       INPUT-OUTPUT SECTION.                                                    
      *------------------------*                                                
       FILE-CONTROL.                                                            
      *------------------------*                                                
                                                                                
           SELECT BP13F120 ASSIGN TO  BP13F120.                                 
                                                                                
           SELECT BP13F410 ASSIGN TO BP13F410.                                  
                                                                                
           SELECT BP13K830 ASSIGN TO BP13K830                                   
                            ACCESS       IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K830-KEY-FLD                        
                            ALTERNATE RECORD KEY IS K830-NUM-REGN               
                                         WITH DUPLICATES                        
                            FILE STATUS  IS WS-K830-STATUS.                     
                                                                                
           SELECT BP13L485 ASSIGN TO BP13L485.                                  
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
      *------------------------*                                                
                                                                                
       FD  BP13F120                                                             
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F120.                                                           
                                                                                
       FD  BP13F410                                                             
           RECORDING MODE IS F                                                  
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F410.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD BP13L485                                                              
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  BP13L485-REC                  PIC X(132).                            
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01  WS-SWITCHES-STATS.                                                   
           05  WS-F120-EOF-SW            PIC X(01)    VALUE 'N'.                
           05  WS-F410-EOF-SW            PIC X(01)    VALUE 'N'.                
           05  WS-CHAR-NOT-SPACE         PIC X(01)    VALUE 'N'.                
           05  WS-K830-STATUS            PIC 9(02)    VALUE ZEROES.             
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-CNT-READ-F120          PIC 9(05)    VALUE ZEROES.             
           05  WS-CNT-READ-F410          PIC 9(05)    VALUE ZEROES.             
           05  WS-CNT-MATCH              PIC 9(05)    VALUE ZEROES.             
           05  WS-CNT-F410-ERROR         PIC 9(05)    VALUE ZEROES.             
           05  WS-CNT-NOTFND-K830        PIC 9(05)    VALUE ZEROES.             
           05  WS-CNT-WRIT-L485          PIC 9(05)    VALUE ZEROES.             
           05  WS-L485-PAGE-CTR          PIC 9(04)    VALUE ZEROES.             
           05  WS-L485-LINE-CTR          PIC 9(02)    VALUE 56.                 
           05  WS-L485-SNO-CTR           PIC 9(06)    VALUE ZEROES.             
           05  WS-REP-CTR                PIC 9(03)    VALUE ZEROES.             
           05  WS-CTR                    PIC 9(03)    VALUE ZEROES.             
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F120-AMT-RECEIPT       PIC 9(07)V99 VALUE ZEROES.             
           05  WS-STORE-REGN-PRT         PIC X(08)    VALUE SPACES.             
           05  WS-STORE-REGN-MTCH        PIC X(08)    VALUE SPACES.             
           05  WS-PREV-NUM-REGN          PIC X(08)    VALUE SPACES.             
           05  WS-ADDRESS.                                                      
               10 WS-NUM-BLK             PIC X(16)    VALUE SPACES.             
               10 WS-NUM-FLAT            PIC X(11)    VALUE SPACES.             
               10 WS-NME-STREET          PIC X(32)    VALUE SPACES.             
               10 WS-NUM-POSTAL          PIC X(06)    VALUE SPACES.             
           05  WS-ADD-TABLE.                                                    
               10 WS-ADD-CHAR            OCCURS 65                              
                                         PIC X.                                 
           05  WS-REP-TABLE.                                                    
               10 WS-REP-CHAR            OCCURS 71                              
                                         PIC X.                                 
                                                                                
       01  WS-PRINT-DATA.                                                       
           05  WS-NUM-REGN               PIC X(08)     VALUE SPACES.            
           05  WS-ADDR                   PIC X(65)     VALUE SPACES.            
           05  WS-F410-SELL-PRICE        PIC 9(08)V99  VALUE ZEROES.            
           05  WS-F120-SELL-PRICE        PIC S9(08)V99  VALUE ZEROES.           
                                                                                
       01  WS-RUN-DATE.                                                         
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC         PIC 9(2).                              
                   15  WS-SYS-YY         PIC 9(2).                              
               10  WS-SYS-MM             PIC 9(2).                              
               10  WS-SYS-DD             PIC 9(2).                              
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR CONTROL LISTING                         *            
      *------------------------------------------------------------*            
       01  L485-HDG1.                                                           
           05  FILLER                   PIC X(14) VALUE 'BP13L485'.             
           05  FILLER                   PIC X(42) VALUE SPACES.                 
           05  FILLER                   PIC X(20) VALUE                         
               'SYSTEM OF COMMITMENT'.                                          
           05  FILLER                   PIC X(15) VALUE SPACES.                 
           05  FILLER                   PIC X(07) VALUE 'DATE : '.              
           05  L485-DATE                PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(06) VALUE SPACES.                 
           05  FILLER                   PIC X(6)  VALUE 'PAGE: '.               
           05  L485-PAGE-NO             PIC ZZZ,ZZ9 VALUE ZEROES.               
                                                                                
       01  L485-HDG2.                                                           
           05  FILLER                   PIC X(52) VALUE SPACES.                 
           05  FILLER                   PIC X(27) VALUE                         
              'SALES UNIT (AGREEMENT TEAM)'.                                    
                                                                                
       01  L485-HDG3.                                                           
           05  FILLER                   PIC X(49) VALUE SPACES.                 
           05  FILLER                   PIC X(33) VALUE                         
              'WEEKLY REPORT FOR DEFERRED INCOME'.                              
                                                                                
       01  L485-HDG4.                                                           
           05  FILLER                   PIC X(53).                              
           05  FILLER                   PIC X(26) VALUE                         
              'NOT EQUAL TO SELLING PRICE'.                                     
                                                                                
       01  L485-HDG5.                                                           
           05  FILLER               PIC X(09) VALUE 'SERIAL NO'.                
           05  FILLER               PIC X(04) VALUE SPACES.                     
           05  FILLER               PIC X(08) VALUE 'REGN NO '.                 
           05  FILLER               PIC X(36) VALUE SPACES.                     
           05  FILLER               PIC X(07) VALUE 'ADDRESS'.                  
           05  FILLER               PIC X(36) VALUE SPACES.                     
           05  FILLER               PIC X(15) VALUE 'DEFERRED INCOME'.          
           05  FILLER               PIC X(04) VALUE SPACES.                     
           05  FILLER               PIC X(13) VALUE 'SELLING PRICE'.            
                                                                                
       01  L485-HDG6.                                                           
           05  FILLER               PIC X(106) VALUE SPACES.                    
           05  FILLER               PIC X(03)  VALUE '($)'.                     
           05  FILLER               PIC X(16)  VALUE SPACES.                    
           05  FILLER               PIC X(03)  VALUE '($)'.                     
                                                                                
       01  L485-HDG7.                                                           
           05  FILLER               PIC X(132) VALUE ALL '-'.                   
                                                                                
       01  WS-SPACES-L485           PIC X(132) VALUE SPACES.                    
                                                                                
       01  L485-DETAIL.                                                         
           05  FILLER               PIC X(01) VALUE SPACES.                     
           05  L485-SNO             PIC ZZZ,ZZ9.                                
           05  FILLER               PIC X(05) VALUE SPACES.                     
           05  L485-NUM-REGN        PIC X(08).                                  
           05  FILLER               PIC X(04) VALUE SPACES.                     
           05  L485-ADDR            PIC X(71).                                  
           05  FILLER               PIC X(05) VALUE SPACES.                     
           05  L485-DEF-INCOME      PIC ZZ,ZZZ,ZZ9.99.                          
           05  FILLER               PIC X(05) VALUE SPACES.                     
           05  L485-SELLING-PR      PIC ZZ,ZZZ,ZZ9.99.                          
                                                                                
       01  L485-NO-RECORDS.                                                     
           05  FILLER               PIC X(48) VALUE SPACES.                     
           05  FILLER               PIC X(35) VALUE                             
                 '* * *   N O   R E C O R D S   * * *'.                         
                                                                                
       01  L485-TRAILER.                                                        
           05  FILLER               PIC X(42) VALUE SPACES.                     
           05  FILLER               PIC X(40) VALUE                             
                 '* * *   E N D  O F  R E P O R T   * * *'.                     
                                                                                
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
                                                                                
      ********************                                                      
       000-MAINT-ROUTINE.                                                       
      ********************                                                      
           PERFORM 100-OPEN-FILE             THRU 100-EXIT.                     
           PERFORM 200-READ-F410             THRU 200-EXIT.                     
           PERFORM 210-READ-F120             THRU 210-EXIT.                     
           PERFORM 300-PROCESS-GLTRAN-DETAIL THRU 300-EXIT                      
                   UNTIL WS-F410-EOF-SW = 'Y'.                                  
           PERFORM 999-CLOSE-FILES           THRU 999-EXIT.                     
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************                                                          
       100-OPEN-FILE.                                                           
      ****************                                                          
           OPEN INPUT BP13F410                                                  
                      BP13F120                                                  
                      BP13K830                                                  
               OUTPUT BP13L485.                                                 
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K830 ERROR ' WS-K830-STATUS                  
              MOVE     WS-K830-STATUS TO RETURN-CODE                            
              PERFORM  999-CLOSE-FILES THRU 999-EXIT                            
           END-IF.                                                              
                                                                                
           STRING  FUNCTION CURRENT-DATE(7:2) '/'                               
                   FUNCTION CURRENT-DATE(5:2) '/'                               
                   FUNCTION CURRENT-DATE(1:4)                                   
           DELIMITED BY SIZE INTO L485-DATE.                                    
                                                                                
           PERFORM 600-PRINT-HDG THRU 600-EXIT.                                 
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***************                                                           
       200-READ-F410.                                                           
      ****************                                                          
           READ BP13F410  AT END                                                
                MOVE 'Y' TO WS-F410-EOF-SW                                      
                GO TO 200-EXIT.                                                 
                                                                                
           DISPLAY ' '                                                          
           DISPLAY 'READ F410 ' F410-NUM-REGN.                                  
                                                                                
           ADD  +1                     TO WS-CNT-READ-F410.                     
           MOVE F410-NUM-REGN          TO WS-NUM-REGN                           
           MOVE F410-AMT-SELLING-PRICE TO WS-F410-SELL-PRICE.                   
                                                                                
           PERFORM 220-READ-K830       THRU 220-EXIT.                           
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************                                                          
       210-READ-F120.                                                           
      ****************                                                          
           IF WS-F120-EOF-SW = 'Y'                                              
              DISPLAY 'F120 REACHED EOF'                                        
              MOVE HIGH-VALUES TO F120-NUM-REGN                                 
              GO TO 210-EXIT.                                                   
                                                                                
           READ BP13F120  AT END                                                
                DISPLAY 'F120 REACHED EOF'                                      
                MOVE 'Y' TO WS-F120-EOF-SW                                      
                GO TO 210-EXIT.                                                 
                                                                                
           ADD 1 TO WS-CNT-READ-F120.                                           
           DISPLAY ' '                                                          
           DISPLAY 'READ F120 ' F120-NUM-REGN.                                  
                                                                                
       210-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************                                                          
       220-READ-K830.                                                           
      ****************                                                          
           INITIALIZE BP13K830-REC.                                             
           MOVE F410-NUM-REGN   TO   K830-KEY-FLD.                              
                                                                                
           READ BP13K830.                                                       
                                                                                
           EVALUATE WS-K830-STATUS                                              
           WHEN 00                                                              
                PERFORM 240-GET-ADDR            THRU 240-EXIT                   
           WHEN 23                                                              
                PERFORM 230-READ-K830-ALT-KEY   THRU 230-EXIT                   
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K830 = ' WS-K830-STATUS              
                DISPLAY '      K830-KEY-FLD     = ' K830-KEY-FLD                
                PERFORM 999-CLOSE-FILES         THRU 999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       220-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ************************                                                  
       230-READ-K830-ALT-KEY.                                                   
      ************************                                                  
           INITIALIZE BP13K830-REC.                                             
           MOVE F410-NUM-REGN  TO   K830-NUM-REGN.                              
                                                                                
           READ BP13K830 KEY IS K830-NUM-REGN                                   
           END-READ.                                                            
                                                                                
           EVALUATE WS-K830-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
                PERFORM 240-GET-ADDR   THRU 240-EXIT                            
           WHEN 10                                                              
                DISPLAY 'END OF FILE   BP13K830 = ' WS-K830-STATUS              
                DISPLAY '      K830-NUM-REGN    = ' K830-NUM-REGN               
           WHEN 23                                                              
                MOVE SPACES TO L485-ADDR                                        
                IF F410-NUM-REGN NOT = WS-PREV-NUM-REGN                         
                   DISPLAY 'F410 NOTFND IN K830, REGN IS '                      
                             F410-NUM-REGN                                      
                   ADD 01 TO WS-CNT-NOTFND-K830                                 
                   MOVE F410-NUM-REGN TO WS-PREV-NUM-REGN                       
                END-IF                                                          
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K830 = ' WS-K830-STATUS              
                DISPLAY '      K830-KEY-FLD     = ' K830-KEY-FLD                
                PERFORM 999-CLOSE-FILES         THRU 999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       230-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***************                                                           
       240-GET-ADDR.                                                            
      ***************                                                           
           MOVE    K830-NUM-BLK         TO WS-NUM-BLK.                          
           MOVE    K830-NUM-FLAT        TO WS-NUM-FLAT.                         
           MOVE    K830-NME-STREET      TO WS-NME-STREET.                       
           MOVE    K830-NUM-POSTAL      TO WS-NUM-POSTAL.                       
                                                                                
           MOVE SPACES                  TO WS-ADD-TABLE                         
                                           WS-REP-TABLE                         
                                           WS-ADDR.                             
           MOVE WS-ADDRESS              TO WS-ADD-TABLE.                        
           MOVE ZEROES                  TO WS-REP-CTR.                          
           MOVE 'N'                     TO WS-CHAR-NOT-SPACE.                   
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 65                 
              IF WS-ADD-CHAR(WS-CTR) NOT = ' ' AND LOW-VALUES                   
                 ADD   1  TO WS-REP-CTR                                         
                 IF WS-CHAR-NOT-SPACE = 'N'                                     
                    MOVE SPACES           TO WS-REP-CHAR(WS-REP-CTR)            
                    ADD   1               TO WS-REP-CTR                         
                 END-IF                                                         
                 IF WS-CTR = 60                                                 
                    MOVE 'S'              TO WS-REP-CHAR(WS-REP-CTR)            
                    ADD 1                 TO WS-REP-CTR                         
                    MOVE '('              TO WS-REP-CHAR(WS-REP-CTR)            
                    ADD 1                 TO WS-REP-CTR                         
                 END-IF                                                         
                 MOVE WS-ADD-CHAR(WS-CTR) TO WS-REP-CHAR(WS-REP-CTR)            
                 MOVE 'Y'                 TO WS-CHAR-NOT-SPACE                  
                 IF WS-CTR = 65                                                 
                    ADD 1                 TO WS-REP-CTR                         
                    MOVE ')'              TO WS-REP-CHAR(WS-REP-CTR)            
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'N'                 TO WS-CHAR-NOT-SPACE                  
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           MOVE WS-REP-TABLE          TO WS-ADDR.                               
                                                                                
       240-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************************                                              
       300-PROCESS-GLTRAN-DETAIL.                                               
      ****************************                                              
           DISPLAY '--------------------------------------------'.              
           DISPLAY F410-NUM-REGN '          ' F120-NUM-REGN                     
           DISPLAY WS-F410-SELL-PRICE '        ' WS-F120-SELL-PRICE             
           DISPLAY 'F120-CDE-PAYMENT-TYPE = ' F120-CDE-PAYMENT-TYPE             
           DISPLAY 'F120-CDE-TRANS-TYPE   = ' F120-CDE-TRANS-TYPE               
                                                                                
           IF F410-NUM-REGN = F120-NUM-REGN                                     
              PERFORM 400-PROCESS-CDE-PAYTYPE THRU 400-EXIT                     
              PERFORM 210-READ-F120           THRU 210-EXIT                     
           ELSE                                                                 
              IF F410-NUM-REGN > F120-NUM-REGN                                  
                 PERFORM 210-READ-F120 THRU 210-EXIT                            
              ELSE                                                              
                 IF (WS-CNT-READ-F410 > 0) AND                                  
                    (WS-F410-SELL-PRICE NOT = WS-F120-SELL-PRICE)               
                    PERFORM 500-WRITE-L485 THRU 500-EXIT                        
                 ELSE                                                           
                    INITIALIZE WS-PRINT-DATA                                    
                 END-IF                                                         
                 IF F410-NUM-REGN NOT = WS-STORE-REGN-MTCH                      
                    DISPLAY 'F410 NOTFND IN F120, REGN IS '                     
                             F410-NUM-REGN                                      
                    ADD 01 TO WS-CNT-F410-ERROR                                 
                 END-IF                                                         
                 PERFORM 200-READ-F410 THRU 200-EXIT                            
              END-IF                                                            
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      **************************                                                
       400-PROCESS-CDE-PAYTYPE.                                                 
      **************************                                                
           IF WS-F120-EOF-SW = 'Y'                                              
              GO TO 400-EXIT.                                                   
                                                                                
           IF F120-NUM-REGN NOT = WS-STORE-REGN-MTCH                            
              ADD 1 TO WS-CNT-MATCH.                                            
                                                                                
           MOVE ZEROES                 TO WS-F120-AMT-RECEIPT.                  
           MOVE F120-AMT-RECEIPT       TO WS-F120-AMT-RECEIPT.                  
           DISPLAY 'F120-AMT-RECEIPT      = ' F120-AMT-RECEIPT                  
           MOVE F120-NUM-REGN          TO WS-NUM-REGN                           
                                          WS-STORE-REGN-MTCH.                   
                                                                                
           IF (F410-CDE-ALLOC-RENTAL-FLAT = 'Y') AND                            
              (F120-CDE-PAYMENT-TYPE = 'ADV')    AND                            
              (F120-CDE-TRANS-TYPE = '23')       AND                            
              ((F410-AMT-REFUND-CD      > ZEROES) OR                            
               (F410-AMT-REFUND-BK      > ZEROES) OR                            
               (F410-AMT-REFUND-ADV-COL > ZEROES))                              
              DISPLAY 'F120-AMT-RECEIPT      = ' F120-AMT-RECEIPT               
              DISPLAY 'WS-F120-SELL          = ' WS-F120-SELL-PRICE             
              SUBTRACT WS-F120-AMT-RECEIPT FROM WS-F120-SELL-PRICE              
              GO TO 400-EXIT.                                                   
                                                                                
           IF F120-CDE-PAYMENT-TYPE = 'SOC'                                     
              IF F120-CDE-TRANS-TYPE = '22'                                     
                 ADD WS-F120-AMT-RECEIPT TO WS-F120-SELL-PRICE                  
              END-IF                                                            
           ELSE                                                                 
           IF F120-CDE-PAYMENT-TYPE = 'BK '                                     
              IF F120-CDE-TRANS-TYPE = '19'                                     
                 ADD WS-F120-AMT-RECEIPT TO WS-F120-SELL-PRICE                  
              END-IF                                                            
           ELSE                                                                 
           IF F120-CDE-PAYMENT-TYPE = 'CD '                                     
              IF F120-CDE-TRANS-TYPE = '29'                                     
                 ADD WS-F120-AMT-RECEIPT TO WS-F120-SELL-PRICE                  
              END-IF                                                            
           ELSE                                                                 
           IF F120-CDE-PAYMENT-TYPE = 'LSE' OR 'CON' OR 'CST' OR                
                                      'SRV' OR 'SST' OR 'LRF' OR                
                                      'PRM'                                     
              SUBTRACT WS-F120-AMT-RECEIPT FROM WS-F120-SELL-PRICE              
           ELSE                                                                 
           IF F120-CDE-PAYMENT-TYPE = 'ADV' OR 'LVL'                            
              ADD WS-F120-AMT-RECEIPT TO WS-F120-SELL-PRICE.                    
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *****************                                                         
       500-WRITE-L485.                                                          
      *****************                                                         
           MOVE SPACES                TO BP13L485-REC.                          
           INITIALIZE                    BP13L485-REC.                          
                                                                                
           ADD  1                     TO WS-L485-SNO-CTR.                       
           MOVE WS-L485-SNO-CTR       TO L485-SNO.                              
                                                                                
           MOVE WS-NUM-REGN           TO L485-NUM-REGN                          
                                         WS-STORE-REGN-PRT.                     
           MOVE WS-ADDR               TO L485-ADDR.                             
           MOVE WS-F120-SELL-PRICE    TO L485-DEF-INCOME.                       
           MOVE WS-F410-SELL-PRICE    TO L485-SELLING-PR.                       
                                                                                
           IF WS-L485-LINE-CTR > 55                                             
              PERFORM 600-PRINT-HDG  THRU 600-EXIT                              
           END-IF.                                                              
                                                                                
           WRITE BP13L485-REC FROM L485-DETAIL AFTER 1.                         
           ADD  1  TO WS-L485-LINE-CTR.                                         
                                                                                
           ADD +1  TO WS-CNT-WRIT-L485.                                         
           INITIALIZE WS-PRINT-DATA.                                            
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************                                                          
       600-PRINT-HDG.                                                           
      ****************                                                          
           ADD 1                   TO WS-L485-PAGE-CTR.                         
           MOVE WS-L485-PAGE-CTR   TO L485-PAGE-NO.                             
           WRITE BP13L485-REC FROM L485-HDG1      AFTER PAGE.                   
           WRITE BP13L485-REC FROM L485-HDG2      AFTER 1.                      
           WRITE BP13L485-REC FROM WS-SPACES-L485 AFTER 1.                      
           WRITE BP13L485-REC FROM L485-HDG3      AFTER 1.                      
           WRITE BP13L485-REC FROM L485-HDG4      AFTER 1.                      
           WRITE BP13L485-REC FROM WS-SPACES-L485 AFTER 1.                      
           WRITE BP13L485-REC FROM L485-HDG5      AFTER 1.                      
           WRITE BP13L485-REC FROM L485-HDG6      AFTER 1.                      
           WRITE BP13L485-REC FROM L485-HDG7      AFTER 1.                      
           WRITE BP13L485-REC FROM WS-SPACES-L485 AFTER 1.                      
           MOVE  10                TO WS-L485-LINE-CTR.                         
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************                                                        
       999-CLOSE-FILES.                                                         
      ******************+                                                       
           IF WS-L485-LINE-CTR > 55                                             
              PERFORM 600-PRINT-HDG  THRU 600-EXIT                              
           END-IF.                                                              
                                                                                
           IF (WS-STORE-REGN-MTCH NOT = WS-STORE-REGN-PRT) AND                  
              (WS-F410-SELL-PRICE NOT = WS-F120-SELL-PRICE)                     
               PERFORM 500-WRITE-L485 THRU 500-EXIT                             
           END-IF.                                                              
                                                                                
           IF WS-CNT-WRIT-L485 = ZEROES                                         
              WRITE BP13L485-REC FROM L485-NO-RECORDS AFTER 3                   
           ELSE                                                                 
              WRITE BP13L485-REC FROM L485-TRAILER    AFTER 3                   
           END-IF.                                                              
                                                                                
           CLOSE BP13F120                                                       
                 BP13F410                                                       
                 BP13K830                                                       
                 BP13L485.                                                      
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K830 ERROR ' WS-K830-STATUS                  
              MOVE     WS-K830-STATUS TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : '  L485-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13C485 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY                                                              
            'BP13F410 RECS READ               = ' WS-CNT-READ-F410.             
           DISPLAY SPACES.                                                      
           DISPLAY                                                              
            'BP13F120 RECS READ               = ' WS-CNT-READ-F120.             
           DISPLAY SPACES.                                                      
           DISPLAY                                                              
            'BP13K830 RECS NOTFND             = ' WS-CNT-NOTFND-K830            
           DISPLAY SPACES.                                                      
           DISPLAY                                                              
            'MTCHED BP13F410 & BP13F120 REGN  = ' WS-CNT-MATCH.                 
           DISPLAY SPACES.                                                      
           DISPLAY                                                              
            'BP13F410 RECS NOTFND IN BP13F120 = ' WS-CNT-F410-ERROR.            
           DISPLAY SPACES.                                                      
           DISPLAY                                                              
            'BP13L485 RECORDS WRITTEN         = ' WS-CNT-WRIT-L485.             
           DISPLAY '(DEFERED INCOME NOT = SP)'                                  
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
