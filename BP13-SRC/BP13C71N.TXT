       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C71N.                                                  
      *AUTHOR.       YCH2.                                                      
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *  OBJECTIVE:                                                *            
      *  UPDATE RESALE LEVY WHEN FOUND IN RESALE HISTORY           *            
      *                                                            *            
      *  I    :  BP13F520                                          *            
      *  I/O  :  BP13K800                                          *            
      *------------------------------------------------------------*            
      *  REQ-NO  DATE     OIC  DESCRIPTION                         *            
      *  ======  ====     ===  ==============                      *            
      * BP133527 13042009 YCH2 BYPASS STUDIO APARTMENT             *            
      * BP134838 12032013 YCH2)INCLUDE NEW LEVY CODE               *            
      * BP134843 12032013 YCH2)                                    *            
      * BP135168 13122013 YCH2 INCLUDE SELLER1 CHECK IF BOTH LN    *            
      * BP136019 15102015 LSB1 ADD NEW CODE FOR LEVY               *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
                                                                                
           SELECT BP13K825  ASSIGN TO BP13K825                                  
                  ORGANIZATION     IS INDEXED                                   
                  ACCESS MODE      IS DYNAMIC                                   
                  RECORD KEY       IS K825-KEY-FLD                              
                  FILE STATUS      IS WS-BP13K825-STATUS.                       
                                                                                
           SELECT BB24K510  ASSIGN TO BB24K510                                  
                  ORGANIZATION     IS INDEXED                                   
                  ACCESS MODE      IS DYNAMIC                                   
                  RECORD KEY       IS K510-KEY-FLD                              
                  FILE STATUS      IS WS-BB24K510-STATUS.                       
                                                                                
           SELECT BB24K500  ASSIGN TO BB24K500                                  
                  ORGANIZATION     IS INDEXED                                   
                  ACCESS MODE      IS RANDOM                                    
                  RECORD KEY       IS K500-KEY-FLD                              
                  FILE STATUS      IS WS-BB24K500-STATUS.                       
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-BP13K800-STATUS.                        
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F520   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 500 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD                                
                      RECORDING MODE IS F.                                      
       COPY BP13F520.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
                                                                                
       COPY BP13K825.                                                           
                                                                                
       FD   BB24K510                                                            
            RECORD CONTAINS 50 CHARACTERS.                                      
                                                                                
       COPY BB24K510.                                                           
                                                                                
       FD   BB24K500                                                            
            RECORD CONTAINS 1700 CHARACTERS.                                    
                                                                                
       COPY BB24K500.                                                           
                                                                                
       FD  BP13K800                                                             
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-LVY-RSL-STATUS             PIC X(3)  VALUE SPACES.                
       01  WS-RESALE-STATUS              PIC X(3)  VALUE SPACES.                
       01  WS-NUM-UIN                    PIC X(9)  VALUE SPACES.                
       01  WS-SWITCHES.                                                         
           05  WS-BP13K800-STATUS        PIC 9(2)  VALUE ZEROES.                
           05  WS-BP13K825-STATUS        PIC 9(2)  VALUE ZEROES.                
           05  WS-BB24K500-STATUS        PIC 9(2)  VALUE ZEROES.                
           05  WS-BB24K510-STATUS        PIC 9(2)  VALUE ZEROES.                
                                                                                
           05  WS-F520-EOF               PIC X     VALUE 'N'.                   
           05  WS-BP13K825-EOF           PIC X     VALUE 'N'.                   
           05  WS-BB24K510-EOF           PIC X     VALUE 'N'.                   
           05  WS-LVY-TAG                PIC X     VALUE 'N'.                   
                                                                                
           05  WS-F520-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-F520-BYPASS            PIC 9(6)  VALUE ZEROS.                 
           05  WS-K500-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-K510-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-K825-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-K800-READ              PIC 9(6)  VALUE ZEROS.                 
           05  WS-K800-REWRITE           PIC 9(6)  VALUE ZEROS.                 
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
            PERFORM 100-OPEN-FILES      THRU 100-EXIT.                          
            PERFORM 200-READ-BP13F520   THRU 200-EXIT.                          
            PERFORM 300-PROCESS-F520    THRU 300-EXIT                           
                             UNTIL WS-F520-EOF = 'Y'.                           
            PERFORM 999-CLOSE-FILES     THRU 999-EXIT.                          
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F520                                                 
                       BB24K510                                                 
                       BB24K500                                                 
                       BP13K825                                                 
                I-O    BP13K800.                                                
                                                                                
           IF WS-BP13K800-STATUS NOT = 00 AND 97                                
              DISPLAY 'OPENING ERROR, K800-STATUS ' WS-BP13K800-STATUS          
              MOVE WS-BP13K800-STATUS   TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-BP13K825-STATUS NOT = 00 AND 97                                
              DISPLAY 'OPENING ERROR, K825-STATUS ' WS-BP13K825-STATUS          
              MOVE WS-BP13K825-STATUS   TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-BB24K510-STATUS NOT = ZEROS AND 97                             
              DISPLAY 'FILE BB24K510 OPEN ERROR'                                
              DISPLAY 'ERROR STATUS = ' WS-BB24K510-STATUS                      
              MOVE WS-BB24K510-STATUS TO RETURN-CODE                            
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-BB24K500-STATUS NOT = ZEROS AND 97                             
              DISPLAY 'FILE BB24K500 OPEN ERROR'                                
              DISPLAY 'ERROR STATUS = ' WS-BB24K500-STATUS                      
              MOVE WS-BB24K500-STATUS TO RETURN-CODE                            
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-BP13F520.                                                       
      *-------------------------------------------------------------            
           READ BP13F520 AT END                                                 
           MOVE 'Y'                      TO WS-F520-EOF                         
           GO TO 200-EXIT.                                                      
                                                                                
           IF (F520-CDE-IDP-SCH = 'SA ') OR                                     
              (F520-CDE-FLAT-TYPE = '1A' OR '2A')                               
               ADD 1 TO WS-F520-BYPASS                                          
               GO TO 200-READ-BP13F520                                          
           ELSE                                                                 
               ADD 1   TO WS-F520-READ                                          
           END-IF.                                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-F520.                                                        
      *-------------------------------------------------------------            
           MOVE 'N'            TO WS-LVY-TAG.                                   
           MOVE 'N'            TO WS-BB24K510-EOF.                              
           MOVE 'N'            TO WS-BP13K825-EOF.                              
           MOVE SPACES         TO WS-LVY-RSL-STATUS.                            
           MOVE SPACES         TO WS-NUM-UIN.                                   
           MOVE SPACES         TO K825-KEY-FLD.                                 
           MOVE F520-NUM-REGN  TO K825-NUM-REGN.                                
                                                                                
           PERFORM 400-START-BP13K825 THRU 400-EXIT.                            
                                                                                
           IF WS-LVY-TAG = 'Y'                                                  
              PERFORM 500-UPDATE-BP13K800 THRU 500-EXIT                         
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-BP13F520   THRU 200-EXIT.                           
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-START-BP13K825.                                                      
      *-------------------------------------------------------------            
           START BP13K825 KEY >=  K825-KEY-FLD.                                 
           EVALUATE WS-BP13K825-STATUS                                          
              WHEN  00                                                          
                    PERFORM 405-READ-BP13K825 THRU 405-EXIT                     
                      UNTIL K825-NUM-REGN NOT = F520-NUM-REGN                   
                        OR  WS-BP13K825-EOF = 'Y'                               
                        OR  WS-LVY-TAG = 'Y'                                    
              WHEN  23                                                          
                    CONTINUE                                                    
              WHEN OTHER                                                        
                   DISPLAY 'FILE BP13K825 START ERR ' K825-NUM-REGN             
                   DISPLAY 'ERROR STATUS = ' WS-BP13K825-STATUS                 
                   MOVE WS-BP13K825-STATUS TO RETURN-CODE                       
                   PERFORM 999-CLOSE-FILES THRU 999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       405-READ-BP13K825.                                                       
      *-------------------------------------------------------------            
           READ BP13K825 NEXT AT END                                            
                         MOVE 'Y' TO WS-BP13K825-EOF                            
                         GO TO 405-EXIT.                                        
                                                                                
           ADD 1 TO WS-K825-READ.                                               
                                                                                
           IF  (K825-NUM-REGN = F520-NUM-REGN)                                  
                MOVE SPACES        TO K510-KEY-FLD                              
                MOVE K825-NUM-NRIC TO K510-NUM-UIN                              
                PERFORM 410-START-BB24K510 THRU 410-EXIT                        
           END-IF.                                                              
                                                                                
       405-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       410-START-BB24K510.                                                      
      *-------------------------------------------------------------            
           START BB24K510 KEY >=  K510-KEY-FLD.                                 
           EVALUATE WS-BB24K510-STATUS                                          
              WHEN  00                                                          
                    PERFORM 420-READ-BB24K510 THRU 420-EXIT                     
                      UNTIL K510-NUM-UIN NOT = K825-NUM-NRIC                    
                        OR  WS-BB24K510-EOF = 'Y'                               
                        OR  WS-LVY-TAG = 'Y'                                    
              WHEN  23                                                          
                    CONTINUE                                                    
              WHEN OTHER                                                        
                   DISPLAY 'FILE BB24K510 START ERR ' K510-NUM-UIN              
                   DISPLAY 'ERROR STATUS = ' WS-BB24K510-STATUS                 
                   MOVE WS-BB24K510-STATUS TO RETURN-CODE                       
                   PERFORM 999-CLOSE-FILES THRU 999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
       420-READ-BB24K510.                                                       
      *-------------------------------------------------------------            
           READ BB24K510 NEXT AT END                                            
                         MOVE 'Y' TO WS-BB24K510-EOF                            
                         GO TO 420-EXIT.                                        
                                                                                
           ADD 1 TO WS-K510-READ.                                               
                                                                                
           IF  (K825-NUM-NRIC = K510-NUM-UIN) AND                               
               (K510-CDE-STATUS = 'P' OR                                        
                K510-CDE-STATUS = 'S')                                          
                PERFORM 421-READ-BB24K500 THRU 421-EXIT                         
           END-IF.                                                              
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
       421-READ-BB24K500.                                                       
      *-------------------------------------------------------------            
           MOVE K510-NUM-HDB-REF   TO K500-NUM-HDB-REF.                         
           MOVE K510-DTE-APPROVAL  TO K500-DTE-APPROVAL.                        
           MOVE K510-NUM-UIN       TO WS-NUM-UIN.                               
                                                                                
           READ BB24K500.                                                       
                                                                                
           EVALUATE WS-BB24K500-STATUS                                          
             WHEN 00                                                            
                  ADD 1 TO WS-K500-READ                                         
                  IF (K500-CDE-RESALE-STATUS2 = LOW-VALUES OR SPACES)           
                     MOVE K500-CDE-RESALE-STATUS TO WS-RESALE-STATUS            
                     PERFORM 422-MORE-LEVY-CODE-CHECK THRU 422-EXIT             
                  ELSE                                                          
                    IF (WS-NUM-UIN = K500-NUM-UIN-SELLER2) OR                   
                       (WS-NUM-UIN = K500-NUM-UIN-SPOUSE2)                      
                       MOVE K500-CDE-RESALE-STATUS2 TO WS-RESALE-STATUS         
                       PERFORM 422-MORE-LEVY-CODE-CHECK THRU 422-EXIT           
                    ELSE                                                        
                      IF (WS-NUM-UIN = K500-NUM-UIN-SELLER1)                    
                        MOVE K500-CDE-RESALE-STATUS TO WS-RESALE-STATUS         
                        PERFORM 422-MORE-LEVY-CODE-CHECK THRU 422-EXIT          
                      END-IF                                                    
                    END-IF                                                      
                  END-IF                                                        
             WHEN 23                                                            
                  CONTINUE                                                      
             WHEN OTHER                                                         
                   DISPLAY 'FILE BB24K500 READ ERR ' K500-KEY-FLD               
                   DISPLAY 'ERROR STATUS = ' WS-BB24K500-STATUS                 
                   MOVE WS-BB24K500-STATUS TO RETURN-CODE                       
                   PERFORM 999-CLOSE-FILES THRU 999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       421-EXIT.                                                                
           EXIT.                                                                
                                                                                
       422-MORE-LEVY-CODE-CHECK.                                                
                                                                                
           IF WS-RESALE-STATUS = 'LN ' OR 'LNS' OR                              
                                 'FN ' OR 'FNH' OR                              
                                 'LNH' OR 'PN ' OR                              
                                 'REN' OR 'VLN'                                 
              MOVE WS-RESALE-STATUS TO WS-LVY-RSL-STATUS                        
              MOVE 'Y' TO WS-LVY-TAG                                            
           END-IF.                                                              
                                                                                
           IF WS-LVY-RSL-STATUS = SPACES OR LOW-VALUES                          
              IF WS-RESALE-STATUS = 'LD ' OR 'SA ' OR                           
                                    'GS ' OR 'GP ' OR                           
                                    'PG ' OR 'CH ' OR                           
                                    'FG ' OR 'HNQ' OR                           
                                    'BQN' OR 'BHN' OR                           
                                    'TOM' OR 'TOT' OR                           
                                    'QNH' OR 'FNG' OR                           
                                    'PNG'                                       
                 MOVE WS-RESALE-STATUS                                          
                             TO WS-LVY-RSL-STATUS                               
                 MOVE 'Y' TO WS-LVY-TAG                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       422-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-UPDATE-BP13K800.                                                     
      *-------------------------------------------------------------            
           MOVE F520-NUM-REGN TO K800-NUM-REGN.                                 
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-BP13K800-STATUS                                          
           WHEN  00                                                             
                 ADD 1 TO  WS-K800-READ                                         
                 PERFORM 510-REWRITE-BP13K800 THRU 510-EXIT                     
           WHEN  23                                                             
                 DISPLAY 'RECORD NOT FND IN BP13K800 '                          
                          WS-BP13K800-STATUS ' ' F520-NUM-REGN                  
           WHEN  OTHER                                                          
                 DISPLAY 'ERROR READ BP13K800,STATUS '                          
                                     WS-BP13K800-STATUS                         
                 MOVE WS-BP13K800-STATUS TO RETURN-CODE                         
                 GO TO 999-CLOSE-FILES                                          
           END-EVALUATE.                                                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       510-REWRITE-BP13K800.                                                    
      *-------------------------------------------------------------            
            MOVE WS-LVY-RSL-STATUS TO K800-NUM-LVY-RSL-STATUS                   
                                                                                
            REWRITE BP13K800-MASTER                                             
                                                                                
            IF WS-BP13K800-STATUS = 00                                          
               ADD 1                 TO WS-K800-REWRITE                         
            ELSE                                                                
               DISPLAY 'ERROR REWRITING BP13K800 STATUS '                       
                                               WS-BP13K800-STATUS               
               MOVE WS-BP13K800-STATUS  TO RETURN-CODE                          
               PERFORM 999-CLOSE-FILES  THRU 999-EXIT                           
            END-IF.                                                             
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
       999-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY '* ------------ BP13C71N ------------- * '.                  
           DISPLAY '  NO OF REC READ    BP13F520 = ' WS-F520-READ.              
           DISPLAY '  NO OF REC BYPASS  BP13F520 = ' WS-F520-BYPASS.            
           DISPLAY '  NO OF REC READ    BP13K825 = ' WS-K825-READ.              
           DISPLAY '  NO OF REC READ    BP24K510 = ' WS-K510-READ.              
           DISPLAY '  NO OF REC READ    BP24K500 = ' WS-K500-READ.              
           DISPLAY '  NO OF REC READ    BP13K800 = ' WS-K800-READ.              
           DISPLAY '  NO OF REC REWRITE BP13K800 = ' WS-K800-REWRITE.           
                                                                                
           CLOSE BP13F520                                                       
                 BP13K825                                                       
                 BB24K500                                                       
                 BB24K510                                                       
                 BP13K800.                                                      
                                                                                
           IF WS-BP13K800-STATUS NOT = 00 AND 97                                
              DISPLAY 'ERROR CLOSING BP13K800 ' WS-BP13K800-STATUS      US      
              MOVE WS-BP13K800-STATUS    TO RETURN-CODE.                        
                                                                                
           IF WS-BP13K825-STATUS NOT = 00 AND 97                                
              DISPLAY 'ERROR CLOSING BP13K825 ' WS-BP13K825-STATUS      US      
              MOVE WS-BP13K825-STATUS    TO RETURN-CODE.                        
                                                                                
           IF WS-BB24K510-STATUS NOT = ZEROS AND 97                             
              DISPLAY 'ERROR CLOSING BB24K510 ' WS-BB24K510-STATUS              
              MOVE WS-BB24K510-STATUS TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           IF WS-BB24K500-STATUS NOT = ZEROS AND 97                             
              DISPLAY 'ERROR CLOSEING BB24K500 ' WS-BB24K500-STATUS             
              MOVE WS-BB24K500-STATUS TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
