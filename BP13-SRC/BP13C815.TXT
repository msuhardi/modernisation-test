       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C815.                                                 
      *AUTHOR.        LSB.                                                      
      *DATE-WRITTEN.  20/07/94.                                                 
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *                                                             *           
      *  OBJECTIVE   : TO OUTPUT CASES AS NEW HOUSEHOLD ONLY        *           
      *                IF DETERMINED HOUSEHOLD ARE BOTH 'H'         *           
      *                IE UNDER ONE REGNO APPEAR MORE THAN ONE      *           
      *                HDB-REF NO, IF ONE IS 'G' THEN SHOULD        *           
      *                NOT OUTPUT                                   *           
      *                                                             *           
      *  INPUT FILE   : BP13F558                                    *           
      *  OUTPUT FILE  : P13F558A                                    *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG-NO   BY   DATE      DETAILS                             *           
      * -------- ---  --------  -------                             *           
      * BP130833 YCH2 20001006  ENHANCEMENT ON PARENT CHILD         *           
      *                         (SEE NOTE IN 710 PARA)              *           
      *                                                             *           
      * =========================================================== *           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
           SELECT P13F558A ASSIGN TO P13F558A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD  BP13F558                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F558.                                                           
                                                                                
       FD  P13F558A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F558A-REC                   PIC X(600).                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-COUNTERS.                                                         
           05 WS-F558-READ                PIC 9(8)    VALUE ZEROS.              
           05 WS-OUTPUT                   PIC 9(8)    VALUE ZEROS.              
                                                                                
       01  WS-PREV-REGN                   PIC X(8)    VALUE SPACES.             
       01  WS-EOF-F558                    PIC X       VALUE 'N'.                
       01  WS-OUTPUT-TAG                  PIC X       VALUE 'Y'.                
                                                                                
       01  WS-BP13F558-REC                PIC X(600)  VALUE SPACES.             
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F558      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL WS-EOF-F558 = 'Y'.                                           
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F558                                                 
                OUTPUT P13F558A.                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-F558.                                                           
      *-------------------------------------------------------------            
           READ BP13F558 AT END                                                 
                MOVE 'Y'        TO WS-EOF-F558                                  
                GO TO 200-EXIT.                                                 
                                                                                
           ADD  1 TO WS-F558-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-RTN.                                                         
      *-------------------------------------------------------------            
           MOVE 'Y'           TO WS-OUTPUT-TAG.                                 
           MOVE F558-NUM-REGN TO WS-PREV-REGN.                                  
                                                                                
           PERFORM 310-CHECK-REGN THRU 310-EXIT                                 
             UNTIL F558-NUM-REGN NOT = WS-PREV-REGN                             
               OR  WS-EOF-F558 = 'Y'.                                           
                                                                                
           IF WS-OUTPUT-TAG = 'Y'                                               
              PERFORM 400-WRITE-OUTPUT THRU 400-EXIT                            
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       310-CHECK-REGN.                                                          
      *-------------------------------------------------------------            
           IF F558-CDE-HOUSEHOLD = 'H'                                          
              MOVE BP13F558-REC TO WS-BP13F558-REC                              
           ELSE                                                                 
               MOVE 'N' TO WS-OUTPUT-TAG                                        
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F558 THRU 200-EXIT.                                 
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-WRITE-OUTPUT.                                                        
                                                                                
           WRITE P13F558A-REC FROM WS-BP13F558-REC                              
           ADD 1 TO WS-OUTPUT.                                                  
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       999-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           CLOSE BP13F558                                                       
                 P13F558A.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C815 ================*'.              
           DISPLAY '1.  NO. OF F558 RECORDS READ     = ' WS-F558-READ.          
           DISPLAY '2.  NO. OF F558 RECORDS OUTPUT   = ' WS-OUTPUT.             
           DISPLAY ' '.                                                         
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
