       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13Q747.                                                    
       AUTHOR.     SURESH.                                                      
       INSTALLATION. HDB.                                                       
       DATE-WRITTEN. 26-01-1999.                                                
                                                                                
      *-----------------------------------------------------------*             
      *        SOC  - BP13                                        *             
      *-----------------------------------------------------------*             
      * OBJECTIVES :  TO EXTRACT MOBILITY RECORD BASED ON         *             
      *               IDENTITY NO AND IDENTITY NO TYPE            *             
      *                                                           *             
      * INPUT FILES   : BP13F716                                  *             
      * DATABASE      : RDB2                                      *             
      *                                                           *             
      * OUTPUT FILE   : P13F716A                                  *             
      *                                                           *             
      * REQ NO.   DATE       CHG BY  DESCRIPTION                  *             
      * -------   ----       ------  -----------                  *             
      * BP130630 26-01-1999   SRN    NEW PROGRAM                  *             
      *                                                           *             
      *-----------------------------------------------------------*             
       ENVIRONMENT DIVISION.                                                    
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F716   ASSIGN TO BP13F716.                                
                                                                                
           SELECT P13F716A   ASSIGN TO P13F716A.                                
                                                                                
           SELECT BP13K060   ASSIGN TO BP13K060                                 
                             ORGANIZATION    INDEXED                            
                             ACCESS MODE     RANDOM                             
                             RECORD KEY      K060-KEY-FLD                       
                             FILE STATUS     WS-K060-STATUS.                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD BP13F716.                                                             
       COPY BP13F716.                                                           
                                                                                
       FD P13F716A.                                                             
       01 P13F716A-REC                   PIC X(350).                            
                                                                                
       FD BP13K060.                                                             
       COPY BP13K060.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
           EXEC SQL INCLUDE P13CSROL  END-EXEC.                                 
                                                                                
           EXEC SQL INCLUDE P13CXROL  END-EXEC.                                 
                                                                                
           EXEC SQL INCLUDE P13IDNO   END-EXEC.                                 
                                                                                
           EXEC SQL INCLUDE P13RSDNT  END-EXEC.                                 
                                                                                
           EXEC SQL INCLUDE P13CSID   END-EXEC.                                 
                                                                                
           EXEC SQL INCLUDE P13PERSN  END-EXEC.                                 
                                                                                
           EXEC SQL INCLUDE SQLCA     END-EXEC.                                 
                                                                                
                                                                                
           EXEC SQL                                                             
             DECLARE CURSOR1 CURSOR FOR                                         
               SELECT T2.NUM_ROLE,                                              
                      T2.NUM_BSNS_REF,                                          
                      T2.DTE_ACPTNC,                                            
                      T2.DTE_ROLE_START,                                        
                      T3.NUM_CSTMR_SOURCE,                                      
                      T4.NUM_SEX,                                               
                      DATE('0001-01-01')                                        
               FROM                                                             
                 IDNTY_NO       T1,                                             
                 CSTMR_ROLE     T2,                                             
                 CSTMR_ID       T3,                                             
                 PERSONS        T4                                              
               WHERE                                                            
                 T1.NUM_CSTMR         = T2.NUM_CSTMR                 AND        
                 T1.NUM_CSTMR         = T3.NUM_CSTMR                 AND        
                 T1.NUM_CSTMR         = T4.NUM_CSTMR                 AND        
                 T2.NUM_ROLE IN ('001','002','003','004','005','006',           
                                 '011','012','013','015','016','017',           
                                 '018','019','020','021','022','023',           
                                 '024','025','026','027','028','029',           
                                 '030','031','032','033','038')      AND        
                 T1.NUM_IDNTY_NO      = :DCLIDNTY-NO.NUM-IDNTY-NO    AND        
                 T1.NUM_IDNTY_NO_TYPE = :DCLIDNTY-NO.NUM-IDNTY-NO-TYPE          
               UNION                                                            
               SELECT T2.NUM_ROLE,                                              
                      T2.NUM_BSNS_REF,                                          
                      T2.DTE_ACPTNC,                                            
                      T2.DTE_ROLE_START,                                        
                      T3.NUM_CSTMR_SOURCE,                                      
                      'X',                                                      
                      T2.DTE_ROLE_END                                           
               FROM                                                             
                 IDNTY_NO       T1,                                             
                 CSTMR_EX_ROLE  T2,                                             
                 CSTMR_ID       T3                                              
               WHERE                                                            
                 T1.NUM_CSTMR         = T2.NUM_CSTMR                 AND        
                 T1.NUM_CSTMR         = T3.NUM_CSTMR                 AND        
                 T2.NUM_ROLE IN ('001','002','003','004','005','006',           
                                 '011','012','013','015','016','017',           
                                 '018','019','020','021','022','023',           
                                 '024','025','026','027','028','029',           
                                 '030','031','032','033','038')      AND        
                 T1.NUM_IDNTY_NO      = :DCLIDNTY-NO.NUM-IDNTY-NO    AND        
                 T1.NUM_IDNTY_NO_TYPE = :DCLIDNTY-NO.NUM-IDNTY-NO-TYPE          
                                                                                
                 ORDER BY 6                                                     
           END-EXEC                                                             
                                                                                
      *--------------------------------------------------------------*          
      * COMAREA FOR CALLING AV02C013 FOR FINDING NRIC NO TYPE        *          
      *--------------------------------------------------------------*          
       01  WS-COM-AREA.                                                         
           05  WS-COM-NRIC             PIC X(9)    VALUE SPACES.                
           05  WS-COM-RFLAG            PIC X(1)    VALUE SPACES.                
           05  WS-COM-NUMBER-TYPE      PIC X(3)    VALUE SPACES.                
                                                                                
       01  WS-CONTROL-COUNT.                                                    
           05  WS-F716-READ            PIC 9(8)    VALUE ZEROS.                 
           05  WS-INVALID-UINFIN       PIC 9(8)    VALUE ZEROS.                 
           05  WS-RDB2-READ            PIC 9(8)    VALUE ZEROS.                 
           05  WS-RDB2-NFND            PIC 9(8)    VALUE ZEROS.                 
           05  WS-RSDNT-READ           PIC 9(8)    VALUE ZEROS.                 
           05  WS-RSDNT-NFND           PIC 9(8)    VALUE ZEROS.                 
           05  WS-K060-READ            PIC 9(8)    VALUE ZEROS.                 
           05  WS-K060-NFND            PIC 9(8)    VALUE ZEROS.                 
           05  WS-ACTV-RECS            PIC 9(8)    VALUE ZEROS.                 
           05  WS-HIST-RECS            PIC 9(8)    VALUE ZEROS.                 
           05  WS-F716-WRITE           PIC 9(8)    VALUE ZEROS.                 
                                                                                
       01  WS-EOF-F716                 PIC X       VALUE 'N'.                   
           88 EOF-F716                             VALUE 'Y'.                   
                                                                                
       01  WS-K060-STATUS              PIC 9(2)    VALUE ZEROS.                 
                                                                                
       01  WS-SQLCODE                  PIC 9(4)    VALUE ZEROS.                 
                                                                                
       01  WS-EOC                      PIC X(1)    VALUE 'N'.                   
           88 END-OF-CURSOR                        VALUE 'Y'.                   
                                                                                
       01  WS-SYS-DATE                 PIC X(8)    VALUE SPACES.                
       01  WS-PRT-DATE                 PIC X(10)   VALUE SPACES.                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN.                                                               
                                                                                
            EXEC SQL WHENEVER SQLERROR                                          
                              GO TO 9900-ABORT-PROCESS                          
            END-EXEC.                                                           
                                                                                
            EXEC SQL WHENEVER SQLWARNING                                        
                              GO TO 9900-ABORT-PROCESS                          
            END-EXEC.                                                           
                                                                                
            PERFORM 0500-OPEN-FILES        THRU 0500-EXIT.                      
                                                                                
            PERFORM 1000-READ-BP13F716     THRU 1000-EXIT.                      
                                                                                
            PERFORM 2000-RETRIEVE-MOBILITY THRU 2000-EXIT                       
                    UNTIL EOF-F716.                                             
                                                                                
            PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                      
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       0500-OPEN-FILES.                                                         
            OPEN INPUT  BP13F716                                                
                        BP13K060                                                
                 OUTPUT P13F716A.                                               
                                                                                
            MOVE FUNCTION CURRENT-DATE(1:8)   TO WS-SYS-DATE.                   
                                                                                
            IF WS-K060-STATUS NOT = 0 AND 97                                    
               DISPLAY 'ERROR IN OPENING BP13K060'                              
               DISPLAY 'FILE-STATUS :- ' WS-K060-STATUS                         
               MOVE WS-K060-STATUS       TO RETURN-CODE                         
               GO TO 9000-CLOSE-FILES                                           
            END-IF.                                                             
       0500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-READ-BP13F716.                                                      
            READ BP13F716 AT END MOVE 'Y'     TO WS-EOF-F716                    
                 GO TO 1000-EXIT.                                               
                                                                                
            ADD 1 TO WS-F716-READ.                                              
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2000-RETRIEVE-MOBILITY.                                                  
                                                                                
            MOVE SPACES                  TO WS-COM-AREA.                        
            MOVE F716-NUM-NRIC           TO WS-COM-NRIC                         
                                                                                
            IF WS-COM-NRIC(1:1) = 'X'                                           
               MOVE 'XIN'                TO WS-COM-NUMBER-TYPE                  
               MOVE 'Y'                  TO WS-COM-RFLAG                        
            ELSE                                                                
               CALL 'AV02C013'           USING WS-COM-NRIC,                     
                                               WS-COM-RFLAG,                    
                                               WS-COM-NUMBER-TYPE               
            END-IF.                                                             
                                                                                
                                                                                
            IF WS-COM-RFLAG = 'Y'                                               
               PERFORM 3000-READ-RDB        THRU 3000-EXIT                      
            ELSE                                                                
               ADD 1                        TO WS-INVALID-UINFIN                
            END-IF.                                                             
                                                                                
            PERFORM 1000-READ-BP13F716   THRU 1000-EXIT.                        
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3000-READ-RDB.                                                           
            MOVE WS-COM-NUMBER-TYPE      TO NUM-IDNTY-NO-TYPE                   
                                                                                
            IF WS-COM-NUMBER-TYPE = 'XIN'                                       
               MOVE F716-NUM-NRIC(2:8)      TO                                  
                                       NUM-IDNTY-NO OF DCLIDNTY-NO              
            ELSE                                                                
               MOVE F716-NUM-NRIC           TO                                  
                                       NUM-IDNTY-NO OF DCLIDNTY-NO              
            END-IF.                                                             
                                                                                
            MOVE 'N'                        TO WS-EOC.                          
                                                                                
            PERFORM 3100-OPEN-CURSOR     THRU 3100-EXIT                         
            PERFORM 3200-FETCH-CURSOR    THRU 3200-EXIT                         
                                                                                
            IF END-OF-CURSOR                                                    
               ADD 1    TO WS-RDB2-NFND                                         
               DISPLAY 'RECORD NOT FOUND IN RDBII'                              
               DISPLAY 'IDENTITY NO :- ' F716-NUM-NRIC                          
            END-IF.                                                             
                                                                                
            PERFORM 3200-FETCH-CURSOR    THRU 3200-EXIT                         
                    UNTIL END-OF-CURSOR.                                        
                                                                                
            PERFORM 3300-CLOSE-CURSOR    THRU 3300-EXIT.                        
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3100-OPEN-CURSOR.                                                        
            EXEC SQL                                                            
              OPEN CURSOR1                                                      
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
              WHEN 0                                                            
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING RDBII'                                
                   DISPLAY 'SQLCODE :- ' SQLCODE                                
                   MOVE SQLCODE          TO WS-SQLCODE                          
                   MOVE WS-SQLCODE       TO RETURN-CODE                         
                   GO TO 9000-CLOSE-FILES                                       
            END-EVALUATE.                                                       
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3200-FETCH-CURSOR.                                                       
            EXEC SQL                                                            
              FETCH CURSOR1                                                     
                INTO                                                            
                  :DCLCSTMR-ROLE.NUM-ROLE,                                      
                  :DCLCSTMR-ROLE.NUM-BSNS-REF,                                  
                  :DCLCSTMR-ROLE.DTE-ACPTNC,                                    
                  :DCLCSTMR-ROLE.DTE-ROLE-START,                                
                  :DCLCSTMR-ID.NUM-CSTMR-SOURCE,                                
                  :DCLPERSONS.NUM-SEX,                                          
                  :DCLCSTMR-EX-ROLE.DTE-ROLE-END                                
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
              WHEN 0                                                            
                ADD 1    TO WS-RDB2-READ                                        
                PERFORM 4000-WRITE-BP13F716  THRU 4000-EXIT                     
              WHEN 100                                                          
                 MOVE 'Y'                    TO WS-EOC                          
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING RDBII'                                  
                 DISPLAY 'SQLCODE :- ' SQLCODE                                  
                 MOVE SQLCODE          TO WS-SQLCODE                            
                 MOVE WS-SQLCODE       TO RETURN-CODE                           
                 GO TO 9000-CLOSE-FILES                                         
            END-EVALUATE.                                                       
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3300-CLOSE-CURSOR.                                                       
            EXEC SQL                                                            
              CLOSE CURSOR1                                                     
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
              WHEN 0                                                            
                CONTINUE                                                        
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING RDBII'                                  
                 DISPLAY 'SQLCODE :- ' SQLCODE                                  
                 MOVE SQLCODE          TO WS-SQLCODE                            
                 MOVE WS-SQLCODE       TO RETURN-CODE                           
                 GO TO 9000-CLOSE-FILES                                         
            END-EVALUATE.                                                       
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4000-WRITE-BP13F716.                                                     
                                                                                
            MOVE NUM-BSNS-REF OF DCLCSTMR-ROLE TO F716-NUM-HDB-REF              
            MOVE NUM-ROLE     OF DCLCSTMR-ROLE TO F716-NUM-ROLE                 
                                                                                
            IF DTE-ACPTNC OF DCLCSTMR-ROLE = '0001-01-01'                       
               MOVE SPACES                     TO F716-DTE-ACCEPTED             
            ELSE                                                                
               STRING DTE-ACPTNC OF DCLCSTMR-ROLE(1:4),                         
                      DTE-ACPTNC OF DCLCSTMR-ROLE(6:2),                         
                      DTE-ACPTNC OF DCLCSTMR-ROLE(9:2) DELIMITED BY SIZE        
                      INTO                                                      
                      F716-DTE-ACCEPTED                                         
            END-IF.                                                             
                                                                                
            IF DTE-ROLE-START OF DCLCSTMR-ROLE = '0001-01-01'                   
               MOVE SPACES                     TO F716-DTE-MOVING-IN            
            ELSE                                                                
               STRING DTE-ROLE-START OF DCLCSTMR-ROLE(1:4),                     
                      DTE-ROLE-START OF DCLCSTMR-ROLE(6:2),                     
                      DTE-ROLE-START OF DCLCSTMR-ROLE(9:2)                      
                                     DELIMITED BY SIZE,                         
                      INTO                                                      
                      F716-DTE-MOVING-IN                                        
            END-IF.                                                             
                                                                                
                                                                                
      **-------------------------------------------------------------**         
      ** IN THE QUERY NUM-SEX WILL BE 'X' FOR HISTORICAL RECORDS     **         
      **-------------------------------------------------------------**         
            IF NUM-SEX NOT = 'X'                                                
               ADD  1                              TO WS-ACTV-RECS              
               PERFORM 4500-READ-RSDNT             THRU 4500-EXIT               
               MOVE SPACES                         TO                           
                                                  F716-DTE-MOVING-OUT           
            ELSE                                                                
               ADD  1                              TO WS-HIST-RECS              
               IF DTE-ROLE-END OF DCLCSTMR-EX-ROLE = '0001-01-01'               
                  MOVE SPACES                      TO                           
                                                   F716-DTE-MOVING-OUT          
               ELSE                                                             
                  STRING DTE-ROLE-END   OF DCLCSTMR-EX-ROLE(1:4),               
                         DTE-ROLE-END   OF DCLCSTMR-EX-ROLE(6:2),               
                         DTE-ROLE-END   OF DCLCSTMR-EX-ROLE(9:2)                
                                        DELIMITED BY SIZE,                      
                         INTO                                                   
                         F716-DTE-MOVING-OUT                                    
               END-IF                                                           
               MOVE SPACES                         TO                           
                                                   F716-CDE-RELATIONSHIP        
            END-IF.                                                             
                                                                                
            WRITE P13F716A-REC        FROM BP13F716-REC.                        
            ADD 1                     TO WS-F716-WRITE.                         
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4500-READ-RSDNT.                                                         
            MOVE NUM-CSTMR-SOURCE   OF DCLCSTMR-ID         TO                   
                                   NUM-CSTMR-SOURCE OF DCLRSDNT.                
            MOVE NUM-BSNS-REF   OF DCLCSTMR-ROLE (1:4)     TO                   
                                   NUM-SCHEME       OF DCLRSDNT.                
            MOVE NUM-BSNS-REF   OF DCLCSTMR-ROLE (5:5)     TO                   
                                   NUM-ACNT         OF DCLRSDNT.                
            MOVE NUM-BSNS-REF   OF DCLCSTMR-ROLE (10:2)    TO                   
                                   NUM-TNCY         OF DCLRSDNT.                
                                                                                
            EXEC SQL                                                            
                 SELECT                                                         
                   NUM_RLTNSP                                                   
                 INTO                                                           
                   :NUM-RLTNSP                                                  
                 FROM                                                           
                   RSDNT                                                        
                 WHERE                                                          
                   NUM_CSTMR_SOURCE = :DCLRSDNT.NUM-CSTMR-SOURCE AND            
                   NUM_SCHEME       = :DCLRSDNT.NUM-SCHEME       AND            
                   NUM_ACNT         = :DCLRSDNT.NUM-ACNT         AND            
                   NUM_TNCY         = :DCLRSDNT.NUM-TNCY                        
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
              WHEN 0                                                            
                ADD 1                          TO WS-RSDNT-READ                 
                MOVE SPACES                    TO BP13K060-REC                  
                MOVE '49'                      TO K060-SERIAL-NO                
                                                                                
                IF NUM-RLTNSP = '01' OR '21'                                    
                   STRING NUM-RLTNSP, NUM-SEX                                   
                          DELIMITED BY SIZE                                     
                          INTO                                                  
                          K060-CODE                                             
                ELSE                                                            
                   MOVE NUM-RLTNSP OF DCLRSDNT TO K060-CODE                     
                END-IF                                                          
                                                                                
                PERFORM 5000-READ-BP13K060     THRU 5000-EXIT                   
                MOVE K060-DESC1                TO F716-CDE-RELATIONSHIP         
              WHEN 100                                                          
                ADD 1                          TO WS-RSDNT-NFND                 
                DISPLAY 'RECORD NOT FOUND IN RSDNT TABLE '                      
                DISPLAY 'NUM-BSNS-REF : '                                       
                              NUM-BSNS-REF     OF DCLCSTMR-ROLE                 
                DISPLAY 'NUM-CSTMR-SOURCE : '                                   
                              NUM-CSTMR-SOURCE OF DCLRSDNT                      
              WHEN OTHER                                                        
                DISPLAY 'ERROR READING RSDNT'                                   
                DISPLAY 'SQL-CODE ' SQLCODE                                     
                MOVE SQLCODE          TO WS-SQLCODE                             
                MOVE WS-SQLCODE       TO RETURN-CODE                            
                GO TO 9000-CLOSE-FILES                                          
            END-EVALUATE.                                                       
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       5000-READ-BP13K060.                                                      
            READ BP13K060.                                                      
                                                                                
            EVALUATE WS-K060-STATUS                                             
              WHEN 0                                                            
                ADD 1                          TO WS-K060-READ                  
              WHEN 23                                                           
                MOVE SPACES                    TO K060-DESC1                    
                ADD 1                          TO WS-K060-NFND                  
                DISPLAY 'ERROR !! RECORD NOT FOUND IN BP13K060 '                
                DISPLAY 'K060-KEY-FLD :- ' K060-KEY-FLD                         
              WHEN OTHER                                                        
                DISPLAY 'ERROR READING BP13K060 '                               
                DISPLAY 'FILE-STATUS :- ' WS-K060-STATUS                        
                MOVE WS-K060-STATUS       TO RETURN-CODE                        
                GO TO 9000-CLOSE-FILES                                          
            END-EVALUATE.                                                       
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       9000-CLOSE-FILES.                                                        
            STRING WS-SYS-DATE(7:2) DELIMITED BY SIZE,                          
                   '-'                                                          
                   WS-SYS-DATE(5:2) DELIMITED BY SIZE,                          
                   '-'                                                          
                   WS-SYS-DATE(1:4) DELIMITED BY SIZE                           
                   INTO                                                         
                   WS-PRT-DATE.                                                 
                                                                                
            DISPLAY '   '.                                                      
            DISPLAY 'SUMMARY REPORT   BP13Q747   RUNDATE ' WS-PRT-DATE          
            DISPLAY '______________________________________________'.           
            DISPLAY '   '.                                                      
            DISPLAY 'NO OF RECORDS READ FROM BP13F716    : '                    
                                                    WS-F716-READ                
            DISPLAY 'NO OF RECORDS WITH INVALID UINFIN   : '                    
                                                    WS-INVALID-UINFIN           
            DISPLAY 'NO OF RECORDS READ FROM RDB II      : '                    
                                                    WS-RDB2-READ                
            DISPLAY 'NO OF RECORDS NOT FOUND IN RDB II   : '                    
                                                    WS-RDB2-NFND                
            DISPLAY 'NO OF ACTIVE RECORDS READ           : '                    
                                                    WS-ACTV-RECS                
            DISPLAY 'NO OF HISTORICAL RECORDS READ       : '                    
                                                    WS-HIST-RECS                
            DISPLAY 'NO OF RECORDS READ FROM RSDNT       : '                    
                                                    WS-RSDNT-READ               
            DISPLAY 'NO OF RECORDS NOT FOUND IN RSDNT    : '                    
                                                    WS-RSDNT-NFND               
            DISPLAY 'NO OF RECORDS READ FROM BP13K060    : '                    
                                                    WS-K060-READ                
            DISPLAY 'NO OF RECORDS NOT FOUND IN BP13K060 : '                    
                                                    WS-K060-NFND                
            DISPLAY 'NO OF RECORDS WRITTEN TO P13F716A   : '                    
                                                    WS-F716-WRITE               
                                                                                
            CLOSE BP13F716                                                      
                  BP13K060                                                      
                  P13F716A.                                                     
                                                                                
            IF WS-K060-STATUS NOT = 0                                           
               DISPLAY 'ERROR CLOSING BP13K060 '                                
               DISPLAY 'FILE-STATUS :- ' WS-K060-STATUS                         
               MOVE WS-K060-STATUS       TO RETURN-CODE                         
            END-IF.                                                             
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       9900-ABORT-PROCESS.                                                      
            CALL 'DBATIAR' USING SQLCA.                                         
            PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT.                         
