      *--------------------------------------------------------------*          
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------------*          
       PROGRAM-ID.    BP13C278.                                                 
       AUTHOR.        SK.                                                       
      *DATE-WRITTEN.  12/07/2003.                                               
      *****************************************************************         
      *                                                               *         
      *  SYSTEM NAME :  SYSTEM OF COMMITMENT                          *         
      *  SYSTEM ID   :  BP13                                          *         
      *                                                               *         
      *  OBJECTIVE :  REPORT ON APPLICATIONS UNDER SERS SCHEME        *         
      *                                                               *         
      * CHG-NO   BY   DATE      DESCRIPTION                           *         
      * ------   ---- -----     -----------                           *         
      * BP132357 SK   12072003  NEW PGM                               *         
      *****************************************************************         
                                                                                
      *--------------------------------------------------------------*          
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *--------------------------------------------------------------*          
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F310 ASSIGN TO BP13F310.                                  
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K110-KEY-FLD                               
                  FILE STATUS     IS BM06K110-STATUS.                           
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS K800-STATUS.                               
                                                                                
           SELECT BP13L278 ASSIGN TO BP13L278.                                  
                                                                                
      *--------------------------------------------------------------*          
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------------*          
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BP13F310                                                             
           RECORDING MODE IS F                                                  
           BLOCK  CONTAINS 0 CHARACTERS                                         
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS IS STANDARD.                                           
       COPY BP13F310.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13L278                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  BP13L278-REC        PIC X(132).                                      
                                                                                
      *--------------------------------------------------------------*          
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------*          
                                                                                
       01  WS-DATE                 PIC X(8)   VALUE SPACES.                     
                                                                                
       01  BM06K110-STATUS         PIC 99       VALUE ZEROS.                    
       01  K800-STATUS             PIC 99       VALUE ZEROS.                    
       01  WS-K800-MATCH           PIC X.                                       
       01  WS-K110-MATCH           PIC X.                                       
       01  WS-EOF-F310             PIC X.                                       
       01  WS-K800-BYPASS          PIC 9(08)    VALUE ZEROES.                   
                                                                                
       01  WS-K110-REC             PIC X(500).                                  
       01  WS-CNT-READ-F310        PIC 9(5)     VALUE ZEROS.                    
       01  WS-SERS-DISC            PIC 9(8)     VALUE ZEROS.                    
       01  WS-OCS-DISC             PIC 9(8)     VALUE ZEROS.                    
       01  WS-CNT-UPDATE           PIC 9(5)     VALUE ZEROS.                    
                                                                                
       01  WS-LISTING-CONTROLS.                                                 
           05  WS-SEQ           PIC 9(5)        VALUE ZEROS.                    
           05  CNT-PAGE         PIC 9(5)        VALUE ZEROS.                    
           05  CNT-NUM-LINE     PIC 9(5)        VALUE ZEROS.                    
                                                                                
       COPY SERSCH.                                                             
                                                                                
      ******************************************************************        
      *    BP13L278-REC                                                *        
      ******************************************************************        
                                                                                
       01  L278-PR-HEAD-01.                                                     
           05  FILLER              PIC X(8)      VALUE 'BP13L278'.              
           05  FILLER              PIC X(4)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(35)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'SYSTEM OF COMMITMENT'.                     
           05  FILLER              PIC X(20)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  L278-DATE           PIC X(10).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  L278-PAGENO         PIC ZZZZ9 .                                  
           05  FILLER              PIC X(6)      VALUE SPACES.                  
                                                                                
       01  L278-PR-HEAD-02.                                                     
           05  FILLER              PIC X(42)     VALUE SPACES.                  
           05  FILLER              PIC X(48)     VALUE                          
           'REPORT ON UPDATE OF CURRENT SELLING PRICE FOR   '.                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(27)     VALUE                          
           'USER : SALES UNIT, ERU, PPU'.                                       
                                                                                
       01  L278-PR-HEAD-03.                                                     
           05  FILLER              PIC X(42)     VALUE SPACES.                  
           05  FILLER              PIC X(49)     VALUE                          
           'APPLICATIONS UNDER SERS SCHEME - AFTER ACCEPTANCE'.                 
                                                                                
       01  L278-PR-HEAD-04.                                                     
           05  FILLER              PIC X(14) VALUE ' S/NO  REGN NO'.            
           05  FILLER              PIC X(09) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE 'OFFER    '.                 
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE 'CURRENT  '.                 
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE 'SERS DISC'.                 
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE 'OCS DISC '.                 
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(30) VALUE 'FLAT ADDRESS'.              
           05  FILLER              PIC X(04) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE 'ACCEPTANCE'.                
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(05) VALUE 'ALLOC'.                     
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(04) VALUE 'FLAT'.                      
           05  FILLER              PIC X(01) VALUE SPACES.                      
           05  FILLER              PIC X(03) VALUE 'OCS'.                       
                                                                                
       01  L278-PR-HEAD-05.                                                     
           05  FILLER              PIC X(07) VALUE SPACES.                      
           05  FILLER              PIC X(11) VALUE 'HDB-REF    '.               
           05  FILLER              PIC X(05) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE 'SELL PRICE'.                
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE 'SELL PRICE'.                
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE 'AMOUNT    '.                
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE 'AMOUNT    '.                
           05  FILLER              PIC X(36) VALUE SPACES.                      
           05  FILLER              PIC X(10) VALUE 'DATE      '.                
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(05) VALUE 'SCH  '.                     
           05  FILLER              PIC X(02) VALUE SPACES.                      
           05  FILLER              PIC X(04) VALUE 'TYPE'.                      
           05  FILLER              PIC X(01) VALUE SPACES.                      
           05  FILLER              PIC X(03) VALUE 'TAG'.                       
                                                                                
       01  L278-PR-DETAIL.                                                      
           05  L278-NO             PIC ZZZZ9.                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  L278-CUR-REGN-NO    PIC X(8).                                    
           05  FILLER              PIC X(8)      VALUE SPACES.                  
           05  L278-OFFR-SPRICE    PIC ZZ,ZZZ,ZZ9.                              
           05  FILLER              PIC X(2) VALUE SPACES.                       
           05  L278-CURR-SPRICE    PIC ZZ,ZZZ,ZZ9.                              
           05  FILLER              PIC X(5)      VALUE SPACES.                  
           05  L278-AMT-SERS-DISC  PIC ZZ,ZZ9.                                  
           05  FILLER              PIC X(6)      VALUE SPACES.                  
           05  L278-AMT-OCS-DISC   PIC Z,ZZ9.                                   
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  L278-BLK            PIC X(04)     VALUE 'BLK '.                  
           05  L278-BLK-NO         PIC X(05).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  L278-FLAT-NO.                                                    
               10  L278-HEX        PIC X(02)     VALUE '# '.                    
               10  L278-LEVEL-NO   PIC X(02).                                   
               10  L278-DASH       PIC X(01)     VALUE '-'.                     
               10  L278-UNIT-NO    PIC X(04).                                   
               10  L278-SUB-UNIT   PIC X(03).                                   
           05  FILLER              PIC X(11)  VALUE SPACES.                     
           05  L278-DTE-ACCEPT     PIC X(10)  VALUE SPACES.                     
           05  FILLER              PIC X(2)   VALUE SPACES.                     
           05  L278-ALLOC-SCH      PIC X(03)  VALUE SPACES.                     
           05  FILLER              PIC X(4)   VALUE SPACES.                     
           05  L278-FLAT-TYPE      PIC X(02)  VALUE SPACES.                     
           05  FILLER              PIC X(4)   VALUE SPACES.                     
           05  L278-OCS-TAG        PIC X(01)  VALUE SPACES.                     
                                                                                
       01  L278-PR-DETAIL-A.                                                    
           05  FILLER              PIC X(7) VALUE SPACES.                       
           05  L278-SCHEME         PIC 9(4).                                    
           05  L278-D1             PIC X.                                       
           05  L278-ACC            PIC 9(4).                                    
           05  L278-D2             PIC X.                                       
           05  L278-CHK            PIC 9.                                       
           05  L278-D3             PIC X.                                       
           05  L278-ACC-TYPE       PIC 9(2).                                    
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(48)     VALUE SPACES.                  
           05  L278-STREET         PIC X(20).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(2)      VALUE 'S('.                    
           05  L278-POSTAL-CODE    PIC X(6).                                    
           05  FILLER              PIC X(1)      VALUE ')'.                     
                                                                                
       01  L278-PR-LINE.                                                        
           05  FILLER              PIC X         VALUE SPACES.                  
           05  FILLER              PIC X(131)    VALUE ALL '-'.                 
                                                                                
      *--------------------------------------------------------------*          
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------------*          
                                                                                
      *--------------------------------------------------------------*          
       0000-MAIN.                                                               
      *--------------------------------------------------------------*          
                                                                                
           PERFORM 1000-OPENING-ROUTINE THRU 1000-EXIT.                         
           PERFORM 1500-READ-F310       THRU 1500-EXIT.                         
           PERFORM 2000-PROCESS-F310    THRU 2000-EXIT                          
             UNTIL WS-EOF-F310 = 'Y'.                                           
           PERFORM 9999-CLOSING-ROUTINE THRU 9999-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       1000-OPENING-ROUTINE.                                                    
      *--------------------------------------------------------------*          
                                                                                
           OPEN INPUT  BP13F310                                                 
                       BP13K800                                                 
                       BM06K110                                                 
                OUTPUT BP13L278.                                                
                                                                                
           IF BM06K110-STATUS NOT = 0 AND 97                                    
              DISPLAY 'OPENING ERROR, K110-STATUS ' BM06K110-STATUS             
              MOVE BM06K110-STATUS TO RETURN-CODE                               
              PERFORM 9999-CLOSING-ROUTINE THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF K800-STATUS NOT = 0 AND 97                                        
              DISPLAY 'OPENING ERROR, K800-STATUS ' K800-STATUS                 
              MOVE K800-STATUS TO RETURN-CODE                                   
              PERFORM 9999-CLOSING-ROUTINE THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE 60                   TO CNT-NUM-LINE.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       1500-READ-F310.                                                          
      *--------------------------------------------------------------*          
                                                                                
            READ BP13F310 AT END                                                
                MOVE 'Y' TO WS-EOF-F310                                         
                GO TO 1500-EXIT                                                 
            END-READ.                                                           
                                                                                
            ADD 1   TO WS-CNT-READ-F310.                                        
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       2000-PROCESS-F310.                                                       
      *--------------------------------------------------------------*          
                                                                                
           MOVE SPACES TO WS-K800-MATCH.                                        
           MOVE SPACES TO WS-K110-MATCH.                                        
           MOVE ZEROES TO WS-OCS-DISC.                                          
           MOVE ZEROES TO WS-SERS-DISC.                                         
                                                                                
           MOVE F310-REGN-NO      TO   K800-NUM-REGN.                           
           PERFORM 3100-READ-K800 THRU 3100-EXIT.                               
                                                                                
           IF WS-K800-MATCH = 'Y'                                               
                                                                                
              IF F310-SCH-ACC = K800-NUM-SCH-ACC                                
                 MOVE K800-NUM-SCH-ACC TO K110-KEY-FLD                          
                 PERFORM 3200-READ-K110 THRU 3200-EXIT                          
                 IF WS-K110-MATCH = 'Y'                                         
                    IF K110-REGN-NO = F310-REGN-NO                              
                       SET WS-SERS-PTR TO 1                                     
                       SEARCH WS-SERS-SCH                                       
                              AT END ADD 1 TO WS-K800-BYPASS                    
                          WHEN WS-SERS-SCHEME(WS-SERS-PTR) =                    
                              K800-NUM-ALLO-SCHEME                              
                          IF WS-SERS-DISCOUNT(WS-SERS-PTR) = 'F' OR 'R'         
                              PERFORM 5000-PROCESS-LIST THRU 5000-EXIT          
                          END-IF                                                
                       END-SEARCH                                               
                    ELSE                                                        
                       DISPLAY 'F310 --> SCH-ACC, REGN'                         
                                F310-SCH-ACC ',' F310-REGN-NO                   
                       DISPLAY 'K110 --> SCH-ACC, REGN'                         
                                K110-KEY-FLD ',' K110-REGN-NO                   
                    END-IF                                                      
                 ELSE                                                           
                    DISPLAY 'F310 --> SCH-ACC, REGN'                            
                             F310-SCH-ACC ',' F310-REGN-NO                      
                    DISPLAY 'K800 --> SCH-ACC, REGN'                            
                             K800-NUM-SCH-ACC ',' K800-NUM-REGN                 
                 END-IF                                                         
              ELSE                                                              
                 DISPLAY 'F310 --> SCH-ACC, REGN'                               
                          F310-SCH-ACC ',' F310-REGN-NO                         
                 DISPLAY 'K800-ALLOC-SCHEME --> ' K800-NUM-ALLO-SCHEME          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 1500-READ-F310 THRU 1500-EXIT.                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       3100-READ-K800.                                                          
      *--------------------------------------------------------------*          
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF K800-STATUS = 0                                                   
              MOVE 'Y' TO WS-K800-MATCH                                         
           ELSE                                                                 
              IF K800-STATUS = 23                                               
                 MOVE 'N' TO WS-K800-MATCH                                      
              ELSE                                                              
                 DISPLAY 'READ ERROR, K800-STATUS = ' K800-STATUS               
                 DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                       
                 MOVE K800-STATUS TO RETURN-CODE                                
                 PERFORM 9999-CLOSING-ROUTINE THRU 9999-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       3200-READ-K110.                                                          
      *--------------------------------------------------------------*          
                                                                                
           READ BM06K110.                                                       
                                                                                
           IF BM06K110-STATUS = 0                                               
              MOVE 'Y' TO WS-K110-MATCH                                         
              MOVE K110-REC TO WS-K110-REC                                      
           ELSE                                                                 
              IF BM06K110-STATUS = 23                                           
                 MOVE 'N' TO WS-K110-MATCH                                      
                 DISPLAY 'REC NOT FOUND IN K110 ' K110-KEY-FLD                  
              ELSE                                                              
                 DISPLAY 'READ ERROR, K110-STATUS = ' BM06K110-STATUS           
                 DISPLAY 'K110-KEY-FLD = ' K110-KEY-FLD                         
                 MOVE BM06K110-STATUS TO RETURN-CODE                            
                 PERFORM 9999-CLOSING-ROUTINE THRU 9999-EXIT                    
             END-IF                                                             
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       5000-PROCESS-LIST.                                                       
      *--------------------------------------------------------------*          
                                                                                
           IF CNT-NUM-LINE > 53                                                 
               PERFORM 6000-PRINT-LIST THRU 6000-EXIT                           
           END-IF.                                                              
                                                                                
           ADD 1                  TO WS-SEQ.                                    
           MOVE WS-SEQ            TO L278-NO.                                   
           PERFORM 5500-PREP-LIST  THRU 5500-EXIT.                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       5500-PREP-LIST.                                                          
      *--------------------------------------------------------------*          
                                                                                
            MOVE F310-REGN-NO         TO L278-CUR-REGN-NO.                      
            MOVE '-'                  TO L278-D1 L278-D2                        
                                         L278-D3.                               
            MOVE K110-SCHEME          TO L278-SCHEME.                           
            MOVE K110-ACC             TO L278-ACC.                              
            MOVE K110-CHK             TO L278-CHK.                              
            MOVE K110-ACC-TYPE        TO L278-ACC-TYPE.                         
                                                                                
            MOVE F310-NUM-BLK            TO L278-BLK-NO.                        
            MOVE F310-NME-STREET         TO L278-STREET.                        
            MOVE F310-NUM-LEVEL          TO L278-LEVEL-NO.                      
            MOVE F310-NUM-MAIN (1:4)     TO L278-UNIT-NO.                       
            MOVE F310-NUM-MAIN (5:1)     TO L278-SUB-UNIT.                      
            MOVE F310-NUM-POSTAL         TO L278-POSTAL-CODE.                   
            MOVE F310-CDE-TYP-ACTUSE     TO L278-FLAT-TYPE.                     
                                                                                
            MOVE K800-NUM-ALLO-SCHEME TO L278-ALLOC-SCH.                        
                                                                                
            STRING F310-DTE-DD '/'                                              
                   F310-DTE-MM '/'                                              
                   F310-DTE-CC                                                  
                   F310-DTE-YY                                                  
                   DELIMITED BY SIZE                                            
                   INTO L278-DTE-ACCEPT                                         
            END-STRING.                                                         
                                                                                
            MOVE K110-1ST-OFFER-SELLING-PRICE TO L278-OFFR-SPRICE.              
            MOVE K110-CURR-SELLING-PRICE      TO L278-CURR-SPRICE.              
            MOVE K110-AMT-FLAT-DISCOUNT       TO L278-AMT-SERS-DISC.            
            MOVE K110-AMT-OCS-DISCOUNT        TO L278-AMT-OCS-DISC.             
            MOVE K110-CDE-OCS-TAG             TO L278-OCS-TAG.                  
                                                                                
            WRITE BP13L278-REC         FROM L278-PR-DETAIL   AFTER 1.           
            WRITE BP13L278-REC         FROM L278-PR-DETAIL-A AFTER 1.           
            MOVE SPACES TO BP13L278-REC.                                        
            WRITE BP13L278-REC.                                                 
            ADD 3                      TO CNT-NUM-LINE.                         
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       6000-PRINT-LIST.                                                         
      *--------------------------------------------------------------*          
                                                                                
            PERFORM 6500-PRINT-HEADER THRU 6500-EXIT.                           
            MOVE SPACES           TO BP13L278-REC.                              
            ADD 1                 TO CNT-NUM-LINE.                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------------*          
       6500-PRINT-HEADER.                                                       
      *--------------------------------------------------------------*          
                                                                                
            ADD 1                 TO CNT-PAGE.                                  
            MOVE CNT-PAGE         TO L278-PAGENO.                               
            MOVE 8                TO CNT-NUM-LINE.                              
                                                                                
            MOVE FUNCTION CURRENT-DATE TO WS-DATE.                              
            STRING WS-DATE (7:2) '/'                                            
                   WS-DATE (5:2) '/'                                            
                   WS-DATE (1:4)                                                
                   DELIMITED BY SIZE                                            
                   INTO L278-DATE                                               
            END-STRING.                                                         
                                                                                
            WRITE BP13L278-REC FROM L278-PR-HEAD-01 AFTER PAGE.                 
            WRITE BP13L278-REC FROM L278-PR-HEAD-02 AFTER 1.                    
            WRITE BP13L278-REC FROM L278-PR-HEAD-03 AFTER 1.                    
            WRITE BP13L278-REC FROM L278-PR-HEAD-04 AFTER 3.                    
            WRITE BP13L278-REC FROM L278-PR-HEAD-05 AFTER 1.                    
            WRITE BP13L278-REC FROM L278-PR-LINE    AFTER 1.                    
                                                                                
            MOVE SPACES           TO BP13L278-REC.                              
            WRITE BP13L278-REC.                                                 
                                                                                
       6500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       9999-CLOSING-ROUTINE.                                                    
      *--------------------------------------------------------------*          
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*---------------  BP13C278  --------------*'.               
           DISPLAY 'TOTAL NO OF REC READ FROM BP13F310 = '                      
                   WS-CNT-READ-F310.                                            
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF K800 NOT SERS SCH           = '                      
                   WS-K800-BYPASS.                                              
                                                                                
           CLOSE BP13F310                                                       
                 BM06K110                                                       
                 BP13K800                                                       
                 BP13L278.                                                      
                                                                                
           IF BM06K110-STATUS NOT = 0                                           
               DISPLAY 'CLOSING ERROR, K110-STATUS ' BM06K110-STATUS            
           END-IF.                                                              
                                                                                
           IF K800-STATUS NOT = 0                                               
               DISPLAY 'CLOSING ERROR, K800-STATUS ' K800-STATUS                
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
                                                                                
