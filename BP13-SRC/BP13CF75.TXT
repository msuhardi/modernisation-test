       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF75.                                                 
      *AUTHOR.        ZARITA DANE SARMIENTO                                     
      *DATE-WRITTEN.  04/12/2012.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE  : REPORT  FOR   CASES WITH HLE DEFER TAG OR     *            
      *              DATE                                          *            
      *                                                            *            
      * INPUT      : BP13F800                                      *            
      * OUTPUT     : BP13LF75                                      *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTIONS                       *            
      * -------- -------- ----- ------------                       *            
      * BP134449 20121204 ZDD1  NEW PROGRAM                        *            
      * BP135149 20150724 ZDD1  ENHANCE CHECKING FOR HEL DEF DATE  *            
      *                         BP135782 CR#                       *            
      * BP138047 20151125 KAC1  RECOMPILE FOR BP27 COPYBOOK UPDATE *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
           SELECT BP13K022  ASSIGN        TO BP13K022                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K022-KEY-FLD                       
                            ALTERNATE KEY IS K022-NUM-REGN                      
                            FILE STATUS   IS BP13K022-STATUS.                   
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            ALTERNATE KEY IS K110-REGN-NO                       
                            FILE STATUS   IS BM06K110-STATUS.                   
                                                                                
           SELECT BM06K100  ASSIGN        TO BM06K100                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K100-KEY-FLD                       
                            FILE STATUS   IS BM06K100-STATUS.                   
                                                                                
           SELECT BP27K800  ASSIGN        TO BP27K800                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-HLA                       
                            FILE STATUS   IS BP27K800-STATUS.                   
                                                                                
           SELECT BP13LF75  ASSIGN TO BP13LF75.                                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            RECORDING MODE IS F.                                                
       COPY BP13F800.                                                           
                                                                                
       FD  BP13LF75                                                             
           RECORD CONTAINS 120 CHARACTERS                                       
           RECORDING  MODE  IS F.                                               
       01  LF75-PRTREC                       PIC X(120).                        
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS  100 CHARACTERS.                                    
       COPY BP13K022.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BM06K110.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BM06K100.                                                           
                                                                                
       FD   BP27K800                                                            
            RECORD CONTAINS 1200 CHARACTERS.                                    
       COPY BP27K800.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-F800-EOF                    PIC X     VALUE 'N'.                   
       01 WS-BYPASS                      PIC 9(6)  VALUE ZEROS.                 
       01 WS-BYPASS-LN-TAG-B             PIC 9(6)  VALUE ZEROS.                 
       01 WS-BYPASS-WITH-HLE-EXP         PIC 9(6)  VALUE ZEROS.                 
       01 WS-BYPASS-CANCELLED            PIC 9(6)  VALUE ZEROS.                 
       01 WS-F800-READ                   PIC 9(6)  VALUE ZEROS.                 
       01 WS-SNO                         PIC 9(4)  VALUE ZEROS.                 
       01 WS-WRT-LF75                    PIC 9(4)  VALUE ZEROS.                 
       01 WS-K022-FND                    PIC 9(4)  VALUE ZEROS.                 
       01 WS-K022-NFND                   PIC 9(4)  VALUE ZEROS.                 
       01 WS-K100-FND                    PIC 9(4)  VALUE ZEROS.                 
       01 WS-K100-NFND                   PIC 9(4)  VALUE ZEROS.                 
       01 WS-K110-FND                    PIC 9(4)  VALUE ZEROS.                 
       01 WS-K110-NFND                   PIC 9(4)  VALUE ZEROS.                 
       01 WS-K800-FND                    PIC 9(4)  VALUE ZEROS.                 
       01 WS-K800-NFND                   PIC 9(4)  VALUE ZEROS.                 
       01 BP13K022-STATUS                PIC X(2)  VALUE SPACES.                
       01 BM06K100-STATUS                PIC X(2)  VALUE SPACES.                
       01 BM06K110-STATUS                PIC X(2)  VALUE SPACES.                
       01 BP27K800-STATUS                PIC X(2)  VALUE SPACES.                
       01 WS-MONTH                       PIC 9(2)  VALUE ZEROS.                 
       01 WS-PAGE-CTR                    PIC 9(3)  VALUE ZEROS.                 
       01 WS-LINE-CTR                    PIC 9(2)  VALUE 51.                    
       01 WS-SNO-VAR                     PIC 9(5)  VALUE ZEROS.                 
                                                                                
       01 WS-DATE                        PIC X(8)  VALUE SPACES.                
       01 WS-DATE-TAG                    PIC X(8)  VALUE SPACES.                
       01 WS-DATE-DEF                    PIC X(8)  VALUE SPACES.                
                                                                                
       01  WS-NUM-FIELD.                                                00840001
           05  WS-REM                   PIC 9(3).                       00850001
           05  WS-YEAR                  PIC 9(4).                       00860001
                                                                        00880001
       01  WS-CURR-DATE.                                                00840001
           05  WS-CURRENT-DATE.                                                 
               10  WS-CURR-CC           PIC 99     VALUE 00.                    
               10  WS-CURR-DATE.                                                
                   15  WS-CURR-YY       PIC 99     VALUE 00.                    
                   15  WS-CURR-MM       PIC 99     VALUE 00.                    
                   15  WS-CURR-DD       PIC 99     VALUE 00.                    
                                                                                
       01  DATE-RECORD.                                                 00890001
           05  DATE-REC                   PIC X(8).                     00900001
           05  DATE-REC1 REDEFINES DATE-REC.                            00910001
               10 CAL-DATE.                                             00920001
                   15 CAL-YEAR            PIC 9(4).                     00930001
                       88 VALID-YEAR          VALUE 1900 THRU 2100.     00940001
                   15 CAL-MONTH           PIC 9(2).                     00950001
                       88 31-DAY-MONTH        VALUE 01, 03, 05, 07, 08, 00960001
                                                        10, 12.         00970001
                       88 30-DAY-MONTH        VALUE 04, 06, 09, 11.     00980001
                       88 28-29-DAY-MONTH     VALUE 02.                 00990001
                       88 VALID-MONTH         VALUE 01 THRU 12.         01000001
                   15 CAL-DAY             PIC 9(2).                     01010001
                       88 VALID-DAY           VALUE 01 THRU 31.         01020001
                                                                        01030001
                                                                                
                                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
      * REPORT LAYOUT                                                           
      *-------------------------------------------------------------            
       01  LF75-HD1.                                                            
           05  LF75-RPT-NAME              PIC X(10)   VALUE 'P13LF75A'.         
           05  FILLER                     PIC X(22)   VALUE                     
               'HDB3                  '.                                        
           05  FILLER                     PIC X(11)   VALUE SPACES.             
           05  FILLER                     PIC X(41)   VALUE                     
               'CASES WITH HLE DEF DATE OR HLE DEF TAG   '.                     
           05  FILLER                     PIC X(08)   VALUE SPACES.             
           05  FILLER                     PIC X(16)   VALUE                     
               '         DATE : '.                                              
           05  LF75-DATE                  PIC X(10)   VALUE SPACES.             
                                                                                
       01  LF75-HD2.                                                            
           05  FILLER                     PIC X(05) VALUE                       
               'S/NO.'.                                                         
           05  FILLER                     PIC X(03)   VALUE SPACES.             
           05  FILLER                     PIC X(08)   VALUE                     
               'REGN NO.'.                                                      
           05  FILLER                     PIC X(03)   VALUE SPACES.             
           05  FILLER                     PIC X(13)   VALUE                     
               'CSM IN CHARGE'.                                                 
           05  FILLER                     PIC X(03)   VALUE SPACES.             
           05  FILLER                     PIC X(12)   VALUE                     
               'HLE DEF DATE'.                                                  
           05  FILLER                     PIC X(03)   VALUE SPACES.             
           05  FILLER                     PIC X(11)   VALUE                     
               'HLE DEF TAG'.                                                   
           05  FILLER                     PIC X(03)   VALUE SPACES.             
           05  FILLER                     PIC X(16)   VALUE                     
               'HLE EXPIRY DATE'.                                               
           05  FILLER                     PIC X(03)   VALUE SPACES.             
           05  FILLER                     PIC X(10)   VALUE                     
               'SOCA DATE '.                                                    
           05  FILLER                     PIC X(03)   VALUE SPACES.             
           05  FILLER                     PIC X(08)   VALUE                     
               'LOAN TAG'.                                                      
                                                                                
       01  LF75-DETAIL.                                                         
           05  LF75-SNO                   PIC ZZZZ9.                            
           05  FILLER                     PIC X(03)   VALUE SPACES.             
           05  LF75-REGN                  PIC X(08)   VALUE SPACES.             
           05  FILLER                     PIC X(03)   VALUE SPACES.             
           05  LF75-CSM                   PIC X(13)   VALUE SPACES.             
           05  FILLER                     PIC X(03)   VALUE SPACES.             
           05  LF75-HLE-DEF-DATE          PIC X(12)   VALUE SPACES.             
           05  FILLER                     PIC X(03)   VALUE SPACES.             
           05  LF75-HLE-DEF-TAG           PIC X(11)   VALUE SPACES.             
           05  FILLER                     PIC X(03)   VALUE SPACES.             
           05  LF75-HLA-EXPIRY            PIC X(16)   VALUE SPACES.             
           05  FILLER                     PIC X(03)   VALUE SPACES.             
           05  LF75-SOCA-DATE             PIC X(10)   VALUE SPACES.             
           05  FILLER                     PIC X(03)   VALUE SPACES.             
           05  LF75-LOAN-TAG              PIC X(08)   VALUE SPACES.             
                                                                                
       01  LF75-TRLR.                                                           
           05  FILLER                     PIC X(37)   VALUE SPACES.             
           05  FILLER                     PIC X(56)   VALUE                     
               '******************* END OF REPORT *******************'.         
                                                                                
       01  WS-DASH-LINE                   PIC X(132)  VALUE ALL '-'.            
       01  WS-BLANK-LINE                  PIC X(132)  VALUE ALL SPACES.         
                                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-ROUTINE     THRU 1000-EXIT.                       
            PERFORM 2100-CHECK-DATE-MONTH THRU 2100-EXIT.                       
            PERFORM 2110-CHECK-DATE-DAY   THRU 2110-EXIT.                       
            PERFORM 2000-READ-BP13F800    THRU 2000-EXIT.                       
            PERFORM 3000-PROCESS-DETAIL   THRU 3000-EXIT                        
                    UNTIL WS-F800-EOF = 'Y'.                                    
            PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            OPEN  INPUT  BP13F800                                               
                         BP13K022                                               
                         BM06K100                                               
                         BM06K110                                               
                         BP27K800                                               
                  OUTPUT BP13LF75.                                              
                                                                                
           IF BP13K022-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13K022 : ' BP13K022-STATUS             
              MOVE     BP13K022-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BM06K100-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BM06K100 : ' BM06K100-STATUS             
              MOVE     BM06K100-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BM06K110 : ' BM06K110-STATUS             
              MOVE     BM06K110-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
           IF BP27K800-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP27K800 : ' BP27K800-STATUS             
              MOVE     BP27K800-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
            READ BP13F800                                                       
                 AT END MOVE 'Y' TO WS-F800-EOF                                 
                 GO TO 2000-EXIT.                                               
                                                                                
            ADD 1   TO WS-F800-READ.                                            
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2100-CHECK-DATE-MONTH.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE FUNCTION CURRENT-DATE TO    WS-CURRENT-DATE.                    
           STRING WS-CURRENT-DATE(7:2)  '/'                                     
                  WS-CURRENT-DATE(5:2)  '/'                                     
                  WS-CURRENT-DATE(1:4) DELIMITED BY SIZE INTO                   
                  LF75-DATE.                                                    
                                                                                
           MOVE WS-CURRENT-DATE       TO DATE-REC                               
           MOVE ZEROS            TO WS-NUM-FIELD.                       02570036
           ADD  3                TO CAL-MONTH.                          02610036
                                                                        02620001
           PERFORM 2200-PROCESS-DATE  THRU 2200-EXIT.                   02630001
           MOVE DATE-REC         TO WS-DATE-TAG.                                
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2110-CHECK-DATE-DAY.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE FUNCTION CURRENT-DATE TO    WS-CURRENT-DATE.                    
           MOVE WS-CURRENT-DATE       TO DATE-REC                               
           MOVE ZEROS            TO WS-NUM-FIELD.                       02570036
           ADD  42               TO CAL-DAY.                            02610036
                                                                        02620001
           PERFORM 2200-PROCESS-DATE  THRU 2200-EXIT.                   02630001
           MOVE DATE-REC         TO WS-DATE-DEF.                                
                                                                                
       2110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2200-PROCESS-DATE.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF 31-DAY-MONTH                                              02740001
               IF CAL-DAY > 31                                          02750002
               COMPUTE CAL-DAY    = CAL-DAY    - 31                     02760001
               COMPUTE CAL-MONTH  = CAL-MONTH  + 1                      02770002
               END-IF                                                   02771002
           END-IF.                                                      02772002
                                                                        02780001
           IF 30-DAY-MONTH                                              02790001
               IF CAL-DAY > 30                                          02800002
               COMPUTE CAL-DAY    = CAL-DAY    - 30                     02810001
               COMPUTE CAL-MONTH  = CAL-MONTH  + 1                      02820002
               END-IF                                                   02821002
           END-IF.                                                      02822002
                                                                        02830001
           IF 28-29-DAY-MONTH                                           02840001
               IF  CAL-DAY < 29                                         02850002
                   GO TO 2200-EXIT                                      02860001
               ELSE                                                     02870001
                   PERFORM 2300-FEBRUARY                                02880001
                      THRU 2300-EXIT                                    02890001
               END-IF                                                   02891002
           END-IF.                                                      02892002
                                                                        02910001
           IF CAL-MONTH  > 12                                           02920001
              COMPUTE WS-MONTH = WS-MONTH - 12                                  
              MOVE WS-MONTH TO CAL-MONTH                                02930001
              ADD 01 TO CAL-YEAR.                                       02940001
                                                                        02950001
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2300-FEBRUARY.                                                   03160001
      *-------------------------------------------------------------            
                                                                                
           DIVIDE 4 INTO CAL-YEAR GIVING WS-YEAR REMAINDER WS-REM.      03180001
           IF (WS-REM = ZERO) AND (CAL-DAY > 29)                        03190002
              COMPUTE CAL-DAY = CAL-DAY - 29                            03210001
              MOVE 03 TO CAL-MONTH                                      03220001
           ELSE                                                         03230001
           IF (WS-REM NOT = ZERO) AND (CAL-DAY > 28)                    03250002
              COMPUTE CAL-DAY = CAL-DAY - 28                            03270001
              MOVE 03 TO CAL-MONTH.                                     03280001
                                                                        03290001
       2300-EXIT.                                                       03300001
           EXIT.                                                        03310001
                                                                        03320001
      *-------------------------------------------------------------            
       3000-PROCESS-DETAIL.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES        TO LF75-DETAIL.                                   
                                                                                
           IF F800-NUM-HLE-DEFER-TAG NOT = 'Y'                                  
             IF WS-DATE-DEF >=  F800-DTE-HLE-DEFERMENT                          
                CONTINUE                                                        
             ELSE                                                               
                ADD 1 TO WS-BYPASS                                              
                PERFORM 2000-READ-BP13F800  THRU 2000-EXIT                      
                GO TO 3000-EXIT                                                 
             END-IF                                                             
           ELSE                                                                 
              PERFORM 4100-READ-BM06K110 THRU 4100-EXIT                         
              PERFORM 4200-READ-BM06K100 THRU 4200-EXIT                         
              IF WS-DATE-TAG >=  K100-DTE-PACD                                  
                IF  K100-DTE-PACD = ZEROES OR SPACES OR LOW-VALUES              
                  ADD 1 TO WS-BYPASS                                            
                  PERFORM 2000-READ-BP13F800  THRU 2000-EXIT                    
                  GO TO 3000-EXIT                                               
                ELSE                                                            
                  CONTINUE                                                      
                END-IF                                                          
             ELSE                                                               
                ADD 1 TO WS-BYPASS                                              
                PERFORM 2000-READ-BP13F800  THRU 2000-EXIT                      
                GO TO 3000-EXIT                                                 
             END-IF                                                             
           END-IF.                                                              
                                                                                
           IF F800-NUM-LOAN-TAG = 'B'                                           
              ADD 1 TO WS-BYPASS-LN-TAG-B                                       
              PERFORM 2000-READ-BP13F800  THRU 2000-EXIT                        
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
           IF F800-DTE-CANCEL NOT = SPACES AND LOW-VALUES AND ZEROES            
              ADD 1 TO WS-BYPASS-CANCELLED                                      
              PERFORM 2000-READ-BP13F800  THRU 2000-EXIT                        
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 4000-READ-BP13K022     THRU 4000-EXIT.                       
           PERFORM 4300-READ-BP27K800     THRU 4300-EXIT.                       
                                                                                
           IF K800-DTE-HLA-EXPIRY    = SPACES OR LOW-VALUES                     
              MOVE SPACES              TO LF75-HLE-DEF-DATE                     
           ELSE                                                                 
              ADD 1 TO WS-BYPASS-WITH-HLE-EXP                                   
              PERFORM 2000-READ-BP13F800  THRU 2000-EXIT                        
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE F800-NUM-REGN          TO LF75-REGN.                            
           ADD 1                       TO WS-SNO-VAR                            
           MOVE WS-SNO-VAR             TO LF75-SNO                              
           MOVE K022-NUM-OIC           TO LF75-CSM                              
           IF F800-DTE-HLE-DEFERMENT = SPACES OR LOW-VALUES                     
              MOVE SPACES              TO LF75-HLE-DEF-DATE                     
           ELSE                                                                 
              STRING F800-DTE-HLE-DEFERMENT(7:2) '/'                            
                     F800-DTE-HLE-DEFERMENT(5:2) '/'                            
                     F800-DTE-HLE-DEFERMENT(1:4)                                
              DELIMITED BY SIZE INTO LF75-HLE-DEF-DATE                          
           END-IF.                                                              
           MOVE F800-NUM-LOAN-TAG      TO LF75-LOAN-TAG                         
           MOVE F800-NUM-HLE-DEFER-TAG TO LF75-HLE-DEF-TAG                      
           MOVE K800-DTE-HLA-EXPIRY    TO LF75-HLA-EXPIRY                       
           IF K800-DTE-HLA-EXPIRY           = SPACES OR LOW-VALUES              
              MOVE SPACES              TO LF75-HLA-EXPIRY                       
           ELSE                                                                 
              STRING K800-DTE-HLA-EXPIRY(7:2) '/'                               
                     K800-DTE-HLA-EXPIRY(5:2) '/'                               
                     K800-DTE-HLA-EXPIRY(1:4)                                   
              DELIMITED BY SIZE INTO LF75-HLA-EXPIRY                            
           END-IF.                                                              
           IF F800-DTE-PRINCIPAL-SALES-AGMT = SPACES OR LOW-VALUES              
              MOVE SPACES              TO LF75-SOCA-DATE                        
           ELSE                                                                 
              STRING F800-DTE-PRINCIPAL-SALES-AGMT(7:2) '/'                     
                     F800-DTE-PRINCIPAL-SALES-AGMT(5:2) '/'                     
                     F800-DTE-PRINCIPAL-SALES-AGMT(1:4)                         
              DELIMITED BY SIZE INTO LF75-SOCA-DATE                             
           END-IF.                                                              
                                                                                
           PERFORM 5000-OUTPUT-REPORT   THRU 5000-EXIT.                         
                                                                                
           PERFORM 2000-READ-BP13F800  THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4000-READ-BP13K022.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES           TO  BP13K022-REC.                              
           MOVE F800-NUM-REGN    TO  K022-NUM-REGN.                             
                                                                                
           READ BP13K022  KEY IS K022-NUM-REGN.                                 
                                                                                
           EVALUATE BP13K022-STATUS                                             
             WHEN 00                                                            
                 ADD 1 TO WS-K022-FND                                           
             WHEN 23                                                            
                 ADD 1 TO WS-K022-NFND                                          
             WHEN OTHER                                                         
              DISPLAY 'ERROR READING - BP13K022 : ' BP13K022-STATUS             
              MOVE     BP13K022-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4100-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES           TO  K110-REC.                                  
           MOVE F800-NUM-REGN    TO  K110-REGN-NO.                              
                                                                                
           READ BM06K110  KEY IS K110-REGN-NO.                                  
                                                                                
           EVALUATE BM06K110-STATUS                                             
             WHEN 00                                                            
                 ADD 1 TO WS-K110-FND                                           
             WHEN 23                                                            
                 ADD 1 TO WS-K110-NFND                                          
             WHEN OTHER                                                         
              DISPLAY 'ERROR READING - BM06K110 : ' BM06K110-STATUS             
              MOVE     BM06K110-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4200-READ-BM06K100.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES           TO  K100-REC.                                  
           MOVE K110-AIX1        TO  K100-KEY-FLD.                              
                                                                                
           READ BM06K100.                                                       
                                                                                
           EVALUATE BM06K100-STATUS                                             
             WHEN 00                                                            
                 ADD 1 TO WS-K100-FND                                           
             WHEN 23                                                            
                 ADD 1 TO WS-K100-NFND                                          
             WHEN OTHER                                                         
              DISPLAY 'ERROR READING - BM06K100 : ' BM06K100-STATUS             
              MOVE     BM06K100-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4300-READ-BP27K800.                                                      
      *-------------------------------------------------------------            
           MOVE SPACES           TO  BP27K800-MASTER.                           
           MOVE F800-NUM-HLA     TO  K800-NUM-HLA.                              
                                                                                
           READ BP27K800.                                                       
                                                                                
           EVALUATE BP27K800-STATUS                                             
             WHEN 00                                                            
                 ADD 1 TO WS-K800-FND                                           
             WHEN 23                                                            
                 ADD 1 TO WS-K800-NFND                                          
             WHEN OTHER                                                         
              DISPLAY 'ERROR READING - BP27K800 : ' BP27K800-STATUS             
              MOVE     BP27K800-STATUS      TO RETURN-CODE                      
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-OUTPUT-REPORT.                                                      
      *-------------------------------------------------------------            
                                                                                
           PERFORM 5100-PRINT-HEADER   THRU 5100-EXIT.                          
           WRITE LF75-PRTREC           FROM LF75-DETAIL.                        
           ADD   1 TO WS-LINE-CTR.                                              
                                                                                
           ADD   1 TO WS-WRT-LF75.                                              
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       5100-PRINT-HEADER.                                                       
      *-------------------------------------------------------------            
           IF WS-LINE-CTR > 50                                                  
             ADD 1                    TO WS-PAGE-CTR                            
             WRITE LF75-PRTREC      FROM LF75-HD1                               
             WRITE LF75-PRTREC      FROM LF75-HD2                               
             MOVE SPACES   TO            LF75-PRTREC                            
             WRITE LF75-PRTREC                                                  
             MOVE 4                   TO WS-LINE-CTR                            
           END-IF.                                                              
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
            DISPLAY ' '.                                                        
            DISPLAY '- BP13CF75 CONTROL LIST --'                                
            DISPLAY '---------------------------------------------'.            
            DISPLAY ' 1) TOT BP13F800 READ   : ' WS-F800-READ.                  
            DISPLAY ' 2) TOT RECORD WRITTEN  : ' WS-WRT-LF75.                   
            DISPLAY ' 3) TOT RECORD BYPASS   : ' WS-BYPASS.                     
            DISPLAY ' 4) BYPASS LOAN TAG B   : ' WS-BYPASS-LN-TAG-B             
            DISPLAY ' 5) BYPASS HLE DTE EXP  : ' WS-BYPASS-WITH-HLE-EXP         
            DISPLAY ' 6) BYPASS CANCELLED    : ' WS-BYPASS-CANCELLED            
            DISPLAY ' '.                                                        
                                                                                
            CLOSE  BP13F800                                                     
                   BP13K022                                                     
                   BM06K110                                                     
                   BM06K100                                                     
                   BP13LF75.                                                    
                                                                                
           IF BP13K022-STATUS  NOT =  00 AND 97                                 
              DISPLAY 'ERROR OPENING - BP13K022 : ' BP13K022-STATUS             
              MOVE     BP13K022-STATUS      TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
