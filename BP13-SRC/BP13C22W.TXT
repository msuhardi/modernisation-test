      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C22W.                                                 
      *AUTHOR.        LKH.                                                      
      *DATE-WRITTEN.  11.1.91.                                                  
      *COPY OF.       BP13C226.                                                 
                                                                                
      * ======================================================== *              
      * SYSTEM OF COMMITMENT  (BP13)                             *              
      * ======================================================== *              
      *    OBJECTIVE  :                                          *              
      *        1.  PROGRAM TO EXTRACT DATA TO PRINT APPENDIX B   *              
      *            CONSISTING OF ALL UNITS AVAILABLE FOR         *              
      *            SELECTION, DISCARD UNITS THAT ARE SET ASIDE   *              
      *            FOR SAF AND MHA OR FREEZE FROM ALLOCATIONS.   *              
      *                                                          *              
      *     INPUT FILES:                                         *              
      *           1. BP13F208  - ADJUSTED FLATLIST               *              
      *           2. BP13K270  - BOOKING PROGRAM                 *              
      *           3. BM06K110  - FLAT INVENTORY MASTER           *              
      *                                                          *              
      *     OUTPUT FILES:                                        *              
      *           1. BM06F800  - EXTRACT RECORD FOR PRINTING     *              
      *                          SELLING PRICE                   *              
      *                                                          *              
      *  NOTE : THIS PROGRAM NEED TO RUN BEFORE BM06696A         *              
      * -------------------------------------------------------- *              
      * CHG NO   DATE     BY   DESCRIPTION                       *              
      * -------  -------- ---  -----------                       *              
      * BP132562 30/09/05 GAN1 REPLACE F205 WITH F208 INPUT      *              
      *                        ORIGINAL COPY IN BP13C226         *              
      * BP132864 16/05/06 LMS1 TO CHECK SELTY AND QTR WHEN       *              
      *                        EXTRACTING UNITS.                 *              
      * BP13XXXX 13/10/09 LMS1 TO CATER FOR NPL ZONE.            *              
      * BP134731 09/11/12 PCL4 TO CATER FOR MODL-DESIGN 89       *              
      * BP135838 01/07/15 ESA1 TO CATER FOR MODL-DESIGN 92       *              
      * ======================================================== *              
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13K270 ASSIGN TO BP13K270                                   
                ORGANIZATION IS INDEXED                                         
                ACCESS MODE  IS DYNAMIC                                         
                RECORD KEY   IS K270-KEY-FLD                                    
                FILE STATUS  IS K270-STATUS.                                    
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                ORGANIZATION  IS INDEXED                                        
                ACCESS MODE   IS DYNAMIC                                        
                RECORD KEY    IS K110-KEY-FLD                                   
                ALTERNATE KEY IS K110-AIX1                                      
                FILE STATUS   IS K110-STATUS.                                   
                                                                                
           SELECT BP13F208 ASSIGN TO BP13F208.                                  
           SELECT BM06F800 ASSIGN TO BM06F800.                                  
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
                                                                                
       FD   BP13K270                 RECORD CONTAINS 250 CHARACTERS.            
       COPY BP13K270.                                                           
                                                                                
       FD   BM06K110                 RECORD CONTAINS 500 CHARACTERS.            
       COPY BM06K110.                                                           
                                                                                
       FD   BP13F208                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13F208.                                                           
                                                                                
       FD  BM06F800                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 800 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BM06F800.                                                           
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
       01  FILES-STS.                                                           
           05  K110-STATUS         PIC 99      VALUE ZEROS.                     
           05  K270-STATUS         PIC 99      VALUE ZEROS.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-CNT-CTL     PIC 9(5)    VALUE ZEROS.                     
           05  WS-READ-CNT-AS      PIC 9(5)    VALUE ZEROS.                     
           05  WS-READ-CNT-HOLD    PIC 9(5)    VALUE ZEROS.                     
           05  WS-READ-CNT-RP      PIC 9(5)    VALUE ZEROS.                     
           05  WS-READ-CNT-SD      PIC 9(5)    VALUE ZEROS.                     
           05  WS-READ-CNT-K270    PIC 9(5)    VALUE ZEROS.                     
           05  WS-READ-CNT-K110    PIC 9(5)    VALUE ZEROS.                     
           05  WS-SKIP-RECORD      PIC 9(5)    VALUE ZEROS.                     
           05  WS-WRITE-CNT-F800   PIC 9(5)    VALUE ZEROS.                     
           05  WS-NUM-ALLOC.                                                    
               10  WS-NUM-ALLOC1   PIC X(2)    VALUE SPACES.                    
               10  WS-NUM-ALLOC2   PIC X(2)    VALUE SPACES.                    
               10  WS-NUM-ALLOC3   PIC X(2)    VALUE SPACES.                    
               10  WS-NUM-ALLOC4   PIC X(2)    VALUE SPACES.                    
               10  WS-NUM-ALLOC5   PIC X(2)    VALUE SPACES.                    
               10  WS-NUM-ALLOC6   PIC X(2)    VALUE SPACES.                    
               10  WS-NUM-ALLOC7   PIC X(2)    VALUE SPACES.                    
               10  WS-NUM-ALLOC8   PIC X(2)    VALUE SPACES.                    
               10  WS-NUM-ALLOC9   PIC X(2)    VALUE SPACES.                    
                                                                                
           05  WS-OFFER-COUNT-ARRAY.                                            
               10  WS-OFFER-COUNT-TABLE OCCURS 9 TIMES.                         
                   15  WS-OFFER-COUNT                PIC 9(3).                  
                                                                                
       01  WS-K110-FLAT-TYPE       PIC X(2)    VALUE SPACES.                    
       01  WS-NUM-FLAT-TYPE        PIC X(2)    VALUE SPACES.                    
       01  WS-NUM-FLAT-STATUS      PIC X(1)    VALUE SPACES.                    
       01  WS-NUM-UPG-STATUS       PIC X(3)    VALUE SPACES.                    
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-EOF-CTL          PIC X       VALUE SPACES.                    
           05  WS-EOF-K110         PIC X       VALUE SPACES.                    
           05  WS-EOF-K270         PIC X       VALUE SPACES.                    
           05  WS-UPDATE-FLAG      PIC X       VALUE SPACES.                    
                                                                                
       01  WS-K270-KEY-FLD.                                                     
           05  WS-K270-ALLOCN-DATE PIC 9(8).                                    
           05  WS-K270-ALLOCN-INFO PIC X(16).                                   
                                                                                
       01  WS-K110-AIX1.                                                        
           05  WS-K110-ESTATE      PIC X(3).                                    
           05  WS-K110-NEIGH       PIC X(4).                                    
           05  WS-K110-CONTRACT-NO PIC X(4).                                    
           05  WS-K110-BLK-NO      PIC X(5).                                    
                                                                                
      *---------------------------------------------------------------*         
      *        LINKAGE FOR BP13C913                                   *         
      *---------------------------------------------------------------*         
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH   PIC X(4).                                  
               10  WS-LINK-NUM-ACC   PIC X(5).                                  
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *-----------------------------------------------------------*             
      *      LINK-AREA TO CALL SUB-PROGRAM (DJ02C906)             *             
      *-----------------------------------------------------------*             
       01  LINK-AREA.                                                           
           05  LINK-SQL-RETCODE             PIC S9(4).                          
           05  LINK-NUM-SCHM                PIC X(4).                           
           05  LINK-NUM-ACCNT               PIC X(7).                           
           05  LINK-NUM-BNDRY-STATUS        PIC X(3).                           
                                                                                
       01  WS-SQL-RETCODE                   PIC -9(3).                          
                                                                                
                                                                                
      *-----------------------*                                                 
       PROCEDURE DIVISION.                                                      
      *-----------------------*                                                 
      *-----------------------------------------------------------              
       000-MAIN-ROUTINE.                                                        
      *-----------------------------------------------------------              
           PERFORM 100-OPEN-FILES  THRU 100-EXIT.                               
           PERFORM 200-READ-CTL    THRU 200-EXIT.                               
           PERFORM 300-PROCESS-REC THRU 300-EXIT                                
                   UNTIL WS-EOF-CTL = 'Y'.                                      
           PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       100-OPEN-FILES.                                                          
      *-----------------------------------------------------------              
           OPEN INPUT  BP13F208                                                 
                OUTPUT BM06F800.                                                
                                                                                
           MOVE ZEROS TO WS-COUNTERS.                                           
           MOVE 'N'   TO WS-EOF-CTL.                                            
           MOVE 'N'   TO WS-EOF-K110.                                           
           MOVE 'N'   TO WS-EOF-K270.                                           
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       200-READ-CTL.                                                            
      *-----------------------------------------------------------              
           READ BP13F208 AT END                                                 
                MOVE 'Y' TO WS-EOF-CTL                                          
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1 TO WS-READ-CNT-CTL.                                            
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       300-PROCESS-REC.                                                         
      *-----------------------------------------------------------              
           OPEN INPUT  BM06K110                                                 
                       BP13K270.                                                
                                                                                
           IF K110-STATUS NOT = ZEROS AND 97                                    
              DISPLAY 'OPEN ERROR,BM06K110-STATUS ' K110-STATUS                 
              MOVE K110-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                          
                                                                                
           IF K270-STATUS NOT = ZEROS AND 97                                    
              DISPLAY 'OPEN ERROR,BP13K270-STATUS ' K270-STATUS                 
              MOVE K270-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                          
                                                                                
           MOVE 'N' TO WS-EOF-K270.                                             
           PERFORM 400-START-K270 THRU 400-EXIT.                                
           PERFORM 410-READ-K270  THRU 410-EXIT UNTIL                           
                   WS-EOF-K270 = 'Y'.                                           
                                                                                
           IF WS-EOF-K270 = 'Y'                                                 
              PERFORM 200-READ-CTL THRU 200-EXIT                                
           END-IF.                                                              
                                                                                
           CLOSE BP13K270                                                       
                 BM06K110.                                                      
                                                                                
           IF K110-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, BM06K110-STATUS ' K110-STATUS.              
           IF K270-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSE ERROR, BP13K270-STATUS ' K270-STATUS.              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       400-START-K270.                                                          
      *-----------------------------------------------------------              
           MOVE LOW-VALUE       TO K270-KEY-FLD.                                
           MOVE F208-DTE-ALLOCN TO WS-K270-ALLOCN-DATE.                         
           MOVE WS-K270-KEY-FLD TO K270-KEY-FLD.                                
                                                                                
           START BP13K270 KEY NOT < K270-KEY-FLD INVALID                        
                 DISPLAY 'START ERROR, BP13K270-STATUS ' K270-STATUS            
                 DISPLAY 'K270-KEY-FLD ' K270-KEY-FLD                           
                 MOVE 'Y' TO WS-EOF-K270.                                       
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------------------------------------------------              
       410-READ-K270.                                                           
      *-----------------------------------------------------------              
           READ BP13K270 NEXT AT END                                            
                MOVE 'Y' TO WS-EOF-K270                                         
                GO TO 410-EXIT.                                                 
                                                                                
           IF K270-ALLOCN-DATE = F208-DTE-ALLOCN                                
              CONTINUE                                                          
           ELSE                                                                 
              MOVE 'Y' TO WS-EOF-K270                                           
              GO TO 410-EXIT.                                                   
                                                                                
           IF (F208-NUM-NT-ZONE    = K270-CDE-NT)                               
              CONTINUE                                                          
           ELSE                                                                 
              GO TO 410-EXIT                                                    
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-READ-CNT-K270.                                           
                                                                                
           IF K270-NUM-ALLOC(1) = ZEROES                                        
              MOVE '01'             TO WS-NUM-ALLOC1.                           
                                                                                
           IF K270-NUM-ALLOC(2) = ZEROES                                        
              MOVE '02'             TO WS-NUM-ALLOC2.                           
                                                                                
           IF K270-NUM-ALLOC(3) = ZEROES                                        
              MOVE '03'             TO WS-NUM-ALLOC3.                           
                                                                                
           IF K270-NUM-ALLOC(4) = ZEROES                                        
              MOVE '04'             TO WS-NUM-ALLOC4.                           
                                                                                
           IF K270-NUM-ALLOC(5) = ZEROES                                        
              MOVE '4S'             TO WS-NUM-ALLOC5.                           
                                                                                
           IF K270-NUM-ALLOC(6) = ZEROES                                        
              MOVE '05'             TO WS-NUM-ALLOC6.                           
                                                                                
           IF K270-NUM-ALLOC(7) = ZEROES                                        
              MOVE '06'             TO WS-NUM-ALLOC7.                           
                                                                                
           IF K270-NUM-ALLOC(8) = ZEROES                                        
              MOVE '07'             TO WS-NUM-ALLOC8.                           
                                                                                
           IF K270-NUM-ALLOC(9) = ZEROES                                        
              MOVE '08'             TO WS-NUM-ALLOC9.                           
                                                                                
           PERFORM 420-DECODE-FLAT-TYPE THRU 420-EXIT.                          
           PERFORM 430-PROCESS-K110     THRU 430-EXIT.                          
                                                                                
           MOVE SPACES              TO WS-NUM-ALLOC.                            
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       420-DECODE-FLAT-TYPE.                                                    
      *-----------------------------------------------------------              
           EVALUATE F208-NUM-FLAT-TYPE                                          
              WHEN '1 '                                                         
                MOVE '01' TO WS-NUM-FLAT-TYPE                                   
              WHEN '1A'                                                         
                MOVE '1A' TO WS-NUM-FLAT-TYPE                                   
              WHEN '2 '                                                         
                MOVE '02' TO WS-NUM-FLAT-TYPE                                   
              WHEN '2A'                                                         
                MOVE '2A' TO WS-NUM-FLAT-TYPE                                   
              WHEN '3 '                                                         
                MOVE '03' TO WS-NUM-FLAT-TYPE                                   
              WHEN '4 '                                                         
              WHEN '4D'                                                         
                MOVE '04' TO WS-NUM-FLAT-TYPE                                   
              WHEN '5 '                                                         
              WHEN '5D'                                                         
                MOVE '05' TO WS-NUM-FLAT-TYPE                                   
              WHEN 'E '                                                         
              WHEN 'ED'                                                         
                MOVE '06' TO WS-NUM-FLAT-TYPE                                   
              WHEN 'H '                                                         
                MOVE '07' TO WS-NUM-FLAT-TYPE                                   
              WHEN 'MG'                                                         
                MOVE '08' TO WS-NUM-FLAT-TYPE                                   
           END-EVALUATE.                                                        
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       425-DECODE-K110-FLAT-TYPE.                                               
      *-----------------------------------------------------------              
           IF COMM8-CDE-TYP-ACTLUSE = '04' AND                                  
              COMM8-CDE-MODL-DESG = '06'                                        
              MOVE '4S'                  TO WS-K110-FLAT-TYPE                   
           ELSE                                                                 
              MOVE COMM8-CDE-TYP-ACTLUSE TO WS-K110-FLAT-TYPE.                  
                                                                                
       425-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       430-PROCESS-K110.                                                        
      *-----------------------------------------------------------              
           MOVE 'N' TO WS-EOF-K110, WS-UPDATE-FLAG.                             
           INITIALIZE WS-OFFER-COUNT-ARRAY.                                     
           PERFORM 431-START-K110 THRU 431-EXIT.                                
           PERFORM 432-READ-K110  THRU 432-EXIT UNTIL                           
                   WS-EOF-K110 = 'Y'.                                           
                                                                                
                                                                                
       430-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       431-START-K110.                                                          
      *-----------------------------------------------------------              
           MOVE 'N' TO WS-EOF-K110.                                             
           MOVE K270-ESTATE         TO WS-K110-ESTATE.                          
           MOVE K270-NEIGHBOURHOOD  TO WS-K110-NEIGH.                           
           MOVE K270-CONTRACT-NO    TO WS-K110-CONTRACT-NO.                     
           MOVE K270-NUM-BLK        TO WS-K110-BLK-NO.                          
           MOVE WS-K110-AIX1        TO K110-AIX1.                               
                                                                                
           START BM06K110 KEY = K110-AIX1 INVALID                               
                 DISPLAY 'START ERROR, BM06K110-STATUS ' K110-STATUS            
                 DISPLAY 'K110-AIX1 ' K110-AIX1                                 
                 MOVE 'Y' TO WS-EOF-K110.                                       
                                                                                
       431-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *                                                                         
      * IF K270-NUM-ALLOC = ZEROS, DO NOT OUTPUT REC FOR THE FLAT TYPE          
      *                                                                         
      *-----------------------------------------------------------              
       432-READ-K110.                                                           
      *-----------------------------------------------------------              
           READ BM06K110 NEXT AT END                                            
                MOVE 'Y' TO WS-EOF-K110                                         
                GO TO 432-EXIT.                                                 
                                                                                
           IF K110-AIX1 = WS-K110-AIX1                                          
              CONTINUE                                                          
           ELSE                                                                 
              MOVE 'Y' TO WS-EOF-K110                                           
              GO TO 432-EXIT.                                                   
                                                                                
           IF (K110-DTE-ALLOCN     = K270-ALLOCN-DATE)   AND                    
              (K110-NUM-SELECTION  = F208-NUM-SELTY)     AND                    
              (K110-NUM-ALLOCN-QTR = F208-DTE-BALLOT)                           
              CONTINUE                                                          
           ELSE                                                                 
              GO TO 432-EXIT.                                                   
                                                                                
           IF K110-FLAT-STATUS = 'SD'                                           
              ADD 1 TO WS-READ-CNT-SD                                           
              GO TO 432-EXIT.                                                   
           MOVE SPACES               TO BP13COMM8-REC.                          
           INITIALIZE                   BP13COMM8-REC.                          
                                                                                
           MOVE K110-SCH-ACC-NO      TO WS-LINK-REC.                            
                                                                                
           CALL 'BP13C913'  USING WS-LINK-REC,                                  
                                  BP13COMM8-REC.                                
                                                                                
                                                                                
           IF (WS-NUM-FLAT-TYPE = '1A' OR '2A')                                 
              IF (K270-NUM-COMBINE-SA = 'Y') AND                                
                 (COMM8-CDE-TYP-ACTLUSE = '01' OR '02') AND                     
                 (COMM8-CDE-MODL-DESG = '80' OR '89' OR '92')                   
                  CONTINUE                                                      
              ELSE                                                              
                 IF ((WS-NUM-FLAT-TYPE = '1A') AND                              
                     (COMM8-CDE-TYP-ACTLUSE = '01') AND                         
                     (COMM8-CDE-MODL-DESG = '80' OR '89' OR '92')) OR           
                    ((WS-NUM-FLAT-TYPE = '2A') AND                              
                     (COMM8-CDE-TYP-ACTLUSE = '02') AND                         
                     (COMM8-CDE-MODL-DESG = '80' OR '89' OR '92'))              
                      CONTINUE                                                  
                 ELSE                                                           
                    GO TO 432-EXIT                                              
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              IF COMM8-CDE-TYP-ACTLUSE = WS-NUM-FLAT-TYPE                       
                 CONTINUE                                                       
              ELSE                                                              
                 GO TO 432-EXIT                                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
      **---> GET UPGRADING STATUS                                               
           MOVE SPACES      TO LINK-AREA.                                       
           INITIALIZE          LINK-AREA.                                       
           MOVE K110-SCH-ACC-NO(1:4) TO LINK-NUM-SCHM.                          
           MOVE K110-SCH-ACC-NO(5:7) TO LINK-NUM-ACCNT.                         
                                                                                
           MOVE SPACES        TO WS-NUM-UPG-STATUS.                             
           CALL 'DJ02C906' USING LINK-AREA.                                     
                                                                                
           IF LINK-SQL-RETCODE = 0                                              
              MOVE LINK-NUM-BNDRY-STATUS  TO WS-NUM-UPG-STATUS                  
           ELSE                                                                 
              IF LINK-SQL-RETCODE = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PIDB : '                         
                                                K110-SCH-ACC-NO(1:11)           
              ELSE                                                              
                 MOVE LINK-SQL-RETCODE TO WS-SQL-RETCODE                        
                 DISPLAY 'RETCODE : ' WS-SQL-RETCODE                            
                 DISPLAY 'ERROR IN CALLING DJ02C906'.                           
                                                                                
           IF (K110-DTE-ACCEPTANCE = ZEROS OR SPACES OR LOW-VALUES) AND         
              (K110-DTE-KEY-ISSUED  = ZEROS OR SPACES OR LOW-VALUES)            
               CONTINUE                                                         
           ELSE                                                                 
              ADD 1 TO WS-READ-CNT-AS                                           
              GO TO 432-EXIT.                                                   
                                                                                
           IF K110-ALLOCN-TAG NOT = SPACES AND LOW-VALUES                       
              ADD 1 TO WS-READ-CNT-HOLD                                         
              GO TO 432-EXIT.                                                   
                                                                                
           IF K110-FLAT-STATUS = 'RP' OR 'RR'                                   
              ADD   1  TO WS-READ-CNT-RP.                                       
                                                                                
           ADD 1 TO WS-READ-CNT-K110.                                           
                                                                                
           PERFORM 425-DECODE-K110-FLAT-TYPE THRU 425-EXIT.                     
                                                                                
           IF WS-K110-FLAT-TYPE = WS-NUM-ALLOC1 OR WS-NUM-ALLOC2 OR             
                                  WS-NUM-ALLOC3 OR WS-NUM-ALLOC4 OR             
                                  WS-NUM-ALLOC5 OR WS-NUM-ALLOC6 OR             
                                  WS-NUM-ALLOC7 OR WS-NUM-ALLOC8 OR             
                                  WS-NUM-ALLOC9                                 
              ADD 1   TO WS-SKIP-RECORD                                         
              GO TO 432-EXIT.                                                   
                                                                                
           PERFORM 600-WRITE-F800 THRU 600-EXIT.                                
                                                                                
       432-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       600-WRITE-F800.                                                          
      *-----------------------------------------------------------              
           MOVE SPACES                 TO F800-REC.                             
           INITIALIZE F800-REC.                                                 
                                                                                
           IF K110-REGN-NO(1:2) = '##'                                          
               MOVE SPACES TO K110-REGN-NO                                      
           END-IF.                                                              
                                                                                
           MOVE K110-REC               TO F800-REC (1:500).                     
                                                                                
           MOVE COMM8-NME-STREET       TO F800-STREET-NAME.                     
           MOVE COMM8-NUM-POSTAL-CODE  TO F800-POSTAL-CODE.                     
           MOVE COMM8-NUM-LEVEL        TO F800-LEVEL-NO.                        
           MOVE COMM8-NUM-UNIT-MAIN    TO F800-MAIN-UNIT.                       
           MOVE COMM8-NUM-UNIT-SUB     TO F800-SUB-UNIT.                        
           MOVE COMM8-CDE-BUC          TO F800-CDE-BUC.                         
           MOVE COMM8-QTY-AREA-TOTAL   TO F800-QTY-AREA-TOTAL.                  
           MOVE COMM8-QTY-AREA-ORGNL   TO F800-QTY-AREA-ORGNL.                  
           MOVE K270-CDE-NT            TO F800-CDE-NT.                          
           IF K270-CDE-SELTY = 'BTO' OR 'WIS' OR 'WIT' OR 'WIH' OR              
                               'WIP' OR 'WIA' OR 'WIB' OR 'WIC' OR              
                               'BE ' OR 'SBF'                                   
              MOVE SPACES             TO F800-CDE-ZONE                          
           ELSE                                                                 
              MOVE K270-CDE-APPL-ZONE TO F800-CDE-ZONE                          
           END-IF.                                                              
           MOVE K270-CDE-FLAT-STATUS   TO F800-CDE-FLAT-STATUS.                 
           MOVE K270-CDE-SELTY         TO F800-CDE-SELTY.                       
           MOVE K270-NUM-SITE          TO F800-NUM-SITE.                        
           MOVE K270-NUM-BONUS         TO F800-NUM-BONUS.                       
           IF (COMM8-CDE-TYP-ACTLUSE = '01' OR '02') AND                        
              (COMM8-CDE-MODL-DESG = '80' OR '89' OR '92')                      
               MOVE K270-NUM-COMBINE-SA TO F800-NUM-COMBINE-SA                  
           END-IF.                                                              
           MOVE F208-DTE-BALLOT        TO F800-DTE-BALLOT.                      
           MOVE WS-NUM-UPG-STATUS      TO F800-NUM-UPG-STATUS.                  
                                                                                
           EVALUATE F800-CDE-TYP-ACTUSE                                         
              WHEN '01'                                                         
                IF F800-CDE-MODL-DESG = '80' OR '89' OR '92'                    
                   MOVE '1A' TO F800-NUM-FT                                     
                ELSE                                                            
                   MOVE '01' TO F800-NUM-FT                                     
                END-IF                                                          
              WHEN '02'                                                         
                IF F800-CDE-MODL-DESG = '80' OR '89' OR '92'                    
                   MOVE '2A' TO F800-NUM-FT                                     
                ELSE                                                            
                   MOVE '02' TO F800-NUM-FT                                     
                END-IF                                                          
              WHEN '03'                                                         
                MOVE '03' TO F800-NUM-FT                                        
              WHEN '04'                                                         
                MOVE '04' TO F800-NUM-FT                                        
              WHEN '05'                                                         
                MOVE '05' TO F800-NUM-FT                                        
              WHEN '06'                                                         
                MOVE '06' TO F800-NUM-FT                                        
           END-EVALUATE.                                                        
                                                                                
           WRITE F800-REC.                                                      
           ADD 1 TO WS-WRITE-CNT-F800.                                          
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       999-CLOSE-ROUTINE.                                                       
      *-----------------------------------------------------------              
           DISPLAY '*----------  BP13C22W SUMMARY LIST  ----------*'.           
           DISPLAY ' NO OF BP13F208 READ    = ' WS-READ-CNT-CTL.                
           DISPLAY ' NO OF BP13K270 READ    = ' WS-READ-CNT-K270.               
           DISPLAY ' NO OF BM06K110 READ    = ' WS-READ-CNT-K110.               
           DISPLAY ' NO OF BM06F800 WRITE   = ' WS-WRITE-CNT-F800.              
           DISPLAY ' REC NOT FOR ALLOCN     = ' WS-SKIP-RECORD.                 
           DISPLAY ' NO OF RECORD AS        = ' WS-READ-CNT-AS.                 
           DISPLAY ' NO OF RECORD SD        = ' WS-READ-CNT-SD.                 
           DISPLAY ' REC FREEZE FR ALLOCN   = ' WS-READ-CNT-HOLD.               
           DISPLAY ' NO OF RECORD RP        = ' WS-READ-CNT-RP.                 
                                                                                
           CLOSE BP13F208                                                       
                 BM06F800.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
