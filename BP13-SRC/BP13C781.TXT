       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C781.                                                 
       AUTHOR.        RAYATHATHA ASHOK.                                         
       DATE-WRITTEN.  06/06/92.                                                 
       DATE-COMPILED.                                                           
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  TO UPDATE RESERVE REQUEST INFO TO MAIN RESERVE*                
      *          LIST.                                         *                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *      1  BP13F780 - UNSUCCESSFUL RESERVE REQUESTS       *                
      *                                                        *                
      *     OUTPUT FILES :                                     *                
      *      1  BP13K785 - RESERVE REQUEST DATE                *                
      *                                                        *                
      * CHGE    BY    DATE   DESCRIPTION                       *                
      * ------- ----  ------ -----------------                 *                
      * N930012 YPH   260693 SOC PH8 - COBOL 2                 *                
      *BP130652 MCC   230999 EXPAND F780 AND F785 RECL FROM    *                
      *                      300 TO 500.                       *                
      **********************************************************                
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
      *                                                                         
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F780 ASSIGN TO BP13F780.                                  
                                                                                
           SELECT BP13K785 ASSIGN TO BP13K785                                   
                     ORGANIZATION IS INDEXED                                    
                     ACCESS MODE  IS DYNAMIC                                    
                     RECORD KEY   IS K785-KEY-FLD                               
                     FILE STATUS  IS WS-K785-STATUS.                            
                                                                                
      *                                                                         
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *                                                                         
       FD   BP13K785                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
                                                                                
       COPY BP13K785.                                                           
      *                                                                         
       FD   BP13F780                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
                                                                                
       COPY BP13F780.                                                           
      *                                                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-EOF-F780             PIC X       VALUE 'N'.                       
           88  EOF-F780                        VALUE 'Y'.                       
       01  WS-K785-STATUS          PIC 9(2)    VALUE ZEROS.                     
       01  WS-K785-CNT             PIC 9(6)    VALUE ZEROS.                     
       01  WS-REWRITE              PIC 9(6)    VALUE ZEROS.                     
       01  WS-F780-CNT             PIC 9(6)    VALUE ZEROS.                     
                                                                                
       01  WS-K785-KEY.                                                         
           05 WS-NUM-RANDOM        PIC X(28)   VALUE SPACES.                    
           05 WS-NUM-SEQ-REF       PIC X(6)    VALUE SPACES.                    
           05 WS-NUM-REF           PIC X(6)    VALUE SPACES.                    
           05 WS-ALT1-KEY          PIC X(9)    VALUE SPACES.                    
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-LOGIC.                                                         
           PERFORM 1000-OPEN-ROUTINE  THRU 1000-EXIT.                           
           PERFORM 2000-READ-F780     THRU 2000-EXIT.                           
           PERFORM 3000-PROCESS-K785  THRU 3000-EXIT                            
                       UNTIL EOF-F780.                                          
           PERFORM 4000-CLOSE-ROUTINE THRU 4000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPEN-ROUTINE.                                                       
           OPEN INPUT  BP13F780                                                 
                  I-O  BP13K785.                                                
                                                                                
           IF WS-K785-STATUS NOT = 00 AND 97                                    
              DISPLAY ' ERROR OPENING BP13K785, STATUS ' WS-K785-STATUS         
              MOVE WS-K785-STATUS        TO RETURN-CODE                         
              PERFORM 4000-CLOSE-ROUTINE THRU 4000-EXIT.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-F780.                                                          
           READ BP13F780                 AT END                                 
           MOVE 'Y'                      TO WS-EOF-F780                         
           GO TO 2000-EXIT.                                                     
                                                                                
           ADD 1                         TO WS-F780-CNT.                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-PROCESS-K785.                                                       
                                                                                
           MOVE SPACES                   TO K785-KEY-FLD.                       
           MOVE F780-NUM-RANDOM          TO WS-NUM-RANDOM.                      
           MOVE F780-NUM-SEQ-REF         TO WS-NUM-SEQ-REF.                     
           MOVE F780-NUM-REF             TO WS-NUM-REF.                         
           MOVE F780-NUM-NRIC1           TO WS-ALT1-KEY.                        
           MOVE WS-K785-KEY              TO K785-KEY-FLD.                       
                                                                                
           DISPLAY 'F780-NUM-NRIC1  '  F780-NUM-NRIC1.                          
           READ BP13K785.                                                       
                                                                                
           IF WS-K785-STATUS = 00                                               
              PERFORM 3500-REWRITE-K785  THRU 3500-EXIT                         
           ELSE                                                                 
              IF WS-K785-STATUS = 23                                            
                 DISPLAY ' RECORD NOT FOUND IN BP13K785, STATUS '               
                 WS-K785-STATUS ' RECORD KEY IS ' K785-KEY-FLD                  
                 MOVE WS-K785-STATUS        TO RETURN-CODE                      
              ELSE                                                              
                 DISPLAY ' ERROR READING BP13K785, STATUS '                     
                         WS-K785-STATUS                                         
                 MOVE WS-K785-STATUS        TO RETURN-CODE                      
                 PERFORM 4000-CLOSE-ROUTINE THRU 4000-EXIT.                     
                                                                                
           PERFORM 2000-READ-F780    THRU 2000-EXIT.                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3500-REWRITE-K785.                                                       
                                                                                
              ADD 1                      TO WS-K785-CNT.                        
              MOVE BP13F780-REC          TO BP13K785-REC.                       
              REWRITE BP13K785-REC.                                             
              IF WS-K785-STATUS = 00                                            
                 ADD 1                   TO WS-REWRITE                          
              ELSE                                                              
                 DISPLAY ' ERROR REWRITING BP13K785, STATUS '                   
                         WS-K785-STATUS                                         
                 MOVE WS-K785-STATUS        TO RETURN-CODE.                     
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-CLOSE-ROUTINE.                                                      
           DISPLAY ' *** CONTROL COUNT FOR BP13C781 ***'.                       
           DISPLAY 'TOTAL NO READ FROM BP13F780   = ' WS-F780-CNT.              
           DISPLAY 'TOTAL NO READ FROM BP13K785   = ' WS-K785-CNT.              
           DISPLAY 'TOTAL NO REWRITTEN TO BP13K785= ' WS-REWRITE.               
                                                                                
           CLOSE BP13K785                                                       
                 BP13F780.                                                      
                                                                                
           IF WS-K785-STATUS NOT = 00 AND 97                                    
              DISPLAY ' ERROR CLOSING BP13K785, STATUS ' WS-K785-STATUS         
              MOVE WS-K785-STATUS        TO RETURN-CODE.                        
                                                                                
           STOP RUN.                                                            
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
