      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C108.                                                 
      *AUTHOR.        LSB.                                                      
      *DATE-WRITTEN.  10/03/2005.                                               
                                                                                
      * ======================================================= *               
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                   *               
      *    SYSTEM ID : BP13                                     *               
      *                                                         *               
      *    OBJECTIVE  :                                         *               
      *        1.  AFTER KEY-ISSUED OF SA, IS MONEY DID NOT     *               
      *            COME IN ON THE SAME DAY, THIS PROGRAM WILL   *               
      *            WHEN THE MONEY COME IN, BP13C104 WILL UPDATE *               
      *            SOC-CASH, SOC-DEBTOR= ZERO, POST OUT SOC-AMT *               
      *            AND GENERATE GL TRANSACTION RECORDS.         *               
      *                                                         *               
      *     INPUT/OUTPUT FILES:                                 *               
      *              BP13K800 (APPLICANT MASTER)                *               
      *              BP13F120 (SOC HISTORY MASTER)              *               
      *                                                         *               
      * ======================================================= *               
      *                                                         *               
      * AMENDMENTS:                                             *               
      * CHG NO.   BY    DATE        DESCRIPTION                 *               
      * --------  ----  ----------  --------------------------- *               
      * BP132698  LSB1  11/03/2005  NEW PROGRAM                 *               
      * ======================================================= *               
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
      *------------------------*                                                
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *------------------------*                                                
       INPUT-OUTPUT SECTION.                                                    
      *------------------------*                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS DYNAMIC                                       
                  RECORD KEY   IS K800-NUM-REGN                                 
                  FILE STATUS  IS WS-K800-STATUS.                               
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K110-KEY-FLD                                  
                  FILE STATUS  IS WS-K110-STATUS.                               
                                                                                
           SELECT BP13F120 ASSIGN TO BP13F120.                                  
                                                                                
           SELECT BP13L108 ASSIGN TO BP13L108.                                  
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
      *------------------------*                                                
                                                                                
       FD   BP13F800           BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 2000 CHARACTERS                  
                               LABEL RECORDS ARE STANDARD                       
                               RECORDING MODE IS F.                             
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K800           RECORD CONTAINS 2000 CHARACTERS.                 
       COPY BP13K800.                                                           
                                                                                
       FD   BM06K110           RECORD CONTAINS 500 CHARACTERS.                  
       COPY BM06K110.                                                           
                                                                                
       FD   BP13F120           BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 150 CHARACTERS                   
                               LABEL RECORDS ARE STANDARD                       
                               RECORDING MODE IS F.                             
       COPY BP13F120.                                                           
                                                                                
       FD   BP13L108           BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 132 CHARACTERS                   
                               LABEL RECORDS ARE STANDARD                       
                               RECORDING MODE IS F.                             
       01   BP13L108-REC                 PIC X(132).                            
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01  WS-RPT-HDR1.                                                         
           05  FILLER                    PIC X(08)  VALUE 'BP13L108'.           
           05  FILLER                    PIC X(39)  VALUE SPACES.               
           05  FILLER                    PIC X(37)  VALUE                       
               'S Y S T E M  O F  C O M M I T M E N T'.                         
           05  FILLER                    PIC X(31)   VALUE SPACES.              
           05  FILLER                    PIC X(07)   VALUE 'DATE : '.           
           05  WS-HDR-DATE               PIC X(10)   VALUE SPACES.              
                                                                                
       01  WS-RPT-HDR2.                                                         
           05  FILLER                    PIC X(35)   VALUE SPACES.              
           05  FILLER                    PIC X(67)   VALUE                      
               'REPORT ON STUDIO APARTMENT CASE DEBTOR PAID AFTER KEY           
      -        'ISSUED DATE'.                                                   
           05  FILLER                    PIC X(18)   VALUE SPACES.              
           05  FILLER                    PIC X(07)   VALUE 'PAGE : '.           
           05  WS-HDR-PAGE               PIC ZZZZ9.                             
           05  FILLER                    PIC X(05)   VALUE SPACES.              
                                                                                
       01  WS-BLANK-LINE                 PIC X(132)  VALUE SPACES.              
                                                                                
       01  WS-DTL-TITLE.                                                        
           05  FILLER                    PIC X(05)   VALUE SPACES.              
           05  FILLER                    PIC X(05)   VALUE 'S/NO.'.             
           05  FILLER                    PIC X(02)   VALUE SPACES.              
           05  FILLER                    PIC X(08)   VALUE 'REGN NO.'.          
           05  FILLER                    PIC X(02)   VALUE SPACES.              
           05  FILLER                    PIC X(02)   VALUE 'FT'.                
           05  FILLER                    PIC X(02)   VALUE SPACES.              
           05  FILLER                    PIC X(02)   VALUE 'NT'.                
           05  FILLER                    PIC X(03)   VALUE SPACES.              
           05  FILLER                    PIC X(04)   VALUE 'ASCH'.              
           05  FILLER                    PIC X(02)   VALUE SPACES.              
           05  FILLER                    PIC X(04)   VALUE 'ACAT'.              
           05  FILLER                    PIC X(06)   VALUE SPACES.              
           05  FILLER                    PIC X(02)   VALUE 'KI'.                
           05  FILLER                    PIC X(06)   VALUE SPACES.              
           05  FILLER                    PIC X(12)   VALUE                      
                                                     'BOOKING PAID'.            
           05  FILLER                    PIC X(04)   VALUE SPACES.              
           05  FILLER                    PIC X(09)   VALUE 'AMOUNT CD'.         
           05  FILLER                    PIC X(05)   VALUE SPACES.              
           05  FILLER                    PIC X(10)   VALUE 'SOC DEBTOR'.        
           05  FILLER                    PIC X(06)   VALUE SPACES.              
           05  FILLER                    PIC X(08)   VALUE 'SOC CASH'.          
           05  FILLER                    PIC X(06)   VALUE SPACES.              
           05  FILLER                    PIC X(10)   VALUE 'AMOUNT CPF'.        
           05  FILLER                    PIC X(07)   VALUE SPACES.              
                                                                                
       01  WS-DTL-INFO.                                                         
           05  FILLER                    PIC X(05)   VALUE SPACES.              
           05  WS-SEQ-NO                 PIC ZZZZ9.                             
           05  FILLER                    PIC X(02)   VALUE SPACES.              
           05  WS-REGN-NO                PIC X(08)   VALUE SPACES.              
           05  FILLER                    PIC X(02)   VALUE SPACES.              
           05  WS-FT                     PIC X(02)   VALUE SPACES.              
           05  FILLER                    PIC X(02)   VALUE SPACES.              
           05  WS-NT                     PIC X(02)   VALUE SPACES.              
           05  FILLER                    PIC X(02)   VALUE SPACES.              
           05  WS-ASCH                   PIC X(03)   VALUE SPACES.              
           05  FILLER                    PIC X(03)   VALUE SPACES.              
           05  WS-ACAT                   PIC X(03)   VALUE SPACES.              
           05  FILLER                    PIC X(03)   VALUE SPACES.              
           05  WS-KI                     PIC X(10)   VALUE SPACES.              
           05  FILLER                    PIC X(02)   VALUE SPACES.              
           05  WS-BOOKING-PAID           PIC $$,$$9.99.                         
           05  FILLER                    PIC X(02)   VALUE SPACES.              
           05  WS-AMOUNT-CD              PIC $$,$$$,$$9.99.                     
           05  FILLER                    PIC X(02)   VALUE SPACES.              
           05  WS-SOC-DEBTOR             PIC $$,$$$,$$9.99.                     
           05  FILLER                    PIC X(02)   VALUE SPACES.              
           05  WS-SOC-CASH               PIC $$,$$$,$$9.99.                     
           05  FILLER                    PIC X(02)   VALUE SPACES.              
           05  WS-CPF                    PIC $$,$$$,$$9.99.                     
           05  FILLER                    PIC X(05)   VALUE SPACES.              
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K800-STATUS            PIC 9(2)    VALUE ZEROES.              
           05  WS-K110-STATUS            PIC 9(2)    VALUE ZEROES.              
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F800-READ              PIC 9(6)    VALUE ZEROES.              
           05  WS-K800-FND               PIC 9(6)    VALUE ZEROES.              
           05  WS-K800-NTFND             PIC 9(6)    VALUE ZEROES.              
           05  WS-K110-FND               PIC 9(6)    VALUE ZEROES.              
           05  WS-K110-NTFND             PIC 9(6)    VALUE ZEROES.              
           05  WS-K800-REWRT             PIC 9(6)    VALUE ZEROES.              
           05  WS-F120-WRITE             PIC 9(6)    VALUE ZEROES.              
           05  WS-L108-WRITE             PIC 9(6)    VALUE ZEROES.              
           05  WS-L108-PAGE-CNT          PIC 9(5)    VALUE ZEROES.              
           05  WS-L108-LINE-CNT          PIC 9(2)    VALUE ZEROES.              
           05  WS-SEQ-NO-CNT             PIC 9(5)    VALUE ZEROES.              
                                                                                
       01  WS-PROGRAM-SWITCHES.                                                 
           05  WS-EOF-F800               PIC X       VALUE 'N'.                 
           05  WS-K800-REC-FND           PIC X       VALUE 'N'.                 
           05  WS-K110-REC-FND           PIC X       VALUE 'N'.                 
                                                                                
       01  WS-CPF-TOTAL                  PIC S9(7)V99 VALUE ZEROES.             
                                                                                
       01  WS-DATE.                                                             
           05  WS-CC                     PIC X(2)    VALUE SPACES.              
           05  WS-YY                     PIC X(2)    VALUE SPACES.              
           05  WS-MM                     PIC X(2)    VALUE SPACES.              
           05  WS-DD                     PIC X(2)    VALUE SPACES.              
                                                                                
       01  WS-NUM-RECEIPT-JRNO.                                                 
           05  WS-RECEIPT-CD             PIC X(2)    VALUE SPACES.              
           05  WS-RECEIPT-DATE.                                                 
               10  WS-RECEIPT-DD         PIC 9(2)    VALUE ZEROES.              
               10  WS-RECEIPT-MM         PIC 9(2)    VALUE ZEROES.              
               10  WS-SLASH              PIC X       VALUE SPACE.               
               10  WS-RECEIPT-YY         PIC 9(2)    VALUE ZEROES.              
           05  WS-NUM-DOC                PIC 9(5)    VALUE ZEROES.              
                                                                                
                                                                                
      *------------------------*                                                
       PROCEDURE DIVISION.                                                      
      *------------------------*                                                
                                                                                
      *------------------------*                                                
       000-MAIN.                                                                
      *------------------------*                                                
           PERFORM 100-START-PROCESS  THRU 100-EXIT.                            
           PERFORM 200-READ-BP13F800  THRU 200-EXIT.                            
           PERFORM 300-PROCESS-UPDATE THRU 300-EXIT                             
             UNTIL WS-EOF-F800 = 'Y'.                                           
           PERFORM 999-END-PROCESS    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       100-START-PROCESS.                                                       
      *------------------------*                                                
           OPEN INPUT  BP13F800                                                 
                       BM06K110                                                 
                I-O    BP13K800                                                 
                OUTPUT BP13F120                                                 
                       BP13L108.                                                
                                                                                
           IF WS-K800-STATUS NOT = 0 AND 97                                     
              DISPLAY 'ERROR OPENNING BP13K800, STATUS : '                      
                      WS-K800-STATUS                                            
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 999-END-PROCESS THRU 999-EXIT                             
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 0 AND 97                                     
              DISPLAY 'ERROR OPENNING BM06K110, STATUS : '                      
                      WS-K110-STATUS                                            
              MOVE WS-K110-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 999-END-PROCESS THRU 999-EXIT                             
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-DATE.                            
                                                                                
           STRING WS-DD '/'                                                     
                  WS-MM '/'                                                     
                  WS-CC                                                         
                  WS-YY                                                         
                  DELIMITED BY SIZE                                             
             INTO WS-HDR-DATE.                                                  
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *             INITIAL READING OF BP13F800 FILE                   *        
      ******************************************************************        
      *------------------------*                                                
       200-READ-BP13F800.                                                       
      *------------------------*                                                
           READ BP13F800 AT END                                                 
                         MOVE 'Y' TO WS-EOF-F800                                
                         GO TO 200-EXIT.                                        
                                                                                
           ADD 1 TO WS-F800-READ.                                               
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       300-PROCESS-UPDATE.                                                      
      *------------------------*                                                
           PERFORM 310-READ-BP13K800 THRU 310-EXIT.                             
                                                                                
           IF WS-K800-REC-FND = 'Y' AND WS-CPF-TOTAL = ZEROS                    
               PERFORM 330-PROCESS-SOC-CASH THRU 330-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-BP13F800 THRU 200-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    USE THE REG  NUMBER IN BP13F800 FILE TO ACCESS THE RECORDS  *        
      *    IN BP13K800 FILE.                                           *        
      ******************************************************************        
      *------------------------*                                                
       310-READ-BP13K800.                                                       
      *------------------------*                                                
           MOVE 'N'                      TO WS-K800-REC-FND.                    
           MOVE F800-NUM-REGN            TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    ADD 1                TO WS-K800-FND                         
                    MOVE 'Y'             TO WS-K800-REC-FND                     
                    MOVE SPACES          TO WS-DTL-INFO                         
                    INITIALIZE              WS-DTL-INFO                         
                                                                                
                    PERFORM 315-MOVE-K800-VALUES THRU 315-EXIT                  
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1                TO WS-K800-NTFND                       
                    DISPLAY 'REC NOT FOUND IN BP13K800 ' F800-NUM-REGN          
                                                                                
                    MOVE 'N'             TO WS-K800-REC-FND                     
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K800, STATUS : '                 
                            WS-K800-STATUS                                      
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 999-END-PROCESS THRU 999-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       315-MOVE-K800-VALUES.                                                    
      *------------------------*                                                
           MOVE K800-NUM-REGN             TO WS-REGN-NO.                        
           MOVE K800-NUM-FLAT-TYPE        TO WS-FT.                             
           MOVE K800-NUM-NEW-TOWN         TO WS-NT.                             
           MOVE K800-NUM-ALLO-SCHEME      TO WS-ASCH.                           
           MOVE K800-NUM-ALLO-CAT         TO WS-ACAT.                           
                                                                                
           IF K800-AMT-BK-PAID NOT NUMERIC                                      
              MOVE ZEROES                 TO WS-BOOKING-PAID                    
                                             K800-AMT-BK-PAID                   
           ELSE                                                                 
              MOVE K800-AMT-BK-PAID       TO WS-BOOKING-PAID                    
           END-IF.                                                              
                                                                                
           IF K800-AMT-CD NOT NUMERIC                                           
              MOVE ZEROES                 TO WS-AMOUNT-CD                       
                                             K800-AMT-CD                        
           ELSE                                                                 
              MOVE K800-AMT-CD            TO WS-AMOUNT-CD                       
           END-IF.                                                              
                                                                                
           IF K800-AMT-SOC-DEBTOR NOT NUMERIC                                   
              MOVE ZEROES                 TO WS-SOC-DEBTOR                      
                                             K800-AMT-SOC-DEBTOR                
           ELSE                                                                 
              MOVE K800-AMT-SOC-DEBTOR    TO WS-SOC-DEBTOR                      
           END-IF.                                                              
                                                                                
           IF K800-AMT-SOC-CASH NOT NUMERIC                                     
              MOVE ZEROES                 TO WS-SOC-CASH                        
                                             K800-AMT-SOC-CASH                  
           ELSE                                                                 
              MOVE K800-AMT-SOC-CASH      TO WS-SOC-CASH                        
           END-IF.                                                              
                                                                                
           IF K800-AMT-CPF1-PAID NOT NUMERIC                                    
              MOVE ZEROES                 TO K800-AMT-CPF1-PAID                 
           END-IF.                                                              
                                                                                
           IF K800-AMT-CPF2-PAID NOT NUMERIC                                    
              MOVE ZEROES                 TO K800-AMT-CPF2-PAID                 
           END-IF.                                                              
                                                                                
           IF K800-AMT-CPF3-PAID NOT NUMERIC                                    
              MOVE ZEROES                 TO K800-AMT-CPF3-PAID                 
           END-IF.                                                              
                                                                                
           IF K800-AMT-CPF4-PAID NOT NUMERIC                                    
              MOVE ZEROES                 TO K800-AMT-CPF4-PAID                 
           END-IF.                                                              
                                                                                
           MOVE ZEROES                    TO WS-CPF-TOTAL.                      
                                                                                
           COMPUTE WS-CPF-TOTAL = K800-AMT-CPF1-PAID +                          
                                  K800-AMT-CPF2-PAID +                          
                                  K800-AMT-CPF3-PAID +                          
                                  K800-AMT-CPF4-PAID.                           
                                                                                
           MOVE WS-CPF-TOTAL              TO WS-CPF.                            
                                                                                
       315-EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       330-PROCESS-SOC-CASH.                                                    
      *------------------------*                                                
           IF K800-AMT-SOC-CASH IS NOT NUMERIC                                  
              MOVE ZEROES TO K800-AMT-SOC-CASH                                  
           END-IF.                                                              
                                                                                
           IF K800-AMT-SOC-CASH > 0 AND K800-AMT-SOC-DEBTOR = 0                 
              PERFORM 350-READ-BM06K110 THRU 350-EXIT                           
                                                                                
              IF WS-K110-REC-FND = 'Y'                                          
                 IF K110-DTE-KEY-ISSUED > ZEROES AND                            
                    K110-DTE-KEY-ISSUED < WS-DATE                               
                    PERFORM 370-PROCESS-SOC-PAID THRU 370-EXIT                  
                    PERFORM 400-REWRITE-BP13K800 THRU 400-EXIT                  
                    PERFORM 500-PRINT-REPORT THRU 500-EXIT                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       330-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       350-READ-BM06K110.                                                       
      *------------------------*                                                
           MOVE 'N'                      TO WS-K110-REC-FND.                    
           MOVE SPACES                   TO K110-REC.                           
           INITIALIZE                       K110-REC.                           
                                                                                
           MOVE K800-NUM-SCH-ACC         TO K110-KEY-FLD.                       
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
                    ADD 1                TO WS-K110-FND                         
                    MOVE 'Y'             TO WS-K110-REC-FND                     
                    DISPLAY 'K110 = ' K110-KEY-FLD                              
                            ' KI= ' K110-DTE-KEY-ISSUED                         
                                                                                
                    IF K110-DTE-KEY-ISSUED NOT NUMERIC                          
                       MOVE ZEROES       TO K110-DTE-KEY-ISSUED                 
                    END-IF                                                      
                                                                                
                    IF K110-DTE-KEY-ISSUED = SPACES OR LOW-VALUES OR            
                                             '00000000'                         
                       MOVE '00/00/0000'           TO WS-KI                     
                    ELSE                                                        
                       STRING K110-DTE-KEY-ISSUED(7:2) '/'                      
                              K110-DTE-KEY-ISSUED(5:2) '/'                      
                              K110-DTE-KEY-ISSUED(1:4)                          
                              DELIMITED BY SIZE                                 
                         INTO WS-KI                                             
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1                TO WS-K110-NTFND                       
                    MOVE 'N'             TO WS-K110-REC-FND                     
                                                                                
                    DISPLAY 'REC NOT FOUND IN BM06K110 '                        
                            K800-NUM-SCH-ACC                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BM06K110, STATUS : '                 
                            WS-K110-STATUS                                      
                    MOVE WS-K110-STATUS TO RETURN-CODE                          
                                                                                
                    PERFORM 999-END-PROCESS THRU 999-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       370-PROCESS-SOC-PAID.                                                    
      *------------------------*                                                
           MOVE SPACES                TO BP13F120-CASHTRAN.                     
           MOVE '20'                  TO F120-CDE-TRANS-TYPE.                   
           MOVE K800-NUM-REGN         TO F120-NUM-REGN.                         
           MOVE K800-NUM-NRIC1        TO F120-NUM-NRIC.                         
           MOVE WS-DATE               TO F120-DTE-POST.                         
           MOVE ZEROES                TO F120-COUNT-HISTORY                     
           MOVE SPACES                TO F120-NUM-CPF-APPL.                     
           MOVE SPACES                TO F120-NME-NAME.                         
           MOVE 'SOC'                 TO F120-CDE-PAYMENT-TYPE.                 
           MOVE WS-DD                 TO WS-RECEIPT-DD.                         
           MOVE WS-MM                 TO WS-RECEIPT-MM.                         
           MOVE WS-YY                 TO WS-RECEIPT-YY.                         
           MOVE 'JR'                  TO WS-RECEIPT-CD.                         
           MOVE '/'                   TO WS-SLASH.                              
           MOVE WS-NUM-RECEIPT-JRNO   TO F120-NUM-RECPT-JRNO.                   
                                                                                
           COMPUTE F120-AMT-RECEIPT = K800-AMT-SOC-CASH * -1                    
                                                                                
           MOVE 'D'                   TO F120-CDE-CRDR.                         
           MOVE WS-DATE               TO F120-DTE-TRANS.                        
           MOVE F800-NUM-SCH-ACC      TO F120-NUM-SCH-ACCT.                     
                                                                                
           WRITE BP13F120-CASHTRAN.                                             
                                                                                
           ADD  1                     TO WS-F120-WRITE.                         
                                                                                
       370-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       400-REWRITE-BP13K800.                                                    
      *------------------------*                                                
           COMPUTE K800-AMT-SOC-CASH  = ZEROES.                                 
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    ADD 1                TO WS-K800-REWRT                       
               WHEN OTHER                                                       
                    DISPLAY 'ERROR REWRITING BP13K800, STATUS : '               
                             WS-K800-STATUS                                     
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 999-END-PROCESS    THRU 999-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       500-PRINT-REPORT.                                                        
      *------------------------*                                                
           IF WS-L108-LINE-CNT > 58 OR WS-L108-LINE-CNT = 0                     
              ADD 1                      TO WS-L108-PAGE-CNT                    
              MOVE WS-L108-PAGE-CNT      TO WS-HDR-PAGE                         
                                                                                
              WRITE BP13L108-REC         FROM WS-RPT-HDR1 AFTER PAGE            
              WRITE BP13L108-REC         FROM WS-RPT-HDR2 AFTER 1               
              WRITE BP13L108-REC         FROM WS-BLANK-LINE AFTER 1             
              WRITE BP13L108-REC         FROM WS-DTL-TITLE AFTER 1              
              WRITE BP13L108-REC         FROM WS-BLANK-LINE AFTER 1             
                                                                                
              MOVE 5                     TO WS-L108-LINE-CNT                    
                                                                                
              PERFORM 510-WRITE-REPORT THRU 510-EXIT                            
           ELSE                                                                 
              PERFORM 510-WRITE-REPORT THRU 510-EXIT                            
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       510-WRITE-REPORT.                                                        
      *------------------------*                                                
           ADD 1                         TO WS-SEQ-NO-CNT.                      
           MOVE WS-SEQ-NO-CNT            TO WS-SEQ-NO.                          
                                                                                
           WRITE BP13L108-REC            FROM WS-DTL-INFO AFTER 1.              
                                                                                
           ADD 1                         TO WS-L108-LINE-CNT                    
                                            WS-L108-WRITE.                      
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       999-END-PROCESS.                                                         
      *------------------------*                                                
           DISPLAY ' '.                                                         
           DISPLAY 'RUN DATE : ' WS-HDR-DATE.                                   
           DISPLAY '================  BP13C108 ================='.              
           DISPLAY '  RECORDS READ FROM BP13F800      = ' WS-F800-READ.         
           DISPLAY '  RECORDS FOUND FROM BP13K800     = ' WS-K800-FND.          
           DISPLAY '  RECORDS NOT FOUND FROM BP13K800 = ' WS-K800-NTFND.        
           DISPLAY '  RECORDS FOUND FROM BM06K110     = ' WS-K110-FND.          
           DISPLAY '  RECORDS NOT FOUND FROM BM06K110 = ' WS-K110-NTFND.        
           DISPLAY '  RECORDS REWRITTEN TO BP13K800   = ' WS-K800-REWRT.        
           DISPLAY '  RECORDS WRITTEN TO BP13F120     = ' WS-F120-WRITE.        
           DISPLAY '  RECORDS WRITTEN TO BP13L108     = ' WS-L108-WRITE.        
           DISPLAY '============================================'.              
                                                                                
           CLOSE   BP13F800                                                     
                   BP13K800                                                     
                   BM06K110                                                     
                   BP13F120                                                     
                   BP13L108.                                                    
                                                                                
           IF WS-K800-STATUS NOT = 0 AND 97                                     
              DISPLAY 'CLOSING ERROR, K800-STATUS IS ' WS-K800-STATUS           
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 0 AND 97                                     
              DISPLAY 'CLOSING ERROR, K110-STATUS IS ' WS-K110-STATUS           
              MOVE WS-K110-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------------*                                          
      *         END OF PROGRAM       *                                          
      *------------------------------*                                          
