       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C214.                                                 
      *AUTHOR.        EDWARD S CRUZ.                                            
      *DATE-WRITTEN.  23FEB2001.                                                
      *========================================================*                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *========================================================*                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1.  TO CREATE INPUT FILE FOR BP13C215.              *                
      *    2.  TO EXTRACT K800 RECORDS IF ALLOC-TAG NOT = 'NI' *                
      *        OR SCH-ACC-NO = SPACES, NOT BOOKED CASES        *                
      *                                                        *                
      *    INPUT  FILE  : BP13F200 - BOOKING APPOINTMENT FILE  *                
      *                   BP13K800 - SOC MASTER FILE           *                
      *                   SY02F001 - PARAMETER FILE            *                
      *                                                        *                
      *    I/O    FILE  : NIL                                  *                
      *                                                        *                
      *    OUTPUT FILES : BP13F800 - SOC MASTER FILE           *                
      *                                                        *                
      *    LISTINGS     : NIL                                  *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY    DATE    DETAILS                          *                
      * ======  ==   ====== ===================================*                
      *BP130970 ED   230201 NEW PGM                            *                
      *BP131035 ED   130601 TO CHECK IF K800-DTE-BALLOT EQUAL  *                
      *                     TO F200-DTE-BALLOT                 *                
      *BP132095 MTD  220402 EXPAND LENGTH OF BP13F200          *                
      *                     (FROM 100 TO 300)                  *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F200 ASSIGN TO BP13F200.                                  
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K800-NUM-REGN                                 
                  FILE STATUS  IS WS-K800-STATUS.                               
                                                                                
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F200                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13F200.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13F800                                                            
            RECORDING MODE     F                                                
            BLOCK CONTAINS     0 RECORDS                                        
            LABEL RECORDS    ARE STANDARD                                       
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F800.                                                           
                                                                                
                                                                                
      *================================================================         
       WORKING-STORAGE SECTION.                                                 
      *================================================================         
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F200-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-K800-STATUS                 PIC 9(2)  VALUE ZEROS.            
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-CNT-READ-F200               PIC 9(5)  VALUE ZEROS.            
           05  WS-CNT-PROC-F200               PIC 9(5)  VALUE ZEROS.            
           05  WS-CNT-SKIP-F800               PIC 9(5)  VALUE ZEROS.            
           05  WS-CNT-WRIT-F800               PIC 9(5)  VALUE ZEROS.            
                                                                                
       01  WS-RUN-DATE.                                                         
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC              PIC 9(2).                         
                   15  WS-SYS-YY              PIC 9(2).                         
               10  WS-SYS-MM                  PIC 9(2).                         
               10  WS-SYS-DD                  PIC 9(2).                         
           05 WS-EDITED-DATE.                                                   
               10 WS-EDITED-DD            PIC 9(2).                             
               10 FILLER                  PIC X(1)  VALUE '/'.                  
               10 WS-EDITED-MM            PIC 9(2).                             
               10 FILLER                  PIC X(1)  VALUE '/'.                  
               10 WS-EDITED-YY            PIC 9(4).                             
                                                                                
       01  WS-MISC-VARS.                                                        
           05  WS-SY02-CURR-DATE              PIC X(8)  VALUE ZEROS.            
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
           PERFORM 2000-READ-SY02F001     THRU 2000-EXIT.                       
           PERFORM 2500-READ-F200         THRU 2500-EXIT.                       
                                                                                
           PERFORM 3000-PROCESS-F200                                            
              THRU 3000-EXIT                                                    
             UNTIL (WS-F200-EOF-SW = 'Y')  OR                                   
                   (F200-DTE-BKAPPMT-DATE > WS-SY02-CURR-DATE)                  
                                                                                
           PERFORM 9999-CLOSE-ROUTINE.                                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F200                                                 
                       SY02F001                                                 
                       BP13K800                                                 
                OUTPUT BP13F800.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 OPEN ERROR, STATUS IS ' WS-K800-STATUS          
              GO TO   9999-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYSTEM-DATE.                 
           MOVE   WS-SYS-DD           TO WS-EDITED-DD.                          
           MOVE   WS-SYS-MM           TO WS-EDITED-MM.                          
           MOVE   WS-SYS-CCYY         TO WS-EDITED-YY.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       2000-READ-SY02F001.                                                      
      ****************************************************************          
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT  TO WS-SY02-CURR-DATE.                         
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       2500-READ-F200.                                                          
      ****************************************************************          
                                                                                
           READ BP13F200                                                        
              AT END     MOVE 'Y'  TO WS-F200-EOF-SW.                           
                                                                                
           ADD +1                  TO WS-CNT-READ-F200.                         
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-F200.                                                       
      ****************************************************************          
                                                                                
           IF F200-DTE-BKAPPMT-DATE =  WS-SY02-CURR-DATE                        
                                                                                
              ADD +1                   TO WS-CNT-PROC-F200                      
              PERFORM 4000-READ-K800   THRU 4000-EXIT                           
              IF WS-K800-STATUS = 00                                            
                 IF (K800-NUM-SCH-ACC = SPACES OR LOW-VALUES) AND               
                    (K800-DTE-BALLOT = F200-DTE-BALLOT) AND                     
                    (K800-NUM-ALLOC-TAG = 'FN')                                 
                    PERFORM 4500-WRITE-F800  THRU 4500-EXIT                     
                 ELSE                                                           
                    ADD +1             TO WS-CNT-SKIP-F800                      
                 END-IF                                                         
              END-IF                                                            
                                                                                
           END-IF.                                                              
           PERFORM 2500-READ-F200      THRU 2500-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4000-READ-K800.                                                          
      ****************************************************************          
                                                                                
           MOVE F200-NUM-REGN  TO K800-NUM-REGN.                                
           READ BP13K800 KEY   IS K800-NUM-REGN.                                
                                                                                
           IF WS-K800-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
           IF WS-K800-STATUS = 23                                               
              DISPLAY 'BP13K800 RECORD NOT FOUND, KEY IS ' K800-NUM-REGN        
           ELSE                                                                 
              DISPLAY 'BP13K800 READ ERROR, STATUS IS ' WS-K800-STATUS          
              DISPLAY 'K800 KEY-FLD-' K800-NUM-REGN                             
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4500-WRITE-F800.                                                         
      ****************************************************************          
                                                                                
           INITIALIZE                 BP13F800-MASTER.                          
           MOVE  BP13K800-MASTER   TO BP13F800-MASTER.                          
           WRITE BP13F800-MASTER.                                               
                                                                                
           ADD +1                  TO WS-CNT-WRIT-F800.                         
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           CLOSE BP13F200                                                       
                 SY02F001                                                       
                 BP13K800                                                       
                 BP13F800.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : '  WS-EDITED-DATE.                               
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13C214 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F200 RECORDS READ        = ' WS-CNT-READ-F200.          
           DISPLAY 'BP13F200 RECORDS PROCESSED   = ' WS-CNT-PROC-F200.          
           DISPLAY 'BP13K800 RECORDS SKIPPED     = ' WS-CNT-SKIP-F800.          
           DISPLAY 'BP13F800 RECORDS WRITTEN     = ' WS-CNT-WRIT-F800.          
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           STOP RUN.                                                            
                                                                                
