       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C35B.                                                 
      *AUTHOR.        ALWYN BENNY.                                              
      *DATE-WRITTEN.  24/08/2012.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *  OBJECTIVE :                                               *            
      *                                                            *            
      *    TO SEND EF35B TO CSM WHEN COURT ORDER WITH NO CPF REFUND*            
      *    IS UPDATED IN SOC MASTER FILE.                          *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE(YMD)  BY    DESCRIPTION                      *            
      * -------- ---------- ----- -------------------------------- *            
      * BP134561 2012-08-24 AB9   NEW PROGRAM                      *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F800 ASSIGN        TO BP13F800.                           
                                                                                
           SELECT BP13K022  ASSIGN       TO BP13K022                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K022-KEY-FLD                        
                            ALTERNATE RECORD KEY IS K022-NUM-REGN               
                            WITH DUPLICATES                                     
                            FILE STATUS  IS BP13K022-STATUS.                    
                                                                                
           SELECT P13F35B  ASSIGN        TO P13F35B.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
                                                                                
       FD   BP13F800                                                            
            RECORDING  MODE IS F                                                
            RECORD CONTAINS 2000 CHARACTERS.                                    
                                                                                
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD  P13F35B                                                              
           RECORD CONTAINS 80 CHARACTERS                                        
           RECORDING  MODE IS F.                                                
       01  P13F35B-REC    PIC X(80).                                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
                                                                                
       01  WS-VARIABLES.                                                        
           05  CNT-F800-READ        PIC 9(05)  VALUE ZEROES.                    
           05  CNT-K022-NOTFND      PIC 9(05)  VALUE ZEROES.                    
           05  WS-I                 PIC 9(02)  VALUE ZEROES.                    
           05  WS-CNT               PIC 9(02)  VALUE ZEROES.                    
           05  WS-F800-EOF          PIC X(01)  VALUE SPACES.                    
               88  F800-EOF                    VALUE 'Y'.                       
           05  WS-K022-FND          PIC X(01)  VALUE SPACES.                    
               88  K022-FND                    VALUE 'Y'.                       
           05  BP13K022-STATUS      PIC 9(02)  VALUE ZEROES.                    
           05  WS-SYS-DATE          PIC X(8)   VALUE SPACES.                    
           05  WS-UPD-HDR-WRITE     PIC X(1)   VALUE SPACES.                    
       01  WS-PREB-CSM              PIC X(5)   VALUE SPACES.                    
       01  WS-POSB-CSM              PIC X(5)   VALUE SPACES.                    
       01  WS-CSM-NKNME             PIC X(5)   VALUE SPACES.                    
       01  WS-CSM-FOUND             PIC X(01)  VALUE SPACES.                    
                                                                                
       01  P13F35B-DETAIL.                                                      
           05 FILLER                   PIC X(01)  VALUE SPACES.                 
           05 F35B-REGN                PIC X(08)  VALUE SPACES.                 
           05 FILLER                   PIC X(04)  VALUE SPACES.                 
           05 F35B-SCH-ACC             PIC X(11)  VALUE SPACES.                 
           05 FILLER                   PIC X(04)  VALUE SPACES.                 
           05 F35B-LOAN-TAG            PIC X(01)  VALUE SPACES.                 
           05 FILLER                   PIC X(03)  VALUE SPACES.                 
           05 F35B-OIC-NCK-NAME        PIC X(05)  VALUE SPACES.                 
           05 FILLER                   PIC X(02)  VALUE SPACES.                 
           05 F35B-CORDER-STATUS       PIC X(10)  VALUE SPACES.                 
           05 FILLER                   PIC X(02)  VALUE SPACES.                 
           05 F35B-DTE-APPROVAL        PIC X(08)  VALUE SPACES.                 
           05 FILLER                   PIC X(01)  VALUE SPACES.                 
           05 F35B-NUM-COMPL-TAG       PIC X(01)  VALUE SPACES.                 
           05 FILLER                   PIC X(19)  VALUE SPACES.                 
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
            PERFORM 1000-OPEN-ROUTINE         THRU 1000-EXIT.                   
            PERFORM 2000-READ-BP13F800        THRU 2000-EXIT.                   
            PERFORM 3000-PROCESS-DETAIL       THRU 3000-EXIT                    
                              UNTIL F800-EOF.                                   
            PERFORM 9000-CLOSE-ROUTINE        THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-OPEN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F800                                                 
                       BP13K022                                                 
                OUTPUT P13F35B.                                                 
                                                                                
           IF BP13K022-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K022 - OPEN ERROR (' BP13K022-STATUS ')'             
              MOVE BP13K022-STATUS  TO RETURN-CODE                              
              GO TO 9000-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F800.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F800                                                        
             AT END                                                             
                MOVE 'Y'   TO WS-F800-EOF                                       
                GO         TO 2000-EXIT                                         
           END-READ.                                                            
           ADD             1 TO     CNT-F800-READ.                              
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DETAIL.                                                     
      *-------------------------------------------------------------            
                                                                                
           PERFORM 8000-READ-BP13K022       THRU  8000-EXIT.                    
           MOVE  SPACES                     TO  P13F35B-DETAIL.                 
           INITIALIZE                           P13F35B-DETAIL.                 
           MOVE F800-NUM-REGN               TO F35B-REGN.                       
           MOVE F800-NUM-SCH-ACC            TO F35B-SCH-ACC.                    
           MOVE F800-NUM-LOAN-TAG           TO F35B-LOAN-TAG.                   
           MOVE WS-CSM-NKNME                TO F35B-OIC-NCK-NAME.               
           MOVE F800-DTE-CORDER-APPROVAL     TO F35B-DTE-APPROVAL.              
           MOVE F800-NUM-SUBTRF-COMPL-MAIL   TO F35B-NUM-COMPL-TAG.             
           MOVE 'APPROVED'                   TO F35B-CORDER-STATUS.             
           WRITE P13F35B-REC FROM P13F35B-DETAIL.                               
           ADD 1                    TO WS-CNT.                                  
                                                                                
           PERFORM 2000-READ-BP13F800        THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       8000-READ-BP13K022.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE F800-NUM-REGN  TO K022-NUM-REGN.                                
           MOVE SPACES         TO WS-PREB-CSM WS-POSB-CSM                       
                                             WS-CSM-FOUND.                      
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           IF BP13K022-STATUS = 00                                              
              MOVE 'Y'                     TO   WS-CSM-FOUND                    
              PERFORM  8001-GET-CSM        THRU 8001-EXIT                       
           ELSE                                                                 
           IF BP13K022-STATUS = 23                                              
              DISPLAY '> CSM FOR ' K022-NUM-REGN ' NOT FOUND.'                  
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K022, STATUS ' BP13K022-STATUS         
              MOVE BP13K022-STATUS TO RETURN-CODE                               
              PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                       
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       8001-GET-CSM.                                                            
      *-------------------------------------------------------------            
                                                                                
            MOVE SPACES                 TO   WS-CSM-NKNME.                      
            IF K022-NUM-OIC NOT = SPACES AND LOW-VALUES                         
               IF K022-NUM-OIC-APPL = SPACES OR LOW-VALUES                      
                  MOVE K022-NUM-OIC        TO WS-PREB-CSM                       
               ELSE                                                             
                  MOVE K022-NUM-OIC-APPL   TO WS-PREB-CSM                       
               END-IF                                                           
            END-IF.                                                             
           IF K022-NUM-OIC-APPL = SPACES OR LOW-VALUES                          
              MOVE SPACES                          TO WS-POSB-CSM               
           ELSE                                                                 
              MOVE K022-NUM-OIC                    TO WS-POSB-CSM               
           END-IF.                                                              
           IF F800-NUM-PROV-TAG = 'Y'                                           
              MOVE SPACES         TO    WS-POSB-CSM                             
              MOVE WS-PREB-CSM    TO    WS-CSM-NKNME                            
           ELSE                                                                 
              IF WS-POSB-CSM NOT = SPACES                                       
                 MOVE WS-POSB-CSM  TO   WS-CSM-NKNME                            
              ELSE                                                              
                 MOVE WS-PREB-CSM  TO   WS-CSM-NKNME                            
              END-IF                                                            
           END-IF.                                                              
                                                                                
       8001-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' - BP13C35B CONTROL TOTALS -----'.                          
           DISPLAY ' '.                                                         
           DISPLAY ' NO. OF BP13F800 RECS READ   = ' CNT-F800-READ.             
           DISPLAY ' NO. OF RECORDS  WRITTEN     = ' WS-CNT.                    
           DISPLAY ' '.                                                         
                                                                                
            CLOSE BP13F800                                                      
                  BP13K022                                                      
                  P13F35B.                                                      
                                                                                
           IF BP13K022-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K022 - CLOSE ERROR (' BP13K022-STATUS ')'            
           END-IF.                                                              
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
