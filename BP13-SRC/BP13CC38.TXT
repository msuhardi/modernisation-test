      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CC38.                                                 
       AUTHOR.        BALA AMUTHAN.                                             
       DATE-WRITTEN.  17/10/2006.                                               
      *****************************************************************         
      * OBJECTIVE - PROGRESS PAYMENT LISTING REPORT.                            
      *                                                                         
      *     INPUT FILE :                                                        
      *                  1. BP13KA10                                            
      *                  2. BP13FD10                                            
      *                  3. SY02F001                                            
      *                                                                         
      *     OUTPUT FILE:                                                        
      *                  1. NIL                                                 
      *                                                                         
      *     LISTING:                                                            
      *                  1. BP13LC38 (REPORT)                                   
      *                                                                         
      *                                                                         
      *----------------------------------------------------------------         
      * REF NO    DATE      BY   AMENDMENTS/ENHANCEMENTS                        
      * -------   --------  ---  ------------------------                       
      * BP132888  18072006  BA2  NEW PROGRAM.                                   
      * BP133398  18052009  JB8  EXPAND KD10 LENGTH FR 450  TO 1400             
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13KA10  ASSIGN      TO BP13KA10                            
                   ACCESS MODE  IS RANDOM                                       
                   ORGANIZATION IS INDEXED                                      
                   RECORD KEY   IS KA10-KEY-FLD                                 
                   FILE STATUS  IS WS-KA10-STATUS.                              
                                                                                
           SELECT  BP13FD10  ASSIGN      TO BP13FD10.                           
                                                                                
           SELECT  SY02F001  ASSIGN      TO SY02F001.                           
                                                                                
           SELECT  BP13LC38  ASSIGN      TO BP13LC38.                           
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13KA10                   RECORD CONTAINS 800 CHARACTERS.          
       COPY BP13KA10.                                                           
                                                                                
       FD   BP13FD10                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 1400 CHARACTERS          
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13FD10.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13LC38                    BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 132 CHARACTERS           
                                       LABEL RECORDS ARE OMITTED                
                                       RECORDING MODE IS F.                     
       01  PRINT-LC38-REC              PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-KA10-STATUS          PIC 99    VALUE ZEROES.                  
                                                                                
       01  WS-SWITCHS.                                                          
           05  WS-FD10-EOF                 PIC X     VALUE 'N'.                 
               88 CS-FD10-EOF                        VALUE 'Y'.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-FD10             PIC 9(7) COMP VALUE ZEROES.              
           05  WS-CNT-LC38             PIC 9(7) COMP VALUE ZEROES.              
           05  WS-SNO-CNT-LC38         PIC 9(5) COMP VALUE ZEROES.              
           05  WS-PAGE-CNT-LC38        PIC 9(5) COMP VALUE ZEROES.              
           05  WS-LINE-CNT-LC38        PIC 9(2)      VALUE 60.                  
                                                                                
       01  WS-GRANT-AMT.                                                        
           05  WS-TOT-AMT              PIC 9(9)V99 COMP VALUE ZEROES.           
                                                                                
       01  WS-CURR-DATE.                                                        
           05  WS-CURR-CCYY            PIC X(4)  VALUE SPACES.                  
           05  WS-CURR-MM              PIC X(2)  VALUE SPACES.                  
           05  WS-CURR-DD              PIC X(2)  VALUE SPACES.                  
                                                                                
       01  WS-PREV-VAR.                                                         
           05  WS-PREV-DTE-IBG         PIC X(08) VALUE SPACES.                  
           05  WS-PREV-DTE-DUE-PP      PIC X(08) VALUE SPACES.                  
           05  WS-PREV-PRJCT-ID        PIC X(03) VALUE SPACES.                  
                                                                                
      ******************************************************************        
      *                   REPORT HEADINGS                              *        
      ******************************************************************        
       01  LC38-PR-HEAD.                                                        
           05  FILLER                  PIC X(41) VALUE SPACES.                  
           05  FILLER                  PIC X(24) VALUE                          
                                       'H O U S I N G   &   D E '.              
           05  FILLER                  PIC X(29) VALUE                          
                                       'V E L O P M E N T   B O A R D'.         
           05  FILLER                  PIC X(38) VALUE SPACES.                  
                                                                                
       01  LC38-PR-HEAD-01.                                                     
           05  FILLER                  PIC X(8)  VALUE 'BP13LC38'.              
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'HDBCAT3'.               
           05  FILLER                  PIC X(25) VALUE SPACES.                  
           05  FILLER                  PIC X(20) VALUE                          
                                       'S Y S T E M   O F   '.                  
           05  FILLER                  PIC X(19) VALUE                          
                                       'C O M M I T M E N T'.                   
           05  FILLER                  PIC X(15) VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'DATE :'.                
           05  LC38-DATE-HD            PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'PAGE : '.               
           05  LC38-PAGENO             PIC ZZZZ9 .                              
           05  FILLER                  PIC X(1)  VALUE SPACES.                  
                                                                                
       01  LC38-PR-HEAD-02.                                                     
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(32) VALUE                          
               'PROGRESS PAYMENT LISTING FOR : '.                               
           05  LC38-PROG-PAY           PIC X(30) VALUE SPACES.                  
           05  FILLER                  PIC X(67) VALUE SPACES.                  
                                                                                
       01  LC38-PR-HEAD-03.                                                     
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(21) VALUE                          
               'SOLICITOR        : '.                                           
           05  LC38-SOLICITOR          PIC X(66) VALUE SPACES.                  
           05  FILLER                  PIC X(42) VALUE SPACES.                  
                                                                                
       01  LC38-PR-HEAD-04.                                                     
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(21) VALUE                          
               'DEVELOPER        : '.                                           
           05  LC38-DEVLOPER           PIC X(30) VALUE SPACES.                  
           05  FILLER                  PIC X(78) VALUE SPACES.                  
                                                                                
       01  LC38-PR-HEAD-05.                                                     
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(27) VALUE                          
               'VALUE DATE OF REMITTANCE : '.                                   
           05  LC38-REMITT-DTE         PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(92) VALUE SPACES.                  
                                                                                
       01  LC38-PR-HEAD-06.                                                     
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE 'S/NO'.                  
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE 'REGN-NO'.               
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  FILLER                  PIC X(16) VALUE                          
                                       'PROPERTY ADDRESS'.                      
           05  FILLER                  PIC X(46) VALUE SPACES.                  
           05  FILLER                  PIC X(16) VALUE                          
                                       'PAYMENT SCHEDULE'.                      
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(12) VALUE                          
                                      'IBG REF NO. '.                           
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE                          
                                      'AMOUNT ($)'.                             
           05  FILLER                  PIC X(07) VALUE SPACES.                  
                                                                                
       01  LC38-PR-HEAD-TOT.                                                    
           05  FILLER                  PIC X(99)  VALUE SPACES.                 
           05  FILLER                  PIC X(13)  VALUE                         
               '  TOTAL      '.                                                 
           05  LC38-TOT-AMT            PIC ZZZ,ZZZ,ZZ9.99 VALUE SPACES.         
           05  FILLER                  PIC X(06) VALUE SPACES.                  
                                                                                
       01  LC38-PR-ROLL.                                                        
           05  FILLER                  PIC X(02)  VALUE SPACES.                 
           05  FILLER                  PIC X(122) VALUE ALL '-'.                
           05  FILLER                  PIC X(08)  VALUE SPACES.                 
                                                                                
       01  LC38-PR-TOT-ROLL.                                                    
           05  FILLER                  PIC X(101) VALUE SPACES.                 
           05  FILLER                  PIC X(05)  VALUE ALL '-'.                
           05  FILLER                  PIC X(06)  VALUE SPACES.                 
           05  FILLER                  PIC X(14)  VALUE ALL '-'.                
           05  FILLER                  PIC X(06)  VALUE SPACES.                 
                                                                                
       01  LC38-PR-DETAILS.                                                     
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  LC38-SNO                PIC Z(4)9 VALUE SPACES.                  
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  LC38-REGNO              PIC X(08) VALUE 'REGN-NO'.               
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  LC38-ADDRESS            PIC X(60) VALUE SPACES.                  
           05  FILLER                  PIC X(09) VALUE SPACES.                  
           05  LC38-PAY-SCHUD          PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(09) VALUE SPACES.                  
           05  LC38-IBG-REF            PIC X(12) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  LC38-AMT                PIC Z,ZZ,ZZ9.99                          
                                                 VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE SPACES.                  
                                                                                
      *-------------------------------------------------------*                 
      *    COPY BOOK FOR SUBROUTINE - BP13C913.               *                 
      *-------------------------------------------------------*                 
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------*                 
      *    LINKAGE VARIABLES                                  *                 
      *-------------------------------------------------------*                 
       01   WS-LINK-REC.                                                        
            05 WS-LINK-NUM-SCH            PIC X(4).                             
            05 WS-LINK-NUM-ACC            PIC X(4).                             
            05 WS-LINK-NUM-CHK            PIC X(1).                             
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN-PARA.                                                          
      ******************************************************************        
                                                                                
           PERFORM 1000-START-UP           THRU  1000-EXIT.                     
                                                                                
           PERFORM 1500-READ-SY02F001      THRU  1500-EXIT.                     
                                                                                
           PERFORM 2000-READ-BP13FD10      THRU  2000-EXIT.                     
                                                                                
           MOVE FD10-NUM-PRJCT       TO WS-PREV-PRJCT-ID.                       
           MOVE FD10-DTE-IBG-VALUE   TO WS-PREV-DTE-IBG.                        
           MOVE FD10-DTE-DUE-PP      TO WS-PREV-DTE-DUE-PP.                     
                                                                                
           PERFORM 2100-READ-BP13KA10      THRU  2100-EXIT.                     
           PERFORM 3000-PRINT-PROCESS      THRU  3000-EXIT                      
                   UNTIL CS-FD10-EOF.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-START-UP.                                                           
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13KA10                                                 
                       BP13FD10                                                 
                       SY02F001                                                 
                OUTPUT BP13LC38.                                                
                                                                                
           IF WS-KA10-STATUS NOT = 0  AND                                       
              WS-KA10-STATUS NOT = 97                                           
              DISPLAY ' OPENING ERROR, BP13KA10 STATUS ' WS-KA10-STATUS         
              MOVE WS-KA10-STATUS           TO RETURN-CODE                      
              PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                  
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1500-READ-SY02F001.                                                      
      ******************************************************************        
                                                                                
           READ SY02F001.                                                       
           MOVE F001-DTE-CURRENT      TO WS-CURR-DATE.                          
                                                                                
           STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CCYY                    
                  DELIMITED BY SIZE INTO LC38-DATE-HD.                          
                                                                                
       1500-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2000-READ-BP13FD10.                                                      
      ******************************************************************        
                                                                                
           READ BP13FD10           AT   END                                     
                MOVE 'Y'           TO   WS-FD10-EOF                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO   WS-CNT-FD10.                            
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2100-READ-BP13KA10.                                                      
      ******************************************************************        
           MOVE SPACES                     TO   BP13KA10-REC.                   
           INITIALIZE     BP13KA10-REC.                                         
           MOVE FD10-NUM-PRJCT             TO   KA10-NUM-PRJCT.                 
                                                                                
           READ BP13KA10.                                                       
                                                                                
           IF WS-KA10-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
             IF WS-KA10-STATUS = 23                                             
                DISPLAY 'PROJECT ID NOT FOUND-BP13KA10 - '                      
                         KA10-KEY-FLD                                           
             ELSE                                                               
                DISPLAY 'ERROR WITH READ FILE-BP13KA10. STATUS IS '             
                         WS-KA10-STATUS                                         
                MOVE WS-KA10-STATUS       TO RETURN-CODE                        
                PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT                 
             END-IF                                                             
           END-IF.                                                              
                                                                                
       2100-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       3000-PRINT-PROCESS.                                                      
      ******************************************************************        
                                                                                
           PERFORM 4000-MOVE-DETAILS               THRU 4000-EXIT               
           PERFORM 6000-PRINT-REPORT               THRU 6000-EXIT               
           PERFORM 2000-READ-BP13FD10              THRU 2000-EXIT.              
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       4000-MOVE-DETAILS.                                                       
      ******************************************************************        
           MOVE SPACES                   TO BP13COMM8-REC.                      
           MOVE FD10-NUM-REGN            TO LC38-REGNO.                         
                                                                                
      *  TO CALL SUBROUTINE BP13C913,GETTING ADDRESS                            
           MOVE FD10-NUM-SCH-ACC(1:4)    TO WS-LINK-NUM-SCH.                    
           MOVE FD10-NUM-SCH-ACC(5:4)    TO WS-LINK-NUM-ACC.                    
           MOVE FD10-NUM-SCH-ACC(9:1)    TO WS-LINK-NUM-CHK.                    
           PERFORM 5000-LINK-BP13C913    THRU 5000-EXIT.                        
           STRING 'BLK '                 DELIMITED BY SIZE                      
                  COMM8-NUM-BLK          DELIMITED BY SIZE                      
                  ' #'                   DELIMITED BY SIZE                      
                  COMM8-NUM-UNIT-MAIN    DELIMITED BY SIZE                      
                  ' '                    DELIMITED BY SIZE                      
                  COMM8-NME-STREET       DELIMITED BY '%'                       
                  ' S('                  DELIMITED BY SIZE                      
                  COMM8-NUM-POSTAL-CODE  DELIMITED BY SIZE                      
                  ')'                    DELIMITED BY SIZE                      
                  INTO LC38-ADDRESS.                                            
           MOVE FD10-NUM-PP              TO  LC38-PAY-SCHUD.                    
           MOVE FD10-TXT-IBG-SERIAL      TO  LC38-IBG-REF.                      
           IF FD10-AMT-PAYMENT-DEVELOPER NOT NUMERIC                            
              MOVE ZEROES                TO  FD10-AMT-PAYMENT-DEVELOPER         
           END-IF.                                                              
           MOVE FD10-AMT-PAYMENT-DEVELOPER TO LC38-AMT.                         
                                                                                
       4000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       5000-LINK-BP13C913.                                                      
      ******************************************************************        
           CALL 'BP13C913' USING                                                
                 WS-LINK-REC, BP13COMM8-REC                                     
           END-CALL.                                                            
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE'                       
                         COMM8-CDE-SYSERR                                       
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR             
              END-IF                                                            
              MOVE SPACES        TO BP13COMM8-REC                               
           END-IF.                                                              
                                                                                
       5000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       6000-PRINT-REPORT.                                                       
      ******************************************************************        
                                                                                
           IF FD10-NUM-PRJCT      NOT = WS-PREV-PRJCT-ID   OR                   
              FD10-DTE-IBG-VALUE  NOT = WS-PREV-DTE-IBG    OR                   
              FD10-DTE-DUE-PP     NOT = WS-PREV-DTE-DUE-PP                      
              PERFORM  6050-PRINT-TOTAL        THRU 6050-EXIT                   
      * FOR GETTING PROJECT NAME.                                               
              PERFORM  2100-READ-BP13KA10      THRU 2100-EXIT                   
              PERFORM  6100-PRINT-HEAD         THRU 6100-EXIT                   
           ELSE                                                                 
             IF WS-LINE-CNT-LC38 > 50                                           
                PERFORM  6100-PRINT-HEAD       THRU 6100-EXIT                   
             END-IF                                                             
           END-IF.                                                              
                                                                                
           ADD +1                          TO WS-SNO-CNT-LC38.                  
           MOVE WS-SNO-CNT-LC38            TO LC38-SNO.                         
           WRITE PRINT-LC38-REC            FROM LC38-PR-DETAILS.                
                                                                                
           ADD +1                          TO WS-LINE-CNT-LC38                  
                                              WS-CNT-LC38.                      
                                                                                
           COMPUTE  WS-TOT-AMT  = WS-TOT-AMT +                                  
                                  FD10-AMT-PAYMENT-DEVELOPER.                   
                                                                                
       6000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ******************************************************************        
       6050-PRINT-TOTAL.                                                        
      ******************************************************************        
                                                                                
      *FOR PRINITNG TOTAL.                                                      
           MOVE SPACES                TO PRINT-LC38-REC.                        
           WRITE PRINT-LC38-REC.                                                
           MOVE WS-TOT-AMT            TO LC38-TOT-AMT.                          
           WRITE PRINT-LC38-REC        FROM LC38-PR-TOT-ROLL.                   
           WRITE PRINT-LC38-REC        FROM LC38-PR-HEAD-TOT.                   
           WRITE PRINT-LC38-REC        FROM LC38-PR-TOT-ROLL.                   
           MOVE ZEROES                 TO WS-TOT-AMT.                           
           MOVE ZEROES                 TO WS-SNO-CNT-LC38.                      
                                                                                
       6050-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       6100-PRINT-HEAD.                                                         
      ******************************************************************        
                                                                                
                                                                                
           MOVE  SPACES              TO PRINT-LC38-REC.                         
           ADD 1                     TO WS-PAGE-CNT-LC38.                       
           MOVE WS-PAGE-CNT-LC38     TO LC38-PAGENO.                            
                                                                                
           MOVE KA10-NME-PRJCT       TO LC38-PROG-PAY.                          
           MOVE FD10-NME-SOLICITOR   TO LC38-SOLICITOR.                         
           MOVE FD10-NME-DEVELOPER   TO LC38-DEVLOPER.                          
           MOVE FD10-NUM-PRJCT       TO WS-PREV-PRJCT-ID.                       
           MOVE FD10-DTE-IBG-VALUE   TO WS-PREV-DTE-IBG.                        
           MOVE FD10-DTE-DUE-PP      TO WS-PREV-DTE-DUE-PP.                     
                                                                                
           IF FD10-DTE-IBG-VALUE NOT = SPACES AND LOW-VALUES                    
              STRING FD10-DTE-IBG-VALUE(7:2)   '/'                              
                     FD10-DTE-IBG-VALUE(5:2)   '/'                              
                     FD10-DTE-IBG-VALUE(1:4)   '/'                              
                     DELIMITED BY SIZE INTO LC38-REMITT-DTE                     
           ELSE                                                                 
              MOVE SPACES         TO LC38-REMITT-DTE                            
           END-IF.                                                              
                                                                                
           MOVE  SPACES          TO   PRINT-LC38-REC.                           
           WRITE PRINT-LC38-REC AFTER PAGE.                                     
           WRITE PRINT-LC38-REC.                                                
           WRITE PRINT-LC38-REC.                                                
                                                                                
           WRITE PRINT-LC38-REC  FROM LC38-PR-HEAD.                             
                                                                                
           MOVE  SPACES          TO   PRINT-LC38-REC.                           
           WRITE PRINT-LC38-REC.                                                
                                                                                
           WRITE PRINT-LC38-REC  FROM LC38-PR-HEAD-01.                          
                                                                                
           MOVE  SPACES          TO   PRINT-LC38-REC.                           
           WRITE PRINT-LC38-REC.                                                
           WRITE PRINT-LC38-REC.                                                
                                                                                
           WRITE PRINT-LC38-REC  FROM LC38-PR-HEAD-02.                          
                                                                                
           MOVE  SPACES          TO   PRINT-LC38-REC.                           
           WRITE PRINT-LC38-REC.                                                
                                                                                
           WRITE PRINT-LC38-REC  FROM LC38-PR-HEAD-03.                          
                                                                                
           MOVE  SPACES          TO   PRINT-LC38-REC.                           
           WRITE PRINT-LC38-REC.                                                
                                                                                
           WRITE PRINT-LC38-REC  FROM LC38-PR-HEAD-04.                          
                                                                                
           MOVE  SPACES          TO   PRINT-LC38-REC.                           
           WRITE PRINT-LC38-REC.                                                
                                                                                
           WRITE PRINT-LC38-REC  FROM LC38-PR-HEAD-05.                          
                                                                                
           MOVE  SPACES          TO   PRINT-LC38-REC.                           
           WRITE PRINT-LC38-REC.                                                
           WRITE PRINT-LC38-REC.                                                
                                                                                
           WRITE PRINT-LC38-REC  FROM LC38-PR-HEAD-06.                          
           WRITE PRINT-LC38-REC  FROM LC38-PR-ROLL.                             
                                                                                
           MOVE  SPACES          TO   PRINT-LC38-REC.                           
           WRITE PRINT-LC38-REC.                                                
                                                                                
           MOVE  20              TO   WS-LINE-CNT-LC38.                         
                                                                                
       6100-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
           IF WS-LINE-CNT-LC38 > 0 AND WS-LINE-CNT-LC38 < 60                    
              IF WS-LINE-CNT-LC38 > 50                                          
                 PERFORM  6100-PRINT-HEAD     THRU 6100-EXIT                    
              END-IF                                                            
              PERFORM  6050-PRINT-TOTAL       THRU 6050-EXIT                    
           END-IF.                                                              
                                                                                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CC38'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13FD10 RECORDS READ..........: '                    
                    WS-CNT-FD10.                                                
           DISPLAY 'NO OF RECORDS PRINTED................: '                    
                    WS-CNT-LC38.                                                
                                                                                
           CLOSE BP13KA10                                                       
                 BP13FD10                                                       
                 SY02F001                                                       
                 BP13LC38.                                                      
                                                                                
           IF WS-KA10-STATUS NOT = 0 AND WS-KA10-STATUS NOT = 97                
              DISPLAY ' CLOSING ERROR, BP13KA10 STATUS ' WS-KA10-STATUS         
              MOVE WS-KA10-STATUS TO RETURN-CODE.                               
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
