       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CAD4.                                                 
      *AUTHOR.        KARL ANDRUS MAGALONA CABUG.                               
      *DATE-WRITTEN.  31/08/2023.                                               
      * ========================================================== *            
      *           HFE REF SALES REGN TO BE LINKED                  *            
      * ========================================================== *            
      * INPUT  FILE  :  1.) BP13F001                               *            
      *   I-O  FILE  :  2.) BP13K800                               *            
      * MATCH  ERROR :  3.) DISPLAYED                              *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP13#### KAC1 31/08/2023 NEW PROGRAM                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F001  ASSIGN        TO BP13F001.                          
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS WS-K800-STATUS.                    
                                                                                
                                                                                
      *    SELECT BP13F002  ASSIGN        TO BP13F002.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F001                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 2000 CHARACTERS.                                     
       01  BP13F001-REC.                                                        
           05 F001-NUM-REGN             PIC X(08).                              
           05 F001-NUM-HLA-STATUS       PIC X(03).                              
           05 F001-NUM-HLA              PIC X(09).                              
           05 FILLER                    PIC X(1980).                            
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000  CHARACTERS.                                    
           COPY BP13K800.                                                       
                                                                                
      *FD  BP13F002                                                             
      *    RECORDING MODE IS F                                                  
      *    RECORD CONTAINS 60 CHARACTERS.                                       
      *01  BP13F002-REC.                                                        
      *    05 WS-F002-REGN                 PIC X(08).                           
      *    05 FILLER                       PIC X(22).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K800-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                      PIC 9(2)   VALUE ZEROES.               
           05  WS-CNT-F001-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K800-UPDT          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F002-WRITE         PIC 9(6)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F001-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K800-FND               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-FILLER                 PIC X(8)   VALUE SPACES.               
           05  WS-NUM-HLA                PIC X(9)   VALUE SPACES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-BP13F001-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F001-EOF = 'Y'                                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13F001                                                 
                I-O    BP13K800.                                                
      *         OUTPUT BP13F002.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE     WS-K800-STATUS       TO   RETURN-CODE                    
              PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       2000-BP13F001-READ.                                                      
      *=============================================================            
           READ BP13F001                                                        
              AT END MOVE 'Y'              TO  WS-F001-EOF                      
                 GO TO 2000-EXIT.                                               
                                                                                
           ADD 1                           TO  WS-CNT-F001-READ.                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
           MOVE 'N'                           TO WS-K800-FND.                   
           MOVE F001-NUM-REGN                 TO K800-NUM-REGN.                 
           PERFORM 3100-READ-BP13K800       THRU 3100-EXIT.                     
                                                                                
           IF WS-K800-FND = 'Y'                                                 
              PERFORM 3200-REWRITE-BP13K800 THRU 3200-EXIT                      
      *    ELSE                                                                 
      *      ADD 1                            TO WS-CNT-F002-WRITE              
      *      MOVE SPACES                      TO BP13F002-REC                   
      *      MOVE F001-NUM-REGN               TO WS-F002-REGN                   
      *      WRITE BP13F002-REC                                                 
           END-IF.                                                              
                                                                                
           PERFORM 2000-BP13F001-READ       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3100-READ-BP13K800.                                                      
      *=============================================================            
           READ BP13K800.                                                       
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                   TO WS-K800-FND                   
               WHEN 23                                                          
                    DISPLAY 'BP13K800 - NOTFND, KEY : ' K800-NUM-REGN           
               WHEN OTHER                                                       
                    DISPLAY ' '                                                 
                    DISPLAY '**BP13K800 - READ FILE ERROR, KEY:'                
                                                  K800-NUM-REGN                 
                    DISPLAY ' '                                                 
                    MOVE WS-K800-STATUS        TO RETURN-CODE                   
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3200-REWRITE-BP13K800.                                                   
      *=============================================================            
           MOVE F001-NUM-HLA                  TO K800-NUM-HLA.                  
                                                                                
           IF F001-NUM-HLA-STATUS = 'APR'                                       
              MOVE F001-NUM-HLA-STATUS        TO K800-NUM-HLA-STATUS            
           ELSE                                                                 
              MOVE 'PP'                       TO K800-NUM-HLA-STATUS            
           END-IF.                                                              
                                                                                
           REWRITE BP13K800-MASTER.                                             
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    ADD 1                     TO WS-CNT-K800-UPDT               
               WHEN OTHER                                                       
                    DISPLAY ' '                                                 
                    DISPLAY '*WRITE ERROR, KEY:' K800-NUM-REGN                  
                    DISPLAY ' '                                                 
                    MOVE WS-K800-STATUS       TO RETURN-CODE                    
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13F001                                                       
                 BP13K800.                                                      
      *          BP13F002.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K800 ERROR ' WS-K800-STATUS                  
              MOVE WS-K800-STATUS          TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13CAD4 CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13F001 READ      : ' WS-CNT-F001-READ.          
           DISPLAY ' 2. NO OF BP13K800 UPDATE    : ' WS-CNT-K800-UPDT.          
      *    DISPLAY ' 3. NO OF BP13F002 WRITTEN   : ' WS-CNT-F002-WRITE.         
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
       9000-EXIT.                                                               
           EXIT.                                                                
