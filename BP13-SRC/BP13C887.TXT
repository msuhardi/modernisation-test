       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C887.                                                 
      *AUTHOR.        EVM.                                                      
      *DATE-WRITTEN.  25/07/96.                                                 
      *DATE-COMPILED.                                                           
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *    1.   PROGRAM TO WRITE BP17K000 REGISTRATION NUMBER  *                
      *         TO BP13K740 JB REGISTRATION NUMBER FOR SER JB  *                
      *         CASES.                                         *                
      *                                                        *                
      *    INPUT FILES :                                       *                
      *    1.   BP13F740                                       *                
      *                                                        *                
      *    I-O FILES    :                                      *                
      *    1.   BP13K740                                       *                
      *    2.   BP17K000                                       *                
      *                                                        *                
      * CHG-NO.  BY  ON       DESCRIPTION                      *                
      * -------- --- -------- -----------                      *                
      * BP130118 EVM 19960725 NEW PROGRAM                      *                
      * BP130652 NOR 19990222 EXPAND F740 & K740 RECL 300 - 500*                
      * BP132604 YCH 20041011 EXPAND F000 FROM 80 TO 200       *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740   ASSIGN TO BP13F740.                                
                                                                                
           SELECT BP13K740   ASSIGN TO BP13K740                                 
                  ACCESS MODE IS RANDOM                                         
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K740-KEY-FLD                                    
                  ALTERNATE RECORD KEY IS K740-NUM-REGN                         
                  FILE STATUS IS WS-K740-STATUS.                                
                                                                                
           SELECT BP17K000   ASSIGN TO BP17K000                                 
                  ACCESS MODE IS RANDOM                                         
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K000-KEY-FLD                                    
                  ALTERNATE RECORD KEY IS K000-NUM-REF                          
                  FILE STATUS IS WS-K000-STATUS.                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F740                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K740                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13K740.                                                           
                                                                                
       FD   BP17K000                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP17K000.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-K000-STATUS              PIC 99.                                  
       01  WS-K740-STATUS              PIC 99.                                  
       01  WS-F740-EOF                 PIC X(1).                                
           88  F740-EOF                            VALUE 'Y'.                   
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURR-YY              PIC 9999.                                
           05  WS-CURR-MM              PIC 99.                                  
           05  WS-CURR-DD              PIC 99.                                  
                                                                                
       01  WS-F740-READ                PIC 9(7)   VALUE ZEROES.                 
       01  WS-K000-READ                PIC 9(7)   VALUE ZEROES.                 
       01  WS-K000-NOTFND              PIC 9(7)   VALUE ZEROES.                 
       01  WS-K740-REWRITE             PIC 9(7)   VALUE ZEROES.                 
                                                                                
       01  WS-K000-REGN                PIC X(8)   VALUE SPACES.                 
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      ******************************************************************        
       MAIN-ROUTINE SECTION.                                                    
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-BP13F740   THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-F740    THRU 3000-EXIT                          
                        UNTIL F740-EOF.                                         
                                                                                
           DISPLAY 'PROGRAM : BP13C887    '.                                    
           DISPLAY 'DATE    : ' WS-CURRENT-DATE.                                
           DISPLAY '*=========== COUNTERS ===========*'                         
           DISPLAY 'F740-REC-READ     : ' WS-F740-READ.                         
           DISPLAY 'K000-REC-READ     : ' WS-K000-READ.                         
           DISPLAY 'K000-REC-NOTFND   : ' WS-K000-NOTFND.                       
           DISPLAY 'K740-REC-UPDATED  : ' WS-K740-REWRITE.                      
                                                                                
           PERFORM 9999-CLOSE-RTN THRU 9999-EXIT.                               
                                                                                
       MAIN-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
                                                                                
           OPEN INPUT   BP13F740                                                
                I-O     BP13K740 BP17K000.                                      
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              MOVE WS-K740-STATUS      TO RETURN-CODE                           
              DISPLAY 'BP13K740 OPEN ERROR, STATUS : ' WS-K740-STATUS           
              PERFORM 9999-CLOSE-RTN.                                           
                                                                                
           IF WS-K000-STATUS NOT = 00 AND 97                                    
              MOVE WS-K000-STATUS      TO RETURN-CODE                           
              DISPLAY 'BP17K000 OPEN ERROR, STATUS : ' WS-K000-STATUS           
              PERFORM 9999-CLOSE-RTN.                                           
                                                                                
           MOVE SPACES              TO WS-F740-EOF.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F740.                                                      
      ******************************************************************        
                                                                                
           READ BP13F740 AT END                                                 
                MOVE 'Y'          TO WS-F740-EOF                                
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F740-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-F740.                                                       
      ******************************************************************        
           MOVE F740-NUM-JB-REF   TO K000-NUM-REF.                              
           READ BP17K000 KEY IS K000-NUM-REF.                                   
           EVALUATE WS-K000-STATUS                                              
           WHEN 00                                                              
              ADD 1                TO WS-K000-READ                              
              MOVE SPACES          TO WS-K000-REGN                              
              MOVE K000-NUM-REGNO  TO WS-K000-REGN                              
              PERFORM 4000-PROCESS-K740  THRU 4000-EXIT                         
           WHEN 23                                                              
              ADD 1                TO WS-K000-NOTFND                            
              DISPLAY 'BP17K000 RECORD NOT FND, KEY : ' K000-NUM-REF            
           WHEN OTHER                                                           
              DISPLAY 'BP17K000 READ ERROR, STATUS : ' WS-K000-STATUS           
              MOVE WS-K000-STATUS    TO RETURN-CODE                             
              PERFORM 9999-CLOSE-RTN                                            
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2000-READ-BP13F740   THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-PROCESS-K740.                                                       
      ******************************************************************        
           MOVE F740-NUM-REGN     TO K740-NUM-REGN.                             
           READ BP13K740 KEY IS K740-NUM-REGN.                                  
           EVALUATE WS-K740-STATUS                                              
           WHEN 00                                                              
                PERFORM 4100-REWRITE-BP13K740  THRU 4100-EXIT                   
           WHEN 23                                                              
              DISPLAY 'BP13K740 RECORD NOT FND KEY : ' K740-NUM-REGN            
           WHEN OTHER                                                           
              DISPLAY 'BP13K740 READ ERROR, STATUS : ' WS-K740-STATUS.          
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4100-REWRITE-BP13K740.                                                   
      ******************************************************************        
           MOVE WS-K000-REGN      TO K740-NUM-JB-REGN.                          
           ACCEPT WS-CURRENT-DATE FROM DATE YYYYMMDD.                           
           MOVE WS-CURRENT-DATE   TO K740-DTE-UPDATE.                           
           MOVE 'P13C887'         TO K740-NUM-USER-ID.                          
                                                                                
           REWRITE BP13K740-REC.                                                
           IF WS-K740-STATUS = 00 OR 02                                         
              ADD 1               TO WS-K740-REWRITE                            
           ELSE                                                                 
              DISPLAY 'BP13K740 REWRITE ERROR, STATUS : ' WS-K740-STATUS        
              DISPLAY 'K740-KEY : ' K740-NUM-REGN.                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-RTN.                                                          
      ******************************************************************        
           CLOSE BP13F740                                                       
                 BP13K740                                                       
                 BP17K000.                                                      
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              MOVE WS-K740-STATUS      TO RETURN-CODE                           
              DISPLAY 'BP13K740 CLOSE ERROR, STATUS : ' WS-K740-STATUS.         
                                                                                
           IF WS-K000-STATUS NOT = 00 AND 97                                    
              MOVE WS-K000-STATUS      TO RETURN-CODE                           
              DISPLAY 'BP17K000 CLOSE ERROR, STATUS : ' WS-K000-STATUS.         
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
