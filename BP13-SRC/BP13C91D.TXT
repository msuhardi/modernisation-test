       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C91D.                                                 
      *AUTHOR.        SHEILA MAE PATIAG.                                        
      *DATE-WRITTEN.  15/03/2012.                                               
      *===============================================================*         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      *  OBJECTIVE  :  SHORTLIST PARENT UNIT FOR MGP                  *         
      *                                                               *         
      *  I-O        :  BP13K115  BP13.K115.MGPSUNIT                   *         
      *  INPUT      :  BP13FLE1  BP13.F100.SLCTUNIT.PARENT            *         
      *  OUTPUT     :  BP13F10P  BP13.F100.MGPUNIT.PARENT             *         
      *                BP13FLE2  BP13.F100.SLCTUNIT.CHILD             *         
      *                BP13FQ50  BP13.FQ50.GROUNIT                    *         
      *                BP13L91B  REPORT (PARENT UNITS)                *         
      *                                                               *         
      *===============================================================*         
      * CHG-REF#  BY  DDMMCCYY DESCRIPTION                            *         
      * -------- ---- -------- -------------------------------------- *         
      * BP134482 SMR2 15032012 NEW PROGRAM                            *         
      * BP134558 SMR2 13062012 FORMAT FQ50-NUM-UNIT                   *         
      * BP135080 SMR2 03102013 INCLUDE TYP-ACTUSE = '3' & REMOVE FQ50 *         
      * BP135140 SMR2 12112013 BYPASS PROCESSED RECORDS IN BP13K115   *         
      * BP135140 SMR2 21102013 CHANGE OUTPUT FILE FOR PARENT UNITS,   *         
      *                        USE BP13K120 INSTEAD OF BP13F100       *         
      * BP135661 SMR2 19032015 WRITE REMAINING UNITS FOR CHILD        *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K115  ASSIGN       TO BP13K115                            
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS SEQUENTIAL                                    
                  RECORD KEY   IS K115-KEY-FLD                                  
                  FILE STATUS  IS BP13K115-STATUS.                              
           SELECT BP13FLE1  ASSIGN       TO BP13FLE1.                           
           SELECT BP13K120  ASSIGN       TO BP13K120                            
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS DYNAMIC                                       
                  RECORD KEY   IS K120-KEY-FLD                                  
                  FILE STATUS  IS BP13K120-STATUS.                              
           SELECT BP13FLE2  ASSIGN       TO BP13FLE2.                           
           SELECT BP13L91B  ASSIGN       TO BP13L91B.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K115                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K115.                                                           
                                                                                
       FD  BP13FLE1                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 800 CHARACTERS.                                      
       COPY BP13F100.                                                           
                                                                                
       FD  BP13K120                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13K120.                                                           
                                                                                
       FD  BP13FLE2                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 800 CHARACTERS.                                      
       01  BP13FLE2-REC           PIC X(800).                                   
                                                                                
       FD  BP13L91B                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  BP13L91B-REC           PIC X(132).                                   
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01 WS-FILE-STATUS.                                                       
           05 BP13K115-STATUS     PIC 9(02) VALUE ZEROES.                       
           05 BP13K120-STATUS     PIC 9(02) VALUE ZEROES.                       
                                                                                
       01 WS-K115-EOF             PIC X(01) VALUE 'N'.                          
          88 K115-END-OF-FILE   VALUE 'Y'.                                      
       01 WS-FLE1-EOF             PIC X(01) VALUE 'N'.                          
          88 FLE1-END-OF-FILE   VALUE 'Y'.                                      
       01 WS-K115-PROCESS         PIC X(01) VALUE 'N'.                          
       01 WS-K115-READ-CNT        PIC 9(08) VALUE ZEROES.                       
       01 WS-FLE1-READ-CNT        PIC 9(08) VALUE ZEROES.                       
       01 WS-K115-REWRITE-CNT     PIC 9(08) VALUE ZEROES.                       
       01 WS-K120-WRITE-CNT       PIC 9(08) VALUE ZEROES.                       
       01 WS-FLE2-WRITE-CNT       PIC 9(08) VALUE ZEROES.                       
       01 WS-L91B-WRITE-CNT       PIC 9(08) VALUE ZEROES.                       
       01 WS-TYP1-CNT             PIC 9(08) VALUE ZEROES.                       
       01 WS-UNIT-REQ-CNT         PIC 9(08) VALUE ZEROES.                       
       01 WS-UNIT-SLCT-CNT        PIC 9(08) VALUE ZEROES.                       
       01 WS-K115-NOT-PARENT-CNT  PIC 9(08) VALUE ZEROES.                       
       01 WS-K120-FND-CNT         PIC 9(08) VALUE ZEROES.                       
                                                                                
       01 WS-EDT-DATE             PIC X(10) VALUE SPACES.                       
       01 WS-CUR-DATE             PIC X(08) VALUE SPACES.                       
                                                                                
       01 WS-EDIT1                PIC Z(07)9.                                   
                                                                                
       01 WS-LINE-CNT             PIC 9(02) VALUE ZEROES.                       
       01 WS-PAGE-CNT             PIC 9(06) VALUE ZEROES.                       
       01 WS-PRINT-UNIT-CNT       PIC 9(03) VALUE ZEROES.                       
       01 WS-MAX-LINES            PIC 9(02) VALUE ZEROES.                       
       01 WS-SLNO                 PIC 9(05) VALUE ZEROES.                       
       01 WS-PREV-KEY-FLD         PIC X(18) VALUE SPACES.                       
                                                                                
       01 WS-FLE1-KEY-FLD.                                                      
          05 WS-FLE1-NT           PIC X(03).                                    
          05 WS-FLE1-FT           PIC X(02).                                    
          05 WS-FLE1-NEIGH        PIC X(04).                                    
          05 WS-FLE1-CNTRCT       PIC X(04).                                    
          05 WS-FLE1-BLOCK        PIC X(05).                                    
                                                                                
       01 WS-K115-KEY-FLD.                                                      
          05 WS-K115-NT           PIC X(03).                                    
          05 WS-K115-FT           PIC X(02).                                    
          05 WS-K115-NEIGH        PIC X(04).                                    
          05 WS-K115-CNTRCT       PIC X(04).                                    
          05 WS-K115-BLOCK        PIC X(05).                                    
                                                                                
       01 WS-RIGHT-JUST.                                                        
          05 WS-UNIT              PIC X(04) VALUE SPACES.                       
          05 WS-UNIT1             PIC X(04) VALUE SPACES.                       
          05 WS-SUB               PIC 9(01) VALUE ZEROES.                       
          05 WS-SUB1              PIC 9(01) VALUE ZEROES.                       
          05 WS-CHECK             PIC 9(01) VALUE ZEROES.                       
          05 WS-CNT               PIC 9(01) VALUE ZEROES.                       
                                                                                
       01 WS-REPORT-VARS.                                                       
          05 L91-HEADER1.                                                       
             10 FILLER            PIC X(08) VALUE 'BP13L91B'.                   
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(08) VALUE 'HDBCAT 3'.                   
             10 FILLER            PIC X(29) VALUE SPACES.                       
             10 FILLER            PIC X(20) VALUE                               
                'SYSTEM OF COMMITMENT'.                                         
             10 FILLER            PIC X(23) VALUE SPACES.                       
             10 FILLER            PIC X(07) VALUE 'DATE : '.                    
             10 L91-DATE          PIC X(12) VALUE SPACES.                       
             10 FILLER            PIC X(04) VALUE SPACES.                       
             10 FILLER            PIC X(07) VALUE 'PAGE : '.                    
             10 L91-PAGE          PIC Z(05)9.                                   
          05 L91-HEADER2.                                                       
             10 FILLER            PIC X(41) VALUE SPACES.                       
             10 FILLER            PIC X(39) VALUE                               
                'MGPS FLAT LIST BY NT/FLAT TYPE (PARENT)'.                      
             10 FILLER            PIC X(47) VALUE SPACES.                       
          05 L91-HEADER4.                                                       
             10 FILLER            PIC X(12) VALUE 'NEW TOWN  : '.               
             10 L91-NT            PIC X(03) VALUE SPACES.                       
          05 L91-HEADER5.                                                       
             10 FILLER            PIC X(12) VALUE 'FLAT TYPE : '.               
             10 L91-FLAT-TYPE     PIC X(02) VALUE SPACES.                       
          05 L91-HEADER5A.                                                      
             10 FILLER            PIC X(12) VALUE 'NEIGH     : '.               
             10 L91-NGH           PIC X(04) VALUE SPACES.                       
          05 L91-HEADER5B.                                                      
             10 FILLER            PIC X(12) VALUE 'CONTRACT  : '.               
             10 L91-CNRT          PIC X(04) VALUE SPACES.                       
          05 L91-HEADER5C.                                                      
             10 FILLER            PIC X(12) VALUE 'UNITS     : '.               
             10 L91-UNITS         PIC Z9.                                       
          05 L91-HEADER6.                                                       
             10 FILLER            PIC X(33) VALUE                               
                ' S/NO    NGH   CONT  BLOCK   UNIT'.                            
             10 FILLER            PIC X(15) VALUE                               
                '         STREET'.                                              
             10 FILLER            PIC X(28) VALUE SPACES.                       
             10 FILLER            PIC X(27) VALUE                               
                'SCHEME A/C    MODL   RANDOM'.                                  
             10 FILLER            PIC X(25) VALUE SPACES.                       
          05 L91-HEADER7.                                                       
             10 FILLER            PIC X(132) VALUE ALL '-'.                     
          05 L91-DETAILS.                                                       
             10 L91-SLNO          PIC Z(4)9.                                    
             10 FILLER            PIC X(04) VALUE SPACES.                       
             10 L91-NEIGH         PIC X(04) VALUE SPACES.                       
             10 FILLER            PIC X(02) VALUE SPACES.                       
             10 L91-CNTRCT        PIC X(04) VALUE SPACES.                       
             10 FILLER            PIC X(02) VALUE SPACES.                       
             10 L91-BLOCK         PIC X(05) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 L91-UNIT          PIC X(10) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 L91-STREET        PIC X(32) VALUE SPACES.                       
             10 FILLER            PIC X(02) VALUE SPACES.                       
             10 L91-SCHACC        PIC X(11) VALUE SPACES.                       
             10 FILLER            PIC X(03) VALUE SPACES.                       
             10 L91-MODL          PIC X(02) VALUE SPACES.                       
             10 FILLER            PIC X(05) VALUE SPACES.                       
             10 L91-RANDOM        PIC X(28) VALUE SPACES.                       
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13K115      THRU 2000-EXIT                       
             UNTIL WS-K115-PROCESS = 'Y'                                        
                OR K115-END-OF-FILE.                                            
           PERFORM 3000-READ-BP13FLE1      THRU 3000-EXIT.                      
           PERFORM 4000-PROCESS-DATA       THRU 4000-EXIT                       
             UNTIL K115-END-OF-FILE.                                            
           IF K115-END-OF-FILE AND WS-FLE1-EOF = 'N'                            
              PERFORM UNTIL WS-FLE1-EOF = 'Y'                                   
                 PERFORM 4200-WRITE-FLE2      THRU 4200-EXIT                    
                 PERFORM 3000-READ-BP13FLE1   THRU 3000-EXIT                    
              END-PERFORM                                                       
           END-IF.                                                              
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  BP13FLE1                                                 
                I-O    BP13K115                                                 
                       BP13K120                                                 
                OUTPUT BP13FLE2                                                 
                       BP13L91B.                                                
                                                                                
           IF BP13K115-STATUS NOT = ZEROES AND 97                               
              DISPLAY 'OPEN ERROR, BP13K115 STATUS ' BP13K115-STATUS            
              MOVE BP13K115-STATUS         TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF BP13K120-STATUS NOT = ZEROES AND 97                               
              DISPLAY 'OPEN ERROR, BP13K120 STATUS ' BP13K120-STATUS            
              MOVE BP13K120-STATUS         TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CUR-DATE.                      
           STRING WS-CUR-DATE(7:2), '/',                                        
                  WS-CUR-DATE(5:2), '/',                                        
                  WS-CUR-DATE(1:4)  DELIMITED BY SIZE                           
                  INTO WS-EDT-DATE.                                             
                                                                                
           MOVE '55'                       TO WS-MAX-LINES.                     
           MOVE '60'                       TO WS-LINE-CNT.                      
           INITIALIZE WS-PREV-KEY-FLD                                           
                      WS-REPORT-VARS.                                           
           MOVE WS-EDT-DATE                TO L91-DATE.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13K115.                                                      
      *---------------------------------------------------------------*         
           READ BP13K115                                                        
             AT END                                                             
                MOVE 'Y'                   TO WS-K115-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           IF K115-NUM-UNIT-SLCT = SPACES OR LOW-VALUES OR '00'                 
              MOVE K115-NUM-ESTATE         TO WS-K115-NT                        
              MOVE K115-NUM-TYP-ACTUSE     TO WS-K115-FT                        
              MOVE K115-NUM-NGHBRD         TO WS-K115-NEIGH                     
              MOVE K115-NUM-CNTRCT         TO WS-K115-CNTRCT                    
              MOVE K115-NUM-BLOCK          TO WS-K115-BLOCK                     
              MOVE 'Y'                     TO WS-K115-PROCESS                   
              ADD 1                        TO WS-K115-READ-CNT                  
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-READ-BP13FLE1.                                                      
      *---------------------------------------------------------------*         
           READ BP13FLE1                                                        
             AT END                                                             
                MOVE 'Y'                   TO WS-FLE1-EOF                       
                GO TO 3000-EXIT.                                                
                                                                                
           MOVE F100-NUM-NT                TO WS-FLE1-NT.                       
           MOVE F100-NUM-TYP-ACTUSE        TO WS-FLE1-FT.                       
           MOVE F100-NUM-NGHBRD            TO WS-FLE1-NEIGH.                    
           MOVE F100-NUM-CNTRCT            TO WS-FLE1-CNTRCT.                   
           MOVE F100-NUM-BLOCK             TO WS-FLE1-BLOCK.                    
                                                                                
           ADD 1                           TO WS-FLE1-READ-CNT.                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-PROCESS-DATA.                                                       
      *---------------------------------------------------------------*         
           MOVE 'N'                         TO WS-K115-PROCESS.                 
           IF FLE1-END-OF-FILE                                                  
              IF K115-NUM-UNIT-TAG = 'P'                                        
                 MOVE ZEROES                TO WS-UNIT-SLCT-CNT                 
                 PERFORM 7000-UPDATE-K115   THRU 7000-EXIT                      
              ELSE                                                              
                 ADD 1                      TO WS-K115-NOT-PARENT-CNT           
              END-IF                                                            
              PERFORM 2000-READ-BP13K115    THRU 2000-EXIT                      
                UNTIL WS-K115-PROCESS = 'Y'                                     
                   OR K115-END-OF-FILE                                          
              GO TO 4000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF K115-NUM-UNIT-TAG = 'P'                                           
              EVALUATE TRUE                                                     
              WHEN WS-K115-KEY-FLD = WS-FLE1-KEY-FLD                            
                 MOVE ZEROES                TO WS-UNIT-REQ-CNT                  
                                               WS-UNIT-SLCT-CNT                 
                 PERFORM 4100-PROCESS-FLE1  THRU 4100-EXIT                      
                   UNTIL WS-K115-KEY-FLD NOT = WS-FLE1-KEY-FLD                  
                      OR K115-END-OF-FILE                                       
                      OR FLE1-END-OF-FILE                                       
                 PERFORM 7000-UPDATE-K115   THRU 7000-EXIT                      
                 PERFORM 2000-READ-BP13K115 THRU 2000-EXIT                      
                   UNTIL WS-K115-PROCESS = 'Y'                                  
                      OR K115-END-OF-FILE                                       
              WHEN WS-K115-KEY-FLD < WS-FLE1-KEY-FLD                            
                 MOVE ZEROES                TO WS-UNIT-SLCT-CNT                 
                 PERFORM 7000-UPDATE-K115   THRU 7000-EXIT                      
                 PERFORM 2000-READ-BP13K115 THRU 2000-EXIT                      
                   UNTIL WS-K115-PROCESS = 'Y'                                  
                      OR K115-END-OF-FILE                                       
              WHEN WS-K115-KEY-FLD > WS-FLE1-KEY-FLD                            
                 PERFORM 4200-WRITE-FLE2    THRU 4200-EXIT                      
                 PERFORM 3000-READ-BP13FLE1 THRU 3000-EXIT                      
              END-EVALUATE                                                      
           ELSE                                                                 
              ADD 1                         TO WS-K115-NOT-PARENT-CNT           
              PERFORM 2000-READ-BP13K115    THRU 2000-EXIT                      
                UNTIL WS-K115-PROCESS = 'Y'                                     
                   OR K115-END-OF-FILE                                          
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4100-PROCESS-FLE1.                                                       
      *---------------------------------------------------------------*         
           IF K115-NUM-UNIT-REQ > WS-UNIT-REQ-CNT                               
              PERFORM 4300-WRITE-K120      THRU 4300-EXIT                       
              ADD 1                        TO WS-UNIT-SLCT-CNT                  
              PERFORM 6000-REPORT-L91B     THRU 6000-EXIT                       
           ELSE                                                                 
              PERFORM 4200-WRITE-FLE2      THRU 4200-EXIT                       
           END-IF.                                                              
           PERFORM 3000-READ-BP13FLE1      THRU 3000-EXIT.                      
           ADD 1                           TO WS-UNIT-REQ-CNT.                  
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4200-WRITE-FLE2.                                                         
      *---------------------------------------------------------------*         
           IF F100-NUM-TYP-ACTUSE(1:1) = '2' OR '3'                             
              WRITE BP13FLE2-REC           FROM BP13F100-REC                    
              ADD 1                        TO WS-FLE2-WRITE-CNT                 
           ELSE                                                                 
              ADD 1                        TO WS-TYP1-CNT                       
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4300-WRITE-K120.                                                         
      *---------------------------------------------------------------*         
           MOVE SPACE                       TO BP13K120-REC.                    
           MOVE F100-NUM-NT                 TO K120-NUM-NT.                     
           MOVE F100-NUM-NGHBRD             TO K120-NUM-NGHBRD.                 
           MOVE F100-NUM-CNTRCT             TO K120-NUM-CNTRCT.                 
           MOVE F100-NUM-BLOCK              TO K120-NUM-BLOCK.                  
           MOVE F100-NUM-TYP-ACTUSE(1:1)    TO K120-NUM-FT.                     
           MOVE F100-NUM-RANDOM             TO K120-NUM-RANDOM.                 
           MOVE F100-NUM-LEVEL              TO K120-NUM-LEVEL.                  
           MOVE F100-NUM-MAIN               TO WS-UNIT.                         
           PERFORM 4400-GET-MAIN-UNIT       THRU 4400-EXIT.                     
           MOVE WS-UNIT1                    TO K120-NUM-MAIN.                   
           MOVE F100-NUM-SUB                TO K120-NUM-SUB.                    
           MOVE 'P'                         TO K120-NUM-UNIT-TAG.               
           MOVE F100-NUM-MODL-DESG          TO K120-NUM-MODL-DESG.              
           MOVE F100-NME-STREET             TO K120-NME-STREET.                 
           MOVE F100-NUM-ESTATE             TO K120-NUM-ESTATE.                 
           MOVE F100-NUM-WARD               TO K120-NUM-WARD.                   
           MOVE F100-NUM-ALLO-CAT           TO K120-NUM-ALLO-CAT.               
           MOVE F100-DTE-BALLOT             TO K120-DTE-BALLOT.                 
           MOVE F100-NUM-PARENT-FT          TO K120-NUM-PARENT-FT.              
           MOVE F100-NUM-TYP-ACTUSE         TO K120-NUM-TYP-ACTUSE.             
           MOVE F100-NUM-HDB-REF            TO K120-NUM-HDB-REF.                
           MOVE F100-NUM-PRIO               TO K120-NUM-PRIO.                   
           MOVE F100-NUM-FT-BALLOT          TO K120-NUM-FT-BALLOT.              
           MOVE F100-NUM-PRIO-UPDATE        TO K120-NUM-PRIO-UPDATE.            
           MOVE WS-CUR-DATE                 TO K120-DTE-UPDATE.                 
           ACCEPT K120-TME-UPDATE           FROM TIME.                          
           MOVE 'BP13C91D'                  TO K120-NUM-USERID.                 
           WRITE BP13K120-REC.                                                  
                                                                                
           IF BP13K120-STATUS = 00 OR 02                                        
              ADD 1                         TO WS-K120-WRITE-CNT                
           ELSE                                                                 
              IF BP13K120-STATUS = 22                                           
                 ADD 1                      TO WS-K120-FND-CNT                  
              ELSE                                                              
                 DISPLAY 'ERROR WRITING - BP13K120 : ' BP13K120-STATUS          
                 MOVE BP13K120-STATUS       TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4400-GET-MAIN-UNIT.                                                      
      *---------------------------------------------------------------*         
           MOVE SPACES                     TO WS-UNIT1.                         
           MOVE ZEROES                     TO WS-SUB1                           
                                              WS-CHECK.                         
                                                                                
           PERFORM VARYING WS-SUB FROM 1 BY 1                                   
             UNTIL WS-SUB > 4                                                   
             IF WS-UNIT(WS-SUB:1) = SPACES OR LOW-VALUES                        
                ADD 1                      TO WS-SUB1                           
             END-IF                                                             
           END-PERFORM.                                                         
                                                                                
           IF WS-SUB1 > 0                                                       
              MOVE SPACES                  TO WS-UNIT1(1:WS-SUB1)               
              COMPUTE WS-CNT = 4 - WS-SUB1                                      
                                                                                
              PERFORM VARYING WS-CHECK FROM 1 BY 1                              
                UNTIL WS-CNT = 0                                                
                 IF WS-UNIT(WS-CHECK:1) NOT = SPACES AND LOW-VALUES             
                    SUBTRACT 1               FROM WS-CNT                        
                    ADD 1                    TO WS-SUB1                         
                    MOVE WS-UNIT(WS-CHECK:1) TO WS-UNIT1(WS-SUB1:1)             
                 END-IF                                                         
              END-PERFORM                                                       
           ELSE                                                                 
              MOVE WS-UNIT                 TO WS-UNIT1                          
           END-IF.                                                              
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       6000-REPORT-L91B.                                                        
      *---------------------------------------------------------------*         
           IF (WS-LINE-CNT > WS-MAX-LINES) OR                                   
              (WS-K115-KEY-FLD NOT = WS-PREV-KEY-FLD)                           
              MOVE SPACES                  TO BP13L91B-REC                      
              IF (WS-K115-KEY-FLD NOT = WS-PREV-KEY-FLD)                        
                 MOVE WS-K115-KEY-FLD      TO WS-PREV-KEY-FLD                   
                 MOVE ZEROES               TO WS-SLNO                           
                                              WS-PAGE-CNT                       
              END-IF                                                            
              ADD 1                        TO WS-PAGE-CNT                       
              MOVE ZEROES                  TO WS-LINE-CNT                       
              MOVE WS-PAGE-CNT             TO L91-PAGE                          
              MOVE K115-NUM-ESTATE         TO L91-NT                            
              MOVE K115-NUM-TYP-ACTUSE     TO L91-FLAT-TYPE                     
              MOVE K115-NUM-NGHBRD         TO L91-NGH                           
              MOVE K115-NUM-CNTRCT         TO L91-CNRT                          
              MOVE K115-NUM-UNIT-REQ       TO L91-UNITS                         
              WRITE BP13L91B-REC           FROM L91-HEADER1 AFTER PAGE          
              WRITE BP13L91B-REC           FROM L91-HEADER2                     
              WRITE BP13L91B-REC           FROM L91-HEADER4                     
              WRITE BP13L91B-REC           FROM L91-HEADER5                     
              WRITE BP13L91B-REC           FROM L91-HEADER5A                    
              WRITE BP13L91B-REC           FROM L91-HEADER5B                    
              WRITE BP13L91B-REC           FROM L91-HEADER5C                    
              WRITE BP13L91B-REC           FROM L91-HEADER6 AFTER 2             
              WRITE BP13L91B-REC           FROM L91-HEADER7                     
              ADD 10                       TO WS-LINE-CNT                       
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO L91-DETAILS.                      
           INITIALIZE L91-DETAILS.                                              
           ADD 1                           TO WS-SLNO.                          
           MOVE WS-SLNO                    TO L91-SLNO.                         
           MOVE F100-NUM-NGHBRD            TO L91-NEIGH.                        
           MOVE F100-NUM-CNTRCT            TO L91-CNTRCT.                       
           MOVE F100-NUM-BLOCK             TO L91-BLOCK.                        
           MOVE F100-NUM-UNIT              TO L91-UNIT.                         
           MOVE F100-NME-STREET            TO L91-STREET.                       
           MOVE F100-NUM-HDB-REF           TO L91-SCHACC.                       
           MOVE F100-NUM-MODL-DESG         TO L91-MODL.                         
           MOVE F100-NUM-RANDOM            TO L91-RANDOM.                       
                                                                                
           IF F100-NUM-SUB = SPACES OR LOW-VALUES                               
              STRING F100-NUM-LEVEL '-'                                         
                     F100-NUM-MAIN                                              
                     DELIMITED BY SIZE INTO L91-UNIT                            
           ELSE                                                                 
              STRING F100-NUM-LEVEL '-'                                         
                     F100-NUM-MAIN '-'                                          
                     F100-NUM-SUB                                               
                     DELIMITED BY SIZE INTO L91-UNIT                            
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO BP13L91B-REC.                     
           WRITE BP13L91B-REC              FROM L91-DETAILS.                    
           ADD 1                           TO WS-LINE-CNT                       
                                              WS-L91B-WRITE-CNT.                
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       7000-UPDATE-K115.                                                        
      *---------------------------------------------------------------*         
           MOVE WS-UNIT-SLCT-CNT           TO K115-NUM-UNIT-SLCT.               
           REWRITE BP13K115-REC.                                                
                                                                                
           IF BP13K115-STATUS NOT = ZEROES                                      
              DISPLAY 'UPDATE ERROR, BP13K115 STATUS ' BP13K115-STATUS          
              MOVE BP13K115-STATUS         TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           ELSE                                                                 
              ADD 1                        TO WS-K115-REWRITE-CNT               
           END-IF.                                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           CLOSE   BP13FLE1                                                     
                   BP13K115                                                     
                   BP13K120                                                     
                   BP13FLE2                                                     
                   BP13L91B.                                                    
                                                                                
           IF BP13K115-STATUS NOT = ZEROES AND 97                               
              DISPLAY 'CLOSE ERROR, BP13K115 STATUS ' BP13K115-STATUS           
              MOVE BP13K115-STATUS         TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF BP13K120-STATUS NOT = ZEROES AND 97                               
              DISPLAY 'CLOSE ERROR, BP13K120 STATUS ' BP13K120-STATUS           
              MOVE BP13K120-STATUS         TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13C91D       ' WS-EDT-DATE.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-K115-READ-CNT           TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS READ FROM BP13K115  : ' WS-EDIT1.                
           MOVE WS-FLE1-READ-CNT           TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS READ FROM BP13FLE1  : ' WS-EDIT1.                
           MOVE WS-K115-REWRITE-CNT        TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS UPDATED ON BP13K115 : ' WS-EDIT1.                
           MOVE WS-K120-WRITE-CNT          TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS WRITTEN TO BP13K120 : ' WS-EDIT1.                
           MOVE WS-FLE2-WRITE-CNT          TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS WRITTEN TO BP13FLE2 : ' WS-EDIT1.                
           MOVE WS-TYP1-CNT                TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS WITH TYP-ACTUSE = 1 : ' WS-EDIT1.                
           MOVE WS-L91B-WRITE-CNT          TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS WRITTEN TO BP13L91B : ' WS-EDIT1.                
           MOVE WS-K115-NOT-PARENT-CNT     TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS W/ UNIT-TAG NOT = P : ' WS-EDIT1.                
           MOVE WS-K120-FND-CNT            TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS FOUND FROM BP13K120 : ' WS-EDIT1.                
           DISPLAY '*---------------------------------------------*'.           
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
