      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
       PROGRAM-ID.    BP13C134.                                                 
      *AUTHOR.        ERWIN GAW.                                                
      *DATE-WRITTEN.  29/03/96.                                                 
                                                                                
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME  :    SYSTEM OF COMMITMENT   (SOC)               *        
      *                                                                *        
      *   SYSTEM ID    :    BP13                                       *        
      *                                                                *        
      *   OBJECTIVE    :    TO PRINT SUMMARY AGEING REPORT ON  SUB-TRF *        
      *                     BASED ON THE PRINT FILE PRODUCED BY        *        
      *                     BP13C133.                                  *        
      *                                                                *        
      *   INPUT FILES  :    PRTFILE                                    *        
      *                                                                *        
      *   REPORT       :    1. BP13L134                                *        
      *                                                                *        
      *   MODIFICATION HISTORY:                                        *        
      *   =====================                                        *        
      *   CHGE-NO  BY    DATE   DESCRIPTION                            *        
      *   -------  ---  ------  -------------------------------------  *        
      * BP130103   EG1  29031996 TO PRINT AGING REPORT FOR SUBTRANSFER.*        
      * BP130227   RHB  03011997 TO PRINT ONLY SUSPENSE > 0.           *        
      * BP130418   CLT  25081998 TO CATER FOR Y2K.                     *        
      * BP132027   MALC 25092001 TO CATER FOR DETAIL PRINTING.         *        
      ******************************************************************        
       ENVIRONMENT    DIVISION.                                                 
      ******************************************************************        
      *================================================================*        
       CONFIGURATION  SECTION.                                                  
      *================================================================*        
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
      *================================================================*        
       INPUT-OUTPUT SECTION.                                                    
      *================================================================*        
      *----------------------------------------------------------------*        
       FILE-CONTROL.                                                            
      *----------------------------------------------------------------*        
                                                                                
           SELECT PRTFILE   ASSIGN       TO PRTFILE.                            
           SELECT BP13L134  ASSIGN       TO BP13L134.                           
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      *================================================================*        
       FILE SECTION.                                                            
      *================================================================*        
                                                                                
       FD   PRTFILE                                                             
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 420 CHARACTERS.                                     
       01   IN-REC.                                                             
            05 IN-K885-REC               PIC X(400).                            
            05 IN-FSA-DATE               PIC X(08).                             
            05 IN-AGE-MONTHS             PIC 9(02).                             
            05 IN-AGE-DAYS               PIC 9(02).                             
            05 FILLER                    PIC X(08).                             
                                                                                
       FD   BP13L134                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   L134-REC            PIC X(132).                                     
                                                                                
      *================================================================*        
       WORKING-STORAGE SECTION.                                                 
      *================================================================*        
      *----------------------------------------------------------------*        
      *     ERROR VARIABLES                                            *        
      *----------------------------------------------------------------*        
       01  WS-ERR-MSG                     PIC X(80).                            
                                                                                
      *----------------------------------------------------------------*        
      *     BP13F885 COPYCOB                                           *        
      *----------------------------------------------------------------*        
       COPY BP13F885.                                                           
                                                                                
      *----------------------------------------------------------------*        
      *     FLAG VARIABLES                                             *        
      *----------------------------------------------------------------*        
       01  WS-FLAGS.                                                            
           05  WS-PRTFILE-EOF             PIC X(1)   VALUE 'N'.                 
                                                                                
      *----------------------------------------------------------------*        
      *    COUNTER VARIABLES                                           *        
      *----------------------------------------------------------------*        
       01  WS-COUNTERS.                                                         
           05  WS-CNT-PRTFILE-READ        PIC 9(8)   VALUE ZEROES.              
           05  WS-CNT-L134-WRITE          PIC 9(8)   VALUE ZEROES.              
           05  WS-FTR-SW                  PIC 9(8)   VALUE ZEROES.              
                                                                                
      *----------------------------------------------------------------*        
      *    PRINTING VARIABLES                                          *        
      *----------------------------------------------------------------*        
       01  WS-PRINT-VARIABLES.                                                  
           05  WS-SEQ-CNT                 PIC 9(2)   VALUE ZEROES.              
           05  WS-PAGE-CNT                PIC 9(5)   VALUE 0.                   
           05  WS-LINE-CNT                PIC 9(2)   VALUE 60.                  
                                                                                
      *----------------------------------------------------------------*        
      *  MISCELLEANEOUS VARIABLES                                      *        
      *----------------------------------------------------------------*        
       01  WS-COMPUTE-VARIABLES.                                                
           05  WS-AMT-TOTAL-BALANCE       PIC S9(10)V99 VALUE +0.               
           05  WS-NO-TOTAL-CASES          PIC 9(8)      VALUE  0.               
           05  WS-AMT-LE-2-BALANCE        PIC S9(10)V99 VALUE +0.               
           05  WS-NO-LE-2-CASES           PIC 9(8)      VALUE  0.               
           05  WS-AMT-2-LE-3-BALANCE      PIC S9(10)V99 VALUE +0.               
           05  WS-NO-2-LE-3-CASES         PIC 9(8)      VALUE  0.               
           05  WS-AMT-GT-3-BALANCE        PIC S9(10)V99 VALUE +0.               
           05  WS-NO-GT-3-CASES           PIC 9(8)      VALUE  0.               
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURR-CC                 PIC X(2).                             
           05  WS-CURR-YY                 PIC X(2).                             
           05  WS-CURR-MM                 PIC X(2).                             
           05  WS-CURR-DD                 PIC X(2).                             
                                                                                
      *----------------------------------------------------------------*        
      *    REPORT LAYOUT OF SUMMARY AGEING REPORT.                     *        
      *----------------------------------------------------------------*        
       01  L134-PRT-HDR-1.                                                      
           05  FILLER                     PIC X      VALUE SPACE.               
           05  FILLER                     PIC X(8)   VALUE 'BP13L134'.          
           05  FILLER                     PIC X(13)  VALUE SPACES.              
           05  FILLER                     PIC X(4)   VALUE 'HDB3'.              
           05  FILLER                     PIC X(14)  VALUE SPACES.              
           05  FILLER                     PIC X(39)                             
               VALUE 'S Y S T E M   O F   C O M M I T M E N T'.                 
           05  FILLER                     PIC X(10)  VALUE SPACES.              
           05  FILLER                     PIC X(7)   VALUE 'DATE : '.           
           05  L134-PRT-DATE              PIC X(10)  VALUE SPACES.              
           05  FILLER                     PIC X(10)  VALUE SPACES.              
           05  FILLER                     PIC X(9)   VALUE 'PAGE   : '.         
           05  L134-PRT-PAGE              PIC ZZ9.                              
                                                                                
       01  L134-PRT-HDR-2.                                                      
           05  FILLER                     PIC X(34)  VALUE SPACES.              
           05  FILLER                     PIC X(40)                             
           VALUE 'SUMMARY AGEING REPORT ON SUB-TRANFER AT '.                    
           05  L134-RUN-DATE              PIC X(10).                            
                                                                                
       01  L134-PRT-HDR-3.                                                      
           05  FILLER                     PIC X(51)  VALUE SPACES.              
           05  FILLER                     PIC X(17)                             
               VALUE ' NUMBER OF CASES '.                                       
           05  FILLER                     PIC X(09)  VALUE SPACES.              
           05  FILLER                     PIC X(17)                             
               VALUE '    AMOUNT ($)   '.                                       
                                                                                
       01  L134-PRT-HDR-4.                                                      
           05  FILLER                     PIC X(51)  VALUE SPACES.              
           05  FILLER                     PIC X(17)                             
               VALUE '================='.                                       
           05  FILLER                     PIC X(09)  VALUE SPACES.              
           05  FILLER                     PIC X(17)                             
               VALUE '================='.                                       
                                                                                
       01  L134-PRT-SUB-HDR.                                                    
           05  FILLER                     PIC X(28)  VALUE SPACES.              
           05  FILLER                     PIC X(42)                             
               VALUE 'S/N           REGN #        SCHEME ACCT NO'.              
           05  FILLER                     PIC X(11)  VALUE SPACES.              
           05  FILLER                     PIC X(10) VALUE 'AMOUNT ($)'.         
                                                                                
       01  L134-PRT-END-LINE.                                                   
           05  FILLER                     PIC X(28)  VALUE SPACES.              
           05  END-PRINT                  PIC X(60)  VALUE SPACES.              
                                                                                
       01  L134-PRT-SUB-LINE.                                                   
           05  FILLER                     PIC X(25)  VALUE SPACES.              
           05  FILLER                     PIC X(69)  VALUE ALL '-'.             
                                                                                
       01  L134-PRT-DTL-A.                                                      
           05  FILLER                     PIC X(25)  VALUE SPACES.              
           05  L134-SERIAL-NO             PIC 9(7)   VALUE ZEROES.              
           05  FILLER                     PIC X(08)  VALUE SPACES.              
           05  L134-REGN-NO               PIC X(08)  VALUE SPACES.              
           05  FILLER                     PIC X(08)  VALUE SPACES.              
           05  L134-SCH-ACCT-NO           PIC X(14)  VALUE SPACES.              
           05  FILLER                     PIC X(10)  VALUE SPACES.              
           05  L134-SUSPENSE-AMT          PIC ZZZ,ZZZ,ZZZ.99.                   
                                                                                
       01  L134-PRT-DTL-1.                                                      
           05  FILLER                     PIC X(04)  VALUE SPACES.              
           05  FILLER                     PIC X(51)                             
               VALUE 'BALANCE AS AT END OF MONTH'.                              
           05  FILLER                     PIC X(03)  VALUE SPACES.              
           05  L134-TOTAL-CASES           PIC ZZ,ZZZ,ZZ9.                       
           05  FILLER                     PIC X(10)  VALUE SPACES.              
           05  L134-TOTAL-BALANCE-AMT     PIC Z,ZZZ,ZZZ,ZZ9.99.                 
                                                                                
       01  L134-PRT-DTL-2.                                                      
           05  FILLER                     PIC X(51)  VALUE SPACES.              
           05  L134-CASE-LINE             PIC X(17)                             
               VALUE '-----------------'.                                       
           05  FILLER                     PIC X(09)  VALUE SPACES.              
           05  FILLER                     PIC X(17)                             
               VALUE '-----------------'.                                       
                                                                                
       01  L134-PRT-DTL-3.                                                      
           05  FILLER                     PIC X(56)                             
               VALUE 'BALANCE <= 2 MONTHS OLD'.                                 
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  L134-LE-2-TOTAL-CASES      PIC ZZ,ZZZ,ZZ9.                       
           05  FILLER                     PIC X(10)  VALUE SPACES.              
           05  L134-LE-2-TOTAL-BAL-AMT    PIC Z,ZZZ,ZZZ,ZZ9.99.                 
                                                                                
       01  L134-PRT-DTL-4.                                                      
           05  FILLER                     PIC X(56)                             
               VALUE 'BALANCE > 2 MONTHS OLD BUT <= 3 MONTHS OLD'.              
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  L134-2-LE-3-TOTAL-CASES    PIC ZZ,ZZZ,ZZ9.                       
           05  FILLER                     PIC X(10)  VALUE SPACES.              
           05  L134-2-LE-3-TOTAL-BAL-AMT  PIC Z,ZZZ,ZZZ,ZZ9.99.                 
                                                                                
       01  L134-PRT-DTL-5.                                                      
           05  FILLER                     PIC X(56)                             
               VALUE 'BALANCE > 3 MONTHS OLD'.                                  
           05  FILLER                     PIC X(02)  VALUE SPACES.              
           05  L134-GT-3-TOTAL-CASES      PIC ZZ,ZZZ,ZZ9.                       
           05  FILLER                     PIC X(10)  VALUE SPACES.              
           05  L134-GT-3-TOTAL-BAL-AMT    PIC Z,ZZZ,ZZZ,ZZ9.99.                 
                                                                                
       01  L134-PRT-DTL-6.                                                      
           05  FILLER                     PIC X(06)                             
               VALUE 'NOTE: '.                                                  
           05  FILLER                     PIC X(30)                             
               VALUE 'AGEING IS THE DIFFERENCE BETWE'.                          
           05  FILLER                     PIC X(40)                             
               VALUE 'EN RUN DATE AND SUB-TRANSFER ORDER DATE '.                
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      *================================================================*        
       0000-MAIN-ROUTINE.                                                       
      *================================================================*        
           PERFORM 1000-INITIALIZATION      THRU 1000-EXIT.                     
           PERFORM 2000-PROCESS-ROUTINE     THRU 2000-EXIT                      
                   UNTIL WS-PRTFILE-EOF = 'Y'.                                  
                                                                                
           IF WS-CNT-PRTFILE-READ > ZEROES                                      
              PERFORM 3100-PRINT-FOOTER     THRU 3100-EXIT                      
           ELSE                                                                 
              PERFORM 3200-PRINT-HDG        THRU 3200-EXIT                      
              MOVE 'BP13F885 NO RECORD !!!' TO END-PRINT                        
              WRITE L134-REC FROM L134-PRT-END-LINE                             
           END-IF.                                                              
                                                                                
           PERFORM 9999-CLOSE-FILES         THRU 9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       1000-INITIALIZATION.                                                     
      *================================================================*        
           OPEN INPUT  PRTFILE                                                  
                OUTPUT BP13L134.                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
                                                                                
           STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CC WS-CURR-YY           
                  DELIMITED BY SIZE INTO L134-PRT-DATE.                         
                                                                                
           READ  PRTFILE  AT END                                                
                 MOVE 'Y'          TO  WS-PRTFILE-EOF.                          
                                                                                
           IF WS-PRTFILE-EOF = 'Y'                                              
              STRING 'NO PRINT FILE RECORDS FOUND'                              
                     DELIMITED BY SIZE INTO WS-ERR-MSG                          
           ELSE                                                                 
              ADD 1                TO WS-CNT-PRTFILE-READ                       
              MOVE IN-K885-REC     TO BP13F885-REC                              
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       2000-PROCESS-ROUTINE.                                                    
      *================================================================*        
           PERFORM 3000-PROCESS-PRINTING THRU 3000-EXIT.                        
                                                                                
           IF (IN-AGE-MONTHS < 2) OR                                            
              (IN-AGE-MONTHS = 2 AND IN-AGE-DAYS = 0)                           
              ADD 1                  TO WS-NO-LE-2-CASES                        
              ADD F885-AMT-SUSPENSE  TO WS-AMT-LE-2-BALANCE                     
           ELSE                                                                 
              IF (IN-AGE-MONTHS = 2 AND IN-AGE-DAYS > 0) OR                     
                 (IN-AGE-MONTHS = 3 AND IN-AGE-DAYS = 0)                        
                 ADD 1                 TO WS-NO-2-LE-3-CASES                    
                 ADD F885-AMT-SUSPENSE TO WS-AMT-2-LE-3-BALANCE                 
              ELSE                                                              
                 ADD 1                 TO WS-NO-GT-3-CASES                      
                 ADD F885-AMT-SUSPENSE TO WS-AMT-GT-3-BALANCE                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           READ  PRTFILE  AT END                                                
                 MOVE 'Y'        TO WS-PRTFILE-EOF                              
                 GO  TO 2000-EXIT                                               
                 END-READ.                                                      
                                                                                
           ADD   1                   TO WS-CNT-PRTFILE-READ.                    
           MOVE IN-K885-REC          TO BP13F885-REC.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       3000-PROCESS-PRINTING.                                                   
      *================================================================*        
           ADD 1                   TO L134-SERIAL-NO.                           
           MOVE F885-NUM-REGN      TO L134-REGN-NO.                             
           MOVE F885-AMT-SUSPENSE  TO L134-SUSPENSE-AMT.                        
                                                                                
           STRING F885-NUM-SCH-ACCT(1:4) '-'                                    
                  F885-NUM-SCH-ACCT(5:4) '-'                                    
                  F885-NUM-SCH-ACCT(9:1) '-'                                    
                  F885-NUM-SCH-ACCT(10:1) DELIMITED BY SIZE                     
                  INTO L134-SCH-ACCT-NO                                         
           END-STRING.                                                          
                                                                                
           PERFORM 3200-PRINT-HDG THRU 3200-EXIT.                               
           WRITE L134-REC FROM L134-PRT-DTL-A   AFTER 1.                        
           ADD 1 TO WS-LINE-CNT.                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       3100-PRINT-FOOTER.                                                       
      *================================================================*        
           COMPUTE WS-NO-TOTAL-CASES = WS-NO-LE-2-CASES   +                     
                                       WS-NO-2-LE-3-CASES +                     
                                       WS-NO-GT-3-CASES.                        
                                                                                
           COMPUTE WS-AMT-TOTAL-BALANCE = WS-AMT-LE-2-BALANCE   +               
                                       WS-AMT-2-LE-3-BALANCE    +               
                                       WS-AMT-GT-3-BALANCE.                     
                                                                                
           IF WS-AMT-LE-2-BALANCE > 0                                           
              MOVE WS-NO-LE-2-CASES    TO L134-LE-2-TOTAL-CASES                 
              MOVE WS-AMT-LE-2-BALANCE TO L134-LE-2-TOTAL-BAL-AMT               
           ELSE                                                                 
              MOVE ZEROES              TO L134-LE-2-TOTAL-CASES                 
                                          L134-LE-2-TOTAL-BAL-AMT.              
                                                                                
           IF WS-AMT-2-LE-3-BALANCE > 0                                         
              MOVE WS-NO-2-LE-3-CASES     TO L134-2-LE-3-TOTAL-CASES            
              MOVE WS-AMT-2-LE-3-BALANCE  TO L134-2-LE-3-TOTAL-BAL-AMT          
           ELSE                                                                 
              MOVE ZEROES                 TO L134-2-LE-3-TOTAL-CASES            
                                             L134-2-LE-3-TOTAL-BAL-AMT.         
                                                                                
           IF WS-AMT-GT-3-BALANCE > 0                                           
              MOVE WS-NO-GT-3-CASES       TO L134-GT-3-TOTAL-CASES              
              MOVE WS-AMT-GT-3-BALANCE    TO L134-GT-3-TOTAL-BAL-AMT            
           ELSE                                                                 
              MOVE ZEROES                 TO L134-GT-3-TOTAL-CASES              
                                             L134-GT-3-TOTAL-BAL-AMT.           
                                                                                
           MOVE WS-AMT-TOTAL-BALANCE      TO L134-TOTAL-BALANCE-AMT.            
                                                                                
           IF WS-LINE-CNT > 53                                                  
              MOVE 60                     TO WS-LINE-CNT                        
              PERFORM 3200-PRINT-HDG THRU 3200-EXIT                             
           END-IF.                                                              
           MOVE  SPACES                   TO L134-CASE-LINE                     
           WRITE L134-REC    FROM L134-PRT-DTL-2   AFTER 1.                     
           WRITE L134-REC    FROM L134-PRT-DTL-1   AFTER 1.                     
           WRITE L134-REC    FROM L134-PRT-DTL-2   AFTER 1.                     
                                                                                
           MOVE  60                       TO WS-LINE-CNT.                       
           MOVE  1                        TO WS-FTR-SW.                         
           PERFORM 3200-PRINT-HDG THRU    3200-EXIT.                            
           MOVE  WS-NO-TOTAL-CASES        TO L134-TOTAL-CASES.                  
                                                                                
           WRITE L134-REC    FROM L134-PRT-HDR-3   AFTER 3.                     
           WRITE L134-REC    FROM L134-PRT-HDR-4   AFTER 1.                     
                                                                                
           MOVE  ALL '-'                  TO L134-CASE-LINE.                    
           WRITE L134-REC    FROM L134-PRT-DTL-1   AFTER 3.                     
           WRITE L134-REC    FROM L134-PRT-DTL-2   AFTER 1.                     
                                                                                
           IF WS-AMT-LE-2-BALANCE > 0                                           
              WRITE L134-REC FROM L134-PRT-DTL-3   AFTER 4.                     
                                                                                
           IF WS-AMT-2-LE-3-BALANCE > 0                                         
              WRITE L134-REC FROM L134-PRT-DTL-4   AFTER 2.                     
                                                                                
           IF WS-AMT-GT-3-BALANCE > 0                                           
              WRITE L134-REC FROM L134-PRT-DTL-5   AFTER 2.                     
                                                                                
           WRITE L134-REC    FROM L134-PRT-DTL-6   AFTER 6.                     
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       3200-PRINT-HDG.                                                          
      *================================================================*        
           IF WS-LINE-CNT > 54                                                  
              ADD 1                  TO WS-PAGE-CNT                             
              MOVE WS-PAGE-CNT       TO L134-PRT-PAGE                           
              MOVE L134-PRT-DATE     TO L134-RUN-DATE                           
                                                                                
              WRITE L134-REC FROM L134-PRT-HDR-1    AFTER PAGE                  
              WRITE L134-REC FROM L134-PRT-HDR-2    AFTER 2                     
              IF WS-FTR-SW NOT = 1                                              
                 WRITE L134-REC FROM L134-PRT-SUB-HDR  AFTER 2                  
                 WRITE L134-REC FROM L134-PRT-SUB-LINE AFTER 1                  
              END-IF                                                            
              MOVE SPACES            TO L134-PRT-END-LINE                       
              WRITE L134-REC FROM L134-PRT-END-LINE AFTER 1                     
              MOVE  8                TO WS-LINE-CNT                             
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       9999-CLOSE-FILES.                                                        
      *================================================================*        
            MOVE '********   E N D   O F   R E P O R T   ********'              
                 TO END-PRINT.                                                  
            WRITE L134-REC FROM L134-PRT-END-LINE AFTER 5.                      
                                                                                
            CLOSE   PRTFILE                                                     
                    BP13L134.                                                   
                                                                                
            DISPLAY '***************************************'                   
            DISPLAY '*    BP13C134  CONTROL TOTAL          *'                   
            DISPLAY '***************************************'                   
            DISPLAY '* NO. OF INPUT READ : ' WS-CNT-PRTFILE-READ.               
            DISPLAY '***************************************'                   
            DISPLAY '* REMARKS: ' WS-ERR-MSG.                                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
