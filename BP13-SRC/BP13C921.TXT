       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C921.                                                    
       AUTHOR.     SURESH.                                                      
       INSTALLATION. HDB.                                                       
       DATE-WRITTEN. 16-10-2001.                                                
       DATE-COMPILED.                                                           
      *-----------------------------------------------------------*             
      *        SYSTEM OF COMMITMENT - BP13                        *             
      *-----------------------------------------------------------*             
      * OBJECTIVES :                                              *             
      *                                                           *             
      *  GET ROM INFORMATION FROM CDB, BE01F416, UPDATE SOC       *             
      *  BP13K517, THE INFOR IS FOR ENQUIRY IN BP13B772.          *             
      *                                                           *             
      * INPUT  FILE   : BE01F416 (INTERFACE FILE FROM CDB)        *             
      * OUTPUT FILE   : BP13F517 (OCCUP FILE)                     *             
      *                                                           *             
      * REQ NO.   DATE    BY  DESCRIPTION                         *             
      * -------   ----    --- -----------                         *             
      * BP132025 16102001 SN  NEW PROGRAM                         *             
      *                                                           *             
      *-----------------------------------------------------------*             
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F517   ASSIGN TO BP13F517.                                
                                                                                
           SELECT BE01F416   ASSIGN TO BE01F416.                                
                                                                                
           SELECT  BP13K517   ASSIGN       TO  BP13K517                         
                   ACCESS MODE             IS  DYNAMIC                          
                   ORGANIZATION            IS  INDEXED                          
                   RECORD KEY              IS  K517-KEY-FLD                     
                   FILE STATUS             IS  BP13K517-STATUS.                 
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD BE01F416.                                                             
       COPY BE01F416.                                                           
                                                                                
       FD BP13F517.                                                             
       COPY BP13F517.                                                           
                                                                                
       FD BP13K517.                                                             
       COPY BP13K517.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-CONTROL-COUNT.                                                    
           05  WS-F517-READ            PIC 9(8)    VALUE ZEROS.                 
           05  WS-F416-READ            PIC 9(8)    VALUE ZEROS.                 
           05  WS-MATCH-REC            PIC 9(8)    VALUE ZEROS.                 
           05  WS-K517-REWRITE         PIC 9(8)    VALUE ZEROS.                 
           05  WS-NOTFD-F416           PIC 9(8)    VALUE ZEROS.                 
           05  WS-NOTFD-F517           PIC 9(8)    VALUE ZEROS.                 
                                                                                
       01  WS-MARR-DATE.                                                        
           05  WS-MARR-CCYY            PIC X(04)   VALUE SPACES.                
           05  WS-MARR-MM              PIC X(02)   VALUE SPACES.                
           05  WS-MARR-DD              PIC X(02)   VALUE SPACES.                
                                                                                
       01  BP13K517-STATUS             PIC X(2)    VALUE SPACES.                
       01  WS-F416-PREV                PIC X(20)   VALUE SPACES.                
       01  WS-SYS-DATE                 PIC 9(8)    VALUE ZEROES.                
       01  WS-PRT-DATE                 PIC X(10)   VALUE SPACES.                
       01  WS-F517-EOF                 PIC X(01)   VALUE 'N'.                   
       01  WS-F416-EOF                 PIC X(01)   VALUE 'N'.                   
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------*                 
       0000-MAIN.                                                               
      *-------------------------------------------------------*                 
            PERFORM 0500-OPEN-FILES       THRU 0500-EXIT.                       
            PERFORM 1000-READ-BE01F416    THRU 1000-EXIT.                       
            PERFORM 2000-READ-BP13F517    THRU 2000-EXIT.                       
            PERFORM 3000-PROCESS          THRU 3000-EXIT                        
                    UNTIL WS-F517-EOF = 'Y' AND WS-F416-EOF = 'Y'.              
            PERFORM 9000-CLOSE-FILES.                                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------*                 
       0500-OPEN-FILES.                                                         
      *-------------------------------------------------------*                 
            OPEN INPUT  BE01F416                                                
                        BP13F517                                                
                 I-O    BP13K517.                                               
                                                                                
            IF BP13K517-STATUS NOT = 00 AND 97                                  
               DISPLAY 'OPENING ERROR-BP13K517-STATUS ' BP13K517-STATUS         
               MOVE     BP13K517-STATUS TO RETURN-CODE                          
               PERFORM 9000-CLOSE-FILES.                                        
                                                                                
            MOVE FUNCTION CURRENT-DATE(1:8)   TO WS-SYS-DATE.                   
                                                                                
       0500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       1000-READ-BE01F416.                                                      
      *-------------------------------------------------------*                 
            READ BE01F416 AT END                                                
                 MOVE HIGH-VALUES TO BE01F416-IDNO                              
                 MOVE 'Y' TO WS-F416-EOF                                        
                 GO TO 1000-EXIT.                                               
                                                                                
            ADD 1                            TO WS-F416-READ.                   
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       2000-READ-BP13F517.                                                      
      *-------------------------------------------------------*                 
            READ BP13F517 AT END                                                
                 MOVE HIGH-VALUES TO F517-NUM-NRIC                              
                 MOVE 'Y' TO WS-F517-EOF                                        
                 GO TO 2000-EXIT.                                               
                                                                                
            ADD 1                            TO WS-F517-READ.                   
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       2500-READ-BP13K517.                                                      
      *-------------------------------------------------------*                 
            READ BP13K517 KEY IS K517-KEY-FLD                                   
              IF BP13K517-STATUS = 00                                           
                 CONTINUE                                                       
              ELSE                                                              
                 IF BP13K517-STATUS = 23                                        
                    DISPLAY 'RECORD NOT FOUND' BP13K517-STATUS                  
                 ELSE                                                           
                    DISPLAY 'READ ERROR, BP13K517-STATUS = '                    
                                                     BP13K517-STATUS            
                    MOVE BP13K517-STATUS TO RETURN-CODE                         
                    GO TO 9000-CLOSE-FILES.                                     
                                                                                
                                                                                
       2500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       3000-PROCESS.                                                            
      *-------------------------------------------------------*                 
                                                                                
            IF BE01F416-IDNO = F517-NUM-NRIC                                    
               MOVE BE01F416-IDNO                TO WS-F416-PREV                
               ADD  1                            TO WS-MATCH-REC                
               PERFORM 4200-REWRITE-BP13K517     THRU 4200-EXIT                 
               PERFORM 4100-DUP-CHECK            THRU 4100-EXIT                 
                  UNTIL BE01F416-IDNO NOT = WS-F416-PREV OR                     
                   WS-F416-EOF = 'Y'                                            
                  PERFORM 2000-READ-BP13F517     THRU 2000-EXIT                 
            ELSE                                                                
               IF BE01F416-IDNO > F517-NUM-NRIC                                 
                  ADD 1                          TO WS-NOTFD-F416               
                  PERFORM 2000-READ-BP13F517     THRU 2000-EXIT                 
               ELSE                                                             
                  IF BE01F416-IDNO < F517-NUM-NRIC                              
                     ADD 1                       TO WS-NOTFD-F517               
                     PERFORM 1000-READ-BE01F416  THRU 1000-EXIT                 
                  END-IF                                                        
               END-IF                                                           
            END-IF.                                                             
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       4100-DUP-CHECK.                                                          
      *-------------------------------------------------------*                 
               PERFORM 1000-READ-BE01F416   THRU 1000-EXIT.                     
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       4200-REWRITE-BP13K517.                                                   
      *-------------------------------------------------------*                 
               MOVE BE01F416-IDNO          TO  K517-KEY-FLD.                    
               PERFORM 2500-READ-BP13K517 THRU 2500-EXIT                        
               MOVE BE01F416-SPOUSE-IDNO   TO  K517-NUM-NRIC-SPOUSE             
               MOVE BE01F416-SPOUSE-SEX    TO  K517-NUM-SPOUSE-SEX              
               MOVE BE01F416-SPOUSE-NAME   TO  K517-NME-SPOUSE                  
               MOVE BE01F416-IDNO-NAME-ROM TO  K517-NME                         
               MOVE BE01F416-MRG-REF-NO    TO  K517-NUM-MARRIAGE                
               MOVE BE01F416-MRG-DATE(1:4) TO  WS-MARR-CCYY                     
               MOVE BE01F416-MRG-DATE(6:2) TO  WS-MARR-MM                       
               MOVE BE01F416-MRG-DATE(9:2) TO  WS-MARR-DD                       
               MOVE WS-MARR-DATE           TO  K517-DTE-MARRIAGE                
               MOVE BE01F416-IDNO          TO  K517-NUM-NRIC                    
               MOVE WS-SYS-DATE            TO  K517-DTE-RECEIVED                
               REWRITE BP13K517-REC                                             
               IF BP13K517-STATUS = 00 OR 02                                    
                  ADD 1                        TO WS-K517-REWRITE               
                                                                                
               ELSE                                                             
                  DISPLAY 'ERROR REWRITING BP13K517 : ' K517-KEY-FLD            
                          '  STATUS ' BP13K517-STATUS                           
                  MOVE BP13K517-STATUS         TO RETURN-CODE                   
                  PERFORM 9000-CLOSE-FILES                                      
               END-IF.                                                          
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------*                 
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------*                 
            STRING WS-SYS-DATE (7:2), '/',                                      
                   WS-SYS-DATE (5:2), '/',                                      
                   WS-SYS-DATE (1:4)                                            
                   DELIMITED BY SIZE                                            
                   INTO                                                         
                   WS-PRT-DATE.                                                 
                                                                                
            DISPLAY '   '.                                                      
            DISPLAY 'SUMMARY REPORT   BP13C921   RUNDATE ' WS-PRT-DATE.         
            DISPLAY '______________________________________________'.           
            DISPLAY '   '.                                                      
            DISPLAY 'NO OF RECORDS READ FROM BE01F416   : '                     
                                            WS-F416-READ                        
            DISPLAY 'NO OF RECORDS READ FROM BP13F517   : '                     
                                            WS-F517-READ                        
            DISPLAY 'NO OF RECORDS MATCHED              : '                     
                                            WS-MATCH-REC                        
            DISPLAY 'NO OF REC NOT FOUND IN F416        : '                     
                                            WS-NOTFD-F416                       
            DISPLAY 'NO OF REC NOT FOUND IN F517        : '                     
                                            WS-NOTFD-F517                       
            DISPLAY 'NO OF RECORDS WRITTEN TO BP13K517  : '                     
                                            WS-K517-REWRITE                     
                                                                                
            CLOSE BE01F416                                                      
                  BP13F517                                                      
                  BP13K517.                                                     
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
