       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C163.                                                 
       AUTHOR.        NOR.                                                      
      *DATE-WRITTEN.  28/07/1998.                                               
                                                                                
      *****************************************************************         
      * SYSTEM OF COMMITMENT (SOC)                                    *         
      *===============================================================*         
      *                                                               *         
      *  OBJECTIVES : 1. PROGRAM TO MATCH NRIC OF BP13F558 WITH NRIC  *         
      *                  OF TRANSFER HISTORY ON BUYER FOR             *         
      *                  . TREATED AS UPGRADER                        *         
      *                  . 10-YEAR TIME BAR                           *         
      *                  . CANCELLATION IF CA-LOAN GRANTED            *         
      *                                                               *         
      *               2. THE POLICY ONLY CHECK CASES TRANSFER AFTER   *         
      *                  OR ON 1998/07/01.                            *         
      *                                                               *         
      *               3. READ SMS TO GET OWNERSHIP STATUS,DTE-TRANSFER*         
      *                                                               *         
      *               4. IF THE SCH-ACC-NO IN TRANSFER IS SAME AS     *         
      *                  SMS-SCH-ACC-NO, THEN  OUTPUT.                *         
      *===============================================================*         
      * SIMILAR TO BP13C619 - GET TRANSFEROR INFOR/ SELLER            *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                ACCESS      REC                                *         
      * DD NAME  I/O/E  MODE       LEN  KEY          REMARKS          *         
      * -------  ----- -------     ---  ---          --------         *         
      * BP13F558 I      SEQUENTIAL                                    *         
      * BB13K510 I      DYNAMIC                                       *         
      * BB13K500 I      RANDOM                                        *         
      * P13F558O O      SEQUENTIAL                                    *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      * CHGE REQ  DATE      BY     REMARKS                            *         
      * --------- ----      ---    -------                            *         
      * BP130528  28071998  LSB - NEW PGM                             *         
      * BP130589  16041999  LSB - HDB-REF-SMS = HDB-REF-TRF,THEN WRITE*         
      * BP130897  31072000  AKT - EXPAND F558 RECL FROM 400 TO 600    *         
      * BP139518  28022023  EAA2  TO INCLUDE 6 SALES TYPE CODE IN     *         
      *                           HOUSEHOLD DETERMINATION             *         
      *                           (8L/8M/8N/8R/8S/8T)                 *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BB13K500 ASSIGN TO BB13K500                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE IS RANDOM                                
                           RECORD KEY IS K500-KEY-FLD                           
                           FILE STATUS IS BB13K500-STATUS.                      
                                                                                
           SELECT BB13K510 ASSIGN TO BB13K510                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE IS DYNAMIC                               
                           RECORD KEY IS K510-KEY-FLD                           
                           FILE STATUS IS BB13K510-STATUS.                      
                                                                                
           SELECT AB03K700   ASSIGN TO AB03K700                                 
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K700-NUM-HDB-REF                              
                  FILE STATUS  IS AB03K700-STATUS.                              
                                                                                
           SELECT AB03K030   ASSIGN TO AB03K030                                 
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K030-NUM-HDB-REF                              
                  FILE STATUS  IS AB03K030-STATUS.                              
                                                                                
                                                                                
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
                                                                                
           SELECT P13F558O ASSIGN TO P13F558O.                                  
                                                                                
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BB13K500     RECORD CONTAINS 1000 CHARACTERS.                        
       COPY BB13K500.                                                           
                                                                                
       FD  BB13K510     RECORD CONTAINS 100 CHARACTERS.                         
       COPY BB13K510.                                                           
                                                                                
       FD  BP13F558     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 600 CHARACTERS                          
                        LABEL RECORD IS STANDARD                                
                        RECORDING MODE IS F.                                    
       COPY BP13F558.                                                           
                                                                                
       FD   AB03K700                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
                                                                                
       COPY AB03K700.                                                           
                                                                                
       FD   AB03K030                                                            
            RECORD CONTAINS 800 CHARACTERS.                                     
                                                                                
       COPY AB03K030.                                                           
                                                                                
       FD  P13F558O     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 600 CHARACTERS                          
                        LABEL RECORD IS OMITTED                                 
                        RECORDING MODE IS F.                                    
       01  P13F558O-REC.                                                        
           05  FILLER           PIC X(600).                                     
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-FLAGS.                                                       
           05  BB13K500-STATUS     PIC 9(2)      VALUE ZEROS.                   
           05  BB13K510-STATUS     PIC 9(2)      VALUE ZEROS.                   
           05  AB03K030-STATUS     PIC 9(2)      VALUE ZEROS.                   
           05  AB03K700-STATUS     PIC 9(2)      VALUE ZEROS.                   
           05  WS-F558-EOF         PIC X         VALUE 'N'.                     
           05  WS-K510-EOF         PIC X         VALUE 'N'.                     
           05  EOF-AB03K030        PIC X         VALUE 'N'.                     
           05  EOF-AB03K700        PIC X         VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F558-CNT-READ    PIC 9(8)      VALUE ZEROS.                   
           05  WS-K510-CNT-READ    PIC 9(8)      VALUE ZEROS.                   
           05  WS-F558O-CNT-WRT    PIC 9(8)      VALUE ZEROS.                   
           05  WS-TRF-FD           PIC 9(8)      VALUE ZEROS.                   
           05  WS-TRF-VALID        PIC 9(8)      VALUE ZEROS.                   
           05  WS-TRF-LESSEE       PIC X(1)      VALUE SPACES.                  
           05  WS-OPEN-MARKET-CNT  PIC 9(8)      VALUE ZEROS.                   
           05  WS-PUBLIC-CNT       PIC 9(8)      VALUE ZEROS.                   
                                                                                
       01  WS-TRANSFER-INFO.                                                    
           05  WS-F558O-NUM-TRANSFEROR     PIC X(2).                            
           05  WS-F558O-NUM-RSHIP-TR       PIC X(2).                            
           05  WS-F558O-NUM-MSTAT-TR       PIC X(2).                            
           05  WS-F558O-NUM-HDB-REF-TR     PIC X(11).                           
           05  WS-F558O-NUM-SALES-TYPE-TR  PIC X(2).                            
           05  WS-F558O-NUM-ALLO-SCH-TR    PIC X(3).                            
           05  WS-F558O-NUM-ELIG-SCH-TR    PIC X(3).                            
           05  WS-F558O-DTE-EFF-SALE-TR    PIC X(8).                            
           05  WS-F558O-DTE-TRANSFER-TR    PIC X(8).                            
                                                                                
       01  WS-DTE-CC               PIC X(2).                                    
       01  WS-DTE-CURRENT.                                                      
           05  WS-DTE-DD           PIC X(2).                                    
           05  WS-DTE-MM           PIC X(2).                                    
           05  WS-DTE-YY           PIC X(2).                                    
                                                                                
       01  WS-TME-SYSTEM           PIC X(8).                                    
       01  WS-TAG-ADMIN            PIC X(1)      VALUE 'A'.                     
       01  WS-DTE-TRANSFER         PIC X(8)      VALUE SPACES.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 1010-READ-BP13F558-REC   THRU 1010-EXIT.                     
           PERFORM 2000-MAIN-ROUTINE        THRU 2000-EXIT                      
             UNTIL WS-F558-EOF = 'Y' OR                                         
                   F558-NUM-NRIC = HIGH-VALUE.                                  
                                                                                
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BB13K500                                                 
                       BB13K510                                                 
                       BP13F558                                                 
                       AB03K030                                                 
                       AB03K700                                                 
                OUTPUT P13F558O.                                                
                                                                                
                                                                                
           IF BB13K500-STATUS NOT = ZEROES AND 97                               
              MOVE BB13K500-STATUS TO RETURN-CODE                               
              DISPLAY 'OPEN BB13K500 FAIL STATUS=' BB13K500-STATUS              
              GO TO 9000-CLOSE-FILES.                                           
                                                                                
           IF BB13K510-STATUS NOT = ZEROES AND 97                               
              MOVE BB13K510-STATUS TO RETURN-CODE                               
              DISPLAY 'OPEN BB13K510 FAIL STATUS=' BB13K510-STATUS              
              GO TO 9000-CLOSE-FILES.                                           
                                                                                
           IF AB03K030-STATUS NOT = ZEROS AND 97                                
               DISPLAY 'FILE AB03K030 OPEN ERROR'                               
               DISPLAY 'ERROR STATUS = ' AB03K030-STATUS                        
               MOVE AB03K030-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-FILES.                                        
                                                                                
           IF AB03K700-STATUS NOT = ZEROS AND 97                                
               DISPLAY 'FILE AB03K700 OPEN ERROR'                               
               DISPLAY 'ERROR STATUS = ' AB03K700-STATUS                        
               MOVE AB03K700-STATUS TO RETURN-CODE                              
               PERFORM 9000-CLOSE-FILES.                                        
                                                                                
                                                                                
           ACCEPT WS-DTE-CURRENT FROM DATE.                                     
           ACCEPT WS-TME-SYSTEM  FROM TIME.                                     
                                                                                
           IF WS-DTE-YY < '90'                                                  
              MOVE '20'      TO WS-DTE-CC                                       
           ELSE                                                                 
              MOVE '19'      TO WS-DTE-CC.                                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1010-READ-BP13F558-REC.                                                  
      *****************************************************************         
           READ BP13F558 AT END MOVE 'Y'        TO WS-F558-EOF                  
                                MOVE HIGH-VALUE TO F558-NUM-NRIC                
                GO TO 1010-EXIT.                                                
           ADD 1 TO WS-F558-CNT-READ.                                           
       1010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1020-READ-BB13K510-REC.                                                  
      *****************************************************************         
           READ BB13K510 NEXT AT END MOVE HIGH-VALUE TO K510-NUM-UIN.           
                                                                                
       1020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-MAIN-ROUTINE.                                                       
      *****************************************************************         
                                                                                
      *----------------------------------------------------*                    
      * COMMENTS :  DO THE MATCHING.                       *                    
      *----------------------------------------------------*                    
                                                                                
           MOVE SPACES  TO WS-TRANSFER-INFO.                                    
           MOVE SPACES  TO WS-DTE-TRANSFER.                                     
                                                                                
           IF F558-NUM-NRIC = SPACES OR LOW-VALUES                              
              PERFORM 1010-READ-BP13F558-REC        THRU 1010-EXIT              
              GO TO 2000-EXIT.                                                  
                                                                                
           MOVE SPACES                  TO K510-KEY-FLD.                        
           MOVE F558-NUM-NRIC           TO K510-NUM-UIN.                        
           START BB13K510 KEY >= K510-KEY-FLD.                                  
           IF BB13K510-STATUS = ZEROES                                          
              PERFORM 1020-READ-BB13K510-REC  THRU 1020-EXIT                    
              PERFORM 2005-CHECK-STATUS-REC   THRU 2005-EXIT                    
                UNTIL F558-NUM-NRIC NOT = K510-NUM-UIN OR                       
                      K510-NUM-UIN = HIGH-VALUE                                 
           ELSE                                                                 
           IF BB13K510-STATUS = 23                                              
              DISPLAY 'K510-NUM-UIN : ' K510-NUM-UIN ' NOT FOUND IN '           
                      'BB13K510!'                                               
           END-IF.                                                              
                                                                                
           IF WS-DTE-TRANSFER > '19980700'                                      
             PERFORM 2020-WRITE-F558-REC      THRU 2020-EXIT                    
           END-IF.                                                              
                                                                                
           PERFORM 1010-READ-BP13F558-REC     THRU 1010-EXIT.                   
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2005-CHECK-STATUS-REC.                                                   
      *****************************************************************         
                                                                                
           IF K510-CDE-REC = 'B'                                                
              IF F558-NUM-HDB-REF-SMS = K510-NUM-HDB-REF                        
                 PERFORM 2010-EVALUATE-DATA   THRU 2010-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 1020-READ-BB13K510-REC        THRU 1020-EXIT.                
                                                                                
       2005-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2010-EVALUATE-DATA.                                                      
      *****************************************************************         
              MOVE SPACES                    TO WS-TRF-LESSEE.                  
              MOVE SPACES                    TO K500-KEY-FLD.                   
              MOVE K510-NUM-HDB-REF          TO K500-NUM-HDB-REF.               
              MOVE K510-CDE-REC              TO K500-CDE-REC.                   
                                                                                
              READ BB13K500                                                     
              IF BB13K500-STATUS = ZEROES                                       
                 IF K500-CDE-REC = 'B' AND                                      
                    K510-NUM-HDB-REF = K500-NUM-HDB-REF                         
                    PERFORM  3000-SMS-CURRENT-FLAT THRU 3000-EXIT               
                    IF WS-DTE-TRANSFER = SPACES                                 
                       CONTINUE                                                 
                    ELSE                                                        
                       PERFORM 2015-FILLIN-TRANS-INFO  THRU 2015-EXIT           
                    END-IF                                                      
                 END-IF                                                         
              ELSE                                                              
              IF BB13K500-STATUS = 23                                           
                 DISPLAY 'K510-NUM-HDB-REF ' K510-NUM-HDB-REF                   
                         ' NOT FOUND IN BB13K500'                               
              END-IF.                                                           
                                                                                
       2010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2015-FILLIN-TRANS-INFO.                                                  
      *****************************************************************         
              MOVE SPACES                  TO WS-TRANSFER-INFO                  
              ADD 1 TO WS-TRF-FD                                                
              EVALUATE K510-NUM-UIN                                             
              WHEN K500-NRIC-TE1                                                
                   MOVE 'Y'                TO WS-TRF-LESSEE                     
                   MOVE 'L1'               TO WS-F558O-NUM-TRANSFEROR           
                   MOVE K500-CDE-RSHIP-TE1 TO WS-F558O-NUM-RSHIP-TR             
                                                                                
              WHEN K500-NRIC-TE2                                                
                   MOVE 'Y'                TO WS-TRF-LESSEE                     
                   MOVE 'L2'               TO WS-F558O-NUM-TRANSFEROR           
                   MOVE K500-CDE-RSHIP-TE2 TO WS-F558O-NUM-RSHIP-TR             
                                                                                
              WHEN K500-NRIC-TE3                                                
                   MOVE 'Y'                TO WS-TRF-LESSEE                     
                   MOVE 'L3'               TO WS-F558O-NUM-TRANSFEROR           
                   MOVE K500-CDE-RSHIP-TE3 TO WS-F558O-NUM-RSHIP-TR             
                                                                                
              WHEN K500-NRIC-TE4                                                
                   MOVE 'Y'                TO WS-TRF-LESSEE                     
                   MOVE 'L4'               TO WS-F558O-NUM-TRANSFEROR           
                   MOVE K500-CDE-RSHIP-TE4 TO WS-F558O-NUM-RSHIP-TR             
              END-EVALUATE                                                      
                                                                                
              IF WS-TRF-LESSEE = 'Y'                                            
                MOVE K500-NUM-HDB-REF      TO WS-F558O-NUM-HDB-REF-TR           
                MOVE K500-CDE-SALES-TYPE   TO WS-F558O-NUM-SALES-TYPE-TR        
                MOVE K500-CDE-ALLOC-TE     TO WS-F558O-NUM-ALLO-SCH-TR          
                MOVE K500-CDE-ELIG-TE      TO WS-F558O-NUM-ELIG-SCH-TR          
                MOVE SPACES                TO WS-F558O-DTE-EFF-SALE-TR          
                MOVE WS-DTE-TRANSFER       TO WS-F558O-DTE-TRANSFER-TR          
              END-IF.                                                           
                                                                                
       2015-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2020-WRITE-F558-REC.                                                     
      *****************************************************************         
                                                                                
           MOVE SPACES                     TO P13F558O-REC.                     
            ADD 1 TO WS-TRF-VALID.                                              
            MOVE WS-F558O-NUM-TRANSFEROR   TO F558-NUM-TRANSFEROR.              
            MOVE WS-F558O-NUM-RSHIP-TR     TO F558-NUM-RSHIP-TR.                
            MOVE WS-F558O-NUM-MSTAT-TR     TO F558-NUM-MSTAT-TR.                
            MOVE WS-F558O-NUM-HDB-REF-TR   TO F558-NUM-HDB-REF-TR.              
            MOVE WS-F558O-NUM-SALES-TYPE-TR TO F558-NUM-SALES-TYPE-TR.          
            MOVE WS-F558O-NUM-ALLO-SCH-TR  TO F558-NUM-ALLO-SCH-TR.             
            MOVE WS-F558O-NUM-ELIG-SCH-TR  TO F558-NUM-ELIG-SCH-TR.             
            MOVE WS-F558O-DTE-EFF-SALE-TR  TO F558-DTE-EFF-SALE-TR.             
            MOVE WS-F558O-DTE-TRANSFER-TR  TO F558-DTE-TRANSFER-TR.             
                                                                                
           WRITE P13F558O-REC FROM BP13F558-REC.                                
           ADD 1                      TO WS-F558O-CNT-WRT.                      
       2020-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-SMS-CURRENT-FLAT.                                                   
                                                                                
           MOVE 'A' TO  WS-TAG-ADMIN.                                           
           MOVE K500-NUM-HDB-REF TO K030-NUM-HDB-REF.                           
           READ AB03K030.                                                       
           IF  AB03K030-STATUS = ZEROES                                         
               NEXT SENTENCE                                                    
           ELSE                                                                 
               DISPLAY 'SCHACC NOT FOUND IN AB03K030 ' K030-NUM-HDB-REF         
                       ' F558-REG ' F558-NUM-REGN                               
                       ' NRIC ' F558-NUM-NRIC                                   
               GO TO 3000-EXIT.                                                 
                                                                                
           IF K030-CDE-ACCT-STAT = 'C'                                          
              PERFORM 3330-READ-AB03K700     THRU 3330-EXIT                     
              PERFORM 3350-GET-SMS-DATA THRU 3350-EXIT                          
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3330-READ-AB03K700.                                                      
           MOVE K500-NUM-HDB-REF TO K700-NUM-HDB-REF.                           
                                                                                
           READ AB03K700.                                                       
           IF  AB03K700-STATUS = ZEROES                                         
               NEXT SENTENCE                                                    
           ELSE                                                                 
               DISPLAY 'SCHACC NOT FOUND IN AB03K700 ' K500-NUM-HDB-REF         
               GO TO 3330-EXIT.                                                 
                                                                                
           IF K510-NUM-UIN = K700-NUM-UIN1                                      
              IF  K700-CDE-OWNER-STATUS1 = 'E' OR 'A' OR 'P'                    
                  IF WS-TAG-ADMIN = 'L'                                         
                     NEXT SENTENCE                                              
                  ELSE                                                          
                     MOVE 'A' TO WS-TAG-ADMIN                                   
              ELSE                                                              
                 MOVE 'L' TO WS-TAG-ADMIN                                       
           ELSE                                                                 
              IF K510-NUM-UIN = K700-NUM-UIN2                                   
                 IF K700-CDE-OWNER-STATUS2 = 'E' OR 'A' OR 'P'                  
                    IF WS-TAG-ADMIN = 'L'                                       
                       NEXT SENTENCE                                            
                    ELSE                                                        
                       MOVE 'A' TO WS-TAG-ADMIN                                 
                 ELSE                                                           
                    MOVE 'L' TO WS-TAG-ADMIN                                    
              ELSE                                                              
                IF K510-NUM-UIN = K700-NUM-UIN3                                 
                   IF K700-CDE-OWNER-STATUS3 = 'E' OR 'A' OR 'P'                
                      IF WS-TAG-ADMIN = 'L'                                     
                         NEXT SENTENCE                                          
                      ELSE                                                      
                         MOVE 'A' TO WS-TAG-ADMIN                               
                   ELSE                                                         
                      MOVE 'L' TO WS-TAG-ADMIN                                  
                ELSE                                                            
                   IF K510-NUM-UIN = K700-NUM-UIN4                              
                      IF K700-CDE-OWNER-STATUS4 = 'E' OR 'A' OR 'P'             
                         IF WS-TAG-ADMIN = 'L'                                  
                            NEXT SENTENCE                                       
                         ELSE                                                   
                            MOVE 'A' TO WS-TAG-ADMIN                            
                      ELSE                                                      
                         MOVE 'L' TO WS-TAG-ADMIN.                              
                                                                                
       3330-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3350-GET-SMS-DATA.                                                       
                                                                                
           IF WS-TAG-ADMIN = 'L'                                                
              IF (K030-CDE-SALES-TYP = '7 ' OR '7J' OR '8A' OR '8B' OR          
                                       '7C' OR '8E' OR '8L' OR '8M' OR          
                                       '8N' OR '8R' OR '8S' OR '8T') OR         
                 (K030-CDE-ALLOC-SCH = 'OM ' OR 'PPO' OR 'AOM' OR               
                                       'ROM' OR 'PPR' OR 'SPR')                 
                  ADD 1 TO WS-OPEN-MARKET-CNT                                   
              ELSE                                                              
                  ADD 1 TO WS-PUBLIC-CNT                                        
                  PERFORM 3360-CHECK-TRANSFER-DATE THRU 3360-EXIT.              
                                                                                
       3350-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3360-CHECK-TRANSFER-DATE.                                                
                                                                                
            IF K030-DTE-TRSF = SPACES                                           
               IF K030-DTE-EXE-SALE-AGRMT = SPACES                              
                  MOVE K030-DTE-SALE-EFF TO WS-DTE-TRANSFER                     
               ELSE                                                             
                  MOVE K030-DTE-EXE-SALE-AGRMT TO WS-DTE-TRANSFER               
            ELSE                                                                
               MOVE K030-DTE-TRSF TO WS-DTE-TRANSFER                            
            END-IF.                                                             
                                                                                
       3360-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           DISPLAY 'PROGRAM = BP13C163 MATCH TRANSFER HISTORY'.                 
           DISPLAY 'TOTAL F558 REC READ     = ' WS-F558-CNT-READ.               
           DISPLAY 'TOTAL F558 REC WRITTEN  = ' WS-F558O-CNT-WRT.               
           DISPLAY 'REC FD IN TRANSFER SYS  = ' WS-TRF-FD.                      
           DISPLAY 'TRF REC AFTER 19980700  = ' WS-TRF-VALID.                   
                                                                                
           CLOSE      BB13K510                                                  
                      BB13K500                                                  
                      BP13F558                                                  
                      AB03K030                                                  
                      AB03K700                                                  
                      P13F558O.                                                 
                                                                                
           IF  AB03K700-STATUS NOT = ZEROS AND 97                               
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'AB03K700-STATUS ' AB03K700-STATUS.                      
                                                                                
           IF  AB03K030-STATUS NOT = ZEROS AND 97                               
               DISPLAY 'CLOSING ERROR '                                         
               DISPLAY 'AB03K030-STATUS ' AB03K030-STATUS.                      
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
