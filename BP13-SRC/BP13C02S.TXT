       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         BP13C02S.                                            
      *AUTHOR.             ZARITA SARMIENTO.                                    
      *DATE-WRITTEN.       17 MAR 2009.                                         
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  ONETIME PROGRAM TO READ BP13KA00 AND COMPUTE*            
      *                FOR THE DIFFERENCE OF KA00-NUM-FLAT-PA-UPG  *            
      *                AND KA00-NUM-REG-PA-UPG AND MOVE THE RESULT *            
      *                TO BP13FB63 AND THE OTHER REQUIRED FIELD    *            
      *                COMING FROM BP13KA00                        *            
      *                                                            *            
      *  INPUT FILE :  1. BP13KA00                                 *            
      *  OUTPUT FILE:  1. BP13FB63                                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      *  BP133533 ZDD1 17032009 NEW PROGRAM                         *           
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
            SELECT BP13KA00 ASSIGN TO BP13KA00                                  
                   ORGANIZATION IS INDEXED                                      
                   RECORD KEY IS KA00-KEY-FLD                                   
                   FILE STATUS IS WS-BP13KA00-STAT                              
                   ACCESS MODE IS SEQUENTIAL.                                   
                                                                                
            SELECT BP13FB63 ASSIGN TO BP13FB63                                  
                   FILE STATUS  IS WS-BP13FB63-STAT.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13KA00                                                            
            RECORD CONTAINS  200 CHARACTERS.                                    
       COPY BP13KA00.                                                           
                                                                                
       FD  BP13FB63   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 20 CHARACTERS                             
                      LABEL RECORDS ARE STANDARD                                
                      RECORDING MODE IS F.                                      
       COPY BP13FB63.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01 WS-VARIABLES.                                                         
          05 WS-BP13FB63-STAT            PIC X(02) VALUE SPACES.                
          05 WS-BP13KA00-STAT            PIC X(02) VALUE SPACES.                
          05 EOF-FLAG-BP13KA00           PIC X(01) VALUE 'N'.                   
          05 WS-DATE-CURR                PIC X(08) VALUE SPACES.                
          05 WS-TOT-UPG                  PIC 9(03) VALUE ZEROES.                
          05 WS-FB63-WRITE               PIC 9(08) VALUE ZEROES.                
          05 WS-KA00-READ                PIC 9(08) VALUE ZEROES.                
          05 WS-FB63-VAR-ZERO            PIC X(03) VALUE '  0'.                 
                                                                                
       PROCEDURE DIVISION.                                                      
      ****************************                                              
       0000-MAIN-CONTROL.                                                       
      ****************************                                              
                                                                                
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-MAIN-ROUTINE      THRU 2000-EXIT                        
                   UNTIL EOF-FLAG-BP13KA00 = 'Y'.                               
           PERFORM 9999-CLOSE-FILES       THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       1000-OPEN-FILES.                                                         
      ****************************                                              
                                                                                
           OPEN INPUT  BP13KA00                                                 
                OUTPUT BP13FB63.                                                
                                                                                
           IF WS-BP13FB63-STAT  NOT = '00' AND '97'                             
              DISPLAY 'ERROR IN OPENING BP13FB63 STATUS - '                     
                              WS-BP13FB63-STAT                                  
              MOVE WS-BP13FB63-STAT             TO RETURN-CODE                  
              PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT                  
           END-IF.                                                              
                                                                                
           IF WS-BP13KA00-STAT  NOT = '00' AND '97'                             
              DISPLAY 'ERROR IN OPENING BP13KA00 STATUS - '                     
                              WS-BP13KA00-STAT                                  
              MOVE WS-BP13KA00-STAT             TO RETURN-CODE                  
              PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT                  
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       2000-MAIN-ROUTINE.                                                       
      ****************************                                              
                                                                                
           READ BP13KA00                                                        
              AT END MOVE 'Y'      TO    EOF-FLAG-BP13KA00                      
              GO TO 2000-EXIT.                                                  
           ADD 1                   TO    WS-KA00-READ.                          
                                                                                
           DISPLAY 'KA00-NUM-PROJECT -' KA00-NUM-PRJCT.                         
                                                                                
           IF KA00-NUM-PRJCT NOT =  SPACES AND LOW-VALUES                       
             PERFORM 3000-PROCESS-RECORD   THRU 3000-EXIT                       
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3000-PROCESS-RECORD.                                                     
      ****************************                                              
                                                                                
           MOVE ZEROES              TO  WS-TOT-UPG.                             
                                                                                
           IF KA00-NUM-FLAT-PA-UPG  =   SPACES OR LOW-VALUES                    
              MOVE ZEROES           TO  KA00-NUM-FLAT-PA-UPG                    
           END-IF.                                                              
                                                                                
           IF KA00-NUM-REG-PA-UPG   =   SPACES OR LOW-VALUES                    
              MOVE ZEROES           TO  KA00-NUM-REG-PA-UPG                     
           END-IF.                                                              
                                                                                
           COMPUTE WS-TOT-UPG = KA00-NUM-FLAT-PA-UPG -                          
                                KA00-NUM-REG-PA-UPG                             
                                                                                
           DISPLAY KA00-NUM-FLAT-PA-UPG '/ '  KA00-NUM-REG-PA-UPG.              
                                                                                
           PERFORM 4000-MOVE-DATA       THRU  4000-EXIT.                        
           PERFORM 5000-WRITE-BP13FB63  THRU  5000-EXIT.                        
                                                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       4000-MOVE-DATA.                                                          
      ****************************                                              
           MOVE SPACES               TO BP13FB63-REC.                           
           INITIALIZE BP13FB63-REC.                                             
                                                                                
           MOVE ';'                  TO     FB63-FIL01                          
                                            FB63-FIL02                          
                                            FB63-FIL03.                         
                                                                                
           MOVE KA00-NUM-PRJCT         TO     FB63-NUM-PRJCT.                   
                                                                                
           IF KA00-NUM-FLAT-PA-UPG NOT = ZEROES                                 
             MOVE KA00-NUM-FLAT-PA-UPG TO     FB63-NUM-MAX-UPG                  
           ELSE                                                                 
             MOVE WS-FB63-VAR-ZERO     TO     FB63-NUM-MAX-UPG                  
           END-IF.                                                              
                                                                                
           IF WS-TOT-UPG NOT = ZEROES                                           
             MOVE WS-TOT-UPG           TO     FB63-NUM-TOT-UPG                  
           ELSE                                                                 
             MOVE WS-FB63-VAR-ZERO     TO     FB63-NUM-TOT-UPG                  
           END-IF.                                                              
                                                                                
           ACCEPT WS-DATE-CURR       FROM   DATE YYYYMMDD.                      
           MOVE WS-DATE-CURR         TO     FB63-DTE-UPDATE.                    
                                                                                
           DISPLAY 'FB63-NUM-PRJCT '   FB63-NUM-PRJCT.                          
           DISPLAY 'FB63-MAX-UPG '     FB63-NUM-MAX-UPG.                        
           DISPLAY 'WS-TOT-UPG '       WS-TOT-UPG.                              
           DISPLAY 'FB63-NUM-TOT-UPG ' FB63-NUM-TOT-UPG.                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************                                              
       5000-WRITE-BP13FB63.                                                     
      ****************************                                              
                                                                                
           WRITE      BP13FB63-REC.                                             
                                                                                
           ADD 1      TO   WS-FB63-WRITE.                                       
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       9999-CLOSE-FILES.                                                        
      ****************************                                              
                                                                                
           CLOSE BP13FB63                                                       
                 BP13KA00.                                                      
                                                                                
           IF WS-BP13FB63-STAT  NOT = '00' AND '97'                             
              DISPLAY 'ERROR IN CLOSING BP13FB63 STATUS - '                     
                              WS-BP13FB63-STAT                                  
              MOVE WS-BP13FB63-STAT             TO RETURN-CODE                  
           END-IF.                                                              
                                                                                
           IF WS-BP13KA00-STAT  NOT = '00' AND '97'                             
              DISPLAY 'ERROR IN CLOSING BP13KA00 STATUS - '                     
                              WS-BP13KA00-STAT                                  
              MOVE WS-BP13KA00-STAT             TO RETURN-CODE                  
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13C02S ================*'.              
           DISPLAY '  NO. OF KA00 RECORDS READ    = ' WS-KA00-READ.             
           DISPLAY '  NO. OF FB63 RECORDS WRITTEN = ' WS-FB63-WRITE.            
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
