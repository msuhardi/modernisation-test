       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C000.                                                 
      *AUTHOR.        GOBBURU BHASKAR RAJU.                                     
      *DATE-WRITTEN.  10/12/1999.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE : TO AUTO-DELETE SECOND-LEVEL ID IF NO          *            
      *              1ST-LEVEL RACF ID IS FOUND.                   *            
      *                                                            *            
      *  INPUT FILES:                                              *            
      *      1. BP13F860                                           *            
      *      2. SY08K100                                           *            
      *                                                            *            
      *  I-O   FILES:                                              *            
      *      1. BP13K860                                           *            
      *      2. BP13K870                                           *            
      *      3. BP13K868                                           *            
      *      4. BP13K878                                           *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  OIC  DATE     DESCRIPTION                         *            
      * -------- ---- -------- -----------                         *            
      * BP130777 GB   19991210 NEW PROGRAM.                        *            
      * BP130866 LMS  20020304 CHANGE IN KEY FLD.                  *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13K860 ASSIGN TO BP13K860                                   
           ORGANIZATION IS INDEXED                                              
           ACCESS MODE IS DYNAMIC                                               
           RECORD KEY IS K860-KEY-FLD                                           
           FILE STATUS IS WS-K860-STATUS.                                       
                                                                                
           SELECT BP13K870 ASSIGN TO BP13K870                                   
           ORGANIZATION IS INDEXED                                              
           ACCESS MODE IS DYNAMIC                                               
           RECORD KEY IS K870-KEY-FLD                                           
           FILE STATUS IS WS-K870-STATUS.                                       
                                                                                
           SELECT SY08K100 ASSIGN TO SY08K100                                   
           ORGANIZATION IS INDEXED                                              
           ACCESS MODE IS RANDOM                                                
           RECORD KEY IS K100-KEY                                               
           FILE STATUS IS WS-K100-STATUS.                                       
                                                                                
           SELECT BP13K868 ASSIGN TO BP13K868                                   
           ORGANIZATION IS INDEXED                                              
           ACCESS MODE IS RANDOM                                                
           RECORD KEY IS K868-KEY-FLD                                           
           FILE STATUS IS WS-K868-STATUS.                                       
                                                                                
           SELECT BP13K878 ASSIGN TO BP13K878                                   
           ORGANIZATION IS INDEXED                                              
           ACCESS MODE IS RANDOM                                                
           RECORD KEY IS K878-KEY-FLD                                           
           FILE STATUS IS WS-K878-STATUS.                                       
                                                                                
           SELECT BP13F860 ASSIGN TO BP13F860.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13K860                                                             
           RECORD CONTAINS 80 CHARACTERS.                                       
       COPY BP13K860.                                                           
                                                                                
       FD  BP13K870                                                             
           RECORD CONTAINS 30 CHARACTERS.                                       
       COPY BP13K870.                                                           
                                                                                
       FD  SY08K100                                                             
           RECORD CONTAINS 80 CHARACTERS.                                       
       COPY SY08K100.                                                           
                                                                                
       FD  BP13K868                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13K868.                                                           
                                                                                
       FD  BP13K878                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K878.                                                           
                                                                                
       FD  BP13F860                                                             
           BLOCK  CONTAINS 0   RECORDS                                          
           RECORD CONTAINS 80  CHARACTERS                                       
           LABEL  RECORDS  ARE STANDARD                                         
           RECORDING MODE IS F.                                                 
       COPY BP13F860.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-K860-STATUS           PIC   9(2)  VALUE  0.                       
       01  WS-K870-STATUS           PIC   9(2)  VALUE  0.                       
       01  WS-K100-STATUS           PIC   9(2)  VALUE  0.                       
       01  WS-K868-STATUS           PIC   9(2)  VALUE  0.                       
       01  WS-K878-STATUS           PIC   9(2)  VALUE  0.                       
                                                                                
       01  WS-F860-READ             PIC   9(4)  VALUE  ZEROES.                  
       01  WS-K860-READ             PIC   9(4)  VALUE  ZEROES.                  
       01  WS-K870-READ             PIC   9(4)  VALUE  ZEROES.                  
       01  WS-K100-READ             PIC   9(4)  VALUE  ZEROES.                  
       01  WS-K868-WRITE            PIC   9(4)  VALUE  ZEROES.                  
       01  WS-K878-WRITE            PIC   9(4)  VALUE  ZEROES.                  
       01  WS-K860-DELETE           PIC   9(4)  VALUE  ZEROES.                  
       01  WS-K870-DELETE           PIC   9(4)  VALUE  ZEROES.                  
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05  WS-F860-FLAG         PIC   X(1)  VALUE  'N'.                     
               88  WS-F860-EOF                  VALUE  'Y'.                     
           05  WS-K860-FLAG         PIC   X(1)  VALUE  'N'.                     
               88  WS-K860-EOF                  VALUE  'Y'.                     
           05  WS-K870-FLAG         PIC   X(1)  VALUE  'N'.                     
               88  WS-K870-EOF                  VALUE  'Y'.                     
           05  WS-K100-FLAG         PIC   X(1)  VALUE  'N'.                     
               88  WS-K100-EOF                  VALUE  'Y'.                     
           05  WS-K868-FLAG         PIC   X(1)  VALUE  'N'.                     
               88  WS-K868-EOF                  VALUE  'Y'.                     
           05  WS-K878-FLAG         PIC   X(1)  VALUE  'N'.                     
               88  WS-K878-EOF                  VALUE  'Y'.                     
                                                                                
       01 WS-DATE                      PIC X(8)  VALUE SPACES.                  
                                                                                
       01 WS-CURRENT-TIME              PIC   9(8).                              
       01 WS-HHMMSS-CURRENT REDEFINES  WS-CURRENT-TIME.                         
          05  WS-CURRENT-HH            PIC   9(2).                              
          05  WS-CURRENT-MM            PIC   9(2).                              
          05  WS-CURRENT-SS            PIC   9(2).                              
          05  WS-CURRENT-MS            PIC   9(2).                              
       77 WS-TME-CNT                   PIC   9(4) VALUE 0.                      
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-FILES THRU 1000-EXIT.                             
            PERFORM 2000-F860-READ THRU 2000-EXIT UNTIL WS-F860-EOF.            
            PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
            OPEN INPUT SY08K100                                                 
                       BP13F860                                                 
                 I-O   BP13K860                                                 
                       BP13K870                                                 
                       BP13K868                                                 
                       BP13K878.                                                
                                                                                
            IF WS-K860-STATUS NOT = ZEROES AND 97                               
               DISPLAY 'OPEN FAILURE IN BP13K860: ' WS-K860-STATUS              
               MOVE WS-K860-STATUS TO RETURN-CODE                               
               PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                         
            IF WS-K870-STATUS NOT = ZEROES AND 97                               
               DISPLAY 'OPEN FAILURE IN BP13K870: ' WS-K870-STATUS              
               MOVE WS-K870-STATUS TO RETURN-CODE                               
               PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                         
            IF WS-K100-STATUS NOT = ZEROES AND 97                               
               DISPLAY 'OPEN FAILURE IN SY08K100: ' WS-K100-STATUS              
               MOVE WS-K100-STATUS TO RETURN-CODE                               
               PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                         
            IF WS-K868-STATUS NOT = ZEROES AND 97                               
               DISPLAY 'OPEN FAILURE IN BP13K868: ' WS-K868-STATUS              
               MOVE WS-K868-STATUS TO RETURN-CODE                               
               PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                         
            IF WS-K878-STATUS NOT = ZEROES AND 97                               
               DISPLAY 'OPEN FAILURE IN BP13K878: ' WS-K878-STATUS              
               MOVE WS-K878-STATUS TO RETURN-CODE                               
               PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                         
                                                                                
            MOVE FUNCTION CURRENT-DATE TO WS-DATE.                              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2000-F860-READ.                                                          
      *-------------------------------------------------------------            
            READ BP13F860                                                       
                   AT END MOVE 'Y' TO WS-F860-FLAG                              
            GO TO 2000-EXIT.                                                    
                                                                                
            ADD 1 TO WS-F860-READ.                                              
            PERFORM 3000-K100-READ THRU 3000-EXIT.                              
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3000-K100-READ.                                                          
      *-------------------------------------------------------------            
            MOVE 'BP13' TO K100-NUM-SYSTEM-ID.                                  
            MOVE F860-NUM-USERID TO K100-NUM-USERID.                            
            READ SY08K100.                                                      
            IF WS-K100-STATUS = 0                                               
               ADD 1 TO WS-K100-READ                                            
            ELSE                                                                
               IF WS-K100-STATUS = 23                                           
                  PERFORM 3500-K860-READ THRU 3500-EXIT                         
                  PERFORM 4000-K870-READ THRU 4000-EXIT                         
               ELSE                                                             
                  DISPLAY 'ERROR READING SY08K100: ' WS-K100-STATUS             
                  MOVE WS-K100-STATUS TO RETURN-CODE                            
                  PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3500-K860-READ.                                                          
      *-------------------------------------------------------------            
            MOVE  LOW-VALUES      TO K860-KEY-FLD.                              
            MOVE  F860-NUM-USERID TO K860-NUM-USERID.                           
            START BP13K860 KEY IS >= K860-KEY-FLD.                              
            IF WS-K860-STATUS = 0                                               
               PERFORM 3600-K860-READ THRU 3600-EXIT                            
                    UNTIL (K860-NUM-USERID NOT = F860-NUM-USERID)               
                    OR WS-K860-EOF                                              
            ELSE                                                                
              IF WS-K860-STATUS = 23                                            
                 NEXT SENTENCE                                                  
              ELSE                                                              
                 DISPLAY 'START FAILURE IN BP13K860: ' WS-K860-STATUS           
                 MOVE WS-K860-STATUS TO RETURN-CODE                             
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
              END-IF                                                            
            END-IF.                                                             
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3600-K860-READ.                                                          
      *-------------------------------------------------------------            
           READ BP13K860 NEXT AT END MOVE 'Y' TO WS-K860-FLAG                   
                                     GO TO 3600-EXIT.                           
           IF WS-K860-STATUS = 0                                                
              IF K860-NUM-USERID = F860-NUM-USERID                              
                 ADD 1 TO WS-K860-READ                                          
                 PERFORM 8000-K868-WRITE THRU 8000-EXIT                         
              END-IF                                                            
           ELSE                                                                 
             IF WS-K860-STATUS = 23                                             
                MOVE HIGH-VALUES TO K860-KEY-FLD                                
                GO TO 3600-EXIT                                                 
             ELSE                                                               
                 DISPLAY 'ERROR READING BP13K860: ' WS-K860-STATUS              
                 MOVE WS-K860-STATUS TO RETURN-CODE                             
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
             END-IF                                                             
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       4000-K870-READ.                                                          
      *-------------------------------------------------------------            
           MOVE  LOW-VALUES      TO K870-KEY-FLD.                               
           MOVE  F860-NUM-USERID TO K870-NUM-USERID.                            
           START BP13K870 KEY IS >= K870-KEY-FLD.                               
           IF WS-K870-STATUS = 0                                                
              PERFORM 5000-K870-READ THRU 5000-EXIT                             
                   UNTIL (K870-NUM-USERID NOT = F860-NUM-USERID)                
                   OR WS-K870-EOF                                               
           ELSE                                                                 
             IF WS-K870-STATUS = 23                                             
                NEXT SENTENCE                                                   
             ELSE                                                               
               DISPLAY 'START FAILURE IN BP13K870: ' WS-K870-STATUS             
               MOVE WS-K870-STATUS TO RETURN-CODE                               
               PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                          
             END-IF                                                             
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       5000-K870-READ.                                                          
      *-------------------------------------------------------------            
           READ BP13K870 NEXT AT END                                            
                MOVE 'Y' TO WS-K870-FLAG                                        
                GO TO 5000-EXIT.                                                
           IF WS-K870-STATUS = 0                                                
              IF K870-NUM-USERID = F860-NUM-USERID                              
                 ADD 1 TO WS-K870-READ                                          
                 PERFORM 6000-K878-WRITE THRU 6000-EXIT                         
              END-IF                                                            
           ELSE                                                                 
             IF WS-K870-STATUS = 23                                             
                MOVE HIGH-VALUES TO K870-KEY-FLD                                
                GO TO 5000-EXIT                                                 
             ELSE                                                               
                 DISPLAY 'ERROR READING BP13K870: ' WS-K870-STATUS              
                 MOVE WS-K870-STATUS TO RETURN-CODE                             
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
             END-IF                                                             
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       6000-K878-WRITE.                                                         
      *-------------------------------------------------------------            
            ACCEPT WS-CURRENT-TIME FROM TIME.                                   
            MOVE   WS-DATE         TO K878-DTE-UPDATE.                          
            MOVE   WS-CURRENT-TIME TO K878-TME-UPDATE.                          
                                                                                
            ADD 1 TO WS-TME-CNT.                                                
            MOVE WS-TME-CNT TO K878-TME-UPDATE-CNT.                             
            MOVE 'BP13C000' TO K878-NUM-USERID.                                 
            MOVE BP13K870-REC TO K878-NUM-UPDATE-BEFORE.                        
            WRITE BP13K878-REC                                                  
                  INVALID KEY DISPLAY 'ERROR WRITING BP13K878'.                 
            IF WS-K878-STATUS = 0                                               
               ADD 1 TO WS-K878-WRITE                                           
               PERFORM 7000-K870-DELETE THRU 7000-EXIT                          
            ELSE                                                                
               DISPLAY 'ERROR WRITING BP13K878: ' WS-K878-STATUS                
               MOVE WS-K878-STATUS TO RETURN-CODE                               
               PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                          
            END-IF.                                                             
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       7000-K870-DELETE.                                                        
      *-------------------------------------------------------------            
            DELETE BP13K870                                                     
                   INVALID KEY DISPLAY 'ERROR DELETING BP13K870'.               
            IF WS-K870-STATUS = 0                                               
               ADD 1 TO WS-K870-DELETE                                          
            ELSE                                                                
               DISPLAY 'ERROR DELETING BP13K870: ' WS-K870-STATUS               
               MOVE WS-K870-STATUS TO RETURN-CODE                               
               PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                          
            END-IF.                                                             
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       8000-K868-WRITE.                                                         
      *-------------------------------------------------------------            
            ACCEPT WS-CURRENT-TIME FROM TIME.                                   
            MOVE   WS-DATE         TO K868-DTE-UPDATE.                          
            MOVE   WS-CURRENT-TIME TO K868-TME-UPDATE.                          
                                                                                
            ADD 1 TO WS-TME-CNT.                                                
            MOVE WS-TME-CNT TO K868-TME-UPDATE-CNT.                             
            MOVE 'BP13C000' TO K868-NUM-USER.                                   
            MOVE SPACES TO K868-CDE-ACTION.                                     
            MOVE BP13K860-REC TO K868-NUM-LOG-BEFORE.                           
            WRITE BP13K868-REC.                                                 
            IF WS-K868-STATUS = 0                                               
               ADD 1 TO WS-K868-WRITE                                           
               PERFORM 8500-K860-DELETE THRU 8500-EXIT                          
            ELSE                                                                
               DISPLAY 'ERROR WRITING BP13K868: ' WS-K868-STATUS                
               DISPLAY 'KEY                   : ' K868-KEY-FLD                  
               MOVE WS-K868-STATUS  TO RETURN-CODE                              
               PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                          
            END-IF.                                                             
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       8500-K860-DELETE.                                                        
      *-------------------------------------------------------------            
            DELETE BP13K860.                                                    
            IF WS-K860-STATUS = 0                                               
               ADD 1 TO WS-K860-DELETE                                          
            ELSE                                                                
               DISPLAY 'ERROR DELETING BP13K860:' WS-K860-STATUS                
               MOVE WS-K860-STATUS TO RETURN-CODE                               
               PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                          
            END-IF.                                                             
                                                                                
       8500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
            DISPLAY 'NO REC READ FROM BP13F860  =' WS-F860-READ.                
            DISPLAY 'NO REC READ FROM BP13K860  =' WS-K860-READ.                
            DISPLAY 'NO REC READ FROM BP13K870  =' WS-K870-READ.                
            DISPLAY 'NO REC READ FROM SY08K100  =' WS-K100-READ.                
            DISPLAY 'NO REC WRITE TO BP13K868   =' WS-K868-WRITE.               
            DISPLAY 'NO REC WRITE TO BP13K878   =' WS-K878-WRITE.               
            DISPLAY 'NO REC DELETE FROM BP13K860=' WS-K860-DELETE.              
            DISPLAY 'NO REC DELETE FROM BP13K870=' WS-K870-DELETE.              
                                                                                
            CLOSE BP13F860                                                      
                  BP13K860                                                      
                  BP13K870                                                      
                  BP13K868                                                      
                  BP13K878                                                      
                  SY08K100.                                                     
                                                                                
            IF WS-K860-STATUS NOT = ZEROES AND 97                               
               DISPLAY 'CLOSE FAILURE IN BP13K860: ' WS-K860-STATUS.            
            IF WS-K870-STATUS NOT = ZEROES AND 97                               
               DISPLAY 'CLOSE FAILURE IN BP13K870: ' WS-K870-STATUS.            
            IF WS-K100-STATUS NOT = ZEROES AND 97                               
               DISPLAY 'CLOSE FAILURE IN SY08K100: ' WS-K100-STATUS.            
            IF WS-K868-STATUS NOT = ZEROES AND 97                               
               DISPLAY 'CLOSE FAILURE IN BP13K868: ' WS-K868-STATUS.            
            IF WS-K878-STATUS NOT = ZEROES AND 97                               
               DISPLAY 'CLOSE FAILURE IN BP13K878: ' WS-K878-STATUS.            
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
