       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CS04.                                                 
       AUTHOR.        SHARON DUMDUM.                                            
      *DATE-WRITTEN.  12/03/2007.                                               
                                                                                
      *************************************************************             
      *      SYSTEM OF COMMITMENT (BP13)                          *             
      *===========================================================*             
      *                                                           *             
      *  OBJECTIVES: 1. TO NOTIFY APPLICANTS THRU SMS OF THE      *             
      *                 RESULTS OF THE BALLOT AND THEIR SCHEDULED *             
      *                 APPOINTMENT TO BOOK FLAT.                 *             
      *              2. PROGRAM TO PUT MQ MESSAGE INTO QUEUE      *             
      *                 WHICH CONTAINS CUSTOMER'S HP#, QUEUE NO., *             
      *                 APPT DATE/TIME TO BE SENT BY THE JAVA     *             
      *                 INTERFACE.                                *             
      *                                                           *             
      *   PROGRAM LOGIC:                                          *             
      *         PROCESS INPUT FILES                               *             
      *         MQCONNECT TO QUEUE MANAGER                        *             
      *         MQOPEN QUEUE                                      *             
      *         MQPUT                                             *             
      *         MQCLOSE QUEUE                                     *             
      *         MQDISCONNECT FROM QUEUE MANAGER                   *             
      *                                                           *             
      *===========================================================*             
      * MODIFICATIONS :                                           *             
      *                                                           *             
      * CHG REQ#    DATE    AUTHOR REMARKS                        *             
      * -------- ---------- ------ ------------------------------ *             
      * BP133010 12/03/2007  SD10  NEW PROGRAM                    *             
      * BP132994 05/04/2007  SD10  CHANGE 12AM TO 12PM            *             
      * BP133062 30/04/2007  SD10  ADD APPT TYPE = 'AR'           *             
      *          02/05/2007  SD10  CHANGE FORMAT FOR APPT DTE     *             
      * BP133090 28/06/2007  SD10  CHANGE FORMAT FOR APPT DTE FROM*             
      *                            DD/MM/YY TO DD MMM YY          *             
      * BP133439 21/10/2008  CT2   ADD APPT TYPE = AV'            *             
      * BP133596 04/05/2009  PCL3  TO READ SYSIN CARD             *             
      * BP137144 05/01/2017  ESA1  TO EXPAND THE BP13K209 TO 500  *             
      *************************************************************             
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       SPECIAL-NAMES.                                                           
           SYSIN IS PARM-INPUT.                                                 
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT BP13F203 ASSIGN TO BP13F203                                   
                  ORGANIZATION    IS SEQUENTIAL                                 
                  FILE STATUS     IS BP13F203-STAT.                             
                                                                                
           SELECT BP13K209 ASSIGN TO BP13K209                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K209-KEY-FLD                               
                  FILE STATUS     IS BP13K209-STAT.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F203                                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       COPY BP13F203.                                                           
                                                                                
       FD  BP13K209                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K209.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
      *----------------------------------------------------------*              
      *  IMPORTANT!                                              *              
      *  FOR PRODUCTION, QUEUE MANAGER IS 'MQP1'                 *              
      *  FOR TESTING,    QUEUE MANAGER IS 'MQT1'                 *              
      *----------------------------------------------------------*              
       01  WS-QM-NAME               PIC X(48) VALUE 'MQP1'.                     
      *01  WS-QM-NAME               PIC X(48) VALUE 'MQT1'.                     
       01  WS-BP13Q-NAME            PIC X(48) VALUE SPACES.                     
                                                                                
      *----------------------------------------------------------*              
      *  OUTPUT QUEUE NAME TO CRIMSONLOGIC                       *              
      *  THE QUEUE NAME IS THE SAME FOR PRODUCTION AND TESTING   *              
      *----------------------------------------------------------*              
       01  WS-QUEUE-NAME   PIC X(48) VALUE 'BP13.WIS.MDB.REQUESTQ'.             
      *01  WS-QUEUE-NAME   PIC X(48) VALUE 'BP13S.WIS.MDB.REQUESTQ'.            
                                                                                
      *----------------------------------------------------------*              
      *  MESSAGE LENGTH                                          *              
      *----------------------------------------------------------*              
       01  WS-MESSAGE-LENGTH        PIC 9(06) VALUE 105008.                     
                                                                                
      *----------------------------------------------------------*              
      *  MESSAGE                                                 *              
      *----------------------------------------------------------*              
       01  WS-SEND-MESSAGE.                                                     
           05  WS-MSG-CDE           PIC X(03).                                  
           05  WS-FILLER-01         PIC X(01).                                  
           05  WS-MSG-CTR           PIC 9(03).                                  
           05  WS-FILLER-02         PIC X(01).                                  
           05  WS-MSG OCCURS 500 TIMES.                                         
               10  WS-NUM-HP        PIC 9(08).                                  
               10  WS-FILLER-03     PIC X(01).                                  
               10  WS-TXT-MSG       PIC X(200).                                 
               10  WS-FILLER-04     PIC X(01).                                  
                                                                                
      *----------------------------------------------------------*              
      *  OTHER VARIABLES                                         *              
      *----------------------------------------------------------*              
       01  WS-VARIABLES.                                                        
           05  WS-CTR               PIC 9(03).                                  
           05  WS-START             PIC 9(03).                                  
           05  WS-DTE-BALLOT        PIC X(06).                                  
           05  WS-QUEUE-NO          PIC X(05).                                  
           05  WS-DTE-APPMT         PIC X(09).                                  
           05  WS-TME-APPMT         PIC X(07).                                  
           05  WS-MSG-DETAIL        PIC X(320).                                 
           05  WS-MTH-BALLOT        PIC X(04).                                  
           05  WS-DTE               PIC X(08).                                  
           05  WS-TME.                                                          
               10  WS-TME-HH        PIC 9(02).                                  
               10  WS-TME-MM        PIC 9(02).                                  
           05  WS-APPT-HH           PIC 9(02).                                  
                                                                                
       01  WS-MTH-CONV.                                                         
           05  FILLER               PIC X(05)    VALUE 'Jan01'.                 
           05  FILLER               PIC X(05)    VALUE 'Feb02'.                 
           05  FILLER               PIC X(05)    VALUE 'Mar03'.                 
           05  FILLER               PIC X(05)    VALUE 'Apr04'.                 
           05  FILLER               PIC X(05)    VALUE 'May05'.                 
           05  FILLER               PIC X(05)    VALUE 'Jun06'.                 
           05  FILLER               PIC X(05)    VALUE 'Jul07'.                 
           05  FILLER               PIC X(05)    VALUE 'Aug08'.                 
           05  FILLER               PIC X(05)    VALUE 'Sep09'.                 
           05  FILLER               PIC X(05)    VALUE 'Oct10'.                 
           05  FILLER               PIC X(05)    VALUE 'Nov11'.                 
           05  FILLER               PIC X(05)    VALUE 'Dec12'.                 
       01  WS-MTH-CONV-TABLE REDEFINES WS-MTH-CONV.                             
           05  WS-TF-MTH    OCCURS 12 TIMES INDEXED BY WS-IDX.                  
               10  WS-TRAN-MM      PIC X(3).                                    
               10  WS-NUM-MM       PIC X(2).                                    
                                                                                
      *-------------------------------------------------------------            
      *  MQI - TEMPORARY VARIABLES FOR ERROR CODES                              
      *-------------------------------------------------------------            
       01  BP13CS04-REC.                                                        
           05 CS04-REASON          PIC 9(4).                                    
           05 CS04-COMPLETION-CODE PIC 9(4).                                    
           05 CS04-ERR-MSG         PIC X(66).                                   
           05 FILLER               PIC X(27).                                   
                                                                                
      *----------------------------------------------------------*              
      *              MQI STRUCTURE DECLARATION                   *              
      *----------------------------------------------------------*              
       01 MY-MQ-CONSTANTS.                                                      
          COPY CMQV.                                                            
                                                                                
       01 OBJECT-DESCRIPTOR.                                                    
          COPY CMQODV.                                                          
                                                                                
       01 MESSAGE-DESCRIPTOR.                                                   
          COPY CMQMDV.                                                          
                                                                                
      * PUT MESSAGE OPTIONS                                                     
       01 PMOPTIONS.                                                            
          COPY CMQPMOV.                                                         
                                                                                
      *----------------------------------------------------------*              
      * VARIABLE DESCRIPTION:                                    *              
      * (1) HCONN                                                *              
      *       - CONNECTION HANDLE - REPRESENTS THE CONNECTION TO *              
      *         THE QUEUE MANAGER. VALUE IS RETURNED BY MQCONN   *              
      *         AND SUBSEQUENTLY REFERENCED IN MQOPEN, MQPUT,    *              
      *         MQCLOSE AND MQDISC                               *              
      *                                                          *              
      * (2) Q-HANDLE                                             *              
      *       - ACCESS THAT HAS BEEN ESTABLISHED  TO THE OBJECT  *              
      *         CONNECTION HANDLE - VALUE IS RETURNED BY MQOPEN  *              
      *         AND SUBSEQUENTLY REFERENCED IN MQPUT AND MQCLOSE *              
      *                                                          *              
      * (3) OBJECT-DESCRIPTOR                                    *              
      *       - THIS IDENTIFIES THE OBJECT TO BE OPENED          *              
      *                                                          *              
      * (4) OPTIONS                                              *              
      *       WHEN USED IN MQOPEN                                *              
      *       - OPTIONS THAT CONTROL THE ACTION OF MQOPEN        *              
      *       - OPTIONS USED IN THIS PROGRAM:                    *              
      *         MQOO-OUTPUT - THE QUEUE IS OPENED FOR USE WITH   *              
      *                       SUBSEQUENT MQPUT CALL              *              
      *         MQOO-FAIL-IF-QUIESCING - FORCES MQCALL TO FAIL   *              
      *                       IF QUEUE MANAGER IS IN QUIESCING   *              
      *                       STATE (I.E. PREPARING TO SHUTDOWN) *              
      *                                                          *              
      *       WHEN USED IN MQCLOSE                               *              
      *       - OPTIONS THAT CONTROL THE ACTION OF MQCLOSE       *              
      *       - OPTIONS USED IN THIS PROGRAM:                    *              
      *         MQCO-NONE   - NO OPTIONAL CLOSE PROCESSING       *              
      *                       REQUIRED                           *              
      *                                                          *              
      * (5) MESSAGE-DESCRIPTOR                                   *              
      *       - THIS DESCRIBES THE ATTRIBUTES OF THE MESSAGE     *              
      *         BEING SENT, AND RECEIVES INFO ABOUT THE MESSAGE  *              
      *         AFTER THE PUT REQUEST IS COMPLETE                *              
      *                                                          *              
      * (6) PMOPTIONS (PUT MESSAGE OPTIONS)                      *              
      *       - OPTIONS THAT CONTROL THE ACTION OF MQPUT         *              
      *       - OPTION USED IN THIS PROGRAM:                     *              
      *         MQMD-EXPIRY - MESSAGE LIFETIME. THE MESSAGE      *              
      *                       BECOMES ELIGIBLE TO BE DISCARDED   *              
      *                       IF IT HAS NOT BEEN REMOVED FROM    *              
      *                       FROM THE QUEUE BEFORE THIS PERIOD  *              
      *                       OF TIME ELAPSED                    *              
      *                     - MUST BE SET TO MQEI_UNLIMITED      *              
      *         MQMD-PERSISTENCE = MQPER-PERSISTENT              *              
      *                                                          *              
      * (7) BUFFER-LENGTH                                        *              
      *       - LENGTH OF THE MESSAGE IN BUFFER                  *              
      *                                                          *              
      * (7) BUFFER                                               *              
      *       - MESSAGE DATA                                     *              
      *----------------------------------------------------------*              
       01 HCONN                    PIC S9(9) BINARY.                            
       01 Q-HANDLE                 PIC S9(9) BINARY.                            
       01 OPTIONS                  PIC S9(9) BINARY.                            
       01 COMPLETION-CODE          PIC S9(9) BINARY.                            
       01 OPEN-CODE                PIC S9(9) BINARY.                            
       01 CON-REASON               PIC S9(9) BINARY.                            
       01 REASON                   PIC S9(9) BINARY.                            
       01 MQMSGD                   PIC S9(9) BINARY.                            
       01 BUFFER-LENGTH            PIC S9(9) BINARY.                            
       01 BUFFER                   PIC X(105008).                               
       01 QM-NAME                  PIC X(48) VALUE SPACES.                      
       01 TARGET-QUEUE             PIC X(48) VALUE SPACES.                      
                                                                                
      *-------------------------------------------------------------            
      *  FILE STATUS                                                            
      *-------------------------------------------------------------            
       01  WS-FILE-STAT.                                                        
           05 BP13F203-STAT        PIC X(02)  VALUE SPACES.                     
           05 BP13K209-STAT        PIC X(02)  VALUE SPACES.                     
                                                                                
      *-------------------------------------------------------------            
      *  FLAGS / SWITCHES                                                       
      *-------------------------------------------------------------            
       01  WS-FLAGS.                                                            
           05 F203-EOF             PIC X(01) VALUE 'N'.                         
           05 K209-EOF             PIC X(01) VALUE 'N'.                         
           05 F203-EMPTY           PIC X(01) VALUE 'N'.                         
           05 K209-EMPTY           PIC X(01) VALUE 'N'.                         
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
                                                                                
      ********************                                                      
       0000-MAIN-PROCESS.                                                       
      ********************                                                      
           PERFORM 0100-OPEN-FILES         THRU 0100-EXIT.                      
           PERFORM 0200-INITIALIZATION     THRU 0200-EXIT.                      
                                                                                
           ACCEPT WS-BP13Q-NAME            FROM PARM-INPUT.                     
                                                                                
           PERFORM 1000-CHECK-MESSAGE      THRU 1000-EXIT.                      
           PERFORM 3000-PROCESS-BP13F203   THRU 3000-EXIT                       
                   UNTIL F203-EOF = 'Y'.                                        
           DISPLAY 'WS-SEND-MESSAGE : ' WS-SEND-MESSAGE.                        
           PERFORM 5300-MQ-PUT-MESSAGE     THRU 5300-EXIT.                      
           DISPLAY ' *** APPOINTMENT NOTIFICATION SENT!'                        
           PERFORM 5400-MQ-CLOSE           THRU 5400-EXIT.                      
           PERFORM 5500-MQ-DISCONNECT      THRU 5500-EXIT.                      
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       0100-OPEN-FILES.                                                         
      ********************                                                      
           DISPLAY ' ***** STARTING PROGRAM BP13CS04 . . . '.                   
           DISPLAY ' '.                                                         
           OPEN    INPUT   BP13F203                                             
                           BP13K209.                                            
                                                                                
           IF  BP13F203-STAT     NOT  =  00                                     
               DISPLAY ' OPEN ERROR BP13F203'                                   
               DISPLAY 'BP13F203-STAT = ' BP13F203-STAT                         
               MOVE     12                   TO RETURN-CODE                     
               PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT.                    
                                                                                
           IF  BP13K209-STAT     NOT  =  00                                     
               DISPLAY ' OPEN ERROR BP13K209'                                   
               DISPLAY 'BP13K209-STAT = ' BP13K209-STAT                         
               MOVE     12                   TO RETURN-CODE                     
               PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT.                    
                                                                                
       0100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       0200-INITIALIZATION.                                                     
      **********************                                                    
                                                                                
           MOVE SPACES     TO WS-SEND-MESSAGE.                                  
           INITIALIZE         WS-SEND-MESSAGE.                                  
           MOVE ';'        TO WS-FILLER-01                                      
                              WS-FILLER-02.                                     
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 500                
              MOVE ';'     TO WS-FILLER-03(WS-CTR)                              
                              WS-FILLER-04(WS-CTR)                              
           END-PERFORM.                                                         
                                                                                
           MOVE 'SMS'      TO WS-MSG-CDE.                                       
                                                                                
      *-----------------------------                                            
      *  INITIAL READ OF INPUT FILES                                            
      *-----------------------------                                            
           READ BP13F203                                                        
               AT END                                                           
                   MOVE 'Y' TO F203-EOF                                         
                   MOVE 'Y' TO F203-EMPTY                                       
                   DISPLAY ' *** '                                              
                   DISPLAY ' *** NO RECORDS FOUND IN SMS FILE'                  
                   DISPLAY ' *** '.                                             
                                                                                
           READ BP13K209 NEXT RECORD                                            
               AT END                                                           
                   MOVE 'Y' TO K209-EOF                                         
                   MOVE 'Y' TO K209-EMPTY                                       
                   DISPLAY ' *** '                                              
                   DISPLAY ' *** NO RECORDS FOUND IN SMS MESSAGES FILE'         
                   DISPLAY ' *** '.                                             
                                                                                
       0200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       1000-CHECK-MESSAGE.                                                      
      ************************                                                  
           IF F203-EMPTY = 'Y' AND K209-EMPTY = 'Y'                             
               PERFORM 9000-CLOSE-FILES  THRU  9000-EXIT                        
           ELSE                                                                 
               PERFORM 5100-MQ-CONNECT   THRU  5100-EXIT                        
               PERFORM 5200-MQ-OPEN      THRU  5200-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO BUFFER.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       3000-PROCESS-BP13F203.                                                   
      ************************                                                  
           IF F203-TYPE-APPMT = 'AP' OR 'AQ' OR 'AR' OR 'AT' OR 'AV'            
              IF F203-NUM-HP NOT = SPACES AND LOW-VALUES AND ZEROES             
                 MOVE F203-TYPE-APPMT         TO K209-TYPE-APPMT                
                 PERFORM 3100-READ-BP13K209   THRU 3100-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
           READ BP13F203                                                        
               AT END                                                           
                   MOVE 'Y' TO F203-EOF.                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3100-READ-BP13K209.                                                      
      ****************************************************************          
                                                                                
           READ BP13K209.                                                       
           IF BP13K209-STAT = 00                                                
              MOVE K209-NUM-MSG      TO WS-MSG-DETAIL                           
              PERFORM 3200-PASS-MSG  THRU 3200-EXIT                             
           ELSE                                                                 
              IF BP13K209-STAT = 23                                             
                 DISPLAY 'APPOINTMENT TYPE NOT FOUND : ' K209-KEY-FLD           
                 MOVE BP13K209-STAT  TO RETURN-CODE                             
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3200-PASS-MSG.                                                           
      ****************************************************************          
                                                                                
           MOVE SPACES     TO WS-DTE-BALLOT                                     
                              WS-QUEUE-NO                                       
                              WS-DTE-APPMT                                      
                              WS-TME-APPMT.                                     
                                                                                
           ADD 1  TO WS-MSG-CTR.                                                
                                                                                
           IF F203-DTE-BALLOT NOT = SPACES AND LOW-VALUES AND ZEROES            
              MOVE F203-DTE-BALLOT            TO WS-DTE                         
              PERFORM 3300-FORMAT-DTE         THRU 3300-EXIT                    
              STRING WS-MTH-BALLOT                                              
                     F203-DTE-BALLOT(3:2)                                       
                     DELIMITED BY SIZE                                          
                INTO WS-DTE-BALLOT                                              
           END-IF.                                                              
                                                                                
           IF F203-NUM-QUEUE NOT = SPACES AND LOW-VALUES AND ZEROES             
              MOVE F203-NUM-QUEUE    TO WS-QUEUE-NO                             
           END-IF.                                                              
                                                                                
           IF F203-DTE-APPMT NOT = SPACES AND LOW-VALUES AND ZEROES             
              MOVE F203-DTE-APPMT             TO WS-DTE                         
              PERFORM 3300-FORMAT-DTE         THRU 3300-EXIT                    
              STRING F203-DTE-APPMT(7:2) ' '                                    
                     WS-MTH-BALLOT                                              
                     F203-DTE-APPMT(3:2)                                        
                     DELIMITED BY SIZE                                          
                INTO WS-DTE-APPMT                                               
           END-IF.                                                              
                                                                                
           IF F203-TME-APPMT NOT = SPACES AND LOW-VALUES AND ZEROES             
              MOVE F203-TME-APPMT    TO WS-TME                                  
              IF WS-TME-HH > 12                                                 
                 COMPUTE WS-APPT-HH = WS-TME-HH - 12                            
                 STRING WS-APPT-HH ':'                                          
                        WS-TME-MM  'pm'                                         
                        DELIMITED BY SIZE                                       
                 INTO WS-TME-APPMT                                              
              ELSE                                                              
                 IF WS-TME-HH = 12                                              
                    STRING WS-TME-HH  ':'                                       
                           WS-TME-MM  'pm'                                      
                           DELIMITED BY SIZE                                    
                    INTO WS-TME-APPMT                                           
                 ELSE                                                           
                    STRING WS-TME-HH  ':'                                       
                           WS-TME-MM  'am'                                      
                           DELIMITED BY SIZE                                    
                    INTO WS-TME-APPMT                                           
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3400-CHG-BALLOT-DTE THRU 3400-EXIT.                          
           PERFORM 3500-CHG-QUEUE-NUM  THRU 3500-EXIT.                          
           PERFORM 3600-CHG-APPT-DTE   THRU 3600-EXIT.                          
           PERFORM 3700-CHG-APPT-TME   THRU 3700-EXIT.                          
                                                                                
                                                                                
           MOVE F203-NUM-HP(1:8)     TO WS-NUM-HP (WS-MSG-CTR).                 
           MOVE WS-MSG-DETAIL        TO WS-TXT-MSG (WS-MSG-CTR).                
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3300-FORMAT-DTE.                                                         
      ****************************************************************          
                                                                                
           SET WS-IDX TO 1.                                                     
                                                                                
           SEARCH WS-TF-MTH                                                     
               AT END                                                           
                  MOVE SPACES              TO WS-MTH-BALLOT                     
               WHEN WS-NUM-MM (WS-IDX) = WS-DTE(5:2)                            
                  MOVE WS-TRAN-MM (WS-IDX) TO WS-MTH-BALLOT                     
           END-SEARCH.                                                          
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3400-CHG-BALLOT-DTE.                                                     
      ****************************************************************          
                                                                                
           MOVE ZEROES              TO WS-START.                                
           PERFORM VARYING WS-START FROM 1 BY 1 UNTIL WS-START > 320            
              IF WS-MSG-DETAIL(WS-START:6) = 'BLTDTE'                           
                 MOVE WS-DTE-BALLOT TO WS-MSG-DETAIL(WS-START:6)                
                 MOVE 321           TO WS-START                                 
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3500-CHG-QUEUE-NUM.                                                      
      ****************************************************************          
                                                                                
           MOVE ZEROES              TO WS-START.                                
           PERFORM VARYING WS-START FROM 1 BY 1 UNTIL WS-START > 320            
              IF WS-MSG-DETAIL(WS-START:4) = 'QNUM'                             
                 MOVE WS-QUEUE-NO   TO WS-MSG-DETAIL(WS-START:5)                
                 MOVE 321           TO WS-START                                 
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3600-CHG-APPT-DTE.                                                       
      ****************************************************************          
                                                                                
           MOVE ZEROES              TO WS-START.                                
           PERFORM VARYING WS-START FROM 1 BY 1 UNTIL WS-START > 320            
              IF WS-MSG-DETAIL(WS-START:9) = 'DD/MMM/YY'                        
                 MOVE WS-DTE-APPMT  TO WS-MSG-DETAIL(WS-START:9)                
                 MOVE 321           TO WS-START                                 
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3700-CHG-APPT-TME.                                                       
      ****************************************************************          
                                                                                
           MOVE ZEROES              TO WS-START.                                
           PERFORM VARYING WS-START FROM 1 BY 1 UNTIL WS-START > 320            
              IF WS-MSG-DETAIL(WS-START:5) = 'HH:MM'                            
                 MOVE WS-TME-APPMT  TO WS-MSG-DETAIL(WS-START:7)                
                 MOVE 321           TO WS-START                                 
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5100-MQ-CONNECT.                                                         
      ****************************************************************          
      *---------------------------------------------------------------          
      *  SET THE QUEUE MANAGER NAME                                             
      *---------------------------------------------------------------          
           IF WS-BP13Q-NAME  NOT = SPACES AND LOW-VALUES                        
              MOVE WS-BP13Q-NAME           TO   QM-NAME                         
           ELSE                                                                 
              MOVE WS-QM-NAME              TO   QM-NAME                         
           END-IF.                                                              
                                                                                
      *---------------------------------------------------------------          
      *   MQCONN                                                                
      *---------------------------------------------------------------          
           CALL 'MQCONN'                                                        
                USING QM-NAME, HCONN,                                           
                COMPLETION-CODE, CON-REASON.                                    
                                                                                
           IF COMPLETION-CODE  = MQCC-FAILED                                    
              MOVE CON-REASON              TO   CS04-REASON                     
              MOVE COMPLETION-CODE         TO   CS04-COMPLETION-CODE            
              MOVE 'BP13CS04 MQCONN ERROR' TO   CS04-ERR-MSG                    
              PERFORM 9100-EXIT-MQCONN     THRU 9100-EXIT                       
           END-IF.                                                              
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       5200-MQ-OPEN.                                                            
      ****************************************************************          
      *---------------------------------------------------------------          
      *  USE DEFAULT WS-QUEUE-NAME                                              
      *---------------------------------------------------------------          
           MOVE  WS-QUEUE-NAME         TO MQOD-OBJECTNAME.                      
           ADD   MQOO-OUTPUT MQOO-FAIL-IF-QUIESCING                             
                               GIVING OPTIONS.                                  
                                                                                
      *---------------------------------------------------------------          
      *   MQOPEN                                                                
      *---------------------------------------------------------------          
           CALL 'MQOPEN'                                                        
                USING HCONN, OBJECT-DESCRIPTOR,                                 
                OPTIONS, Q-HANDLE,                                              
                OPEN-CODE, REASON.                                              
                                                                                
           IF REASON NOT =  MQRC-NONE                                           
              MOVE REASON                     TO   CS04-REASON                  
              MOVE OPEN-CODE                  TO   CS04-COMPLETION-CODE         
              MOVE 'MQOPEN ENDED WITH REASON CODE'                              
                                              TO   CS04-ERR-MSG                 
              PERFORM 9100-EXIT-MQCONN        THRU 9100-EXIT                    
           END-IF.                                                              
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       5300-MQ-PUT-MESSAGE.                                                     
      ***********************                                                   
           MOVE WS-SEND-MESSAGE     TO BUFFER.                                  
                                                                                
      *---------------------------------------------------------------          
      *  SET UP MESSAGE DESCRIPTOR                                              
      *---------------------------------------------------------------          
           MOVE MQEI-UNLIMITED      TO MQMD-EXPIRY.                             
           MOVE MQPER-PERSISTENT    TO MQMD-PERSISTENCE.                        
           MOVE MQFMT-STRING        TO MQMD-FORMAT.                             
           MOVE WS-MESSAGE-LENGTH   TO BUFFER-LENGTH.                           
      *---------------------------------------------------------------          
      *   MQPUT                                                                 
      *---------------------------------------------------------------          
           CALL 'MQPUT'                                                         
                USING HCONN, Q-HANDLE,                                          
                      MESSAGE-DESCRIPTOR, PMOPTIONS,                            
                      BUFFER-LENGTH, BUFFER,                                    
                      COMPLETION-CODE, REASON.                                  
                                                                                
           IF REASON NOT =  MQRC-NONE                                           
              MOVE REASON                  TO   CS04-REASON                     
              MOVE COMPLETION-CODE         TO   CS04-COMPLETION-CODE            
              MOVE 'BP13CS04 MQPUT ERROR'  TO   CS04-ERR-MSG                    
              PERFORM 9100-EXIT-MQCONN     THRU 9100-EXIT                       
           END-IF.                                                              
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5400-MQ-CLOSE.                                                           
      ****************************************************************          
           MOVE MQCO-NONE TO OPTIONS.                                           
                                                                                
      *---------------------------------------------------------------          
      *   MQCLOSE                                                               
      *---------------------------------------------------------------          
           CALL 'MQCLOSE'                                                       
                USING HCONN, Q-HANDLE, OPTIONS,                                 
                COMPLETION-CODE, REASON.                                        
                                                                                
           IF REASON NOT =  MQRC-NONE                                           
              MOVE REASON                   TO   CS04-REASON                    
              MOVE COMPLETION-CODE          TO   CS04-COMPLETION-CODE           
              MOVE 'BP13CS04 MQCLOSE ERROR' TO   CS04-ERR-MSG                   
              PERFORM 9100-EXIT-MQCONN      THRU 9100-EXIT                      
           END-IF.                                                              
                                                                                
       5400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5500-MQ-DISCONNECT.                                                      
      ****************************************************************          
      *---------------------------------------------------------------          
      *  DISCONNECT FROM QUEUE MANAGER                                          
      *---------------------------------------------------------------          
           CALL 'MQDISC'                                                        
           USING HCONN, COMPLETION-CODE, REASON                                 
                                                                                
           IF REASON NOT =  MQRC-NONE                                           
              MOVE REASON                  TO   CS04-REASON                     
              MOVE COMPLETION-CODE         TO   CS04-COMPLETION-CODE            
              MOVE 'BP13CS04 MQDISC ERROR' TO   CS04-ERR-MSG                    
              PERFORM 9100-EXIT-MQCONN     THRU 9100-EXIT                       
           END-IF.                                                              
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       9000-CLOSE-FILES.                                                        
      ****************************************************************          
                                                                                
           CLOSE BP13F203                                                       
                 BP13K209.                                                      
                                                                                
           IF    RETURN-CODE NOT = ZERO                                         
                 DISPLAY ' *** RETURN-CODE = ' RETURN-CODE ' ERROR'             
           ELSE                                                                 
                 DISPLAY ' '                                                    
                 DISPLAY ' *** SUCCESSFUL EXECUTION OF BP13CS04.'               
                 DISPLAY ' *** MESSAGE/S PUT INTO QUEUE SUCCESSFULLY!'          
                 DISPLAY ' *** Q MANAGER : ' QM-NAME                            
                 DISPLAY ' *** TARGET Q  : ' WS-QUEUE-NAME                      
                 DISPLAY ' '                                                    
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9100-EXIT-MQCONN.                                                        
      ****************************************************************          
           DISPLAY ' *** PROGRAM ID:  BP13CS04 '                                
           DISPLAY ' *** Q MANAGER : ' QM-NAME                                  
           DISPLAY ' *** TARGET Q  : ' WS-QUEUE-NAME                            
           DISPLAY ' '                                                          
           IF CS04-COMPLETION-CODE NOT = ZEROES AND LOW-VALUES                  
              DISPLAY ' *** MQ COMPLETION CODE: ' CS04-COMPLETION-CODE          
              DISPLAY ' *** MQ REASON         : ' CS04-REASON                   
              DISPLAY ' *** MQ ERROR MSG      : ' CS04-ERR-MSG                  
              MOVE 99                    TO  RETURN-CODE                        
           END-IF.                                                              
                                                                                
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       9100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
      **********            END OF BP13CS04                 **********          
      ****************************************************************          
