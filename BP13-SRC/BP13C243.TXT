       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C243.                                                  
      *AUTHOR.       ANNALYN BANTA.                                             
      *DATE-WRITTEN. 18.03.94.                                                  
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *  OBJECTIVE   : TO UPDATE ALLOCATION SUMMARY RESULT.        *            
      *                                                            *            
      *  BP13F200    : SEQUENTIAL BKNG APPMT FILE                  *            
      *  BP13K222    : ALLOCATION SUMMARY RESULT FILE              *            
      *  BP13K800    : SOC MASTER FILE                             *            
      * ---------------------------------------------------------- *            
      * CHG-NO   BY    DATE   DESCRIPTION                          *            
      * -------- ---  ------  -----------                          *            
      * SOC-PH9  LMS  261094  TO INCLUDE ZONE.                     *            
      * BP130067 LMS  221295  CHANGE IN FILE BP13K222.             *            
      * BP130418 MPA  030498  YEAR 2000 CHANGES.                   *            
      * BP132095 MTD  230402  EXPAND LENGTH OF BP13F200            *            
      *                       (FROM 100 TO 300)                    *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F200 ASSIGN       TO BP13F200.                            
                                                                                
           SELECT BP13K222 ASSIGN       TO BP13K222                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K222-KEY-FLD                         
                           FILE STATUS  IS K222-STATUS.                         
                                                                                
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS K800-STATUS.                         
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F200                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 300 CHARACTERS                                       
           LABEL  RECORDS ARE STANDARD                                          
           RECORDING MODE IS F.                                                 
       COPY BP13F200.                                                           
                                                                                
       FD  BP13K222                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K222.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  K222-STATUS         PIC 99      VALUE ZERO.                      
           05  K800-STATUS         PIC 99      VALUE ZERO.                      
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F200-READ        PIC 9(8)    VALUE ZERO.                      
           05  WS-K222-UPDATE      PIC 9(8)    VALUE ZERO.                      
                                                                                
       01  WS-SWITCHES.                                                         
           05  F200-EOF            PIC X       VALUE 'N'.                       
               88  END-OF-F200                 VALUE 'Y'.                       
                                                                                
       01  WS-TEMP-VARIABLES.                                                   
           05 WS-F200-MATCH-KEY.                                                
              10 WS-F200-DTE-ALLOC PIC X(8)    VALUE SPACES.                    
              10 WS-F200-NT-ZONE   PIC X(3)    VALUE SPACES.                    
              10 WS-F200-FT        PIC X(2)    VALUE SPACES.                    
           05 WS-K222-MATCH-KEY.                                                
              10 WS-DTE-ALLOC      PIC X(8)    VALUE SPACES.                    
              10 WS-NT-ZONE        PIC X(3)    VALUE SPACES.                    
              10 WS-FT             PIC X(2)    VALUE SPACES.                    
           05 WS-START-BKAPPMT     PIC X(8)    VALUE SPACES.                    
           05 WS-END-BKAPPMT       PIC X(8)    VALUE SPACES.                    
           05 WS-ALLOC-DATE        PIC X(8)    VALUE SPACES.                    
           05 WS-CNT-BKAPPMT       PIC 9(4)    VALUE ZEROES.                    
           05 WS-CNT-BOOKED        PIC 9(4)    VALUE ZEROES.                    
           05 WS-FLAT-OFFER        PIC 9(4)    VALUE ZEROES.                    
           05 WS-FT-NO             PIC 9(2)    VALUE ZEROES.                    
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-LOGIC.                                                         
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 2000-READ-F200       THRU 2000-EXIT.                         
           PERFORM 3000-PROCESS-RTN     THRU 3000-EXIT                          
             UNTIL END-OF-F200.                                                 
           PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT.                         
                                                                                
                                                                                
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN  INPUT BP13F200                                                 
                       BP13K800                                                 
                 I-O   BP13K222.                                                
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              MOVE K800-STATUS            TO RETURN-CODE                        
              DISPLAY 'ERROR OPENING BP13K800 FILE, ' K800-STATUS               
              PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                         
                                                                                
           IF K222-STATUS NOT = 00 AND 97                                       
              MOVE K222-STATUS            TO RETURN-CODE                        
              DISPLAY 'ERROR OPENING BP13K222 FILE, ' K222-STATUS               
              PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                         
                                                                                
           INITIALIZE      WS-SWITCHES                                          
                           WS-TEMP-VARIABLES                                    
                           WS-COUNTERS.                                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2000-READ-F200.                                                          
      *-------------------------------------------------------------            
           READ BP13F200                                                        
             AT END MOVE 'Y' TO F200-EOF                                        
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                          TO WS-F200-READ.                      
           MOVE F200-DTE-ALLOC            TO WS-F200-DTE-ALLOC.                 
           MOVE F200-NUM-NT-ZONE          TO WS-F200-NT-ZONE.                   
           MOVE F200-NUM-FLAT-TYPE        TO WS-F200-FT.                        
                                                                                
           IF WS-F200-READ = 1                                                  
              MOVE F200-DTE-ALLOC         TO WS-DTE-ALLOC                       
              MOVE F200-NUM-NT-ZONE       TO WS-NT-ZONE                         
              MOVE F200-NUM-FLAT-TYPE     TO WS-FT                              
              MOVE F200-DTE-BKAPPMT-DATE  TO WS-START-BKAPPMT                   
              MOVE ZEROES                 TO WS-CNT-BKAPPMT                     
                                             WS-CNT-BOOKED                      
                                             WS-FLAT-OFFER.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3000-PROCESS-RTN.                                                        
      *-------------------------------------------------------------            
           IF WS-F200-MATCH-KEY NOT = WS-K222-MATCH-KEY                         
              PERFORM 4000-UPDATE-K222  THRU 4000-EXIT                          
              MOVE F200-DTE-ALLOC         TO WS-DTE-ALLOC                       
              MOVE F200-NUM-NT-ZONE       TO WS-NT-ZONE                         
              MOVE F200-NUM-FLAT-TYPE     TO WS-FT                              
              MOVE F200-DTE-BKAPPMT-DATE  TO WS-START-BKAPPMT                   
              MOVE ZEROES                 TO WS-CNT-BKAPPMT                     
                                             WS-CNT-BOOKED                      
                                             WS-FLAT-OFFER.                     
                                                                                
           PERFORM 3100-ACCUM-BKD-APPMT THRU 3100-EXIT.                         
           PERFORM 2000-READ-F200       THRU 2000-EXIT.                         
                                                                                
      * TO UPDATE THE LAST NT-ZONE-FT *                                         
                                                                                
           IF F200-EOF = 'Y'                                                    
              PERFORM 4000-UPDATE-K222  THRU 4000-EXIT                          
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3100-ACCUM-BKD-APPMT.                                                    
      *-------------------------------------------------------------            
           MOVE F200-DTE-BKAPPMT-DATE     TO WS-END-BKAPPMT.                    
           MOVE F200-DTE-ALLOC            TO WS-ALLOC-DATE.                     
           ADD  1                         TO WS-CNT-BKAPPMT.                    
                                                                                
           MOVE F200-NUM-REGN             TO K800-NUM-REGN.                     
           READ BP13K800.                                                       
           IF K800-STATUS = 00                                                  
              CONTINUE                                                          
           ELSE                                                                 
              IF K800-STATUS = 23                                               
                 DISPLAY 'RECORD NOT FOUND IN BP13K800 FILE'                    
                 DISPLAY 'REGN NO : ' K800-NUM-REGN                             
                 GO TO 3100-EXIT                                                
              ELSE                                                              
                 MOVE K800-STATUS         TO RETURN-CODE                        
                 DISPLAY 'ERROR READING BP13K800 FILE, ' K800-STATUS            
                 DISPLAY 'K800-KEY-FLD = ' K800-NUM-REGN                        
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT.                       
                                                                                
           IF K800-NUM-SCH-ACC NOT = SPACES                                     
              ADD 1                       TO WS-CNT-BOOKED.                     
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4000-UPDATE-K222.                                                        
      *-------------------------------------------------------------            
           MOVE WS-DTE-ALLOC              TO K222-DTE-ALLOC.                    
           MOVE WS-NT-ZONE                TO K222-NUM-NT-ZONE.                  
           MOVE WS-FT                     TO K222-NUM-FLAT-TYPE.                
                                                                                
           READ BP13K222.                                                       
                                                                                
           IF K222-STATUS = 00                                                  
              CONTINUE                                                          
           ELSE                                                                 
              IF K222-STATUS = 23                                               
                 DISPLAY 'BP13K222 REC NOT FOUND, ' K222-KEY-FLD                
                 GO TO 4000-EXIT                                                
              ELSE                                                              
                 MOVE K222-STATUS         TO RETURN-CODE                        
                 DISPLAY 'ERROR READING BP13K222 FILE, ' K222-STATUS            
                 DISPLAY 'K222-KEY-FLD = ' K222-KEY-FLD                         
                 PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                  
                                                                                
           IF K222-NUM-BOOKED NOT = WS-CNT-BOOKED                               
              MOVE WS-CNT-BOOKED          TO K222-NUM-BOOKED                    
              REWRITE BP13K222-REC                                              
              IF K222-STATUS = 00                                               
                 ADD 1                    TO WS-K222-UPDATE                     
              ELSE                                                              
                 MOVE K222-STATUS         TO RETURN-CODE                        
                 DISPLAY 'ERROR UPDATING BP13K222 FILE, ' K222-STATUS           
                 DISPLAY 'K222-KEY-FLD = ' K222-KEY-FLD                         
                 PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY '*-------------- BP13C243 --------------*'.                  
           DISPLAY 'TOTAL REC READ    (BP13F200) = ' WS-F200-READ.              
           DISPLAY 'TOTAL REC UPDATED (BP13K222) = ' WS-K222-UPDATE.            
                                                                                
           CLOSE BP13F200                                                       
                 BP13K800                                                       
                 BP13K222.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
