      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13CFA9.                                                 
       AUTHOR.        ZARITA DANE SARMIENTO.                                    
       DATE-WRITTEN.  07 AUG  2017.                                             
      *===============================================================*         
      * OBJECTIVE  :  TO CREATE BP13F23A FORMAT                       *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F213                                     *         
      *               2. BP13K813                                     *         
      *                                                               *         
      * OUTPUT FILE:                                                            
      *               1. BP13F23A                                     *         
      *               2. BP13F23B                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP136992 07/08/2017 ZDD1 NEW PROGRAM.                         *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F213  ASSIGN        TO BP13F213.                         
                                                                                
           SELECT BP13K800   ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
                                                                                
           SELECT  BP13K813  ASSIGN        TO BP13K813                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K813-KEY-FLD                      
                             FILE STATUS   IS WS-K813-STATUS.                   
                                                                                
                                                                                
           SELECT  BP13F23A  ASSIGN        TO BP13F23A.                         
           SELECT  BP13F23B  ASSIGN        TO BP13F23B.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F213                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01  BP13F213-REC.                                                        
           05  F213-DETAILS.                                                    
               10  F213-NUM-ALLO-MODE         PIC X(3).                 00161400
               10  F213-NUM-NEW-TOWN          PIC X(3).                 00161000
               10  F213-NUM-FLAT-TYPE         PIC X(2).                 00161105
               10  F213-TYPE-APPMT            PIC X(2).                 00161000
               10  F213-NUM-ETHNIC            PIC X(1).                 00161000
           05  F213-KEY-FLD.                                                    
               10  F213-NUM-REGN              PIC X(8).                 00161000
           05  F213-DETAILS2.                                                   
               10  F213-DTE-BALLOT            PIC X(6).                 00161400
               10  F213-DTE-APPMT             PIC X(8).                 00161200
               10  F213-NUM-QUEUE             PIC X(5).                 00161400
           05  F213-TME-APPMT                 PIC X(4).                 00161200
           05  F213-NUM-NRIC                  PIC X(9).                 00161400
           05  F213-NME-NRIC                  PIC X(66).                00161400
           05  K203-ALT-KEY.                                                    
               10  F213-NUM-HP                PIC X(9).                 00161400
           05  F213-NUM-APLCNT                PIC X(3).                 00161400
           05  F213-NUM-ZONE                  PIC X(2).                 00161000
           05  F213-NUM-REF                   PIC X(8).                 00161000
           05  F213-NUM-BLOCK                 PIC X(1).                 00161000
           05  F213-NUM-STATUS                PIC X(1).                 00161400
           05  F213-DTE-BOOK-PD-END           PIC X(8).                 00161400
           05  F213-NUM-UNIT-OFFER            PIC X(4).                 00161400
           05  FILLER                         PIC X(47).                00161400
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13F23A                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   BP13F23A-REC   PIC X(200).                                          
                                                                                
       FD   BP13F23B                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   BP13F23B-REC   PIC X(200).                                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
                                                                                
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F213             PIC X(1)  VALUE 'N'.                     
           05  WS-K813-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F213-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-F23A             PIC 9(7)  VALUE ZEROES.                  
           05  WS-K800-FND             PIC 9(7)  VALUE ZEROES.                  
           05  WS-K800-NFND            PIC 9(7)  VALUE ZEROES.                  
           05  WS-K813-FND             PIC 9(7)  VALUE ZEROES.                  
           05  WS-K813-NFND            PIC 9(7)  VALUE ZEROES.                  
           05  WS-LE-300               PIC 9(7)  VALUE ZEROES.                  
           05  WS-GT-300               PIC 9(7)  VALUE ZEROES.                  
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F213      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F213 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F213                                                 
                       BP13K813                                                 
                       BP13K800                                                 
               OUTPUT  BP13F23A                                                 
                       BP13F23B.                                                
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K813 - ERROR OPENING : ' WS-K813-STATUS              
              MOVE WS-K813-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE         THRU 9999-EXIT                
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE         THRU 9999-EXIT                
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F213.                                                      
      ******************************************************************        
                                                                                
           READ BP13F213           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F213                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                    TO WS-F213-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
           MOVE F213-NUM-REGN  TO K800-NUM-REGN                                 
           PERFORM 4100-READ-BP13K800             THRU 4100-EXIT.               
                                                                                
           PERFORM 5000-READ-BP13K813          THRU 5000-EXIT                   
                                                                                
           MOVE K813-NUM-UNIT-OFFER  TO F213-NUM-UNIT-OFFER                     
                                                                                
           IF F213-NUM-QUEUE > K813-NUM-QUEUE-END                               
             WRITE BP13F23B-REC   FROM BP13F213-REC                             
             ADD 1 TO WS-GT-300                                                 
           ELSE                                                                 
             WRITE BP13F23A-REC   FROM BP13F213-REC                             
             ADD 1 TO WS-LE-300                                                 
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F213                   THRU 2000-EXIT.         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4100-READ-BP13K800.                                                      
      *********************                                                     
           INITIALIZE BP13K800-MASTER.                                          
                                                                                
           MOVE F213-NUM-REGN   TO K800-NUM-REGN.                               
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = ZEROS OR 02                                      
              ADD 1 TO WS-K800-FND                                              
           ELSE                                                                 
              IF WS-K800-STATUS = 23                                            
                 ADD 1 TO WS-K800-NFND                                          
                 DISPLAY 'READ BP13K800 NOT FD = ' F213-NUM-REGN                
              ELSE                                                              
                 DISPLAY 'READ ERROR, BP13K800 STATUS = '                       
                 WS-K800-STATUS                                                 
                 MOVE WS-K800-STATUS TO RETURN-CODE                             
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-READ-BP13K813.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                            TO BP13K813-REC.              
           INITIALIZE                                BP13K813-REC.              
                                                                                
      *    MOVE ZEROES                            TO WS-K813-MID                
      *                                              WS-K813-END                
      *                                              WS-K813-START.             
                                                                                
           MOVE 'ROF'                          TO K813-NUM-ZONE                 
           MOVE 'RF'                           TO K813-NUM-FLAT-TYPE            
                                                                                
           MOVE K800-DTE-BALLOT                   TO K813-DTE-BALLOT.           
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
               WHEN 00                                                          
                    ADD 1                         TO WS-K813-FND                
                    IF K813-NUM-QUEUE-START NOT NUMERIC                         
                       MOVE ZEROES              TO K813-NUM-QUEUE-START         
                    END-IF                                                      
                                                                                
                    IF K813-NUM-QUEUE-MID NOT NUMERIC                           
                       MOVE ZEROES                TO K813-NUM-QUEUE-MID         
                    END-IF                                                      
                                                                                
                    IF K813-NUM-QUEUE-END NOT NUMERIC                           
                       MOVE ZEROES                TO K813-NUM-QUEUE-END         
                    END-IF                                                      
                                                                                
               WHEN 23                                                          
                    ADD 1                         TO WS-K813-NFND               
                    DISPLAY 'BP13K813 NOT FND : ' K813-KEY-FLD                  
                            ' REGN : ' F213-NUM-REGN                            
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K813 : ' WS-K813-STATUS          
                            ' ZONE = ' K813-NUM-ZONE                            
                    PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CFA9'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY ' (1) NO OF BP13F213 RECORDS READ............. : '           
                    WS-F213-READ.                                               
           DISPLAY 'NO OF REC FOUND BP13K800             ........ : '           
                    WS-K800-FND.                                                
           DISPLAY 'NO OF REC NOT FOUND BP13K800         ........ : '           
                    WS-K800-NFND.                                               
           DISPLAY 'NO OF REC FOUND BP13K813             ........ : '           
                    WS-K813-FND.                                                
           DISPLAY 'NO OF REC WRITTEN ON F23A -LE 300%   ........ : '           
                    WS-LE-300.                                                  
           DISPLAY 'NO OF REC WRITTEN ON F23A -GT 300%   ........ : '           
                    WS-GT-300.                                                  
           DISPLAY ' '.                                                         
                                                                                
           CLOSE    BP13F213                                                    
                    BP13K813                                                    
                    BP13K800                                                    
                    BP13F23A.                                                   
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR CLOSING : ' WS-K800-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K813 - ERROR CLOSING : ' WS-K813-STATUS              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
