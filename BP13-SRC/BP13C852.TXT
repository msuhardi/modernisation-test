       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C852.                                                    
      *AUTHOR. MARIA THERESA R. SANTOS.                                         
      *DATE-WRITTEN.   13/5/95.                                                 
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *                                                        *                
      *    1.  PROGRAM TO CHECK THE JOINT BALLOT LINKS, SO LONG*                
      *        AS ONE OF THEM IS NOT SUCCESSFUL, OUTPUT THE    *                
      *        WHOLE LINKS FOR ANOTHER ROUND OF JOINT BALLOT   *                
      *        PIGEONHOLING                                    *                
      *        OR THE WHOLE LINKS ARE UNUSCCESSFUL.            *                
      *                                                        *                
      *        DO NOT OUTPUT IF ::                             *                
      *        - ALL ARE SUCCESSFUL                            *                
      *                                                        *                
      *    INPUT  FILES : BP13F520                             *                
      *                   BP13K520                             *                
      *                                                        *                
      *    OUTPUT FILES : BP13F510                             *                
      *                                                        *                
      *    LISTING      : NIL                                  *                
      *                                                        *                
      * CHG-NO   BY   ON     DESCRIPTION                       *                
      * --------------------------------                       *                
      * C950543 LSB 950516 - 2ND QTR, NEW REQ FOR JB           *                
      * C950543 LSB 950621 - TO INCLUDE WHOLE LINK UNSUCC      *                
      *BP130146 LMS 960914 - TO INCREASE THE COUNTER FOR       *                
      *                      STORE-CTR AND OUTPUT-CTR.         *                
      *BP130652 NOR 990219 - EXPAND F510 F520 & K520 RECL FROM *                
      *                      300 TO 500 CHARACTERS             *                
      **********************************************************                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F520   ASSIGN TO BP13F520.                                
           SELECT BP13K520   ASSIGN TO BP13K520                                 
                  ORGANIZATION    INDEXED                                       
                  ACCESS          RANDOM                                        
                  RECORD KEY      K520-NUM-REGN                                 
                  FILE STATUS     K520-FSTAT.                                   
           SELECT BP13F510   ASSIGN TO BP13F510.                                
           SELECT BP13L852   ASSIGN TO BP13L852.                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F520                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F520.                                                           
                                                                                
       FD   BP13K520                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
                                                                                
       COPY BP13K520.                                                           
                                                                                
       FD   BP13F510                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY  BP13F510.                                                          
                                                                                
       FD   BP13L852                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORDS ARE OMITTED.                                          
                                                                                
       01   PRINT-REC              PIC X(132).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WORK-AREAS.                                                          
           05  WS-F520-READ-SW     PIC X       VALUE '0'.                       
               88   END-OF-F520                VALUE '1'.                       
           05  WS-JB-BALLOT-STATUS PIC X       VALUE 'N'.                       
           05  WS-K520-SW          PIC X       VALUE 'N'.                       
           05  WS-READ-F520        PIC 9(8)    VALUE 0.                         
           05  WS-WRITE-F510       PIC 9(8)    VALUE 0.                         
           05  WS-BYPASS-F520      PIC 9(8)    VALUE 0.                         
           05  STORE-CTR           PIC 9(2)    VALUE 0.                         
           05  WS-UNSUCC-CNT       PIC 9(1)    VALUE 0.                         
           05  OUTPUT-CTR          PIC 9(2)    VALUE 0.                         
           05  K520-FSTAT          PIC X(2)    VALUE SPACES.                    
           05  WS-K520-BALLOT-STATUS PIC X(1)  VALUE SPACES.                    
           05  WS-SAVE-BALLOT-STATUS PIC X(1)  VALUE SPACES.                    
           05  WS-SAVE-SEQJB.                                                   
               10  WS-SAVE-SEQ     PIC X(8)    VALUE SPACES.                    
               10  WS-SAVE-JB      PIC X(8)    VALUE SPACES.                    
           05  WS-CUR-SEQJB.                                                    
               10  WS-CUR-SEQ      PIC X(8)    VALUE SPACES.                    
               10  WS-CUR-JB       PIC X(8)    VALUE SPACES.                    
           05  F520-TABLE.                                                      
               10  F520-REC        PIC X(500)  OCCURS 9.                        
                                                                                
      *                                                                         
       01  WS-COUNTERS.                                                         
           05  WS-PAGE-CNT                      PIC 9(4)  VALUE ZEROES.         
           05  WS-REC-CNT                       PIC 9(8)  VALUE ZEROES.         
           05  WS-READ-F500-CNT                 PIC 9(5)  VALUE ZEROES.         
           05  WS-WRITE-F500-CNT                PIC 9(5)  VALUE ZEROES.         
                                                                                
       01  WS-CURR-DATE.                                                        
           05  WS-CURR-YY                       PIC 9999  VALUE 0000.           
           05  WS-CURR-MM                       PIC 99    VALUE 00.             
           05  WS-CURR-DD                       PIC 99    VALUE 00.             
                                                                                
       01  WS-EDIT-DATE.                                                        
           05  WS-EDIT-DD                       PIC 99    VALUE 00.             
           05  FILLER                           PIC X     VALUE '/'.            
           05  WS-EDIT-MM                       PIC 99    VALUE 00.             
           05  FILLER                           PIC X     VALUE '/'.            
           05  WS-EDIT-YY                       PIC 9999  VALUE 0000.           
                                                                                
       01  WS-MISC.                                                             
           05  WS-SERNO                         PIC 9(4)  VALUE 0.              
           05  WS-DESC                          PIC X(3)  VALUE SPACES.         
                                                                                
      *----------------------------------------------------------------*        
      *   COMMON HEADER USE FOR BOTH REPORTS                           *        
      *----------------------------------------------------------------*        
       01  L852-HDG1.                                                           
           05  PRT-REPORT-TITLE         PIC X(9)  VALUE 'BP13L852'.             
           05  FILLER                   PIC X(37) VALUE SPACES.                 
           05  FILLER                   PIC X(40) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(29) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE :'.               
           05  PRT-DATE                 PIC X(10).                              
                                                                                
       01  L852-HDG2.                                                           
           05  FILLER                   PIC X(39) VALUE SPACES.                 
           05  FILLER                   PIC X(52) VALUE                         
               '       LIST OF UNSUCCESSFUL JOINT BALLOT LINKS      '.          
           05  FILLER                   PIC X(24) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'PAGE : '.              
           05  PRT-PAGE                 PIC ZZZ9.                               
                                                                                
       01  L852-HDG3.                                                           
           05  FILLER                   PIC X(4)  VALUE SPACES.                 
           05  FILLER                   PIC X(6)  VALUE                         
               'S/NO  '.                                                        
           05  FILLER                   PIC X(32) VALUE                         
               'ZONE/FT          SEQ            '.                              
           05  FILLER                   PIC X(41) VALUE                         
               'JB REGN         CUR REGN      SUCC NT   '.                      
           05  FILLER                   PIC X(48) VALUE                         
               'UNSUCC     ALLOC SCH   CATEGORY       HOUSEHOLD'.               
      *----------------------------------------------------------------*        
      *    05  FILLER                   PIC X(6)  VALUE                         
      *        'S/NO  '.                                                        
      *        '9999  '.                                                        
      *    05  FILLER                   PIC X(32) VALUE                         
      *        'ZONE/FT          SEQ            '.                              
      *        'XXX  XX        XXXXXXXX         '.                              
      *    05  FILLER                   PIC X(41) VALUE                         
      *        'JB REGN         CUR REGN      SUCC NT   '.                      
      *        'XXXXXXXX        XXXXXXXX        XXX     '.                      
      *    05  FILLER                   PIC X(48) VALUE                         
      *        'UNSUCC     ALLOC SCH   CATEGORY       HOUSEHOLD'.               
      *        '  X          XXX           X              X    '.               
      *----------------------------------------------------------------*        
      *----------------------------------------------------------------*        
      *   DETAIL RECORD                                                *        
      *----------------------------------------------------------------*        
       01  L852-DTL.                                                            
           05  FILLER                   PIC X(4)  VALUE SPACES.                 
           05  PRT-SERNO                PIC ZZZ9.                               
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  PRT-ZONE                 PIC X(3).                               
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  PRT-FT                   PIC X(2).                               
           05  FILLER                   PIC X(8)  VALUE SPACES.                 
           05  PRT-SEQ                  PIC X(8).                               
           05  FILLER                   PIC X(9)  VALUE SPACES.                 
           05  PRT-JBREGNO              PIC X(8).                               
           05  FILLER                   PIC X(8)  VALUE SPACES.                 
           05  PRT-CREGNO               PIC X(8).                               
           05  FILLER                   PIC X(8)  VALUE SPACES.                 
           05  PRT-SUCC-NT              PIC X(3).                               
           05  FILLER                   PIC X(7)  VALUE SPACES.                 
           05  PRT-UNSUCC               PIC X(3).                               
           05  FILLER                   PIC X(10) VALUE SPACES.                 
           05  PRT-ALLOC-SCH            PIC X(3)  VALUE SPACES.                 
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  PRT-CATEGORY             PIC X(1)  VALUE SPACES.                 
           05  FILLER                   PIC X(14) VALUE SPACES.                 
           05  PRT-HSEHOLD              PIC X(1)  VALUE SPACES.                 
      *                                                                         
       PROCEDURE DIVISION.                                                      
       000-MAIN.                                                                
                                                                                
           PERFORM 010-OPEN-RTN      THRU 010-EXIT.                             
                                                                                
           PERFORM 100-READ-F520     THRU 100-EXIT.                             
                                                                                
           PERFORM 200-PROCESS-VALIDATION  THRU 200-EXIT                        
                   UNTIL END-OF-F520                                            
                                                                                
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       010-OPEN-RTN.                                                            
                                                                                
           OPEN INPUT  BP13F520                                                 
                       BP13K520                                                 
                OUTPUT BP13F510                                                 
                       BP13L852.                                                
                                                                                
           IF K520-FSTAT NOT = '00' AND '97'                                    
              DISPLAY 'BP13K520 OPEN ERROR : ' K520-FSTAT                       
              GO TO 910-CLOSE-FILES.                                            
                                                                                
                                                                                
           MOVE ZEROES TO  WS-COUNTERS.                                         
           MOVE 50     TO  WS-REC-CNT                                           
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO   WS-CURR-DATE.                  
           MOVE    WS-CURR-YY       TO     WS-EDIT-YY.                          
           MOVE    WS-CURR-MM       TO     WS-EDIT-MM.                          
           MOVE    WS-CURR-DD       TO     WS-EDIT-DD.                          
           MOVE    WS-EDIT-DATE     TO     PRT-DATE.                            
                                                                                
       010-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-READ-F520.                                                           
                                                                                
           READ BP13F520                                                        
                AT END MOVE '1'  TO  WS-F520-READ-SW                            
                GO TO 100-EXIT.                                                 
                                                                                
           ADD 1 TO WS-READ-F520.                                               
                                                                                
           MOVE F520-NUM-SEQUENCE       TO WS-CUR-SEQ.                          
           MOVE F520-NUM-JB-PARENT-REGN TO WS-CUR-JB.                           
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-PROCESS-VALIDATION.                                                  
                                                                                
           MOVE 'N'  TO WS-JB-BALLOT-STATUS.                                    
           MOVE 0    TO STORE-CTR.                                              
           MOVE SPACES                  TO WS-SAVE-BALLOT-STATUS.               
           MOVE F520-NUM-SEQUENCE       TO WS-SAVE-SEQ.                         
           MOVE F520-NUM-JB-PARENT-REGN TO WS-SAVE-JB.                          
           MOVE 0    TO WS-UNSUCC-CNT.                                          
                                                                                
           PERFORM 300-PROCESS-JB-CASES THRU 300-EXIT UNTIL                     
                   WS-CUR-SEQJB NOT = WS-SAVE-SEQJB                             
                   OR END-OF-F520.                                              
                                                                                
           IF (WS-JB-BALLOT-STATUS = 'Y') OR (WS-UNSUCC-CNT = STORE-CTR)        
              MOVE   SPACES      TO   PRINT-REC                                 
              WRITE  PRINT-REC                                                  
              ADD    1                     TO     WS-REC-CNT                    
                                                                                
              MOVE STORE-CTR TO OUTPUT-CTR                                      
              PERFORM 500-WRITE-BP13F510  THRU 500-EXIT                         
                      VARYING STORE-CTR FROM 1 BY 1 UNTIL STORE-CTR >   R1.     
                                   OUTPUT-CTR.                                  
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-PROCESS-JB-CASES.                                                    
                                                                                
           PERFORM 400-CHECK-K520  THRU 400-EXIT.                               
           IF WS-SAVE-BALLOT-STATUS = SPACES                                    
                 MOVE WS-K520-BALLOT-STATUS TO WS-SAVE-BALLOT-STATUS            
           END-IF.                                                              
                                                                                
           IF WS-K520-BALLOT-STATUS NOT = WS-SAVE-BALLOT-STATUS                 
              MOVE 'Y'   TO  WS-JB-BALLOT-STATUS.                               
                                                                                
           IF WS-SAVE-BALLOT-STATUS = 'U'                                       
              IF WS-K520-BALLOT-STATUS = 'U'                                    
                 ADD 1 TO WS-UNSUCC-CNT.                                        
                                                                                
           ADD 1 TO STORE-CTR.                                                  
           MOVE BP13F520-REC    TO   F520-REC  (STORE-CTR).                     
                                                                                
           PERFORM 100-READ-F520     THRU 100-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-CHECK-K520.                                                          
                                                                                
           MOVE F520-NUM-REGN             TO   K520-NUM-REGN.                   
           READ BP13K520                                                        
           IF K520-FSTAT  = '00'                                                
              IF K520-CDE-SUCC-NT-ZONE = SPACES                                 
                  MOVE 'U' TO WS-K520-BALLOT-STATUS                             
              ELSE                                                              
                  MOVE 'S' TO WS-K520-BALLOT-STATUS                             
              END-IF                                                            
           ELSE                                                                 
              DISPLAY 'BP13K520 NO RECORD FOUND FOR : ' F520-NUM-REGN.          
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-WRITE-BP13F510.                                                      
                                                                                
           MOVE F520-REC (STORE-CTR) TO BP13F510-REC                            
           PERFORM 600-PRINTOUT-F510   THRU 600-EXIT.                           
           WRITE BP13F510-REC                                                   
           ADD 1 TO WS-WRITE-F510.                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       600-PRINTOUT-F510.                                                       
      ******************************************************************        
                                                                                
           IF WS-REC-CNT > 44                                                   
              PERFORM 700-PRINT-HEADER  THRU  700-EXIT.                         
                                                                                
           MOVE   SPACES                TO     PRINT-REC.                       
                                                                                
           ADD    1                     TO     WS-SERNO                         
           MOVE   WS-SERNO              TO     PRT-SERNO.                       
           MOVE   F510-CDE-ZONE         TO     PRT-ZONE.                        
           MOVE   F510-CDE-FLAT-TYPE    TO     PRT-FT.                          
           MOVE   F510-NUM-SEQUENCE     TO     PRT-SEQ.                         
           MOVE   F510-NUM-JB-PARENT-REGN      TO     PRT-JBREGNO.              
           MOVE   F510-NUM-CUR-REGN     TO     PRT-CREGNO.                      
           MOVE   F510-CDE-SUCC-NT-ZONE TO     PRT-SUCC-NT.                     
           MOVE   F510-CDE-UNSUCC       TO     PRT-UNSUCC.                      
           MOVE   F510-CDE-ALLOC-SCH    TO     PRT-ALLOC-SCH.                   
           MOVE   F510-CDE-CATEGORY     TO     PRT-CATEGORY.                    
           MOVE   F510-CDE-HOUSEHOLD    TO     PRT-HSEHOLD.                     
           WRITE  PRINT-REC             FROM   L852-DTL AFTER 1.                
           ADD    1                     TO     WS-REC-CNT.                      
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       700-PRINT-HEADER.                                                        
      ******************************************************************        
           ADD    1                   TO     WS-PAGE-CNT.                       
           MOVE   WS-PAGE-CNT         TO     PRT-PAGE.                          
           WRITE  PRINT-REC   FROM L852-HDG1   AFTER PAGE.                      
           WRITE  PRINT-REC   FROM L852-HDG2   AFTER 1.                         
           WRITE  PRINT-REC   FROM L852-HDG3   AFTER 2.                         
           MOVE   SPACES      TO   PRINT-REC.                                   
           WRITE  PRINT-REC.                                                    
           MOVE   ZEROES      TO   WS-REC-CNT.                                  
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *                                                                         
       900-CLOSE-ROUTINE.                                                       
                                                                                
           COMPUTE WS-BYPASS-F520 = WS-READ-F520 - WS-WRITE-F510                
                                                                                
           DISPLAY '********************  '.                                    
           DISPLAY '                      '.                                    
           DISPLAY 'PROGRAM-ID : BP13C852 '.                                    
           DISPLAY '                      '.                                    
           DISPLAY 'CURR-DATE  : ' WS-EDIT-DATE.                                
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ     : '                                  
           DISPLAY '   BP13F520            : ' WS-READ-F520                     
           DISPLAY 'NO OF RECORDS WRITTEN  : '                                  
           DISPLAY '   BP13F510            : ' WS-WRITE-F510                    
           DISPLAY 'NO OF RECORDS BYPASSED : '                                  
           DISPLAY '   BP13F500            : ' WS-BYPASS-F520                   
           DISPLAY '                      '.                                    
           DISPLAY '********************  '.                                    
                                                                                
      *                                                                         
       910-CLOSE-FILES.                                                         
                                                                                
           CLOSE BP13F520                                                       
                 BP13K520                                                       
                 BP13F510                                                       
                 BP13L852.                                                      
                                                                                
           IF K520-FSTAT NOT = '00'                                             
              DISPLAY 'BP13K520 CLOSING ERROR : ' K520-FSTAT.                   
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
