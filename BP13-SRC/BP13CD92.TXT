      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CD92.                                                 
       AUTHOR.        IRVIN CHING.                                              
       DATE-WRITTEN.  11/06/12.                                                 
      *****************************************************************         
      * OBJECTIVE - UPDATE DBSS FLAT HANDOVER TO HDB, AND WILL UPDATE           
      *             BP13KA30, BM06K100, BM06K110.                               
      *                                                                         
      *     INPUT FILES :                                                       
      *                  1. BP13FA20                                            
      *                  2. BP135A20                                            
      *                  3. BM06K100                                            
      *                  4. BM06K110                                            
      *----------------------------------------------------------------         
      * REF NO    DD/MM/YY  BY   AMENDMENTS/ENHANCEMENTS                        
      * --------  --------  ---  ------------------------                       
      * BP134199  11/06/12  IMC1 NEW PROGRAM.                                   
      * BP134808  08/05/13  ESA1 TO CATER FOR UPDATE OF DTE-ALLOCN              
      * BP134957  25/08/14  IMC1 TO CATER FOR UPDATE OF BB14K500                
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FA20  ASSIGN        TO BP13FA20.                          
                                                                                
           SELECT BP13KA30  ASSIGN        TO BP13KA30                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KA30-KEY-FLD                       
                            FILE STATUS   IS WS-KA30-STATUS.                    
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            FILE STATUS   IS WS-K110-STATUS.                    
                                                                                
           SELECT BM06K100  ASSIGN        TO BM06K100                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K100-KEY-FLD                       
                            FILE STATUS   IS WS-K100-STATUS.                    
                                                                                
           SELECT BB14K500  ASSIGN        TO  BB14K500                          
                            ORGANIZATION  IS  INDEXED                           
                            ACCESS MODE   IS  RANDOM                            
                            RECORD KEY    IS  K500-KEY-FLD                      
                            FILE STATUS   IS  WS-K500-STATUS.                   
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13FA20                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13FA20.                                                           
                                                                                
       FD   BP13KA30                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13KA30.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K100.                                                           
                                                                                
       FD   BB14K500                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BB14K500.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  WORK-FILE-VARIABLE.                                                  
           05  WS-KA30-STATUS          PIC 9(2)   VALUE ZEROES.                 
           05  WS-K110-STATUS          PIC 9(2)   VALUE ZEROES.                 
           05  WS-K100-STATUS          PIC 9(2)   VALUE ZEROES.                 
           05  WS-K500-STATUS          PIC 9(2)   VALUE ZEROES.                 
           05  WS-EOF-FA20             PIC X      VALUE 'N'.                    
           05  WS-EOF-KA30             PIC X      VALUE 'N'.                    
           05  WS-EOF-K110             PIC X      VALUE 'N'.                    
           05  WS-EOF-K100             PIC X      VALUE 'N'.                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FA20-READ            PIC 9(7)   VALUE ZEROES.                 
           05  WS-KA30-READ-CTR        PIC 9(7)   VALUE ZEROES.                 
           05  WS-KA30-NOTFND-CTR      PIC 9(7)   VALUE ZEROES.                 
           05  WS-K110-READ-CTR        PIC 9(7)   VALUE ZEROES.                 
           05  WS-K110-NOTFND-CTR      PIC 9(7)   VALUE ZEROES.                 
           05  WS-K110-UPD-CNT         PIC 9(7)   VALUE ZEROES.                 
           05  WS-K100-READ-CTR        PIC 9(7)   VALUE ZEROES.                 
           05  WS-K100-NOTFND-CTR      PIC 9(7)   VALUE ZEROES.                 
           05  WS-K100-UPD-CNT         PIC 9(7)   VALUE ZEROES.                 
           05  WS-K500-UPD-CNT         PIC 9(7)   VALUE ZEROES.                 
           05  WS-MATCH                PIC 9(7)   VALUE ZEROES.                 
           05  WS-NUM-ALLOCN-QTR       PIC X(6)   VALUE SPACES.                 
                                                                                
       01  WS-K100-KEY                 PIC X(16)  VALUE SPACES.                 
                                                                                
       01  WS-CURRENT-DATE             PIC 9(08)  VALUE ZEROES.                 
       01  WS-CURRENT-TIME             PIC 9(08)  VALUE ZEROES.                 
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13FA20      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-FA20 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13FA20                                                 
                       BP13KA30                                                 
                  I-O  BM06K110                                                 
                       BM06K100                                                 
                       BB14K500.                                                
                                                                                
           IF WS-KA30-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13KA30 OPEN ERROR ' WS-KA30-STATUS                     
              MOVE WS-KA30-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BM06K110 OPEN ERROR ' WS-K110-STATUS                     
              MOVE WS-K110-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BM06K100 OPEN ERROR ' WS-K100-STATUS                     
              MOVE WS-K100-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K500-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BB14K500 OPEN ERROR ' WS-K500-STATUS                     
              MOVE WS-K500-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13FA20.                                                      
      ******************************************************************        
                                                                                
           READ BP13FA20           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-FA20                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO   WS-FA20-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           PERFORM 3100-START-BP13KA30     THRU 3100-EXIT.                      
           MOVE WS-K100-KEY                TO K100-KEY-FLD.                     
           PERFORM 3400-READ-BM06K100   THRU 3400-EXIT.                         
                                                                                
           PERFORM 2000-READ-BP13FA20   THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3100-START-BP13KA30.                                                     
      ****************************                                              
                                                                                
           MOVE 'N'              TO WS-EOF-KA30                                 
                                    WS-EOF-K110                                 
                                    WS-EOF-K100.                                
                                                                                
           MOVE FA20-KEY-FLD     TO KA30-KEY-FLD.                               
                                                                                
           START BP13KA30 KEY IS >= KA30-KEY-FLD.                               
           EVALUATE WS-KA30-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 PERFORM 3200-READNEXT-KA30   THRU 3200-EXIT                    
                   UNTIL FA20-KEY-FLD NOT = KA30-KEY-FLD(1:8) OR                
                          WS-EOF-KA30 = 'Y'                                     
              WHEN 10                                                           
              WHEN 23                                                           
                   ADD 1 TO WS-KA30-NOTFND-CTR                                  
              WHEN OTHER                                                        
                   DISPLAY 'ERROR READING BP13KA30..' WS-KA30-STATUS            
                   MOVE WS-KA30-STATUS TO RETURN-CODE                           
                   PERFORM 9999-CLOSE-ROUTINE        THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3200-READNEXT-KA30.                                                      
      ****************************                                              
                                                                                
           READ BP13KA30 NEXT RECORD                                            
                                                                                
           EVALUATE WS-KA30-STATUS                                              
               WHEN 00                                                          
               WHEN 22                                                          
                    IF FA20-KEY-FLD = KA30-KEY-FLD(1:8)                         
                       ADD 1 TO WS-KA30-READ-CTR                                
                       PERFORM 3300-READ-BM06K110   THRU 3300-EXIT              
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'Y' TO WS-EOF-KA30                                     
               WHEN OTHER                                                       
                   DISPLAY 'ERROR PROCESSING BP13KA30' WS-KA30-STATUS           
                   MOVE WS-KA30-STATUS               TO RETURN-CODE             
                   PERFORM 9999-CLOSE-ROUTINE        THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3300-READ-BM06K110.                                                      
      ****************************                                              
                                                                                
           MOVE KA30-NUM-SCH-ACC       TO K110-KEY-FLD.                         
                                                                                
           READ BM06K110                                                        
           END-READ.                                                            
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
               WHEN 22                                                          
                    ADD 1 TO WS-K110-READ-CTR                                   
                    MOVE K110-AIX1 TO WS-K100-KEY                               
                                                                                
                    PERFORM 3350-UPDATE-BM06K110 THRU 3350-EXIT                 
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1 TO WS-K110-NOTFND-CTR                                 
                    MOVE 'Y' TO WS-EOF-K110                                     
               WHEN OTHER                                                       
                   DISPLAY 'ERROR PROCESSING BM06K110' WS-K110-STATUS           
                   MOVE WS-K110-STATUS               TO RETURN-CODE             
                   PERFORM 9999-CLOSE-ROUTINE        THRU 9999-EXIT             
           END-EVALUATE.                                                        
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3350-UPDATE-BM06K110.                                                    
      ****************************                                              
           IF K110-DTE-HANDOVER NOT = FA20-DTE-HANDOVER                         
              MOVE FA20-DTE-HANDOVER  TO K110-DTE-HANDOVER                      
           END-IF.                                                              
                                                                                
           IF K110-DTE-1ST-SALE NOT = FA20-DTE-HANDOVER                         
              MOVE FA20-DTE-HANDOVER  TO K110-DTE-1ST-SALE                      
           END-IF.                                                              
                                                                                
           IF K110-DTE-KEY-AVAIL NOT = FA20-DTE-HANDOVER                        
              MOVE FA20-DTE-HANDOVER  TO K110-DTE-KEY-AVAIL                     
           END-IF.                                                              
                                                                                
           IF K110-DTE-ALLOCN = SPACES OR LOW-VALUES OR ZEROES                  
              MOVE FA20-DTE-HANDOVER        TO K110-DTE-ALLOCN                  
           END-IF.                                                              
           IF K110-NUM-SELECTION = SPACES OR LOW-VALUES                         
              MOVE 'DBS'                 TO K110-NUM-SELECTION                  
           END-IF.                                                              
           IF K110-NUM-ALLOCN-QTR = SPACES OR LOW-VALUES                        
              EVALUATE FA20-NUM-PRJCT                                           
              WHEN 'D01'                                                        
                 MOVE '200610'      TO K110-NUM-ALLOCN-QTR                      
              WHEN 'D02'                                                        
                 MOVE '200801'      TO K110-NUM-ALLOCN-QTR                      
              WHEN 'D03'                                                        
                 MOVE '200807'      TO K110-NUM-ALLOCN-QTR                      
              WHEN 'D04'                                                        
                 MOVE '200810'      TO K110-NUM-ALLOCN-QTR                      
              WHEN 'D05'                                                        
                 MOVE '200904'      TO K110-NUM-ALLOCN-QTR                      
              WHEN 'D06'                                                        
                 MOVE '200904'      TO K110-NUM-ALLOCN-QTR                      
              WHEN 'D07'                                                        
                 MOVE '201102'      TO K110-NUM-ALLOCN-QTR                      
              WHEN 'D08'                                                        
                 MOVE '201106'      TO K110-NUM-ALLOCN-QTR                      
              WHEN 'D09'                                                        
                 MOVE '201110'      TO K110-NUM-ALLOCN-QTR                      
              WHEN 'D10'                                                        
                 MOVE '201201'      TO K110-NUM-ALLOCN-QTR                      
              WHEN 'D11'                                                        
                 MOVE '201110'      TO K110-NUM-ALLOCN-QTR                      
              WHEN 'D12'                                                        
                 MOVE '201110'      TO K110-NUM-ALLOCN-QTR                      
              WHEN 'D13'                                                        
                 MOVE '201204'      TO K110-NUM-ALLOCN-QTR                      
              END-EVALUATE                                                      
                                                                                
           REWRITE K110-REC                                                     
           EVALUATE WS-K110-STATUS                                              
             WHEN 00                                                            
                ADD  1     TO WS-K110-UPD-CNT                                   
             WHEN 23                                                            
                DISPLAY 'BM06K110 REWRITE KEY NOT FOUND, KEY-FLD : '            
                                                   K110-KEY-FLD                 
             WHEN OTHER                                                         
                DISPLAY 'BM06K110 REWRITE ERROR, STATUS : '                     
                                                   WS-K110-STATUS               
                MOVE WS-K110-STATUS  TO RETURN-CODE                             
                PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3350-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3400-READ-BM06K100.                                                      
      ****************************                                              
                                                                                
           READ BM06K100                                                        
           END-READ.                                                            
                                                                                
           EVALUATE WS-K100-STATUS                                              
               WHEN 00                                                          
               WHEN 22                                                          
                    ADD 1 TO WS-K100-READ-CTR                                   
                                                                                
                    PERFORM 3450-UPDATE-BM06K100 THRU 3450-EXIT                 
                    PERFORM 3500-UPDATE-BB14K500 THRU 3500-EXIT                 
               WHEN 10                                                          
               WHEN 23                                                          
                    ADD 1 TO WS-K100-NOTFND-CTR                                 
                    MOVE 'Y' TO WS-EOF-K100                                     
               WHEN OTHER                                                       
                   DISPLAY 'ERROR PROCESSING BM06K100' WS-K100-STATUS           
                   MOVE WS-K100-STATUS               TO RETURN-CODE             
                   PERFORM 9999-CLOSE-ROUTINE        THRU 9999-EXIT             
           END-EVALUATE.                                                        
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3450-UPDATE-BM06K100.                                                    
      ****************************                                              
                                                                                
           IF K100-DTE-HANDOVER NOT = FA20-DTE-HANDOVER                         
              MOVE FA20-DTE-HANDOVER  TO K100-DTE-HANDOVER                      
           END-IF.                                                              
                                                                                
           IF K100-DTE-1ST-SALE NOT = FA20-DTE-HANDOVER                         
              MOVE FA20-DTE-HANDOVER  TO K100-DTE-1ST-SALE                      
           END-IF.                                                              
                                                                                
           IF K100-DTE-1ST-KEY-ISSUE NOT = FA20-DTE-HANDOVER                    
              MOVE FA20-DTE-HANDOVER  TO K100-DTE-1ST-KEY-ISSUE                 
           END-IF.                                                              
                                                                                
           IF K100-DTE-KEY-AVAIL NOT = FA20-DTE-HANDOVER                        
              MOVE FA20-DTE-HANDOVER  TO K100-DTE-KEY-AVAIL                     
           END-IF.                                                              
                                                                                
           IF K100-DTE-ALLOCN = SPACES OR LOW-VALUES OR ZEROES                  
              MOVE FA20-DTE-HANDOVER     TO K100-DTE-ALLOCN                     
           END-IF.                                                              
                                                                                
           IF K100-DTE-1ST-KEY-ISSUE = SPACES OR LOW-VALUES OR ZEROES           
              MOVE FA20-DTE-HANDOVER        TO K100-DTE-1ST-KEY-ISSUE           
           END-IF.                                                              
                                                                                
           REWRITE K100-REC                                                     
           EVALUATE WS-K100-STATUS                                              
             WHEN 00                                                            
                ADD  1     TO WS-K100-UPD-CNT                                   
             WHEN 23                                                            
                DISPLAY 'BM06K100 REWRITE KEY NOT FOUND, KEY-FLD : '            
                                                   K100-KEY-FLD                 
             WHEN OTHER                                                         
                DISPLAY 'BM06K100 REWRITE ERROR, STATUS : '                     
                                                   WS-K100-STATUS               
                MOVE WS-K100-STATUS  TO RETURN-CODE                             
                PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3450-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3500-UPDATE-BB14K500.                                                    
      ****************************                                              
                                                                                
           MOVE SPACES       TO K500-KEY-FLD                                    
           MOVE K100-CDE-BLK TO K500-KEY-FLD                                    
                                                                                
           READ BB14K500                                                        
                                                                                
           EVALUATE WS-K500-STATUS                                              
             WHEN 00                                                            
             WHEN 02                                                            
               IF K500-NUM-LEASE-PERIOD-FLAT = SPACES OR LOW-VALUES OR          
                                               ZEROES                           
                  MOVE FA20-DTE-LEASE-COMM TO K500-DTE-LEASE-COMM-FLAT          
                  MOVE 99                  TO K500-NUM-LEASE-PERIOD-FLAT        
               END-IF                                                           
                                                                                
               REWRITE BB14K500-REC                                             
               IF WS-K500-STATUS = 00                                           
                   ADD 1 TO WS-K500-UPD-CNT                                     
               ELSE                                                             
                   DISPLAY ' ERROR IN REWRITE BB14K500:'                        
                                     WS-K500-STATUS                             
               END-IF                                                           
             WHEN 23                                                            
              DISPLAY 'RECORD NOT FOUND IN K500:' K100-CDE-BLK                  
             WHEN OTHER                                                         
              DISPLAY 'ERROR READING BB14K500 FILE:' WS-K500-STATUS             
              PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CD92'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13FA20 RECORDS READ............. : '                
                    WS-FA20-READ.                                               
           DISPLAY 'NO OF BP13KA30 RECORDS READ............. : '                
                    WS-KA30-READ-CTR.                                           
           DISPLAY 'NO OF BM06K110 RECORDS READ............. : '                
                    WS-K110-READ-CTR.                                           
           DISPLAY 'NO OF BM06K100 RECORDS READ............. : '                
                    WS-K100-READ-CTR.                                           
           DISPLAY 'NO OF BM06K110 RECORDS UPDATED.......... : '                
                    WS-K110-UPD-CNT.                                            
           DISPLAY 'NO OF BM06K100 RECORDS UPDATED.......... : '                
                    WS-K100-UPD-CNT.                                            
           DISPLAY 'NO OF BB14K500 RECORDS UPDATED.......... : '                
                    WS-K500-UPD-CNT.                                            
           DISPLAY 'NO OF BM06K110 RECORDS NOT FOUND........ : '                
                    WS-K110-NOTFND-CTR.                                         
           DISPLAY 'NO OF BM06K100 RECORDS NOT FOUND........ : '                
                    WS-K100-NOTFND-CTR.                                         
                                                                                
           CLOSE BP13FA20                                                       
                 BP13KA30                                                       
                 BM06K110                                                       
                 BM06K100                                                       
                 BB14K500.                                                      
                                                                                
           IF WS-KA30-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BP13KA30 OPEN ERROR ' WS-KA30-STATUS                     
              MOVE WS-KA30-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BM06K110 OPEN ERROR ' WS-K110-STATUS                     
              MOVE WS-K110-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF WS-K100-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BM06K110 OPEN ERROR ' WS-K100-STATUS                     
              MOVE WS-K100-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF WS-K500-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BB14K500 OPEN ERROR ' WS-K500-STATUS                     
              MOVE WS-K500-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
