       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C09A.                                                 
      *AUTHOR.        IRVIN CHING.                                              
      *                                                                         
      *==========================================================*              
      * DATE WRITTEN      : 28 AUG 2012                          *              
      *                                                          *              
      * 1. OBJECTIVE      : GET ADDRESS FOR BP13FD99 DOS REPORT  *              
      *                                                          *              
      *==========================================================*              
      *  MODIFICATIONS:                                          *              
      *                                                          *              
      *  CHGE-NO  DATE       OIC  DESCRIPTION                    *              
      *  -------- ---------- ---- ------------------------------ *              
      *  BP134650 28/08/2012 IMC1 NEW PROGRAM.                   *              
      *  BP134990 12/08/2013 IMC1 ADD MCPS FIELDS,MARITAL STATUS,*              
      *                           EDUCATION                      *              
      *  BP134990 20/08/2013 IMC1 REFORMAT COPYBOOK              *              
      ************************************************************              
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FD99     ASSIGN        TO BP13FD99.                       
                                                                                
           SELECT BP13FOUT     ASSIGN        TO BP13FOUT.                       
                                                                                
           SELECT AB03F089     ASSIGN TO AB03F089.                              
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13FD99                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 1200 CHARACTERS                  
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       COPY BP13FD99.                                                           
                                                                                
       FD  BP13FOUT                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 1200 CHARACTERS                  
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  FOUT-REC            PIC X(1200).                                     
                                                                                
       FD   AB03F089                                                            
            RECORD CONTAINS 250 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY AB03F089.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  END-OF-FILE-FLAGS.                                                   
           05  WS-FD99-EOF               PIC X     VALUE 'N'.                   
           05  WS-FD11-EOF               PIC X     VALUE 'N'.                   
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-POSTAL-CODE        PIC X(5)  VALUE SPACES.                
           05  WS-NME-STREET             PIC X(32) VALUE SPACES.                
                                                                                
       01  FD99-TRAILER.                                                        
           05  WS-CURRENT-DATE           PIC X(8)  VALUE SPACES.                
           05  WS-TOTAL-REC              PIC 9(10) VALUE ZEROES.                
                                                                                
       01  COUNTERS.                                                            
           05  WS-FD99-READ              PIC 9(6)  VALUE ZEROES.                
           05  WS-READ-F089              PIC 9(8)  VALUE 0.                     
           05  WS-CNT-WRITE              PIC 9(10) VALUE ZEROES.                
           05  WS-CNT-NFND-AB03          PIC 9(6)  VALUE ZEROES.                
           05  WS-CNT-FND-AB03           PIC 9(6)  VALUE ZEROES.                
           05  WS-COUNTERS             COMP-3.                                  
               10  WS-TOT              PIC 9(5)       VALUE 0.                  
               10  WS-VAC              PIC 9(5)       VALUE 0.                  
               10  WS-IDX              PIC 9(2)       VALUE 0.                  
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN.                                                               
      *************************************************************             
                                                                                
           PERFORM 1000-OPEN-ROUTINE    THRU 1000-EXIT.                         
           PERFORM 1100-READ-BP13FD99   THRU 1100-EXIT.                         
           PERFORM 3000-READ-F089       THRU 3000-EXIT.                         
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL WS-FD99-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       1000-OPEN-ROUTINE.                                                       
      *************************************************************             
                                                                                
           OPEN INPUT  BP13FD99                                                 
                       AB03F089                                                 
                OUTPUT BP13FOUT.                                                
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       1100-READ-BP13FD99.                                                      
      *************************************************************             
                                                                                
           READ BP13FD99                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-FD99-EOF                         
            NOT AT END                                                          
                ADD 1                    TO WS-FD99-READ                        
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------------------------------------------*                
       3000-READ-F089.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ AB03F089                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F089-NUM-SCH-ACCNT                       
                NOT AT END                                                      
                   ADD 1 TO WS-READ-F089                                        
           END-READ.                                                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       2000-PROCESS-RECORDS.                                                    
      *************************************************************             
                                                                                
           IF  FD99-NUM-SCH-ACC(1:9)  =  F089-NUM-SCH-ACCNT                     
               ADD 1                          TO WS-CNT-FND-AB03                
               MOVE F089-NUM-BLK              TO FD99-BLK-NO                    
               MOVE F089-NUM-LVL              TO FD99-NUM-LVL                   
               MOVE F089-NUM-UNIT             TO FD99-NUM-UNIT                  
               MOVE F089-CDE-ST               TO FD99-NME-STREET                
               MOVE F089-NUM-POSTAL-DSTRCT    TO FD99-NUM-POSTAL                
               PERFORM 4000-WRITE-BP13FOUT    THRU 4000-EXIT                    
               PERFORM 1100-READ-BP13FD99     THRU 1100-EXIT                    
           ELSE                                                                 
               IF FD99-NUM-SCH-ACC(1:9) <  F089-NUM-SCH-ACCNT                   
                  PERFORM 4000-WRITE-BP13FOUT  THRU 4000-EXIT                   
                  PERFORM 1100-READ-BP13FD99   THRU 1100-EXIT                   
               ELSE                                                             
                 IF  FD99-NUM-SCH-ACC(1:9) >  F089-NUM-SCH-ACCNT                
                     PERFORM  3000-READ-F089      THRU  3000-EXIT               
                 END-IF                                                         
               END-IF                                                           
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       4000-WRITE-BP13FOUT.                                                     
      *************************************************************             
                                                                                
           MOVE SPACES       TO FOUT-REC.                                       
           INITIALIZE           FOUT-REC.                                       
                                                                                
           WRITE FOUT-REC FROM BP13FD99-REC.                                    
           ADD 1 TO WS-CNT-WRITE.                                               
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       9000-CLOSE-ROUTINE.                                                      
      *************************************************************             
                                                                                
           ACCEPT WS-CURRENT-DATE         FROM DATE YYYYMMDD.                   
           MOVE WS-CNT-WRITE              TO WS-TOTAL-REC.                      
                                                                                
           WRITE FOUT-REC FROM FD99-TRAILER.                                    
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*--------- BP13C09A CONTROL TOTAL ---------*'.              
           DISPLAY '   '.                                                       
           DISPLAY 'NO. OF READS FROM FD99      : ' WS-FD99-READ.               
           DISPLAY 'RECS WRITTEN TO BP13FOUT    : ' WS-CNT-WRITE.               
           DISPLAY 'BM06 RECS FOUND IN AB03     : ' WS-CNT-FND-AB03.            
           DISPLAY 'BM06 RECS NOT FOUND IN AB03 : ' WS-CNT-NFND-AB03.           
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13FD99                                                       
                 BP13FOUT.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
