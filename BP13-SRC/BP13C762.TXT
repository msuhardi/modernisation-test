       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C762.                                                  
       AUTHOR.       LSB.                                                       
      *DATE-WRITTEN. 20.10.92.                                                  
      *REMARKS.                                                                 
      ************************************************************              
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                      *              
      *                                                          *              
      *  SYSTEM ID   : BP13                                      *              
      *                                                          *              
      *  OBJECTIVE   : TO OUTPUT UNSUCC REN REQUEST TO BE        *              
      *                PIGEONHOLE TOGETHER WITH MATURE PA.       *              
      *                                                          *              
      *  INPUT FILE  :                                           *              
      *  BP13F520    : BALLOT FILE                               *              
      *                                                          *              
      *  OUTPUT FILES:                                           *              
      *  BP13PA      : UNSUCC REN                                *              
      *                                                          *              
      * CHG-NO  BY   ON        DESCRIPTION                       *              
      * ----------------------------------                       *              
      * C9209D3 LSB 921020 - FOR REN TO BE PIGEONHOLE IN MATURE  *              
      *                      PA                                  *              
      * C9303B4 LJL 930412 - FOR RDC TO BE PIGEONHOLE IN MATURE  *              
      *                      PA                                  *              
      * I920718 LJL 930412 - CONVERT COBOL 74 TO  COBOLII.       *              
      * C9309B4 YCH 931001 - READ NT-WITH-LAND FR BP13K015       *              
      * SOC-PH9 LSB 941015 - DO NOT READ BP13K010, RUN 2 TIMES IN*              
      *                      BP13A525, OUTPUT UNSUCC REN FOR     *              
      *                      MATURE AND NON-MATURE NT            *              
      *BP130652 NOR 990220 - EXPAND F520 RECL FROM 300 TO 500    *              
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
           SELECT BP13PA   ASSIGN TO BP13PA.                                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F520                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       COPY BP13F520.                                                           
                                                                                
                                                                                
       FD  BP13PA                                                               
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  BP13PA-REC.                                                          
           05 BP13TR-DETAIL              PIC X(500).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
                                                                                
       01  WS-ALLOCSCH-TYPE              PIC X(3)  VALUE SPACES.                
       COPY PRIOSCH.                                                            
                                                                                
       01  WS-F520-CNT                   PIC 9(6)  VALUE ZERO.                  
       01  WS-WRITE-CNT                  PIC 9(6)  VALUE ZERO.                  
       01  WS-EOF-F520                   PIC X     VALUE 'N'.                   
           88  EOF-OF-F520                         VALUE 'Y'.                   
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN-LOGIC.                                                         
                                                                                
           PERFORM 1000-INIT-ROUTINE     THRU 1000-EXIT.                        
           PERFORM 2000-READ-F520        THRU 2000-EXIT.                        
           PERFORM 3000-MAIN-ROUTINE     THRU 3000-EXIT                         
                   UNTIL EOF-OF-F520.                                           
           PERFORM 4000-CLOSE-ROUTINE    THRU 4000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-INIT-ROUTINE.                                                       
                                                                                
           OPEN INPUT  BP13F520                                                 
               OUTPUT  BP13PA.                                                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-F520.                                                          
                                                                                
           READ BP13F520                 AT END                                 
           MOVE 'Y'                      TO WS-EOF-F520                         
           GO TO 2000-EXIT.                                                     
                                                                                
           ADD 1 TO WS-F520-CNT.                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-MAIN-ROUTINE.                                                       
                                                                                
           PERFORM 3100-CHECK-PRIORITY-CODE THRU 3100-EXIT.                     
                                                                                
           IF WS-ALLOCSCH-TYPE = 'REN'                                          
              WRITE BP13PA-REC FROM BP13F520-REC                                
              ADD  1                     TO WS-WRITE-CNT.                       
                                                                                
           PERFORM 2000-READ-F520        THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3100-CHECK-PRIORITY-CODE.                                                
           SET WS-ALLOC-PTR TO 1.                                               
                                                                                
           SEARCH WS-ALLOC-SCH                                                  
              AT END MOVE 'XXX' TO WS-ALLOCSCH-TYPE                             
              WHEN F520-CDE-ALLOC-SCH =                                         
                                WS-ALLOC-SCH-DESP(WS-ALLOC-PTR)                 
              MOVE WS-MAIN-SCH-CD(WS-ALLOC-PTR) TO                              
                                      WS-ALLOCSCH-TYPE.                         
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4000-CLOSE-ROUTINE.                                                      
                                                                                
           DISPLAY '  '.                                                        
           DISPLAY '***** SUMMARY REPORT FOR BP13C762 *****'.                   
           DISPLAY '  '.                                                        
           DISPLAY '1) TOTAL NO OF REC IN BALLOT FILE BP13F520= '               
                   WS-F520-CNT.                                                 
           DISPLAY '  '.                                                        
           DISPLAY '2) TOTAL NO OF UNSUCCESSFUL REN OUTPUT = '                  
                   WS-WRITE-CNT.                                                
           DISPLAY '  '.                                                        
                                                                                
           CLOSE  BP13F520                                                      
                  BP13PA.                                                       
                                                                                
           STOP RUN.                                                            
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
