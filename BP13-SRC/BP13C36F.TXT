      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C36F.                                                 
      *AUTHOR.        ALWYN BENNY.                                              
      *DATE-WRITTEN.  07-07-2011.                                               
      *****************************************************************         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *****************************************************************         
      *  OBJECTIVE   :  REPORT ON ACCEPTED AMR VOUCHER CASES          *         
      *---------------------------------------------------------------*         
      *  INPUT FILES : 1.  BP13F360 - BP13T.F360.PREPVRCH             *         
      *                                                               *         
      *  OUPUT FILES : 1.  BP13L36F                                   *         
      *                2.  BP13L36H                                   *         
      *---------------------------------------------------------------*         
      * CHANGE   BY    DATE      DESCRIPTION                          *         
      * -------- ---- --------   -------------------------------------*         
      * BP134276 AB9  07/07/2011 PRINT REPORT WITH MONIES GOING OUT   *         
      * BP134355 AB9  09/03/2012 cater for flat type '2R' or '3R'     *         
      *                                                               *         
      *****************************************************************         
      *---------------------------------------------------------------*         
       ENVIRONMENT DIVISION.                                                    
      *---------------------------------------------------------------*         
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F360 ASSIGN         TO BP13F360.                          
                                                                                
           SELECT BP13L36F  ASSIGN        TO BP13L36F.                          
           SELECT BP13L36H  ASSIGN        TO BP13L36H.                          
                                                                                
      *---------------------------------------------------------------*         
       DATA DIVISION.                                                           
      *---------------------------------------------------------------*         
       FILE SECTION.                                                            
                                                                                
       FD BP13F360                                                              
            RECORD CONTAINS  160  CHARACTERS                                    
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F360.                                                           
                                                                                
       FD  BP13L36F                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  PRINT-REC                 PIC X(132).                                
                                                                                
       FD  BP13L36H                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           RECORDING MODE IS F.                                                 
       01  PRINT-REC1                PIC X(132).                                
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F360-EOF                PIC X          VALUE 'N'.             
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F360-READ           PIC 9(5)       VALUE ZEROES.          
                                                                                
       01  WS-LINE-CNT                    PIC 99         VALUE 66.              
       01  WS-PAGE-CNT                    PIC 9(5)       VALUE ZERO.            
       01  CNT-KIV-CASES                  PIC 9(4)       VALUE ZERO.            
       01  WS-LINE-CNT1                   PIC 99         VALUE 66.              
       01  WS-PAGE-CNT1                   PIC 9(5)       VALUE ZERO.            
       01  CNT-KIV-CASES1                 PIC 9(4)       VALUE ZERO.            
                                                                                
       01  WS-DTE-CUR.                                                          
           05  WS-CC                      PIC 99.                               
           05  WS-YY                      PIC 99.                               
           05  WS-MM                      PIC 99.                               
           05  WS-DD                      PIC 99.                               
                                                                                
       01  WS-FLAT-ADDRESS                PIC X(33) VALUE SPACES.               
       01  WS-CDE-PRINT                   PIC X     VALUE SPACES.               
                                                                                
       01  L36F-PR-HEAD-01.                                                     
           05  FILLER              PIC X(8)      VALUE 'BP13L36F'.              
           05  FILLER              PIC X(9)      VALUE SPACES .                 
           05  FILLER              PIC X(10)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(39)     VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(6)      VALUE 'DATE: '.                
           05  13S1-DATE           PIC X(10).                                   
           05  FILLER              PIC X(1)      VALUE SPACES.                  
           05  FILLER              PIC X(6)      VALUE 'PAGE :'.                
           05  13S1-PAGE           PIC ZZ9 .                                    
           05  FILLER              PIC X(4)      VALUE SPACES.                  
                                                                                
       01  L36F-PR-HEAD-02.                                                     
           05  FILLER              PIC X(40)     VALUE SPACES .                 
           05  FILLER              PIC X(47)     VALUE                          
               '  REPORT ON ACCEPTED 2R/3R AMR CASES   FROM'.                   
           05  FILLER              PIC X(13)     VALUE                          
                                                 ' VOUCHER FILE'.               
           05  FILLER              PIC X(33)     VALUE SPACES.                  
                                                                                
       01  L36F-PR-HEAD-02S.                                                    
           05  FILLER              PIC X(40)     VALUE SPACES .                 
           05  FILLER              PIC X(54)     VALUE                          
               '  REPORT ON ACCEPTED STUDIO APARTMENT CASES FROM'.              
           05  FILLER              PIC X(13)     VALUE                          
                                                 ' VOUCHER FILE'.               
           05  FILLER              PIC X(26)     VALUE SPACES.                  
                                                                                
       01  L36F-PR-HEAD-03.                                                     
           05  FILLER              PIC X(32)                                    
                  VALUE ' SNO  VOUCHER/          SCH ACC '.                     
           05  FILLER              PIC X(34)                                    
                  VALUE '      ADDRESS                     '.                   
           05  FILLER              PIC X(34)                                    
                  VALUE '  TRANS    AMOUNT      DR/  SALES'.                    
           05  FILLER              PIC X(32)                                    
                  VALUE ' KEY-AVAIL FLAT  REGN NO    '.                         
                                                                                
       01  L36F-PR-HEAD-04.                                                     
           05  FILLER              PIC X(32)                                    
                  VALUE '      RECEIPT NO                '.                     
           05  FILLER              PIC X(34) value spaces.                      
           05  FILLER              PIC X(34)                                    
                  VALUE '  DATE                 CR   MODE '.                    
           05  FILLER              PIC X(32)                                    
                  VALUE ' DATE      TYPE             '.                         
                                                                                
       01  L36F-PR-DETAILS.                                                     
           05  13S1-SNO            PIC ZZZZ9.                                   
           05  FILLER              PIC X(01)     VALUE SPACES .                 
           05  13S1-NUM-RECEIPT    PIC X(14) .                                  
           05  FILLER              PIC X(01)     VALUE SPACES .                 
           05  13S1-SCH-ACC        PIC X(11)     VALUE SPACES.                  
           05  FILLER              PIC X(01)     VALUE SPACES .                 
           05  13S1-ADDR           PIC X(35).                                   
           05  13S1-DTE-TRANS      PIC X(08) .                                  
           05  FILLER              PIC X(01)     VALUE SPACES .                 
           05  13S1-AMT            PIC $$$$$$$9.99DB.                           
           05  FILLER              PIC X(02)     VALUE SPACES .                 
           05  13S1-DRCR           PIC X(01).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  13S1-SALES-MODE     PIC X(03).                                   
           05  FILLER              PIC X(03)     VALUE SPACES .                 
           05  13S1-KEY-AVAIL      PIC X(08).                                   
           05  FILLER              PIC X(03)     VALUE SPACES .                 
           05  13S1-FLAT-TYP       PIC X(02).                                   
           05  FILLER              PIC X(02)     VALUE SPACES .                 
           05  13S1-REG-NO         PIC X(08).                                   
           05  FILLER              PIC X(11)     VALUE SPACES .                 
                                                                                
       01  L36F-CONTROL-LINE.                                                   
           05  FILLER              PIC X(20)     VALUE  SPACES.                 
           05  CNTL-ITEM           PIC X(30)     VALUE  SPACES.                 
           05  CNTL-CNT            PIC ZZZZ9 .                                  
           05  FILLER              PIC X(58)     VALUE  SPACES.                 
                                                                                
      *----------------------------------------------------------------*        
      *        LINKAGE FOR SUB-PROGRAM BP13C913                        *        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH     PIC X(4).                                
               10  WS-LINK-NUM-ACC     PIC X(5).                                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *---------------------------------------------------------------*         
       PROCEDURE DIVISION.                                                      
      *---------------------------------------------------------------*         
      *-------------------*                                                     
       0000-MAIN-ROUTINE.                                                       
      *-------------------*                                                     
           PERFORM 1000-OPEN-FILES             THRU 1000-EXIT.                  
           PERFORM 2000-READ-F360              THRU 2000-EXIT.                  
           PERFORM 3000-PROCESS-RECORDS        THRU 3000-EXIT                   
             UNTIL WS-F360-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES            THRU 9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       1000-OPEN-FILES.                                                         
      *-----------------*                                                       
                                                                                
           OPEN INPUT BP13F360                                                  
               OUTPUT BP13L36F                                                  
               OUTPUT BP13L36H.                                                 
           MOVE FUNCTION CURRENT-DATE TO WS-DTE-CUR.                            
           STRING WS-DTE-CUR(7:2) '/' WS-DTE-CUR(5:2) '/'                       
                  WS-DTE-CUR(1:4)                                               
           DELIMITED BY SIZE INTO 13S1-DATE.                                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-F360.                                                          
      *-------------------------------------------------------------            
           READ BP13F360                                                        
                AT END                                                          
                MOVE HIGH-VALUES TO  BP13F360-REC                               
                MOVE 'Y' TO  WS-F360-EOF                                        
                GO TO 2000-EXIT.                                                
           ADD 1 TO WS-CNT-F360-READ.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       3000-PROCESS-RECORDS.                                                    
      *-------------------------------------------------------------*           
                                                                                
           PERFORM 3200-CALL-BP13C913          THRU 3200-EXIT.                  
           IF F360-NUM-FLAT-TYPE = '2 ' OR '3 ' OR '2R' OR '3R'                 
              MOVE 'A'                         TO  WS-CDE-PRINT                 
              PERFORM 3500-WRITE-REPORT           THRU 3500-EXIT                
           ELSE                                                                 
              MOVE 'S'                            TO   WS-CDE-PRINT             
              PERFORM 3500-WRITE-REPORT           THRU 3500-EXIT                
           END-IF.                                                              
           PERFORM 2000-READ-F360              THRU 2000-EXIT.                  
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       3200-CALL-BP13C913.                                                      
      *-------------------------------------------------------------*           
           MOVE F360-NUM-HDB-REF     TO WS-LINK-REC.                            
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC, BP13COMM8-REC.                    
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
             WHEN 000                                                           
                STRING 'BLK '  COMM8-NUM-BLK  '  '                              
                       '#'  COMM8-NUM-LEVEL   '-'                               
                       COMM8-NUM-UNIT-MAIN    '  ' DELIMITED BY SIZE            
                       COMM8-NME-STREET       DELIMITED BY '   '                
                       INTO  WS-FLAT-ADDRESS                                    
                                                                                
             WHEN 100                                                           
                DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE - '                     
                         F360-NUM-HDB-REF ', REGN : ' F360-NUM-REG              
                MOVE SPACES                     TO   WS-FLAT-ADDRESS            
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR              
                MOVE SPACES                     TO   WS-FLAT-ADDRESS            
           END-EVALUATE.                                                        
                                                                                
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3500-WRITE-REPORT.                                                       
      *-------------------------------------------------------------            
           IF WS-CDE-PRINT = 'A'                                                
             IF WS-LINE-CNT > 46                                                
                 MOVE 1 TO WS-LINE-CNT                                          
                 PERFORM 4000-REPORT-HEADING THRU 4000-EXIT                     
             ELSE                                                               
                 ADD 1 TO WS-LINE-CNT                                           
             END-IF                                                             
           END-IF.                                                              
           IF WS-CDE-PRINT = 'S'                                                
             IF WS-LINE-CNT1 > 46                                               
                 MOVE 1 TO WS-LINE-CNT1                                         
                 PERFORM 4000-REPORT-HEADING THRU 4000-EXIT                     
             ELSE                                                               
                 ADD 1 TO WS-LINE-CNT1                                          
             END-IF                                                             
           END-IF.                                                              
                                                                                
           MOVE  F360-NUM-HDB-REF           TO 13S1-SCH-ACC.                    
           MOVE  F360-NUM-RECPT-JRNO        TO 13S1-NUM-RECEIPT.                
           MOVE  F360-DTE-TRANS             TO 13S1-DTE-TRANS.                  
           MOVE  F360-AMT-TRANS             TO 13S1-AMT.                        
           MOVE  F360-NUM-REG               TO 13S1-REG-NO.                     
           MOVE  F360-NUM-SALE-MODE         TO 13S1-SALES-MODE.                 
           MOVE  F360-NUM-FLAT-TYPE         TO 13S1-FLAT-TYP.                   
           MOVE  F360-DTE-KEY-AVAIL         TO 13S1-KEY-AVAIL.                  
           MOVE  F360-NUM-TRANS-DR-CR       TO 13S1-DRCR.                       
           MOVE  WS-FLAT-ADDRESS            TO 13S1-ADDR.                       
           IF WS-CDE-PRINT = 'A'                                                
              ADD 1 TO CNT-KIV-CASES                                            
              MOVE  CNT-KIV-CASES               TO 13S1-SNO                     
              WRITE PRINT-REC FROM L36F-PR-DETAILS                              
           END-IF.                                                              
           IF WS-CDE-PRINT = 'S'                                                
              ADD 1 TO CNT-KIV-CASES1                                           
              MOVE  CNT-KIV-CASES1              TO 13S1-SNO                     
              WRITE PRINT-REC1 FROM L36F-PR-DETAILS                             
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-REPORT-HEADING.                                                     
      *-------------------------------------------------------------            
                                                                                
            IF WS-CDE-PRINT = 'A'                                               
               ADD 1         TO WS-PAGE-CNT                                     
               MOVE WS-PAGE-CNT TO 13S1-PAGE                                    
               MOVE SPACES   TO PRINT-REC                                       
               WRITE PRINT-REC AFTER PAGE                                       
               MOVE 'BP13L36F'      TO L36F-PR-HEAD-01(1:8)                     
               WRITE PRINT-REC FROM L36F-PR-HEAD-01 AFTER 2                     
               WRITE PRINT-REC FROM L36F-PR-HEAD-02 AFTER 3                     
               WRITE PRINT-REC FROM L36F-PR-HEAD-03 AFTER 3                     
               WRITE PRINT-REC FROM L36F-PR-HEAD-04 AFTER 1                     
               MOVE ALL '-'  TO PRINT-REC                                       
               WRITE PRINT-REC                                                  
               MOVE SPACES   TO PRINT-REC                                       
               WRITE PRINT-REC AFTER 1                                          
            ELSE                                                                
            IF WS-CDE-PRINT = 'S'                                               
               ADD 1         TO WS-PAGE-CNT1                                    
               MOVE WS-PAGE-CNT1 TO 13S1-PAGE                                   
               MOVE SPACES   TO PRINT-REC1                                      
               WRITE PRINT-REC1 AFTER PAGE                                      
               MOVE 'BP13L36H'      TO L36F-PR-HEAD-01(1:8)                     
               WRITE PRINT-REC1 FROM L36F-PR-HEAD-01 AFTER 2                    
               WRITE PRINT-REC1 FROM L36F-PR-HEAD-02S AFTER 3                   
               WRITE PRINT-REC1 FROM L36F-PR-HEAD-03 AFTER 3                    
               WRITE PRINT-REC1 FROM L36F-PR-HEAD-04 AFTER 1                    
               MOVE ALL '-'  TO PRINT-REC1                                      
               WRITE PRINT-REC1                                                 
               MOVE SPACES   TO PRINT-REC1                                      
               WRITE PRINT-REC1 AFTER 1                                         
              END-IF                                                            
             END-IF.                                                            
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------*                                                    
       9000-CLOSE-FILES.                                                        
      *--------------------*                                                    
                                                                                
           DISPLAY 'PROGRAM ID   : BP13C36F'.                                   
           DISPLAY 'CURRENT DATE : ' WS-DTE-CUR.                                
           DISPLAY SPACES.                                                      
           DISPLAY '*--------------- COUNTERS ---------------*'.                
           DISPLAY 'NUMBER OF BP13F360 RECORDS  : ' WS-CNT-F360-READ.           
           DISPLAY 'NO OF 2R/3R RECORDS WRITTEN : ' CNT-KIV-CASES.              
           DISPLAY 'NO OF STUDIO APARTMENT RECORDS  = ' CNT-KIV-CASES1.         
                                                                                
            CLOSE  BP13F360                                                     
                   BP13L36F                                                     
                   BP13L36H.                                                    
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
