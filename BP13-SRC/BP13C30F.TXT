      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13C30F.                                                 
       AUTHOR.        KV5.                                                      
       DATE-WRITTEN.  08/02/2019.                                               
                                                                                
      *************************************************************             
      * SYSTEM NAME : SYSTEM OF COMMITTMENT - BP13                *             
      *===========================================================*             
      * OBJECTIVE   : TO REMOVE CPFB TAG AFTER SOCA.              *             
      *                                                           *             
      * FILES USED  : BP13F820 - SORTED FILE FROM K820.           *             
      *               BP13K820 - UPDATE K820-NUM-CPF-MEMBER FIELD.*             
      *               BP13K330 - VALIDATE F820 RECORD.            *             
      *===========================================================*             
      * CHGEREQ  DATE       BY   AMMENDMENTS                      *             
      * -------- ---------- ---- -----------                      *             
      * BP137680 08/02/2019 KV5  NEW PROGRAM                      *             
      *************************************************************             
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F820 ASSIGN           TO BP13F820.                        
           SELECT BP13K820 ASSIGN           TO BP13K820                         
                           ORGANIZATION     IS INDEXED                          
                           ACCESS MODE      IS RANDOM                           
                           RECORD KEY       IS K820-KEY-FLD                     
                           FILE STATUS      IS WS-K820-STATUS.                  
           SELECT BP13K330 ASSIGN           TO BP13K330                         
                           ORGANIZATION     IS INDEXED                          
                           ACCESS MODE      IS DYNAMIC                          
                           RECORD KEY       IS K330-KEY-FLD                     
                           FILE STATUS      IS WS-K330-STATUS.                  
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F820                                                            
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13F820.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K330                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K330.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01 WS-FILE-STATUS.                                                       
          05 WS-K820-STATUS                 PIC 9(02) VALUE ZEROS.              
          05 WS-K330-STATUS                 PIC 9(02) VALUE ZEROS.              
                                                                                
       01 WS-COUNTERS.                                                          
          05 WS-F820-READ                   PIC 9(05) VALUE ZEROS.              
          05 WS-K820-REWRITE                PIC 9(05) VALUE ZEROS.              
                                                                                
       01 WS-SWITCHES.                                                          
          05 WS-FND                         PIC X(01) VALUE SPACES.             
          05 WS-F820-EOF                    PIC X(01) VALUE SPACES.             
          05 WS-K330-EOF                    PIC X(01) VALUE SPACES.             
          05 WS-K330-FND                    PIC X(01) VALUE SPACES.             
                                                                                
       01 WS-DTE-TME.                                                           
          05 WS-DTE                         PIC X(08) VALUE SPACES.             
          05 WS-TME                         PIC X(08) VALUE SPACES.             
          05 FILLER                         PIC X(05) VALUE SPACES.             
                                                                                
       01 WS-DISPLAY-DATE.                                                      
          05 WS-DD                          PIC X(02) VALUE SPACES.             
          05 FILLER                         PIC X(01) VALUE '/'.                
          05 WS-MM                          PIC X(02) VALUE SPACES.             
          05 FILLER                         PIC X(01) VALUE '/'.                
          05 WS-CCYY                        PIC X(04) VALUE SPACES.             
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *-----------------*                                                       
       0000-MAIN-ROUTINE.                                                       
      *-----------------*                                                       
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F820       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECORD      THRU 3000-EXIT                      
             UNTIL WS-F820-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
                                                                                
      *---------------*                                                         
       1000-OPEN-FILES.                                                         
      *---------------*                                                         
           OPEN INPUT BP13F820                                                  
                      BP13K330                                                  
                I-O   BP13K820.                                                 
                                                                                
           IF WS-K330-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K330 OPENING ERROR, STATUS : ' WS-K330-STATUS        
              MOVE WS-K330-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 OPENING ERROR, STATUS : ' WS-K820-STATUS        
              MOVE WS-K820-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-DTE-TME.                      
           MOVE WS-DTE(1:4)                 TO WS-CCYY.                         
           MOVE WS-DTE(5:2)                 TO WS-MM.                           
           MOVE WS-DTE(7:2)                 TO WS-DD.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       2000-READ-BP13F820.                                                      
      *------------------*                                                      
           READ BP13F820                                                        
             AT END                                                             
                MOVE 'Y'                    TO WS-F820-EOF                      
             NOT AT END                                                         
                ADD 1                       TO WS-F820-READ                     
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3000-PROCESS-RECORD.                                                     
      *-------------------*                                                     
           PERFORM 3100-START-BP13K330      THRU 3100-EXIT.                     
           IF WS-FND = 'Y'                                                      
              MOVE 'N'                      TO WS-K330-FND                      
                                               WS-K330-EOF                      
              PERFORM 3200-READNXT-BP13K330 THRU 3200-EXIT                      
                UNTIL WS-K330-FND = 'Y' OR WS-K330-EOF = 'Y'                    
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F820       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3100-START-BP13K330.                                                     
      *-------------------*                                                     
           MOVE SPACES                      TO K330-KEY-FLD.                    
           MOVE F820-NUM-REGN               TO K330-CURR-REG.                   
                                                                                
           START BP13K330 KEY >= K330-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K330-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                TO WS-FND                           
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'N'                TO WS-FND                           
               WHEN OTHER                                                       
                    DISPLAY 'BP13K330 START READ ERROR, STATUS : '              
                             WS-K330-STATUS                                     
                    MOVE WS-K330-STATUS      TO RETURN-CODE                     
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3200-READNXT-BP13K330.                                                   
      *---------------------*                                                   
           READ BP13K330 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K330-STATUS                                              
               WHEN 00                                                          
                    IF F820-NUM-REGN = K330-CURR-REG                            
                       PERFORM 4000-READ-BP13K820 THRU 4000-EXIT                
                       MOVE 'Y'             TO WS-K330-FND                      
                    ELSE                                                        
                       MOVE 'Y'             TO WS-K330-EOF                      
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'Y'                TO WS-K330-EOF                      
               WHEN OTHER                                                       
                    DISPLAY 'BP13K330 READ NEXT ERROR, STATUS : '               
                             WS-K330-STATUS                                     
                    MOVE WS-K330-STATUS      TO RETURN-CODE                     
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4000-READ-BP13K820.                                                      
      *------------------*                                                      
           MOVE SPACES                      TO K820-KEY-FLD.                    
           MOVE F820-KEY-FLD                TO K820-KEY-FLD.                    
                                                                                
           READ BP13K820.                                                       
                                                                                
           EVALUATE WS-K820-STATUS                                              
               WHEN 00                                                          
                    MOVE SPACES             TO K820-NUM-CPF-MEMBER              
                    PERFORM 4100-REWRITE-BP13K820 THRU 4100-EXIT                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K820 READ ERROR, STATUS : '                    
                             WS-K820-STATUS                                     
                    MOVE WS-K820-STATUS      TO RETURN-CODE                     
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       4100-REWRITE-BP13K820.                                                   
      *---------------------*                                                   
           REWRITE BP13K820-REC.                                                
                                                                                
           IF WS-K820-STATUS = ZEROS                                            
              ADD 1 TO WS-K820-REWRITE                                          
           ELSE                                                                 
              DISPLAY 'BP13K820 REWRITING ERROR, STATUS : '                     
                       WS-K820-STATUS                                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       9000-CLOSE-FILES.                                                        
      *----------------*                                                        
           CLOSE BP13F820                                                       
                 BP13K330                                                       
                 BP13K820.                                                      
                                                                                
           IF WS-K330-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K330 CLOSING ERROR, STATUS : ' WS-K330-STATUS        
              MOVE WS-K330-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 CLOSING ERROR, STATUS : ' WS-K820-STATUS        
              MOVE WS-K820-STATUS           TO RETURN-CODE                      
           END-IF.                                                              
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'PROGRAM NAME   :  BP13C30F'.                                
           DISPLAY 'DATE OF RUN    :' WS-DISPLAY-DATE.                          
           DISPLAY ' '.                                                         
           DISPLAY 'RECORDS READ FROM BP13F820  : ' WS-F820-READ.               
           DISPLAY 'RECODRS UPDATED IN BP13K820 : ' WS-K820-REWRITE.            
           DISPLAY ' '.                                                         
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
