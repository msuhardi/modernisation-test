       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C20I.                                                  
       AUTHOR.       LMS1.                                                      
      *DATE-WRITTEN. 06/11/2007.                                                
      ************************************************************              
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                      *              
      *  SYSTEM ID   : BP13                                      *              
      *                                                          *              
      *  OBJECTIVE   : TO MATCH F110 TO K950 THEN GET ACCEPTANCE *              
      *                DATE OF FIRST TRANS = 'AI'                *              
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BM06F110  ASSIGN       TO BM06F110.                           
           SELECT BM06K950  ASSIGN       TO BM06K950                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K950-KEY-FLD                        
                            FILE STATUS  IS WS-K950-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BM06K950                                                             
           RECORD CONTAINS 600 CHARACTERS.                                      
       COPY  BM06K950.                                                          
                                                                                
       FD  BM06F110                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY  BM06F110.                                                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-F110-CNT             PIC 9(8)    VALUE ZEROES.                    
       01  WS-MATCH-CNT            PIC 9(8)    VALUE ZEROES.                    
       01  WS-K950-EOF             PIC X(1)    VALUE 'N'.                       
       01  WS-AI-FOUND             PIC X(1)    VALUE 'N'.                       
       01  WS-K950-STATUS          PIC 9(2)    VALUE ZEROES.                    
       01  WS-EDITED-DATE          PIC X(10)   VALUE SPACES.                    
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY         PIC 9(4)    VALUE 0.                         
           05  WS-SYS-MM           PIC 9(2)    VALUE 0.                         
           05  WS-SYS-DD           PIC 9(2)    VALUE 0.                         
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-F110     THRU 2000-EXIT                            
           PERFORM 3000-PROCESS       THRU 3000-EXIT                            
                   UNTIL F110-KEY-FLD = HIGH-VALUES.                            
                                                                                
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BM06K950                                                 
                I-O    BM06F110.                                                
                                                                                
           IF WS-K950-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'FILE BM06K950 OPEN ERROR'                                
              DISPLAY 'ERROR STATUS = ' WS-K950-STATUS                          
              MOVE WS-K950-STATUS TO RETURN-CODE                                
              PERFORM 9999-CLOSE-ROUTINE.                                       
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
                                                                                
           STRING  WS-SYSTEM-DATE(7:2) '/' WS-SYSTEM-DATE(5:2) '/'              
                   WS-SYSTEM-DATE(1:4) DELIMITED BY SIZE                        
                   INTO WS-EDITED-DATE                                          
           END-STRING.                                                          
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-F110.                                                          
      *-------------------------------------------------------------            
                                                                                
           READ BM06F110                                                        
              AT END MOVE HIGH-VALUES TO F110-KEY-FLD                           
              GO TO 2000-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F110-CNT.                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS.                                                            
      *-------------------------------------------------------------            
                                                                                
           MOVE 'N'    TO WS-AI-FOUND WS-K950-EOF.                              
           MOVE SPACES TO K950-KEY-FLD.                                         
           MOVE F110-SCH-ACC-NO TO K950-NUM-SCH-ACC.                            
                                                                                
           START BM06K950 KEY >= K950-KEY-FLD.                                  
                                                                                
           IF WS-K950-STATUS = ZEROES                                           
              PERFORM 4000-READ-BM06K950 THRU 4000-EXIT                         
                UNTIL WS-K950-EOF = 'Y' OR WS-AI-FOUND = 'Y'                    
                OR (K950-NUM-SCH-ACC NOT = F110-SCH-ACC-NO)                     
           END-IF.                                                              
                                                                                
           REWRITE F110-REC.                                                    
                                                                                
           PERFORM 2000-READ-F110     THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-READ-BM06K950.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BM06K950 NEXT AT END MOVE 'Y' TO WS-K950-EOF                    
                                     GO TO 4000-EXIT.                           
                                                                                
           IF K950-NUM-SCH-ACC = F110-SCH-ACC-NO                                
              IF K950-NUM-TRANS = 'AI' OR 'AS'                                  
                 IF K950-DTE-ACCEPTANCE = ZEROES OR SPACES OR LOW-VALUES        
                    MOVE K950-DTE-UPDATE TO F110-DTE-ACCEPTANCE                 
                 ELSE                                                           
                    MOVE K950-DTE-ACCEPTANCE TO F110-DTE-ACCEPTANCE             
                 END-IF                                                         
                 MOVE K950-REGN-NO        TO F110-REC(471:8)                    
                 MOVE K950-NUM-SELECTION  TO F110-REC(479:3)                    
                 MOVE K950-NUM-ALLOCN-QTR TO F110-REC(482:6)                    
                 MOVE K950-DTE-ALLOCN     TO F110-REC(488:8)                    
                 MOVE 'Y' TO WS-AI-FOUND                                        
                 ADD 1 TO WS-MATCH-CNT                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '--- BP13C20I CONTROL TOTAL --------------'.                 
           DISPLAY '                    RUN DATE  : ' WS-EDITED-DATE.           
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF REC READ FROM BM06F110  : ' WS-F110-CNT.              
           DISPLAY 'NO OF REC FOUND IN BM06K950   : ' WS-MATCH-CNT.             
           DISPLAY '-----------------------------------------'.                 
                                                                                
           CLOSE BM06F110                                                       
                 BM06K950.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
