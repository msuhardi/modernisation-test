      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CD48.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  25/07/08.                                                 
      *****************************************************************         
      *OBJECTIVE - OUTPUT DUED CASES TO SMS AFTER TOP.                *         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13FD38                                  *         
      *                  2. BP13KD00                                  *         
      *                  3. BP13KD03                                  *         
      *                  4. BP13KD35                                  *         
      *                                                               *         
      *     OUTPUT FILE: 1. P13FD38A                                  *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP13     25/07/2008 JB8  NEW PROGRAM.                         *         
      * BP133298 14/01/2009 JB8  RUN AS A DAILY JOB(BP13D110)         *         
      * BP133535 10/02/2009 JB8  REMOVE CHECK OF DTE-INSTAL-NEW       *         
      * BP133298 16/02/2009 JB8  GET OPEN-BAL FROM KD03               *         
      * BP133612 14/08/2009 JB8  OUTPUT FD38 ONLY IF DTE-DUE > DTE-SMS*         
      * BP134213 24/05/2011 JB8  TO READ INST-RATE FROM BP13KD35      *         
      * BP134310 15/08/2011 JB8  TO CATER FOR DEFER PAYMENT           *         
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  SY02F001  ASSIGN        TO SY02F001.                         
           SELECT  BP13FD38  ASSIGN        TO BP13FD38.                         
           SELECT  BP13KD00  ASSIGN        TO BP13KD00                          
                             ORGANIZATION  IS INDEXED                           
                             ACCESS MODE   IS RANDOM                            
                             RECORD KEY    IS KD00-KEY-FLD                      
                             FILE STATUS   IS WS-KD00-STATUS.                   
           SELECT  BP13KD03  ASSIGN        TO BP13KD03                          
                             ORGANIZATION  IS INDEXED                           
                             ACCESS MODE   IS RANDOM                            
                             RECORD KEY    IS KD03-KEY-FLD                      
                             FILE STATUS   IS WS-KD03-STATUS.                   
           SELECT  BP13KD35  ASSIGN        TO BP13KD35                          
                             ORGANIZATION  IS INDEXED                           
                             ACCESS MODE   IS RANDOM                            
                             RECORD KEY    IS KD35-KEY-FLD                      
                             FILE STATUS   IS WS-KD35-STATUS.                   
           SELECT  P13FD38A ASSIGN         TO P13FD38A.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13FD38                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13FD38.                                                           
                                                                                
       FD   BP13KD00                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13KD00.                                                           
                                                                                
       FD   BP13KD03                                                            
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13KD03.                                                           
                                                                                
       FD   BP13KD35                                                            
            RECORD CONTAINS 3500 CHARACTERS.                                    
       COPY BP13KD35.                                                           
                                                                                
       FD   P13FD38A                                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       01   P13FD38A-REC               PIC X(400).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-F001-EOF             PIC X(1)  VALUE 'N'.                     
               88 EOF-F001                       VALUE 'Y'.                     
           05  WS-EOF-FD38             PIC X(1)  VALUE 'N'.                     
           05  WS-KD00-STATUS          PIC 99    VALUE ZEROES.                  
           05  WS-KD03-STATUS          PIC 99    VALUE ZEROES.                  
           05  WS-KD35-STATUS          PIC 99    VALUE ZEROES.                  
           05  WS-KD00-FND             PIC X(01) VALUE SPACE.                   
               88  KD00-FND                      VALUE 'Y'.                     
               88  KD00-NOTFND                   VALUE 'N'.                     
           05  WS-KD03-FND             PIC X(01) VALUE SPACE.                   
               88  KD03-FND                      VALUE 'Y'.                     
               88  KD03-NOTFND                   VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FD38-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD00-FOUND           PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD00-NOTFND          PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD03-FOUND           PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD03-NOTFND          PIC 9(7)  VALUE ZEROES.                  
           05  WS-FD38A-WRITE          PIC 9(7)  VALUE ZEROES.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13FD38      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-FD38 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13FD38                                                 
                       SY02F001                                                 
                       BP13KD00                                                 
                       BP13KD03                                                 
                       BP13KD35                                                 
              OUTPUT   P13FD38A.                                                
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD00 - ERROR OPENING : ' WS-KD00-STATUS              
              MOVE WS-KD00-STATUS              TO RETURN-CODE                   
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF WS-KD03-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD03 - ERROR OPENING : ' WS-KD03-STATUS              
              MOVE WS-KD03-STATUS              TO RETURN-CODE                   
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF WS-KD35-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD35 - ERROR OPENING : ' WS-KD35-STATUS              
              MOVE WS-KD35-STATUS              TO RETURN-CODE                   
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           READ SY02F001 AT END MOVE 'Y'  TO WS-F001-EOF.                       
           IF EOF-F001                                                          
              DISPLAY 'RECORD NOT FOUND IN SY01F001 '                           
              DISPLAY 'PROCESSING TERMINATED '                                  
              GO TO 9999-CLOSE-ROUTINE                                          
           ELSE                                                                 
              MOVE F001-DTE-CURRENT      TO   WS-SYSTEM-DATE                    
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13FD38.                                                      
      ******************************************************************        
                                                                                
           READ BP13FD38           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-FD38                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO   WS-FD38-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
           IF FD38-DTE-DUE = F001-DTE-CURRENT                                   
                                                                                
              PERFORM 4000-READ-BP13KD00           THRU 4000-EXIT               
              IF KD00-NOTFND                                                    
                 PERFORM 4500-READ-BP13KD03        THRU 4500-EXIT               
                 IF KD03-FND                                                    
                    IF KD03-DTE-TRANSFER-SMS(1:6) = FD38-DTE-DUE(1:6)           
                       MOVE KD03-AMT-OPEN-LOAN-BAL TO FD38-AMT-OPEN-BAL1        
                    END-IF                                                      
                    IF FD38-DTE-DUE > KD03-DTE-TRANSFER-SMS                     
                       PERFORM 5000-WRITE-P13FD38A THRU 5000-EXIT               
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FD38        THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-READ-BP13KD00.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                         TO BP13KD00-REC.                 
           INITIALIZE                             BP13KD00-REC.                 
           MOVE 'Y'                            TO WS-KD00-FND.                  
                                                                                
           MOVE FD38-NUM-REGN                  TO KD00-NUM-REGN.                
                                                                                
           READ BP13KD00.                                                       
                                                                                
           EVALUATE WS-KD00-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 ADD 1                         TO WS-KD00-FOUND                 
                 MOVE 'Y'                      TO WS-KD00-FND                   
              WHEN 23                                                           
                 MOVE 'N'                      TO WS-KD00-FND                   
              WHEN OTHER                                                        
                 DISPLAY 'BP13KD00 - ERROR READING : ' WS-KD00-STATUS           
                 MOVE WS-KD00-STATUS           TO RETURN-CODE                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4500-READ-BP13KD03.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                         TO BP13KD03-REC.                 
           INITIALIZE                             BP13KD03-REC.                 
           MOVE 'N'                            TO WS-KD03-FND.                  
                                                                                
           MOVE FD38-NUM-REGN                  TO KD03-NUM-REGN.                
                                                                                
           READ BP13KD03.                                                       
                                                                                
           EVALUATE WS-KD03-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 ADD 1                         TO WS-KD03-FOUND                 
                 MOVE 'Y'                      TO WS-KD03-FND                   
              WHEN 23                                                           
                 DISPLAY 'BP13KD03 - RECORD NOT FOUND : ' KD03-NUM-REGN         
                 ADD 1                         TO WS-KD03-NOTFND                
                 MOVE 'N'                      TO WS-KD03-FND                   
              WHEN OTHER                                                        
                 DISPLAY 'BP13KD03 - ERROR READING : ' WS-KD03-STATUS           
                 MOVE WS-KD03-STATUS           TO RETURN-CODE                   
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-WRITE-P13FD38A.                                                     
      ******************************************************************        
                                                                                
           MOVE FD38-NUM-REGN             TO KD35-NUM-REGN                      
           PERFORM 8001-READ-BP13KD35     THRU 8001-EXIT                        
           MOVE KD35-NUM-INSTAL-RATE-CPF1 TO FD38-NUM-INSTAL-RATE-CPF1.         
           MOVE KD35-NUM-INSTAL-RATE-CPF2 TO FD38-NUM-INSTAL-RATE-CPF2.         
           MOVE KD35-NUM-INSTAL-RATE-CPF3 TO FD38-NUM-INSTAL-RATE-CPF3.         
           MOVE KD35-NUM-INSTAL-RATE-CPF4 TO FD38-NUM-INSTAL-RATE-CPF4.         
                                                                                
           MOVE 'Y'                            TO FD38-NUM-DUE                  
           WRITE P13FD38A-REC                FROM BP13FD38-TRANS-REC.           
           ADD 1                               TO WS-FD38A-WRITE.               
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       8001-READ-BP13KD35.                                                      
      ******************************************************************        
                                                                                
           READ BP13KD35.                                                       
                                                                                
           IF WS-KD35-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              IF WS-KD35-STATUS = 23                                            
                 MOVE ZEROES               TO KD35-NUM-INSTAL-RATE-CPF1         
                                              KD35-NUM-INSTAL-RATE-CPF2         
                                              KD35-NUM-INSTAL-RATE-CPF3         
                                              KD35-NUM-INSTAL-RATE-CPF4         
              ELSE                                                              
                 DISPLAY 'BP13KD35 - ERROR READING : ' WS-KD35-STATUS           
                 MOVE WS-KD35-STATUS           TO RETURN-CODE                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF KD35-NUM-INSTAL-RATE-CPF1 NOT NUMERIC                             
              MOVE ZEROES TO KD35-NUM-INSTAL-RATE-CPF1.                         
           IF KD35-NUM-INSTAL-RATE-CPF2 NOT NUMERIC                             
              MOVE ZEROES TO KD35-NUM-INSTAL-RATE-CPF2.                         
           IF KD35-NUM-INSTAL-RATE-CPF3 NOT NUMERIC                             
              MOVE ZEROES TO KD35-NUM-INSTAL-RATE-CPF3.                         
           IF KD35-NUM-INSTAL-RATE-CPF4 NOT NUMERIC                             
              MOVE ZEROES TO KD35-NUM-INSTAL-RATE-CPF4.                         
                                                                                
       8001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CD48'.                                     
           DISPLAY 'RUN DATE   : ' WS-SYSTEM-DATE(7:2)                          
                               '/' WS-SYSTEM-DATE(5:2)                          
                               '/' WS-SYSTEM-DATE(1:4)                          
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13FD38 RECORDS READ ............ : '            
                    WS-FD38-READ.                                               
           DISPLAY '(2) NO OF P13FD38A RECORDS WRITTEN ......... : '            
                    WS-FD38A-WRITE.                                             
           DISPLAY '(3) NO OF BP13KD00 RECORDS FOUND ........... : '            
                    WS-KD00-FOUND.                                              
           DISPLAY '(4) NO OF BP13KD03 RECORDS FOUND ........... : '            
                    WS-KD03-FOUND.                                              
           DISPLAY '(5) NO OF BP13KD03 RECORDS NOT FOUND........ : '            
                    WS-KD03-NOTFND.                                             
                                                                                
           CLOSE SY02F001                                                       
                 BP13FD38                                                       
                 BP13KD00                                                       
                 BP13KD03                                                       
                 BP13KD35                                                       
                 P13FD38A.                                                      
                                                                                
           IF WS-KD00-STATUS NOT = 00                                           
              DISPLAY 'BP13KD00 - ERROR CLOSING : ' WS-KD00-STATUS              
              MOVE WS-KD00-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF WS-KD03-STATUS NOT = 00                                           
              DISPLAY 'BP13KD03 - ERROR CLOSING : ' WS-KD03-STATUS              
              MOVE WS-KD03-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           IF WS-KD35-STATUS NOT = 00                                           
              DISPLAY 'BP13KD35 - ERROR CLOSING : ' WS-KD35-STATUS              
              MOVE WS-KD35-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
