  /*===============================================================*/           
  /*                                                               */           
  /*   PROGRAM ID     :  BP13S569                                  */           
  /*   PROGRAM NAME   :  PRINT PPS/NON-PPS TOTALS FOR EACH ZONE    */           
  /*   AUTHOR         :  SMR2                                      */           
  /*   DATE WRITTEN   :  06 FEB 2013                               */           
  /*   OBJECTIVES     :  PRINT TOTAL NUMBER OF PPS AND NON-PPS     */           
  /*                     APPLICANTS UNDER EACH ZONE, FLAT TYPE,    */           
  /*                     ALLOC SCHEME, HOUSEHOLD AND CATEGORY      */           
  /*                                                               */           
  /*===============================================================*/           
  /* CHG-REF#  BY    DDMMCCYY  DESCRIPTION                         */           
  /* --------  ----  --------  ----------------------------------- */           
  /* BP134782  SMR2  06022013  NEW SAS PROGRAM                     */           
  /* BP134903  SMR2  19042013  CATER FOR SAPS                      */           
  /* BP134914  SMR2  13062013  GENERATE REPORT FOR AST AND SAP     */           
  /*                           SEPARATE COLUMN FOR PREGNANT PPS    */           
  /* BP135392  IL5   04072014  CATER FOR FLAT TYPE 3P              */           
  /* BP135492  SMR2  16092014  SPLIT MCPS (WITH AND NEAR)          */           
  /* BP136055  IL5   04122015  CATER FOR 2-ROOM FLEXI APPLICATIONS */           
  /* BP136055  SMR2  29122015  NEW REPORT FOR HARDLUCK             */           
  /* BP136096  SMR2  26012016  REMOVE "NEW" IN REPORT FOR HARDLUCK */           
  /* BP136633  SMR2  03032017  ADD NEW ROWS TO SHOW FSH/RHS CASES  */           
  /* BP136633  SMR2  09032017  ADD NEW ROW TO SHOW RHO CASES       */           
  /* BP136866  IL5   10082017  GENERATE PPO REPORT FOR RF FLATTYPE */           
  /* BP139670  AM25  25082023  CATER FOR FT(PMC)                   */           
  /*===============================================================*/           
                                                                                
   PROC  FORMAT;                                                                
         VALUE  $RM_TYP "1 "="STUDIO"                                           
                        "1A"="STUDIO"                                           
                        "SP"="STUDIO"                                           
                        "2 "="2-ROOM"                                           
                        "2A"="2A-ROOM"                                          
                        "2F"="2-ROOM FLEXI"                                     
                        "3 "="3-ROOM"                                           
                        "3P"="3P-ROOM"                                          
                        "4 "="4-ROOM"                                           
                        "4P"="4-ROOM"                                           
                        "5 "="5-ROOM"                                           
                        "5P"="5-ROOM"                                           
                        "E "="EXECUTIVE";                                       
                                                                                
         VALUE  $CAT_FMT "1"="CAT1"                                             
                         "2"="CAT2"                                             
                         "3"="CAT3"                                             
                         "4"="CAT3";                                            
                                                                                
         VALUE  $TIMER   "H"="FIRST-TIMER"                                      
                         "T"="FIRST-TIMER"                                      
                         "F"="FIRST-TIMER"                                      
                         "N"="N-TIMER"                                          
                         "G"="SECOND-TIMER";                                    
                                                                                
         VALUE  $A_SCH   "01"="PA"                                              
                         "02"="MCPS-NEAR"                                       
                         "03"="MCPS-WITH"                                       
                         "04"="TCP"                                             
                         "05"="GRO"                                             
                         "06"="TPS"                                             
                         "07"="REN"                                             
                         "08"="MGPS"                                            
                         "09"="SERS"                                            
                         "10"="SAPS"                                            
                         "11"="FTS"                                             
                         "12"="STS"                                             
                         "13"="FSH"                                             
                         "14"="RHS"                                             
                         "15"="RHO";                                            
                                                                                
         VALUE  $PPS     "E"="PPSE"                                             
                         "Y"="PPSY"                                             
                         " "="PPS "                                             
                         "F"="PPSF"                                             
                         "N"="PPSN";                                            
                                                                                
         VALUE  $FTPMC_FMT "A"="FTPMCA"                                         
                           "C"="FTPMCC"                                         
                           "M"="FTPMCM"                                         
                           "O"="FTPMCO"                                         
                           " "="FTPMC "                                         
                           "N"="FTPMCN";                                        
                                                                                
         VALUE  $AST     "Y"="ELIGIBLE AST"                                     
                         " "="NON-AST"                                          
                         "N"="NON-AST";                                         
                                                                                
         VALUE  $PPO     "A"="A      "                                          
                         "B"="B      "                                          
                         "C"="C      "                                          
                         " "="NO PPO "                                          
                         "E"="E      ";                                         
                                                                                
         VALUE  $ELDER   "F"="ELDERLY"                                          
                         "S"="ELDERLY"                                          
                         "N"="NON-ELDERLY"                                      
                         " "="NON-ELDERLY";                                     
                                                                                
         VALUE  $ELDERS  "F"="FAMILY"                                           
                         "S"="SINGLE";                                          
                                                                                
         VALUE  $HLUCK   "1"="ELIGIBLE HARDLUCK"                                
                         "2"="ELIGIBLE HARDLUCK"                                
                         "3"="ELIGIBLE HARDLUCK"                                
                         "4"="ELIGIBLE HARDLUCK"                                
                         " "="NON-HARDLUCK";                                    
                                                                                
   %GLOBAL DATE;                                                                
   OPTIONS NODATE;                                                              
                                                                                
   DATA BP13F740;                                                               
                                                                                
      INFILE BP13F740 END=EOF;                                                  
      INPUT @116   HHTY1       $CHAR1.                                          
            @118   MCPS        $CHAR1.                                          
            @163   GRO_RANK    $CHAR2.                                          
            @187   NT1         $CHAR3.                                          
            @204   INCOME      5.                                               
            @210   ELDER       $CHAR1.                                          
            @211   FLAT_TYPE   $CHAR2.                                          
            @211   FT          $CHAR1.                                          
            @213   ALLOC_SCH   $CHAR3.                                          
            @225   CATG        $CHAR1.                                          
            @226   SUCC_NT     $CHAR3.                                          
            @243   APPS        $CHAR1.                                          
            @244   APPS_ELIG   $CHAR1.                                          
            @245   MGPS        $CHAR1.                                          
            @253   HA1_RES     $CHAR1.                                          
            @269   FTPMC       $CHAR1.                                          
            @347   PPO         $CHAR1.                                          
            @433   PORT_ELIG   $CHAR1.                                          
            @461   HHTY        $CHAR1.                                          
            @466   HLUCK_ELIG  $CHAR1.                                          
            @466   HLUCK_TAG   $CHAR1.                                          
            @469   PPS_TAG     $CHAR1.                                          
            @471   AST_ELIG    $CHAR1.;                                         
                                                                                
      IF (ALLOC_SCH='PA ') AND                                                  
         (HA1_RES =' '  OR                                                      
          HA1_RES ='M'  OR                                                      
          HA1_RES ='H'  OR                                                      
          HA1_RES ='P') THEN                                                    
          ALLOC_SCH_TAG ='01';                                                  
                                                                                
      IF (FLAT_TYPE = '2F' AND (ELDER = 'F' OR ELDER = 'S')) THEN               
         DO;                                                                    
            IF ((MCPS NOT=' ') AND                                              
                (HA1_RES ='1' OR                                                
                 HA1_RES ='A' OR                                                
                 HA1_RES ='T')) OR                                              
               ((APPS NOT=' ') AND                                              
                (APPS_ELIG = '1' OR                                             
                 APPS_ELIG = 'T')) THEN                                         
                 ALLOC_SCH_TAG='10';                                            
         END;                                                                   
      ELSE                                                                      
         DO;                                                                    
            IF ((ALLOC_SCH='MCP') OR                                            
                (HA1_RES ='1' OR                                                
                 HA1_RES ='A' OR                                                
                 HA1_RES ='T')) THEN                                            
               DO;                                                              
                  IF (MCPS='C' OR                                               
                      MCPS='P') THEN                                            
                      ALLOC_SCH_TAG='02';                                       
                  IF (MCPS='M' OR                                               
                      MCPS='B') THEN                                            
                      ALLOC_SCH_TAG='03';                                       
               END;                                                             
         END;                                                                   
                                                                                
      IF ALLOC_SCH='TCP' THEN                                                   
          ALLOC_SCH_TAG='04';                                                   
                                                                                
      IF ALLOC_SCH='TPS' THEN                                                   
          ALLOC_SCH_TAG='06';                                                   
                                                                                
      IF ALLOC_SCH='FSH' THEN                                                   
          ALLOC_SCH_TAG='13';                                                   
                                                                                
      IF ALLOC_SCH='RHS' THEN                                                   
          ALLOC_SCH_TAG='14';                                                   
                                                                                
      IF ALLOC_SCH='RHO' THEN                                                   
          ALLOC_SCH_TAG='15';                                                   
                                                                                
      IF (ALLOC_SCH='REN') OR                                                   
         (ALLOC_SCH='SLA') THEN                                                 
          ALLOC_SCH_TAG='07';                                                   
                                                                                
      IF (MGPS='P') OR                                                          
         (MGPS='C') THEN                                                        
          ALLOC_SCH_TAG='08';                                                   
                                                                                
      IF (GRO_RANK NOT=' ') AND                                                 
         (ALLOC_SCH NOT='PA ') THEN                                             
          ALLOC_SCH_TAG='05';                                                   
                                                                                
      IF PORT_ELIG='P' THEN                                                     
          ALLOC_SCH_TAG='09';                                                   
                                                                                
      IF ALLOC_SCH='FTS' AND ALLOC_SCH_TAG='  ' THEN                            
          ALLOC_SCH_TAG='11';                                                   
                                                                                
      IF ALLOC_SCH='STS' AND ALLOC_SCH_TAG='  ' THEN                            
          ALLOC_SCH_TAG='12';                                                   
                                                                                
   /* READ HH IF BALLOT HH IS BLANK */                                          
      IF HHTY = " " THEN                                                        
         HHTY = HHTY1;                                                          
                                                                                
   IF _N_ =1 THEN                                                               
   DO;                                                                          
      DATE = PUT(TODAY(),DDMMYY8.);                                             
      CALL SYMPUT('DATE',DATE);                                                 
   END;                                                                         
                                                                                
   RUN;                                                                         
                                                                                
   PROC SORT;                                                                   
   BY  NT1 HHTY FLAT_TYPE ELDER ALLOC_SCH_TAG PPS_TAG FTPMC CATG;               
                                                                                
   RUN;                                                                         
                                                                                
   OPTIONS MISSING=0 PAGENO=1 NOCENTER;                                         
   PROC PRINTTO FILE=BP13L569 NEW;                                              
   PROC TABULATE DATA=BP13F740 MISSING FORMAT=7.;                               
                                                                                
 TITLE1 "BP13L56A  HDB3               S Y S T E M    O F   C O M M I T M E N T  
                   DATE : &DATE       PAGE :    ";                              
 TITLE2 "                              REPORT ON FPPS AND NON-FPPS APPLICANTS   
                                                 ";                             
 TITLE3 "        ";                                                             
                                                                                
     CLASS ELDER NT1 HHTY FLAT_TYPE ALLOC_SCH_TAG PPS_TAG FTPMC CATG;           
                                                                                
     FORMAT  CATG          $CAT_FMT.                                            
             FLAT_TYPE     $RM_TYP.                                             
             ALLOC_SCH_TAG $A_SCH.                                              
             HHTY          $TIMER.                                              
             ELDER         $ELDER.                                              
             PPS_TAG       $PPS.                                                
             FTPMC         $FTPMC_FMT.;                                         
                                                                                
     LABEL   NT1      = "NEW TOWN/ZONE :";                                      
                                                                                
     KEYLABEL N=" " ALL="TOTAL";                                                
                                                                                
     TABLE   NT1*(HHTY=" " ALL),                                                
             (FLAT_TYPE=" ")*(ELDER=" ")*(ALLOC_SCH_TAG=" " ALL),               
             ((PPS_TAG=" "*FTPMC=" "*CATG=" ")                                  
             ALL="SUB-TOTAL"*CATG=" ")                                          
             /RTS=24  BOX="FLAT TYPE   ALLOC SCH";                              
                                                                                
   RUN;                                                                         
                                                                                
   PROC SORT;                                                                   
   BY  NT1  HHTY  FLAT_TYPE ELDER ALLOC_SCH_TAG AST_ELIG  CATG;                 
                                                                                
   RUN;                                                                         
                                                                                
   OPTIONS MISSING=0;                                                           
   PROC PRINTTO FILE=BP13L569;                                                  
   PROC TABULATE DATA=BP13F740 MISSING FORMAT=7.;                               
                                                                                
 TITLE1 "BP13L56B  HDB3               S Y S T E M    O F   C O M M I T M E N T  
                   DATE : &DATE       PAGE :    ";                              
 TITLE2 "                              REPORT ON AST AND NON-AST APPLICANTS     
                                                 ";                             
 TITLE3 "        ";                                                             
                                                                                
     CLASS  ELDER NT1 HHTY FLAT_TYPE ALLOC_SCH_TAG AST_ELIG CATG;               
                                                                                
     FORMAT  CATG          $CAT_FMT.                                            
             FLAT_TYPE     $RM_TYP.                                             
             ALLOC_SCH_TAG $A_SCH.                                              
             HHTY          $TIMER.                                              
             ELDER         $ELDER.                                              
             AST_ELIG      $AST.;                                               
                                                                                
     LABEL   NT1      = "NEW TOWN/ZONE :";                                      
                                                                                
     KEYLABEL N=" " ALL="TOTAL";                                                
                                                                                
     TABLE   NT1*(HHTY=" " ALL),                                                
             (FLAT_TYPE=" ")*(ELDER=" ")*(ALLOC_SCH_TAG=" " ALL),               
             (AST_ELIG=" ")*(CATG=" " ALL="SUB-TOTAL")                          
             /RTS=26  BOX="FLAT TYPE   ALLOC SCH";                              
                                                                                
   RUN;                                                                         
                                                                                
   PROC SORT;                                                                   
        BY  NT1  HHTY  FLAT_TYPE ELDER ALLOC_SCH_TAG  CATG;                     
                                                                                
   RUN;                                                                         
                                                                                
   OPTIONS MISSING=0;                                                           
   PROC PRINTTO FILE=BP13L569;                                                  
   PROC TABULATE DATA=BP13F740 MISSING FORMAT=7.;                               
   WHERE ELDER ^= " " AND ELDER ^= "N";                                         
                                                                                
 TITLE1 "BP13L56C  HDB3               S Y S T E M    O F   C O M M I T M E N T  
                   DATE : &DATE       PAGE :    ";                              
 TITLE2 "                                    REPORT ON ELDERLY APPLICANTS       
                                                 ";                             
 TITLE3 "                                         BREAKDOWN BY ETHNIC ";        
                                                                                
     CLASS ELDER NT1 HHTY FLAT_TYPE ALLOC_SCH_TAG CATG;                         
                                                                                
     FORMAT  CATG          $CAT_FMT.                                            
             FLAT_TYPE     $RM_TYP.                                             
             ALLOC_SCH_TAG $A_SCH.                                              
             HHTY          $TIMER.;                                             
                                                                                
     LABEL   NT1      = "NEW TOWN/ZONE :";                                      
                                                                                
     KEYLABEL N=" " ALL="TOTAL";                                                
                                                                                
     TABLE   NT1,                                                               
             (FLAT_TYPE=" ")*(ALLOC_SCH_TAG=" " ALL),                           
             ((HHTY=" ")*(CATG=" " ALL="SUB-TOTAL") ALL)                        
             /RTS=26  BOX="FLAT TYPE   ALLOC SCH";                              
                                                                                
   RUN;                                                                         
                                                                                
   OPTIONS MISSING=0;                                                           
   PROC PRINTTO FILE=BP13L569;                                                  
   PROC TABULATE DATA=BP13F740 MISSING FORMAT=7.;                               
   WHERE FLAT_TYPE = "2F" OR "RF";                                              
                                                                                
 TITLE1 "BP13L56D  HDB3               S Y S T E M    O F   C O M M I T M E N T  
                   DATE : &DATE       PAGE :    ";                              
 TITLE2 "                                    REPORT ON APPLICANTS WITH PPO      
                                                 ";                             
 TITLE3 "                                                     ";                
                                                                                
     CLASS ELDER NT1 HHTY FLAT_TYPE ALLOC_SCH_TAG CATG PPO;                     
                                                                                
     FORMAT  CATG          $CAT_FMT.                                            
             PPO           $PPO.                                                
             FLAT_TYPE     $RM_TYP.                                             
             ALLOC_SCH_TAG $A_SCH.                                              
             ELDER         $ELDERS.                                             
             HHTY          $TIMER.;                                             
                                                                                
     LABEL   NT1      = "NEW TOWN/ZONE :";                                      
                                                                                
     KEYLABEL N=" " ALL="TOTAL";                                                
                                                                                
     TABLE   NT1,                                                               
             (FLAT_TYPE=" ")*(ALLOC_SCH_TAG=" " ALL),                           
             ((HHTY=" ")*(PPO=" " ALL="SUB-TOTAL") ALL)                         
             /RTS=26  BOX="FLAT TYPE   ALLOC SCH";                              
                                                                                
   RUN;                                                                         
                                                                                
   OPTIONS MISSING=0;                                                           
   PROC PRINTTO FILE=BP13L569;                                                  
   PROC TABULATE DATA=BP13F740 MISSING FORMAT=7.;                               
   WHERE (FLAT_TYPE = '2F' AND INCOME > 06000);                                 
                                                                                
 TITLE1 "BP13L56E  HDB3               S Y S T E M    O F   C O M M I T M E N T  
                   DATE : &DATE       PAGE :    ";                              
 TITLE2 "                                    REPORT ON ELDERLY APPLICANTS       
                                                 ";                             
 TITLE3 "                                             INCOME >6K ";             
                                                                                
     CLASS ELDER NT1 HHTY FLAT_TYPE ALLOC_SCH_TAG CATG;                         
                                                                                
     FORMAT  CATG          $CAT_FMT.                                            
             FLAT_TYPE     $RM_TYP.                                             
             ALLOC_SCH_TAG $A_SCH.                                              
             ELDER         $ELDERS.                                             
             HHTY          $TIMER.;                                             
                                                                                
     LABEL   NT1      = "NEW TOWN/ZONE :";                                      
                                                                                
     KEYLABEL N=" " ALL="TOTAL";                                                
                                                                                
     TABLE   NT1,                                                               
             (FLAT_TYPE=" ")*(ELDER=" ")*(ALLOC_SCH_TAG=" " ALL),               
             ((HHTY=" ")*(CATG=" " ALL="SUB-TOTAL") ALL)                        
             /RTS=26  BOX="FLAT TYPE   ALLOC SCH";                              
                                                                                
   RUN;                                                                         
                                                                                
   PROC SORT;                                                                   
   BY  NT1 HHTY FLAT_TYPE ELDER ALLOC_SCH_TAG HLUCK_ELIG HLUCK_TAG CATG;        
                                                                                
   RUN;                                                                         
                                                                                
   OPTIONS MISSING=0;                                                           
   PROC PRINTTO FILE=BP13L569;                                                  
   PROC TABULATE DATA=BP13F740 MISSING FORMAT=7.;                               
                                                                                
 TITLE1 "BP13L56F  HDB3               S Y S T E M    O F   C O M M I T M E N T  
                   DATE : &DATE       PAGE :    ";                              
 TITLE2 "                           REPORT ON HARDLUCK & NON-HARDLUCK APPLICANTS
                                                 ";                             
 TITLE3 "        ";                                                             
                                                                                
     CLASS ELDER NT1 HHTY FLAT_TYPE ALLOC_SCH_TAG HLUCK_ELIG HLUCK_TAG CATG;    
                                                                                
     FORMAT  CATG          $CAT_FMT.                                            
             FLAT_TYPE     $RM_TYP.                                             
             ALLOC_SCH_TAG $A_SCH.                                              
             HHTY          $TIMER.                                              
             ELDER         $ELDER.                                              
             HLUCK_ELIG    $HLUCK.;                                             
                                                                                
     LABEL   NT1      = "NEW TOWN/ZONE :";                                      
                                                                                
     KEYLABEL N=" " ALL="TOTAL";                                                
                                                                                
     TABLE   NT1*(HHTY=" " ALL),                                                
             (FLAT_TYPE=" ")*(ELDER=" ")*(ALLOC_SCH_TAG=" " ALL),               
             (HLUCK_ELIG=" ")*((HLUCK_TAG=" "*CATG=" ")                         
             ALL="SUB-TOTAL"*CATG=" ")                                          
             /RTS=24  BOX="FLAT TYPE   ALLOC SCH";                              
   RUN;                                                                         
                                                                                
/*                                                                              
