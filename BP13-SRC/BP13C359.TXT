      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13C359.                                                 
      *AUTHOR.        EG1.                                                      
      *DATE-WRITTEN.  21/03/96.                                                 
                                                                                
      ******************************************************************        
      *  PROGRAM NAME : BP13C359                                       *        
      *  OBJECTIVE    : 1. TO PRINT REPORT ON NUMBER OF HPI FORMS      *        
      *                    SEND TO CPF FOR SUB-TRANSFER CASES          *        
      ******************************************************************        
      *  INPUT/OUTPUT FILES :                                          *        
      *  ====================                                          *        
      *  TYPE  FILE NAME                   DD NAME   ACCESS    KEYLEN  *        
      *  ----  ---------                   -------   ------    ------  *        
      *  I     BP13K882                    BP13K882   KSDS             *        
      *  I     BP13K820                    BP13K820   KSDS             *        
      *  I     AB02F860                    AB02F860   SEQ.             *        
      *  O     BP13L359                    BP13L359                    *        
      *  O     P13L359A                    P13L359A                    *        
      *  O     P13L359B                    P13L359B                    *        
      *================================================================*        
      * MODIFICATION HISTORY:                                          *        
      * ---------------------                                          *        
      * CHG REQ. AUTHOR  DATE     DESCRIPTIONS                         *        
      * =======  ====== =======   =====================================*        
      * BP130290  SGK    08/01/98 TO RECOMPILE THE CHANGES MADE IN F435*        
      * BP130315  SGK    19/06/98 TO CATER FOR RETIREMENT ACCOUNT.     *        
      * BP130522  SGK    29/07/98 TO CATER FOR CPFB Y2K.               *        
      * BP130418  RHB    26/06/98 TO CHANGE BP13K820 TO BP13K820; Y2K  *        
      * BP130929  MCC    13/10/00 TO CHNGE BP13F435 TO AB02F860        *        
      * BP136692  KSJ    15/02/17 EXPAND AB02F860 FROM 600 TO 2000     *        
      ******************************************************************        
                                                                                
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
      *================================================================*        
       CONFIGURATION SECTION.                                                   
      *================================================================*        
       SOURCE-COMPUTER.  IBM-4341.                                              
       OBJECT-COMPUTER.  IBM-4341.                                              
                                                                                
                                                                                
      *================================================================*        
       INPUT-OUTPUT SECTION.                                                    
      *================================================================*        
      *----------------------------------------------------------------*        
       FILE-CONTROL.                                                            
      *----------------------------------------------------------------*        
           SELECT AB02F860 ASSIGN TO AB02F860.                                  
                                                                                
           SELECT BP13K882 ASSIGN TO BP13K882                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS          IS DYNAMIC                                    
                  RECORD KEY      IS K882-KEY-FLD                               
                  FILE STATUS     IS K882-STATUS.                               
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS          IS DYNAMIC                                    
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS K820-STATUS.                               
                                                                                
           SELECT BP13L359 ASSIGN TO BP13L359.                                  
           SELECT P13L359A ASSIGN TO P13L359A.                                  
           SELECT P13L359B ASSIGN TO P13L359B.                                  
                                                                                
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      *================================================================*        
       FILE SECTION.                                                            
      *================================================================*        
       FD   AB02F860                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY AB02F860.                                                           
                                                                                
       FD   BP13K882                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K882.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13L359                                                            
            LABEL RECORDS ARE OMITTED                                           
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       01   L359-LINE                   PIC X(132).                             
                                                                                
       FD   P13L359A                                                            
            LABEL RECORDS ARE OMITTED                                           
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       01   L359A-LINE                  PIC X(132).                             
                                                                                
       FD   P13L359B                                                            
            LABEL RECORDS ARE OMITTED                                           
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       01   L359B-LINE                  PIC X(132).                             
                                                                                
                                                                                
      *================================================================*        
       WORKING-STORAGE SECTION.                                                 
      *================================================================*        
      *----------------------------------------------------------------*        
      *    ERROR HANDLING VARIABLES                                    *        
      *----------------------------------------------------------------*        
       01 WS-ERR-MSG                     PIC X(80) VALUE SPACES.                
                                                                                
      *----------------------------------------------------------------*        
      *    FILE STATUS                                                 *        
      *----------------------------------------------------------------*        
       01 WS-FILE-STATUS-VAR.                                                   
          05 K882-STATUS                 PIC X(2).                              
          05 K820-STATUS                 PIC X(2).                              
       01 WS-EOF-STATUS-VAR.                                                    
          05 WS-F860-EOF                 PIC X(1) VALUE 'N'.                    
          05 WS-K882-FND                 PIC X(1) VALUE SPACES.                 
                                                                                
      *----------------------------------------------------------------*        
      *    WORKING STORAGE AREA FOR COUNTER                            *        
      *----------------------------------------------------------------*        
       01  WS-COUNTERS.                                                         
           05 WS-CNT-F860-READ           PIC 9(07) VALUE 0.                     
           05 WS-CNT-WRITE-L359          PIC 9(07) VALUE 0.                     
           05 WS-CNT-WRITE-L359A         PIC 9(07) VALUE 0.                     
           05 WS-CNT-WRITE-L359B         PIC 9(07) VALUE 0.                     
                                                                                
      *----------------------------------------------------------------*        
      *    WORKING STORAGE AREA FOR DATE VARIABLES                     *        
      *----------------------------------------------------------------*        
       01  WS-CURRENT-DATE.                                                     
           05 WS-CURR-CC                 PIC X(02) VALUE SPACES.                
           05 WS-CURR-YYMMDD.                                                   
              10 WS-CURR-YY              PIC X(02) VALUE SPACES.                
              10 WS-CURR-MM              PIC X(02) VALUE SPACES.                
              10 WS-CURR-DD              PIC X(02) VALUE SPACES.                
                                                                                
      *----------------------------------------------------------------*        
      *    WORKING STORAGE AREA FOR REPORT VARIABLES                   *        
      *----------------------------------------------------------------*        
       01  WS-PRINT-VARIABLES.                                                  
           05 WS-SEQ-CNT1                PIC 9(5)  VALUE 0.                     
           05 WS-PAGE-CNT1               PIC 9(5)  VALUE 0.                     
           05 WS-LINE-CNT1               PIC 9(2)  VALUE 0.                     
           05 WS-SEQ-CNT2                PIC 9(5)  VALUE 0.                     
           05 WS-PAGE-CNT2               PIC 9(5)  VALUE 0.                     
           05 WS-LINE-CNT2               PIC 9(2)  VALUE 0.                     
           05 WS-SEQ-CNT3                PIC 9(5)  VALUE 0.                     
           05 WS-PAGE-CNT3               PIC 9(5)  VALUE 0.                     
           05 WS-LINE-CNT3               PIC 9(2)  VALUE 0.                     
           05 WS-NAME                    PIC X(20) VALUE SPACES.                
           05 WS-NRIC                    PIC X(09) VALUE SPACES.                
                                                                                
      *----------------------------------------------------------------*        
      *    WORKING STORAGE AREA FOR REPORT LAYOUT                      *        
      *----------------------------------------------------------------*        
       01  L359-RPT-HDR-1.                                                      
           05 L359-RPT-ID                PIC X(08) VALUE 'BP13L359'.            
           05 FILLER                     PIC X(06) VALUE SPACES.                
           05 FILLER                     PIC X(04) VALUE 'HDB3'.                
           05 FILLER                     PIC X(28) VALUE SPACES.                
           05 FILLER                     PIC X(39) VALUE                        
             'S Y S T E M   O F   C O M M I T M E N T'.                         
           05 FILLER                     PIC X(17) VALUE SPACES.                
           05 FILLER                     PIC X(07) VALUE 'DATE : '.             
           05 L359-PRINT-DATE            PIC X(10).                             
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(07) VALUE 'PAGE : '.             
           05 L359-PAGE-CNT              PIC ZZZZ9.                             
                                                                                
       01  L359-RPT-HDR-2.                                                      
           05 FILLER                     PIC X(57) VALUE SPACES.                
           05 FILLER                     PIC X(14) VALUE                        
             'AGREEMENT TEAM'.                                                  
                                                                                
       01  L359-RPT-HDR-3.                                                      
           05 FILLER                     PIC X(49) VALUE SPACES.                
           05 FILLER                     PIC X(30) VALUE                        
             'LIST OF HPI FORMS TO CPF BOARD'.                                  
                                                                                
       01  L359-RPT-HDR-3-A.                                                    
           05 FILLER                     PIC X(40) VALUE SPACES.                
           05 FILLER                     PIC X(51) VALUE                        
             'LIST OF UPDATED WITHDRAWAL FORMS TO BE SENT TO CPFB'.             
                                                                                
       01  L359-RPT-HDR-3-B.                                                    
           05 FILLER                     PIC X(40) VALUE SPACES.                
           05 FILLER                     PIC X(51) VALUE                        
             'LIST OF DELETED WITHDRAWAL FORMS TO BE SENT TO CPFB'.             
                                                                                
       01  L359-RPT-HDR-4.                                                      
           05 FILLER                     PIC X(49) VALUE SPACES.                
           05 FILLER                     PIC X(51) VALUE                        
             '      SUB-TRANSFER CASES                  '.                      
                                                                                
       01  L359-RPT-HDR-5.                                                      
           05 FILLER                     PIC X(02) VALUE SPACES.                
           05 FILLER                     PIC X(03) VALUE 'NO.'.                 
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(08) VALUE 'REGN NO.'.            
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(09) VALUE 'SERIAL NO'.           
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(08) VALUE 'CPF  NO.'.            
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(09) VALUE '  NRIC   '.           
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(20) VALUE                        
             '  APPL NAME(TRUNC)  '.                                            
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(10) VALUE                        
             ' AMT SUSP '.                                                      
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(09) VALUE                        
             ' AMT  SF '.                                                       
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(09) VALUE                        
             ' AMT  CV '.                                                       
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(10) VALUE                        
             ' AMT  CD  '.                                                      
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(10) VALUE                        
             ' SA DATE  '.                                                      
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(10) VALUE                        
             'RETIREMENT'.                                                      
                                                                                
       01  L359-RPT-HDR-6.                                                      
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(04) VALUE '===='.                
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(08) VALUE '========'.            
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(09) VALUE '========='.           
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(09) VALUE '========'.            
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(09) VALUE '========='.           
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(20) VALUE                        
             '===================='.                                            
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(10) VALUE                        
             '=========='.                                                      
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(09) VALUE                        
             '========='.                                                       
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(09) VALUE                        
             '========='.                                                       
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(10) VALUE                        
             '=========='.                                                      
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(10) VALUE                        
             '=========='.                                                      
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 FILLER                     PIC X(10) VALUE                        
             '=========='.                                                      
                                                                                
       01  L359-CONTROL-LINE1.                                                  
           05 FILLER                     PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(10) VALUE 'TO HDB : '.           
                                                                                
       01  L359-CONTROL-LINE2.                                                  
           05 FILLER                     PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(02) VALUE 'I '.                  
           05 FILLER                     PIC X(40) VALUE ALL '_'.               
           05 FILLER                     PIC X(02) VALUE SPACES.                
           05 FILLER                     PIC X(24) VALUE                        
             'ACKNOWLEDGED RECEIPT OF '.                                        
           05 L359-TOTAL-CNT             PIC ZZZ9.                              
           05 FILLER                     PIC X(17) VALUE                        
             '  CPF FORMS FOR  '.                                               
           05 L359-CONTROL-DATE          PIC X(10).                             
           05 FILLER                     PIC X(01) VALUE '.'.                   
                                                                                
       01  L359-CONTROL-LINE3.                                                  
           05 FILLER                     PIC X(70) VALUE SPACES.                
           05 FILLER                     PIC X(20) VALUE ALL '_'.               
                                                                                
       01  L359-CONTROL-LINE4.                                                  
           05 FILLER                     PIC X(70) VALUE SPACES.                
           05 FILLER                     PIC X(20) VALUE                        
             '  CPF BOARD'.                                                     
                                                                                
       01  L359-DETAIL-LINE.                                                    
           05 L359-PRT-NO                PIC ZZZZ9.                             
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 L359-CUR-REG               PIC X(08).                             
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 L359-SER-NO                PIC X(09).                             
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 L359-CPF-NO                PIC X(09).                             
           05 FILLER                     PIC X(01) VALUE SPACE.                 
           05 L359-NRIC                  PIC X(09).                             
           05 FILLER                     PIC X(01) VALUE SPACE.                 
           05 L359-NAME                  PIC X(20).                             
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 L359-AMT-SUSP              PIC ZZZ,ZZ9.99.                        
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 L359-AMT-SF                PIC ZZ,ZZ9.99.                         
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 L359-AMT-CV                PIC ZZ,ZZ9.99.                         
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 L359-AMT-CD                PIC ZZZ,ZZ9.99.                        
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 L359-SA-DATE               PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X(01) VALUE SPACES.                
           05 L359-AMT-RETIRE            PIC ZZZ,ZZ9.99.                        
           05 L359-AMT-RETIRE-A REDEFINES L359-AMT-RETIRE.                      
              10 WS-AMT-RETIRE-A         PIC X(10).                             
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *================================================================*        
       0000-MAIN-ROUTINE.                                                       
      *================================================================*        
           PERFORM 1000-INITIALIZATION      THRU 1000-EXIT.                     
           PERFORM 2000-PROCESS-ROUTINE     THRU 2000-EXIT                      
                   UNTIL WS-F860-EOF = 'Y'.                                     
           PERFORM 8000-WRITE-ACKNOWLEDGE   THRU 8000-EXIT.                     
           PERFORM 9999-CLOSE-FILES         THRU 9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       1000-INITIALIZATION.                                                     
      *================================================================*        
           OPEN INPUT  AB02F860                                                 
                       BP13K882                                                 
                       BP13K820                                                 
                OUTPUT BP13L359                                                 
                       P13L359A                                                 
                       P13L359B.                                                
                                                                                
           IF K882-STATUS NOT EQUAL 00 AND 97                                   
              STRING 'BP13K882 OPENING ERROR (' K882-STATUS ')'                 
                     DELIMITED BY SIZE INTO WS-ERR-MSG                          
              PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                          
           END-IF.                                                              
                                                                                
           IF K820-STATUS NOT EQUAL 00 AND 97                                   
              STRING 'BP13K820 OPENING ERROR (' K820-STATUS ')'                 
                     DELIMITED BY SIZE INTO WS-ERR-MSG                          
              PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                          
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
                                                                                
           MOVE 50                 TO  WS-LINE-CNT1                             
                                       WS-LINE-CNT2                             
                                       WS-LINE-CNT3.                            
                                                                                
           READ  AB02F860 AT END                                                
                 MOVE 'Y'          TO  WS-F860-EOF.                             
                                                                                
           IF WS-F860-EOF = 'Y'                                                 
              STRING 'NO INPUT F860 RECORDS FOUND'                              
                     DELIMITED BY SIZE INTO WS-ERR-MSG                          
              PERFORM 9999-CLOSE-FILES         THRU 9999-EXIT                   
           ELSE                                                                 
              ADD 1                TO WS-CNT-F860-READ                          
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       2000-PROCESS-ROUTINE.                                                    
      *================================================================*        
           PERFORM  3000-VALIDATE-RECS THRU 3000-EXIT.                          
           PERFORM  4000-FORMAT-PRINT  THRU 4000-EXIT.                          
                                                                                
           IF F860-CDE-ACTION EQUAL '3'                                         
              PERFORM 6100-WRITE-L359A          THRU 6100-EXIT                  
           ELSE                                                                 
              IF F860-CDE-ACTION EQUAL '1'                                      
                 PERFORM 6200-WRITE-L359B       THRU 6200-EXIT                  
              ELSE                                                              
                 PERFORM 6000-WRITE-L359        THRU 6000-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           READ  AB02F860 AT END                                                
                 MOVE 'Y'        TO WS-F860-EOF                                 
                 GO  TO 2000-EXIT                                               
                 END-READ.                                                      
                                                                                
           ADD   1   TO  WS-CNT-F860-READ.                                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       3000-VALIDATE-RECS.                                                      
      *================================================================*        
           MOVE LOW-VALUES                 TO K882-KEY-FLD.                     
           MOVE F860-NUM-ORIG-REGN         TO K882-NUM-REGN-SUBTRF.             
                                                                                
           PERFORM  8100-START-BP13K882    THRU 8100-EXIT.                      
           PERFORM  8150-READNEXT-BP13K882 THRU 8150-EXIT.                      
                                                                                
           IF WS-K882-FND = 'N'                                                 
              STRING 'AB02F860 RECORD NOT FND IN BP13K882 ('                    
                      F860-NUM-ORIG-REGN   ')' DELIMITED BY SIZE                
                             INTO WS-ERR-MSG                                    
              PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 3100-RETRIEVE-NAME      THRU 3100-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       3100-RETRIEVE-NAME.                                                      
      *================================================================*        
           MOVE SPACES                     TO WS-NAME WS-NRIC.                  
                                                                                
           IF K882-NUM-NRIC1-TREE > SPACES                                      
              MOVE F860-NUM-ORIG-REGN      TO K820-NUM-REGN                     
              MOVE K882-NUM-NRIC1-TREE     TO K820-NUM-NRIC                     
              PERFORM 8200-READ-BP13K820   THRU 8200-EXIT                       
              IF K820-NUM-CPF  = F860-NUM-CPF-ACCT                              
                 MOVE K820-NUM-NRIC        TO WS-NRIC                           
                 MOVE K820-NME-OCCP        TO WS-NAME                           
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K882-NUM-NRIC2-TREE > SPACES AND                                  
              WS-NRIC = SPACES                                                  
              MOVE F860-NUM-ORIG-REGN      TO K820-NUM-REGN                     
              MOVE K882-NUM-NRIC2-TREE     TO K820-NUM-NRIC                     
              PERFORM 8200-READ-BP13K820   THRU 8200-EXIT                       
              IF K820-NUM-CPF  = F860-NUM-CPF-ACCT                              
                 MOVE K820-NUM-NRIC        TO WS-NRIC                           
                 MOVE K820-NME-OCCP        TO WS-NAME                           
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K882-NUM-NRIC3-TREE > SPACES AND                                  
              WS-NRIC = SPACES                                                  
              MOVE F860-NUM-ORIG-REGN      TO K820-NUM-REGN                     
              MOVE K882-NUM-NRIC3-TREE     TO K820-NUM-NRIC                     
              PERFORM 8200-READ-BP13K820   THRU 8200-EXIT                       
              IF K820-NUM-CPF  = F860-NUM-CPF-ACCT                              
                 MOVE K820-NUM-NRIC        TO WS-NRIC                           
                 MOVE K820-NME-OCCP        TO WS-NAME                           
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF K882-NUM-NRIC4-TREE > SPACES AND                                  
              WS-NRIC = SPACES                                                  
              MOVE F860-NUM-ORIG-REGN      TO K820-NUM-REGN                     
              MOVE K882-NUM-NRIC4-TREE     TO K820-NUM-NRIC                     
              PERFORM 8200-READ-BP13K820   THRU 8200-EXIT                       
              IF K820-NUM-CPF  = F860-NUM-CPF-ACCT                              
                 MOVE K820-NUM-NRIC        TO WS-NRIC                           
                 MOVE K820-NME-OCCP        TO WS-NAME                           
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       4000-FORMAT-PRINT.                                                       
      *================================================================*        
           MOVE SPACES                 TO  L359-DETAIL-LINE.                    
                                                                                
           EVALUATE F860-CDE-ACTION                                             
           WHEN     '1'                                                         
                    ADD 1              TO  WS-SEQ-CNT3                          
                    MOVE WS-SEQ-CNT3   TO  L359-PRT-NO                          
           WHEN     '2'                                                         
                    ADD 1              TO  WS-SEQ-CNT1                          
                    MOVE WS-SEQ-CNT1   TO  L359-PRT-NO                          
           WHEN     '3'                                                         
                    ADD 1              TO  WS-SEQ-CNT2                          
                    MOVE WS-SEQ-CNT2   TO  L359-PRT-NO                          
           END-EVALUATE.                                                        
                                                                                
           MOVE F860-NUM-ORIG-REGN     TO  L359-CUR-REG.                        
           MOVE F860-NUM-SNO           TO  L359-SER-NO.                         
           MOVE F860-NUM-CPF-ACCT      TO  L359-CPF-NO.                         
           MOVE WS-NRIC                TO  L359-NRIC.                           
           MOVE WS-NAME                TO  L359-NAME.                           
                                                                                
           IF F860-AMT-STAMP-FEE NUMERIC AND                                    
              F860-AMT-STAMP-FEE > 0                                            
              MOVE F860-AMT-STAMP-FEE  TO  L359-AMT-SF                          
           ELSE                                                                 
              MOVE ZEROES              TO  L359-AMT-SF                          
           END-IF.                                                              
                                                                                
           IF F860-AMT-CONV-FEE NUMERIC AND                                     
              F860-AMT-CONV-FEE > 0                                             
              MOVE F860-AMT-CONV-FEE   TO  L359-AMT-CV                          
           ELSE                                                                 
              MOVE ZEROES              TO  L359-AMT-CV                          
           END-IF.                                                              
                                                                                
           IF F860-AMT-MSS IS NOT NUMERIC                                       
              MOVE ZEROES              TO  F860-AMT-MSS.                        
                                                                                
           IF F860-AMT-MSS > 0                                                  
              MOVE F860-AMT-MSS        TO  L359-AMT-RETIRE                      
           ELSE                                                                 
              MOVE '     - '           TO  L359-AMT-RETIRE-A                    
           END-IF.                                                              
                                                                                
           IF F860-AMT-LOAN-REPAY NUMERIC AND                                   
              F860-AMT-LOAN-REPAY > 0                                           
              IF F860-CDE-REPYMT = 'T'                                          
                 MOVE F860-AMT-LOAN-REPAY TO  L359-AMT-SUSP                     
                 MOVE ZEROES              TO  L359-AMT-CD                       
              ELSE                                                              
                 IF F860-CDE-REPYMT = 'A'                                       
                    MOVE F860-AMT-LOAN-REPAY TO L359-AMT-CD                     
                    MOVE ZEROES              TO L359-AMT-SUSP                   
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              MOVE ZEROES              TO  L359-AMT-CD                          
                                           L359-AMT-SUSP                        
           END-IF.                                                              
           IF K882-DTE-FSA-SIGNED > SPACES                                      
              STRING K882-DTE-FSA-SIGNED(7:2) '/'                               
                     K882-DTE-FSA-SIGNED(5:2) '/'                               
                     K882-DTE-FSA-SIGNED(1:4) DELIMITED BY SIZE                 
                                        INTO  L359-SA-DATE                      
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       6000-WRITE-L359.                                                         
      *================================================================*        
           IF WS-LINE-CNT1 > 46                                                 
              PERFORM 6050-WRITE-HDR1  THRU 6050-EXIT                           
           END-IF.                                                              
                                                                                
           WRITE L359-LINE     FROM  L359-DETAIL-LINE AFTER 2.                  
           ADD   1             TO    WS-CNT-WRITE-L359                          
           ADD   2             TO    WS-LINE-CNT1.                              
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       6050-WRITE-HDR1.                                                         
      *================================================================*        
           MOVE 'BP13L359'     TO  L359-RPT-ID.                                 
           ADD  1              TO  WS-PAGE-CNT1.                                
           MOVE WS-PAGE-CNT1   TO  L359-PAGE-CNT.                               
                                                                                
           STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CC WS-CURR-YY           
                  DELIMITED BY SIZE INTO L359-PRINT-DATE.                       
                                                                                
           WRITE L359-LINE   FROM L359-RPT-HDR-1   AFTER PAGE.                  
           WRITE L359-LINE   FROM L359-RPT-HDR-2   AFTER 1.                     
           WRITE L359-LINE   FROM L359-RPT-HDR-3   AFTER 1.                     
           WRITE L359-LINE   FROM L359-RPT-HDR-4   AFTER 1.                     
           WRITE L359-LINE   FROM L359-RPT-HDR-5   AFTER 3.                     
           WRITE L359-LINE   FROM L359-RPT-HDR-6   AFTER 1.                     
                                                                                
           MOVE 8               TO WS-LINE-CNT1.                                
                                                                                
       6050-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       6100-WRITE-L359A.                                                        
      *================================================================*        
           IF WS-LINE-CNT2 > 46                                                 
              PERFORM 6150-WRITE-HDR2  THRU 6150-EXIT                           
           END-IF.                                                              
                                                                                
           WRITE L359A-LINE    FROM  L359-DETAIL-LINE AFTER 2.                  
           ADD   1             TO    WS-CNT-WRITE-L359A                         
           ADD   2             TO    WS-LINE-CNT2.                              
                                                                                
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       6150-WRITE-HDR2.                                                         
      *================================================================*        
           MOVE 'P13L359A'     TO  L359-RPT-ID.                                 
           ADD  1              TO  WS-PAGE-CNT2.                                
           MOVE WS-PAGE-CNT2   TO  L359-PAGE-CNT.                               
                                                                                
           STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CC WS-CURR-YY           
                  DELIMITED BY SIZE INTO L359-PRINT-DATE.                       
                                                                                
           WRITE L359A-LINE   FROM L359-RPT-HDR-1   AFTER PAGE.                 
           WRITE L359A-LINE   FROM L359-RPT-HDR-2   AFTER 1.                    
           WRITE L359A-LINE   FROM L359-RPT-HDR-3-A AFTER 1.                    
           WRITE L359A-LINE   FROM L359-RPT-HDR-4   AFTER 1.                    
           WRITE L359A-LINE   FROM L359-RPT-HDR-5   AFTER 3.                    
           WRITE L359A-LINE   FROM L359-RPT-HDR-6   AFTER 1.                    
                                                                                
           MOVE 8               TO WS-LINE-CNT2.                                
                                                                                
       6150-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       6200-WRITE-L359B.                                                        
      *================================================================*        
           IF WS-LINE-CNT3 > 46                                                 
              PERFORM 6250-WRITE-HDR3  THRU 6250-EXIT                           
           END-IF.                                                              
                                                                                
           WRITE L359B-LINE    FROM  L359-DETAIL-LINE AFTER 2.                  
           ADD   1             TO    WS-CNT-WRITE-L359B                         
           ADD   2             TO    WS-LINE-CNT3.                              
                                                                                
       6200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       6250-WRITE-HDR3.                                                         
      *================================================================*        
           MOVE 'P13L359B'     TO  L359-RPT-ID.                                 
           ADD  1              TO  WS-PAGE-CNT3.                                
           MOVE WS-PAGE-CNT3   TO  L359-PAGE-CNT.                               
                                                                                
           STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CC WS-CURR-YY           
                  DELIMITED BY SIZE INTO L359-PRINT-DATE.                       
                                                                                
           WRITE L359B-LINE   FROM L359-RPT-HDR-1   AFTER PAGE.                 
           WRITE L359B-LINE   FROM L359-RPT-HDR-2   AFTER 1.                    
           WRITE L359B-LINE   FROM L359-RPT-HDR-3-B AFTER 1.                    
           WRITE L359B-LINE   FROM L359-RPT-HDR-4   AFTER 1.                    
           WRITE L359B-LINE   FROM L359-RPT-HDR-5   AFTER 3.                    
           WRITE L359B-LINE   FROM L359-RPT-HDR-6   AFTER 1.                    
                                                                                
           MOVE 8               TO WS-LINE-CNT3.                                
                                                                                
       6250-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       8000-WRITE-ACKNOWLEDGE.                                                  
      *================================================================*        
           MOVE WS-SEQ-CNT1  TO L359-TOTAL-CNT.                                 
                                                                                
           STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CC WS-CURR-YY           
                  DELIMITED BY SIZE INTO L359-CONTROL-DATE.                     
                                                                                
           MOVE SPACES    TO L359-DETAIL-LINE.                                  
                                                                                
           WRITE L359-LINE FROM L359-DETAIL-LINE    AFTER PAGE.                 
           WRITE L359-LINE FROM L359-CONTROL-LINE1  AFTER 3.                    
           WRITE L359-LINE FROM L359-CONTROL-LINE2  AFTER 4.                    
           WRITE L359-LINE FROM L359-CONTROL-LINE3  AFTER 8.                    
           WRITE L359-LINE FROM L359-CONTROL-LINE4  AFTER 1.                    
                                                                                
       8000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       8100-START-BP13K882.                                                     
      *================================================================*        
            MOVE 'Y'                 TO WS-K882-FND.                            
                                                                                
            START BP13K882 KEY >= K882-KEY-FLD                                  
                  INVALID KEY  MOVE 'N'    TO WS-K882-FND                       
                               GO TO 8100-EXIT.                                 
                                                                                
            IF K882-STATUS NOT = '00'                                           
               STRING 'BP13K882 START ERROR CODE(' K882-STATUS ')'              
                      DELIMITED BY SIZE INTO WS-ERR-MSG                         
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
       8100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       8150-READNEXT-BP13K882.                                                  
      *================================================================*        
            READ  BP13K882 NEXT                                                 
                  AT END   MOVE 'N'        TO WS-K882-FND                       
                           GO TO 8150-EXIT.                                     
                                                                                
            IF K882-STATUS NOT = '00'                                           
               STRING 'BP13K882 READ NEXT ERROR CODE(' K882-STATUS ')'          
                      DELIMITED BY SIZE INTO WS-ERR-MSG                         
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
            IF K882-NUM-REGN-SUBTRF NOT = F860-NUM-ORIG-REGN                    
               MOVE 'N'              TO WS-K882-FND                             
            END-IF.                                                             
                                                                                
       8150-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       8200-READ-BP13K820.                                                      
      *================================================================*        
            READ  BP13K820                                                      
                  INVALID KEY MOVE SPACES  TO BP13K820-REC                      
                           GO TO 8200-EXIT.                                     
                                                                                
            IF K820-STATUS NOT = '00'                                           
               STRING 'BP13K820 READ ERROR CODE(' K820-STATUS ')'               
                      DELIMITED BY SIZE INTO WS-ERR-MSG                         
               PERFORM 9900-HANDLE-ABEND  THRU 9900-EXIT                        
            END-IF.                                                             
                                                                                
       8200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       9900-HANDLE-ABEND.                                                       
      *================================================================*        
            CLOSE   AB02F860                                                    
                    BP13K882                                                    
                    BP13K820                                                    
                    BP13L359                                                    
                    P13L359A                                                    
                    P13L359B.                                                   
                                                                                
            DISPLAY '***************************************'                   
            DISPLAY '*    BP13C359  ABEND TOTAL            *'                   
            DISPLAY '***************************************'                   
            DISPLAY '* NO. OF F860 READ: ' WS-CNT-F860-READ.                    
            DISPLAY '* NO. OF NEW CASES: ' WS-CNT-WRITE-L359.                   
            DISPLAY '* NO. OF UPD CASES: ' WS-CNT-WRITE-L359A.                  
            DISPLAY '* NO. OF DEL CASES: ' WS-CNT-WRITE-L359B.                  
            DISPLAY '***************************************'                   
            DISPLAY '* ABEND DESC: ' WS-ERR-MSG.                                
            MOVE '4095'               TO RETURN-CODE.                           
                                                                                
            STOP RUN.                                                           
       9900-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       9999-CLOSE-FILES.                                                        
      *================================================================*        
            CLOSE   AB02F860                                                    
                    BP13K882                                                    
                    BP13K820                                                    
                    BP13L359                                                    
                    P13L359A                                                    
                    P13L359B.                                                   
                                                                                
            DISPLAY '***************************************'                   
            DISPLAY '*    BP13C359  CONTROL TOTAL          *'                   
            DISPLAY '***************************************'                   
            DISPLAY '* NO. OF F860 READ: ' WS-CNT-F860-READ.                    
            DISPLAY '* NO. OF NEW CASES: ' WS-CNT-WRITE-L359.                   
            DISPLAY '* NO. OF UPD CASES: ' WS-CNT-WRITE-L359A.                  
            DISPLAY '* NO. OF DEL CASES: ' WS-CNT-WRITE-L359B.                  
            DISPLAY '* ABEND DESC: ' WS-ERR-MSG.                                
            DISPLAY '***************************************'                   
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
