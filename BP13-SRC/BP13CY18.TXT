      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CY18.                                                 
       AUTHOR.        JIANG BO.                                                 
       DATE-WRITTEN.  30/10/08.                                                 
      *****************************************************************         
      * OBJECTIVE -                                                             
      *   MATCH F205 WITH F816 TO GET NT OFFERED UNDER THE NT CODE              
      * TO EXTRACT SPECIFY NT FOR PRINT CASES WITH MULTIPLE REQUEST             
      *                                                                         
      *     INPUT FILE :                                                        
      *                  1. BP13F205                                            
      *                  2. BP13F816                                            
      *                                                                         
      *     OUTPUT FILE:                                                        
      *                  1. P13F816A                                            
      *                                                                         
      *----------------------------------------------------------------         
      * REF NO    DATE      BY   AMENDMENTS/ENHANCEMENTS                        
      * -------   --------  ---  ------------------------                       
      * BP133434  30/10/08  JB8  NEW PROGRAM.                                   
      * BP133580  28/11/08  JB8  CATER FOR EMPTY NT                             
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F205  ASSIGN        TO BP13F205.                         
           SELECT  BP13F816  ASSIGN        TO BP13F816.                         
           SELECT  P13F816A  ASSIGN        TO P13F816A.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F205                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 80 CHARACTERS            
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F816                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 400 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13K816.                                                           
                                                                                
       FD   P13F816A                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 400 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       01  P13F816A-REC                PIC X(150).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  WORK-FILE-VARIABLE.                                                  
           05  WS-EOF-BP13F816         PIC X     VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-BP13F816-READ         PIC 9(7)  VALUE ZEROES.                  
           05 WS-P13F816A-WRITEN       PIC 9(7)  VALUE ZEROES.                  
                                                                                
       01  WS-CURR-DATE.                                                        
           05  WS-CURR-CCYY            PIC X(4)  VALUE SPACES.                  
           05  WS-CURR-MM              PIC X(2)  VALUE SPACES.                  
           05  WS-CURR-DD              PIC X(2)  VALUE SPACES.                  
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F205      THRU 2000-EXIT.                      
           PERFORM 2100-READ-BP13F816      THRU 2100-EXIT                       
                                                                                
           PERFORM 3000-PROCESS-BP13F816   THRU 3000-EXIT                       
                   UNTIL WS-EOF-BP13F816   = 'Y'.                               
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F205                                                 
                       BP13F816                                                 
                OUTPUT P13F816A.                                                
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2000-READ-BP13F205.                                                      
      ******************************************************************        
                                                                                
           READ BP13F205           AT END                                       
                GO                 TO 9999-CLOSE-ROUTINE.                       
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2100-READ-BP13F816.                                                      
      ******************************************************************        
                                                                                
           READ BP13F816           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-BP13F816                         
                GO                 TO   2100-EXIT.                              
                                                                                
           ADD  1                  TO   WS-BP13F816-READ.                       
                                                                                
       2100-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3000-PROCESS-BP13F816.                                                   
      ******************************************************************        
                                                                                
           IF F205-NUM-NT-ZONE = SPACES OR LOW-VALUES                           
              PERFORM 5000-PROCESS-BLANK-NT      THRU 5000-EXIT                 
           ELSE                                                                 
              IF K816-CDE-NT = F205-NUM-NT-ZONE                                 
                 PERFORM 4000-WRITE-P13F816A     THRU 4000-EXIT                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2100-READ-BP13F816  THRU 2100-EXIT.                          
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
      *********************                                                     
       4000-WRITE-P13F816A.                                                     
      *********************                                                     
                                                                                
           WRITE P13F816A-REC          FROM BP13K816-REC.                       
           ADD 1                         TO WS-P13F816A-WRITEN.                 
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-PROCESS-BLANK-NT.                                                   
      ******************************************************************        
                                                                                
           MOVE '###'                    TO K816-NUM-ZONE                       
           WRITE P13F816A-REC          FROM BP13K816-REC                        
           ADD 1                         TO WS-P13F816A-WRITEN.                 
           GO TO 9999-CLOSE-ROUTINE.                                            
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CY18'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13F816 RECORDS READ..........: '                    
                    WS-BP13F816-READ.                                           
           DISPLAY 'NO OF P13F816A RECORDS WRITTEN.......: '                    
                    WS-P13F816A-WRITEN.                                         
                                                                                
           CLOSE BP13F205                                                       
                 BP13F816                                                       
                 P13F816A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
