      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
                                                                                
       PROGRAM-ID.    BP13C12C.                                                 
       AUTHOR.        TAN M K.                                                  
       DATE-WRITTEN.  06/10/89.                                                 
       DATE-COMPILED.                                                           
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  THIS IS A PROGRAM TO CHECK TOTAL CREDIT       *                
      *          PAYMENT TYPES CD, CON, GST, LSE                                
      *          WITH DEBIT PAYMENT TYPE SOC. IF NOT TALLY     *                
      *          PRINT REPORT.                                 *                
      *                                                        *                
      *    SIMILAR PROGRAM : BP13C126                          *                
      *                                                        *                
      *    INPUT FILES:                                        *                
      *      1.  BP13F120                                      *                
      *                                                        *                
      *    LISTING:                                            *                
      *      1.  CONTROL LISTING    (BP13L12C)                 *                
      *                                                        *                
      * CHG-NO   BY  ON        DESCRIPTION                     *                
      * -------  --- --------  --------------------------      *                
      * BP135602 JGO105/01/15  NEW PROGRAM COPY FROM BP13C126  *                
      **********************************************************                
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F120  ASSIGN       TO BP13F120.                           
           SELECT SY02F001  ASSIGN       TO SY02F001.                           
           SELECT BP13L12C  ASSIGN       TO BP13L12C.                           
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F120                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13F120.                                                           
                                                                        00630000
       FD  BP13L12C                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS.                                      
       01  L12C-PRINT-REC              PIC X(132).                              
                                                                                
                                                                                
       COPY SY02F001.                                                           
                                                                                
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *                                                                         
       01  WS1-COUNTERS.                                                        
           05  WS1-CNT-F120-READ       PIC 9(5)     VALUE ZEROS.                
           05  WS1-CNT-PRINT-L12C      PIC 9(5)     VALUE ZEROS.                
           05  WS1-CNT-LINE-ON-L12C    PIC 9(2)     VALUE 66.                   
           05  WS1-CNT-L12C-PAGE       PIC 9(5)     VALUE ZEROS.                
      *                                                                         
      *                                                                         
       01  WS2-SWITCHES-N-RANGE.                                                
           05  WS2-EOF-IND             PIC X(01)    VALUE SPACES.               
               88  WS2-EOF                          VALUE 'Y'.                  
           05  SY02F001-EOF            PIC X(01)    VALUE 'N'.                  
           05  WS2-FIRST-READ          PIC X(01)    VALUE 'Y'.                  
      *                                                                         
       01  WS3-DATES-VARIABLES.                                                 
           05  WS-DATE                 PIC X(08)    VALUE SPACES.               
           05  WS3-CURRENT-DATE.                                                
               10  WS3-CUR-CC          PIC 99.                                  
               10  WS3-CUR-YY          PIC 99.                                  
               10  WS3-CUR-MM          PIC 99.                                  
               10  WS3-CUR-DD          PIC 99.                                  
           05  WS3-PRT-DATE.                                                    
               10  WS3-PRT-DD          PIC XX.                                  
               10  FILLER              PIC X        VALUE '/'.                  
               10  WS3-PRT-MM          PIC XX.                                  
               10  FILLER              PIC X        VALUE '/'.                  
               10  WS3-PRT-CC          PIC XX.                                  
               10  WS3-PRT-YY          PIC XX.                                  
           05  WS3-TRAN-DATE.                                                   
               10  WS3-TRAN-CCYY.                                               
                   15  WS3-TRAN-CC     PIC X(2).                                
                   15  WS3-TRAN-YY     PIC X(2).                                
               10  WS3-TRAN-MM         PIC X(2).                                
               10  WS3-TRAN-DD         PIC X(2).                                
           05  WS3-CCYY                PIC X(4).                                
           05  WS3-CCYY-NUM  REDEFINES   WS3-CCYY PIC 9(4).                     
                                                                                
       01  WS4-DEFAULT-VALUES.                                                  
           05  WS4-MAX-LINES           PIC 9(2)     VALUE  60.                  
           05  WS4-L12C-RPT-ID         PIC X(14)    VALUE 'BP13L12C'.           
                                                                                
       01  WS5-SCHEME-ACCT.                                                     
           05  WS5-SCH-ACC.                                                     
               10  WS5-SCH-1-4         PIC X(4)     VALUE SPACES.               
               10  WS5-SCH-5-8         PIC X(4)     VALUE SPACES.               
               10  WS5-SCH-9-9         PIC X(1)     VALUE SPACES.               
               10  WS5-SCH-10-11       PIC X(2)     VALUE SPACES.               
                                                                                
           05  WS5-SCH-ACC-2.                                                   
               10  WS5-SCH-1-4         PIC X(4)     VALUE SPACES.               
               10  FILLER              PIC X(01)    VALUE '-'.                  
               10  WS5-SCH-5-8         PIC X(4)     VALUE SPACES.               
               10  FILLER              PIC X(01)    VALUE '-'.                  
               10  WS5-SCH-9-9         PIC X(1)     VALUE SPACES.               
               10  FILLER              PIC X(01)    VALUE '-'.                  
               10  WS5-SCH-10-11       PIC X(2)     VALUE SPACES.               
                                                                                
       01  WS6-NEW-VARIABLES.                                                   
           05  WS-HOLD-REGN-NUM        PIC X(08)    VALUE SPACES.               
           05  WS-HOLD-DR-AMT          PIC S9(09)V99 VALUE ZEROES.              
           05  WS-HOLD-CR-AMT          PIC S9(09)V99 VALUE ZEROES.              
           05  WS-DIFFERENCE           PIC S9(09)V99 VALUE ZEROES.              
           05  WS-SERIAL-NUM           PIC 9(05)    VALUE ZEROES.               
                                                                                
                                                                                
                                                                                
      **********************************************************                
      *         REPORT LAY-OUT  FOR L12C                       *                
      **********************************************************                
                                                                                
      *========================================================*                
      *           FORMAT  FOR  HEADING  OF L12C                *                
      *========================================================*                
       01  L12C-PRT-HEAD-1.                                                     
           05  L12C-HD1-RPT-ID         PIC X(14)    VALUE 'BP13L12C'.           
           05  FILLER                  PIC X(8)     VALUE 'HDBCAT 3'.           
           05  FILLER                  PIC X(18)    VALUE SPACES.               
           05  FILLER                  PIC X(39)    VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                  PIC X(18)    VALUE SPACES.               
           05  FILLER                  PIC X(7)     VALUE 'DATE: '.             
           05  L12C-HD1-DATE           PIC X(10)    VALUE SPACES.               
           05  FILLER                  PIC X(1)     VALUE SPACES.               
           05  FILLER                  PIC X(7)     VALUE 'PAGE: '.             
           05  L12C-HD1-PAGE-NO        PIC ZZZZ9.                               
                                                                                
       01  L12C-PRT-HEAD-2.                                                     
           05  FILLER                  PIC X(38)    VALUE SPACES.               
           05  FILLER                  PIC X(46)    VALUE                       
               'SOC - REPORT FOR CASES WITH DR/CR NOT TALLY   '.                
                                                                                
                                                                                
       01  L12C-PRT-HEAD-3.                                                     
           05  FILLER                  PIC X(05)    VALUE SPACES.               
           05  FILLER                  PIC X(05)    VALUE 'S/NO'.               
           05  FILLER                  PIC X(02)    VALUE SPACES.               
           05  FILLER                  PIC X(08)    VALUE 'REGN NO'.            
           05  FILLER                  PIC X(05)    VALUE SPACES.               
           05  FILLER                  PIC X(12)  VALUE 'SCHEME-AC-NO'.         
           05  FILLER                  PIC X(09)    VALUE SPACES.               
           05  FILLER                  PIC X(16)    VALUE                       
                                                    '    DR AMOUNT'.            
           05  FILLER                  PIC X(05)    VALUE SPACES.               
           05  FILLER                  PIC X(16)    VALUE                       
                                                    '    CR AMOUNT'.            
           05  FILLER                  PIC X(07)    VALUE SPACES.               
           05  FILLER                  PIC X(16)    VALUE                       
                                                    'DIFF (DR-CR) '.            
                                                                                
      *========================================================*                
      *           FORMAT  FOR DETAIL LINE                      *                
      *========================================================*                
       01  L12C-PRT-DETAIL-1.                                                   
           05  FILLER                  PIC X(05)    VALUE SPACES.               
           05  L12C-SNUM               PIC ZZZZ9    VALUE ZEROES.               
           05  FILLER                  PIC X(02)    VALUE SPACES.               
           05  L12C-REGN               PIC X(08)    VALUE SPACES.               
           05  FILLER                  PIC X(04)    VALUE SPACES.               
           05  L12C-SCH-ACC            PIC X(14)    VALUE SPACES.               
           05  FILLER                  PIC X(07)    VALUE SPACES.               
           05  L12C-DR-AMOUNT          PIC ZZZ,ZZZ,ZZ9.99.                      
           05  FILLER                  PIC X(07)    VALUE SPACES.               
           05  L12C-CR-AMOUNT          PIC ZZZ,ZZZ,ZZ9.99.                      
           05  FILLER                  PIC X(07)    VALUE SPACES.               
           05  L12C-DIFFERENCE         PIC ZZZ,ZZZ,ZZ9.99.                      
                                                                                
                                                                                
      **********************************************************                
       PROCEDURE DIVISION.                                                      
      **********************************************************                
                                                                                
      *===========================*                                             
       MAIN-ROUTINE SECTION.                                                    
      *====================*                                                    
            PERFORM 100-OPEN-FILES     THRU  100-EXIT.                          
            PERFORM 200-MOVE-PRT-L12C  THRU  200-EXIT  UNTIL WS2-EOF.           
            PERFORM 900-CLOSE-ROUTINE  THRU  900-EXIT.                          
                                                                                
       MAIN-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *===========================*                                             
       100-OPEN-FILES.                                                          
      *========================*                                                
           OPEN INPUT  BP13F120                                                 
                       SY02F001                                                 
                OUTPUT BP13L12C.                                                
      *                                                                         
           READ SY02F001 AT END MOVE 'Y' TO SY02F001-EOF.                       
           IF SY02F001-EOF = 'Y'                                                
              DISPLAY 'RECORD NOT FOUND IN SY02F001.'                           
              DISPLAY 'PROCESSING TERMINATED.'                                  
              PERFORM 900-CLOSE-ROUTINE                                         
           ELSE                                                                 
              STRING F001-CURR-MM '/'                                           
                     F001-CURR-CC                                               
                     F001-CURR-YY DELIMITED BY SIZE                             
                     INTO WS-DATE                                               
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *===========================*                                             
       150-READ-BP13F120.                                                       
      *===========================*                                             
      **************************************************************            
      *                                                            *            
      *   1. READ FILE BP13F120.                                   *            
      *   2. PROCESS NECESSARY DETAILS FOR PRINTING.               *            
      *   3. CALL ROUTINE TO PRINT REPORT.                         *            
      *                                                            *            
      **************************************************************            
           READ BP13F120                                                        
                         AT END                                                 
                         MOVE 'Y' TO WS2-EOF-IND                                
                         GO TO 150-EXIT.                                        
                                                                                
           ADD  1 TO WS1-CNT-F120-READ.                                         
                                                                                
      *                                                                         
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *===========================*                                             
       200-MOVE-PRT-L12C.                                                       
      *===========================*                                             
      **********************************************************                
      *                                                        *                
      *   MOVE NECESSARY DETAILS TO REPORT AND PRINT           *                
      *                                                        *                
      **********************************************************                
                                                                                
           PERFORM 150-READ-BP13F120   THRU 150-EXIT.                           
                                                                                
           IF F120-NUM-REGN NOT = WS-HOLD-REGN-NUM                              
              AND WS2-FIRST-READ NOT = 'Y'                                      
                PERFORM 300-PRINT   THRU  300-EXIT                              
           END-IF.                                                              
                                                                                
           MOVE 'N'                    TO  WS2-FIRST-READ.                      
           MOVE F120-NUM-REGN          TO  WS-HOLD-REGN-NUM                     
           MOVE F120-DTE-TRANS         TO  WS3-TRAN-DATE.                       
           MOVE WS3-TRAN-CC            TO  WS3-PRT-CC.                          
           MOVE WS3-TRAN-YY            TO  WS3-PRT-YY.                          
           MOVE WS3-TRAN-MM            TO  WS3-PRT-MM.                          
           MOVE WS3-TRAN-DD            TO  WS3-PRT-DD.                          
           MOVE SPACES                 TO  L12C-PRINT-REC.                      
           MOVE F120-NUM-REGN          TO  L12C-REGN.                           
           MOVE F120-NUM-SCH-ACCT      TO  WS5-SCH-ACC.                         
           MOVE CORR WS5-SCH-ACC       TO  WS5-SCH-ACC-2.                       
           MOVE WS5-SCH-ACC-2          TO  L12C-SCH-ACC.                        
           IF   F120-CDE-PAYMENT-TYPE = 'SOC'                                   
                COMPUTE F120-AMT-RECEIPT = F120-AMT-RECEIPT * -1                
           END-IF.                                                              
           IF   F120-AMT-RECEIPT > 0                                            
                ADD F120-AMT-RECEIPT   TO  WS-HOLD-CR-AMT                       
           ELSE                                                                 
                ADD F120-AMT-RECEIPT   TO  WS-HOLD-DR-AMT                       
           END-IF.                                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *                                                                         
       300-PRINT.                                                               
      *===================*                                                     
      ******************************************************************        
      *                                                                *        
      *       PRINT DETAIL LINE FOR REPORT L12C                        *        
      *                                                                *        
      ******************************************************************        
           COMPUTE WS-DIFFERENCE = WS-HOLD-DR-AMT + WS-HOLD-CR-AMT.             
                                                                                
           IF WS-DIFFERENCE = ZEROES                                            
              MOVE ZEROES             TO    WS-HOLD-CR-AMT                      
              MOVE ZEROES             TO    WS-HOLD-DR-AMT                      
              GO TO 300-EXIT                                                    
           END-IF.                                                              
                                                                                
           MOVE WS4-L12C-RPT-ID    TO    L12C-HD1-RPT-ID.               04980000
      *                                                                         
           IF  WS1-CNT-LINE-ON-L12C > WS4-MAX-LINES                     05320000
               PERFORM 400-PRINT-RPT-HEADING   THRU 400-EXIT            05330000
               MOVE 1  TO WS1-CNT-LINE-ON-L12C                          05340000
           END-IF.                                                              
      *                                                                         
           ADD 1                   TO    WS-SERIAL-NUM.                         
           MOVE WS-SERIAL-NUM      TO    L12C-SNUM.                             
           MOVE WS-HOLD-DR-AMT     TO    L12C-DR-AMOUNT.                        
           MOVE WS-HOLD-CR-AMT     TO    L12C-CR-AMOUNT.                        
           MOVE WS-DIFFERENCE      TO    L12C-DIFFERENCE.                       
           ADD 1                   TO    WS1-CNT-PRINT-L12C                     
                                         WS1-CNT-LINE-ON-L12C.                  
           WRITE L12C-PRINT-REC    FROM  L12C-PRT-DETAIL-1 AFTER 1.             
           MOVE SPACES             TO    L12C-PRINT-REC.                        
      *                                                                         
           ADD 1                   TO    WS1-CNT-LINE-ON-L12C.                  
           MOVE ZEROES             TO    WS-HOLD-CR-AMT.                        
           MOVE ZEROES             TO    WS-HOLD-DR-AMT.                        
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
      *                                                                         
                                                                                
      *================================*                                05680000
       400-PRINT-RPT-HEADING.                                           05670000
      *================================*                                05680000
      ****************************************************************  05690000
      *                                                              *  05700000
      * PRINT HEADING EITHER FOR L12C                                *  05710000
      *                                                              *  05720000
      ****************************************************************  05730000
           MOVE FUNCTION CURRENT-DATE TO WS3-CURRENT-DATE.                      
           MOVE WS3-CUR-DD            TO    WS3-PRT-DD.                 05760000
           MOVE WS3-CUR-MM            TO    WS3-PRT-MM.                 05770000
           MOVE WS3-CUR-CC            TO    WS3-PRT-CC.                 05780000
           MOVE WS3-CUR-YY            TO    WS3-PRT-YY.                 05780000
           MOVE WS3-PRT-DATE          TO    L12C-HD1-DATE                       
      *                                                                 05800000
           MOVE  SPACES            TO    L12C-PRINT-REC.                20001   
           ADD   1                 TO    WS1-CNT-L12C-PAGE.             40000   
           MOVE  WS1-CNT-L12C-PAGE TO    L12C-HD1-PAGE-NO.              50000   
           WRITE L12C-PRINT-REC    FROM  L12C-PRT-HEAD-1 AFTER PAGE.    40001   
                                                                                
           WRITE L12C-PRINT-REC FROM  L12C-PRT-HEAD-2  AFTER 2.         50001   
                                                                                
           WRITE L12C-PRINT-REC    FROM  L12C-PRT-HEAD-3 AFTER 2.       81001   
           MOVE  SPACES            TO    L12C-PRINT-REC                 90001   
                                         L12C-HD1-RPT-ID.               00001   
           WRITE L12C-PRINT-REC    AFTER 2.                             10000   
                                                                        06040000
       400-EXIT.                                                        06050000
           EXIT.                                                        06060000
                                                                                
                                                                                
      *===========================*                                             
       900-CLOSE-ROUTINE SECTION.                                               
      *===========================*                                             
      ****************************************************************          
      *                                                              *          
      *            CLOSE FILES AND END PROCESSING                    *          
      *                                                              *          
      ****************************************************************          
           DISPLAY SPACES.                                                      
           DISPLAY ' ** BP13C12C ** '.                                          
           DISPLAY 'NO OF RECORDS READ FROM FILE (BP13F120)  = '                
                    WS1-CNT-F120-READ.                                          
           DISPLAY 'NO OF RECORDS PRINTED (BP13L12C) = '                        
                    WS1-CNT-PRINT-L12C.                                         
                                                                                
           CLOSE BP13F120                                                       
                 SY02F001                                                       
                 BP13L12C.                                                      
                                                                                
           IF WS-SERIAL-NUM > 0                                                 
              MOVE 99 TO RETURN-CODE                                            
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
