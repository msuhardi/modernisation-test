      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C49I.                                                 
       AUTHOR.        KARL ANDRUS MAGALONA CABUG.                               
       DATE-WRITTEN.  JANUARY 28, 2020.                                         
      *===============================================================*         
      * OBJECTIVE : TO UPDATE EMAIL PCD FILE (BP13K491)               *         
      *     TO UPDATE EMAIL PCD FOR ELIGIBLE CASES RETURNED BY LEAS   *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :  1. BP27F386 - BP27.BP13.F386.SOC.DA             *         
      * OUTPUT FILE:  1. BP13K491  - BP13.K491.EMAIL.PCD              *         
      *                              FOR SENDING OF EMAIL             *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP138014 28/02/2020 KAC1 NEW PROGRAM                          *         
      *===============================================================*         
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP27F386  ASSIGN        TO BP27F386.                         
                                                                                
           SELECT  BP13K491  ASSIGN        TO BP13K491                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K491-NUM-REGN                     
                             FILE STATUS   IS WS-K491-STATUS.                   
                                                                                
           SELECT  BP13K800  ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
                                                                                
           SELECT  BP13K813  ASSIGN        TO BP13K813                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K813-KEY-FLD                      
                             FILE STATUS   IS WS-K813-STATUS.                   
                                                                                
           SELECT  BP13K493  ASSIGN        TO BP13K493                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K493-NUM-REGN                     
                             FILE STATUS   IS WS-K493-STATUS.                   
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP27F386                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 300 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP27F386.                                                           
                                                                                
       FD   BP13K491                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K491.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K493                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K493.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K491-STATUS          PIC X(2)  VALUE ZEROES.                  
           05  WS-K493-STATUS          PIC X(2)  VALUE ZEROES.                  
           05  WS-K800-STATUS          PIC X(2)  VALUE ZEROES.                  
           05  WS-K813-STATUS          PIC X(2)  VALUE ZEROES.                  
           05  WS-F386-EOF             PIC X(1)  VALUE 'N'.                     
           05  WS-K491-FND             PIC X(1)  VALUE 'N'.                     
           05  WS-K813-FND             PIC X(1)  VALUE 'N'.                     
           05  WS-K800-FND             PIC X(1)  VALUE 'N'.                     
           05  WS-K493-FND             PIC X(1)  VALUE 'N'.                     
           05  WS-CURR-DATE            PIC X(8)  VALUE ZEROES.                  
           05  WS-NUM-AHG              PIC X(1)  VALUE SPACES.                  
           05  WS-NUM-EHG              PIC X(1)  VALUE SPACES.                  
           05  WS-NUM-AHG-SHG          PIC X(1)  VALUE SPACES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F386-CTR             PIC 9(7)  VALUE ZEROES.                  
           05  WS-K491-CTR             PIC 9(7)  VALUE ZEROES.                  
           05  WS-K493-CTR             PIC 9(7)  VALUE ZEROES.                  
           05  WS-K800-CTR             PIC 9(7)  VALUE ZEROES.                  
           05  WS-K491-NF-CTR          PIC 9(7)  VALUE ZEROES.                  
           05  WS-K800-NF-CTR          PIC 9(7)  VALUE ZEROES.                  
           05  WS-K813-NF-CTR          PIC 9(7)  VALUE ZEROES.                  
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP27F386      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-F386-EOF = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP27F386                                                 
                       BP13K813                                                 
                I-O    BP13K800                                                 
                       BP13K491                                                 
                       BP13K493.                                                
                                                                                
                                                                                
            IF WS-K491-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPEN FAILED FOR BP13K491,STATUS ' WS-K491-STATUS        
               MOVE WS-K491-STATUS              TO RETURN-CODE                  
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
            END-IF.                                                             
                                                                                
            IF WS-K800-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPEN FAILED FOR BP13K800,STATUS ' WS-K800-STATUS        
               MOVE WS-K800-STATUS              TO RETURN-CODE                  
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
            END-IF.                                                             
                                                                                
            IF WS-K813-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPEN FAILED FOR BP13K813,STATUS ' WS-K813-STATUS        
               MOVE WS-K813-STATUS              TO RETURN-CODE                  
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
            END-IF.                                                             
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP27F386.                                                      
      ******************************************************************        
           READ BP27F386           AT   END                                     
                MOVE 'Y'           TO   WS-F386-EOF                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F386-CTR.                              
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURR-DATE.                     
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
           MOVE 'N'                             TO WS-K491-FND.                 
           MOVE SPACES                          TO BP13K491-REC                 
                                                   WS-NUM-AHG                   
                                                   WS-NUM-AHG-SHG               
                                                   WS-NUM-EHG.                  
                                                                                
           MOVE F386-NUM-REGN                   TO K491-NUM-REGN.               
           PERFORM 3100-READ-BP13K491         THRU 3100-EXIT.                   
                                                                                
           IF WS-K491-FND = 'Y'                                                 
              MOVE F386-NUM-HLE                 TO K491-NUM-HLA                 
              MOVE WS-CURR-DATE                 TO K491-DTE-UPDATE              
              PERFORM 3200-REWRITE-BP13K491   THRU 3200-EXIT                    
           END-IF.                                                              
                                                                                
           MOVE 'N'                             TO WS-K800-FND.                 
           MOVE SPACES                          TO BP13K800-MASTER.             
           INITIALIZE                              BP13K800-MASTER.             
                                                                                
           MOVE F386-NUM-REGN                   TO K800-NUM-REGN                
           PERFORM 3300-READ-BP13K800         THRU 3300-EXIT.                   
                                                                                
           IF WS-K800-FND = 'Y'                                                 
              MOVE 'N'                          TO WS-K813-FND                  
              MOVE SPACES                       TO BP13K813-REC                 
              INITIALIZE                           BP13K813-REC                 
                                                                                
              IF K800-NUM-ALLO-CAT = 'BTO'                                      
                 MOVE K800-NUM-BTO-ZONE         TO K813-NUM-ZONE                
                 MOVE K800-NUM-BTO-FLAT-TYPE    TO K813-NUM-FLAT-TYPE           
              ELSE                                                              
                 MOVE K800-NUM-NEW-TOWN         TO K813-NUM-ZONE                
                 MOVE K800-NUM-FLAT-TYPE        TO K813-NUM-FLAT-TYPE           
              END-IF                                                            
                                                                                
              MOVE K800-DTE-BALLOT              TO K813-DTE-BALLOT              
                                                                                
              PERFORM 3400-READ-BP13K813      THRU 3400-EXIT                    
              IF WS-K813-FND = 'Y'                                              
                IF K813-NUM-MATURE-EST-TAG = 'Y'                                
                   IF K800-DTE-REGN < 20190911                                  
                      MOVE 'Y'                  TO WS-NUM-AHG                   
                   ELSE                                                         
                     IF K800-DTE-REGN >= 20190911                               
                        MOVE 'Y'                TO WS-NUM-EHG                   
                     END-IF                                                     
                   END-IF                                                       
                ELSE                                                            
                  IF K800-NUM-ROOM-TYPE = '5' OR 'E'                            
                     IF K800-DTE-REGN < 20190911                                
                        MOVE 'Y'                TO WS-NUM-AHG                   
                     ELSE                                                       
                       IF K800-DTE-REGN >= 20190911                             
                          MOVE 'Y'              TO WS-NUM-EHG                   
                       END-IF                                                   
                     END-IF                                                     
                  ELSE                                                          
                    IF K800-DTE-REGN < 20190911                                 
                       MOVE 'Y'                 TO WS-NUM-AHG-SHG               
                    ELSE                                                        
                      IF K800-DTE-REGN >= 20190911                              
                         MOVE 'Y'               TO WS-NUM-EHG                   
                      END-IF                                                    
                    END-IF                                                      
                  END-IF                                                        
                END-IF                                                          
                                                                                
                IF (WS-NUM-AHG NOT = SPACES AND LOW-VALUES) OR                  
                   (WS-NUM-EHG NOT = SPACES AND LOW-VALUES) OR                  
                   (WS-NUM-AHG-SHG NOT = SPACES AND LOW-VALUES)                 
                    PERFORM 4000-UPDT-K493-K800 THRU 4000-EXIT                  
                END-IF                                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP27F386           THRU 2000-EXIT.                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-READ-BP13K491.                                                      
      ******************************************************************        
           READ BP13K491.                                                       
                                                                                
           EVALUATE WS-K491-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                    TO WS-K491-FND                  
               WHEN 23                                                          
                    ADD 1                       TO WS-K491-NF-CTR               
                    DISPLAY 'BP13K491 - REC NOTFND, REGN: '                     
                             K491-NUM-REGN                                      
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K491 : ' WS-K491-STATUS          
                            ' REGN = ' K491-NUM-REGN                            
                    PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                    
           END-EVALUATE.                                                        
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3200-REWRITE-BP13K491.                                                   
      ******************************************************************        
           REWRITE BP13K491-REC.                                                
                                                                                
           EVALUATE WS-K491-STATUS                                              
               WHEN 00                                                          
                    ADD 1                       TO WS-K491-CTR                  
               WHEN OTHER                                                       
                    DISPLAY 'BP13K491 - ERROR REWRITE: ' WS-K491-STATUS         
                            ' REGN = ' K491-NUM-REGN                            
                    PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3300-READ-BP13K800.                                                      
      ******************************************************************        
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                    TO WS-K800-FND                  
               WHEN 23                                                          
                    ADD 1                       TO WS-K800-NF-CTR               
                    DISPLAY 'BP13K800 - REC NOTFND, REGN: '                     
                             K800-NUM-REGN                                      
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K800 : ' WS-K800-STATUS          
                            ' REGN = ' K800-NUM-REGN                            
                    PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                    
           END-EVALUATE.                                                        
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3400-READ-BP13K813.                                                      
      ******************************************************************        
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                    TO WS-K813-FND                  
               WHEN 23                                                          
                    ADD 1                       TO WS-K813-NF-CTR               
                    DISPLAY 'BP13K813 - REC NOTFND, KEY: ' K813-KEY-FLD         
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K813 : ' WS-K813-STATUS          
                            ' REGN = ' K813-KEY-FLD                             
                    PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                    
           END-EVALUATE.                                                        
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-UPDT-K493-K800.                                                     
      ******************************************************************        
           MOVE 'N'                           TO WS-K493-FND.                   
           MOVE SPACES                        TO BP13K493-REC                   
           MOVE F386-NUM-REGN                 TO K493-NUM-REGN.                 
           PERFORM 4100-READ-BP13K493       THRU 4100-EXIT.                     
                                                                                
           IF WS-K493-FND = 'Y'                                                 
              MOVE WS-NUM-AHG                 TO K493-NUM-AHG                   
              MOVE WS-NUM-AHG-SHG             TO K493-NUM-AHG-SHG               
              MOVE WS-NUM-EHG                 TO K493-NUM-EHG                   
              MOVE F386-NUM-REJECT            TO K493-NUM-REJECT                
              MOVE F386-NUM-REJECT-REASON     TO K493-NUM-REJECT-REASON         
              MOVE FUNCTION CURRENT-DATE(1:8) TO K493-DTE-UPDATE                
                                                                                
              PERFORM 4200-REWRITE-BP1K493  THRU 4200-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE 'P'                           TO K800-NUM-LOAN-TAG.             
           MOVE F386-NUM-HLE                  TO K800-NUM-HLA.                  
           PERFORM 4300-REWRITE-BP13K800    THRU 4300-EXIT.                     
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4100-READ-BP13K493.                                                      
      ******************************************************************        
           READ BP13K493.                                                       
           EVALUATE WS-K493-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                   TO WS-K493-FND                   
               WHEN 23                                                          
                    MOVE 'N'                   TO WS-K493-FND                   
                    DISPLAY 'BP13K493 - REC NOFND, KEY : ' F386-NUM-REGN        
               WHEN OTHER                                                       
                    DISPLAY 'BP13K493 - READ ERROR   : ' WS-K493-STATUS         
                            ' REGN = ' K493-NUM-REGN                            
                    PERFORM 9900-HANDLE-ABEND THRU 9900-EXIT                    
           END-EVALUATE.                                                        
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4200-REWRITE-BP1K493.                                                    
      ******************************************************************        
           REWRITE BP13K493-REC.                                                
                                                                                
           EVALUATE WS-K493-STATUS                                              
               WHEN 00                                                          
                    ADD 1                       TO WS-K493-CTR                  
               WHEN OTHER                                                       
                 DISPLAY 'BP13K493 - READ ERROR, STAT: ' WS-K493-STATUS         
                 MOVE WS-K493-STATUS            TO RETURN-CODE                  
                 PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4300-REWRITE-BP13K800.                                                   
      ******************************************************************        
           REWRITE BP13K800-MASTER.                                             
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    ADD 1                       TO WS-K800-CTR                  
               WHEN OTHER                                                       
                 DISPLAY 'BP13K800 - READ ERROR, KEY: ' K800-NUM-REGN           
                 MOVE WS-K800-STATUS            TO RETURN-CODE                  
                 PERFORM 9900-HANDLE-ABEND    THRU 9900-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9900-HANDLE-ABEND.                                                       
      ******************************************************************        
                                                                                
           DISPLAY '************* CONTROL ABEND TOTALS  *************'.         
           DISPLAY 'PROGRAM-ID : BP13C49I      STAT: ABEND'.                    
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF INPUT(F386) RECORDS READ..... : '                 
                    WS-F386-CTR.                                                
           DISPLAY '(2) NO OF BP13K491 RECORDS WRITTEN..... : '                 
                    WS-K491-CTR.                                                
                                                                                
           CLOSE    BP27F386                                                    
                    BP13K491.                                                   
                                                                                
           IF WS-K491-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE FAILED FOR BP13K491,STAT: ' WS-K491-STATUS         
              MOVE WS-K491-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
       9900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C49I'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF INPUT(F386) RECORDS READ..... : '                 
                    WS-F386-CTR.                                                
           DISPLAY '(2) NO OF BP13K491 RECORDS WRITTEN..... : '                 
                    WS-K491-CTR.                                                
           DISPLAY '(2) NO OF BP13K493 RECORDS UPDATED..... : '                 
                    WS-K493-CTR.                                                
           DISPLAY '(2) NO OF BP13K800 RECORDS UPDATED..... : '                 
                    WS-K800-CTR.                                                
           DISPLAY '(3) NO OF BP13K491 RECORDS NOTFND...... : '                 
                    WS-K491-NF-CTR.                                             
           DISPLAY '(4) NO OF BP13K800 RECORDS NOTFND...... : '                 
                    WS-K800-NF-CTR.                                             
                                                                                
           CLOSE    BP27F386                                                    
                    BP13K800                                                    
                    BP13K813                                                    
                    BP13K493                                                    
                    BP13K491.                                                   
                                                                                
           IF WS-K491-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE FAILED FOR BP13K491,STAT: ' WS-K491-STATUS         
              MOVE WS-K491-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE FAILED FOR BP13K800,STAT: ' WS-K800-STATUS         
              MOVE WS-K800-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE FAILED FOR BP13K813,STAT: ' WS-K813-STATUS         
              MOVE WS-K813-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
           IF WS-K493-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSE FAILED FOR BP13K493,STAT: ' WS-K493-STATUS         
              MOVE WS-K493-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
