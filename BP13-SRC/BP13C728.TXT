       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C728.                                                 
      *AUTHOR.        LSB.                                                      
      *DATE-WRITTEN.  30/09/91.                                                 
      *============================================================*            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      *============================================================*            
      *  OBJECTIVE :                                               *            
      *    1.  ASSIGN SERIES NO TO THE 28TH BYTE OF THE RANDOM     *            
      *        NUMBER FOR JB GROUP.                                *            
      *                                                            *            
      *  INPUT FILES:  1. BP13F500                                 *            
      *                                                            *            
      *  OUTPUT FILE : 1. BP13F510                                 *            
      *                                                            *            
      *------------------------------------------------------------*            
      * BP130340 19971115 SSS  F500-NUM-RANDOM-CHAR27              *            
      *                        NUM-SEQ TO NUM-RANDOM(1:28)         *            
      * BP130652 19990219 NOR  EXPAND F500 & F510 RECL 300 TO 500  *            
      * BP132317 20030312 ROM1 TO CHANGE COLUMN POSITION FROM      *            
      *                        (1:28) TO (28:1) IN UPDATING THE    *            
      *                        28TH BYTE OF THE RANDOM NUMBER.     *            
      *============================================================*            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F500 ASSIGN TO BP13F500.                                  
                                                                                
           SELECT BP13F510 ASSIGN TO BP13F510.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F500                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F500.                                                           
                                                                                
       FD   BP13F510                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F510.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-COUNTERS.                                                         
           05  WS-MIN-RANDOM           PIC X(27)   VALUE SPACES.                
           05  WS-JB-CNT               PIC 9(1)    VALUE ZEROS.                 
           05  WS-CNT-F500-READ        PIC 9(5)    VALUE ZEROS.                 
           05  WS-JB-PAIR              PIC 9(5)    VALUE ZEROS.                 
           05  WS-CNT-F510-WRITE       PIC 9(5)    VALUE ZEROS.                 
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F500-STATUS          PIC X       VALUE 'N'.                   
               88  WS-F500-EOF                     VALUE 'Y'.                   
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM 100-OPEN-FILES        THRU 100-EXIT.                         
           PERFORM 200-PROCESS-BP13F500  THRU 200-EXIT                          
              UNTIL (WS-F500-EOF).                                              
           PERFORM 900-CLOSE-ROUTINE     THRU 900-EXIT.                         
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F500                                                 
                OUTPUT BP13F510.                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-PROCESS-BP13F500.                                                    
      *-------------------------------------------------------------            
           READ BP13F500                                                        
                AT END                                                          
                   MOVE 'Y' TO  WS-F500-STATUS                                  
                   GO TO 200-EXIT.                                              
                                                                                
           ADD 1 TO WS-CNT-F500-READ.                                           
           IF  F500-NUM-RANDOM(1:27)  NOT = WS-MIN-RANDOM                       
               MOVE ZEROS          TO  WS-JB-CNT                                
               MOVE F500-NUM-RANDOM(1:27)  TO WS-MIN-RANDOM                     
               PERFORM 300-WRITE-OUTPUT-REC THRU 300-EXIT                       
               ADD 1 TO WS-JB-PAIR                                              
           ELSE                                                                 
               PERFORM 300-WRITE-OUTPUT-REC THRU 300-EXIT.                      
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-WRITE-OUTPUT-REC.                                                    
      *-------------------------------------------------------------            
           MOVE BP13F500-REC   TO  BP13F510-REC.                                
           ADD 1               TO  WS-JB-CNT.                                   
           MOVE WS-JB-CNT      TO  F510-NUM-RANDOM(28:1).                       
           WRITE BP13F510-REC.                                                  
           ADD     1           TO  WS-CNT-F510-WRITE.                           
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
           CLOSE BP13F500                                                       
                 BP13F510.                                                      
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*----------- BP13C728 ------------* '.                      
           DISPLAY '  '.                                                        
           DISPLAY                                                              
           'TOTAL NO. OF JB  READ     : '  WS-CNT-F500-READ.                    
           DISPLAY                                                              
           'TOTAL NO. OF JB  WRITTEN  : '  WS-CNT-F510-WRITE.                   
           DISPLAY                                                              
           'TOTAL NO. OF JB  PAIR     : '  WS-JB-PAIR.                          
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
