       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CZ2J.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  02/10/2015.                                               
      *========================================================*                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1.  UPDATE BP13K800 ELDERLY-PREMIUM FIELD WITH Y/N*                
      *          CREATE REPORTS ON TAGGED CASES                *                
      *          CONDITION:                                    *                
      *          ELDERLY-PREMIUM=N                             *                
      *        A)FIRST TIMER FAMILY AND FIRST TIMER SINGLE WITH*                
      *        B)SECOND TIMER NEED TO PAY RESALE PREMIUM/LEVY  *                
      *          OTHERS ELDERLY-PREMIUM=Y                      *                
      *                                                        *                
      *   P13CZ2JA - ELDERLY PREMIUM = Y                       *                
      *   P13CZ2JB - ELDERLY PREMIUM = N                       *                
      *--------------------------------------------------------*                
      * CHG-NO    BY  DATE    DESCRIPTION                      *                
      * -------  ---  ------  -----------                      *                
      * BP135921 FNP1 021015  NEW PROGRAM                      *                
      * BP136096 FNP1 050216  MODIFY CONDITIONS IN EVALUATING  *                
      *                       ELDERLY PREMIUM TAG              *                
      * BP139173 EL27 061022  INCOME CHECK FROM 6000 TO 7000   *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F740   ASSIGN       TO BP13F740.                          
                                                                                
           SELECT BP13K800   ASSIGN       TO BP13K800                           
                             ACCESS MODE  IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K800-NUM-REGN                      
                             FILE STATUS  IS BP13K800-STATUS.                   
                                                                                
                                                                                
           SELECT P13LZ2JA  ASSIGN       TO P13LZ2JA.                           
           SELECT P13LZ2JB ASSIGN        TO P13LZ2JB.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F740                                                            
            RECORD CONTAINS  500 CHARACTERS                                     
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS ARE  STANDARD                                        
            RECORDING MODE IS F.                                                
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   P13LZ2JA                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL  RECORDS ARE  OMITTED.                                        
                                                                                
       01   P13LZ2JA-REC           PIC X(132).                                  
                                                                                
       FD   P13LZ2JB                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL  RECORDS ARE  OMITTED.                                        
                                                                                
       01   P13LZ2JB-REC            PIC X(132).                                 
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-F740-READ             PIC 9(8)    VALUE 0.                        
       01  WS-OUTPUT-CNT            PIC 9(8)    VALUE 0.                        
       01  WS-MATCHED-CNT           PIC 9(8)    VALUE 0.                        
       01  K800-FND-CNT             PIC 9(8)    VALUE 0.                        
       01  K800-NFND-CNT            PIC 9(8)    VALUE 0.                        
       01  WS-K800-UPD              PIC 9(8)    VALUE 0.                        
       01  WS-FILE-STATUS           PIC X(03)   VALUE SPACES.                   
       01  WS-TOTAL                 PIC 9(5)    VALUE 0.                        
       01  WS-TOTAL-A               PIC 9(5)    VALUE 0.                        
       01  WS-LZ2J-PAGENO           PIC 9(5)    VALUE 0.                        
       01  WS-LZ2J-PAGENO-A         PIC 9(5)    VALUE 0.                        
       01  BP13K800-STATUS          PIC 99      VALUE ZEROES.                   
       01  WS-ELDERLY-PREMIUM       PIC X(1)    VALUE 'Y'.                      
       01  WS-K800-FOUND            PIC X(1)    VALUE 'N'.                      
                                                                                
       01  EOF-BP13F740            PIC X        VALUE 'N'.                      
                                                                                
       01  WS-DATE.                                                             
           05  WS-YY               PIC X(2).                                    
           05  WS-MM               PIC X(2).                                    
           05  WS-DD               PIC X(2).                                    
                                                                                
       01  WS-CC                   PIC 99.                                      
                                                                                
                                                                                
       01  LZ2J-HEAD01.                                                         
           05  FILLER              PIC X(8)      VALUE 'BP13LZ2J'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'S Y S T E M   O F   '.                     
           05  FILLER              PIC X(19)     VALUE                          
                                    'C O M M I T M E N T'.                      
           05  FILLER              PIC X(32)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  LZ2J-DATE           PIC X(10).                                   
           05  FILLER              PIC X(2)      VALUE SPACES.                  
                                                                                
       01  LZ2J-HEAD02.                                                         
           05  FILLER              PIC X(32)     VALUE SPACES.                  
           05  FILLER              PIC X(83)     VALUE                          
              '           REPORT ON CASES WITH ELDERLY PREMIUM = Y'.            
           05  FILLER              PIC X(07)     VALUE 'PAGE :'.                
           05  LZ2J-PAGENO         PIC ZZZZ9.                                   
                                                                                
       01  LZ2J-HEAD02A.                                                        
           05  FILLER              PIC X(32)     VALUE SPACES.                  
           05  FILLER              PIC X(83)     VALUE                          
              '           REPORT ON CASES WITH ELDERLY PREMIUM = N'.            
           05  FILLER              PIC X(07)     VALUE 'PAGE :'.                
           05  LZ2J-PAGENO-A       PIC ZZZZ9.                                   
                                                                                
       01  LZ2J-HEAD03.                                                         
           05  FILLER              PIC X(09)     VALUE ' S/N'.                  
           05  FILLER              PIC X(10)     VALUE '  REG NO'.              
           05  FILLER              PIC X(11)     VALUE '  NRIC'.                
           05  FILLER              PIC X(04)     VALUE 'NT'.                    
           05  FILLER              PIC X(04)     VALUE ' FT'.                   
           05  FILLER              PIC X(06)     VALUE ' AS'.                   
           05  FILLER              PIC X(04)     VALUE 'ES'.                    
           05  FILLER              PIC X(03)     VALUE 'HH'.                    
           05  FILLER              PIC X(04)     VALUE 'BHH'.                   
           05  FILLER              PIC X(06)     VALUE 'FT2T'.                  
           05  FILLER              PIC X(08)     VALUE 'NTIMER'.                
           05  FILLER              PIC X(09)     VALUE 'ELDERLY'.               
           05  FILLER              PIC X(05)     VALUE 'PPO'.                   
           05  FILLER              PIC X(11)     VALUE 'HH-INCOME'.             
           05  FILLER              PIC X(08)     VALUE 'EXEMPT'.                
           05  FILLER              PIC X(06)     VALUE 'PREM'.                  
           05  FILLER              PIC X(06)     VALUE 'LEVY'.                  
           05  FILLER              PIC X(12)     VALUE 'ELDER-PREM'.            
                                                                                
       01  LZ2J-DETAIL.                                                         
           05  FILLER                PIC X(01)     VALUE SPACES.                
           05  LZ2J-TOTAL            PIC X(07)     VALUE ZEROES.                
           05  FILLER                PIC X(01)     VALUE SPACES.                
           05  LZ2J-NUM-REGN         PIC X(8)      VALUE SPACES.                
           05  FILLER                PIC X(2)      VALUE SPACES.                
           05  LZ2J-NUM-NRIC1        PIC X(9)      VALUE SPACES.                
           05  FILLER                PIC X(2)      VALUE SPACES.                
           05  LZ2J-NUM-NT           PIC X(3)      VALUE SPACES.                
           05  FILLER                PIC X(2)      VALUE SPACES.                
           05  LZ2J-NUM-FT           PIC X(2)      VALUE SPACES.                
           05  FILLER                PIC X(2)      VALUE SPACES.                
           05  LZ2J-NUM-AS           PIC X(03)     VALUE SPACES.                
           05  FILLER                PIC X(02)     VALUE SPACES.                
           05  LZ2J-NUM-ES           PIC X(3)      VALUE SPACES.                
           05  FILLER                PIC X(02)     VALUE SPACES.                
           05  LZ2J-NUM-HH           PIC X(1)      VALUE SPACES.                
           05  FILLER                PIC X(02)     VALUE SPACES.                
           05  LZ2J-NUM-BHH          PIC X(01)     VALUE SPACES.                
           05  FILLER                PIC X(04)     VALUE SPACES.                
           05  LZ2J-NUM-FT2T         PIC X(01)     VALUE SPACES.                
           05  FILLER                PIC X(07)     VALUE SPACES.                
           05  LZ2J-NTIMER           PIC X(01)     VALUE SPACES.                
           05  FILLER                PIC X(06)     VALUE SPACES.                
           05  LZ2J-ELDERLY          PIC X(01)     VALUE SPACES.                
           05  FILLER                PIC X(06)     VALUE SPACES.                
           05  LZ2J-NUM-PPO          PIC X(01)     VALUE SPACES.                
           05  FILLER                PIC X(02)     VALUE SPACES.                
           05  LZ2J-HH-INCOME        PIC ZZZZ9.                                 
           05  FILLER                PIC X(07)     VALUE SPACES.                
           05  LZ2J-INC-EXEMPT       PIC X(01)     VALUE SPACES.                
           05  FILLER                PIC X(08)     VALUE SPACES.                
           05  LZ2J-RSL-PREM         PIC X(03)     VALUE SPACES.                
           05  FILLER                PIC X(03)     VALUE SPACES.                
           05  LZ2J-RSL-LEVY         PIC X(03)     VALUE SPACES.                
           05  FILLER                PIC X(06)     VALUE SPACES.                
           05  LZ2J-ELDER-PREM       PIC X(01)     VALUE SPACES.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------*                
       000-MAIN.                                                                
      *--------------------------------------------------------*                
           OPEN INPUT  BP13F740                                                 
                I-O    BP13K800                                                 
                OUTPUT P13LZ2JA                                                 
                       P13LZ2JB.                                                
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING BP13K800 (' BP13K800-STATUS ')'            
              MOVE BP13K800-STATUS           TO RETURN-CODE                     
              GO TO 900-CLOSE-ROUTINE                                           
           END-IF.                                                              
                                                                                
           PERFORM 100-READ-F740-REC   THRU 100-EXIT.                           
           PERFORM 150-WRITE-HEADER    THRU 150-EXIT.                           
           PERFORM 170-WRITE-HEADER-A  THRU 170-EXIT.                           
           PERFORM 200-PROCESS-REC     THRU 200-EXIT                            
                   UNTIL EOF-BP13F740 = 'Y'.                                    
           PERFORM 900-CLOSE-ROUTINE.                                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       100-READ-F740-REC.                                                       
      *--------------------------------------------------------*                
                                                                                
           READ BP13F740 AT END MOVE 'Y' TO EOF-BP13F740                        
                MOVE HIGH-VALUE TO F740-NUM-REGN                                
                GO TO 100-EXIT.                                                 
           ADD 1 TO WS-F740-READ.                                               
                                                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       150-WRITE-HEADER.                                                        
      *--------------------------------------------------------*                
                                                                                
           ACCEPT WS-DATE FROM DATE.                                            
           IF WS-YY > '90'                                                      
              MOVE '19' TO WS-CC                                                
           ELSE                                                                 
              MOVE '20' TO WS-CC.                                               
           STRING WS-DD '/' WS-MM '/' WS-CC WS-YY                               
                DELIMITED BY SIZE INTO LZ2J-DATE.                               
                                                                                
           MOVE 1 TO WS-LZ2J-PAGENO.                                            
           MOVE WS-LZ2J-PAGENO TO LZ2J-PAGENO.                                  
           WRITE P13LZ2JA-REC FROM LZ2J-HEAD01 AFTER PAGE.                      
           WRITE P13LZ2JA-REC FROM LZ2J-HEAD02 AFTER 1.                         
           WRITE P13LZ2JA-REC FROM LZ2J-HEAD03 AFTER 3.                         
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       170-WRITE-HEADER-A.                                                      
      *--------------------------------------------------------*                
                                                                                
           ACCEPT WS-DATE FROM DATE.                                            
           IF WS-YY > '90'                                                      
              MOVE '19' TO WS-CC                                                
           ELSE                                                                 
              MOVE '20' TO WS-CC.                                               
           STRING WS-DD '/' WS-MM '/' WS-CC WS-YY                               
                DELIMITED BY SIZE INTO LZ2J-DATE.                               
                                                                                
           MOVE 1 TO WS-LZ2J-PAGENO-A.                                          
           MOVE WS-LZ2J-PAGENO-A TO LZ2J-PAGENO-A.                              
           WRITE P13LZ2JB-REC FROM LZ2J-HEAD01 AFTER PAGE.                      
           WRITE P13LZ2JB-REC FROM LZ2J-HEAD02A AFTER 1.                        
           WRITE P13LZ2JB-REC FROM LZ2J-HEAD03 AFTER 3.                         
                                                                                
       170-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       200-PROCESS-REC.                                                         
      *--------------------------------------------------------*                
                                                                                
           MOVE 'N'            TO WS-ELDERLY-PREMIUM.                           
                                                                                
           IF F740-NUM-ELDERLY = 'F' OR 'S'                                     
              PERFORM 400-READ-BP13K800    THRU 400-EXIT                        
              PERFORM 300-PROCESS-RECORD   THRU 300-EXIT                        
              IF WS-K800-FOUND = 'Y'                                            
                 MOVE WS-ELDERLY-PREMIUM TO K800-NUM-ELDERLY-PREMIUM            
                 PERFORM 410-REWRITE-BP13K800   THRU 410-EXIT                   
                 PERFORM 800-WRITE-REPORT       THRU 800-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 100-READ-F740-REC          THRU 100-EXIT.                    
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       300-PROCESS-RECORD.                                                      
      *--------------------------------------------------------*                
                                                                                
           IF F740-AMT-HSE-INCOME IS NOT NUMERIC                                
              MOVE ZEROES   TO F740-AMT-HSE-INCOME                              
           END-IF.                                                              
                                                                                
           IF F740-NUM-NTIMER = 'Y' OR                                          
              (F740-CDE-HOUSEHOLD = 'G' AND F740-NUM-ELDERLY = 'S')             
              MOVE 'Z'  TO WS-ELDERLY-PREMIUM                                   
              GO TO 300-EXIT                                                    
           END-IF.                                                              
                                                                                
           IF (F740-CDE-HOUSEHOLD = 'H' OR 'T') OR                              
              (F740-NUM-FT2T = 'Y')                                             
              IF F740-NUM-PPO = 'A' OR 'B' OR 'C'                               
                 MOVE 'Y' TO WS-ELDERLY-PREMIUM                                 
              ELSE                                                              
                 IF F740-NUM-ELDERLY = 'S' AND                                  
                    F740-AMT-HSE-INCOME > 7000                                  
                    MOVE 'Y' TO WS-ELDERLY-PREMIUM                              
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              IF F740-CDE-HOUSEHOLD = 'G'                                       
                 IF F740-NUM-PPO = 'A' OR 'B' OR 'C'                            
                    MOVE 'Z' TO WS-ELDERLY-PREMIUM                              
                 ELSE                                                           
                    IF K800-NUM-PREM-CODE NOT = SPACES OR                       
                       K800-NUM-LVY-RSL-STATUS NOT = SPACES                     
                       MOVE 'N' TO WS-ELDERLY-PREMIUM                           
                    ELSE                                                        
                       IF F740-AMT-HSE-INCOME > 7000                            
                          MOVE 'Z' TO WS-ELDERLY-PREMIUM                        
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
      *--------------------------------------------------------*                
       400-READ-BP13K800.                                                       
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES                TO K800-NUM-REGN.                         
           MOVE F740-NUM-REGN         TO K800-NUM-REGN.                         
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE BP13K800-STATUS                                             
              WHEN 00                                                           
                 MOVE 'Y'    TO WS-K800-FOUND                                   
                 ADD 1 TO K800-FND-CNT                                          
             WHEN 23                                                            
                 MOVE 'N'    TO WS-K800-FOUND                                   
                 ADD 1 TO K800-NFND-CNT                                         
                 DISPLAY 'RECORD NOT FOUND IN BP13K800: ' K800-NUM-REGN         
             WHEN OTHER                                                         
                 DISPLAY 'ERROR WITH READ OF BP13K800. STATUS IS '              
                      BP13K800-STATUS                                           
                 MOVE BP13K800-STATUS              TO RETURN-CODE               
                 PERFORM 900-CLOSE-ROUTINE        THRU 900-EXIT                 
             END-EVALUATE.                                                      
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       410-REWRITE-BP13K800.                                                    
      *--------------------------------------------------------*                
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           EVALUATE BP13K800-STATUS                                             
              WHEN 00                                                           
                 ADD 1 TO WS-K800-UPD                                           
              WHEN OTHER                                                        
                 DISPLAY 'ERROR WITH UPDATING OF BP13K800. STATUS IS '          
                     BP13K800-STATUS                                            
                 MOVE BP13K800-STATUS             TO RETURN-CODE                
                 PERFORM 900-CLOSE-ROUTINE        THRU 900-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       800-WRITE-REPORT.                                                        
      *--------------------------------------------------------*                
                                                                                
           INITIALIZE                         P13LZ2JA-REC                      
                                              P13LZ2JB-REC                      
                                              LZ2J-DETAIL.                      
                                                                                
           IF WS-ELDERLY-PREMIUM = 'Y'                                          
              ADD   1                        TO WS-TOTAL                        
              MOVE  WS-TOTAL                 TO LZ2J-TOTAL                      
           ELSE                                                                 
              ADD   1                     TO WS-TOTAL-A                         
              MOVE  WS-TOTAL-A            TO LZ2J-TOTAL                         
           END-IF.                                                              
                                                                                
           MOVE F740-NUM-REGN                TO LZ2J-NUM-REGN                   
           MOVE F740-NUM-NRIC1               TO LZ2J-NUM-NRIC1                  
           MOVE F740-CDE-NT1                 TO LZ2J-NUM-NT                     
           MOVE F740-CDE-FLAT-TYPE           TO LZ2J-NUM-FT                     
           MOVE F740-CDE-ALLOC-SCH           TO LZ2J-NUM-AS                     
           MOVE F740-CDE-ELIG-SCH            TO LZ2J-NUM-ES                     
           MOVE F740-CDE-HOUSEHOLD           TO LZ2J-NUM-HH                     
           MOVE F740-CDE-BALLOT-HOUSEHOLD    TO LZ2J-NUM-BHH                    
           MOVE F740-NUM-FT2T                TO LZ2J-NUM-FT2T                   
           MOVE F740-NUM-NTIMER              TO LZ2J-NTIMER                     
           MOVE F740-NUM-ELDERLY             TO LZ2J-ELDERLY                    
           MOVE F740-NUM-PPO                 TO LZ2J-NUM-PPO                    
           MOVE F740-AMT-HSE-INCOME          TO LZ2J-HH-INCOME                  
           MOVE F740-NUM-INCOME-EXEMPTION    TO LZ2J-INC-EXEMPT                 
           MOVE K800-NUM-PREM-CODE           TO LZ2J-RSL-PREM                   
           MOVE K800-NUM-LVY-RSL-STATUS      TO LZ2J-RSL-LEVY                   
           MOVE WS-ELDERLY-PREMIUM           TO LZ2J-ELDER-PREM                 
                                                                                
           IF WS-ELDERLY-PREMIUM = 'Y'                                          
              WRITE P13LZ2JA-REC              FROM LZ2J-DETAIL                  
              MOVE SPACES                     TO P13LZ2JA-REC                   
              INITIALIZE                         P13LZ2JA-REC                   
           ELSE                                                                 
              WRITE P13LZ2JB-REC              FROM LZ2J-DETAIL                  
              MOVE SPACES                     TO P13LZ2JB-REC                   
              INITIALIZE                         P13LZ2JB-REC                   
           END-IF.                                                              
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       900-CLOSE-ROUTINE.                                                       
      *--------------------------------------------------------*                
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'BP13CZ2J                 CURR-DATE : ' WS-DATE.             
           DISPLAY 'REPORT ON ELDERLY PREMIUM'.                                 
           DISPLAY '*-----------------------------------------------*'.         
           DISPLAY 'NO OF RECORDS READ FROM F740    ' WS-F740-READ.             
           DISPLAY 'NO OF RECORDS FND IN BP13K800  ' K800-FND-CNT.              
           DISPLAY 'NO OF RECORDS NFND IN BP13K800 ' K800-NFND-CNT.             
           DISPLAY 'NO OF RECORDS UPD  IN BP13K800 ' WS-K800-UPD.               
           DISPLAY 'NO OF LZ2JA RECORDS WRITTEN     ' WS-TOTAL.                 
           DISPLAY 'NO OF LZ2JB RECORDS WRITTEN     ' WS-TOTAL-A.               
                                                                                
           CLOSE BP13K800                                                       
                 BP13F740                                                       
                 P13LZ2JA                                                       
                 P13LZ2JB.                                                      
                                                                                
           IF BP13K800-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR CLOSING BP13K800 (' BP13K800-STATUS ')'            
              MOVE BP13K800-STATUS           TO RETURN-CODE                     
              GO TO 900-CLOSE-ROUTINE                                           
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
