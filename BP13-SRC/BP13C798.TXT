       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C798.                                                 
       AUTHOR.        RACHEL G. MENDOZA.                                        
      *DATE-WRITTEN.  22/12/94.                                                 
      *REMARKS.                                                                 
      *****************************************************************         
      *                                                               *         
      *   SYSTEM NAME : SYSTEM OF COMMITMENT                          *         
      *                                                               *         
      *   OBJECTIVE  1: THIS PROGRAM TO SEARCH PRIORITY TABLE TO      *         
      *                 CONVERT ALLOCATION SCHEME TO PRIORITY SCHEME. *         
      *                                                               *         
      *              2: TO CONVERT CAT4 TO CAT3 FOR SORTING PURPOSE.  *         
      *                                                               *         
      *   INPUT FILES :                                               *         
      *   1.  BP13F500                                                *         
      *                                                               *         
      *   OUTPUT FILES:                                               *         
      *   1.  BP13F520                                                *         
      *                                                               *         
      *   AMENDMENTS:                                                 *         
      *   REF      DATE      BY    DESCRIPTIONS                       *         
      *   -------  --------  ----  ------------                       *         
      *   BP130652 19021999  NOR   EXPAND F500 & F520 RECL 300 TO 500 *         
      *   BP132496 22042004  CKK   TO MOVE 'H' TO HHTY = 'T'          *         
      *   BP133432 20012009  IL5   TO USE BALLOT HOUSEHOLD            *         
      *   BP135028 31082013  IL5   MOVE 'H' FOR HHTY = 'F'            *         
      *   BP136259 27052016  SMR2  MOVE 'G' FOR HHTY = 'N'            *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F500 ASSIGN TO BP13F500.                                  
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD BP13F500                                                              
            BLOCK     CONTAINS  0   RECORDS                                     
            RECORD    CONTAINS  500 CHARACTERS                                  
            LABEL     RECORD    IS  STANDARD                                    
            RECORDING MODE      IS  F.                                          
       COPY BP13F500.                                                           
                                                                                
       FD BP13F520                                                              
            BLOCK     CONTAINS  0   RECORDS                                     
            RECORD    CONTAINS  500 CHARACTERS                                  
            LABEL     RECORD    IS  STANDARD                                    
            RECORDING MODE      IS  F.                                          
       COPY BP13F520.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       COPY PRIOSCH.                                                            
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-ALLOCSCH-TYPE     PIC X(3)    VALUE SPACES.                    
           05 WS-CNT-F500-READ     PIC 9(5)    VALUE ZERO.                      
           05 WS-CNT-F520-WRITE    PIC 9(5)    VALUE ZERO.                      
           05 WS-F500-STATUS       PIC X       VALUE 'N'.                       
              88 WS-F500-EOF                   VALUE 'Y'.                       
           05 WS-SEARCH-STATUS     PIC X       VALUE SPACES.                    
              88 WS-CODE-ERROR                 VALUE 'Y'.                       
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      **********************************************************                
       0000-MAIN-PROCESS.                                                       
      **********************************************************                
                                                                                
      **  OPENING FILES :                                                       
                                                                                
           OPEN INPUT  BP13F500                                                 
                OUTPUT BP13F520.                                                
                                                                                
           PERFORM 1000-READ-BP13F500                                           
              THRU 1000-EXIT.                                                   
           PERFORM 2000-PROCESS-BP13F500                                        
              THRU 2000-EXIT                                                    
             UNTIL WS-F500-EOF.                                                 
                                                                                
      **  CLOSING FILES :                                                       
                                                                                
           CLOSE BP13F500                                                       
                 BP13F520.                                                      
                                                                                
           DISPLAY ' ** BP13C798 ** '.                                          
           DISPLAY 'NO OF REC READ    FROM PREREGN  FILE (BP13F500) : '         
                   WS-CNT-F500-READ.                                            
           DISPLAY 'NO OF REC WRITTEN TO   BALLOT   FILE (BP13F520) : '         
                   WS-CNT-F520-WRITE.                                           
                                                                                
       0000-EXIT.                                                               
           STOP RUN.                                                            
                                                                                
                                                                                
      **********************************************************                
       1000-READ-BP13F500.                                                      
      **********************************************************                
                                                                                
           READ BP13F500 AT END                                                 
                MOVE 'Y' TO WS-F500-STATUS                                      
                GO TO 1000-EXIT.                                                
                                                                                
           ADD  1  TO WS-CNT-F500-READ.                                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
       2000-PROCESS-BP13F500.                                                   
      **********************************************************                
                                                                                
           PERFORM 3000-GET-PRIORITY-SCHEME                                     
              THRU 3000-EXIT.                                                   
           PERFORM 4000-WRITE-BP13F520                                          
              THRU 4000-EXIT.                                                   
           PERFORM 1000-READ-BP13F500                                           
              THRU 1000-EXIT.                                                   
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
       3000-GET-PRIORITY-SCHEME.                                                
      **********************************************************                
                                                                                
           MOVE SPACES       TO  WS-SEARCH-STATUS.                              
           SET  WS-ALLOC-PTR TO 1.                                              
                                                                                
           SEARCH WS-ALLOC-SCH                                                  
              AT END MOVE 'XXX' TO WS-ALLOCSCH-TYPE                             
              WHEN F500-CDE-ALLOC-SCH =                                         
                                WS-ALLOC-SCH-DESP(WS-ALLOC-PTR)                 
              MOVE WS-MAIN-SCH-CD(WS-ALLOC-PTR) TO                              
                                      WS-ALLOCSCH-TYPE.                         
                                                                                
           IF WS-ALLOCSCH-TYPE = 'XXX'                                          
              DISPLAY '***  INVALID ALLOC SCHEME FOR BP13F500 REC '             
                            F500-NUM-REF                                        
              DISPLAY '***  INVALID APPLN ALLOC SCHEME IS : '                   
                            F500-CDE-ALLOC-SCH                                  
              MOVE 'Y'    TO  WS-SEARCH-STATUS.                                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
       4000-WRITE-BP13F520.                                                     
      **********************************************************                
                                                                                
           IF NOT WS-CODE-ERROR                                                 
              MOVE SPACES         TO BP13F520-REC                               
              MOVE BP13F500-REC   TO BP13F520-REC                               
              MOVE WS-ALLOCSCH-TYPE TO F520-CDE-ALLOC-SCH                       
              IF F520-CDE-CATEGORY  = '4'                                       
                 MOVE '3' TO F520-CDE-CATEGORY                                  
              END-IF                                                            
              IF F520-CDE-BALLOT-HOUSEHOLD = 'T' OR 'F'                         
                 MOVE 'H' TO F520-CDE-BALLOT-HOUSEHOLD                          
              END-IF                                                            
              IF F520-CDE-BALLOT-HOUSEHOLD = 'N'                                
                 MOVE 'G' TO F520-CDE-BALLOT-HOUSEHOLD                          
              END-IF                                                            
              WRITE BP13F520-REC                                                
              ADD 1               TO WS-CNT-F520-WRITE.                         
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
