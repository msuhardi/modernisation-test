      *************************                                                 
      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CD40.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  25/04/08.                                                 
      *****************************************************************         
      *OBJECTIVE - THIS PROGRAM WILL TAKE IN FILE ON DBSS FLAT WHERE  *         
      *            DEVELOPER WILL KEY IN DATA RELATED TO TAKE         *         
      *            POSSESSION OF DBSS FLAT.                           *         
      *                                                               *         
      *            THIS PROGRAM WILL CREATE SALES ORDER FILE FOR SMS  *         
      *            BASS, AND OTHER DOWN STREAM SYSTEMS.               *         
      *                                                               *         
      *            DBSS FLAT NO   HOUSING GRANT SALES TYPE=14         *         
      *            DBSS FLAT WITH HOUSING GRANT SALES TYPE=15         *         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13FD15                                  *         
      *                  2. BP13K800                                  *         
      *                  3. BP13K820                                  *         
      *                  4. BP13KD00                                  *         
      *                  5. BP13KD35                                  *         
      *                  6. BP13KA10                                  *         
      *                                                               *         
      *     OUTPUT FILE:                                              *         
      *                  1. BP13F400                                  *         
      *                  2. BP13LD40 (REPORT FOR REC NOT FND)         *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * REF NO    DATE      BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------   --------  ---  ------------------------             *         
      * BP133298  07052008  JB8  NEW PROGRAM.                         *         
      * BP133505  08012009  JB8  GET OPTION FEE.                      *         
      * BP133638  22072009  JB8  TO CATER FOR DAILY JOB.              *         
      * BP133929  05012010  JB8  TO SAVE GST-RATE.                    *         
      * BP134641  07082012  MDT2 CHANGE THE LRECL OF BP13KD05 FROM 300*         
      *                          TO 500                               *         
      * BP135027  08102013  ESA1 ADD UPDATE OF KD15-NUM-SCH-ACC       *         
      * BP135339  17062014  ESA1 TO ADD READING OF BP13KD80 TO GET    *         
      *                          AMT-LOAN-QUANTUM                     *         
      * BP135339  05122014  ESA1 MOVE LOAN-QUANTUM FOR BANK LOAN CASES*         
      *                          ONLY                                 *         
      * BP135339  16022015  ZDD1 CHECK FOR KD00-NUM-GIRO              *         
      *           16022015  ESA1 MOVE ZERO TO F400-NUM-LOAN-REPYMT-MAX*         
      *                          F400-NUM-REPAY-PERIOD FOR LOAN='N'   *         
      * BP135696  21042015  ZDD1 REMOVE CHECKING OF KD00-NUM-GIRO-INFO*         
      * BP135816  02072015  ZDD1 REMOVE BANK-CDE                      *         
      * BP136911  20032017  MRN1 CALL BP13CB4A TO READ DB2 GRANT TABLE*         
      *                          AND REPLACE K800-NUM-AHG AND BP13KD05*         
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13FD15  ASSIGN        TO BP13FD15.                         
           SELECT  SY02F001  ASSIGN        TO SY02F001.                         
           SELECT  BP13LD40  ASSIGN        TO BP13LD40.                         
           SELECT  BP13F400  ASSIGN        TO BP13F400.                         
           SELECT  BP13K800  ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
           SELECT  BP13K820  ASSIGN        TO BP13K820                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K820-KEY-FLD                      
                             FILE STATUS   IS WS-K820-STATUS.                   
           SELECT  BP13KD00  ASSIGN        TO BP13KD00                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KD00-KEY-FLD                      
                             FILE STATUS   IS WS-KD00-STATUS.                   
           SELECT  BP13KD15  ASSIGN        TO BP13KD15                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KD15-KEY-FLD                      
                             FILE STATUS   IS WS-KD15-STATUS.                   
           SELECT  BP13KD35  ASSIGN        TO BP13KD35                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KD35-KEY-FLD                      
                             FILE STATUS   IS WS-KD35-STATUS.                   
           SELECT  BP13KD65  ASSIGN        TO BP13KD65                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KD65-CDE-FILEID                   
                             FILE STATUS   IS WS-KD65-STATUS.                   
           SELECT  BP13KA10  ASSIGN        TO BP13KA10                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KA10-KEY-FLD                      
                             FILE STATUS   IS WS-KA10-STATUS.                   
           SELECT  BP13KD80  ASSIGN        TO BP13KD80                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS KD80-KEY-FLD                      
                             FILE STATUS   IS WS-KD80-STATUS.                   
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13FD15                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 300 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13FD15.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13F400                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 2000 CHARACTERS          
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F400.                                                           
                                                                                
       FD   BP13K800                                                            
                                       RECORD CONTAINS 2000 CHARACTERS.         
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K820                                                            
                                       RECORD CONTAINS 400 CHARACTERS.          
       COPY BP13K820.                                                           
                                                                                
       FD   BP13KD00                                                            
                                       RECORD CONTAINS 1600 CHARACTERS.         
       COPY BP13KD00.                                                           
                                                                                
       FD   BP13KD15                                                            
                                       RECORD CONTAINS 300 CHARACTERS.          
       COPY BP13KD15.                                                           
                                                                                
       FD   BP13KD35                                                            
                                       RECORD CONTAINS 3500 CHARACTERS.         
       COPY BP13KD35.                                                           
                                                                                
       FD   BP13KD65                                                            
                                       RECORD CONTAINS 200 CHARACTERS.          
       COPY BP13KD65.                                                           
                                                                                
       FD   BP13KA10                                                            
                                       RECORD CONTAINS 800 CHARACTERS.          
       COPY BP13KA10.                                                           
                                                                                
       FD   BP13KD80                                                            
                                       RECORD CONTAINS 3500 CHARACTERS.         
       COPY BP13KD80.                                                           
                                                                                
       FD   BP13LD40                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 132 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       01   BP13LD40-REC               PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  VARIABLES.                                                           
           05  WS-DTE-CCYYMMDD.                                                 
               15 WS-CCYY              PIC X(04).                               
               15 WS-MM                PIC X(02).                               
               15 WS-DD                PIC X(02).                               
           05  WS-CCYYMMDD-NUM REDEFINES WS-DTE-CCYYMMDD.                       
               15 WS-CCYY-9            PIC 9(04).                               
               15 WS-MM-9              PIC 9(02).                               
               15 WS-DD-9              PIC 9(02).                               
           05  WS-SYSTEM-DATE-TIME.                                             
               15  WS-SYS-DATE         PIC X(08) VALUE SPACES.                  
               15  WS-SYS-TIME         PIC X(08) VALUE SPACES.                  
               15  FILLER              PIC X(05) VALUE SPACES.                  
           05  WS-RETCODE              PIC S9(8)                                
                                       SIGN IS LEADING SEPARATE.                
           05  WS-TMP-DATE             PIC 9(8).                                
           05  WS-HPI-WT               PIC 9(03).                               
           05  WS-CTR                  PIC 9(02).                               
           05  WS-TOTAL-CPF1           PIC 9(7)V99.                             
           05  WS-TOTAL-CPF2           PIC 9(7)V99.                             
           05  WS-TOTAL-CPF3           PIC 9(7)V99.                             
           05  WS-TOTAL-CPF4           PIC 9(7)V99.                             
           05  WS-SURVEY-PROCEED       PIC X(1)  VALUE 'N'.                     
           05  WS-CDE-RACE             PIC X(1)  VALUE SPACES.                  
           05  WS-NUM-SEX              PIC X(1)  VALUE SPACES.                  
           05  WS-AMT-OPTION           PIC 9(06)V99.                            
           05  WS-NUM-AHG              PIC X(3)  VALUE SPACES.                  
           05  WS-A                    PIC 9(02).                               
           05  WS-P                    PIC 9(02) VALUE ZEROES.                  
           05  WS-PRSN                 PIC 9(02) VALUE ZEROES.                  
           05  WS-STORED               PIC X(01) VALUE 'N'.                     
                                                                                
       01  WS-PRSN-DTLS.                                                        
           05  WS-PERSON OCCURS 4 TIMES.                                        
               10 WS-NRIC               PIC X(9)     VALUE SPACES.              
               10 WS-AMT-AHG-GRANT      PIC 9(06)V99 VALUE ZEROES.              
                                                                                
       01  WS-REPORT-VARIABLES.                                                 
           05  WS-REC-CNT              PIC 9(05) VALUE ZEROES.                  
           05  WS-LINE-CNT             PIC 9(05) VALUE ZEROES.                  
           05  WS-PAGENO               PIC 9(05) VALUE ZEROES.                  
           05  WS-FILENAME             PIC X(08) VALUE SPACES.                  
           05  WS-KEY-FLD              PIC X(20) VALUE SPACES.                  
           05  WS-KEY-VALUE            PIC X(30) VALUE SPACES.                  
                                                                                
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-FD15             PIC X(1)  VALUE 'N'.                     
           05  WS-FND-K800             PIC X(1)  VALUE 'N'.                     
           05  WS-FND-K820             PIC X(1)  VALUE 'N'.                     
           05  WS-FND-KD00             PIC X(1)  VALUE 'N'.                     
           05  WS-FND-KD15             PIC X(1)  VALUE 'N'.                     
           05  WS-FND-KD35             PIC X(1)  VALUE 'N'.                     
           05  WS-FND-KA10             PIC X(1)  VALUE 'N'.                     
           05  WS-FND-KD80             PIC X(1)  VALUE 'N'.                     
           05  WS-K800-STATUS          PIC 9(2)  VALUE ZEROES.                  
           05  WS-K820-STATUS          PIC 9(2)  VALUE ZEROES.                  
           05  WS-KD00-STATUS          PIC 9(2)  VALUE ZEROES.                  
           05  WS-KD15-STATUS          PIC 9(2)  VALUE ZEROES.                  
           05  WS-KD35-STATUS          PIC 9(2)  VALUE ZEROES.                  
           05  WS-KD65-STATUS          PIC 9(2)  VALUE ZEROES.                  
           05  WS-KA10-STATUS          PIC 9(2)  VALUE ZEROES.                  
           05  WS-KD80-STATUS          PIC 9(2)  VALUE ZEROES.                  
           05  WS-K030-STATUS          PIC 9(2)  VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FD15-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD15-UPDATE          PIC 9(7)  VALUE ZEROES.                  
           05  WS-F400-WRITE           PIC 9(7)  VALUE ZEROES.                  
           05  WS-K800-FOUND           PIC 9(7)  VALUE ZEROES.                  
           05  WS-K800-NOTFND          PIC 9(7)  VALUE ZEROES.                  
           05  WS-K820-FOUND           PIC 9(7)  VALUE ZEROES.                  
           05  WS-K820-NOTFND          PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD00-FOUND           PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD00-NOTFND          PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD35-FOUND           PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD35-NOTFND          PIC 9(7)  VALUE ZEROES.                  
           05  WS-KA10-FOUND           PIC 9(7)  VALUE ZEROES.                  
           05  WS-KA10-NOTFND          PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD80-FOUND           PIC 9(7)  VALUE ZEROES.                  
           05  WS-KD80-NOTFND          PIC 9(7)  VALUE ZEROES.                  
           05  WS-K030-FOUND           PIC 9(7)  VALUE ZEROES.                  
           05  WS-K030-NOTFND          PIC 9(7)  VALUE ZEROES.                  
           05  WS-BYPASS-SEND          PIC 9(7)  VALUE ZEROES.                  
           05  WS-BYPASS-TOP-DATE      PIC 9(7)  VALUE ZEROES.                  
           05  WS-BYPASS-NOTFND-KD00-K800 PIC 9(7)  VALUE ZEROES.               
                                                                                
      *-----------------------------------------------------------*             
      *    WS VARIABLES PASSED  TO SUB-PROGRAM "BP13C950"         *             
      *-----------------------------------------------------------*             
       01  C950-FIELD.                                                          
           05  C950-PROCESS-DATE       PIC X(08).                               
           05  C950-PROCESS-DATE-NUM REDEFINES C950-PROCESS-DATE.               
               10  C950-PROCESS-CC     PIC 99.                                  
               10  C950-PROCESS-YY     PIC 99.                                  
               10  C950-PROCESS-MM     PIC 99.                                  
               10  C950-PROCESS-DD     PIC 99.                                  
           05  C950-INTREST-RATE       PIC 9(02)V9(4).                          
           05  C950-FACT               PIC 9(02).                               
           05  C950-REPYMT-PERIOD      PIC 9(02).                               
           05  C950-CDE-LOAN-SCH       PIC X(01).                               
      *-------------------------------------------------------------            
      * BP13C913 PARAMETERS                                                     
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH-ACC.                                              
              10 WS-LINK-NUM-SCH       PIC X(4).                                
              10 WS-LINK-NUM-ACC       PIC X(5).                                
       COPY P13COMM8.                                                           
      *-----------------------------------------------------------*             
      *    WS VARIABLES PASSED  TO SUB-PROGRAM "BP13C903"         *             
      *-----------------------------------------------------------*             
       01  WS-LINK-S903.                                                        
           05  WS-S903-NUM-SCH-ACC         PIC X(9)     VALUE SPACES.           
           05  WS-S903-SO-EFF-DATE.                                             
               10  WS-S903-SO-EFF-CC       PIC 9(2)     VALUE ZEROES.           
               10  WS-S903-SO-EFF-YYMMDD   PIC 9(6)     VALUE ZEROES.           
           05  WS-S903-DESIGN-TY           PIC X(3)     VALUE SPACES.           
           05  WS-S903-ROOM-TY             PIC X(2)     VALUE SPACES.           
           05  WS-S903-SC-CC1              PIC 9(6)V99  VALUE ZEROES.           
           05  WS-S903-SC-CC2              PIC 9(6)V99  VALUE ZEROES.           
           05  WS-S903-SC-CC3              PIC 9(6)V99  VALUE ZEROES.           
           05  WS-S903-SC-TOTAL-AMT        PIC 9(7)V99  VALUE ZEROES.           
           05  WS-S903-SC-REBATE1          PIC 9(4)V99  VALUE ZEROES.           
           05  WS-S903-SC-REBATE2          PIC 9(4)V99  VALUE ZEROES.           
           05  WS-S903-SC-REBATE3          PIC 9(4)V99  VALUE ZEROES.           
           05  WS-S903-SC-GRANT1           PIC 9(4)V99  VALUE ZEROES.           
           05  WS-S903-SC-GRANT2           PIC 9(4)V99  VALUE ZEROES.           
           05  WS-S903-SC-GRANT3           PIC 9(4)V99  VALUE ZEROES.           
           05  WS-S903-SC-REC-TC1          PIC 9(4)V99  VALUE ZEROES.           
           05  WS-S903-SC-REC-TC2          PIC 9(4)V99  VALUE ZEROES.           
           05  WS-S903-SC-REC-TC3          PIC 9(4)V99  VALUE ZEROES.           
           05  WS-S903-SC-REB-SP1          PIC 9(4)V99  VALUE ZEROES.           
           05  WS-S903-SC-REB-SP2          PIC 9(4)V99  VALUE ZEROES.           
           05  WS-S903-SC-REB-SP3          PIC 9(4)V99  VALUE ZEROES.           
           05  WS-S903-SC-CURR-EFF-DATE    PIC X(10)    VALUE SPACES.           
           05  WS-S903-SC-NEXT-EFF-DATE    PIC X(10)    VALUE SPACES.           
           05  WS-S903-RET-CODE            PIC X(2)     VALUE SPACES.           
                                                                                
      *-----------------------------------------------------------*             
      * COMMAREA TO GET FI-INFO                                                 
      *-----------------------------------------------------------*             
       COPY AB20M97C.                                                           
                                                                                
      *-----------------------------------------------------------*             
      * LAYOUT FOR NOT FOUND REPORT                               *             
      *-----------------------------------------------------------*             
       01 LD40-HEADER1.                                                         
          05 FILLER                      PIC X(08) VALUE 'BP13LD40'.            
          05 FILLER                      PIC X(06) VALUE SPACES.                
          05 FILLER                      PIC X(04) VALUE 'HDB3'.                
          05 FILLER                      PIC X(38) VALUE SPACES.                
          05 FILLER                      PIC X(20) VALUE                        
                                         'SYSTEM OF COMMITMENT'.                
          05 FILLER                      PIC X(26) VALUE SPACES.                
          05 FILLER                      PIC X(07) VALUE 'DATE : '.             
          05 LD40-DATE                   PIC X(10).                             
          05 FILLER                      PIC X(02) VALUE SPACES.                
          05 FILLER                      PIC X(07) VALUE 'PAGE : '.             
          05 LD40-PAGE                   PIC Z(04).                             
                                                                                
       01 LD40-HEADER2.                                                         
          05 FILLER                      PIC X(34) VALUE SPACES.                
          05 FILLER                      PIC X(27) VALUE                        
             'REPORT OF RECORDS NOT FOUND'.                                     
          05 FILLER                      PIC X(37) VALUE                        
             ' WHEN CREATE SALES ORDER FILE FOR SMS'.                           
          05 FILLER                      PIC X(34) VALUE SPACES.                
                                                                                
       01 LD40-HEADER3.                                                         
          05 FILLER                      PIC X(05) VALUE SPACES.                
          05 FILLER                      PIC X(05) VALUE 'S/N'.                 
          05 FILLER                      PIC X(03) VALUE SPACES.                
          05 FILLER                      PIC X(08) VALUE 'FILENAME'.            
          05 FILLER                      PIC X(03) VALUE SPACES.                
          05 FILLER                      PIC X(20) VALUE 'KEY-FLD'.             
          05 FILLER                      PIC X(03) VALUE SPACES.                
          05 FILLER                      PIC X(30) VALUE 'KEY-VALUE'.           
          05 FILLER                      PIC X(55) VALUE SPACES.                
                                                                                
       01 LD40-HEADER4.                                                         
          05 FILLER                      PIC X(05) VALUE SPACES.                
          05 FILLER                      PIC X(05) VALUE ALL '-'.               
          05 FILLER                      PIC X(03) VALUE SPACES.                
          05 FILLER                      PIC X(08) VALUE ALL '-'.               
          05 FILLER                      PIC X(03) VALUE SPACES.                
          05 FILLER                      PIC X(20) VALUE ALL '-'.               
          05 FILLER                      PIC X(03) VALUE SPACES.                
          05 FILLER                      PIC X(30) VALUE ALL '-'.               
          05 FILLER                      PIC X(55) VALUE SPACES.                
                                                                                
       01 LD40-DETAIL.                                                          
          05 FILLER                      PIC X(05) VALUE SPACES.                
          05 LD40-SN                     PIC ZZZZ9 VALUE ZEROES.                
          05 FILLER                      PIC X(03) VALUE SPACES.                
          05 LD40-FILENAME               PIC X(08) VALUE SPACES.                
          05 FILLER                      PIC X(03) VALUE SPACES.                
          05 LD40-KEY-FLD                PIC X(20) VALUE SPACES.                
          05 FILLER                      PIC X(03) VALUE SPACES.                
          05 LD40-KEY-VALUE              PIC X(30) VALUE SPACES.                
          05 FILLER                      PIC X(55) VALUE SPACES.                
                                                                                
      *-------------------------------------------------------*                 
      *    COPY BOOK FOR SUBROUTINE - BP13CB4A.               *                 
      *-------------------------------------------------------*                 
       COPY P13COM73.                                                           
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13FD15      THRU 2000-EXIT.                      
                                                                                
           PERFORM 2050-PROCESS-RECORDS    THRU 2050-EXIT                       
             UNTIL WS-EOF-FD15 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13FD15                                                 
                       SY02F001                                                 
                       BP13K800                                                 
                       BP13K820                                                 
                       BP13KD00                                                 
                       BP13KD35                                                 
                       BP13KA10                                                 
                       BP13KD65                                                 
                       BP13KD80                                                 
                I-O    BP13KD15                                                 
                OUTPUT BP13F400                                                 
                       BP13LD40.                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : '                             
                      WS-K800-STATUS                                            
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 - ERROR OPENING : '                             
                      WS-K820-STATUS                                            
              MOVE WS-K820-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD00 - ERROR OPENING : '                             
                      WS-KD00-STATUS                                            
              MOVE WS-KD00-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF WS-KD35-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD35 - ERROR OPENING : '                             
                      WS-KD35-STATUS                                            
              MOVE WS-KD35-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF WS-KA10-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KA10 - ERROR OPENING : '                             
                      WS-KA10-STATUS                                            
              MOVE WS-KA10-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF WS-K030-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K030 - ERROR OPENING : '                             
                      WS-K030-STATUS                                            
              MOVE WS-K030-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF WS-KD65-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD65 - ERROR OPENING : '                             
                      WS-KD65-STATUS                                            
              MOVE WS-KD65-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           IF WS-KD80-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD80 - ERROR OPENING : '                             
                      WS-KD80-STATUS                                            
              MOVE WS-KD80-STATUS        TO RETURN-CODE                         
              GO TO 9999-CLOSE-ROUTINE                                          
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE-TIME.                   
                                                                                
           READ SY02F001.                                                       
                                                                                
           PERFORM 1001-READ-BP13KD65  THRU 1001-EXIT.                          
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1001-READ-BP13KD65.                                                      
      ******************************************************************        
                                                                                
           MOVE 'KD65'        TO KD65-CDE-FILEID.                               
                                                                                
           READ BP13KD65                                                        
           EVALUATE WS-KD00-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN 23                                                           
                 MOVE ZEROES  TO KD65-DTE-BAL-CLOSE                             
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN READING BP13KD65 - STATUS '                  
                             WS-KD65-STATUS                                     
                 MOVE        WS-KD65-STATUS    TO RETURN-CODE                   
                 GO TO 9999-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       1001-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13FD15.                                                      
      ******************************************************************        
                                                                                
           READ BP13FD15           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-FD15                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO   WS-FD15-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2050-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF FD15-DTE-SEND-SMS = SPACES OR LOW-VALUES OR ZEROES                
              IF KD65-DTE-BAL-CLOSE = F001-DTE-LAST-DAY-PREV(1:6) AND           
                 FD15-DTE-IND-TP <= WS-SYS-DATE AND                             
                 FD15-DTE-IND-TP > ZEROES                                       
                                                                                
               PERFORM 2100-READ-BP13K800             THRU 2100-EXIT            
               PERFORM 3300-CALL-BP13CB4A             THRU 3300-EXIT            
               PERFORM 2300-READ-BP13KD00             THRU 2300-EXIT            
                                                                                
               IF WS-FND-K800 = 'Y' AND WS-FND-KD00 = 'Y'                       
                  PERFORM 4000-INIT-BP13F400          THRU 4000-EXIT            
                  PERFORM 4100-MOVE-BP13K800          THRU 4100-EXIT            
                  PERFORM 4300-MOVE-BP13KD00          THRU 4300-EXIT            
                  PERFORM 2205-PROCESS-OCCUPIER       THRU 2205-EXIT            
                  IF K800-NUM-LOAN-TAG NOT = 'B' AND 'N'                        
                     PERFORM 2500-READ-BP13KD35       THRU 2500-EXIT            
                  END-IF                                                        
                  PERFORM 2700-PROCESS-S903           THRU 2700-EXIT            
                  PERFORM 4900-DETERMINE-REST         THRU 4900-EXIT            
                  PERFORM 5100-UPDATE-BP13KD15        THRU 5100-EXIT            
                  PERFORM 6000-GET-HPI-SCH            THRU 6000-EXIT            
                  PERFORM 6100-GET-FI-INFO            THRU 6100-EXIT            
                  PERFORM 5000-WRITE-BP13F400         THRU 5000-EXIT            
               ELSE                                                             
                  ADD 1   TO WS-BYPASS-NOTFND-KD00-K800                         
               END-IF                                                           
             ELSE                                                               
                 ADD 1 TO WS-BYPASS-TOP-DATE                                    
             END-IF                                                             
           ELSE                                                                 
              ADD 1 TO WS-BYPASS-SEND                                           
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FD15             THRU 2000-EXIT.               
                                                                                
       2050-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2100-READ-BP13K800.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES             TO BP13K800-MASTER.                          
           INITIALIZE                 BP13K800-MASTER.                          
           MOVE 'N'                      TO WS-FND-K800.                        
                                                                                
           MOVE FD15-NUM-REGN            TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800 KEY IS K800-NUM-REGN.                                  
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 IF K800-NUM-SCH-ACC NOT = SPACES                               
                    MOVE 'Y'                TO WS-FND-K800                      
                    ADD 1                   TO WS-K800-FOUND                    
                 END-IF                                                         
              WHEN 23                                                           
                 ADD 1                   TO WS-K800-NOTFND                      
                 MOVE SPACES TO WS-FILENAME WS-KEY-FLD WS-KEY-VALUE             
                 MOVE 'BP13K800'         TO WS-FILENAME                         
                 MOVE 'REGN'             TO WS-KEY-FLD                          
                 MOVE FD15-NUM-REGN      TO WS-KEY-VALUE                        
                 PERFORM 7000-WRITE-REPORT THRU 7000-EXIT                       
              WHEN OTHER                                                        
                 DISPLAY 'BP13K800 - ERROR READING : '                          
                         WS-K800-STATUS                                         
                 MOVE WS-K800-STATUS     TO RETURN-CODE                         
                 GO TO 9999-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2200-READ-BP13K820.                                                      
      ******************************************************************        
                                                                                
           MOVE 'N'                      TO WS-FND-K820.                        
                                                                                
           READ BP13K820 KEY IS K820-KEY-FLD.                                   
                                                                                
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 ADD 1                   TO WS-K820-FOUND                       
                 MOVE 'Y'                TO WS-FND-K820                         
              WHEN 23                                                           
                 ADD 1                   TO WS-K820-NOTFND                      
                 MOVE SPACES TO WS-FILENAME WS-KEY-FLD WS-KEY-VALUE             
                 MOVE 'BP13K820'         TO WS-FILENAME                         
                 MOVE 'REGN+NRIC'        TO WS-KEY-FLD                          
                 MOVE K820-KEY-FLD       TO WS-KEY-VALUE                        
                 PERFORM 7000-WRITE-REPORT THRU 7000-EXIT                       
              WHEN OTHER                                                        
                 DISPLAY 'BP13K820 - ERROR READING : '                          
                         WS-K820-STATUS                                         
                 MOVE WS-K820-STATUS     TO RETURN-CODE                         
                 GO TO 9999-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.  EXIT.                                                        
                                                                                
      ****************************************************************          
       2205-PROCESS-OCCUPIER.                                                   
      ****************************************************************          
                                                                                
           MOVE SPACES                      TO BP13K820-REC.                    
           INITIALIZE                          BP13K820-REC.                    
                                                                                
           IF K800-NUM-NRIC1 NOT = SPACES AND LOW-VALUES                        
              MOVE K800-NUM-REGN                 TO K820-NUM-REGN               
              MOVE K800-NUM-NRIC1                TO K820-NUM-NRIC               
                                                                                
              PERFORM 2200-READ-BP13K820         THRU 2200-EXIT                 
              IF WS-FND-K820 = 'Y'                                              
                 MOVE K820-NME-OCCP               TO F400-NAME1                 
                 MOVE K820-NUM-NRIC               TO F400-NRIC1                 
                 MOVE K820-NUM-CPF                TO F400-CPF1                  
                 PERFORM 2207-DECODE-SEX         THRU 2207-EXIT                 
                 MOVE WS-NUM-SEX                  TO F400-SEX1                  
                 PERFORM 2206-DECODE-ETHNIC      THRU 2206-EXIT                 
                 MOVE WS-CDE-RACE                 TO F400-RACE1                 
                 MOVE K820-DTE-BIRTH              TO F400-DOB1                  
                 MOVE K820-NUM-NATIONALITY        TO F400-NATIONALITY1          
                                                                                
                 IF WS-NUM-AHG NOT = SPACES                                     
                   PERFORM VARYING WS-PRSN FROM 1 BY 1 UNTIL WS-PRSN > 4        
                    IF WS-NRIC(WS-PRSN) = K800-NUM-NRIC1                        
                     MOVE WS-AMT-AHG-GRANT(WS-PRSN) TO F400-AMT-AHG-CPF1        
                    END-IF                                                      
                   END-PERFORM                                                  
                 END-IF                                                         
                                                                                
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                        
              MOVE K800-NUM-REGN                 TO K820-NUM-REGN               
              MOVE K800-NUM-NRIC2                TO K820-NUM-NRIC               
                                                                                
              PERFORM 2200-READ-BP13K820         THRU 2200-EXIT                 
              IF WS-FND-K820 = 'Y'                                              
                 MOVE K820-NME-OCCP               TO F400-NAME2                 
                 MOVE K820-NUM-NRIC               TO F400-NRIC2                 
                 MOVE K820-NUM-CPF                TO F400-CPF2                  
                 PERFORM 2207-DECODE-SEX         THRU 2207-EXIT                 
                 MOVE WS-NUM-SEX                  TO F400-SEX2                  
                 PERFORM 2206-DECODE-ETHNIC      THRU 2206-EXIT                 
                 MOVE WS-CDE-RACE                 TO F400-RACE2                 
                 MOVE K820-DTE-BIRTH              TO F400-DOB2                  
                 MOVE K820-NUM-NATIONALITY        TO F400-NATIONALITY2          
                                                                                
                 IF WS-NUM-AHG NOT = SPACES                                     
                   PERFORM VARYING WS-PRSN FROM 1 BY 1 UNTIL WS-PRSN > 4        
                    IF WS-NRIC(WS-PRSN) = K800-NUM-NRIC2                        
                     MOVE WS-AMT-AHG-GRANT(WS-PRSN) TO F400-AMT-AHG-CPF2        
                    END-IF                                                      
                   END-PERFORM                                                  
                 END-IF                                                         
                                                                                
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                        
              MOVE K800-NUM-REGN                 TO K820-NUM-REGN               
              MOVE K800-NUM-NRIC3                TO K820-NUM-NRIC               
                                                                                
              PERFORM 2200-READ-BP13K820         THRU 2200-EXIT                 
              IF WS-FND-K820 = 'Y'                                              
                 MOVE K820-NME-OCCP               TO F400-NAME3                 
                 MOVE K820-NUM-NRIC               TO F400-NRIC3                 
                 MOVE K820-NUM-CPF                TO F400-CPF3                  
                 PERFORM 2207-DECODE-SEX         THRU 2207-EXIT                 
                 MOVE WS-NUM-SEX                  TO F400-SEX3                  
                 PERFORM 2206-DECODE-ETHNIC      THRU 2206-EXIT                 
                 MOVE WS-CDE-RACE                 TO F400-RACE3                 
                 MOVE K820-DTE-BIRTH              TO F400-DOB3                  
                 MOVE K820-NUM-NATIONALITY        TO F400-NATIONALITY3          
                                                                                
                 IF WS-NUM-AHG NOT = SPACES                                     
                   PERFORM VARYING WS-PRSN FROM 1 BY 1 UNTIL WS-PRSN > 4        
                    IF WS-NRIC(WS-PRSN) = K800-NUM-NRIC3                        
                     MOVE WS-AMT-AHG-GRANT(WS-PRSN) TO F400-AMT-AHG-CPF3        
                    END-IF                                                      
                   END-PERFORM                                                  
                 END-IF                                                         
                                                                                
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                        
              MOVE K800-NUM-REGN                 TO K820-NUM-REGN               
              MOVE K800-NUM-NRIC4                TO K820-NUM-NRIC               
                                                                                
              PERFORM 2200-READ-BP13K820         THRU 2200-EXIT                 
              IF WS-FND-K820 = 'Y'                                              
                 MOVE K820-NME-OCCP               TO F400-NAME4                 
                 MOVE K820-NUM-NRIC               TO F400-NRIC4                 
                 MOVE K820-NUM-CPF                TO F400-CPF4                  
                 PERFORM 2207-DECODE-SEX         THRU 2207-EXIT                 
                 MOVE WS-NUM-SEX                  TO F400-SEX4                  
                 PERFORM 2206-DECODE-ETHNIC      THRU 2206-EXIT                 
                 MOVE WS-CDE-RACE                 TO F400-RACE4                 
                 MOVE K820-DTE-BIRTH              TO F400-DOB4                  
                 MOVE K820-NUM-NATIONALITY        TO F400-NATIONALITY4          
                                                                                
                 IF WS-NUM-AHG NOT = SPACES                                     
                   PERFORM VARYING WS-PRSN FROM 1 BY 1 UNTIL WS-PRSN > 4        
                    IF WS-NRIC(WS-PRSN) = K800-NUM-NRIC4                        
                     MOVE WS-AMT-AHG-GRANT(WS-PRSN) TO F400-AMT-AHG-CPF4        
                    END-IF                                                      
                   END-PERFORM                                                  
                 END-IF                                                         
                                                                                
              END-IF                                                            
                                                                                
           END-IF.                                                              
                                                                                
                                                                                
       2205-EXIT.  EXIT.                                                        
                                                                                
      *=========================================================*               
       2206-DECODE-ETHNIC.                                                      
      *=========================================================*               
                                                                                
           IF K820-NUM-ETHNIC = 0 OR 1 OR 2 OR 3 OR 4                           
              MOVE 2         TO WS-CDE-RACE                                     
           ELSE IF K820-NUM-ETHNIC = 5                                          
              MOVE 1         TO WS-CDE-RACE                                     
           ELSE IF K820-NUM-ETHNIC = 6                                          
              MOVE 3         TO WS-CDE-RACE                                     
           ELSE IF K820-NUM-ETHNIC = 7 OR 9 OR ' '                              
              MOVE 4         TO WS-CDE-RACE.                                    
                                                                                
       2206-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=========================================================*               
       2207-DECODE-SEX.                                                         
      *=========================================================*               
                                                                                
           IF K820-NUM-SEX NOT = 'M' AND 'F' AND '1' AND '2'                    
              MOVE SPACES            TO WS-NUM-SEX.                             
                                                                                
           IF K820-NUM-SEX = 'M' OR 'F'                                         
              MOVE K820-NUM-SEX      TO WS-NUM-SEX                              
           ELSE                                                                 
           IF K820-NUM-SEX = '1'                                                
              MOVE 'M'               TO WS-NUM-SEX                              
           ELSE                                                                 
           IF K820-NUM-SEX = '2'                                                
              MOVE 'F'               TO WS-NUM-SEX                              
           END-IF.                                                              
                                                                                
       2207-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2300-READ-BP13KD00.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                   TO BP13KD00-REC.                       
           INITIALIZE                       BP13KD00-REC.                       
           MOVE 'N'                      TO WS-FND-KD00.                        
                                                                                
           MOVE FD15-NUM-REGN            TO KD00-NUM-REGN.                      
                                                                                
           READ BP13KD00 KEY IS KD00-KEY-FLD.                                   
                                                                                
           EVALUATE WS-KD00-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 ADD 1                   TO WS-KD00-FOUND                       
                 MOVE 'Y'                TO WS-FND-KD00                         
              WHEN 23                                                           
                 ADD 1                   TO WS-KD00-NOTFND                      
                 MOVE SPACES TO WS-FILENAME WS-KEY-FLD WS-KEY-VALUE             
                 MOVE 'BP13KD00'         TO WS-FILENAME                         
                 MOVE 'REGN'             TO WS-KEY-FLD                          
                 MOVE FD15-NUM-REGN      TO WS-KEY-VALUE                        
                 PERFORM 7000-WRITE-REPORT THRU 7000-EXIT                       
              WHEN OTHER                                                        
                 DISPLAY 'BP13KD00 - ERROR READING : '                          
                         WS-KD00-STATUS                                         
                 MOVE WS-KD00-STATUS     TO RETURN-CODE                         
                 GO TO 9999-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       2300-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2500-READ-BP13KD35.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                   TO BP13KD35-REC.                       
           INITIALIZE                       BP13KD35-REC.                       
           MOVE 'N'                      TO WS-FND-KD35.                        
                                                                                
           MOVE FD15-NUM-REGN            TO KD35-NUM-REGN.                      
                                                                                
           READ BP13KD35 KEY IS KD35-KEY-FLD.                                   
                                                                                
           EVALUATE WS-KD35-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 ADD 1                   TO WS-KD35-FOUND                       
                 MOVE 'Y'                TO WS-FND-KD35                         
                 PERFORM 2550-MOVE-BP13KD35 THRU 2550-EXIT                      
              WHEN 23                                                           
                 ADD 1                   TO WS-KD35-NOTFND                      
                 IF KD00-AMT-OPTION NOT NUMERIC                                 
                    MOVE ZEROES             TO F400-AMT-CD-CASH                 
                 ELSE                                                           
                    MOVE KD00-AMT-OPTION    TO F400-AMT-CD-CASH                 
                 END-IF                                                         
                 MOVE SPACES TO WS-FILENAME WS-KEY-FLD WS-KEY-VALUE             
                 MOVE 'BP13KD35'         TO WS-FILENAME                         
                 MOVE 'REGN'             TO WS-KEY-FLD                          
                 MOVE FD15-NUM-REGN      TO WS-KEY-VALUE                        
                 PERFORM 7000-WRITE-REPORT THRU 7000-EXIT                       
              WHEN OTHER                                                        
                 DISPLAY 'BP13KD35 - ERROR READING : '                          
                         WS-KD35-STATUS                                         
                 MOVE WS-KD35-STATUS     TO RETURN-CODE                         
                 GO TO 9999-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       2500-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2550-MOVE-BP13KD35.                                                      
      ******************************************************************        
                                                                                
           MOVE KD35-AMT-RETIRE-CPF1        TO F400-AMT-RETIRE-CPF1.            
           MOVE KD35-AMT-RETIRE-CPF2        TO F400-AMT-RETIRE-CPF2.            
           MOVE KD35-AMT-RETIRE-CPF3        TO F400-AMT-RETIRE-CPF3.            
           MOVE KD35-AMT-RETIRE-CPF4        TO F400-AMT-RETIRE-CPF4.            
                                                                                
           IF KD35-AMT-AVAIL-CPF1 NOT NUMERIC                                   
              MOVE ZEROES                   TO F400-AMT-CPF1-BALANCE            
           ELSE                                                                 
              MOVE KD35-AMT-AVAIL-CPF1      TO F400-AMT-CPF1-BALANCE.           
           IF KD35-AMT-AVAIL-CPF2 NOT NUMERIC                                   
              MOVE ZEROES                   TO F400-AMT-CPF2-BALANCE            
           ELSE                                                                 
              MOVE KD35-AMT-AVAIL-CPF2      TO F400-AMT-CPF2-BALANCE.           
           IF KD35-AMT-AVAIL-CPF3 NOT NUMERIC                                   
              MOVE ZEROES                   TO F400-AMT-CPF3-BALANCE            
           ELSE                                                                 
              MOVE KD35-AMT-AVAIL-CPF3      TO F400-AMT-CPF3-BALANCE.           
           IF KD35-AMT-AVAIL-CPF4 NOT NUMERIC                                   
              MOVE ZEROES                   TO F400-AMT-CPF4-BALANCE            
           ELSE                                                                 
              MOVE KD35-AMT-AVAIL-CPF4      TO F400-AMT-CPF4-BALANCE.           
                                                                                
           IF KD35-AMT-HPS-PREMIUM-REQ1 > 0                                     
              MOVE 'Y'                      TO F400-NUM-HPS1                    
           END-IF.                                                              
           IF KD35-AMT-HPS-PREMIUM-REQ2 > 0                                     
              MOVE 'Y'                      TO F400-NUM-HPS2                    
           END-IF.                                                              
           IF KD35-AMT-HPS-PREMIUM-REQ3 > 0                                     
              MOVE 'Y'                      TO F400-NUM-HPS3                    
           END-IF.                                                              
           IF KD35-AMT-HPS-PREMIUM-REQ4 > 0                                     
              MOVE 'Y'                      TO F400-NUM-HPS4                    
           END-IF.                                                              
                                                                                
           IF KD35-AMT-OPTION-CPF1 NOT NUMERIC                                  
              MOVE ZEROES                   TO KD35-AMT-OPTION-CPF1.            
           MOVE KD35-AMT-OPTION-CPF1        TO F400-AMT-CD-CPF1.                
           IF KD35-AMT-OPTION-CPF2 NOT NUMERIC                                  
              MOVE ZEROES                   TO KD35-AMT-OPTION-CPF2.            
           MOVE KD35-AMT-OPTION-CPF2        TO F400-AMT-CD-CPF2.                
           IF KD35-AMT-OPTION-CPF3 NOT NUMERIC                                  
              MOVE ZEROES                   TO KD35-AMT-OPTION-CPF3.            
           MOVE KD35-AMT-OPTION-CPF3        TO F400-AMT-CD-CPF3.                
           IF KD35-AMT-OPTION-CPF4 NOT NUMERIC                                  
              MOVE ZEROES                   TO KD35-AMT-OPTION-CPF4.            
           MOVE KD35-AMT-OPTION-CPF4        TO F400-AMT-CD-CPF4.                
           IF KD35-AMT-OPTION-CASH NOT NUMERIC                                  
              MOVE ZEROES                   TO KD35-AMT-OPTION-CASH.            
           MOVE KD35-AMT-OPTION-CASH        TO F400-AMT-CD-CASH.                
           COMPUTE WS-AMT-OPTION = F400-AMT-CD-CASH +                           
                                   F400-AMT-CD-CPF1 +                           
                                   F400-AMT-CD-CPF2 +                           
                                   F400-AMT-CD-CPF3 +                           
                                   F400-AMT-CD-CPF4                             
           IF WS-AMT-OPTION = 0                                                 
              IF KD00-AMT-OPTION NOT NUMERIC                                    
                 MOVE ZEROES                TO F400-AMT-CD-CASH                 
              ELSE                                                              
                 MOVE KD00-AMT-OPTION       TO F400-AMT-CD-CASH                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 15                 
              IF KD35-AMT-PP-CASH(WS-CTR) NOT NUMERIC                           
                 MOVE ZEROES                TO KD35-AMT-PP-CASH(WS-CTR)         
              END-IF                                                            
              IF KD35-AMT-PP-CPF1(WS-CTR) NOT NUMERIC                           
                 MOVE ZEROES                TO KD35-AMT-PP-CPF1(WS-CTR)         
              END-IF                                                            
              IF KD35-AMT-PP-CPF2(WS-CTR) NOT NUMERIC                           
                 MOVE ZEROES                TO KD35-AMT-PP-CPF2(WS-CTR)         
              END-IF                                                            
              IF KD35-AMT-PP-CPF3(WS-CTR) NOT NUMERIC                           
                 MOVE ZEROES                TO KD35-AMT-PP-CPF3(WS-CTR)         
              END-IF                                                            
              IF KD35-AMT-PP-CPF4(WS-CTR) NOT NUMERIC                           
                 MOVE ZEROES                TO KD35-AMT-PP-CPF4(WS-CTR)         
              END-IF                                                            
              ADD KD35-AMT-PP-CASH(WS-CTR)  TO F400-AMT-ICP-CASH                
              ADD KD35-AMT-PP-CPF1(WS-CTR)  TO F400-AMT-ICP-CPF1                
              ADD KD35-AMT-PP-CPF2(WS-CTR)  TO F400-AMT-ICP-CPF2                
              ADD KD35-AMT-PP-CPF3(WS-CTR)  TO F400-AMT-ICP-CPF3                
              ADD KD35-AMT-PP-CPF4(WS-CTR)  TO F400-AMT-ICP-CPF4                
           END-PERFORM.                                                         
                                                                                
       2550-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2600-READ-BP13KD80.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                   TO BP13KD80-REC.                       
           INITIALIZE                       BP13KD80-REC.                       
           MOVE 'N'                      TO WS-FND-KD80.                        
                                                                                
           MOVE FD15-NUM-REGN            TO KD80-NUM-REGN.                      
                                                                                
           READ BP13KD80 KEY IS KD80-KEY-FLD.                                   
                                                                                
           EVALUATE WS-KD80-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 ADD 1                   TO WS-KD80-FOUND                       
                 MOVE 'Y'                TO WS-FND-KD80                         
              WHEN 23                                                           
                 ADD 1                   TO WS-KD80-NOTFND                      
                 MOVE SPACES TO WS-FILENAME WS-KEY-FLD WS-KEY-VALUE             
                 MOVE 'BP13KD80'         TO WS-FILENAME                         
                 MOVE 'REGN'             TO WS-KEY-FLD                          
                 MOVE FD15-NUM-REGN      TO WS-KEY-VALUE                        
                 PERFORM 7000-WRITE-REPORT THRU 7000-EXIT                       
              WHEN OTHER                                                        
                 DISPLAY 'BP13KD80 - ERROR READING : '                          
                         WS-KD80-STATUS                                         
                 MOVE WS-KD80-STATUS     TO RETURN-CODE                         
                 GO TO 9999-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       2600-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2700-PROCESS-S903.                                                       
      ******************************************************************        
           MOVE WS-SYS-DATE            TO WS-S903-SO-EFF-DATE.                  
                                                                                
           MOVE ZEROES                 TO WS-S903-SC-CC1                        
                                          WS-S903-SC-CC2                        
                                          WS-S903-SC-CC3.                       
                                                                                
           MOVE ZEROES                 TO WS-S903-SC-REBATE1                    
                                          WS-S903-SC-REBATE2                    
                                          WS-S903-SC-REBATE3                    
                                          WS-S903-SC-GRANT1                     
                                          WS-S903-SC-GRANT2                     
                                          WS-S903-SC-GRANT3                     
                                          WS-S903-SC-TOTAL-AMT.                 
                                                                                
           MOVE ZEROES                 TO WS-S903-SC-TOTAL-AMT.                 
           MOVE F400-NUM-SCH-ACC(1:9)  TO WS-S903-NUM-SCH-ACC                   
                                          WS-LINK-NUM-SCH-ACC.                  
                                                                                
           PERFORM 2750-READ-PIDB    THRU 2750-EXIT.                            
                                                                                
           MOVE COMM8-CDE-TYP-ACTLUSE  TO WS-S903-ROOM-TY.                      
           MOVE COMM8-CDE-TYP-DESG     TO WS-S903-DESIGN-TY.                    
           MOVE COMM8-NUM-MGMT         TO F400-CDE-BO.                          
           MOVE COMM8-NUM-ED-GL        TO F400-CDE-TC.                          
           MOVE COMM8-NUM-MNTNC        TO F400-CDE-CC.                          
      *-----------------------------------------------------------*             
      *    CALL BP13C903 TO GET THE SC-CC1,SC-CC2 AND SC-CC3      *             
      *-----------------------------------------------------------*             
           CALL 'BP13C903' USING WS-LINK-S903.                                  
                                                                                
           MOVE WS-S903-SC-CC1           TO F400-AMT-SC-CC                      
           MOVE WS-S903-SC-CC2           TO F400-AMT-SC-CC2                     
           MOVE WS-S903-SC-CC3           TO F400-AMT-SC-CC3                     
           MOVE WS-S903-SC-REBATE1       TO F400-AMT-SC-CC-REBATE               
           MOVE WS-S903-SC-REBATE2       TO F400-AMT-SC-CC2-REBATE              
           MOVE WS-S903-SC-REBATE3       TO F400-AMT-SC-CC3-REBATE              
           MOVE WS-S903-SC-GRANT1        TO F400-AMT-SC-CC1-GRANT               
           MOVE WS-S903-SC-GRANT2        TO F400-AMT-SC-CC2-GRANT               
           MOVE WS-S903-SC-GRANT3        TO F400-AMT-SC-CC3-GRANT               
           MOVE WS-S903-SC-REC-TC1       TO F400-AMT-SC-CC1-RECOVER-TC          
           MOVE WS-S903-SC-REC-TC2       TO F400-AMT-SC-CC2-RECOVER-TC          
           MOVE WS-S903-SC-REC-TC3       TO F400-AMT-SC-CC3-RECOVER-TC          
           MOVE WS-S903-SC-REB-SP1       TO F400-AMT-SC-CC1-REBATE-SP           
           MOVE WS-S903-SC-REB-SP2       TO F400-AMT-SC-CC1-REBATE-SP           
           MOVE WS-S903-SC-REB-SP3       TO F400-AMT-SC-CC1-REBATE-SP.          
                                                                                
       2700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2750-READ-PIDB.                                                          
      *-------------------------------------------------------------            
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
           WHEN +000                                                            
              CONTINUE                                                          
           WHEN +100                                                            
              MOVE SPACES   TO BP13COMM8-REC                                    
              INITIALIZE       BP13COMM8-REC                                    
              DISPLAY 'REC NOT FOUND IN PIDB: ' WS-LINK-NUM-SCH-ACC             
           WHEN OTHER                                                           
              MOVE COMM8-CDE-SYSERR      TO WS-RETCODE                          
              DISPLAY 'ERROR IN CALLING BP13C913, RETCODE: '                    
                      WS-RETCODE                                                
              PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       2750-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3300-CALL-BP13CB4A.                                                      
      ******************************************************************        
           INITIALIZE COM73-COMMAREA.                                           
                                                                                
           MOVE 'R'                  TO  COM73-NUM-CALL-ACTION.                 
           MOVE FD15-NUM-REGN        TO  COM73-NUM-RGSTRN-INPUT.                
           CALL 'BP13CB4A' USING COM73-COMMAREA.                                
                                                                                
           IF COM73-CDE-ERROR = SPACES OR ZEROES                                
              NEXT SENTENCE                                                     
           ELSE                                                                 
              MOVE SPACES                TO COM73-COMMAREA                      
              INITIALIZE                    COM73-COMMAREA                      
           END-IF.                                                              
                                                                                
           MOVE SPACES TO WS-NUM-AHG.                                           
           MOVE 'N' TO WS-STORED.                                               
           PERFORM VARYING WS-PRSN FROM 1 BY 1 UNTIL WS-PRSN > 4                
             MOVE SPACES TO WS-NRIC(WS-PRSN)                                    
             MOVE ZEROES TO WS-AMT-AHG-GRANT(WS-PRSN)                           
           END-PERFORM.                                                         
                                                                                
           PERFORM VARYING WS-A FROM 1 BY 1 UNTIL WS-A > 5                      
             IF COM73-NUM-GRANT-TYPE(WS-A) = 'AHG'                              
                MOVE COM73-GRANT-AMOUNT-ELGBL(WS-A) TO WS-NUM-AHG               
             END-IF                                                             
             PERFORM VARYING WS-P FROM 1 BY 1 UNTIL WS-P > 10                   
              IF (COM73-IDNTY-NO(WS-A WS-P) NOT = SPACES AND LOW-VALUES)        
                 MOVE 'N' TO WS-STORED                                          
                 PERFORM VARYING WS-PRSN FROM 1 BY 1 UNTIL WS-PRSN > 4          
                                                OR WS-STORED = 'Y'              
                  IF WS-NRIC(WS-PRSN) = SPACES OR                               
                     WS-NRIC(WS-PRSN) = COM73-IDNTY-NO(WS-A WS-P)               
                     MOVE COM73-IDNTY-NO(WS-A WS-P) TO WS-NRIC(WS-PRSN)         
                     MOVE 'Y' TO WS-STORED                                      
                     IF COM73-NUM-GRANT-TYPE(WS-A) = 'AHG'                      
                        MOVE COM73-AMT-GRANT(WS-A WS-P)                         
                                      TO WS-AMT-AHG-GRANT(WS-PRSN)              
                     END-IF                                                     
                  END-IF                                                        
                 END-PERFORM                                                    
              END-IF                                                            
             END-PERFORM                                                        
           END-PERFORM.                                                         
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       4000-INIT-BP13F400.                                                      
      ***************************************************************           
                                                                                
           MOVE SPACES                      TO BP13F400-REC.                    
           INITIALIZE                          BP13F400-REC.                    
                                                                                
           MOVE FD15-NUM-REGN               TO F400-NUM-REGN.                   
           MOVE 'DBS'                       TO F400-NUM-ALLO-CAT                
           MOVE WS-SYS-DATE                 TO WS-TMP-DATE                      
           MOVE WS-TMP-DATE                 TO F400-DTE-UPDATE                  
           MOVE 'BP13CD40'                  TO F400-NUM-USERID                  
           MOVE 'Y'                         TO F400-NUM-NEW-IDP                 
           MOVE 'N'                         TO F400-CDE-ERL-SCH                 
           MOVE SPACES                      TO F400-NUM-JB                      
           MOVE 20                          TO F400-PCT-CD-REQUIRED             
           MOVE 'N'                         TO F400-NUM-SF-DISC                 
           IF FD15-DTE-IND-TP NOT NUMERIC                                       
              MOVE ZEROES            TO F400-DTE-KEY-ISSUE                      
                                        F400-DTE-SO-EXECUTION                   
                                        F400-DTE-SO-EFFECTIVE                   
                                        WS-DTE-CCYYMMDD                         
           ELSE                                                                 
              MOVE FD15-DTE-IND-TP   TO F400-DTE-KEY-ISSUE                      
                                        F400-DTE-SO-EXECUTION                   
                                        F400-DTE-SO-EFFECTIVE                   
                                        WS-DTE-CCYYMMDD                         
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************************************************           
       4100-MOVE-BP13K800.                                                      
      ***************************************************************           
                                                                                
           MOVE K800-NUM-SPR-TAG            TO F400-NUM-SPR.                    
           MOVE K800-NUM-SCH-ACC            TO F400-NUM-SCH-ACC.                
           MOVE K800-NUM-HOUSEHOLD          TO F400-NUM-HOUSEHOLD               
           MOVE K800-NUM-NEW-TOWN           TO F400-CDE-NT                      
           MOVE K800-NUM-IDP-SCHEME         TO F400-CDE-IDP-SCH                 
           MOVE K800-NUM-ALLO-SCHEME        TO F400-CDE-ALLOC-SCH               
           MOVE K800-NUM-ELIG-SCHEME        TO F400-CDE-ELIG-SCH                
           MOVE K800-NUM-CAT                TO F400-CDE-CATEGORY                
           MOVE K800-NUM-LOAN-TAG           TO F400-NUM-LOAN-TAG                
           MOVE K800-NUM-ORIG-LOAN-TAG      TO F400-NUM-LOAN-TAG-ORIG           
           MOVE K800-NUM-ELIG-LOAN-POLICY   TO F400-NUM-ELIG-LOAN-POLICY        
           MOVE K800-NUM-SCCC-ELGBTY        TO F400-NUM-SCCC-ELGBTY             
           MOVE K800-DTE-BALLOT             TO F400-DTE-BALLOT                  
           MOVE K800-DTE-STAMP-AGMT         TO F400-DTE-SA-STAMP                
           MOVE K800-NUM-CA-TAG             TO F400-NUM-CA-TAG                  
           MOVE K800-NUM-SUBSIDISED-LOAN    TO F400-NUM-SUBSIDISED-LOAN         
           MOVE K800-NUM-PPO                TO F400-NUM-PPO                     
           MOVE K800-NUM-HLA                TO F400-NUM-HLE                     
      *    MOVE                             TO F400-NME-BANK-ACC-1ST            
           MOVE K800-NUM-WARRANT-ACT        TO F400-NUM-WARRANT-ACT             
           MOVE K800-NUM-CF-TYPE            TO F400-NUM-CF-TYPE                 
           IF K800-NUM-LOAN-TAG = 'B'                                           
              MOVE 'NR'                     TO F400-CDE-LOAN-SCH-POLICY         
           ELSE                                                                 
              MOVE K800-NUM-MORT-LOAN-POLICY TO F400-CDE-LOAN-SCH-POLICY        
           END-IF                                                               
           MOVE WS-NUM-AHG                  TO F400-NUM-AHG.                    
           MOVE K800-NUM-HOLDING-TYPE       TO F400-CDE-HOLDING-TYPE.           
           MOVE K800-NUM-APPL1-PR           TO F400-PR-TAG1.                    
           MOVE K800-NUM-APPL2-PR           TO F400-PR-TAG2.                    
           MOVE K800-NUM-APPL3-PR           TO F400-PR-TAG3.                    
           MOVE K800-NUM-APPL4-PR           TO F400-PR-TAG4.                    
           IF K800-AMT-DEFER-PREMIUM NOT NUMERIC                                
              MOVE ZEROES                   TO F400-AMT-DEFER-PREMIUM           
           ELSE                                                                 
              MOVE K800-AMT-DEFER-PREMIUM   TO F400-AMT-DEFER-PREMIUM           
           END-IF.                                                              
           IF K800-NUM-PREMIUM-RATE NOT NUMERIC                                 
              MOVE ZEROES                   TO F400-NUM-PREMIUM-RATE            
           ELSE                                                                 
              MOVE K800-NUM-PREMIUM-RATE    TO F400-NUM-PREMIUM-RATE            
           END-IF.                                                              
           IF K800-AMT-PREMIUM NOT NUMERIC                                      
              MOVE ZEROES                   TO F400-AMT-PREMIUM-NEW             
           ELSE                                                                 
              MOVE K800-AMT-PREMIUM         TO F400-AMT-PREMIUM-NEW             
           END-IF.                                                              
           IF  K800-NUM-SHR1 = SPACES OR LOW-VALUES                             
               MOVE ZEROES                  TO F400-NUM-SHR1                    
           ELSE                                                                 
               MOVE K800-NUM-SHR1           TO F400-NUM-SHR1.                   
           IF  K800-NUM-SHR2 = SPACES OR LOW-VALUES                             
               MOVE ZEROES                  TO F400-NUM-SHR2                    
           ELSE                                                                 
               MOVE K800-NUM-SHR2           TO F400-NUM-SHR2.                   
           IF  K800-NUM-SHR3 = SPACES OR LOW-VALUES                             
               MOVE ZEROES                  TO F400-NUM-SHR3                    
           ELSE                                                                 
               MOVE K800-NUM-SHR3           TO F400-NUM-SHR3.                   
           IF  K800-NUM-SHR4 = SPACES OR LOW-VALUES                             
               MOVE ZEROES                  TO F400-NUM-SHR4                    
           ELSE                                                                 
               MOVE K800-NUM-SHR4           TO F400-NUM-SHR4.                   
           IF  K800-NUM-SHR-BASE = SPACES OR LOW-VALUES                         
               MOVE ZEROES                  TO F400-NUM-SHR-BASE                
           ELSE                                                                 
               MOVE K800-NUM-SHR-BASE TO F400-NUM-SHR-BASE.                     
           IF K800-AMT-BK-REQ  NOT NUMERIC                                      
              MOVE ZEROES                   TO  F400-AMT-BK-REQ                 
           ELSE                                                                 
              MOVE K800-AMT-BK-REQ          TO F400-AMT-BK-REQ.                 
           MOVE K800-NUM-SUB-TRANSFER       TO F400-CDE-SUB-TRANSFER.           
           MOVE K800-NUM-ASPF               TO F400-CDE-ASPF.                   
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       4300-MOVE-BP13KD00.                                                      
      ******************************************************************        
                                                                                
           IF KD00-DTE-HSGNT-APPROVE  = SPACES OR LOW-VALUES                    
              MOVE '14'                     TO F400-CDE-SALES-TYPE              
           ELSE                                                                 
              MOVE '15'                     TO F400-CDE-SALES-TYPE              
           END-IF                                                               
           MOVE KD00-DTE-SNP-SIGNED   TO F400-DTE-PRINCIPAL-SALES-AGMT          
           IF K800-NUM-LOAN-TAG = 'N' OR 'B'                                    
             MOVE ZEROES                    TO F400-NUM-LOAN-REPYMT-MAX         
                                               F400-NUM-REPAY-PERIOD            
           ELSE                                                                 
             MOVE KD00-NUM-LOAN-REPYMT-MAX  TO F400-NUM-LOAN-REPYMT-MAX         
             MOVE KD00-NUM-LOAN-REPYMT-PERIOD TO F400-NUM-REPAY-PERIOD          
           END-IF.                                                              
           MOVE KD00-AMT-SELL-PRICE         TO F400-AMT-OFFER-PRICE.            
           MOVE KD00-AMT-SELL-PRICE         TO F400-AMT-SELLING-PRICE.          
           MOVE KD00-DTE-SNP-SIGNED         TO F400-DTE-ARGORDER.               
           MOVE KD00-AMT-LOAN-BAL           TO F400-AMT-OUTS-LOAN-BAL.          
           MOVE SPACES                      TO F400-DTE-EXPIRED-TP              
           MOVE KD00-CDE-LOAN-SCH           TO F400-CDE-LOAN-ELIG               
           MOVE '1'                         TO F400-CDE-INTEREST-RATE           
           MOVE '1'                         TO F400-CDE-LOAN-SCH.               
                                                                                
           COMPUTE F400-AMT-LOAN-INSTAL = KD00-AMT-INSTAL-CASH +                
                                          KD00-AMT-INSTAL-CPF1 +                
                                          KD00-AMT-INSTAL-CPF2 +                
                                          KD00-AMT-INSTAL-CPF3 +                
                                          KD00-AMT-INSTAL-CPF4                  
           MOVE KD00-AMT-INSTAL-CASH        TO F400-AMT-MTHLY-CASH.             
           MOVE KD00-AMT-INSTAL-CPF1        TO F400-AMT-MTHLY-CPF1.             
           MOVE KD00-AMT-INSTAL-CPF2        TO F400-AMT-MTHLY-CPF2.             
           MOVE KD00-AMT-INSTAL-CPF3        TO F400-AMT-MTHLY-CPF3.             
           MOVE KD00-AMT-INSTAL-CPF4        TO F400-AMT-MTHLY-CPF4.             
                                                                                
           IF K800-NUM-LOAN-TAG = 'B'                                           
              PERFORM 2600-READ-BP13KD80       THRU 2600-EXIT                   
              IF WS-FND-KD80 = 'Y'                                              
                 MOVE KD80-AMT-LOAN-GRANTED    TO F400-AMT-LOAN-QUANTUM         
              ELSE                                                              
                 MOVE ZEROS                    TO F400-AMT-LOAN-QUANTUM         
              END-IF                                                            
           ELSE                                                                 
              MOVE ZEROS                       TO F400-AMT-LOAN-QUANTUM         
           END-IF.                                                              
                                                                                
           IF K800-NUM-MORT-LOAN-POLICY = 'NR'                                  
              MOVE ZEROES                   TO F400-AMT-LOAN                    
           ELSE                                                                 
              MOVE KD00-AMT-LOAN-GRANTED    TO F400-AMT-LOAN                    
           END-IF.                                                              
      * BP13C950                                                                
           MOVE WS-SYS-DATE                 TO C950-PROCESS-DATE.               
           MOVE KD00-NUM-LOAN-REPYMT-PERIOD TO C950-REPYMT-PERIOD.              
           MOVE KD00-CDE-LOAN-SCH           TO C950-CDE-LOAN-SCH.               
                                                                                
           IF KD00-NUM-LOAN-REPYMT-PERIOD = ZEROES                              
              MOVE ZEROES                   TO F400-NUM-INTEREST-RATE           
           ELSE                                                                 
              CALL 'BP13C950' USING C950-FIELD                                  
              IF C950-INTREST-RATE NOT NUMERIC                                  
                 MOVE ZEROES                TO F400-NUM-INTEREST-RATE           
              ELSE                                                              
                 MOVE C950-INTREST-RATE     TO F400-NUM-INTEREST-RATE           
                 COMPUTE F400-NUM-INTEREST-RATE =                               
                                         C950-INTREST-RATE * 100                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE KD00-DTE-GIRO-EFF          TO F400-DTE-SUBS-INSTAL-GIRO.        
           IF KD00-NUM-GIRO = 'Y'                                               
              MOVE 'Y'                      TO F400-CDE-GIRO                    
           ELSE                                                                 
              MOVE 'N'                      TO F400-CDE-GIRO                    
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4900-DETERMINE-REST.                                                     
      ******************************************************************        
                                                                                
           IF F400-AMT-LOAN = 0                                                 
              PERFORM 4901-SET-PAYCDE-NO-LOAN     THRU 4901-EXIT                
           ELSE                                                                 
              PERFORM 4902-SET-PAYCDE-W-LOAN      THRU 4902-EXIT                
           END-IF.                                                              
                                                                                
           MOVE 7                                 TO F400-AMT-GST-RATE.         
                                                                                
       4900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4901-SET-PAYCDE-NO-LOAN.                                                 
      ******************************************************************        
                                                                                
           IF (F400-AMT-CD-CASH = 0) AND (F400-AMT-ICP-CASH = 0)                
              MOVE 3 TO F400-CDE-PAYMENT-CD                                     
              GO TO 4901-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF (F400-AMT-CD-CPF1  = 0) AND                                       
              (F400-AMT-CD-CPF2  = 0) AND                                       
              (F400-AMT-CD-CPF3  = 0) AND                                       
              (F400-AMT-CD-CPF4  = 0) AND                                       
              (F400-AMT-ICP-CPF1 = 0) AND                                       
              (F400-AMT-ICP-CPF2 = 0) AND                                       
              (F400-AMT-ICP-CPF3 = 0) AND                                       
              (F400-AMT-ICP-CPF4 = 0)                                           
              MOVE 4 TO F400-CDE-PAYMENT-CD                                     
              GO TO 4901-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE 6 TO F400-CDE-PAYMENT-CD.                                       
                                                                                
       4901-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4902-SET-PAYCDE-W-LOAN.                                                  
      ******************************************************************        
                                                                                
           IF (F400-AMT-MTHLY-CASH = 0)                                         
              MOVE 1 TO F400-CDE-PAYMENT-CD                                     
              GO TO 4902-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF (F400-AMT-MTHLY-CASH = F400-AMT-LOAN-INSTAL)                      
              MOVE 2 TO F400-CDE-PAYMENT-CD                                     
              GO TO 4902-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE 5 TO F400-CDE-PAYMENT-CD.                                       
                                                                                
       4902-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-WRITE-BP13F400.                                                     
      ******************************************************************        
                                                                                
           WRITE BP13F400-REC.                                                  
           ADD 1 TO WS-F400-WRITE.                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5100-UPDATE-BP13KD15.                                                    
      ******************************************************************        
                                                                                
           PERFORM 5150-READ-BP13KD15         THRU 5150-EXIT.                   
                                                                                
           MOVE F001-DTE-CURRENT                TO KD15-DTE-SEND-SMS.           
                                                                                
           IF K800-NUM-SCH-ACC NOT = KD15-NUM-SCH-ACC                           
              MOVE K800-NUM-SCH-ACC   TO KD15-NUM-SCH-ACC                       
           END-IF.                                                              
                                                                                
           REWRITE BP13KD15-REC.                                                
                                                                                
           EVALUATE WS-KD15-STATUS                                              
              WHEN 00                                                           
                 ADD 1                          TO WS-KD15-UPDATE               
              WHEN OTHER                                                        
                 DISPLAY 'BP13KD15 - ERROR REWRITING : '                        
                         WS-KD15-STATUS                                         
                 DISPLAY '___KD15-KEY-FLD : ' KD15-KEY-FLD                      
                 GO TO 9999-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5150-READ-BP13KD15.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES              TO BP13KD15-REC.                            
           INITIALIZE                  BP13KD15-REC.                            
                                                                                
           MOVE FD15-KEY-FLD        TO KD15-KEY-FLD                             
                                                                                
           READ BP13KD15.                                                       
                                                                                
           EVALUATE WS-KD15-STATUS                                              
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'BP13KD15 - ERROR READING : '                          
                         WS-KD15-STATUS                                         
                 DISPLAY 'KD15-KEY-FLD : ' KD15-KEY-FLD                         
                 GO TO 9999-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       5150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5200-READ-BP13KA10.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                           TO BP13KA10-REC.               
           INITIALIZE                               BP13KA10-REC.               
                                                                                
           MOVE FD15-NUM-PRJCT                   TO KA10-NUM-PRJCT.             
                                                                                
           READ BP13KA10.                                                       
                                                                                
           EVALUATE WS-KA10-STATUS                                              
              WHEN 00                                                           
                 ADD 1                           TO WS-KA10-FOUND               
                 MOVE KA10-DTE-ESTMD-TOP         TO F400-DTE-EXPIRED-TP         
              WHEN 23                                                           
                 ADD 1                           TO WS-KA10-NOTFND              
                 DISPLAY 'BP13KA10 - RECORD NOT FOUND : '                       
                         KA10-NUM-PRJCT                                         
                 MOVE SPACES                     TO F400-DTE-EXPIRED-TP         
              WHEN OTHER                                                        
                 DISPLAY 'BP13KA10 - ERROR READING : '                          
                         WS-KD15-STATUS                                         
                 DISPLAY 'KD15-KEY-FLD : ' KD15-KEY-FLD                         
                 GO TO 9999-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6000-GET-HPI-SCH.                                                        
      ******************************************************************        
      *  WEIGHTAGE CARRIED BY EACH OWNER'S CPF :                                
      *              CPF 1  :  100                                              
      *              CPF 2  :    5                                              
      *              CPF 3  :    5                                              
      *              CPF 4  :    5                                              
      **************************************************************            
                                                                                
           MOVE 0 TO WS-HPI-WT.                                                 
                                                                                
           COMPUTE WS-TOTAL-CPF1 = F400-AMT-MTHLY-CPF1 +                        
                                   F400-AMT-FIRST-CPF1 +                        
                                   F400-AMT-ICP-CPF1   +                        
                                   F400-AMT-CD-CPF1.                            
                                                                                
           COMPUTE WS-TOTAL-CPF2 = F400-AMT-MTHLY-CPF2 +                        
                                   F400-AMT-FIRST-CPF2 +                        
                                   F400-AMT-ICP-CPF2   +                        
                                   F400-AMT-CD-CPF2.                            
                                                                                
           COMPUTE WS-TOTAL-CPF3 = F400-AMT-MTHLY-CPF3 +                        
                                   F400-AMT-FIRST-CPF3 +                        
                                   F400-AMT-ICP-CPF3   +                        
                                   F400-AMT-CD-CPF3.                            
                                                                                
           COMPUTE WS-TOTAL-CPF4 = F400-AMT-MTHLY-CPF4 +                        
                                   F400-AMT-FIRST-CPF4 +                        
                                   F400-AMT-ICP-CPF4   +                        
                                   F400-AMT-CD-CPF4.                            
                                                                                
           IF WS-TOTAL-CPF1 NOT = 0                                             
              ADD 100 TO WS-HPI-WT                                              
           END-IF.                                                              
                                                                                
           IF WS-TOTAL-CPF2 NOT = 0                                             
              ADD   5 TO WS-HPI-WT                                              
           END-IF.                                                              
                                                                                
           IF WS-TOTAL-CPF3 NOT = 0                                             
              ADD   5 TO WS-HPI-WT                                              
           END-IF.                                                              
                                                                                
           IF WS-TOTAL-CPF4 NOT = 0                                             
              ADD   5 TO WS-HPI-WT                                              
           END-IF.                                                              
                                                                                
      * IF NO OWNER USING CPF,                                                  
                                                                                
           IF WS-HPI-WT = 0                                                     
              MOVE 4 TO F400-CDE-HPI-SCH                                        
              GO TO 6000-EXIT                                                   
           END-IF.                                                              
                                                                                
      * IF ONLY THE 1ST OWNER USING CPF,                                        
                                                                                
           IF WS-HPI-WT = 100                                                   
              MOVE 1 TO F400-CDE-HPI-SCH                                        
              GO TO 6000-EXIT                                                   
           END-IF.                                                              
                                                                                
      * IF ALL THE OWNERS USING CPF,                                            
                                                                                
           IF WS-HPI-WT = 115                                                   
              MOVE 6 TO F400-CDE-HPI-SCH                                        
              GO TO 6000-EXIT                                                   
           END-IF.                                                              
                                                                                
      * IF ANY TWO OWNERS USING CPF,                                            
                                                                                
           IF WS-HPI-WT = 105 OR 10                                             
              MOVE 3 TO F400-CDE-HPI-SCH                                        
              GO TO 6000-EXIT                                                   
           END-IF.                                                              
                                                                                
      * IF ANY THREE OWNERS USING CPF,                                          
                                                                                
           IF WS-HPI-WT = 110 OR 15                                             
              MOVE 5 TO F400-CDE-HPI-SCH                                        
              GO TO 6000-EXIT                                                   
           END-IF.                                                              
                                                                                
      * IF ONLY ONE OWNER EXCEPT THE 1ST USING CPF,                             
                                                                                
           IF WS-HPI-WT = 5                                                     
              MOVE 2 TO F400-CDE-HPI-SCH                                        
              GO TO 6000-EXIT                                                   
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6100-GET-FI-INFO.                                                        
      ******************************************************************        
                                                                                
           IF KD00-NUM-LOAN-REPYMT-PERIOD > ZEROES                              
              MOVE 'Y'                  TO  F400-NUM-FI-COMPULSORY              
           ELSE                                                                 
              MOVE 'N'                  TO  F400-NUM-FI-COMPULSORY              
           END-IF.                                                              
                                                                                
           MOVE SPACES               TO F400-DTE-FI-EFFECTIVE                   
                                        F400-NUM-FI-PERIOD.                     
                                                                                
                                                                                
      *    IF (COMM8-CDE-TYP-ACTLUSE = SPACES OR LOW-VALUES) OR                 
      *       (COMM8-CDE-MODL-DESG = SPACES OR LOW-VALUES)                      
      *       MOVE SPACES               TO F400-DTE-FI-EFFECTIVE                
      *                                    F400-NUM-FI-PERIOD                   
      *       GO TO 6100-EXIT                                                   
      *    END-IF.                                                              
                                                                                
      *    MOVE COMM8-CDE-TYP-ACTLUSE   TO  BM97-NUM-FLAT-TYPE.                 
      *    MOVE COMM8-CDE-MODL-DESG     TO  BM97-CDE-MODL-DESG.                 
      *    MOVE F001-DTE-CURRENT        TO  BM97-DTE-COMPARE.                   
                                                                                
      *    INITIALIZE                       BM97-NUM-ERROR                      
      *                                     BM97-NUM-SYSERR.                    
      *                                                                         
      *    CALL 'AB20C900' USING WS-BM97-AREA.                                  
      *                                                                         
      *    IF (BM97-NUM-ERROR = ZEROS OR SPACES) AND                            
      *       BM97-NUM-SYSERR = ZEROS                                           
      *       MOVE BM97-NUM-TERM        TO  F400-NUM-FI-PERIOD                  
      *       MOVE BM97-DTE-PREM-EFF    TO  F400-DTE-FI-EFFECTIVE               
      *    ELSE                                                                 
      *       MOVE BM97-NUM-SYSERR      TO  RETURN-CODE                         
      *       MOVE SPACES               TO  F400-DTE-FI-EFFECTIVE               
      *                                     F400-NUM-FI-PERIOD                  
      *       DISPLAY 'ERROR IN CALLING AB20C900, ' BM97-NUM-ERROR              
      *    END-IF.                                                              
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7000-WRITE-REPORT.                                                       
      ******************************************************************        
                                                                                
           IF WS-PAGENO = ZEROES OR WS-LINE-CNT > 54                            
              PERFORM 7100-WRITE-HEADER  THRU 7100-EXIT                         
           END-IF.                                                              
                                                                                
           ADD 1                      TO WS-REC-CNT.                            
           ADD 1                      TO WS-LINE-CNT.                           
           MOVE WS-REC-CNT            TO LD40-SN                                
           MOVE WS-FILENAME           TO LD40-FILENAME                          
           MOVE WS-KEY-FLD            TO LD40-KEY-FLD                           
           MOVE WS-KEY-VALUE          TO LD40-KEY-VALUE.                        
                                                                                
           WRITE BP13LD40-REC       FROM LD40-DETAIL.                           
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7100-WRITE-HEADER.                                                       
      ******************************************************************        
                                                                                
           ADD   1                    TO WS-PAGENO.                             
           MOVE WS-PAGENO             TO LD40-PAGE.                             
                                                                                
           WRITE BP13LD40-REC       FROM LD40-HEADER1   AFTER PAGE.             
           WRITE BP13LD40-REC       FROM LD40-HEADER2   AFTER 1.                
           WRITE BP13LD40-REC       FROM LD40-HEADER3   AFTER 2.                
           WRITE BP13LD40-REC       FROM LD40-HEADER4   AFTER 1.                
                                                                                
           MOVE   5                   TO WS-LINE-CNT.                           
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CD40'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY 'NO OF BP13FD15 RECORDS READ................. : '            
                    WS-FD15-READ.                                               
           DISPLAY 'NO OF BYPASS (ALREADY PASS TO SMS).......... : '            
                    WS-BYPASS-SEND.                                             
           DISPLAY 'NO OF BYPASS(DTE-TP = SPACES OR NOT <= CUR-MTH):'           
                    WS-BYPASS-TOP-DATE.                                         
           DISPLAY 'NO OF BYPASS(NOT FOUND IN KD00 OR K800) .... : '            
                    WS-BYPASS-NOTFND-KD00-K800.                                 
           DISPLAY 'NO OF BP13F400 RECORDS WRITTEN.............. : '            
                    WS-F400-WRITE.                                              
           DISPLAY 'NO OF BP13KD15 UPDATE ...................... : '            
                    WS-KD15-UPDATE.                                             
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF BP13K800 RECORDS FOUND................ : '            
                    WS-K800-FOUND.                                              
           DISPLAY 'NO OF BP13K800 RECORDS NOT FOUND............ : '            
                    WS-K800-NOTFND.                                             
           DISPLAY 'NO OF BP13K820 RECORDS FOUND................ : '            
                    WS-K820-FOUND.                                              
           DISPLAY 'NO OF BP13K820 RECORDS NOT FOUND............ : '            
                    WS-K820-NOTFND.                                             
           DISPLAY 'NO OF BP13KD00 RECORDS FOUND................ : '            
                    WS-KD00-FOUND.                                              
           DISPLAY 'NO OF BP13KD00 RECORDS NOT FOUND............ : '            
                    WS-KD00-NOTFND.                                             
           DISPLAY 'NO OF BP13KD35 RECORDS FOUND................ : '            
                    WS-KD35-FOUND.                                              
           DISPLAY 'NO OF BP13KD35 RECORDS NOT FOUND............ : '            
                    WS-KD35-NOTFND.                                             
           DISPLAY 'NO OF BP13KD80 RECORDS FOUND................ : '            
                    WS-KD80-FOUND.                                              
           DISPLAY 'NO OF BP13KD80 RECORDS NOT FOUND............ : '            
                    WS-KD80-NOTFND.                                             
                                                                                
           CLOSE BP13FD15                                                       
                 SY02F001                                                       
                 BP13K800                                                       
                 BP13K820                                                       
                 BP13KD00                                                       
                 BP13KD15                                                       
                 BP13KD35                                                       
                 BP13KD65                                                       
                 BP13KA10                                                       
                 BP13KD80                                                       
                 BP13F400                                                       
                 BP13LD40.                                                      
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR CLOSING : '                             
                      WS-K800-STATUS                                            
              MOVE WS-K800-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K820 - ERROR CLOSING : '                             
                      WS-K820-STATUS                                            
              MOVE WS-K820-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD00 - ERROR CLOSING : '                             
                      WS-KD00-STATUS                                            
              MOVE WS-KD00-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KD15-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD15 - ERROR CLOSING : '                             
                      WS-KD15-STATUS                                            
              MOVE WS-KD15-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KD35-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD35 - ERROR CLOSING : '                             
                      WS-KD35-STATUS                                            
              MOVE WS-KD35-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KA10-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KA10 - ERROR CLOSING : '                             
                      WS-KA10-STATUS                                            
              MOVE WS-KA10-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K030-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K030 - ERROR CLOSING : '                             
                      WS-K030-STATUS                                            
              MOVE WS-K030-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KD65-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD65 - ERROR CLOSING : '                             
                      WS-KD65-STATUS                                            
              MOVE WS-KD65-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KD80-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KD80 - ERROR CLOSING : '                             
                      WS-KD80-STATUS                                            
              MOVE WS-KD80-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
