      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C26C.                                                 
       AUTHOR.        ELAINE S ARGA.                                            
       DATE-WRITTEN.  FEBRUARY 6 2018.                                          
      *===============================================================*         
      *   OBJECTIVE:  PRINT DEBARMENT REPORT FOR CASES DEBARRED USING *         
      *               THE EAPG PORTAL DEBARMENT PAGE(BP13IRY3)        *         
      *===============================================================*         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13F936                                  *         
      *                  2. P04K010                                   *         
      *                  3. BP13K800                                  *         
      *                  4. BM06K110                                  *         
      *                                                               *         
      *     OUTPUT FILE: 1. BP13L26C                                  *         
      *                  2. BP13K42B                                  *         
      *                  3. BP13K936                                  *         
      *                  4. BP13K937                                  *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP137196 06/02/2018 ESA1 NEW PROGRAM                          *         
      * BP137074 21/11/2018 FNP1 MODIFY REPORT TO ONLY INCLUDE TP     *         
      *                          CASES WITH DISCREPANCIES IN DEBARMENT          
      * BP137887 14/05/2019 FNP1 UNCOMMENTED OUT WRITING TO BP13K42B  *         
      *                          ADD WRITE TO BP13K937 TO BE USED FOR *         
      *                          ENQUIRY PAGE                         *         
      * BP137887 03/09/2019 KR13 REPORT FIX.                          *         
      * BP138002 27/09/2019 KR13 CHK KEY-ISS IF 0 DEL K936 & SKIP.    *         
      * BP138444 20/10/2020 KV5  INCLUDED SUPERVISOR ID IN K936 FILE  *         
      *                          ADDED NEW FIELD IN L26C REPORT       *         
      * BP138452 02/11/2020 KV5  INCLUDED CHECKING K800-DTE-SALE-AGMT *         
      * BP138383 04/11/2020 KV5  CHNAGED FROM K800-DTE-SALE-AGMT TO   *         
      *                          K110-DTE-KEY-ISSUED                  *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F936  ASSIGN        TO BP13F936.                         
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
           SELECT  P04K010   ASSIGN        TO P04K010                           
                             ACCESS MODE  IS DYNAMIC                            
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K010-PRIME-KEY                     
                             FILE STATUS  IS WS-K010-STATUS.                    
                                                                                
           SELECT  BM06K110  ASSIGN        TO BM06K110                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K110-KEY-FLD                      
                             FILE STATUS   IS WS-K110-STATUS.                   
                                                                                
           SELECT  BP13K42B  ASSIGN        TO BP13K42B                          
                             ACCESS MODE  IS DYNAMIC                            
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K42B-KEY-FLD                       
                             FILE STATUS  IS WS-K42B-STATUS.                    
                                                                                
           SELECT  BP13K936  ASSIGN        TO BP13K936                          
                             ACCESS MODE  IS DYNAMIC                            
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS WS-K936-KEY-FLD                    
                             FILE STATUS  IS WS-K936-STATUS.                    
                                                                                
           SELECT  BP13K937  ASSIGN        TO BP13K937                          
                             ACCESS MODE  IS DYNAMIC                            
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K937-KEY-FLD                       
                             FILE STATUS  IS WS-K937-STATUS.                    
                                                                                
           SELECT  BP13L26C  ASSIGN        TO BP13L26C.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F936                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13K936.                                                           
                                                                                
       FD   P04K010                                                             
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY P04K010.                                                            
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP13K42B                                                            
            RECORD CONTAINS 600 CHARACTERS.                                     
       COPY BP13K42B.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K936                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       01   WS-BP13K936-REC.                                                    
            05 WS-K936-KEY-FLD.                                                 
               10 WS-K936-NUM-REGN          PIC X(08).                          
               10 WS-K936-NUM-PROCESS       PIC X(03).                          
               10 FILLER                    PIC X(02).                          
            05 FILLER                       PIC X(1987).                        
                                                                                
       FD   BP13K937                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K937.                                                           
                                                                                
       FD   BP13L26C                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   BP13L26C-REC               PIC X(200).                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F936             PIC X(01) VALUE 'N'.                     
           05  WS-EOF-K010             PIC X(01) VALUE 'N'.                     
           05  WS-KEY-ISSD             PIC X(01) VALUE SPACES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F936-READ            PIC 9(07) VALUE ZEROES.                  
           05  WS-P04KCODE-NFND        PIC 9(07) VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(07) VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(02) VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(04) VALUE ZEROES.                  
           05  WS-CNT-L26C-WRITE       PIC 9(07) VALUE ZEROES.                  
           05  WS-CNT-K42B-WRITE       PIC 9(07) VALUE ZEROES.                  
           05  WS-CNT-K937-WRITE       PIC 9(07) VALUE ZEROES.                  
           05  WS-CNT-P04K             PIC 9(07) VALUE ZEROES.                  
           05  WS-CNT-K936-DELETE      PIC 9(07) VALUE ZEROES.                  
           05  WS-CTR                  PIC 9(02) VALUE ZEROES.                  
           05  WS-CTR-1                PIC 9(02) VALUE ZEROES.                  
           05  CNT-DEB                 PIC 9(02) VALUE ZEROES.                  
           05  WS-I                    PIC 9(02) VALUE ZEROES.                  
           05  WS-MAX-LINE             PIC 9(02) VALUE 55.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-SYSTEM-DATE           PIC 9(08) VALUE ZEROES.                  
           05 WS-SYSTEM-TIME           PIC 9(08) VALUE ZEROES.                  
           05 WS-REGN-NO               PIC X(08) VALUE SPACES.                  
           05 WS-SNO-ZZZZ9             PIC ZZZZ9 VALUE ZEROES.                  
           05 WS-DEBAR-TAGGED          PIC X(01) VALUE SPACES.                  
           05 WS-DEBAR-DIFF            PIC X(01) VALUE SPACES.                  
           05 WS-K010-STATUS           PIC 9(02) VALUE ZEROES.                  
           05 WS-K110-STATUS           PIC 9(02) VALUE ZEROES.                  
           05 WS-K42B-STATUS           PIC 9(02) VALUE ZEROES.                  
           05 WS-K800-STATUS           PIC 9(02) VALUE ZEROES.                  
           05 WS-K936-STATUS           PIC 9(02) VALUE ZEROES.                  
           05 WS-K937-STATUS           PIC 9(02) VALUE ZEROES.                  
           05 WS-PREV-NUM-REGN         PIC X(08) VALUE SPACES.                  
           05 WS-PPS-ELIG              PIC X(01) VALUE SPACES.                  
           05 WS-HH-TYPE               PIC X(02) VALUE SPACES.                  
           05 WS-1ST-IC                PIC X(01) VALUE 'Y'.                     
           05 WS-1ST-DEB               PIC X(01) VALUE 'Y'.                     
           05 WS-DEB-DIF               PIC X(01) VALUE 'Y'.                     
           05  WS-DEBAR-TAB OCCURS 8 TIMES.                                     
               10  WS-NUM-DEBAR-BK          PIC X(03).                          
               10  WS-NUM-DEBAR-AUTO        PIC X(03).                          
               10  WS-NUM-DEBAR-STAFF       PIC X(01).                          
               10  WS-NUM-DEBSYS-UPDATE     PIC X(01).                          
                                                                                
       01  WS-DATE.                                                             
           05  WS-CCYY                 PIC X(04) VALUE SPACES.                  
           05  WS-MM                   PIC X(02) VALUE SPACES.                  
           05  WS-DD                   PIC X(02) VALUE SPACES.                  
                                                                                
       01  L26C-HEADER01.                                                       
           05  FILLER                  PIC X(08) VALUE 'BP13L26C'.              
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  FILLER                  PIC X(08) VALUE 'HDBCAT 3'.              
           05  FILLER                  PIC X(24) VALUE SPACES.                  
           05  FILLER                  PIC X(20) VALUE                          
               'S Y S T E M   O F   '.                                          
           05  FILLER                  PIC X(19) VALUE                          
               'C O M M I T M E N T'.                                           
           05  FILLER                  PIC X(15) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE 'DATE : '.               
           05  L26C-DATE-HDR           PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE 'PAGE : '.               
           05  L26C-PAGE-HDR           PIC ZZZ9  VALUE ZEROES.                  
                                                                                
       01  L26C-HEADER02.                                                       
           05  FILLER                  PIC X(36) VALUE SPACES.                  
           05  FILLER                  PIC X(48) VALUE                          
               'EXCEPTION REPORT FOR MULTIPLE DEBARMENT AT KEYS '.              
           05  FILLER                  PIC X(10) VALUE                          
               'COLLECTION'.                                                    
                                                                                
       01  L26C-HEADER03.                                                       
           05  FILLER                  PIC X(08) VALUE SPACES.                  
           05  FILLER                  PIC X(11) VALUE 'REGN NO '.              
           05  FILLER                  PIC X(09) VALUE 'HH-CTZ'.                
           05  FILLER                  PIC X(10) VALUE 'ALLO-SCH'.              
           05  FILLER                  PIC X(10) VALUE 'ELIG-SCH'.              
           05  FILLER                  PIC X(09) VALUE 'HH-TYPE'.               
           05  FILLER                  PIC X(07) VALUE 'PPS'.                   
           05  FILLER                  PIC X(09) VALUE 'NRIC'.                  
           05  FILLER                  PIC X(13) VALUE 'DEBAR AT BK'.           
           05  FILLER                PIC X(18) VALUE 'DEBAR TAG BY SYS'.        
           05  FILLER              PIC X(20) VALUE 'DEBAR TAG BY STAFF'.        
           05  FILLER                  PIC X(14) VALUE 'UPD IN DEB SYS'.        
           05  FILLER                  PIC X(14) VALUE ' SUPERVISOR ID'.        
           05  FILLER                  PIC X(48) VALUE SPACES.                  
                                                                                
       01  L26C-DETAIL01.                                                       
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  L26C-SN                 PIC X(05) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  L26C-REGN               PIC X(08) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  L26C-CTZ-HH             PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE SPACES.                  
           05  L26C-ALLO-SCH           PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE SPACES.                  
           05  L26C-ELIG-SCH           PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  L26C-HH-TYPE            PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  L26C-PPS                PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  L26C-NRIC               PIC X(09) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE SPACES.                  
           05  L26C-PREV-DEBAR         PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(11) VALUE SPACES.                  
           05  L26C-DEBAR-TYPE         PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(17) VALUE SPACES.                  
           05  L26C-K936-DEB-TAG       PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(18) VALUE SPACES.                  
           05  L26C-BP04-DEB-TAG       PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE SPACES.                  
           05  L26C-K936-SUP           PIC X(08) VALUE SPACES.                  
           05  FILLER                  PIC X(52) VALUE SPACES.                  
                                                                                
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F936      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F936 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F936                                                 
                       P04K010                                                  
                       BM06K110                                                 
                       BP13K800                                                 
                I-O    BP13K42B                                                 
                       BP13K936                                                 
                       BP13K937                                                 
              OUTPUT   BP13L26C.                                                
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'P04K010  - ERROR OPENING : ' WS-K010-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K42B-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K42B - ERROR OPENING : ' WS-K42B-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR OPENING : ' WS-K110-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K936-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K936 - ERROR OPENING : ' WS-K936-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K937-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K937 - ERROR OPENING : ' WS-K937-STATUS              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO WS-SYSTEM-DATE.                  
           MOVE FUNCTION CURRENT-DATE(9:8)  TO WS-SYSTEM-TIME.                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F936.                                                      
      ******************************************************************        
                                                                                
           READ BP13F936           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F936                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO   WS-F936-READ.                           
                                                                                
           IF WS-F936-READ = 1                                                  
              MOVE K936-NUM-REGN   TO   WS-PREV-NUM-REGN                        
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
           MOVE 'N'                TO WS-DEBAR-DIFF.                            
           MOVE ZEROES             TO CNT-DEB.                                  
                                                                                
           PERFORM 3100-CHECK-DTE-KEY-ISS    THRU 3100-EXIT.                    
                                                                                
           IF WS-KEY-ISSD = 'N'                                                 
              PERFORM 2000-READ-BP13F936     THRU 2000-EXIT                     
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM VARYING WS-I   FROM 1 BY 1 UNTIL WS-I > 8                    
              MOVE SPACES TO        WS-DEBAR-TAB(WS-I)                          
           END-PERFORM.                                                         
                                                                                
           IF (K936-NUM-REGN NOT = SPACES AND LOW-VALUES) AND                   
              (K936-NUM-PROCESS = 'TP ')                                        
              MOVE 'Y'                     TO WS-1ST-IC                         
              PERFORM 9000-READ-BP13K800    THRU 9000-EXIT                      
              PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 12              
                                                                                
               IF K936-NUM-NRIC(WS-CTR) NOT = SPACES AND LOW-VALUES             
                MOVE 'Y'                       TO WS-1ST-DEB                    
                MOVE 'N'                       TO WS-DEB-DIF                    
                PERFORM VARYING WS-CTR-1 FROM 1 BY 1 UNTIL WS-CTR-1 > 8         
                                                                                
                  IF K936-NUM-DEBAR-TYPE(WS-CTR WS-CTR-1) NOT = SPACES          
                                                  AND LOW-VALUES                
                     PERFORM 4000-START-P04K010   THRU 4000-EXIT                
                  ELSE                                                          
                     MOVE 9  TO  WS-CTR-1                                       
                  END-IF                                                        
                END-PERFORM                                                     
                PERFORM 8500-WRITE-BP13K937    THRU 8500-EXIT                   
                MOVE 0 TO CNT-DEB                                               
                PERFORM VARYING WS-I   FROM 1 BY 1 UNTIL WS-I > 8               
                   MOVE SPACES TO        WS-DEBAR-TAB(WS-I)                     
                END-PERFORM                                                     
               ELSE                                                             
                MOVE 13   TO  WS-CTR                                            
               END-IF                                                           
              END-PERFORM                                                       
              IF WS-DEBAR-DIFF = 'Y'                                            
                 PERFORM 8000-WRITE-BP13K42B    THRU 8000-EXIT                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE K936-NUM-REGN             TO   WS-PREV-NUM-REGN.                
                                                                                
           PERFORM 2000-READ-BP13F936     THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-START-P04K010.                                                      
      ******************************************************************        
           MOVE 'N'                           TO WS-DEBAR-TAGGED                
                                                 WS-EOF-K010.                   
           MOVE SPACES                        TO P04K010-REC.                   
           INITIALIZE                            P04K010-REC.                   
           MOVE K936-NUM-NRIC(WS-CTR)         TO K010-NUM-UINFIN.               
                                                                                
           START P04K010  KEY = K010-NUM-UINFIN.                                
                                                                                
           EVALUATE WS-K010-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 4100-READNEXT-P04K010  THRU 4100-EXIT               
                            UNTIL WS-EOF-K010 = 'Y'                             
                    PERFORM 5000-PRINT-REPORT      THRU 5000-EXIT               
               WHEN 20                                                          
               WHEN 23                                                          
                    MOVE SPACES                    TO K010-REASON-CODE          
                    PERFORM 5000-PRINT-REPORT      THRU 5000-EXIT               
               WHEN OTHER                                                       
                    DISPLAY 'START ERROR P04K010 : ' WS-K010-STATUS             
                            ' NRIC : ' K010-NUM-UINFIN                          
                    PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT               
            END-EVALUATE.                                                       
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       4100-READNEXT-P04K010.                                                   
      ******************************************************************        
                                                                                
           READ P04K010 NEXT RECORD                                             
                         AT END MOVE 'Y' TO WS-EOF-K010.                        
                                                                                
           IF (K010-NUM-UINFIN = K936-NUM-NRIC(WS-CTR))                         
              IF K010-REASON-CODE = K936-NUM-DEBAR-TYPE(WS-CTR WS-CTR-1)        
                 IF K110-DTE-KEY-ISSUED = K010-EFF-DATE                         
                    MOVE 'Y'                TO WS-DEBAR-TAGGED                  
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              MOVE 'Y'                       TO WS-EOF-K010                     
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       5000-PRINT-REPORT.                                                       
      ******************************************************************        
           IF WS-DEBAR-TAGGED = 'Y'                                             
              GO TO 5000-EXIT                                                   
           ELSE                                                                 
              MOVE 'Y' TO WS-DEBAR-DIFF WS-DEB-DIF                              
           END-IF.                                                              
                                                                                
           PERFORM 6000-CHECK-WRITE-HEADER   THRU 6000-EXIT.                    
                                                                                
           MOVE SPACES                       TO L26C-DETAIL01.                  
           INITIALIZE                           L26C-DETAIL01.                  
                                                                                
           IF WS-1ST-IC = 'N'                                                   
              MOVE SPACES                   TO L26C-SN                          
                                               L26C-REGN                        
                                               L26C-CTZ-HH                      
           ELSE                                                                 
               ADD 1                        TO WS-SNO                           
               MOVE 'N'                     TO WS-1ST-IC                        
               MOVE WS-SNO                  TO WS-SNO-ZZZZ9                     
               MOVE WS-SNO-ZZZZ9            TO L26C-SN                          
               MOVE K936-NUM-REGN           TO L26C-REGN                        
               MOVE K936-CDE-HH-CITIZENSHIP TO L26C-CTZ-HH                      
               MOVE K936-NUM-ALLO-SCH       TO L26C-ALLO-SCH                    
               MOVE K936-NUM-ELIG-SCH       TO L26C-ELIG-SCH                    
               MOVE WS-HH-TYPE              TO L26C-HH-TYPE                     
               MOVE WS-PPS-ELIG             TO L26C-PPS                         
           END-IF.                                                              
                                                                                
           IF WS-1ST-DEB = 'N'                                                  
              MOVE SPACES                   TO L26C-NRIC                        
           ELSE                                                                 
              MOVE K936-NUM-NRIC(WS-CTR)    TO L26C-NRIC                        
              MOVE 'N'                      TO WS-1ST-DEB                       
           END-IF.                                                              
                                                                                
           ADD 1 TO CNT-DEB.                                                    
                                                                                
           MOVE K936-NUM-DEBAR-TYPE(WS-CTR WS-CTR-1)                            
                                        TO L26C-DEBAR-TYPE                      
                                           WS-NUM-DEBAR-AUTO(CNT-DEB).          
           MOVE K936-NUM-DEBAR-OLD(WS-CTR  WS-CTR-1)                            
                                        TO L26C-PREV-DEBAR                      
                                           WS-NUM-DEBAR-BK(CNT-DEB).            
           MOVE K936-NUM-DEBAR-TAGGED(WS-CTR WS-CTR-1)                          
                                        TO L26C-K936-DEB-TAG                    
                                           WS-NUM-DEBAR-STAFF(CNT-DEB).         
           MOVE WS-DEBAR-TAGGED         TO L26C-BP04-DEB-TAG                    
                                           WS-NUM-DEBSYS-UPDATE(CNT-DEB)        
           MOVE K936-SUPERVISOR-ID      TO L26C-K936-SUP                        
                                                                                
           WRITE BP13L26C-REC                   FROM L26C-DETAIL01.             
                                                                                
           ADD 1    TO WS-LINE-CTR.                                             
           ADD 1    TO WS-CNT-L26C-WRITE.                                       
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-CHECK-DTE-KEY-ISS.                                                  
      ******************************************************************        
                                                                                
           MOVE SPACES                      TO K110-REC                         
                                            WS-BP13K936-REC WS-KEY-ISSD.        
           MOVE K936-SCH-ACC                TO K110-KEY-FLD                     
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
                    IF K110-DTE-KEY-ISSUED = ZEROES OR SPACES                   
                                                    OR LOW-VALUES               
                       MOVE 'N'                TO WS-KEY-ISSD                   
                    END-IF                                                      
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BM06K110 : ' WS-K110-STATUS          
                            ' SCHA-ACC : ' K110-KEY-FLD                         
           END-EVALUATE.                                                        
                                                                                
           IF WS-KEY-ISSD = 'N'                                                 
              MOVE K936-KEY-FLD                TO WS-K936-KEY-FLD               
                                                                                
              READ BP13K936                                                     
                                                                                
              EVALUATE WS-K936-STATUS                                           
                  WHEN 00                                                       
                                                                                
                       DELETE BP13K936                                          
                                                                                
                       IF WS-K936-STATUS NOT = 00                               
                          DISPLAY 'ERROR DELETING BP13K936 : '                  
                                WS-K936-STATUS ' KEY : ' K936-KEY-FLD           
                          PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT           
                       END-IF                                                   
                       ADD 1                TO WS-CNT-K936-DELETE               
                  WHEN OTHER                                                    
                       DISPLAY 'ERROR READING BP13K936 : '                      
                               WS-K936-STATUS ' KEY : ' K936-KEY-FLD            
               END-EVALUATE                                                     
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       6000-CHECK-WRITE-HEADER.                                                 
      ******************************************************************        
                                                                                
           IF WS-LINE-CTR = 0                                                   
              PERFORM 7000-PRINT-HEADER      THRU 7000-EXIT                     
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       7000-PRINT-HEADER.                                                       
      ******************************************************************        
                                                                                
           ADD 1                               TO WS-PAGE-CTR.                  
           MOVE ZEROES                         TO L26C-PAGE-HDR.                
           MOVE WS-PAGE-CTR                    TO L26C-PAGE-HDR.                
                                                                                
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
                  DELIMITED BY SIZE INTO L26C-DATE-HDR                          
           END-STRING.                                                          
                                                                                
           WRITE BP13L26C-REC    FROM L26C-HEADER01.                            
           WRITE BP13L26C-REC    FROM L26C-HEADER02.                            
           WRITE BP13L26C-REC    FROM L26C-HEADER03.                            
                                                                                
           INITIALIZE WS-LINE-CTR.                                              
           MOVE 4  TO WS-LINE-CTR.                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       8000-WRITE-BP13K42B.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES             TO BP13K42B-REC.                             
           INITIALIZE                 BP13K42B-REC.                             
           MOVE 'ED'               TO K42B-NUM-REPORT.                          
           MOVE WS-SYSTEM-DATE     TO K42B-DTE-CREATE                           
                                      K42B-DTE-UPDATE.                          
           MOVE K936-NUM-REGN      TO K42B-NUM-REGN.                            
           MOVE 'BP13C26C'         TO K42B-NUM-USERID.                          
           MOVE WS-SYSTEM-TIME     TO K42B-TME-UPDATE.                          
           MOVE K936-SCH-ACC       TO K42B-NUM-SCH-ACCT.                        
                                                                                
           WRITE BP13K42B-REC.                                                  
                                                                                
           EVALUATE WS-K42B-STATUS                                              
                                                                                
           WHEN 00                                                              
              ADD 1 TO WS-CNT-K42B-WRITE                                        
           WHEN 02                                                              
           WHEN 22                                                              
              DISPLAY ' RECORD EXISTS IN BP13K42B FOR ' K42B-NUM-REGN           
           WHEN OTHER                                                           
              DISPLAY ' ERROR WRITING TO BP13K42B, KEY IS '                     
                       K42B-KEY-FLD ' STATUS IS ' WS-K42B-STATUS                
           END-EVALUATE.                                                        
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       8500-WRITE-BP13K937.                                                     
      ******************************************************************        
           IF WS-DEB-DIF = 'N'                                                  
              GO TO 8500-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE SPACES                  TO BP13K937-REC.                        
           INITIALIZE                      BP13K937-REC.                        
                                                                                
           MOVE K936-NUM-REGN           TO K937-NUM-REGN.                       
           MOVE K936-NUM-NRIC(WS-CTR)   TO K937-NUM-NRIC.                       
           MOVE K936-NUM-PROCESS        TO K937-NUM-PROCESS.                    
           MOVE K936-SCH-ACC            TO K937-NUM-SCH-ACC.                    
                                                                                
           PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > CNT-DEB                
               MOVE WS-NUM-DEBAR-BK(WS-I)  TO K937-NUM-DEBAR-BK(WS-I)           
               MOVE WS-NUM-DEBAR-AUTO (WS-I) TO                                 
                                           K937-NUM-DEBAR-AUTO(WS-I)            
               MOVE WS-NUM-DEBAR-STAFF(WS-I) TO                                 
                                           K937-NUM-DEBAR-STAFF(WS-I)           
               MOVE WS-NUM-DEBSYS-UPDATE (WS-I) TO                              
                                           K937-NUM-DEBSYS-UPDATE(WS-I)         
           END-PERFORM.                                                         
                                                                                
           MOVE WS-SYSTEM-DATE     TO K937-DTE-UPDATE.                          
           MOVE 'BP13C26C'         TO K937-USER-ID.                             
           MOVE WS-SYSTEM-TIME     TO K937-TME-UPDATE.                          
                                                                                
           WRITE BP13K937-REC.                                                  
                                                                                
           EVALUATE WS-K937-STATUS                                              
                                                                                
           WHEN 00                                                              
              ADD 1 TO WS-CNT-K937-WRITE                                        
           WHEN 02                                                              
           WHEN 22                                                              
              DISPLAY ' RECORD EXISTS IN BP13K937 FOR ' K937-KEY-FLD            
           WHEN OTHER                                                           
              DISPLAY ' ERROR WRITING TO BP13K937, KEY IS '                     
                       K937-KEY-FLD ' STATUS IS ' WS-K937-STATUS                
           END-EVALUATE.                                                        
                                                                                
       8500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       9000-READ-BP13K800.                                                      
      ******************************************************************        
                                                                                
           MOVE K936-NUM-REGN               TO K800-NUM-REGN                    
                                                                                
           READ BP13K800                                                        
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    MOVE K800-NUM-PPS-ELIG  TO WS-PPS-ELIG                      
                    MOVE K800-NUM-HOUSEHOLD TO WS-HH-TYPE                       
               WHEN OTHER                                                       
                    MOVE SPACES             TO WS-PPS-ELIG                      
                                               WS-HH-TYPE                       
                    DISPLAY 'ERROR  READING BP13K800. STATUS IS '               
                         WS-K800-STATUS ', KEY: ' K936-NUM-REGN                 
           END-EVALUATE.                                                        
                                                                                
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C26C'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F936 RECORDS READ............. : '            
                    WS-F936-READ.                                               
           DISPLAY '(2) NO OF S/N ROWS FOUND.................... : '            
                    WS-SNO.                                                     
           DISPLAY '(3) NO OF BP13L26C RECORDS WRITTEN.......... : '            
                    WS-CNT-L26C-WRITE.                                          
           DISPLAY '(4) NO OF RECORDS WRITTEN BP13K42B.......... : '            
                    WS-CNT-K42B-WRITE.                                          
           DISPLAY '(5) NO OF RECORDS WRITTEN BP13K937.......... : '            
                    WS-CNT-K937-WRITE.                                          
           DISPLAY '(6) NO OF RECORDS DELETED BP13K936.......... : '            
                    WS-CNT-K936-DELETE.                                         
                                                                                
           CLOSE    BP13F936                                                    
                    P04K010                                                     
                    BM06K110                                                    
                    BP13K42B                                                    
                    BP13K800                                                    
                    BP13K936                                                    
                    BP13K937                                                    
                    BP13L26C.                                                   
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'P04K010  - ERROR CLOSING : ' WS-K010-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K42B-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K42B - ERROR CLOSING : ' WS-K42B-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR CLOSING : ' WS-K110-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR CLOSING : ' WS-K800-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K936-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K936 - ERROR CLOSING : ' WS-K936-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K937-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K937 - ERROR CLOSING : ' WS-K937-STATUS              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
