       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C693.                                                 
       AUTHOR.        LSB                                                       
       DATE-WRITTEN.  17/03/98.                                                 
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : PROGRAM TO UPDATE MTH-RESIDE AFTER    *                
      *                  ADJUSTMENT FORM BP13C639 TO GET THE   *                
      *                  SHORTEST MTH-RESIDE TO BP13C640 TO GET*                
      *                  THE LONGEST MTH-RESIDE                *                
      *                                                        *                
      *    INPUT FILE  : BP13F558                              *                
      *                : BP13F520                              *                
      *                                                        *                
      *   OUTPUT FILE  : P13F558O                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY   DATE     DETAILS                          *                
      * ------------------------------                         *                
      * BP130445 LSB 980327 - NEW PGM                          *                
      * BP130494 LSB 980528 - F558O-REC X(400)                 *                
      * BP130652 NOR 990220 - EXPAND F520 RECL FROM 300 TO 500 *                
      * BP130897 AKT 000804 - EXPAND F558 RECL FROM 400 TO 600 *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
           SELECT P13F558O ASSIGN TO P13F558O.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F558                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F558.                                                           
                                                                                
       FD  BP13F520                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F520.                                                           
                                                                                
       FD  P13F558O                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 600 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01  P13F558O-REC                   PIC X(600).                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F558-READ                PIC 9(8)    VALUE ZEROS.              
           05 WS-F520-READ                 PIC 9(8)    VALUE ZEROS.             
           05 WS-MR                       PIC 9(8)    VALUE 0.                  
           05 WS-F520-NMR                 PIC 9(8)    VALUE 0.                  
           05 WS-F558-NMR                 PIC 9(8)    VALUE 0.                  
           05 WS-OUTPUT-CNT               PIC 9(8)    VALUE 0.                  
                                                                                
       01  WS-EOF-F558                    PIC 9.                                
           88 EOF-F558                    VALUE 1.                              
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
                                                                                
       000-MAIN.                                                                
           OPEN INPUT  BP13F520                                                 
                       BP13F558                                                 
                OUTPUT P13F558O.                                                
                                                                                
           PERFORM 100-READ-F520-REC THRU 100-EXIT.                             
           PERFORM 110-READ-F558-REC THRU 110-EXIT.                             
           PERFORM 200-PROCESS-REC   THRU 200-EXIT                              
                   UNTIL F520-NUM-REGN = HIGH-VALUE                             
                     AND F558-NUM-REGN = HIGH-VALUE.                            
           PERFORM 900-CLOSE-ROUTINE.                                           
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-READ-F520-REC.                                                       
           READ BP13F520 AT END MOVE HIGH-VALUE TO F520-NUM-REGN                
                GO TO 100-EXIT.                                                 
           ADD 1 TO WS-F520-READ.                                               
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
      *                                                                         
       110-READ-F558-REC.                                                       
           READ BP13F558 AT END MOVE HIGH-VALUE TO F558-NUM-REGN                
                GO TO 110-EXIT.                                                 
           ADD 1 TO WS-F558-READ.                                               
                                                                                
       110-EXIT.                                                                
           EXIT.                                                                
      *                                                                         
       200-PROCESS-REC.                                                         
           IF F520-NUM-REGN = F558-NUM-REGN                                     
               ADD 1 TO WS-MR                                                   
               PERFORM 300-WRITE-OUTPUT THRU 300-EXIT                           
                   UNTIL F558-NUM-REGN > F520-NUM-REGN                          
               PERFORM 100-READ-F520-REC THRU 100-EXIT                          
                                                                                
           ELSE                                                                 
               IF  F558-NUM-REGN > F520-NUM-REGN                                
                   ADD 1 TO WS-F520-NMR                                         
                   PERFORM 100-READ-F520-REC THRU 100-EXIT                      
                                                                                
               ELSE                                                             
                   ADD 1 TO WS-F558-NMR                                         
                   WRITE P13F558O-REC FROM BP13F558-REC                         
                   ADD 1 TO WS-OUTPUT-CNT                                       
                   PERFORM 110-READ-F558-REC THRU 110-EXIT.                     
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-WRITE-OUTPUT.                                                        
                                                                                
               IF F558-NUM-MTH-RESIDE IS NOT NUMERIC                            
                   MOVE ZEROS TO F558-NUM-MTH-RESIDE                            
               END-IF.                                                          
                                                                                
               IF F520-NUM-MTH-RESIDE IS NOT NUMERIC                            
                   MOVE ZEROS TO F520-NUM-MTH-RESIDE                            
               END-IF.                                                          
                                                                                
               IF F558-NUM-REGN = F520-NUM-REGN                                 
                  IF F558-NUM-MTH-RESIDE NOT = F520-NUM-MTH-RESIDE              
                     MOVE  F520-NUM-MTH-RESIDE TO F558-NUM-MTH-RESIDE.          
                                                                                
                MOVE BP13F558-REC TO P13F558O-REC.                              
                WRITE P13F558O-REC.                                             
                ADD 1 TO WS-OUTPUT-CNT.                                         
                                                                                
               PERFORM 110-READ-F558-REC THRU 110-EXIT.                         
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
                                                                                
           DISPLAY 'PROGRAM-ID : BP13C693 '.                                    
           DISPLAY 'UPDATE MTH-RESIDE AFTER ADJUSTMENT    '.                    
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM F520 ' WS-F520-READ.                
           DISPLAY 'NO OF RECORDS READ FROM F558 ' WS-F558-READ.                
           DISPLAY 'NO OF REORDS WRITE OUT F558O ' WS-OUTPUT-CNT.               
           DISPLAY 'NO OF RECORDS MATCH          ' WS-MR.                       
           DISPLAY 'NO OF F520 RECORDS NOT MATCH ' WS-F520-NMR.                 
           DISPLAY 'NO OF F558 RECORDS NOT MATCH ' WS-F558-NMR.                 
                                                                                
           CLOSE BP13F558                                                       
                 P13F558O                                                       
                 BP13F520.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
