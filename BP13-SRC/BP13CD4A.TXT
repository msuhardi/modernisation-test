       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD4A.                                                 
      *AUTHOR.        RALSTON JAMES ELIEZER B MEDRANO.                          
      *DATE-WRITTEN.  21/07/11.                                                 
      *****************************************************************         
      *OBJECTIVE - PRINT DBSS TOP LETTER TOGETHER PARTICULAR FORM     *         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13F800                                  *         
      *                  2. BP13K820                                  *         
      *                  3. BP13K830                                  *         
      *                                                               *         
      *     OUTPUT FILE: 1. P13PD4AO  (BP13IO)                        *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP134313 21/07/2011 RJE1 NEW PROGRAM.                         *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *-------------------------------------------------------------            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800  ASSIGN        TO BP13F800.                          
                                                                                
           SELECT BP13K820  ASSIGN        TO BP13K820                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K820-KEY-FLD                       
                            FILE STATUS   IS WS-K820-STATUS.                    
                                                                                
           SELECT BP13K830  ASSIGN        TO BP13K830                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K830-KEY-FLD                       
                            ALTERNATE KEY IS K830-NUM-REGN                      
                            FILE STATUS   IS WS-K830-STATUS.                    
                                                                                
           SELECT P13PD4AO  ASSIGN        TO P13PD4AO.                          
           SELECT BP13LD4A  ASSIGN        TO BP13LD4A.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BP13F800                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F800.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD   BP13K830                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K830.                                                           
                                                                                
       FD  P13PD4AO                                                             
           RECORDING MODE  IS  F.                                               
       01  PD4AO-PRTREC                     PIC X(110).                         
                                                                                
       FD  BP13LD4A                                                             
           RECORDING MODE  IS  F.                                               
       01  LD4A-PRTREC                      PIC X(132).                         
                                                                                
                                                                                
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-STATUS.                                                           
           05  WS-K820-STATUS               PIC 99    VALUE ZEROS.              
           05  WS-K830-STATUS               PIC 99    VALUE ZEROS.              
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F800-READ-CTR             PIC 9(06)  VALUE ZEROS.             
           05  WS-PD4A-WRT-CTR              PIC 9(06)  VALUE ZEROS.             
           05  WS-CNT-PAGE-LD4A             PIC 9(04)  VALUE ZEROS.             
           05  WS-CNT-LINE-LD4A             PIC 9(06)  VALUE ZEROS.             
           05  WS-CNT-CUR-LINE              PIC 9(02)  VALUE 56.                
           05  WS-CTR                       PIC 9(03)  VALUE ZEROS.             
           05  WS-SKIP                      PIC 9(06)  VALUE ZEROS.             
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F800-EOF                  PIC X     VALUE 'N'.                
           05  WS-K800-FND                  PIC X     VALUE 'N'.                
           05  WS-K820-NME-OCCP             PIC X(66) VALUE SPACES.             
           05  WS-K820-NME-HA1              PIC X(66) VALUE SPACES.             
           05  WS-RETCODE                   PIC S9(8)                           
                                            SIGN IS LEADING SEPARATE.           
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-DATE.                                                         
               10  WS-CCYY                  PIC 9(4)  VALUE 0000.               
               10  WS-MM                    PIC 99    VALUE 00.                 
               10  WS-DD                    PIC 99    VALUE 00.                 
           05  WS-CURR-DATE                 PIC 9(8)  VALUE ZEROES.             
           05  WS-NUM-DATE                  PIC X(10) VALUE SPACES.             
                                                                                
       01  WS-ADDRESS.                                                          
           05 FILLER                     PIC X(3)  VALUE 'BLK'.                 
           05 FILLER                     PIC X(1)  VALUE SPACES.                
           05 CD4A-BLK-NO                PIC X(10) VALUE SPACES.                
           05 FILLER                     PIC X     VALUE SPACES.                
           05 CD4A-FLAT-NO.                                                     
              10 FILLER                  PIC X     VALUE '#'.                   
              10 CD4A-LVL-NO             PIC X(2)  VALUE SPACES.                
              10 FILLER                  PIC X     VALUE '-'.                   
              10 CD4A-UNIT-NO            PIC X(7)  VALUE SPACES.                
           05 FILLER                     PIC X(1)  VALUE SPACES.                
           05 CD4A-STREET                PIC X(47) VALUE SPACES.                
           05 FILLER                     PIC X(1)  VALUE SPACES.                
           05 CD4A-SINGAPORE             PIC X(09) VALUE 'SINGAPORE'.           
           05 FILLER                     PIC X(1)  VALUE SPACES.                
           05 CD4A-POSTAL-NO             PIC X(06) VALUE SPACES.                
                                                                                
       01  WS-PARSING.                                                          
           05 WS-STRING-TO-PARSE            PIC X(130).                         
           05 WS-PARSED-STRING              PIC X(130).                         
           05 WS-STRING-LENGTH              PIC 9(03).                          
           05 WS-CUR-POS-SP                 PIC 9(03).                          
           05 WS-CUR-POS-PS                 PIC 9(03).                          
           05 WS-SPACE-FLAG                 PIC X    VALUE 'N'.                 
           05 WS-INSERT-SPACE-FLAG          PIC X    VALUE 'N'.                 
                                                                                
      *-------------------------------------------------------------            
      * BP13C913 PARAMETERS                                                     
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH-ACC.                                              
              10 WS-LINK-NUM-SCH       PIC X(4).                                
              10 WS-LINK-NUM-ACC       PIC X(5).                                
       COPY P13COMM8.                                                           
      *===============================                                          
      *   LINKAGE SECTION FOR AV02C015                                          
      *===============================                                          
       01  WS-LINK-AV02C015.                                                    
           05  AV02C015-IN-POSTCODE             PIC X(6).                       
           05  AV02C015-OUT-POSTCODE-CHKSUM     PIC X(9).                       
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR LETTER BP13IO                                        
      *------------------------------------------------------------*            
       01  PD4AO-LINE01.                                                        
           05  FILLER                   PIC X(72) VALUE SPACES.                 
           05  PD4AO-DATE               PIC X(10) VALUE SPACES.                 
                                                                                
       01  PD4AO-LINE02.                                                        
           05  FILLER                   PIC X(72) VALUE SPACES.                 
           05  PD4AO-NUM-REGN           PIC X(08) VALUE SPACES.                 
                                                                                
       01  PD4AO-LINE03.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  PD4AO-NAME               PIC X(66) VALUE SPACES.                 
                                                                                
       01  PD4AO-LINE04.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  PD4AO-NAME2              PIC X(66) VALUE SPACES.                 
                                                                                
       01  PD4AO-LINE05.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  PD4AO-BLK                PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  PD4AO-UNIT               PIC X(11) VALUE SPACES.                 
                                                                                
       01  PD4AO-LINE06.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  PD4AO-ADDR-L2            PIC X(47) VALUE SPACES.                 
                                                                                
       01  PD4AO-LINE07.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  PD4AO-ADDR-L3            PIC X(20) VALUE SPACES.                 
                                                                                
       01  PD4AO-LINE08.                                                        
           05  FILLER                   PIC X(10) VALUE SPACES.                 
           05  PD4AO-BAR-CODE           PIC X(09) VALUE SPACES.                 
                                                                                
       01  PD4AO-LINE09.                                                        
           05  FILLER                   PIC X(11) VALUE SPACES.                 
           05  CD4A-CORR-ADDR           PIC X(110) VALUE SPACES.                
                                                                                
      *------------------------------------------------------------*            
      *        LAY-OUT FOR CONTROL LISTING                         *            
      *------------------------------------------------------------*            
       01  LD4A-HDG1.                                                           
           05  LD4A-LISTING             PIC X(14) VALUE 'BP13LD4A'.             
           05  FILLER                   PIC X(8)  VALUE 'HDB3'.                 
           05  FILLER                   PIC X(18) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(18) VALUE SPACES.                 
           05  FILLER                   PIC X(7)  VALUE 'DATE: '.               
           05  LD4A-DATE                PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(8)  VALUE ' PAGE: '.              
           05  LD4A-PAGE                PIC ZZZZ9.                              
                                                                                
       01  LD4A-HDG2.                                                           
           05  FILLER                   PIC X(41) VALUE SPACES.                 
           05  FILLER                   PIC X(37) VALUE                         
               'CONTROL LIST FOR NOTIFICATION OF TOP '.                         
                                                                                
       01  LD4A-HDG3.                                                           
           05  FILLER                   PIC X(4)  VALUE SPACES.                 
           05  FILLER                   PIC X(5)  VALUE '  S/N'.                
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X(8)  VALUE 'REGN    '.             
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X(09) VALUE 'NRIC1'.                
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X(30) VALUE 'NAME1'.                
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  FILLER                   PIC X(18) VALUE 'ADDRESS'.              
                                                                                
       01  LD4A-DTL.                                                            
           05  FILLER                   PIC X(4)  VALUE SPACES.                 
           05  LD4A-SN                  PIC ZZZZ9 VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  LD4A-REGN                PIC X(8)  VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  LD4A-NRIC1               PIC X(09) VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  LD4A-NAME1               PIC X(30) VALUE SPACES.                 
           05  FILLER                   PIC X(2)  VALUE SPACES.                 
           05  LD4A-PIDB-ADDR           PIC X(26) VALUE SPACES.                 
                                                                                
      *=============================================================            
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-FILES             THRU 1000-EXIT.                  
           PERFORM 3000-PROCESS-RECORDS        THRU 3000-EXIT                   
                   UNTIL WS-F800-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE          THRU 9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-FILES.                                                         
      *=============================================================            
                                                                                
           OPEN INPUT BP13F800                                                  
                      BP13K820                                                  
                      BP13K830                                                  
               OUTPUT P13PD4AO                                                  
                      BP13LD4A.                                                 
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K820 STATUS ' WS-K820-STATUS               
              MOVE     WS-K820-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K830 STATUS ' WS-K830-STATUS               
              MOVE     WS-K830-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE.                                       
                                                                                
                                                                                
           MOVE '27/07/2011'             TO PD4AO-DATE                          
                                            LD4A-DATE.                          
                                                                                
           PERFORM 2000-READ-BP13F800  THRU 2000-EXIT.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       2000-READ-BP13F800.                                                      
      *=============================================================            
                                                                                
            READ BP13F800 AT END                                                
                 MOVE 'Y'  TO  WS-F800-EOF                                      
                 GO TO 2000-EXIT.                                               
                                                                                
            ADD  1  TO WS-F800-READ-CTR.                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-PROCESS-RECORDS.                                                    
      *=============================================================            
                                                                                
           INITIALIZE     PD4AO-LINE02 PD4AO-LINE03                             
                          PD4AO-LINE04 PD4AO-LINE05 PD4AO-LINE06                
                          PD4AO-LINE07 PD4AO-LINE08 PD4AO-LINE09                
                                                                                
           MOVE F800-NUM-REGN               TO PD4AO-NUM-REGN                   
           MOVE F800-NUM-SCH-ACC(1:9)       TO WS-LINK-NUM-SCH-ACC              
                                                                                
           MOVE F800-NUM-REGN               TO K820-NUM-REGN                    
           MOVE F800-NUM-NRIC1              TO K820-NUM-NRIC                    
           PERFORM 4600-READ-K820         THRU 4600-EXIT                        
           MOVE WS-K820-NME-OCCP            TO PD4AO-NAME                       
                                               WS-K820-NME-HA1.                 
                                                                                
           IF (F800-NUM-NRIC2  = SPACES OR LOW-VALUES) or                       
              F800-NUM-NRIC2(1:1) = '#'                                         
              MOVE SPACES                   TO PD4AO-NAME2                      
           ELSE                                                                 
              MOVE F800-NUM-NRIC2           TO K820-NUM-NRIC                    
              PERFORM 4600-READ-K820      THRU 4600-EXIT                        
              MOVE WS-K820-NME-OCCP         TO PD4AO-NAME2                      
           END-IF.                                                              
                                                                                
           PERFORM 4800-GET-ADDR          THRU 4800-EXIT.                       
           PERFORM 6000-READ-PIDB         THRU 6000-EXIT.                       
                                                                                
           PERFORM 7000-PRT-LETTER        THRU 7000-EXIT                        
           PERFORM 8000-PRN-CNTRL-LIST    THRU 8000-EXIT.                       
                                                                                
           PERFORM 2000-READ-BP13F800     THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       4600-READ-K820.                                                          
      *=============================================================            
                                                                                
           MOVE    SPACES                TO  WS-K820-NME-OCCP                   
           READ BP13K820.                                                       
                                                                                
           IF WS-K820-STATUS = 00 OR 02                                         
              MOVE    K820-NME-OCCP      TO  WS-K820-NME-OCCP                   
           ELSE                                                                 
           IF WS-K820-STATUS = 23                                               
              DISPLAY 'K820 REC NOT FOUND : '   K820-KEY-FLD                    
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K820 = '   WS-K820-STATUS              
                      ', K820-KEY-FLD = '   K820-KEY-FLD                        
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT.                      
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       4800-GET-ADDR.                                                           
      *=============================================================            
                                                                                
           MOVE F800-NUM-REGN TO K830-NUM-REGN                                  
           READ BP13K830  KEY IS K830-NUM-REGN.                                 
                                                                                
           IF WS-K830-STATUS = 00                                               
              IF (K830-NUM-MAIL-ADDR =  SPACES OR LOW-VALUES) OR                
                 ((K830-NUM-MAIL-LVL-CHAR =  '#') AND                           
                 (K830-NUM-MAIL-LVL-NUM =  SPACES OR LOW-VALUES))               
                 MOVE K830-NUM-BLK         TO PD4AO-BLK                         
                 IF K830-NUM-LVL-NUM = SPACES AND                               
                    K830-NUM-HSEMAIN = SPACES                                   
                    MOVE SPACES            TO PD4AO-UNIT                        
                 ELSE                                                           
                    MOVE K830-NUM-FLAT     TO PD4AO-UNIT                        
                 END-IF                                                         
                 MOVE K830-NME-STREET-BLDG(1:47) TO PD4AO-ADDR-L2               
                 IF K830-NUM-POSTAL  = SPACES OR LOW-VALUES OR ZEROES           
                    MOVE SPACES            TO PD4AO-ADDR-L3                     
                    DISPLAY ' RES POSTAL CDE IS ' K830-NUM-POSTAL               
                            ' FOR REC KEY-FLD ' K830-KEY-FLD                    
                 ELSE                                                           
                    STRING 'SINGAPORE '  K830-NUM-POSTAL                        
                       DELIMITED BY SIZE INTO PD4AO-ADDR-L3                     
                 END-IF                                                         
      *------ TO GET THE BAR CODE ------                                        
                 MOVE SPACES                    TO WS-LINK-AV02C015             
                 MOVE K830-NUM-POSTAL           TO AV02C015-IN-POSTCODE         
                 PERFORM 4850-CALL-AV02C015     THRU 4850-EXIT                  
                 MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PD4AO-BAR-CODE            
                                                                                
              ELSE                                                              
                 MOVE K830-NUM-MAIL-BLK        TO PD4AO-BLK                     
                 IF K830-NUM-MAIL-LVL-NUM = SPACES AND                          
                    K830-NUM-MAIL-HSEMAIN = SPACES                              
                    MOVE SPACES                TO PD4AO-UNIT                    
                 ELSE                                                           
                    MOVE K830-NUM-MAIL-FLAT    TO PD4AO-UNIT                    
                 END-IF                                                         
                 MOVE K830-NME-MAIL-STR-BLDG(1:47) TO PD4AO-ADDR-L2             
                 IF K830-NUM-MAIL-POSTAL = SPACES OR LOW-VALUES OR ZEROS        
                    MOVE SPACES                TO PD4AO-ADDR-L3                 
                    DISPLAY ' MAIL POSTAL CDE IS ' K830-NUM-POSTAL              
                            ' FOR REC KEY-FLD ' K830-KEY-FLD                    
                 ELSE                                                           
                    STRING 'SINGAPORE '      K830-NUM-MAIL-POSTAL               
                           DELIMITED BY SIZE INTO PD4AO-ADDR-L3                 
                 END-IF                                                         
      *------ TO GET THE BAR CODE ------                                        
                 MOVE SPACES                    TO WS-LINK-AV02C015             
                 MOVE K830-NUM-MAIL-POSTAL      TO AV02C015-IN-POSTCODE         
                 PERFORM 4850-CALL-AV02C015     THRU 4850-EXIT                  
                 MOVE AV02C015-OUT-POSTCODE-CHKSUM TO PD4AO-BAR-CODE            
              END-IF                                                            
                                                                                
           ELSE                                                                 
           IF WS-K830-STATUS = 23                                               
              INITIALIZE     PD4AO-LINE05                                       
                             PD4AO-LINE06                                       
                             PD4AO-LINE07                                       
                             PD4AO-LINE08                                       
              DISPLAY 'BP13K830 RECORD NOT FOUND = ' WS-K830-STATUS             
                      ',  K830-KEY-FLD IS ' K830-NUM-REGN                       
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K830 = ' WS-K830-STATUS                
                      ',  K830-KEY-FLD IS ' K830-NUM-REGN                       
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
                                                                                
       4800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       4850-CALL-AV02C015.                                                      
      *=============================================================            
                                                                                
           CALL 'AV02C015'          USING                                       
                                    AV02C015-IN-POSTCODE                        
                                    AV02C015-OUT-POSTCODE-CHKSUM.               
       4850-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-READ-PIDB.                                                          
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                  TO CD4A-BLK-NO                          
                                           CD4A-LVL-NO                          
                                           CD4A-UNIT-NO                         
                                           CD4A-STREET                          
                                           CD4A-POSTAL-NO                       
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
           WHEN +000                                                            
              MOVE COMM8-NUM-BLK        TO CD4A-BLK-NO                          
              MOVE COMM8-NUM-LEVEL      TO CD4A-LVL-NO                          
              STRING COMM8-NUM-UNIT-MAIN                                        
                     COMM8-NUM-UNIT-SUB                                         
                     DELIMITED BY SIZE INTO CD4A-UNIT-NO                        
              MOVE COMM8-NME-STREET         TO CD4A-STREET                      
              MOVE COMM8-NUM-POSTAL-CODE    TO CD4A-POSTAL-NO                   
              MOVE SPACES                 TO WS-STRING-TO-PARSE                 
              MOVE WS-ADDRESS             TO WS-STRING-TO-PARSE                 
              MOVE LENGTH OF WS-ADDRESS                                         
                                          TO WS-STRING-LENGTH                   
              MOVE 'Y'                    TO WS-INSERT-SPACE-FLAG               
              PERFORM 6010-PARSE-ADDRESS  THRU 6010-EXIT                        
              MOVE WS-PARSED-STRING       TO  CD4A-CORR-ADDR                    
           WHEN +100                                                            
              MOVE SPACES   TO BP13COMM8-REC                                    
              INITIALIZE       BP13COMM8-REC                                    
              DISPLAY 'REC NOT FOUND IN PIDB: ' WS-LINK-NUM-SCH-ACC             
           WHEN OTHER                                                           
              MOVE COMM8-CDE-SYSERR      TO WS-RETCODE                          
              DISPLAY 'ERROR IN CALLING BP13C913, RETCODE: '                    
                      WS-RETCODE                                                
              MOVE 99                    TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6010-PARSE-ADDRESS.                                                      
      *--------------------------------------------------------------           
                                                                                
           MOVE SPACES                    TO   WS-PARSED-STRING.                
           MOVE 1                         TO   WS-CUR-POS-PS.                   
           ADD  1                         TO   WS-STRING-LENGTH.                
           PERFORM 6020-GET-CHAR          THRU 6020-EXIT                        
              VARYING WS-CUR-POS-SP FROM 1 BY 1                                 
              UNTIL WS-CUR-POS-SP = WS-STRING-LENGTH.                           
                                                                                
                                                                                
       6010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------          
       6020-GET-CHAR.                                                           
      *---------------------------------------------------------------          
                                                                                
           IF WS-STRING-TO-PARSE(WS-CUR-POS-SP:1) NOT = SPACES                  
              IF WS-SPACE-FLAG = 'Y' AND                                        
                 WS-INSERT-SPACE-FLAG = 'Y' AND                                 
                 WS-PARSED-STRING NOT = SPACES                                  
                 MOVE ' ' TO WS-PARSED-STRING(WS-CUR-POS-PS:1)                  
                 ADD 1           TO  WS-CUR-POS-PS                              
              END-IF                                                            
              MOVE WS-STRING-TO-PARSE(WS-CUR-POS-SP:1)                          
                              TO  WS-PARSED-STRING(WS-CUR-POS-PS:1)             
              ADD 1           TO  WS-CUR-POS-PS                                 
              MOVE    'N'      TO WS-SPACE-FLAG                                 
           ELSE                                                                 
              MOVE 'Y'     TO WS-SPACE-FLAG                                     
           END-IF.                                                              
                                                                                
       6020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       7000-PRT-LETTER.                                                         
      *=============================================================            
                                                                                
      * BP13IO                                                                  
            MOVE SPACES           TO   PD4AO-PRTREC.                            
            WRITE PD4AO-PRTREC AFTER PAGE.                                      
                                                                                
            WRITE PD4AO-PRTREC    FROM  PD4AO-LINE01 AFTER 2.                   
            WRITE PD4AO-PRTREC    FROM  PD4AO-LINE02.                           
            WRITE PD4AO-PRTREC    FROM  PD4AO-LINE03 AFTER 3.                   
            WRITE PD4AO-PRTREC    FROM  PD4AO-LINE04.                           
            WRITE PD4AO-PRTREC    FROM  PD4AO-LINE05.                           
            WRITE PD4AO-PRTREC    FROM  PD4AO-LINE06.                           
            WRITE PD4AO-PRTREC    FROM  PD4AO-LINE07.                           
            WRITE PD4AO-PRTREC    FROM  PD4AO-LINE08 AFTER 4.                   
            WRITE PD4AO-PRTREC    FROM  PD4AO-LINE09 AFTER 6.                   
                                                                                
            ADD   1               TO   WS-PD4A-WRT-CTR.                         
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *=============================================================            
       8000-PRN-CNTRL-LIST.                                                     
      *=============================================================            
                                                                                
           INITIALIZE LD4A-DTL.                                                 
                                                                                
           MOVE F800-NUM-REGN             TO LD4A-REGN                          
           MOVE F800-NUM-NRIC1            TO LD4A-NRIC1                         
           MOVE WS-K820-NME-HA1           TO LD4A-NAME1                         
           STRING 'BLK' COMM8-NUM-BLK                                           
                  ' # ' COMM8-NUM-LEVEL                                         
                  ' - ' COMM8-NUM-UNIT-MAIN COMM8-NUM-UNIT-SUB                  
              DELIMITED BY SIZE INTO LD4A-PIDB-ADDR.                            
           ADD 1                          TO WS-CNT-LINE-LD4A.                  
           ADD 1                          TO WS-CNT-CUR-LINE.                   
           MOVE WS-CNT-LINE-LD4A          TO LD4A-SN                            
           IF WS-CNT-CUR-LINE > 55                                              
              ADD 1                       TO WS-CNT-PAGE-LD4A                   
              MOVE WS-CNT-PAGE-LD4A       TO LD4A-PAGE                          
              WRITE LD4A-PRTREC FROM LD4A-HDG1 AFTER PAGE                       
              WRITE LD4A-PRTREC FROM LD4A-HDG2 AFTER 2                          
              WRITE LD4A-PRTREC FROM LD4A-HDG3 AFTER 3                          
              MOVE 6                      TO WS-CNT-CUR-LINE                    
           END-IF                                                               
                                                                                
           WRITE LD4A-PRTREC FROM LD4A-DTL.                                     
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       9000-CLOSE-ROUTINE.                                                      
      **************************************************************            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY ' ====  BP13CD4A CONTROL TOTALS  ======'.                    
           DISPLAY ' '.                                                         
           DISPLAY ' F800 RECORDS READ          : ' WS-F800-READ-CTR.           
           DISPLAY ' NO OF CASES PRINTED        : ' WS-PD4A-WRT-CTR.            
           DISPLAY ' NO OF CASES WRITTEN        : ' WS-CNT-LINE-LD4A.           
           DISPLAY ' '.                                                         
           DISPLAY ' ====================================='.                    
                                                                                
           CLOSE BP13F800                                                       
                 BP13K820                                                       
                 BP13K830                                                       
                 P13PD4AO                                                       
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13K820-STAT ' WS-K820-STATUS.            
                                                                                
           IF WS-K830-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR BP13K830-STAT ' WS-K830-STATUS.            
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
