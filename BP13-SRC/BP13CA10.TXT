       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CA10.                                                 
      *AUTHOR.        BALA AMUTHAN.                                             
      *DATE-WRITTEN.  27 SEP 2006.                                              
      *============================================================*            
      * BP13 - SYSTEM OF COMMITMENT                                *            
      *============================================================*            
      * OBJECTIVE :  CHECK CATEGORY OF DBSS BLOCKS BOOKED          *            
      *                                                            *            
      * INPUT     :  BP13KA20                                      *            
      *              BP13KA30                                      *            
      * OUTPUT    :  BP13MAIL                                      *            
      *                                                            *            
      *----------------------------------------------------------- *            
      * CHG REF  BY    DATE      DESCRIPTION                       *            
      * -------- ----  --------  ------------------------------    *            
      * BP132876 BA2   20060927  NEW PROGRAM.                      *            
      * BP132876 BA2   20061122  CONVERT LISTING TO EMAIL          *            
      * BP133157 IL5   20080221  REMOVE CKK7, ADD IL5              *            
      * BP133509 DW5   20090115  REMOVE LHH2, ADD WLY1 and DW5     *            
      *============================================================*            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *-------------------------------------------------------------            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13FA20 ASSIGN TO BP13FA20.                                  
                                                                                
           SELECT BP13FA30 ASSIGN TO BP13FA30.                                  
                                                                                
           SELECT BP13MAIL ASSIGN TO BP13MAIL.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13FA20                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13KA20.                                                           
                                                                                
       FD  BP13FA30                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13KA30.                                                           
                                                                                
       FD  BP13MAIL                                                             
           RECORD CONTAINS 80 CHARACTERS                                        
           RECORDING  MODE  IS F.                                               
       01  MAIL-PRTREC                   PIC X(80).                             
                                                                                
                                                                                
      *------------------------------------------------------------*            
       WORKING-STORAGE SECTION.                                                 
      *------------------------------------------------------------*            
       01  WS-DATE                       PIC X(8)   VALUE SPACES.               
       01  WS-SNO                        PIC 9(5)   VALUE ZEROS.                
                                                                                
       01  WS-COUNT.                                                            
           05  WS-READ-FA20              PIC 9(5) COMP VALUE ZEROES.            
           05  WS-READ-FA30              PIC 9(5) COMP VALUE ZEROES.            
           05  WS-WRIT-LA10              PIC 9(5) COMP VALUE ZEROES.            
                                                                                
       01  WS-SWITCH.                                                           
           05  WS-FA20-END               PIC X(01)   VALUE 'N'.                 
               88  CS-FA20-END                       VALUE 'Y'.                 
           05  WS-FA30-END               PIC X(01)   VALUE 'N'.                 
               88  CS-FA30-END                       VALUE 'Y'.                 
           05  WS-HEAD-WRITE             PIC X(01)   VALUE 'N'.                 
               88  CS-HEAD-WRITE                     VALUE 'Y'.                 
                                                                                
       01  WS-FA30-KEY.                                                         
           05  WS-FA30-NUM-PRJCT         PIC X(3)    VALUE SPACES.      00110002
           05  WS-FA30-NUM-BLK           PIC X(5)    VALUE SPACES.      00130002
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-CAT1-TOT           PIC 9(3)    VALUE ZEROES.              
           05  WS-NUM-CAT2-TOT           PIC 9(3)    VALUE ZEROES.              
           05  WS-NUM-CAT3-TOT           PIC 9(3)    VALUE ZEROES.              
                                                                                
      *-------------------------------------------------------------            
      * LAYOUT BP13MAIL  -  DATASET FOR EMAIL                                   
      *-------------------------------------------------------------            
       01  MAIL-HDR1.                                                           
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  MAIL-HDR2.                                                           
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  MAIL-SENDID     PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
      *  for testing purpose -------                                            
      *01  MAIL-HDR1.                                                           
      *    05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB3'.                 
      *    05  FILLER          PIC X(68)  VALUE SPACES.                         
      *                                                                         
      *01  MAIL-HDR2.                                                           
      *    05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
      *    05  MAIL-SENDID     PIC X(07)  VALUE 'CSXXXXX'.                      
      *    05  FILLER          PIC X(09)  VALUE '@SGPHDB3>'.                    
      *    05  FILLER          PIC X(53)  VALUE SPACES.                         
      *                                                                         
       01  MAIL-DTL1.                                                           
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  MAILRCPTTOID    PIC X(17)  VALUE SPACES.                         
           05  FILLER          PIC X(58)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL2.                                                           
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(16)  VALUE                                 
               'KKL1@HDB.GOV.SG>'.                                              
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL2B.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(16)  VALUE                                 
               'WLY1@HDB.GOV.SG>'.                                              
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL2C.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(16)  VALUE                                 
               'LSB1@HDB.GOV.SG>'.                                              
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL2D.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(16)  VALUE                                 
               'CT2@HDB.GOV.SG>'.                                               
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL2E.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(16)  VALUE                                 
               'CCC5@HDB.GOV.SG>'.                                              
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL2F.                                                          
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
           05  FILLER          PIC X(16)  VALUE                                 
               'IL5@HDB.GOV.SG>'.                                               
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL3.                                                           
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4.                                                           
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  MAIL-SENDMAILID PIC X(37)  VALUE                                 
               'SOC System - Email Alert@hdb.gov.sg'.                           
           05  FILLER          PIC X(38)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5.                                                           
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  MAILTOID        PIC X(17)  VALUE SPACES.                         
           05  FILLER          PIC X(58)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL6.                                                           
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(16)  VALUE                                 
               'KKL1@HDB.GOV.SG>'.                                              
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL6B.                                                          
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(16)  VALUE                                 
               'WLY1@HDB.GOV.SG>'.                                              
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL6C.                                                          
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(16)  VALUE                                 
               'LSB1@HDB.GOV.SG>'.                                              
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL6D.                                                          
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(16)  VALUE                                 
               'CT2@HDB.GOV.SG>'.                                               
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL6E.                                                          
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(16)  VALUE                                 
               'CCC5@HDB.GOV.SG>'.                                              
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL6F.                                                          
           05  FILLER          PIC X(4)   VALUE 'TO:<'.                         
           05  FILLER          PIC X(16)  VALUE                                 
               'IL5@HDB.GOV.SG>'.                                               
           05  FILLER          PIC X(45)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL7.                                                           
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  MAIL-SUBJECT    PIC X(40)  VALUE                                 
               'DBSS QUOTA NOT TALLY, PLEASE CHECK      '.                      
           05  FILLER          PIC X(30)  VALUE SPACES.                         
                                                                                
       01  MAIL-BODY01.                                                         
           05 FILLER                   PIC X(6)   VALUE 'DATE: '.               
           05 MAIL-DATE                PIC X(10)  VALUE SPACES.                 
                                                                                
       01  MAIL-BODY02.                                                         
           05  FILLER                      PIC X(05)  VALUE SPACES.             
           05  MAIL-PRJECT                 PIC X(03)  VALUE SPACES.             
           05  FILLER                      PIC X(01)  VALUE SPACES.             
           05  MAIL-BLK                    PIC X(05)  VALUE SPACES.             
           05  FILLER                      PIC X(03)  VALUE SPACES.             
           05  MAIL-FA20-DES               PIC X(12)  VALUE SPACES.             
           05  MAIL-FA20-CAT               PIC Z(04)9 VALUE ZEROES.             
           05  FILLER                      PIC X(04)  VALUE SPACES.             
           05  MAIL-FA30-DES               PIC X(12)  VALUE SPACES.             
           05  MAIL-FA30-CAT               PIC Z(04)9 VALUE ZEROES.             
           05  FILLER                      PIC X(77)  VALUE SPACES.             
                                                                                
       01  MAIL-END.                                                            
           05 FILLER                   PIC X      VALUE '.'.                    
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER                   PIC X(80)  VALUE SPACES.                 
                                                                                
                                                                                
      *------------------------------------------------------------*            
       PROCEDURE DIVISION.                                                      
      *------------------------------------------------------------*            
       0000-MAIN.                                                               
      *------------------------------------------------------------*            
           PERFORM 1000-INIT-ROUTINE  THRU 1000-EXIT.                           
           PERFORM 2000-READ-FA20     THRU 2000-EXIT.                           
           PERFORM 3000-READ-FA30     THRU 3000-EXIT.                           
           PERFORM 4000-MAIN-PROCESS  THRU 4000-EXIT                            
                   UNTIL CS-FA30-END.                                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-INIT-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT BP13FA20                                                  
                      BP13FA30                                                  
               OUTPUT BP13MAIL.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DATE.                          
           STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO MAIL-DATE.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-FA20.                                                          
      *-------------------------------------------------------------            
           READ BP13FA20 AT END MOVE 'Y' TO WS-FA20-END                         
                                MOVE HIGH-VALUES TO  KA20-KEY-FLD       00110002
                                GO TO 2000-EXIT.                                
                                                                                
           ADD 1 TO WS-READ-FA20.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-READ-FA30.                                                          
      *-------------------------------------------------------------            
           READ BP13FA30 AT END MOVE 'Y' TO WS-FA30-END                         
                                MOVE HIGH-VALUES TO WS-FA30-KEY         00110002
                                GO TO 3000-EXIT.                                
                                                                                
           MOVE KA30-NUM-PRJCT  TO WS-FA30-NUM-PRJCT.                   00110002
           MOVE KA30-NUM-BLK    TO WS-FA30-NUM-BLK.                     00130002
           ADD 1 TO WS-READ-FA30.                                               
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------------------------------------------*            
       4000-MAIN-PROCESS.                                                       
      *------------------------------------------------------------*            
           MOVE ZEROES         TO  WS-NUM-CAT1-TOT                              
                                   WS-NUM-CAT2-TOT                              
                                   WS-NUM-CAT3-TOT.                             
                                                                                
           IF KA20-KEY-FLD = WS-FA30-KEY                                        
              PERFORM 5000-ACCUMULATE-CAT       THRU 5000-EXIT                  
                      UNTIL KA20-KEY-FLD NOT = WS-FA30-KEY OR                   
                            CS-FA30-END                                         
              PERFORM 5200-CHECK-TOT-CAT        THRU 5200-EXIT                  
           ELSE                                                                 
             IF KA20-KEY-FLD > WS-FA30-KEY                                      
                PERFORM 3000-READ-FA30          THRU 3000-EXIT                  
             ELSE                                                               
               IF KA20-KEY-FLD < WS-FA30-KEY                                    
                  PERFORM 2000-READ-FA20        THRU 2000-EXIT                  
               END-IF                                                           
             END-IF                                                             
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *------------------------------------------------------------*            
       5000-ACCUMULATE-CAT.                                                     
      *------------------------------------------------------------*            
           EVALUATE KA30-NUM-ETHNIC                                             
              WHEN '1'                                                          
                  ADD +1     TO   WS-NUM-CAT1-TOT                               
                                                                                
              WHEN '2'                                                          
                  ADD +1     TO   WS-NUM-CAT2-TOT                               
                                                                                
              WHEN '3'                                                          
              WHEN '4'                                                          
                  ADD +1     TO   WS-NUM-CAT3-TOT                               
           END-EVALUATE.                                                        
                                                                                
           PERFORM 3000-READ-FA30        THRU 3000-EXIT.                        
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *------------------------------------------------------------*            
       5200-CHECK-TOT-CAT.                                                      
      *------------------------------------------------------------*            
            MOVE 'N'                TO WS-HEAD-WRITE.                           
                                                                                
            IF KA20-NUM-TOT-CAT1 NOT NUMERIC                                    
               MOVE ZEROES TO KA20-NUM-TOT-CAT1                                 
            END-IF.                                                             
                                                                                
            IF KA20-NUM-TOT-CAT2 NOT NUMERIC                                    
               MOVE ZEROES TO KA20-NUM-TOT-CAT2                                 
            END-IF.                                                             
                                                                                
            IF KA20-NUM-TOT-CAT3 NOT NUMERIC                                    
               MOVE ZEROES TO KA20-NUM-TOT-CAT3                                 
            END-IF.                                                             
                                                                                
            IF KA20-NUM-TOT-CAT1 NOT = WS-NUM-CAT1-TOT                          
               MOVE 'KA20-CAT1 : '    TO MAIL-FA20-DES                          
               MOVE KA20-NUM-TOT-CAT1 TO MAIL-FA20-CAT                          
               MOVE 'KA30-CAT1 : '    TO MAIL-FA30-DES                          
               MOVE WS-NUM-CAT1-TOT   TO MAIL-FA30-CAT                          
               PERFORM 5400-WRITE-REPORT     THRU 5400-EXIT                     
            END-IF.                                                             
                                                                                
            IF KA20-NUM-TOT-CAT2 NOT = WS-NUM-CAT2-TOT                          
               MOVE 'KA20-CAT2 : '    TO MAIL-FA20-DES                          
               MOVE KA20-NUM-TOT-CAT2 TO MAIL-FA20-CAT                          
               MOVE 'KA30-CAT2 : '    TO MAIL-FA30-DES                          
               MOVE WS-NUM-CAT2-TOT   TO MAIL-FA30-CAT                          
               PERFORM 5400-WRITE-REPORT     THRU 5400-EXIT                     
            END-IF.                                                             
                                                                                
            IF KA20-NUM-TOT-CAT3 NOT = WS-NUM-CAT3-TOT                          
               MOVE 'KA20-CAT3 : '    TO MAIL-FA20-DES                          
               MOVE KA20-NUM-TOT-CAT3 TO MAIL-FA20-CAT                          
               MOVE 'KA30-CAT3 : '    TO MAIL-FA30-DES                          
               MOVE WS-NUM-CAT3-TOT   TO MAIL-FA30-CAT                          
               PERFORM 5400-WRITE-REPORT     THRU 5400-EXIT                     
            END-IF.                                                             
                                                                                
       5200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *------------------------------------------------------------*            
       5400-WRITE-REPORT.                                                       
      *------------------------------------------------------------*            
           ADD 1 TO WS-SNO.                                                     
                                                                                
           IF WS-SNO = 1                                                        
              PERFORM 6000-OUTPUT-EMAIL-HEADER THRU 6000-EXIT                   
           END-IF.                                                              
                                                                                
           IF NOT CS-HEAD-WRITE                                                 
              MOVE KA20-NUM-PRJCT     TO MAIL-PRJECT                            
              MOVE KA20-NUM-BLK       TO MAIL-BLK                               
              ADD +1                  TO WS-WRIT-LA10                           
              MOVE 'Y'                TO WS-HEAD-WRITE                          
           END-IF.                                                              
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-BODY02.                                  
           MOVE SPACES                TO MAIL-BODY02                            
                                         MAIL-PRTREC.                           
                                                                                
       5400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       6000-OUTPUT-EMAIL-HEADER.                                                
      *-------------------------------------------------------------            
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-HDR1                                     
           WRITE MAIL-PRTREC FROM MAIL-HDR2.                                    
                                                                                
           WRITE MAIL-PRTREC FROM MAIL-DTL2.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL2B.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL2C.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL2D.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL2E.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL2F.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL3.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL4.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL6.                                    
           WRITE MAIL-PRTREC FROM MAIL-DTL6B.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL6C.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL6D.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL6E.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL6F.                                   
           WRITE MAIL-PRTREC FROM MAIL-DTL7.                                    
           WRITE MAIL-PRTREC FROM MAIL-BODY01.                                  
           MOVE  SPACES      TO   MAIL-PRTREC.                                  
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       7000-OUTPUT-EMAIL-FOOTER.                                                
      *-------------------------------------------------------------            
                                                                                
           MOVE  SPACES      TO   MAIL-PRTREC.                                  
           WRITE MAIL-PRTREC FROM MAIL-SPACES.                                  
           WRITE MAIL-PRTREC FROM MAIL-END.                                     
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           IF WS-SNO > 0                                                        
              PERFORM 7000-OUTPUT-EMAIL-FOOTER THRU 7000-EXIT                   
           END-IF.                                                              
                                                                                
           DISPLAY '- BP13CA10 CONTROL TOTAL -----'.                            
           DISPLAY 'NO OF RECORDS READ    (BP13FA20) : '                        
                                               WS-READ-FA20.                    
           DISPLAY 'NO OF RECORDS READ    (BP13FA30) : '                        
                                               WS-READ-FA30.                    
           DISPLAY 'NO OF RECORDS WRITTEN (BP13MAIL) : '                        
                                               WS-WRIT-LA10.                    
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13FA20                                                       
                 BP13FA30                                                       
                 BP13MAIL.                                                      
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
