       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C02T.                                                 
       AUTHOR.        ZARITA SARMIENTO.                                         
      *DATE-WRITTEN.  26/03/2009.                                               
      * =========================================================== *           
      *              SYSTEM OF COMMITMENT (BP13)                    *           
      * =========================================================== *           
      *   OBJECTIVE  :  TO GENERATE REPORT ON RECORDS HAVING        *           
      *                 DIFFERENT HOUSEHOLD AND MCPS ELIGIBILITY    *           
      *                 IN BP13KB20 AND BP13F730                    *           
      *                                                             *           
      *   INPUT      :  BP13F730 -- SEQUENTIAL FILE                 *           
      *                 BP13KB20 -- VSAM       FILE                 *           
      *   OUTPUT     :  BP13L02T                                    *           
      *                                                             *           
      *   AMENDMENTS:                                               *           
      *    REF      DATE     BY          DESCRIPTIONS               *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP133542 26/03/2009 ZDD1 NEW PROGRAM                        *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F730 ASSIGN TO BP13F730                                   
                  FILE STATUS IS WS-BP13F730-STATUS.                            
                                                                                
           SELECT BP13L02T ASSIGN TO BP13L02T                                   
                  FILE STATUS IS WS-BP13L02T-STATUS.                            
                                                                                
           SELECT BP13KB20  ASSIGN       TO BP13KB20                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS KB20-KEY-FLD                        
                            FILE STATUS  IS WS-BP13KB20-STATUS.                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F730       BLOCK  CONTAINS 0 RECORDS                             
                          RECORD CONTAINS 500 CHARACTERS                        
                          LABEL RECORDS ARE STANDARD                            
                          RECORDING MODE IS F.                                  
       COPY BP13F730.                                                           
                                                                                
       FD   BP13KB20                                                            
            RECORD CONTAINS 3300 CHARACTERS.                                    
       COPY BP13KB20.                                                           
                                                                                
                                                                                
       FD   BP13L02T                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE IS F.                                                
                                                                                
       01   BP13L02T-REC           PIC X(132).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  BLANKLINE               PIC X(132).                                  
                                                                                
       01 WS-DATE01-CURR.                                                       
          05 WS-YEAR-CURR                PIC X(04).                             
          05 WS-MONTH-CURR               PIC X(02).                             
          05 WS-DAY-CURR                 PIC X(02).                             
                                                                                
       01 WS-DATE-IN.                                                           
          05 WS-DAY-IN                   PIC X(02).                             
          05 FILLER                      PIC X(1) VALUE '/'.                    
          05 WS-MONTH-IN                 PIC X(02).                             
          05 FILLER                      PIC X(1) VALUE '/'.                    
          05 WS-YEAR-IN                  PIC X(04).                             
                                                                                
       01 WS-VARIABLES.                                                         
          05 WS-BP13KB20-STATUS          PIC 9(02) VALUE ZEROES.                
          05 WS-BP13F730-STATUS          PIC 9(02) VALUE ZEROES.                
          05 WS-BP13L02T-STATUS          PIC 9(02) VALUE ZEROES.                
          05 WS-EOF-F730                 PIC X(01) VALUE 'N'.                   
          05 WS-F730-READ                PIC 9(05) VALUE ZEROES.                
          05 WS-KB20-READ                PIC 9(05) VALUE ZEROES.                
          05 WS-L02T-WRITE               PIC 9(05) VALUE ZEROES.                
          05 WS-F730-BYPASS              PIC 9(05) VALUE ZEROES.                
          05 WS-PAGE-CTR                 PIC 9(02) VALUE ZEROES.                
          05 WS-LINE-CTR                 PIC 9(02) VALUE ZEROES.                
          05 WS-SEQ-NO                   PIC 9(04) VALUE ZEROES.                
          05 WS-MCPS-TAG-KB20            PIC X(01) VALUE 'N'.                   
          05 WS-MCPS-TAG-F730            PIC X(01) VALUE 'N'.                   
          05 WS-PRINT                    PIC X(01) VALUE 'N'.                   
                                                                                
      *============================================================*            
      *         BP13L02T -   R E P O R T    L A Y O U T            *            
      *============================================================*            
                                                                                
       01  L02T-HEADER-01.                                                      
           05  FILLER              PIC X(08)     VALUE 'BP13L02T'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(41)     VALUE                          
               'S Y S T E M    O F    C O M M I T M E N T'.                     
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  L02T-RUN-DATE       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L02T-PAGE-NO        PIC ZZZZ9.                                   
                                                                                
       01  L02T-HEADER-02.                                                      
           05  FILLER              PIC X(45)     VALUE SPACES.                  
           05  FILLER              PIC X(44)     VALUE                          
               'DAILY REPORT ON MATCHING OF MCPS ELIGIBILITY'.                  
           05  FILLER              PIC X(43)     VALUE SPACES.                  
                                                                                
       01  L02T-HEADER-2A.                                                      
           05  FILLER              PIC X(47)     VALUE SPACES.                  
           05  FILLER              PIC X(35)     VALUE                          
               'AND HOUSEHOLD FOR DBSS APPLICATIONS'.                           
           05  FILLER              PIC X(50)     VALUE SPACES.                  
                                                                                
       01  L02T-HEADER-3A.                                                      
           05  FILLER              PIC X(54)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'KB20'.                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'F730'.                  
                                                                                
       01  L02T-HEADER-3B.                                                      
           05  FILLER              PIC X(04)     VALUE ' SEQ'.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'REGN NO'.               
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'BALLOT'.                
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HA1 NRIC'.              
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HA2 NRIC'.              
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE 'FT'.                    
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE 'HH'.                    
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'MCPS'.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE 'HH'.                    
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'MCPS'.                  
                                                                                
                                                                                
       01  L02T-HEADER-04.                                                      
           05  FILLER              PIC X(130)    VALUE ALL '-'.                 
                                                                                
       01  L02T-DETAILS.                                                        
           05  L02T-SEQ-NO         PIC ZZZ9.                                    
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L02T-REG-NO         PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L02T-BALLOT         PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L02T-HA1-NRIC       PIC X(09)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L02T-HA2-NRIC       PIC X(09)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L02T-FT             PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  L02T-KB20-HH        PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L02T-KB20-MCPS      PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  L02T-F730-HH        PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  L02T-F730-MCPS      PIC X(01)     VALUE SPACES.                  
                                                                                
      ********************************************************                  
       PROCEDURE DIVISION.                                                      
      ********************************************************                  
                                                                                
      *************************                                                 
       0000-MAIN-CONTROL.                                                       
      *************************                                                 
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
           PERFORM 1500-PRINT-HEADINGS    THRU 1500-EXIT.                       
           PERFORM 2000-MAIN-PROCESS      THRU 2000-EXIT                        
              UNTIL WS-EOF-F730 = 'Y'.                                          
           PERFORM 9000-CLOSE-ROUTINE     THRU 9000-EXIT.                       
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       1000-OPEN-ROUTINE.                                                       
      ****************************                                              
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13KB20                                                 
                OUTPUT BP13L02T.                                                
                                                                                
           IF WS-BP13F730-STATUS  NOT = '00' AND '97'                           
              DISPLAY 'ERROR IN OPENING BP13F730 STATUS - '                     
                              WS-BP13F730-STATUS                                
              MOVE WS-BP13F730-STATUS           TO RETURN-CODE                  
              PERFORM 9000-CLOSE-ROUTINE        THRU 9000-EXIT                  
           END-IF.                                                              
                                                                                
           IF WS-BP13KB20-STATUS  NOT = '00' AND '97'                           
              DISPLAY 'ERROR IN OPENING BP13KB20 STATUS - '                     
                              WS-BP13KB20-STATUS                                
              MOVE WS-BP13KB20-STATUS           TO RETURN-CODE                  
              PERFORM 9000-CLOSE-ROUTINE        THRU 9000-EXIT                  
           END-IF.                                                              
                                                                                
                                                                                
           IF WS-BP13L02T-STATUS  NOT = '00' AND '97'                           
              DISPLAY 'ERROR IN OPENING BP13L02T STATUS - '                     
                              WS-BP13L02T-STATUS                                
              MOVE WS-BP13L02T-STATUS           TO RETURN-CODE                  
              PERFORM 9000-CLOSE-ROUTINE        THRU 9000-EXIT                  
           END-IF.                                                              
                                                                                
           ACCEPT WS-DATE01-CURR   FROM   DATE YYYYMMDD.                        
           MOVE WS-YEAR-CURR       TO     WS-YEAR-IN.                           
           MOVE WS-MONTH-CURR      TO     WS-MONTH-IN.                          
           MOVE WS-DAY-CURR        TO     WS-DAY-IN.                            
           MOVE WS-DATE-IN         TO     L02T-RUN-DATE.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       1500-PRINT-HEADINGS.                                                     
      ****************************                                              
                                                                                
           ADD 1 TO WS-PAGE-CTR                                                 
           MOVE WS-PAGE-CTR TO L02T-PAGE-NO.                                    
                                                                                
           WRITE BP13L02T-REC FROM L02T-HEADER-01 AFTER 1 LINE                  
           WRITE BP13L02T-REC FROM L02T-HEADER-02 AFTER 1 LINE                  
           WRITE BP13L02T-REC FROM L02T-HEADER-2A AFTER 1 LINE                  
           WRITE BP13L02T-REC FROM BLANKLINE      AFTER 1 LINE                  
           WRITE BP13L02T-REC FROM L02T-HEADER-3A AFTER 1 LINE                  
           WRITE BP13L02T-REC FROM L02T-HEADER-3B AFTER 1 LINE                  
           WRITE BP13L02T-REC FROM L02T-HEADER-04 AFTER 1 LINE                  
           ADD 7 TO WS-LINE-CTR.                                                
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       2000-MAIN-PROCESS.                                                       
      ****************************                                              
                                                                                
           READ BP13F730                                                        
              AT END MOVE 'Y' TO WS-EOF-F730                                    
              GO TO 2000-EXIT.                                                  
                                                                                
           ADD 1 TO WS-F730-READ.                                               
                                                                                
           MOVE SPACES                TO   L02T-DETAILS.                        
           INITIALIZE                      L02T-DETAILS.                        
                                                                                
           MOVE F730-NUM-REGN TO KB20-NUM-REGN                                  
                                 L02T-REG-NO.                                   
                                                                                
           MOVE 'N'   TO WS-MCPS-TAG-KB20                                       
                           WS-MCPS-TAG-F730                                     
                           WS-PRINT.                                            
           PERFORM 3000-READ-KB20     THRU 3000-EXIT.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3000-READ-KB20.                                                          
      ****************************                                              
                                                                                
           READ BP13KB20.                                                       
                                                                                
                                                                                
           EVALUATE WS-BP13KB20-STATUS                                          
              WHEN 00                                                           
              WHEN 02                                                           
                  ADD 1 TO WS-KB20-READ                                         
                  MOVE KB20-NUM-APPLT1-UIN    TO   L02T-HA1-NRIC                
                  MOVE KB20-NUM-APPLT2-UIN    TO   L02T-HA2-NRIC                
                  MOVE KB20-NUM-FLAT-TYPE     TO   L02T-FT                      
                  MOVE KB20-NUM-HH            TO   L02T-KB20-HH                 
                  PERFORM 4000-PROCESS        THRU 4000-EXIT                    
              WHEN 23                                                           
                  ADD 1 TO WS-F730-BYPASS                                       
              WHEN OTHER                                                        
                  DISPLAY 'ERROR OPENING BP13KB20 STAT - '                      
                  WS-BP13KB20-STATUS                                            
                  MOVE   WS-BP13KB20-STATUS  TO RETURN-CODE                     
                  PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       4000-PROCESS.                                                            
      ****************************                                              
                                                                                
           PERFORM 4100-CHECK-MCPS     THRU 4100-EXIT.                          
           IF KB20-NUM-HH NOT = F730-CDE-HOUSEHOLD OR                           
               WS-PRINT = 'Y'                                                   
                MOVE F730-CDE-HOUSEHOLD      TO   L02T-F730-HH                  
                MOVE F730-DTE-BALLOT         TO   L02T-BALLOT                   
                PERFORM 5000-PRINT-REPORT    THRU 5000-EXIT                     
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       4100-CHECK-MCPS.                                                         
      ****************************                                              
           IF KB20-CDE-ALLOC-SCH = 'MCP'                                        
               MOVE 'Y' TO L02T-KB20-MCPS                                       
               MOVE 'Y' TO WS-MCPS-TAG-KB20                                     
           ELSE                                                                 
               MOVE 'N' TO WS-MCPS-TAG-KB20                                     
               MOVE 'N' TO L02T-KB20-MCPS                                       
           END-IF.                                                              
           IF F730-NUM-MCPS NOT = SPACES AND LOW-VALUES                         
               IF F730-CDE-RESIDENT-HA1 = '1' OR 'A' OR 'T'                     
                    MOVE 'Y' TO L02T-F730-MCPS                                  
                    MOVE 'Y' TO WS-MCPS-TAG-F730                                
               ELSE                                                             
                    MOVE 'N' TO L02T-F730-MCPS                                  
                    MOVE 'N' TO WS-MCPS-TAG-F730                                
               END-IF                                                           
            ELSE                                                                
                MOVE 'N' TO L02T-F730-MCPS                                      
                MOVE 'N' TO WS-MCPS-TAG-F730                                    
            END-IF.                                                             
                                                                                
           IF WS-MCPS-TAG-KB20 = 'Y' AND WS-MCPS-TAG-F730 = 'N'                 
               MOVE 'Y'   TO WS-PRINT                                           
           ELSE                                                                 
               MOVE 'N'   TO WS-PRINT                                           
           END-IF.                                                              
       4100-EXIT.                                                               
           EXIT.                                                                
      ****************************                                              
       5000-PRINT-REPORT.                                                       
      ****************************                                              
                                                                                
           IF WS-LINE-CTR > 58                                                  
              MOVE 0 TO WS-LINE-CTR                                             
              PERFORM 1500-PRINT-HEADINGS THRU 1500-EXIT                        
           END-IF.                                                              
                                                                                
           ADD 1              TO   WS-SEQ-NO                                    
           MOVE WS-SEQ-NO     TO   L02T-SEQ-NO.                                 
           WRITE BP13L02T-REC FROM L02T-DETAILS AFTER 1 LINE.                   
           ADD 1              TO   WS-LINE-CTR.                                 
           ADD 1              TO   WS-L02T-WRITE.                               
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       9000-CLOSE-ROUTINE.                                                      
      ****************************                                              
                                                                                
           DISPLAY '*-----------  BP13C02T ------------*'.                      
           DISPLAY ' F730-READ       ' WS-F730-READ.                            
           DISPLAY ' KB20-READ       ' WS-KB20-READ.                            
           DISPLAY ' L02T-WRITE      ' WS-L02T-WRITE.                           
           DISPLAY ' F730-BYPASS     ' WS-F730-BYPASS.                          
           CLOSE      BP13F730                                                  
                      BP13KB20                                                  
                      BP13L02T.                                                 
                                                                                
           IF WS-BP13F730-STATUS  NOT = '00' AND '97'                           
              DISPLAY 'ERROR IN CLOSING BP13F730 STATUS - '                     
                              WS-BP13F730-STATUS                                
              MOVE WS-BP13F730-STATUS           TO RETURN-CODE                  
           END-IF.                                                              
                                                                                
           IF WS-BP13KB20-STATUS  NOT = '00' AND '97'                           
              DISPLAY 'ERROR IN CLOSING BP13KB20 STATUS - '                     
                              WS-BP13KB20-STATUS                                
              MOVE WS-BP13KB20-STATUS           TO RETURN-CODE                  
           END-IF.                                                              
                                                                                
                                                                                
           IF WS-BP13L02T-STATUS NOT = '00' AND '97'                            
              DISPLAY 'ERROR IN CLOSING BP13L02T STATUS - '                     
                              WS-BP13L02T-STATUS                                
              MOVE WS-BP13L02T-STATUS           TO RETURN-CODE                  
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
