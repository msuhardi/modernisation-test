       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY59.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  07/07/2009.                                               
      ************************************************************              
      *                   SOC SYSTEM (SOC)                       *              
      *==========================================================*              
      * DATE WRITTEN      : 07 JUL 2009                          *              
      *                                                          *              
      * 1. OBJECTIVE      : MATCH RSL & BP13F555                 *              
      *                   : GET APPLN DATE  & CDE-SCH-ALLOC-BYR  *              
      *                                                          *              
      * 2. INPUT          : BP13F555 -                           *              
      *                   : BB24K020 - BB24.K020.OCCUPIER        *              
      *                   : BB24K010 - BB24.K010.MASTER          *              
      *                                                          *              
      * 3. OUTPUT         : P13F555A                             *              
      *==========================================================*              
      * CHGE-NO  DATE       OIC  DESCRIPTION                     *              
      * -------- ---------- ---- ------------------------------- *              
      * BP133653 07/07/2009 JB8  NEW PROGRAM.                    *              
      ************************************************************              
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F555     ASSIGN        TO BP13F555.                       
                                                                                
           SELECT BB24K020     ASSIGN        TO BB24K020                        
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K020-KEY-FLD                    
                               ALTERNATE RECORD KEY IS K020-NUM-UIN             
                                                WITH DUPLICATES                 
                               FILE STATUS   IS WS-K020-STATUS.                 
                                                                                
           SELECT BB24K010     ASSIGN        TO BB24K010                        
                               ACCESS MODE   IS RANDOM                          
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K010-KEY-FLD                    
                               FILE STATUS   IS WS-K010-STATUS.                 
                                                                                
           SELECT P13F555A     ASSIGN        TO P13F555A.                       
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F555                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F555.                                                           
                                                                                
       FD   BB24K010                                                            
            RECORD CONTAINS 5000 CHARACTERS.                                    
       COPY BB24K010.                                                           
                                                                                
       FD   BB24K020                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BB24K020.                                                           
                                                                                
       FD   P13F555A                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       01  P13F555A-REC     PIC X(600).                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILE-STATUS.                                                         
           05  WS-K020-STATUS            PIC 9(02) VALUE ZEROES.                
           05  WS-K010-STATUS            PIC 9(02) VALUE ZEROES.                
                                                                                
       01  END-OF-FILE-FLAGS.                                                   
           05  WS-F555-EOF               PIC X(01) VALUE 'N'.                   
               88  F555-EOF              VALUE 'Y'.                             
           05  WS-K020-EOF               PIC X(01) VALUE 'N'.                   
               88  K020-EOF              VALUE 'Y'.                             
           05  WS-K020-FND               PIC X(01) VALUE 'N'.                   
               88  K020-FND              VALUE 'Y'.                             
           05  WS-K010-FND               PIC X(01) VALUE 'N'.                   
               88  K010-FND              VALUE 'Y'.                             
                                                                                
       01  COUNTERS.                                                            
           05  CNT-F555-READ             PIC 9(07) VALUE ZEROES.                
           05  CNT-F555A-WRITE           PIC 9(07) VALUE ZEROES.                
           05  WS-CTR                    PIC 9(02) VALUE ZEROES.                
           05  CNT-RSL-FND               PIC 9(07) VALUE ZEROES.                
                                                                                
       01  WS-VRIABLES.                                                         
           05  WS-NRIC OCCURS 2 TIMES    PIC X(09) VALUE SPACES.                
           05  WS-K020-NRIC              PIC X(09) VALUE SPACES.                
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN.                                                               
      *************************************************************             
           PERFORM 1000-OPEN-ROUTINE THRU 1000-EXIT.                            
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL WS-F555-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       1000-OPEN-ROUTINE.                                                       
      *************************************************************             
                                                                                
           OPEN INPUT  BP13F555                                                 
                       BB24K010                                                 
                       BB24K020                                                 
                OUTPUT P13F555A.                                                
                                                                                
           IF WS-K010-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BB24K010 OPEN ERROR ' WS-K010-STATUS                     
              MOVE WS-K010-STATUS             TO RETURN-CODE                    
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF WS-K020-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BB24K020 OPEN ERROR ' WS-K020-STATUS                     
              MOVE WS-K020-STATUS             TO RETURN-CODE                    
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 1100-READ-BP13F555      THRU 1100-EXIT.                      
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       1100-READ-BP13F555.                                                      
      *************************************************************             
                                                                                
           READ BP13F555                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F555-EOF                         
            NOT AT END                                                          
                ADD 1                    TO CNT-F555-READ                       
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       2000-PROCESS-RECORDS.                                                    
      *************************************************************             
                                                                                
           MOVE 'N'                            TO WS-K010-FND                   
                                                  WS-K020-FND.                  
           MOVE SPACES                         TO WS-NRIC(1)                    
                                                  WS-NRIC(2)                    
                                                  WS-K020-NRIC.                 
                                                                                
           MOVE F555-LATEST-HA1-NRIC           TO WS-NRIC(1).                   
           MOVE F555-LATEST-HA2-NRIC           TO WS-NRIC(2).                   
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1                                   
             UNTIL WS-CTR > 2 OR K020-FND                                       
              IF WS-NRIC(WS-CTR) NOT = SPACES AND LOW-VALUES                    
                 PERFORM 3000-START-BB24K020 THRU 3000-EXIT                     
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           IF K020-FND                                                          
              PERFORM 2500-READ-BB24K010     THRU 2500-EXIT                     
              IF K010-FND                                                       
                 MOVE K010-DTE-RECEIVE-APPLN                                    
                   TO F555-RSL-DTE-RECEIVE-APPLN                                
                 MOVE K010-CDE-SCH-ALLOC-BYR                                    
                   TO F555-RSL-CDE-SCH-ALLOC-BYR                                
                 ADD 1                         TO CNT-RSL-FND                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE BP13F555-REC                   TO P13F555A-REC                  
           WRITE P13F555A-REC                                                   
           ADD 1                               TO CNT-F555A-WRITE.              
                                                                                
           PERFORM 1100-READ-BP13F555 THRU 1100-EXIT.                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       2500-READ-BB24K010.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                         TO BB24K010-REC.                 
           INITIALIZE                             BB24K010-REC.                 
                                                                                
           MOVE K020-NUM-CASE                  TO K010-NUM-CASE.                
                                                                                
           READ BB24K010.                                                       
                                                                                
           IF WS-K010-STATUS = 00 OR 02                                         
              MOVE 'Y'                         TO WS-K010-FND                   
           ELSE                                                                 
              IF WS-K010-STATUS = 23                                            
                 MOVE 'N'                      TO WS-K010-FND                   
              ELSE                                                              
                 DISPLAY 'ERROR READING BB24K010 , STATUS : '                   
                         WS-K010-STATUS                                         
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3000-START-BB24K020.                                                     
      *************************************************************             
           MOVE SPACES                   TO BB24K020-REC.                       
           INITIALIZE                       BB24K020-REC.                       
                                                                                
           MOVE WS-NRIC(WS-CTR)          TO K020-NUM-UIN                        
                                            WS-K020-NRIC.                       
           MOVE 'N'                      TO WS-K020-EOF                         
                                                                                
           START BB24K020 KEY >= K020-NUM-UIN.                                  
                                                                                
           EVALUATE WS-K020-STATUS                                              
               WHEN 00                                                          
                    PERFORM 3100-READNEXT-BB24K020 THRU 3100-EXIT               
                      UNTIL K020-NUM-UIN NOT = WS-K020-NRIC                     
                         OR K020-EOF                                            
                         OR K020-FND                                            
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K020 START ERROR ' WS-K020-STATUS              
                    DISPLAY 'K020 KEY: ' K020-KEY-FLD                           
                    MOVE WS-K020-STATUS          TO RETURN-CODE                 
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *************************************************************             
       3100-READNEXT-BB24K020.                                                  
      *************************************************************             
                                                                                
           READ BB24K020 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                 TO WS-K020-EOF                         
           END-READ.                                                            
                                                                                
           EVALUATE WS-K020-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K020-NUM-UIN = WS-K020-NRIC                              
                       MOVE 'Y'          TO WS-K020-FND                         
                    ELSE                                                        
                       MOVE 'Y'          TO WS-K020-EOF                         
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'Y'             TO WS-K020-EOF                         
               WHEN OTHER                                                       
                    DISPLAY 'BP13K020 READ NEXT ERROR ' WS-K020-STATUS          
                    DISPLAY 'K020 KEY: ' K020-KEY-FLD                           
                    MOVE WS-K020-STATUS          TO RETURN-CODE                 
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       9000-CLOSE-ROUTINE.                                                      
      *************************************************************             
           DISPLAY '   '.                                                       
           DISPLAY '*--------- BP13CY59 CONTROL TOTAL ---------*'.              
           DISPLAY '   '.                                                       
           DISPLAY 'NO. OF READS FROM F555   : ' CNT-F555-READ.                 
           DISPLAY 'RECS WRITTEN TO P13F555A : ' CNT-F555A-WRITE.               
           DISPLAY 'RECS FOUND IN RSL        : ' CNT-RSL-FND.                   
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13F555                                                       
                 BB24K010                                                       
                 BB24K020                                                       
                 P13F555A                                                       
                                                                                
           IF WS-K010-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BB24K010 CLOSE ERROR ' WS-K010-STATUS                    
              MOVE WS-K010-STATUS             TO RETURN-CODE                    
           END-IF.                                                              
                                                                                
           IF WS-K020-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BB24K020 CLOSE ERROR ' WS-K020-STATUS                    
              MOVE WS-K020-STATUS             TO RETURN-CODE                    
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
