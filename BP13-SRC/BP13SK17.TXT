  /*******************************************************************/         
  /*  SOC  SYSTEM  (BP13)                                            */         
  /*=================================================================*/         
  /*                                                                 */         
  /* OBJECTIVES :                                                    */         
  /*  TO PRINT REPORT ON RESALE EX-SPOUSE FOR BTO/SBF EXERCISE       */         
  /*-----------------------------------------------------------------*/         
  /* MODIFICATIONS                                                   */         
  /* =============                                                   */         
  /* CHGE     DATE     BY   DESCRIPTION                              */         
  /* -------- -------- ---- -----------                              */         
  /* BP134554 13022013 YCH2 NEW PGM                                  */         
  /*******************************************************************/         
                                                                                
 DATA P13F558A;                                                                 
   INFILE BP13F558;                                                             
      INPUT @1   REGNO               $CHAR8.                                    
            @597 ALLOC_SCH_RSL       $CHAR3.;                                   
                                                                                
      IF ALLOC_SCH_RSL ^= '   ';                                                
                                                                                
 PROC SORT; BY REGNO;                                                           
                                                                                
 DATA P13F558B;                                                                 
   INFILE BP13F558;                                                             
      INPUT @1   REGNO               $CHAR8.                                    
            @9   NRIC                $CHAR9.                                    
            @37  DTE_EFF_RSL         $CHAR8.                                    
            @71  HDB_REF_RSL         $CHAR11.                                   
            @112 RSL_STATUS          $CHAR3.                                    
            @117 RSL_FT              $CHAR1.                                    
            @139 DTE_SOLD            $CHAR8.                                    
            @219 DTE_APPROVAL        $CHAR8.                                    
            @597 ALLOC_SCH_RSL       $CHAR3.;                                   
                                                                                
 PROC SORT; BY REGNO;                                                           
                                                                                
 DATA F558MRGE;                                                                 
 MERGE P13F558A(IN=A) P13F558B(IN=B); BY REGNO;                                 
                                                                                
   IF A AND B;                                                                  
                                                                                
 PROC SORT DATA=F558MRGE; BY REGNO;                                             
                                                                                
 DATA BP13F730;                                                                 
   INFILE BP13F730;                                                             
      INPUT @123 REGNO    $CHAR8.                                               
            @256 CDE_BO   $CHAR2.                                               
            @116 HHTYPE   $CHAR1.                                               
            @334 DTE_BALLOT $CHAR6.                                             
            @376 ALLO_CAT $CHAR3.;                                              
                                                                                
      IF HHTYPE='H' OR HHTYPE='T';                                              
                                                                                
 PROC SORT; BY REGNO;                                                           
 DATA F730F558;                                                                 
 MERGE BP13F730(IN=A) F558MRGE(IN=B); BY REGNO;                                 
                                                                                
   IF A AND B;                                                                  
                                                                                
 PROC SORT; BY DTE_BALLOT ALLO_CAT REGNO NRIC;                                  
                                                                                
 DATA _NULL_;                                                                   
 SET F730F558 END=EOF; BY DTE_BALLOT ALLO_CAT REGNO NRIC;                       
                                                                                
   FILE BP13LK17 NOTITLES;                                                      
                                                                                
   IF FIRST.DTE_BALLOT THEN                                                     
      DO;                                                                       
        LINK HDG;                                                               
        SNO=0;                                                                  
      END;                                                                      
                                                                                
   IF FIRST.ALLO_CAT THEN                                                       
      DO;                                                                       
         SNO=0;                                                                 
         PUT @1 'EXERCISE : ' ALLO_CAT /;                                       
      END;                                                                      
                                                                                
   IF FIRST.REGNO THEN                                                          
      DO;                                                                       
         SNO+1;                                                                 
         PUT @1   SNO                 4.                                        
             @6   REGNO               $CHAR8. @;                                
      END;                                                                      
                                                                                
   IF HDB_REF_RSL=' ' THEN                                                      
      PUT  @20  NRIC     $CHAR9.;                                               
   ELSE                                                                         
      PUT  @20  NRIC                $CHAR9.                                     
           @31  HDB_REF_RSL         $CHAR11.                                    
           @46  RSL_STATUS          $CHAR3.                                     
           @52  ALLOC_SCH_RSL       $CHAR3.;                                    
                                                                                
   IF EOF THEN                                                                  
      PUT '.              ';                                                    
                                                                                
   RETURN;                                                                      
                                                                                
 HDG:                                                                           
                                                                                
    CUR_DATE=TODAY();                                                           
                                                                                
    PUT @1 'BP13SK17'                                                           
        @10  'RESALE SPOUSE OCCUPIER FOR DATE BALLOT '                          
        @50   DTE_BALLOT                                                        
        @60  'DATE : ' CUR_DATE DDMMYY10.;                                      
    PUT @31  '<---------- RESALE INFO -------->';                               
    PUT @1   'SNO'                                                              
        @6   'REGNO '                                                           
        @20  'UIN NO'                                                           
        @31  'HDBREF'                                                           
        @44  'STATUS'                                                           
        @52  'ALL SCH'                                                          
        ;                                                                       
   RETURN;                                                                      
