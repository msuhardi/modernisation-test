       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF34.                                                 
       AUTHOR.        RALSTON JAMES ELIEZER B MEDRANO.                          
      *DATE-WRITTEN.  23/01/2013.                                               
                                                                                
      *****************************************************************         
      *                SYSTEM OF COMMITMENT                           *         
      *===============================================================*         
      *  OBJECTIVES : 1. TO PRINT REPORT ON MONTHLY REVIEW OF         *         
      *                  ADMINISTRATOR'S ACCESS TO SOC AND FIS        *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      * CHGE REQ #  DATE      AUTHOR    REMARKS                       *         
      * ----------  ----      ------    -------                       *         
      * BP134542    23012013  RJE1      NEW PROGRAM                   *         
      * BM062250                                                      *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410 ASSIGN        TO BP13F410.                           
                                                                                
           SELECT BP13F300 ASSIGN        TO BP13F300.                           
                                                                                
           SELECT BM06F190 ASSIGN        TO BM06F190.                           
                                                                                
           SELECT BP13K848 ASSIGN        TO BP13K848.                           
                                                                                
           SELECT BP13LF34 ASSIGN        TO BP13LF34.                           
                                                                                
           SELECT SY08K010 ASSIGN        TO SY08K010                            
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K010-NUM-USERID                     
                           FILE STATUS   IS WS-K010-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F410                                                             
           RECORD    CONTAINS 2000 CHARACTERS                                   
           BLOCK     CONTAINS   0  RECORDS                                      
           LABEL     RECORDS  ARE  STANDARD                                     
           RECORDING MODE     IS   F.                                           
       COPY BP13F410.                                                           
                                                                                
       FD  BP13F300                                                             
           RECORD    CONTAINS 2000 CHARACTERS                                   
           BLOCK     CONTAINS   0  RECORDS                                      
           LABEL     RECORDS  ARE  STANDARD                                     
           RECORDING MODE     IS   F.                                           
       COPY BP13F300.                                                           
                                                                                
       FD  BP13K848                                                             
           RECORD    CONTAINS 2000 CHARACTERS                                   
           BLOCK     CONTAINS   0  RECORDS                                      
           LABEL     RECORDS  ARE  STANDARD                                     
           RECORDING MODE     IS   F.                                           
       COPY BP13K848.                                                           
                                                                                
       FD  BM06F190                                                             
           RECORD    CONTAINS 650  CHARACTERS                                   
           BLOCK     CONTAINS   0  RECORDS                                      
           LABEL     RECORDS  ARE  STANDARD                                     
           RECORDING MODE     IS   F.                                           
       COPY BM06F190.                                                           
                                                                                
       FD   SY08K010                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY SY08K010.                                                           
                                                                                
       FD  BP13LF34                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED                                            
           DATA RECORD IS LF34-PRTREC.                                          
       01  LF34-PRTREC             PIC X(132).                                  
                                                                                
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  WS-F300-READ        PIC 9(8)      VALUE ZEROS.                   
           05  WS-K848-READ        PIC 9(8)      VALUE ZEROS.                   
           05  WS-F410-READ        PIC 9(8)      VALUE ZEROS.                   
           05  WS-F190-READ        PIC 9(8)      VALUE ZEROS.                   
           05  WS-LF34-WRITE       PIC 9(8)      VALUE ZEROS.                   
           05  WS-PAGE-CNT         PIC 9(2)      VALUE ZEROS.                   
           05  WS-LINE-CNT         PIC 99        VALUE ZEROS.                   
       01  WS-SWITCHES.                                                         
           05  WS-HEAD3-SW         PIC X         VALUE SPACES.                  
           05  WS-F410-EOF         PIC X         VALUE SPACES.                  
           05  WS-F300-EOF         PIC X         VALUE SPACES.                  
           05  WS-F190-EOF         PIC X         VALUE SPACES.                  
           05  WS-K848-EOF         PIC X         VALUE SPACES.                  
           05  WS-NIL-SW           PIC X         VALUE SPACES.                  
       01  WS-BLANK-LINE           PIC X(132)    VALUE SPACES.                  
       01  WS-K010-STATUS          PIC 9(2)      VALUE  00.                     
       01  WS-USERID               PIC X(08)     VALUE SPACES.                  
                                                                                
       01  WS-DTE-CUR.                                                          
           05  WS-CC               PIC 99.                                      
           05  WS-YY               PIC 99.                                      
           05  WS-MM               PIC 99.                                      
           05  WS-DD               PIC 99.                                      
                                                                                
       01  WS-DATE.                                                             
           05  WS-YEAR             PIC 9(4).                                    
           05  WS-MONTH            PIC 99.                                      
           05  WS-DAY              PIC 99.                                      
                                                                                
       01  LF34-HEADING1.                                                       
           05  FILLER              PIC X(12)     VALUE                          
                                    'REPORT ID : '.                             
           05  FILLER              PIC X(8)      VALUE 'BP13LF34'.              
           05  FILLER              PIC X(7)      VALUE SPACES.                  
           05  FILLER              PIC X(4)      VALUE 'HDB3'.                  
           05  FILLER              PIC X(19)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'SYSTEM OF COMMITMENT'.                     
           05  FILLER              PIC X(28)     VALUE SPACES.                  
           05  FILLER              PIC X(6)      VALUE 'DATE: '.                
           05  LF34-DATE           PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(5)      VALUE SPACES.                  
           05  FILLER              PIC X(6)      VALUE 'PAGE: '.                
           05  LF34-PAGENO         PIC Z9.                                      
                                                                                
       01  LF34-HEADING2.                                                       
           05  FILLER              PIC X(20)     VALUE SPACES.                  
           05  FILLER              PIC X(19)     VALUE SPACES.                  
           05  FILLER              PIC X(18)     VALUE                          
                                   'MONTHLY REVIEW OF '.                        
           05  FILLER              PIC X(22)     VALUE                          
                                   "ADMINSTRATOR'S ACCESS ".                    
           05  FILLER              PIC X(14)     VALUE                          
                                   'TO SOC AND FIS'.                            
                                                                                
       01  LF34-HEADING3.                                                       
           05  FILLER              PIC X(07)     VALUE 'USER-ID'.               
           05  FILLER              PIC X(11)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'NAME'.                  
           05  FILLER              PIC X(20)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'FILE ID'.               
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(06)     VALUE 'TXN ID'.                
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'REGN'.                  
           05  FILLER              PIC X(06)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'SCHM ACNT'.             
           05  FILLER              PIC X(05)     VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE 'UPDATE DATE'.           
                                                                                
       01  LF34-DETAILS.                                                        
           05  LF34-USERID         PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE SPACES.                  
           05  LF34-NAME           PIC X(20)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  LF34-FILEID         PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LF34-TRANID         PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  LF34-REGNO          PIC X(08)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  LF34-SCHACC         PIC X(11)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  LF34-DATE-UPD.                                                   
               10 LF34-DD          PIC X(2).                                    
               10 FILLER           PIC X         VALUE '/'.                     
               10 LF34-MM          PIC X(2).                                    
               10 FILLER           PIC X         VALUE '/'.                     
               10 LF34-CCYY        PIC X(4).                                    
                                                                                
       01  LF34-LINER.                                                          
           05  FILLER              PIC X(132)    VALUE ALL '-'.                 
                                                                                
       01  LF34-NIL.                                                            
           05  FILLER              PIC X(11)     VALUE                          
                                   'NO RECORDS.'.                               
                                                                                
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-F410-READ        THRU 2000-EXIT.                        
           PERFORM 6000-PROCESS          THRU 6000-EXIT.                        
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13F410                                                 
                       BP13F300                                                 
                       BP13K848                                                 
                       BM06F190                                                 
                       SY08K010                                                 
                OUTPUT BP13LF34.                                                
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING SY08K010, STATUS = ' WS-K010-STATUS        
              MOVE     WS-K010-STATUS    TO    RETURN-CODE                      
              PERFORM  9000-CLOSE-FILES  THRU  9000-EXIT                        
           END-IF.                                                              
                                                                                
           INITIALIZE WS-F410-READ                                              
                      WS-F300-READ                                              
                      WS-F190-READ                                              
                      WS-K848-READ                                              
                      WS-F410-EOF                                               
                      WS-F300-EOF                                               
                      WS-F190-EOF                                               
                      WS-K848-EOF                                               
                      WS-LF34-WRITE.                                            
                                                                                
            MOVE FUNCTION CURRENT-DATE TO WS-DTE-CUR.                           
            STRING WS-DD '/' WS-MM '/' WS-CC WS-YY                              
                   DELIMITED BY SIZE INTO LF34-DATE.                            
                                                                                
            PERFORM 1100-WRITE-HEADER    THRU 1100-EXIT.                        
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       1100-WRITE-HEADER.                                                       
      *****************************************************************         
            ADD 1               TO WS-PAGE-CNT.                                 
            MOVE WS-PAGE-CNT    TO LF34-PAGENO.                                 
            MOVE SPACES         TO LF34-PRTREC.                                 
            WRITE LF34-PRTREC FROM LF34-HEADING1 AFTER PAGE.                    
            WRITE LF34-PRTREC FROM LF34-HEADING2.                               
            WRITE LF34-PRTREC FROM WS-BLANK-LINE.                               
                                                                                
            MOVE 'N'                     TO WS-HEAD3-SW.                        
            MOVE 2              TO WS-LINE-CNT.                                 
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       1200-WRITE-HEADER3.                                                      
      *****************************************************************         
            WRITE LF34-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LF34-PRTREC FROM LF34-HEADING3.                               
            WRITE LF34-PRTREC FROM LF34-LINER.                                  
                                                                                
            ADD  4              TO WS-LINE-CNT.                                 
                                                                                
       1200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       1300-WRITE-DETAILS.                                                      
      *****************************************************************         
            IF WS-LINE-CNT > 50                                                 
               PERFORM 1100-WRITE-HEADER  THRU 1100-EXIT                        
            END-IF.                                                             
                                                                                
            IF WS-HEAD3-SW = 'N'                                                
               PERFORM 1200-WRITE-HEADER3 THRU 1200-EXIT                        
               MOVE 'Y'                   TO WS-HEAD3-SW                        
               MOVE 'N'                   TO WS-NIL-SW                          
            END-IF.                                                             
                                                                                
            WRITE LF34-PRTREC FROM LF34-DETAILS.                                
            ADD 1                         TO WS-LINE-CNT.                       
                                                                                
       1300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       1400-WRITE-NIL.                                                          
      *****************************************************************         
            MOVE SPACES         TO LF34-PRTREC                                  
            WRITE LF34-PRTREC FROM WS-BLANK-LINE.                               
            WRITE LF34-PRTREC FROM LF34-NIL.                                    
                                                                                
            ADD 1              TO WS-LINE-CNT.                                  
                                                                                
       1400-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       1500-GET-NAME.                                                           
      *****************************************************************         
                                                                                
           MOVE WS-USERID          TO K010-NUM-USERID.                          
                                                                                
           READ SY08K010 KEY IS K010-NUM-USERID.                                
                                                                                
           EVALUATE WS-K010-STATUS                                              
               WHEN 00                                                          
                    MOVE K010-NME-USER  TO LF34-NAME                            
               WHEN 23                                                          
                    MOVE SPACES         TO LF34-NAME                            
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING SY08K010, RECORD STATUS IS '         
                            WS-K010-STATUS                                      
                    MOVE WS-K010-STATUS TO RETURN-CODE                          
                    PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       1500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       2000-F410-READ.                                                          
      *****************************************************************         
           READ BP13F410 AT END                                                 
                MOVE 'Y'                 TO WS-F410-EOF                         
                GO                       TO 2000-EXIT.                          
                                                                                
           ADD 1                         TO WS-F410-READ.                       
           MOVE F410-NUM-USERID          TO LF34-USERID                         
                                            WS-USERID.                          
           PERFORM 1500-GET-NAME         THRU 1500-EXIT.                        
           MOVE 'BP13F410'               TO LF34-FILEID.                        
           MOVE F410-NUM-TRANSID         TO LF34-TRANID.                        
           MOVE F410-NUM-SCH-ACC         TO LF34-SCHACC.                        
           MOVE F410-DTE-UPDATE          TO WS-DATE.                            
           MOVE WS-YEAR                  TO LF34-CCYY.                          
           MOVE WS-MONTH                 TO LF34-MM.                            
           MOVE WS-DAY                   TO LF34-DD.                            
           MOVE F410-NUM-REGN            TO LF34-REGNO.                         
           PERFORM 1300-WRITE-DETAILS    THRU 1300-EXIT.                        
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       3000-F300-READ.                                                          
      *****************************************************************         
           READ BP13F300 AT END                                                 
                MOVE 'Y'                 TO WS-F300-EOF                         
                GO                       TO 3000-EXIT.                          
                                                                                
           ADD 1                         TO WS-F300-READ.                       
           MOVE F300-USERID              TO LF34-USERID                         
                                            WS-USERID.                          
           PERFORM 1500-GET-NAME         THRU 1500-EXIT.                        
           MOVE 'BP13F300'               TO LF34-FILEID.                        
           MOVE SPACES                   TO LF34-TRANID.                        
           MOVE F300-SCH-ACC             TO LF34-SCHACC.                        
           MOVE F300-DTE-LAST-UPDATE     TO WS-DATE.                            
           MOVE WS-YEAR                  TO LF34-CCYY.                          
           MOVE WS-MONTH                 TO LF34-MM.                            
           MOVE WS-DAY                   TO LF34-DD.                            
           MOVE F300-CUR-REG             TO LF34-REGNO.                         
           PERFORM 1300-WRITE-DETAILS    THRU 1300-EXIT.                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       4000-F190-READ.                                                          
      *****************************************************************         
           READ BM06F190 AT END                                                 
                MOVE 'Y'                 TO WS-F190-EOF                         
                GO                       TO 4000-EXIT.                          
                                                                                
           ADD 1                         TO WS-F190-READ.                       
           MOVE F190-USER-ID             TO LF34-USERID                         
                                            WS-USERID.                          
           PERFORM 1500-GET-NAME         THRU 1500-EXIT.                        
           MOVE 'BM06F190'               TO LF34-FILEID.                        
           MOVE SPACES                   TO LF34-TRANID.                        
           MOVE F190-SCH-ACC-NO          TO LF34-SCHACC.                        
           MOVE F190-DTE-LAST-UPD        TO WS-DATE.                            
           MOVE WS-YEAR                  TO LF34-CCYY.                          
           MOVE WS-MONTH                 TO LF34-MM.                            
           MOVE WS-DAY                   TO LF34-DD.                            
           MOVE F190-REGN-NO             TO LF34-REGNO.                         
           PERFORM 1300-WRITE-DETAILS    THRU 1300-EXIT.                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       5000-K848-READ.                                                          
      *****************************************************************         
           READ BP13K848 AT END                                                 
                MOVE 'Y'                 TO WS-K848-EOF                         
                GO                       TO 5000-EXIT.                          
                                                                                
           ADD 1                         TO WS-K848-READ.                       
           MOVE K848-NUM-USERID          TO LF34-USERID                         
                                            WS-USERID.                          
           PERFORM 1500-GET-NAME         THRU 1500-EXIT.                        
           MOVE 'BP13K848'               TO LF34-FILEID.                        
           MOVE SPACES                   TO LF34-TRANID.                        
           MOVE K848-NUM-SCH-ACC         TO LF34-SCHACC.                        
           MOVE K848-DTE-UPDATE          TO WS-DATE.                            
           MOVE WS-YEAR                  TO LF34-CCYY.                          
           MOVE WS-MONTH                 TO LF34-MM.                            
           MOVE WS-DAY                   TO LF34-DD.                            
           MOVE K848-NUM-REGN            TO LF34-REGNO.                         
           PERFORM 1300-WRITE-DETAILS    THRU 1300-EXIT.                        
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
       6000-PROCESS.                                                            
      *****************************************************************         
                                                                                
           IF WS-F410-EOF NOT = 'Y'                                             
              PERFORM 2000-F410-READ     THRU 2000-EXIT                         
                  UNTIL  WS-F410-EOF   = 'Y'                                    
           END-IF.                                                              
                                                                                
           IF WS-F300-EOF NOT = 'Y'                                             
              PERFORM 3000-F300-READ     THRU 3000-EXIT                         
                  UNTIL  WS-F300-EOF   = 'Y'                                    
           END-IF.                                                              
                                                                                
           IF WS-F190-EOF NOT = 'Y'                                             
              PERFORM 4000-F190-READ     THRU 4000-EXIT                         
                  UNTIL  WS-F190-EOF   = 'Y'                                    
           END-IF.                                                              
                                                                                
           IF WS-K848-EOF NOT = 'Y'                                             
              PERFORM 5000-K848-READ     THRU 5000-EXIT                         
                  UNTIL  WS-K848-EOF   = 'Y'                                    
           END-IF.                                                              
                                                                                
           IF WS-NIL-SW NOT = 'N'                                               
              PERFORM 1400-WRITE-NIL THRU 1400-EXIT                             
           END-IF.                                                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
           DISPLAY '  CONTROL LIST - BP13CF34   '                               
           DISPLAY '  '.                                                        
           DISPLAY '  ** INPUT COUNT **         '.                              
           DISPLAY 'A) TOTAL F400 REC READ      = ' WS-F410-READ.               
           DISPLAY 'B) TOTAL F300 REC READ      = ' WS-F300-READ.               
           DISPLAY 'C) TOTAL F190 REC READ      = ' WS-F190-READ.               
           DISPLAY 'D) TOTAL K848 REC READ      = ' WS-K848-READ.               
           DISPLAY '  '.                                                        
           DISPLAY ' **  OUTPUT COUNT **        '.                              
           DISPLAY '  '.                                                        
           DISPLAY 'E) TOTAL RECORDS WRITTEN   = ' WS-LF34-WRITE.               
                                                                                
           CLOSE      BP13F410                                                  
                      BP13F300                                                  
                      BM06F190                                                  
                      BP13K848                                                  
                      SY08K010                                                  
                      BP13LF34.                                                 
                                                                                
           IF WS-K010-STATUS NOT = 00                                           
              DISPLAY 'SY08K010 CLOSE ERROR, STATUS : ' WS-K010-STATUS          
              MOVE WS-K010-STATUS TO RETURN-CODE                                
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
         9000-EXIT.                                                             
           EXIT.                                                                
      *====================  END OF PROGRAM  ====================*              
