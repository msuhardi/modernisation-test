      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C28B.                                                 
      *AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  MAR 24 2010.                                              
                                                                                
      * ==============================================================*         
      *    SYSTEM NAME : SYSTEM OF COMMITMENT(SOC)                    *         
      * ==============================================================*         
      *                                                               *         
      * OBJECTIVE  :                                                  *         
      *       1.  TO GENERATE GL TRANSACTION RECORDS FOR              *         
      *           STUDIO APARTMENT CASES.                             *         
      *                                                               *         
      * INPUT/OUTPUT FILES:                                           *         
      *       BP13F130 (FINANCIAL HISTORY TRANSACTION FILE)           *         
      *       BP13K800 (APPLICANT MASTER)                             *         
      *       BP13K410                                                *         
      *       BP13F120 (SOC HISTORY MASTER)                           *         
      *                                                               *         
      * CHG-NO   BY    ON      DESCRIPTION                            *         
      * -------- ---  -------- -----------                            *         
      * BP133856 ESA1 20100324 NEW PGM                                *         
      * ============================================================= *         
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
      *------------------------*                                                
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *------------------------*                                                
       INPUT-OUTPUT SECTION.                                                    
      *------------------------*                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F130 ASSIGN TO BP13F130.                                  
                                                                                
           SELECT BP13K410 ASSIGN TO BP13K410                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS DYNAMIC                                       
                  RECORD KEY   IS K410-KEY-FLD                                  
                  FILE STATUS  IS K410-STATUS.                                  
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS DYNAMIC                                       
                  RECORD KEY   IS K800-NUM-REGN                                 
                  FILE STATUS  IS K800-STATUS.                                  
                                                                                
           SELECT BP13F120 ASSIGN TO BP13F120.                                  
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
      *------------------------*                                                
                                                                                
       FD  BP13F130           BLOCK CONTAINS 0 RECORDS                          
                              RECORD CONTAINS 150 CHARACTERS                    
                              LABEL RECORDS ARE STANDARD                        
                              RECORDING MODE IS F.                              
       COPY BP13F130.                                                           
                                                                                
       FD  BP13K410           RECORD CONTAINS 2000 CHARACTERS.                  
       COPY BP13K410.                                                           
                                                                                
       FD  BP13K800           RECORD CONTAINS 2000 CHARACTERS.                  
       COPY BP13K800.                                                           
                                                                                
       FD  BP13F120           BLOCK CONTAINS 0 RECORDS                          
                              RECORD CONTAINS 150 CHARACTERS                    
                              LABEL RECORDS ARE STANDARD                        
                              RECORDING MODE IS F.                              
       COPY BP13F120.                                                           
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  K800-STATUS           PIC 9(2)    VALUE ZEROS.                   
           05  K410-STATUS           PIC 9(2)    VALUE ZEROS.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-CNT           PIC 9(6)    VALUE ZEROS.                   
           05  WS-WRITE-CNT          PIC 9(6)    VALUE ZEROS.                   
                                                                                
       01  WS-DATE.                                                             
           05  WS-CC                 PIC X(2).                                  
           05  WS-YY                 PIC X(2).                                  
           05  WS-MM                 PIC X(2).                                  
           05  WS-DD                 PIC X(2).                                  
                                                                                
       01  WS-EOF-F130               PIC X(1)    VALUE 'N'.                     
       01  WS-AMT-SOC-PAID           PIC S9(7)V99 VALUE ZEROS.                  
       01  WS-NUM-RECEIPT-JRNO.                                                 
           05  WS-RECEIPT-CD         PIC X(2).                                  
           05  WS-RECEIPT-DATE.                                                 
               10  WS-RECEIPT-DD     PIC 9(2).                                  
               10  WS-RECEIPT-MM     PIC 9(2).                                  
               10  WS-SLASH          PIC X.                                     
               10  WS-RECEIPT-YY     PIC 9(2).                                  
           05  WS-NUM-DOC            PIC 9(5)   VALUE ZEROES.                   
                                                                                
      *------------------------*                                                
       PROCEDURE DIVISION.                                                      
      *------------------------*                                                
                                                                                
      *------------------------*                                                
       000-MAIN.                                                                
      *------------------------*                                                
           PERFORM 100-START-PROCESS  THRU 100-EXIT.                            
           PERFORM 200-READ-F130      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-UPDATE THRU 300-EXIT                             
                   UNTIL WS-EOF-F130 = 'Y'.                                     
           PERFORM 400-END-PROCESS    THRU 400-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       100-START-PROCESS.                                                       
      *------------------------*                                                
           OPEN INPUT BP13F130                                                  
                      BP13K410                                                  
                I-O   BP13K800                                                  
               OUTPUT BP13F120.                                                 
                                                                                
           IF K410-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING ERROR K410-STATUS IS ' K410-STATUS               
              PERFORM 400-END-PROCESS THRU 400-EXIT                             
           END-IF.                                                              
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'OPENING ERROR K800-STATUS IS ' K800-STATUS               
              PERFORM 400-END-PROCESS THRU 400-EXIT                             
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           MOVE ZEROS                 TO WS-COUNTERS.                           
           MOVE 'N'                   TO WS-EOF-F130.                           
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *             INITIAL READING OF BP13F130 FILE                   *        
      ******************************************************************        
      *------------------------*                                                
       200-READ-F130.                                                           
      *------------------------*                                                
           READ BP13F130 AT END                                                 
                         MOVE 'Y' TO WS-EOF-F130                                
                         GO TO 200-EXIT.                                        
                                                                                
           ADD 1 TO WS-READ-CNT.                                                
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       300-PROCESS-UPDATE.                                                      
      *------------------------*                                                
           PERFORM 310-READ-BP13K410 THRU 310-EXIT.                             
                                                                                
           PERFORM 200-READ-F130     THRU 200-EXIT.                             
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    USE THE REG  NUMBER IN BP13F130 FILE TO ACCESS THE RECORDS  *        
      *    IN BP13K410 FILE.                                           *        
      ******************************************************************        
      *------------------------*                                                
       310-READ-BP13K410.                                                       
      *------------------------*                                                
           MOVE F130-NUM-REGN TO K410-NUM-REGN.                                 
                                                                                
           READ BP13K410.                                                       
                                                                                
           EVALUATE K410-STATUS                                                 
             WHEN 00                                                            
                  PERFORM 320-READ-BP13K800   THRU 320-EXIT                     
             WHEN 10                                                            
             WHEN 23                                                            
                  DISPLAY 'REC NOT FOUND IN BP13K410 '                          
                      F130-NUM-REGN                                             
             WHEN OTHER                                                         
                  DISPLAY 'ERROR READING BP13K410, STATUS : '                   
                      K410-STATUS                                               
                  PERFORM 400-END-PROCESS    THRU 400-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    USE THE REG  NUMBER IN BP13F400 FILE TO ACCESS THE RECORDS  *        
      *    IN BP13K800 FILE.                                           *        
      ******************************************************************        
      *------------------------*                                                
       320-READ-BP13K800.                                                       
      *------------------------*                                                
           MOVE K410-NUM-REGN TO K800-NUM-REGN.                                 
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE K800-STATUS                                                 
             WHEN 00                                                            
                  PERFORM 330-PROCESS-K800  THRU 330-EXIT                       
             WHEN 10                                                            
             WHEN 23                                                            
                  DISPLAY 'REC NOT FOUND IN BP13K800 '                          
                      K410-NUM-REGN                                             
             WHEN OTHER                                                         
                  DISPLAY 'ERROR READING BP13K410, STATUS : '                   
                      K410-STATUS                                               
                  PERFORM 400-END-PROCESS    THRU 400-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    VALIDATE IF THE AMOUNT IN BP13F130 IS EQUAL TO BP13K800     *        
      *                                                                *        
      ******************************************************************        
      *------------------------*                                                
       330-PROCESS-K800.                                                        
      *------------------------*                                                
           IF F130-TRANS-TYPE = '11' OR '15' OR '16'                            
              IF F130-AMT-RECEIPT = K800-AMT-SOC-CASH                           
                 COMPUTE WS-AMT-SOC-PAID = K800-AMT-SOC-CASH                    
                 COMPUTE K800-AMT-SOC-CASH = K800-AMT-SOC-CASH -                
                                             F130-AMT-RECEIPT                   
                 PERFORM 340-REWRITE-BP13K800  THRU 340-EXIT                    
              END-IF                                                            
           ELSE                                                                 
              IF F130-TRANS-TYPE = '12'                                         
                 IF F130-NUM-NRIC = K800-NUM-NRIC1                              
                    IF F130-AMT-RECEIPT = K800-AMT-CPF1-PAID                    
                       COMPUTE WS-AMT-SOC-PAID = K800-AMT-CPF1-PAID             
                       COMPUTE K800-AMT-CPF1-PAID = K800-AMT-CPF1-PAID -        
                                                    F130-AMT-RECEIPT            
                       PERFORM 340-REWRITE-BP13K800  THRU 340-EXIT              
                    END-IF                                                      
                 END-IF                                                         
                 IF F130-NUM-NRIC = K800-NUM-NRIC2                              
                    IF F130-AMT-RECEIPT = K800-AMT-CPF2-PAID                    
                       COMPUTE WS-AMT-SOC-PAID = K800-AMT-CPF2-PAID             
                       COMPUTE K800-AMT-CPF2-PAID = K800-AMT-CPF2-PAID -        
                                                    F130-AMT-RECEIPT            
                       PERFORM 340-REWRITE-BP13K800  THRU 340-EXIT              
                    END-IF                                                      
                 END-IF                                                         
                 IF F130-NUM-NRIC = K800-NUM-NRIC3                              
                    IF F130-AMT-RECEIPT = K800-AMT-CPF3-PAID                    
                       COMPUTE WS-AMT-SOC-PAID = K800-AMT-CPF3-PAID             
                       COMPUTE K800-AMT-CPF3-PAID = K800-AMT-CPF3-PAID -        
                                                    F130-AMT-RECEIPT            
                       PERFORM 340-REWRITE-BP13K800  THRU 340-EXIT              
                    END-IF                                                      
                 END-IF                                                         
                 IF F130-NUM-NRIC = K800-NUM-NRIC4                              
                    IF F130-AMT-RECEIPT = K800-AMT-CPF4-PAID                    
                       COMPUTE WS-AMT-SOC-PAID = K800-AMT-CPF4-PAID             
                       COMPUTE K800-AMT-CPF4-PAID = K800-AMT-CPF4-PAID -        
                                                    F130-AMT-RECEIPT            
                       PERFORM 340-REWRITE-BP13K800  THRU 340-EXIT              
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       330-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       340-REWRITE-BP13K800.                                                    
      *------------------------*                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           IF K800-STATUS = ZEROES                                              
              PERFORM 350-WRITE-BP13F120    THRU 350-EXIT                       
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING BP13K400, STATUS : '                     
                       K410-STATUS                                              
              PERFORM 400-END-PROCESS    THRU 400-EXIT                          
           END-IF.                                                              
                                                                                
       340-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       350-WRITE-BP13F120.                                                      
      *------------------------*                                                
           MOVE SPACES                TO BP13F120-CASHTRAN.                     
           MOVE '20'                  TO F120-CDE-TRANS-TYPE.                   
           MOVE K800-NUM-REGN         TO F120-NUM-REGN.                         
           MOVE K800-NUM-NRIC1        TO F120-NUM-NRIC.                         
           MOVE WS-DATE               TO F120-DTE-POST.                         
           MOVE ZEROES                TO F120-COUNT-HISTORY                     
           MOVE SPACES                TO F120-NUM-CPF-APPL.                     
           MOVE SPACES                TO F120-NME-NAME.                         
           MOVE 'SOC'                 TO F120-CDE-PAYMENT-TYPE.                 
           MOVE WS-DD                 TO WS-RECEIPT-DD.                         
           MOVE WS-MM                 TO WS-RECEIPT-MM.                         
           MOVE WS-YY                 TO WS-RECEIPT-YY.                         
           MOVE 'JR'                  TO WS-RECEIPT-CD.                         
           MOVE '/'                   TO WS-SLASH.                              
           MOVE WS-NUM-RECEIPT-JRNO   TO F120-NUM-RECPT-JRNO.                   
           COMPUTE F120-AMT-RECEIPT = WS-AMT-SOC-PAID * -1                      
           MOVE 'D'                   TO F120-CDE-CRDR.                         
           MOVE WS-DATE               TO F120-DTE-TRANS.                        
           MOVE F130-NUM-SCH-ACCT     TO F120-NUM-SCH-ACCT.                     
           MOVE K800-NUM-FLAT-TYPE    TO F120-NUM-FLAT-TYPE-SA                  
                                                                                
           WRITE BP13F120-CASHTRAN.                                             
           ADD  1                     TO WS-WRITE-CNT.                          
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------*                                                
       400-END-PROCESS.                                                         
      *------------------------*                                                
           CLOSE   BP13F130                                                     
                   BP13K410                                                     
                   BP13K800                                                     
                   BP13F120.                                                    
                                                                                
           IF K410-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING ERROR, K410-STATUS IS ' K410-STATUS              
           END-IF.                                                              
                                                                                
           IF K800-STATUS NOT = 00 AND 97                                       
              DISPLAY 'CLOSING ERROR, K800-STATUS IS ' K800-STATUS              
           END-IF.                                                              
                                                                                
           DISPLAY ' =============  BP13C28B ===================='.             
           DISPLAY 'NO OF RECORDS READ FROM BP13F130  = ' WS-READ-CNT.          
           DISPLAY 'NO OF RECORDS WRITTEN TO BP13F120 = ' WS-WRITE-CNT.         
           DISPLAY ' ============================================'.             
                                                                                
           STOP RUN.                                                            
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *------------------------------*                                          
      *         END OF PROGRAM       *                                          
      *------------------------------*                                          
