       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C990.                                                  
       AUTHOR.       KVL1.                                                      
      *DATE-WRITTEN. 14/10/2016.                                                
                                                                                
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: 1) To create in-tray alert to oic for pending  *            
      *                cancellation action using BE08C008          *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP136508 20161014 KVL1  NEW PROGRAM                        *            
      **************************************************************            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
            SELECT BP13F62A ASSIGN           TO BP13F62A.                       
            SELECT BP13K022 ASSIGN           TO BP13K022                        
                            ORGANIZATION     IS INDEXED                         
                            ACCESS MODE      IS RANDOM                          
                            RECORD KEY       IS K022-KEY-FLD                    
                            ALTERNATE KEY    IS K022-NUM-REGN                   
                            FILE STATUS      IS BP13K022-STATUS.                
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F62A                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 1200 CHARACTERS          
                                       RECORDING MODE IS F                      
                                       LABEL RECORDS ARE STANDARD.              
       COPY BP13F62A.                                                           
                                                                                
       FD  BP13K022                                                             
                                       RECORD CONTAINS 100  CHARACTERS.         
       COPY BP13K022.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-CONSTANTS.                                                        
           05  WS-TXN-CREATE           PIC X(02)  VALUE 'C'.                    
           05  WS-SYSTEM-ID            PIC X(04)  VALUE 'BP13'.                 
           05  WS-NUM-PRCS-CTGRY       PIC X(04)  VALUE '0076'.                 
           05  WS-NUM-BRNCH            PIC X(06)  VALUE '000001'.               
           05  WS-DEFAULT-DTE          PIC X(10)  VALUE '0001-01-01'.           
           05  WS-PROGRAM-ID           PIC X(08)  VALUE 'BP13C990'.             
           05  WS-STATUS-NEW           PIC X(03)  VALUE '000'.                  
           05  WS-PLANNED-INSPTN       PIC X(03)  VALUE '101'.                  
           05  WS-03-DAYS              PIC 9(01)  VALUE 3.                      
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-CURRENT-DTE-TME      PIC 9(14)  VALUE ZEROES.                 
           05  WS-DTE-CURRENT          PIC X(10)  VALUE SPACES.                 
           05  WS-TME-CURRENT          PIC X(08)  VALUE SPACES.                 
           05  WS-DTE-TEMP             PIC 9(08)  VALUE ZEROES.                 
           05  WS-INT-TEMP             PIC 9(08)  VALUE ZEROES.                 
           05  WS-COMP-DTE             PIC 9(08)  VALUE ZEROES.                 
           05  WS-COMP-DTE-TXT         PIC X(10)  VALUE SPACES.                 
           05  WS-DTE-DUE.                                                      
               10 WS-DTE-CCYY          PIC X(04).                               
               10 FILLER               PIC X(01)  VALUE '-'.                    
               10 WS-DTE-MM            PIC X(02).                               
               10 FILLER               PIC X(01)  VALUE '-'.                    
               10 WS-DTE-DD            PIC X(02).                               
                                                                                
       01  WS-STATUS.                                                           
           05  BP13K022-STATUS         PIC 9(02)  VALUE ZEROES.                 
           05  WS-OIC-EXIST-TAG        PIC X(01)  VALUE 'N'.                    
           05  WS-BP13F62A-EOF         PIC X(01)  VALUE 'N'.                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-CNT             PIC 9(07)  VALUE ZEROES.                 
           05  WS-WRITE-CNT            PIC 9(07)  VALUE ZEROES.                 
                                                                                
       01  WS-FIELDS.                                                           
           05  WS-STAFF                PIC X(06)  VALUE SPACES.                 
           05  WS-OIC                  PIC X(05)  VALUE SPACES.                 
                                                                                
      ******************************************************************        
      *  TEXT URL VARIABLES                                                     
      ******************************************************************        
       01  WS-INBOX-TXT-TARGET-URL.                                             
           05  FILLER              PIC X(69) VALUE                              
              '/be10/home/Newsales/sssCrm/cancellationSoca/processCancel        
      -       'Case?regnNo='.                                                   
           05  WS-CASE-REF-URL     PIC X(12).                                   
                                                                                
       01  WS-INBOX-TXT-REMARK.                                                 
           05  FILLER              PIC X(50) VALUE                              
              '18 days passed. Pending cancellation for Regn No. '.             
           05  WS-REGN-REMARK      PIC X(08) VALUE SPACES.                      
                                                                                
      ******************************************************************        
      *        LINKAGE FOR SUB-PROGRAM BP13CCNT                        *        
      ******************************************************************        
       01  WS-BP13CCNT-LINK.                                                    
           05  LINK-CNTCT-TYPE         PIC X(3).                                
           05  LINK-STAFF-NICK         PIC X(6).                                
           05  LINK-STAFF-NUM          PIC X(6).                                
           05  LINK-STAFF-CNTCT        PIC X(50).                               
           05  LINK-SYSERR             PIC X(4).                                
                                                                                
                                                                                
       COPY BE08W008.                                                           
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-LOGIC.                                                         
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F62A       THRU 2000-EXIT.                     
           IF WS-BP13F62A-EOF = 'Y'                                             
              DISPLAY 'FILE BP13F62A IS  EMPTY'                                 
           END-IF                                                               
           PERFORM 3000-INSERT-INBOX-INTRY  THRU 3000-EXIT                      
             UNTIL WS-BP13F62A-EOF = 'Y'.                                       
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-FILES.                                                         
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F62A                                                 
                       BP13K022.                                                
                                                                                
           IF BP13K022-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K022 OPEN ERROR, STATUS : ' BP13K022-STATUS          
              MOVE     BP13K022-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE   THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-BP13F62A.                                                      
      ****************************************************************          
                                                                                
           READ BP13F62A AT END                                                 
                         MOVE 'Y'  TO WS-BP13F62A-EOF                           
                         NOT AT END                                             
                         ADD  1    TO WS-READ-CNT                               
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2100-READ-BP13K022.                                                      
      ****************************************************************          
                                                                                
           READ BP13K022 KEY  IS K022-NUM-REGN.                                 
                                                                                
           EVALUATE BP13K022-STATUS                                             
             WHEN 00                                                            
                IF K022-NUM-OIC NOT     = SPACES  AND LOW-VALUES                
                   MOVE 'Y'             TO WS-OIC-EXIST-TAG                     
                   MOVE K022-NUM-OIC    TO WS-OIC                               
                ELSE                                                            
                   MOVE 'N'             TO WS-OIC-EXIST-TAG                     
                END-IF                                                          
             WHEN 23                                                            
                MOVE    'N'             TO WS-OIC-EXIST-TAG                     
             WHEN OTHER                                                         
                DISPLAY 'BP13K022 READ ERROR, STATUS : '                        
                         BP13K022-STATUS                                        
                MOVE BP13K022-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2200-CALL-BP13CCNT.                                                      
      ****************************************************************          
                                                                                
           CALL 'BP13CCNT' USING WS-BP13CCNT-LINK.                              
                                                                                
           EVALUATE LINK-SYSERR                                                 
           WHEN ZEROS                                                           
              CONTINUE                                                          
           WHEN 100                                                             
              DISPLAY 'OIC NOT FOUND IN OFFICE_CNTCT TABLE - '                  
                  WS-OIC ', REGN : ' F62A-NUM-REGN                              
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING OFFICE_CNTCT TABLE = '                     
                       LINK-SYSERR                                              
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3000-INSERT-INBOX-INTRY.                                                 
      ****************************************************************          
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:14) TO WS-CURRENT-DTE-TME.              
                                                                                
           STRING WS-CURRENT-DTE-TME(1:4) '-'                                   
                  WS-CURRENT-DTE-TME(5:2) '-'                                   
                  WS-CURRENT-DTE-TME(7:2) DELIMITED BY SIZE                     
                  INTO WS-DTE-CURRENT.                                          
                                                                                
           STRING WS-CURRENT-DTE-TME(9:2)                                       
                  '.'                                                           
                  WS-CURRENT-DTE-TME(11:2)                                      
                  '.'                                                           
                  WS-CURRENT-DTE-TME(13:2) DELIMITED BY SIZE                    
                  INTO  WS-TME-CURRENT.                                         
                                                                                
           MOVE SPACES                      TO  BE08W008-COMMAREA.              
           INITIALIZE                           BE08W008-COMMAREA.              
                                                                                
           PERFORM 3010-CREATE-URL-REMARK THRU  3010-EXIT.                      
           PERFORM 3020-COMPUTE-DATE-DUE  THRU  3020-EXIT.                      
           PERFORM 3030-GET-OIC           THRU  3030-EXIT.                      
                                                                                
           MOVE WS-TXN-CREATE               TO  W008-IN-TXN-TYPE.               
           MOVE WS-SYSTEM-ID                TO  W008-IN-SYSTEM-ID.              
           MOVE WS-NUM-PRCS-CTGRY           TO  W008-IN-NUM-PRCS-CTGRY.         
           MOVE WS-NUM-BRNCH                TO  W008-IN-NUM-BRNCH.              
           MOVE F62A-NUM-REGN               TO  W008-IN-NUM-CASE-REF.           
           MOVE F62A-NUM-HDB-REF            TO  W008-IN-HDBREF.                 
           MOVE WS-STAFF                    TO  W008-IN-NUM-STAFF.              
           MOVE WS-INBOX-TXT-TARGET-URL     TO  W008-IN-TXT-TARGET-URL.         
           MOVE WS-INBOX-TXT-REMARK         TO  W008-IN-TXT-REMARK.             
           MOVE WS-DTE-CURRENT              TO  W008-IN-DTE-INBOX.              
           MOVE WS-TME-CURRENT              TO  W008-IN-TME-INBOX.              
           MOVE WS-DTE-DUE                  TO  W008-IN-DUE-DATE.               
                                                                                
           CALL 'BE08C008'    USING BE08W008-COMMAREA.                          
           IF W008-OUT-RTN-MSGCDE = '0' OR 'W'                                  
              ADD 1                         TO  WS-WRITE-CNT                    
           ELSE                                                                 
              DISPLAY 'ERROR RETURNED CALLING BE08C008 SUBR (C) : '             
                                          W008-IN-NUM-CASE-REF                  
              DISPLAY '{ ' W008-OUT-RTN-MSGCDE ' }'                             
              DISPLAY '{ ' W008-OUT-RTN-MSGNO  ' }'                             
              DISPLAY '{ ' W008-OUT-RTN-DESC   ' }'                             
           END-IF.                                                              
           PERFORM 2000-READ-BP13F62A       THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3010-CREATE-URL-REMARK.                                                  
      ****************************************************************          
                                                                                
           MOVE SPACES           TO  WS-REGN-REMARK                             
                                     WS-CASE-REF-URL.                           
                                                                                
           MOVE F62A-NUM-REGN    TO  WS-CASE-REF-URL                            
                                     WS-REGN-REMARK.                            
                                                                                
       3010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       3020-COMPUTE-DATE-DUE.                                                   
      ****************************************************************          
                                                                                
           MOVE WS-CURRENT-DTE-TME(1:8) TO WS-DTE-TEMP.                         
                                                                                
           COMPUTE WS-INT-TEMP =                                                
                   FUNCTION INTEGER-OF-DATE (WS-DTE-TEMP) + WS-03-DAYS.         
                                                                                
           COMPUTE WS-COMP-DTE =                                                
                   FUNCTION DATE-OF-INTEGER (WS-INT-TEMP).                      
                                                                                
           MOVE WS-COMP-DTE             TO  WS-COMP-DTE-TXT.                    
                                                                                
           MOVE WS-COMP-DTE-TXT(1:4)    TO  WS-DTE-CCYY.                        
           MOVE WS-COMP-DTE-TXT(5:2)    TO  WS-DTE-MM.                          
           MOVE WS-COMP-DTE-TXT(7:2)    TO  WS-DTE-DD.                          
                                                                                
       3020-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3030-GET-OIC.                                                            
      ****************************************************************          
                                                                                
            MOVE SPACES                    TO   K022-NUM-REGN.                  
            MOVE F62A-NUM-REGN             TO   K022-NUM-REGN.                  
            PERFORM 2100-READ-BP13K022     THRU 2100-EXIT.                      
            IF WS-OIC-EXIST-TAG = 'Y'                                           
               MOVE SPACES                 TO WS-BP13CCNT-LINK                  
               MOVE 'EML'                  TO LINK-CNTCT-TYPE                   
               MOVE WS-OIC                 TO LINK-STAFF-NICK                   
               MOVE SPACES                 TO LINK-STAFF-NUM                    
               PERFORM 2200-CALL-BP13CCNT  THRU 2200-EXIT                       
               MOVE LINK-STAFF-NUM         TO WS-STAFF                          
            ELSE                                                                
               DISPLAY 'BP13K022 OIC NOT FOUND FOR REGN NUM '                   
                        F62A-NUM-REGN                                           
               PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                        
            END-IF.                                                             
                                                                                
       3030-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9000-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           DISPLAY '---- BP13C990 CONTROL TOTAL -----'.                         
           DISPLAY ' NO OF REC READ FROM BP13F62A   = '                         
                     WS-READ-CNT.                                               
           DISPLAY ' NO OF RECS SENT TO BE08        = '                         
                     WS-WRITE-CNT.                                              
           DISPLAY SPACES.                                                      
                                                                                
           CLOSE       BP13F62A                                                 
                       BP13K022.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
