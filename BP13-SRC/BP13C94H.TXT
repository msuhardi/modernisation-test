       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C94H.                                                    
       AUTHOR. KRISTINE LANTIN.                                                 
       DATE-WRITTEN. 11-10-2017.                                                
                                                                                
      *---------------------------------------------------------------*         
      * SOC (BP13)                                                    *         
      *---------------------------------------------------------------*         
      * PROGRAM   : BP13C94H                                          *         
      * AUTHOR    : KVL                                               *         
      * OBJECTIVE : TO GENERATE EMAIL ALERT ON OUTSTANDING CASES WITH *         
      *             PENDING LOA STATUS.                               *         
      *                                                               *         
      *                                                               *         
      *  FILES :                                                      *         
      *  INPUT  - BP13F62A                                            *         
      *  OUTPUT - BP13L94H                                            *         
      *                                                               *         
      *----------------------------------------------------------------         
      *                                                               *         
      * CHG-NO   DATE     BY   DESCRIPTION                            *         
      * -------- -------- ---  -----------                            *         
      * BP137028 20171011 KVL1 NEW PROGRAM                            *         
      * BP137155 20180198 PCL4 NEW PROGRAM                            *         
      * BP138757 20210723 KV8  TO REPLACE KATHIJAH TO SUZANNE         *         
      * BP139030 20220107 ZAR7 REPLACE CHUA YEE MIEN TO LIM KOK CHUN            
      * BP139276 20240405 kac1 REPLACE LIM KOK CHUN BY                *         
      *                                KATHIJAH_BEE_ALI_MOHAMED       *         
      *----------------------------------------------------------------         
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
       CONFIGURATION SECTION.                                                   
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F62A ASSIGN           TO BP13F62A.                        
           SELECT SY02F001 ASSIGN           TO SY02F001.                        
                                                                                
           SELECT BP13K022 ASSIGN           TO BP13K022                         
                           ORGANIZATION     IS INDEXED                          
                           ACCESS MODE      IS RANDOM                           
                           RECORD KEY       IS K022-KEY-FLD                     
                           ALTERNATE KEY    IS K022-NUM-REGN                    
                           FILE STATUS      IS BP13K022-STATUS.                 
                                                                                
           SELECT BP13K024 ASSIGN           TO BP13K024                         
                           ORGANIZATION     IS INDEXED                          
                           ACCESS MODE      IS RANDOM                           
                           RECORD KEY       IS K024-KEY-FLD                     
                           FILE STATUS      IS BP13K024-STATUS.                 
                                                                                
           SELECT BP13L94H ASSIGN           TO BP13L94H.                        
                                                                                
                                                                                
      *---------------------*                                                   
       DATA DIVISION.                                                           
      *---------------------*                                                   
       FILE SECTION.                                                            
                                                                                
       FD BP13F62A                                                              
            RECORD CONTAINS 1200 CHARACTERS                                     
            RECORDING MODE IS F.                                                
       COPY BP13F62A.                                                           
                                                                                
       FD BP13K022                                                              
            RECORD CONTAINS 100  CHARACTERS.                                    
       COPY BP13K022.                                                           
                                                                                
       FD BP13K024                                                              
            RECORD CONTAINS 100  CHARACTERS.                                    
       COPY BP13K024.                                                           
                                                                                
       FD BP13L94H                                                              
            RECORD CONTAINS 80  CHARACTERS                                      
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01  BP13L94H-REC             PIC X(80).                                  
                                                                                
       COPY SY02F001.                                                           
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-EOF-VARIABLES.                                                    
           05  WS-F62A-EOF            PIC X VALUE 'N'.                          
           05  WS-PRT-FLAG            PIC X VALUE 'Y'.                          
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F62A-READ           PIC 9(08) VALUE ZEROES.                   
           05  WS-L94H-WRITE          PIC 9(08) VALUE ZEROES.                   
           05  WS-LINE-CTR            PIC 9(02) VALUE 99.                       
           05  WS-SLNO                PIC 9(04) VALUE 1.                        
           05  WS-S                   PIC 9(02) VALUE ZEROES.                   
                                                                                
       01  WS-STATUS.                                                           
           05  BP13K022-STATUS        PIC 9(02) VALUE ZEROES.                   
           05  BP13K024-STATUS        PIC 9(02) VALUE ZEROES.                   
                                                                                
       01  WS-DX                      PIC X(08) VALUE SPACES.                   
       01  WS-DATE REDEFINES WS-DX    PIC 9(08).                                
                                                                                
       01  WS-DATE-FLDS.                                                        
           05  WS-CHECK-DATE.                                                   
               10  WS-CCYY             PIC 9(04)  VALUE ZEROES.                 
               10  WS-MM               PIC 9(02)  VALUE ZEROES.                 
               10  WS-DD               PIC 9(02)  VALUE ZEROES.                 
           05  WS-REMAINDER            PIC 9(01)  VALUE ZERO.                   
           05  WS-QUOTIENT             PIC 9(02)  VALUE ZEROES.                 
           05  WS-DAYS                 PIC 9(03)  VALUE ZEROES.                 
           05  WS-INT-DTE-CANCEL       PIC 9(08)  VALUE ZEROES.                 
           05  WS-INT-DTE-NOTIFY       PIC 9(08)  VALUE ZEROES.                 
           05  WS-DTE-NOTIFY           PIC 9(08)  VALUE ZEROES.                 
                                                                                
       01  WS-DATE1.                                                            
           05  WS-CCYY-D1             PIC X(04).                                
           05  WS-MM-D1               PIC X(02).                                
           05  WS-DD-D1               PIC X(02).                                
                                                                                
       01  WS-REQ-DATE.                                                         
           05  WS-CCYY-S              PIC X(04).                                
           05  WS-MM-S                PIC X(02).                                
           05  WS-DD-S                PIC X(02).                                
                                                                                
       01 WS-REF-DATE.                                                          
           05  WS-REF-CCYY            PIC 9(04).                                
           05  WS-REF-MM              PIC 9(02).                                
           05  WS-REF-DD              PIC 9(02).                                
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-REGN            PIC X(13) VALUE SPACES.                   
           05  WS-HDB-REF             PIC X(11) VALUE SPACES.                   
           05  WS-OIC                 PIC X(05) VALUE SPACES.                   
           05  WS-OIC-NAME            PIC X(30) VALUE SPACES.                   
                                                                                
       01  WS-EMAIL-RPT-VARIABLES.                                              
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27) VALUE                         
                   'MAIL FROM:<OPCP@SGPHDB1>'.                                  
           05  WS-MAIL-RPT3             PIC X(60) VALUE                         
      *            'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG>'.             
                   'RCPT TO:<SUZANNE_SC_HENG@HDB.GOV.SG>'.                      
           05  WS-MAIL-RPT3A            PIC X(60) VALUE                         
                   'RCPT TO:<MUHAMMED_SHARIFF_SAHROM@HDB.GOV.SG>'.              
           05  WS-MAIL-RPT3B            PIC X(60) VALUE                         
                   'RCPT TO:<BALASUBRAMANIAM_ARUNASALAM@HDB.GOV.SG>'.           
           05  WS-MAIL-RPT3C            PIC X(60) VALUE                         
                   'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.             
           05  WS-MAIL-RPT4             PIC X(4)  VALUE                         
                   'DATA'.                                                      
           05  WS-MAIL-RPT5             PIC X(40) VALUE                         
                   'FROM:Soc System - Email Alert'.                             
           05  WS-MAIL-RPT6             PIC X(60) VALUE                         
      *            'TO:<KATHIJAH_BEE_ALI_MOHAMED@HDB.GOV.SG>'.                  
                   'TO:<SUZANNE_SC_HENG@HDB.GOV.SG>'.                           
           05  WS-MAIL-RPT6A            PIC X(60) VALUE                         
                   'CC:<MUHAMMED_SHARIFF_SAHROM@HDB.GOV.SG>'.                   
           05  WS-MAIL-RPT6B            PIC X(60) VALUE                         
                   'CC:<BALASUBRAMANIAM_ARUNASALAM@HDB.GOV.SG>'.                
           05  WS-MAIL-RPT6C            PIC X(60) VALUE                         
                   'CC:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                  
           05  WS-MAIL-RPT7             PIC X(100) VALUE                        
                   'SUBJECT: Outstanding Cancelled Cases with Pending LO        
      -            'A status.'.                                                 
                                                                                
       01  WS-L94H-HDNG1.                                                       
           05  FILLER                 PIC X(08)  VALUE 'BP13L94H'.              
           05  FILLER                 PIC X(02)  VALUE SPACES.                  
           05  FILLER                 PIC X(04)  VALUE 'HDB3'.                  
           05  FILLER                 PIC X(02)  VALUE SPACES.                  
           05  FILLER                 PIC X(39)  VALUE                          
                'S Y S T E M   O F   C O M M I T M E N T'.                      
           05  FILLER                 PIC X(06)  VALUE SPACES.                  
           05  FILLER                 PIC X(07)  VALUE 'DATE: '.                
           05  WS-L94H-DATE           PIC X(11)  VALUE SPACES.                  
                                                                                
       01  WS-L94H-HDNG2.                                                       
           05  FILLER                 PIC X(08) VALUE SPACES.                   
           05  FILLER                 PIC X(70) VALUE                           
              'REPORT ON OUTSTANDING CASES WITH PENDING LOA STATUS'.            
                                                                                
       01  WS-L94H-HDNG3.                                                       
           05  FILLER  PIC X(01) VALUE SPACES.                                  
           05  FILLER  PIC X(04) VALUE 'S/NO'.                                  
           05  FILLER  PIC X(03) VALUE SPACES.                                  
           05  FILLER  PIC X(11) VALUE 'Regn Number'.                           
           05  FILLER  PIC X(04) VALUE SPACES.                                  
           05  FILLER  PIC X(07) VALUE 'Date of'.                               
           05  FILLER  PIC X(06) VALUE SPACES.                                  
           05  FILLER  PIC X(12) VALUE 'Notification'.                          
           05  FILLER  PIC X(07) VALUE SPACES.                                  
           05  FILLER  PIC X(08) VALUE 'POSB OIC'.                              
                                                                                
       01  WS-L94H-HDNG3A.                                                      
           05  FILLER  PIC X(22) VALUE SPACES.                                  
           05  FILLER  PIC X(12) VALUE 'Cancellation'.                          
           05  FILLER  PIC X(04) VALUE SPACES.                                  
           05  FILLER  PIC X(09) VALUE 'Sent Date'.                             
                                                                                
       01  WS-L94H-DET1.                                                        
           05  WS-L94H-CNT            PIC ZZZ99  VALUE ZEROES.                  
           05  FILLER                 PIC X(04)  VALUE SPACES.                  
           05  WS-L94H-NUM-REGN       PIC X(08)  VALUE SPACES.                  
           05  FILLER                 PIC X(06)  VALUE SPACES.                  
           05  WS-L94H-DTE-CANCEL     PIC X(08)  VALUE SPACES.                  
           05  FILLER                 PIC X(07)  VALUE SPACES.                  
           05  WS-L94H-DTE-NOTIFY     PIC X(08)  VALUE SPACES.                  
           05  FILLER                 PIC X(04)  VALUE SPACES.                  
           05  WS-L94H-OIC            PIC X(25)  VALUE SPACES.                  
                                                                                
       01  WS-L94H-NULL-REPORT.                                                 
           05  FILLER                 PIC X(33)  VALUE SPACES.                  
           05  WS-L94H-MESSAGE        PIC X(50)  VALUE SPACES.                  
           05  FILLER                 PIC X(52)  VALUE SPACES.                  
                                                                                
       01  WS-HDNG-LINE               PIC X(132) VALUE ALL '-'.                 
                                                                                
       01  WS-BLANK-LINE              PIC X(132) VALUE SPACES.                  
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      *================================================================*        
       0000-CONTROL.                                                            
      *================================================================*        
                                                                                
            PERFORM 1000-OPEN-FILES       THRU  1000-EXIT.                      
                                                                                
            PERFORM 2000-READ-BP13F62A    THRU  2000-EXIT.                      
            PERFORM 3000-PROCESS-BP13F62A THRU  3000-EXIT                       
                    UNTIL WS-F62A-EOF = 'Y'.                                    
                                                                                
            PERFORM 9000-CLOSE-FILES      THRU  9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       1000-OPEN-FILES.                                                         
      *================================================================*        
            OPEN INPUT     BP13F62A                                             
                           SY02F001                                             
                           BP13K022                                             
                           BP13K024                                             
                 OUTPUT    BP13L94H.                                            
                                                                                
           IF BP13K022-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K022 OPEN ERROR, STATUS : ' BP13K022-STATUS          
              MOVE     BP13K022-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           IF BP13K024-STATUS NOT = 00 AND 97                                   
              DISPLAY 'BP13K024 OPEN ERROR, STATUS : ' BP13K024-STATUS          
              MOVE     BP13K024-STATUS      TO  RETURN-CODE                     
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
            PERFORM 2100-READ-SY02F001 THRU 2100-EXIT.                          
            MOVE F001-DTE-CURRENT      TO   WS-DATE.                            
            MOVE WS-DATE               TO   WS-DATE1.                           
            STRING WS-DD-D1, '/', WS-MM-D1, '/', WS-CCYY-D1                     
                DELIMITED BY SIZE INTO WS-L94H-DATE.                            
                                                                                
            PERFORM 4000-HEADER-EMAIL  THRU 4000-EXIT.                          
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       2000-READ-BP13F62A.                                                      
      *================================================================*        
                                                                                
           READ BP13F62A AT END MOVE 'Y' TO WS-F62A-EOF                         
                           GO TO 2000-EXIT.                                     
                                                                                
              ADD 1 TO WS-F62A-READ.                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       2100-READ-SY02F001.                                                      
      *================================================================*        
                                                                                
           READ SY02F001 AT END                                                 
                DISPLAY 'EMPTY FILE - SY02F001'                                 
                MOVE    10                 TO   RETURN-CODE                     
                ADD     1                  TO   WS-L94H-WRITE                   
                PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT.                      
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3000-PROCESS-BP13F62A.                                                   
      *================================================================*        
                                                                                
           PERFORM 3100-GET-OIC          THRU 3100-EXIT.                        
           PERFORM 3200-GET-DTE-NOTIFY   THRU 3200-EXIT.                        
           MOVE WS-SLNO                    TO WS-L94H-CNT                       
           MOVE F62A-NUM-REGN              TO WS-L94H-NUM-REGN                  
           MOVE F62A-DTE-CANCELLATION      TO WS-L94H-DTE-CANCEL                
           MOVE WS-DTE-NOTIFY              TO WS-L94H-DTE-NOTIFY                
           MOVE WS-OIC-NAME                TO WS-L94H-OIC                       
                                                                                
           WRITE BP13L94H-REC              FROM WS-L94H-DET1                    
           ADD 1                           TO WS-L94H-WRITE                     
           ADD 1                           TO WS-SLNO                           
           PERFORM 2000-READ-BP13F62A      THRU  2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       3100-GET-OIC.                                                            
      *================================================================*        
                                                                                
           MOVE SPACES                  TO   K022-NUM-REGN.                     
           MOVE F62A-NUM-REGN           TO   K022-NUM-REGN.                     
           PERFORM 3110-READ-BP13K022   THRU 3110-EXIT.                         
           MOVE SPACES                  TO   K024-NUM-OIC.                      
           MOVE WS-OIC                  TO   K024-NUM-OIC.                      
           PERFORM 3120-READ-BP13K024   THRU 3120-EXIT.                         
                                                                                
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       3110-READ-BP13K022.                                                      
      *================================================================*        
                                                                                
           READ BP13K022 KEY  IS K022-NUM-REGN.                                 
                                                                                
           EVALUATE BP13K022-STATUS                                             
             WHEN 00                                                            
                IF K022-NUM-OIC NOT        = SPACES  AND LOW-VALUES             
                   MOVE K022-NUM-OIC       TO WS-OIC                            
                ELSE                                                            
                   MOVE SPACES             TO WS-OIC                            
                END-IF                                                          
             WHEN 23                                                            
                MOVE SPACES                TO WS-OIC                            
             WHEN OTHER                                                         
                DISPLAY 'BP13K022 READ ERROR, STATUS : '                        
                         BP13K022-STATUS                                        
                MOVE BP13K022-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       3120-READ-BP13K024.                                                      
      *================================================================*        
                                                                                
           READ BP13K024.                                                       
                                                                                
           EVALUATE BP13K024-STATUS                                             
             WHEN 00                                                            
                MOVE K024-NME-OIC          TO WS-OIC-NAME                       
             WHEN 23                                                            
                MOVE SPACES                TO WS-OIC-NAME                       
             WHEN OTHER                                                         
                DISPLAY 'BP13K024 READ ERROR, STATUS : '                        
                         BP13K024-STATUS                                        
                MOVE BP13K024-STATUS       TO RETURN-CODE                       
                PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       3120-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       3200-GET-DTE-NOTIFY.                                                     
      *================================================================*        
                                                                                
           MOVE ZEROES                      TO   WS-DAYS.                       
           MOVE F62A-DTE-CANCELLATION       TO   WS-DX.                         
           COMPUTE WS-INT-DTE-CANCEL =                                          
                   FUNCTION INTEGER-OF-DATE (WS-DATE).                          
           MOVE WS-DATE                     TO   WS-CHECK-DATE.                 
           PERFORM 3210-GET-MONTHS          THRU 3210-EXIT                      
           IF WS-DAYS = WS-DD                                                   
              ADD 1                         TO   WS-MM                          
              IF WS-MM = 13                                                     
                 MOVE 1                     TO   WS-MM                          
                 ADD  1                     TO   WS-CCYY                        
              END-IF                                                            
           END-IF.                                                              
           MOVE ZEROES                      TO   WS-DAYS.                       
      ************ GETTING 3 MONTHS *************************                   
           PERFORM VARYING WS-S FROM 1 BY 1 UNTIL WS-S > 3                      
              PERFORM 3210-GET-MONTHS       THRU 3210-EXIT                      
              ADD 1                         TO   WS-MM                          
              IF WS-MM = 13                                                     
                 MOVE 1                     TO   WS-MM                          
                 ADD  1                     TO   WS-CCYY                        
              END-IF                                                            
           END-PERFORM.                                                         
           COMPUTE WS-INT-DTE-NOTIFY =                                          
                   WS-INT-DTE-CANCEL + WS-DAYS.                                 
           COMPUTE WS-DTE-NOTIFY     =                                          
                   FUNCTION DATE-OF-INTEGER (WS-INT-DTE-NOTIFY).                
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3210-GET-MONTHS.                                                         
      *================================================================*        
                                                                                
           EVALUATE WS-MM                                                       
              WHEN 01                                                           
              WHEN 03                                                           
              WHEN 05                                                           
              WHEN 07                                                           
              WHEN 08                                                           
              WHEN 10                                                           
              WHEN 12                                                           
                 ADD  31        TO WS-DAYS                                      
              WHEN 02                                                           
                 DIVIDE WS-CCYY BY 4  GIVING    WS-QUOTIENT                     
                                      REMAINDER WS-REMAINDER                    
                 IF WS-CCYY = 1800 OR 1900                                      
                    ADD  28     TO WS-DAYS                                      
                 ELSE                                                           
                    IF WS-REMAINDER = 0                                         
                       ADD  29  TO WS-DAYS                                      
                    ELSE                                                        
                       ADD  28  TO WS-DAYS                                      
                    END-IF                                                      
                 END-IF                                                         
              WHEN OTHER                                                        
                 ADD  30        TO WS-DAYS                                      
           END-EVALUATE.                                                        
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *================================================================*        
       4000-HEADER-EMAIL.                                                       
      *================================================================*        
                                                                                
            WRITE BP13L94H-REC FROM WS-MAIL-RPT1.                               
            WRITE BP13L94H-REC FROM WS-MAIL-RPT2.                               
            WRITE BP13L94H-REC FROM WS-MAIL-RPT3.                               
            WRITE BP13L94H-REC FROM WS-MAIL-RPT3A.                              
            WRITE BP13L94H-REC FROM WS-MAIL-RPT3B.                              
            WRITE BP13L94H-REC FROM WS-MAIL-RPT3C.                              
            WRITE BP13L94H-REC FROM WS-MAIL-RPT4.                               
            WRITE BP13L94H-REC FROM WS-MAIL-RPT5.                               
            WRITE BP13L94H-REC FROM WS-MAIL-RPT6.                               
            WRITE BP13L94H-REC FROM WS-MAIL-RPT6A.                              
            WRITE BP13L94H-REC FROM WS-MAIL-RPT6B.                              
            WRITE BP13L94H-REC FROM WS-MAIL-RPT6C.                              
            WRITE BP13L94H-REC FROM WS-MAIL-RPT7.                               
            MOVE  WS-L94H-DATE   TO WS-DATE-RPT.                                
            WRITE BP13L94H-REC FROM WS-DATE-RPT-DTL.                            
            WRITE BP13L94H-REC FROM WS-BLANK-LINE.                              
            WRITE BP13L94H-REC FROM WS-BLANK-LINE.                              
            WRITE BP13L94H-REC FROM WS-L94H-HDNG1.                              
            WRITE BP13L94H-REC FROM WS-BLANK-LINE.                              
            WRITE BP13L94H-REC FROM WS-L94H-HDNG2.                              
            WRITE BP13L94H-REC FROM WS-BLANK-LINE.                              
            WRITE BP13L94H-REC FROM WS-BLANK-LINE.                              
            WRITE BP13L94H-REC FROM WS-L94H-HDNG3.                              
            WRITE BP13L94H-REC FROM WS-L94H-HDNG3A.                             
            WRITE BP13L94H-REC FROM WS-HDNG-LINE.                               
            WRITE BP13L94H-REC FROM WS-BLANK-LINE.                              
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-FILES.                                                        
      *================================================================*        
           IF WS-L94H-WRITE = ZEROES                                            
              WRITE BP13L94H-REC FROM WS-BLANK-LINE                             
              WRITE BP13L94H-REC FROM WS-BLANK-LINE                             
              MOVE 'NO OUTSTANDING CASES ' TO                                   
                 WS-L94H-MESSAGE                                                
              WRITE BP13L94H-REC       FROM WS-L94H-NULL-REPORT                 
           END-IF.                                                              
                                                                                
           CLOSE BP13F62A                                                       
                 BP13K022                                                       
                 BP13K024                                                       
                 SY02F001                                                       
                 BP13L94H.                                                      
                                                                                
           DISPLAY '** BP13C94H **'.                                            
           DISPLAY 'F62A RECORDS READ    : ' WS-F62A-READ.                      
           DISPLAY 'L94H RECORDS WRITTEN : ' WS-L94H-WRITE.                     
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
