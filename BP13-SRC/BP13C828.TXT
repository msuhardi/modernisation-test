       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C828.                                                    
      *AUTHOR. JAI.                                                             
      *DATE-WRITTEN . 25/JAN/95                                                 
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *========================================================*                
      *    OBJECTIVE :                                         *                
      *      1.  THIS PROGRAM COMPARES F410 FILE WITH F835     *                
      *          AND IF THE REGN NO MATCHES  THEN READ K825    *                
      *          WITH REGN-NO AS KEY AND DELETE THE RECORD.    *                
      **********************************************************                
      *--------------------------------------------------------*                
      * CHG-REQ     BY       DATE            DESCR.            *                
      *--------------------------------------------------------*                
      * BP130418    SGK     03/09/1998      TO CATER FOR Y2K.  *                
      *--------------------------------------------------------*                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410 ASSIGN TO BP13F410.                                  
           SELECT BP13F835 ASSIGN TO BP13F835.                                  
           SELECT BP13K835 ASSIGN TO BP13K835                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K835-KEY-FLD                               
                  FILE STATUS     IS K835-STATUS.                               
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F410                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F410.                                                           
                                                                                
       FD   BP13F835                                                            
            RECORD CONTAINS 250 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F835.                                                           
                                                                                
       FD   BP13K835                                                            
            RECORD CONTAINS 250 CHARACTERS.                                     
       COPY BP13K835.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-F410-READ             PIC 9(08) VALUE 0.                          
       01  WS-F835-READ             PIC 9(08) VALUE 0.                          
       01  WS-K835-FOUND            PIC 9(08) VALUE 0.                          
       01  WS-K835-NOTFND           PIC 9(08) VALUE 0.                          
       01  WS-K835-DELETE           PIC 9(08) VALUE 0.                          
       01  WS-MATCHED               PIC 9(08) VALUE 0.                          
       01  K835-STATUS              PIC 9(02) VALUE 0.                          
                                                                                
       01  WS-CUR-DATE.                                                         
           05 WS-YYYY              PIC 9(04).                                   
           05 WS-MM                PIC 9(02).                                   
           05 WS-DD                PIC 9(02).                                   
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *---------*                                                               
       000-MAIN.                                                                
      *---------*                                                               
                                                                                
           OPEN INPUT  BP13F410                                                 
                       BP13F835                                                 
                I-O    BP13K835.                                                
                                                                                
           IF K835-STATUS EQUAL 00 OR 97                                        
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'ERROR IN OPENING K835 FILE ' K835-STATUS                 
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                           
           END-IF.                                                              
                                                                                
           PERFORM 100-READ-F410-REC THRU 100-EXIT.                             
           PERFORM 200-READ-F835-REC THRU 200-EXIT.                             
                                                                                
           PERFORM 300-PROCESS-REC   THRU 300-EXIT                              
                   UNTIL F410-NUM-REGN      = HIGH-VALUE                        
                     AND F835-NUM-REGN-NRIC = HIGH-VALUE.                       
                                                                                
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       100-READ-F410-REC.                                                       
      *-----------------*                                                       
                                                                                
           READ BP13F410 AT END                                                 
                MOVE HIGH-VALUE TO F410-NUM-REGN                                
                GO TO 100-EXIT                                                  
           END-READ                                                             
                                                                                
           ADD 1 TO WS-F410-READ.                                               
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       200-READ-F835-REC.                                                       
      *-----------------*                                                       
                                                                                
           READ BP13F835 AT END                                                 
                MOVE HIGH-VALUE TO F835-NUM-REGN-NRIC                           
                GO TO 200-EXIT                                                  
           END-READ                                                             
                                                                                
           ADD 1 TO WS-F835-READ.                                               
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *==============================================*                          
      * COMPARE F410 AND F835 ON REGN NO.            *                          
      *==============================================*                          
                                                                                
      *---------------*                                                         
       300-PROCESS-REC.                                                         
      *---------------*                                                         
                                                                                
           IF F410-NUM-REGN = F835-NUM-REGN-NRIC (1:8)                          
              ADD     1                  TO   WS-MATCHED                        
              PERFORM 400-READ-K835-REC  THRU 400-EXIT                          
              PERFORM 200-READ-F835-REC  THRU 200-EXIT                          
           ELSE                                                                 
               IF F410-NUM-REGN > F835-NUM-REGN-NRIC (1:8)                      
                  PERFORM 200-READ-F835-REC  THRU 200-EXIT                      
               ELSE                                                             
                  PERFORM 100-READ-F410-REC  THRU 100-EXIT                      
               END-IF                                                           
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       400-READ-K835-REC.                                                       
      *-----------------*                                                       
                                                                                
           MOVE LOW-VALUES         TO BP13K835-REC.                             
           MOVE F835-KEY-FLD       TO K835-KEY-FLD.                             
                                                                                
           READ BP13K835.                                                       
                                                                                
           EVALUATE K835-STATUS                                                 
           WHEN  00                                                             
                                                                                
                 ADD 1 TO WS-K835-FOUND                                         
                                                                                
                 DELETE BP13K835                                                
                                                                                
                 EVALUATE K835-STATUS                                           
                 WHEN 00                                                        
                      ADD  1  TO WS-K835-DELETE                                 
                 WHEN OTHER                                                     
                      DISPLAY 'ERROR IN DELETEING K835 RECORD '                 
                                                    K835-STATUS                 
                      DISPLAY 'REGISTRATION NO IS ' K835-KEY-FLD                
                      PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                   
                END-EVALUATE                                                    
                                                                                
           WHEN  23                                                             
                 DISPLAY 'RECORD NOT FOUND IN K835 : '   K835-KEY-FLD           
                 ADD 1 TO WS-K835-NOTFND                                        
           WHEN  OTHER                                                          
                 DISPLAY 'ERROR IN READING K835 RECORD ' K835-STATUS            
                 DISPLAY 'REGISTRATION NO IS ' K835-KEY-FLD                     
                 PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       900-CLOSE-ROUTINE.                                                       
      *-----------------*                                                       
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-CUR-DATE.                    
           DISPLAY 'PROGRAM-ID : BP13C828  '.                                   
           DISPLAY ' MATCH FILES         '.                                     
           DISPLAY ' CURRENT DATE IS  :  ' WS-DD '/' WS-MM '/' WS-YYYY.         
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECORDS READ FROM F410  ' WS-F410-READ.               
           DISPLAY 'NO OF RECORDS READ FROM F835  ' WS-F835-READ.               
           DISPLAY 'NO OF RECORDS MATCHED IN FILE ' WS-MATCHED.                 
           DISPLAY 'NO OF RECORDS FOUND IN  K835  ' WS-K835-FOUND.              
           DISPLAY 'NO OF RECORDS NOT FOUND K835  ' WS-K835-NOTFND.             
           DISPLAY 'NO OF RECORDS DELETED   K835  ' WS-K835-DELETE.             
                                                                                
           CLOSE BP13F410                                                       
                 BP13F835                                                       
                 BP13K835.                                                      
                                                                                
           IF K835-STATUS EQUAL 00 OR 97                                        
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'ERROR IN CLOSING K835 FILE ' K835-STATUS                 
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
