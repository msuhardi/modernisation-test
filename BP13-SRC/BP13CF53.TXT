       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF53.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  12/02/2015.                                               
      *===============================================================*         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      *  OBJECTIVE  :  CHECK FOR PRIORITY SCHEMES WITH NO QUOTA       *         
      *                                                               *         
      *  INPUT      :  BP13F730 - FULL FILE                           *         
      *                BP13K811 - PRIORITY SCHEMES WITH NO QUOTA      *         
      *  OUTPUT     :  P13F730A - OUTPUT FILE                         *         
      *                                                               *         
      *===============================================================*         
      * CHG-NO   DATE     BY   DESCRIPTION                            *         
      * -------- -------- ---- -------------------------------------- *         
      * BP135661 12022015 SMR2 NEW PROGRAM                            *         
      * BP136506 08112016 IL5  EXCLUDE ELDERLY APPLICATIONS FROM MCP  *         
      *                        CHANGE HH TO BALLOT HH                 *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN TO BP13F730.                                  
                                                                                
           SELECT BP13K811 ASSIGN TO BP13K811                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K811-KEY-FLD                               
                  FILE STATUS     IS WS-K811-STATUS.                            
                                                                                
           SELECT P13F730A ASSIGN TO P13F730A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F730                                                             
           RECORDING MODE   IS F                                                
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13F730.                                                           
                                                                                
       FD  BP13K811                                                             
           RECORD CONTAINS  80 CHARACTERS.                                      
       COPY BP13K811.                                                           
                                                                                
       FD  P13F730A                                                             
           RECORDING MODE   IS F                                                
           RECORD CONTAINS 500 CHARACTERS.                                      
       01  P13F730A-REC                PIC X(500).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-STATUS.                                                           
           05  WS-K811-STATUS          PIC X(02) VALUE SPACES.                  
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F730-EOF             PIC X(01) VALUE 'N'.                     
                                                                                
       01  WS-FILE-COUNTERS.                                                    
           05  WS-F730-READ            PIC 9(08) VALUE ZEROES.                  
           05  WS-MCPG-WRITE           PIC 9(08) VALUE ZEROES.                  
           05  WS-MCPH-WRITE           PIC 9(08) VALUE ZEROES.                  
           05  WS-TCP-WRITE            PIC 9(08) VALUE ZEROES.                  
           05  WS-TPS-WRITE            PIC 9(08) VALUE ZEROES.                  
           05  WS-OTHER-WRITE          PIC 9(08) VALUE ZEROES.                  
           05  WS-K811-READ            PIC 9(08) VALUE ZEROES.                  
           05  WS-K811-NOTFND          PIC 9(08) VALUE ZEROES.                  
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-EDT-DATE             PIC X(10) VALUE SPACES.                  
           05  WS-CUR-DATE             PIC X(08) VALUE SPACES.                  
           05  WS-EDIT1                PIC Z(07)9.                              
                                                                                
       01  WS-F730-KEY-FLD.                                                     
           05  WS-NUM-SALES-MODE       PIC X(03) VALUE SPACES.                  
           05  WS-DTE-BALLOT           PIC X(06) VALUE SPACES.                  
           05  WS-CDE-NEW-TOWN         PIC X(03) VALUE SPACES.                  
           05  WS-CDE-FLAT-TYPE        PIC X(02) VALUE SPACES.                  
           05  WS-CDE-ALLOC-SCH        PIC X(03) VALUE SPACES.                  
           05  WS-CDE-HOUSEHOLD        PIC X(01) VALUE SPACES.                  
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F730      THRU 2000-EXIT                       
             UNTIL WS-F730-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  BP13F730                                                 
                       BP13K811                                                 
                OUTPUT P13F730A.                                                
                                                                                
           IF WS-K811-STATUS NOT = '00' AND '97'                                
              DISPLAY 'ERROR IN OPENING BP13K811 : ' WS-K811-STATUS             
              MOVE WS-K811-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CUR-DATE.                      
           STRING WS-CUR-DATE(7:2), '/',                                        
                  WS-CUR-DATE(5:2), '/',                                        
                  WS-CUR-DATE(1:4)  DELIMITED BY SIZE                           
                  INTO WS-EDT-DATE.                                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F730.                                                      
      *---------------------------------------------------------------*         
           READ BP13F730                                                        
             AT END                                                             
                MOVE 'Y'                   TO WS-F730-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                           TO WS-F730-READ.                     
                                                                                
           MOVE F730-NUM-ALLO-CAT          TO WS-NUM-SALES-MODE.                
           MOVE F730-DTE-BALLOT            TO WS-DTE-BALLOT.                    
           MOVE F730-CDE-NT1               TO WS-CDE-NEW-TOWN.                  
           MOVE F730-CDE-FLAT-TYPE         TO WS-CDE-FLAT-TYPE.                 
           IF (F730-CDE-RESIDENT-HA1 = '1' OR 'A' OR 'T') AND                   
              (F730-NUM-MCPS = 'M' OR 'C' OR 'P' OR 'B') AND                    
              (F730-NUM-ELDERLY = ' ' OR 'N')                                   
              MOVE 'MCP'                   TO WS-CDE-ALLOC-SCH                  
           ELSE                                                                 
              IF F730-NUM-PORT-ELIG-TAG = 'P'                                   
                 MOVE 'SER'                TO WS-CDE-ALLOC-SCH                  
              ELSE                                                              
                 MOVE F730-CDE-ALLOC-SCH   TO WS-CDE-ALLOC-SCH                  
              END-IF                                                            
           END-IF.                                                              
           IF F730-CDE-BALLOT-HOUSEHOLD = 'H' OR 'T' OR 'F'                     
              MOVE 'H'                     TO WS-CDE-HOUSEHOLD                  
           ELSE                                                                 
              MOVE 'G'                     TO WS-CDE-HOUSEHOLD                  
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-BP13K811      THRU 3000-EXIT.                      
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-READ-BP13K811.                                                      
      *---------------------------------------------------------------*         
           MOVE SPACES                      TO BP13K811-REC.                    
           INITIALIZE                          BP13K811-REC.                    
                                                                                
           DISPLAY WS-F730-KEY-FLD                                              
           MOVE WS-F730-KEY-FLD             TO K811-KEY-FLD.                    
                                                                                
           READ BP13K811.                                                       
                                                                                
           EVALUATE WS-K811-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              PERFORM 4000-WRITE-P13F730A   THRU 4000-EXIT                      
              ADD   1                       TO WS-K811-READ                     
           WHEN 23                                                              
              CONTINUE                                                          
              ADD   1                       TO WS-K811-NOTFND                   
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K811 : ' WS-K811-STATUS             
              MOVE WS-K811-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE    THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-WRITE-P13F730A.                                                     
      *---------------------------------------------------------------*         
           EVALUATE K811-CDE-ALLOC-SCH                                          
           WHEN 'MCP'                                                           
              IF K811-CDE-HOUSEHOLD = 'G'                                       
                 MOVE 'Y'                   TO BP13F730-REC(4:1)                
                 WRITE P13F730A-REC         FROM BP13F730-REC                   
                 ADD 1                      TO WS-MCPG-WRITE                    
              END-IF                                                            
              IF K811-CDE-HOUSEHOLD = 'H'                                       
                 MOVE 'Y'                   TO BP13F730-REC(5:1)                
                 WRITE P13F730A-REC         FROM BP13F730-REC                   
                 ADD 1                      TO WS-MCPH-WRITE                    
              END-IF                                                            
                                                                                
           WHEN 'TCP'                                                           
              MOVE 'Y'                      TO BP13F730-REC(15:1)               
              WRITE P13F730A-REC            FROM BP13F730-REC                   
              ADD 1                         TO WS-TCP-WRITE                     
                                                                                
           WHEN 'TPS'                                                           
              MOVE 'Y'                      TO BP13F730-REC(16:1)               
              WRITE P13F730A-REC            FROM BP13F730-REC                   
              ADD 1                         TO WS-TPS-WRITE                     
                                                                                
           WHEN OTHER                                                           
              MOVE 'Y'                      TO BP13F730-REC(17:1)               
              WRITE P13F730A-REC            FROM BP13F730-REC                   
              ADD 1                         TO WS-OTHER-WRITE                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13CF53       ' WS-EDT-DATE.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-F730-READ               TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS READ FROM BP13F730        : ' WS-EDIT1.          
           MOVE WS-K811-READ               TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS READ FROM BP13K811        : ' WS-EDIT1.          
           MOVE WS-K811-NOTFND             TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS NFND FROM BP13K811        : ' WS-EDIT1.          
           MOVE WS-MCPG-WRITE              TO WS-EDIT1.                         
           DISPLAY 'NO OF MCP-G RECS WRITTEN TO P13F730A : ' WS-EDIT1.          
           MOVE WS-MCPH-WRITE              TO WS-EDIT1.                         
           DISPLAY 'NO OF MCP-H RECS WRITTEN TO P13F730A : ' WS-EDIT1.          
           MOVE WS-TCP-WRITE               TO WS-EDIT1.                         
           DISPLAY 'NO OF TCP RECS WRITTEN TO P13F730A   : ' WS-EDIT1.          
           MOVE WS-TPS-WRITE               TO WS-EDIT1.                         
           DISPLAY 'NO OF TPS RECS WRITTEN TO P13F730A   : ' WS-EDIT1.          
           MOVE WS-OTHER-WRITE             TO WS-EDIT1.                         
           DISPLAY 'NO OF OTHER RECS WRITTEN TO P13F730A : ' WS-EDIT1.          
           DISPLAY ' '.                                                         
           DISPLAY '*---------------------------------------------*'.           
                                                                                
           CLOSE BP13F730                                                       
                 BP13K811                                                       
                 P13F730A.                                                      
                                                                                
           IF WS-K811-STATUS NOT = '00' AND '97'                                
              DISPLAY 'ERROR IN CLOSING BP13K811 : ' WS-K811-STATUS             
              MOVE WS-K811-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
