       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C675.                                                    
      *AUTHOR. RHB.                                                             
      *DATE-WRITTEN : 18/03/97                                                  
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME  :    SYSTEM OF COMMITMENT   (SOC)               *        
      *                                                                *        
      *   SYSTEM ID    :    BP13                                       *        
      *                                                                *        
      *   OBJECTIVE    : 1. TO MATCH F500 AND F520 USING NUM-REGN AND  *        
      *                     OUTPUT THE RECORD IF THERE IS A CHANGE IN  *        
      *                     HOUSEHOLD-TYPE.                            *        
      *                                                                *        
      *                  2. QUARTERLY RUN FOR HOUSEHOLD CHECK          *        
      *                                                                *        
      *   INPUT FILES  :    BP13F500                                   *        
      *                     BP13F520                                   *        
      *                                                                *        
      *   OUTPUT FILES :    BP13F510                                   *        
      *                                                                *        
      *   REPORT       :    BP13L675                                   *        
      *                                                                *        
      *   MODIFICATION HISTORY:                                        *        
      *   =====================                                        *        
      *   CHGE-NO  BY    DATE   DESCRIPTION                            *        
      *   -------  ---  ------  -------------------------------------  *        
      * BP130264 RHB 199070318 - NEW PGM                               *        
      * BP130652 NOR 19990219  - EXPAND F500, F510 & F520 RECL(300-500)*        
      * BP132626 SK  20011124  - CHANGE DTE-REGN TO DTE-REQUEST        *        
      * BP135279 SMR 20140402  - INCLUDE BP13K595, CHECK IF THERE IS A *        
      *                          CHANGE IN CDE-HOUSEHOLD OR NUM-FT2T   *        
      *                          REMOVE BP13F510                       *        
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F500 ASSIGN TO BP13F500.                                  
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
           SELECT BP13K595 ASSIGN       TO BP13K595                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K595-KEY-FLD                         
                           FILE STATUS  IS WS-K595-STATUS.                      
           SELECT BP13L675 ASSIGN TO BP13L675.                                  
                                                                                
           EJECT                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F520                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 500 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F520.                                                           
                                                                                
       FD  BP13F500                BLOCK  CONTAINS 0 RECORDS                    
                                   RECORD CONTAINS 500 CHARACTERS               
                                   LABEL RECORDS ARE STANDARD.                  
       COPY BP13F500.                                                           
                                                                                
       FD   BP13K595                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BP13K595.                                                           
                                                                                
      *                                                                         
       FD  BP13L675                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL  RECORDS  ARE OMITTED.                                         
       01  BP13L675-REC                 PIC X(132).                             
       WORKING-STORAGE SECTION.                                                 
       01  WS-F520-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F500-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-MATCH                PIC 9(8)    VALUE ZERO.                      
       01  WS-F500-NOTMATCH        PIC 9(8)    VALUE ZERO.                      
       01  WS-F520-NOTMATCH        PIC 9(8)    VALUE ZERO.                      
       01  WS-PAGE                 PIC 9(5)    VALUE ZERO.                      
       01  WS-CNT                  PIC 9(2)    VALUE 99.                        
       01  WS-SERIAL               PIC 9(6)    VALUE ZERO.                      
       01  WS-CUR-DATE.                                                         
           05  WS-YY               PIC X(4)    VALUE SPACES.                    
           05  WS-MM               PIC X(2)    VALUE SPACES.                    
           05  WS-DD               PIC X(2)    VALUE SPACES.                    
       01  WS-K595-STATUS          PIC 9(02)   VALUE ZEROES.                    
       01  WS-K595-EOF             PIC X(01)   VALUE 'N'.                       
                                                                                
       01  WS-QUEUE-TIME.                                                       
           05  WS-TIME             PIC 9(2)    VALUE ZEROS.                     
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-EDITED-DD        PIC X(2)    VALUE SPACES.                    
           05  FILLER              PIC X       VALUE '/'.                       
           05  WS-EDITED-MM        PIC X(2)    VALUE SPACES.                    
           05  FILLER              PIC X       VALUE '/'.                       
           05  WS-EDITED-YY        PIC X(4)    VALUE SPACES.                    
                                                                                
       77  WS-EOF-F500             PIC X(3).                                    
           88  EOF-OF-F500                     VALUE 'YES'.                     
       77  WS-EOF-F520             PIC X(3).                                    
           88  EOF-OF-F520                     VALUE 'YES'.                     
                                                                                
       01  L675-PR-HEAD-01.                                                     
           05  FILLER              PIC X(14)   VALUE 'BP13L675      '.          
           05  FILLER              PIC X(8)    VALUE 'HDB3    '.                
           05  FILLER              PIC X(18)   VALUE SPACES.                    
           05  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(08)   VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'DATE: '.                  
           05  PRINT-DATE          PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(3)    VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'PAGE: '.                  
           05  PRINT-PAGE          PIC ZZZ9.                                    
                                                                                
       01  L675-PR-HEAD-02.                                                     
           05  FILLER              PIC X(33)   VALUE SPACES.                    
           05  FILLER              PIC X(60)   VALUE                            
           'REPORT TO VERIFY CHANGE OF HOUSEHOLD TYPE AND FT2T   '.             
           05  FILLER              PIC X(18)   VALUE SPACES.                    
                                                                                
       01  L675-PR-HEAD-03.                                                     
           05  FILLER              PIC X(87)   VALUE SPACES.                    
           05  FILLER              PIC X(21)   VALUE                            
           'HOUSEHOLD        FT2T'.                                             
                                                                                
       01  L675-PR-HEAD-04.                                                     
           05  FILLER              PIC X(52)   VALUE                            
           '   S/N   REGN-NO     NRIC1      NRIC2      NT    FT '.              
           05  FILLER              PIC X(52)   VALUE                            
           ' ALC-SCH  DTE-REQ   PPS  AST  5YR  REQ  BATCH  UPD  '.              
           05  FILLER              PIC X(15)   VALUE                            
           'REQ  BATCH  UPD'.                                                   
                                                                                
       01  L675-PR-DETAIL-1.                                                    
           05  L675-NO             PIC ZZZZZ9.                                  
           05  FILLER              PIC X(3).                                    
           05  L675-REGN           PIC X(8).                                    
           05  FILLER              PIC X(4).                                    
           05  L675-NRIC1          PIC X(9).                                    
           05  FILLER              PIC X(2).                                    
           05  L675-NRIC2          PIC X(9).                                    
           05  FILLER              PIC X(2).                                    
           05  L675-NT             PIC X(3).                                    
           05  FILLER              PIC X(3).                                    
           05  L675-FT             PIC X(2).                                    
           05  FILLER              PIC X(4).                                    
           05  L675-ALLOC          PIC X(3).                                    
           05  FILLER              PIC X(4).                                    
           05  L675-DTE-REGN       PIC X(8).                                    
           05  FILLER              PIC X(3).                                    
           05  L675-PPS            PIC X(1).                                    
           05  FILLER              PIC X(4).                                    
           05  L675-AST            PIC X(1).                                    
           05  FILLER              PIC X(4).                                    
           05  L675-5YR            PIC X(1).                                    
           05  FILLER              PIC X(4).                                    
           05  L675-HH-REQ         PIC X(1).                                    
           05  FILLER              PIC X(5).                                    
           05  L675-HH-BATCH       PIC X(1).                                    
           05  FILLER              PIC X(5).                                    
           05  L675-HH-UPD         PIC X(1).                                    
           05  FILLER              PIC X(4).                                    
           05  L675-FT2T-REQ       PIC X(1).                                    
           05  FILLER              PIC X(5).                                    
           05  L675-FT2T-BATCH     PIC X(1).                                    
           05  FILLER              PIC X(5).                                    
           05  L675-FT2T-UPD       PIC X(1).                                    
                                                                                
       PROCEDURE DIVISION.                                                      
       10-MAIN-LOGIC.                                                           
           PERFORM 20-INITIALIZATION  THRU 20-EXIT.                             
           PERFORM 200-READ-F500 THRU 200-EXIT.                                 
           PERFORM 300-READ-F520 THRU 300-EXIT.                                 
           PERFORM 100-PROCESS   THRU 100-EXIT                                  
              UNTIL F500-NUM-REGN = HIGH-VALUE                                  
               AND  F520-NUM-REGN = HIGH-VALUE.                                 
           PERFORM 900-FINALIZATION THRU 900-EXIT.                              
                                                                                
       10-EXIT.                                                                 
           EXIT.                                                                
                                                                                
       20-INITIALIZATION.                                                       
           OPEN INPUT  BP13F500                                                 
                       BP13F520                                                 
                       BP13K595                                                 
                OUTPUT BP13L675.                                                
                                                                                
           IF WS-K595-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'OPENING BP13K595 ERROR ' WS-K595-STATUS                  
              MOVE WS-K595-STATUS          TO RETURN-CODE                       
              PERFORM 900-FINALIZATION     THRU 900-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE 'NO' TO WS-EOF-F500.                                            
           MOVE 'NO' TO WS-EOF-F520.                                            
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-CUR-DATE.                    
           MOVE WS-YY TO WS-EDITED-YY.                                          
           MOVE WS-MM TO WS-EDITED-MM.                                          
           MOVE WS-DD TO WS-EDITED-DD.                                          
           MOVE SPACES                   TO L675-PR-DETAIL-1.                   
           INITIALIZE                       L675-PR-DETAIL-1.                   
                                                                                
       20-EXIT.                                                                 
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *  MATCH REF                                                    *         
      *****************************************************************         
       100-PROCESS.                                                             
           IF F500-NUM-REGN   =   F520-NUM-REGN                                 
              IF  ((F500-NUM-FT2T = F520-NUM-FT2T) AND                          
                 (((F500-CDE-HOUSEHOLD = 'H' OR 'T') AND                        
                   (F520-CDE-HOUSEHOLD = 'H' OR 'T')) OR                        
                   (F500-CDE-HOUSEHOLD = F520-CDE-HOUSEHOLD)))                  
                 CONTINUE                                                       
              ELSE                                                              
                 PERFORM 500-STARTBR-K595 THRU 500-EXIT                         
                 PERFORM 400-OUTPUT-F500 THRU 400-EXIT                          
              END-IF                                                            
              ADD 1                 TO   WS-MATCH                               
              PERFORM 200-READ-F500 THRU 200-EXIT                               
              PERFORM 300-READ-F520 THRU 300-EXIT                               
           ELSE                                                                 
           IF (F500-NUM-REGN   < F520-NUM-REGN)                                 
              ADD 1 TO WS-F500-NOTMATCH                                         
              PERFORM 200-READ-F500 THRU 200-EXIT                               
           ELSE                                                                 
              ADD 1 TO WS-F520-NOTMATCH                                         
              PERFORM 300-READ-F520 THRU 300-EXIT.                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-F500.                                                           
           READ BP13F500 AT END MOVE 'YES' TO WS-EOF-F500                       
                                MOVE HIGH-VALUE TO F500-NUM-REGN                
               GO TO 200-EXIT.                                                  
            ADD 1 TO WS-F500-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-READ-F520.                                                           
           READ BP13F520 AT END MOVE 'YES' TO WS-EOF-F520                       
                                MOVE HIGH-VALUE TO F520-NUM-REGN                
               GO TO 300-EXIT.                                                  
            ADD 1 TO WS-F520-READ.                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
       400-OUTPUT-F500.                                                         
           IF WS-CNT > 40                                                       
              MOVE 0 TO WS-CNT                                                  
              ADD 1                 TO   WS-PAGE                                
              MOVE WS-PAGE          TO   PRINT-PAGE                             
              MOVE WS-EDITED-DATE   TO   PRINT-DATE                             
              WRITE BP13L675-REC    FROM L675-PR-HEAD-01 AFTER PAGE             
              WRITE BP13L675-REC    FROM L675-PR-HEAD-02                        
              WRITE BP13L675-REC    FROM L675-PR-HEAD-03 AFTER 2                
              WRITE BP13L675-REC    FROM L675-PR-HEAD-04                        
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-CNT.                                                     
           ADD 1 TO WS-SERIAL.                                                  
           MOVE WS-SERIAL                TO L675-NO.                            
           MOVE F500-NUM-REGN            TO L675-REGN.                          
           MOVE F500-NUM-NRIC1           TO L675-NRIC1.                         
           IF F500-NUM-NRIC2(1:2) NOT = '##'                                    
              MOVE F500-NUM-NRIC2        TO L675-NRIC2                          
           END-IF.                                                              
           MOVE F500-CDE-NT1             TO L675-NT.                            
           MOVE F500-CDE-FLAT-TYPE       TO L675-FT.                            
           MOVE F500-CDE-ALLOC-SCH       TO L675-ALLOC.                         
           MOVE F500-DTE-REQUEST         TO L675-DTE-REGN.                      
           MOVE F500-NUM-PPS-ELIG-TAG    TO L675-PPS.                           
           MOVE F500-NUM-AST             TO L675-AST.                           
           MOVE F500-NUM-5YR-OCCUPN      TO L675-5YR.                           
           MOVE F500-CDE-HOUSEHOLD       TO L675-HH-REQ.                        
           MOVE F520-CDE-HOUSEHOLD       TO L675-HH-BATCH.                      
           MOVE F500-NUM-FT2T            TO L675-FT2T-REQ.                      
           MOVE F520-NUM-FT2T            TO L675-FT2T-BATCH.                    
           WRITE BP13L675-REC FROM          L675-PR-DETAIL-1.                   
           MOVE SPACES                   TO L675-PR-DETAIL-1.                   
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-STARTBR-K595.                                                        
           MOVE SPACES                     TO BP13K595-REC.                     
           INITIALIZE                         BP13K595-REC.                     
           MOVE 'N'                        TO WS-K595-EOF.                      
           MOVE F500-NUM-REGN              TO K595-NUM-REGN-REF.                
                                                                                
           START BP13K595 KEY >= K595-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K595-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              PERFORM 510-READNEXT-K595    THRU 510-EXIT                        
                UNTIL WS-K595-EOF = 'Y'                                         
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              CONTINUE                                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'START ERROR BP13K595 : ' WS-K595-STATUS                  
                      ' REGN : ' K595-NUM-REGN                                  
              MOVE WS-K595-STATUS          TO RETURN-CODE                       
              PERFORM 900-FINALIZATION     THRU 900-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       510-READNEXT-K595.                                                       
           READ BP13K595 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-K595-STATUS                                              
           WHEN ZEROES                                                          
              IF K595-NUM-REGN-REF = F500-NUM-REGN                              
                 IF  ((F500-NUM-FT2T = K595-NUM-FT2T) AND                       
                    (((F500-CDE-HOUSEHOLD = 'H' OR 'T') AND                     
                      (K595-CDE-HOUSEHOLD = 'H' OR 'T')) OR                     
                      (F500-CDE-HOUSEHOLD = K595-CDE-HOUSEHOLD)))               
                    CONTINUE                                                    
                 ELSE                                                           
                    IF (((F500-CDE-HOUSEHOLD = 'H' OR 'T') AND                  
                         (K595-CDE-HOUSEHOLD = 'H' OR 'T')) OR                  
                         (F500-CDE-HOUSEHOLD = K595-CDE-HOUSEHOLD))             
                       MOVE 'N'             TO L675-HH-UPD                      
                    ELSE                                                        
                       MOVE 'Y'             TO L675-HH-UPD                      
                    END-IF                                                      
                    IF K595-NUM-FT2T NOT = F500-NUM-FT2T                        
                       MOVE 'Y'             TO L675-FT2T-UPD                    
                    ELSE                                                        
                       MOVE 'N'             TO L675-FT2T-UPD                    
                    END-IF                                                      
                    MOVE 'Y'                TO WS-K595-EOF                      
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'N'                   TO L675-HH-UPD                      
                                               L675-FT2T-UPD                    
                 MOVE 'Y'                   TO WS-K595-EOF                      
              END-IF                                                            
                                                                                
           WHEN 23                                                              
           WHEN 10                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13K595 : ' K595-KEY-FLD            
              MOVE 'Y'                      TO WS-K595-EOF                      
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING ON BP13K595'                               
              MOVE WS-K595-STATUS           TO RETURN-CODE                      
              PERFORM 900-FINALIZATION      THRU 900-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-FINALIZATION.                                                        
             DISPLAY '*----------- BP13C675  ------------*'.                    
             DISPLAY ' F500-READ      ' WS-F500-READ.                           
             DISPLAY ' F520-READ      ' WS-F520-READ.                           
             DISPLAY ' MATCH          ' WS-MATCH.                               
             DISPLAY ' F500-NOT-MT    ' WS-F500-NOTMATCH.                       
             DISPLAY ' F520-NOT-MT    ' WS-F520-NOTMATCH.                       
             DISPLAY ' L675 WRITTEN   ' WS-SERIAL.                              
           CLOSE      BP13F500                                                  
                      BP13F520                                                  
                      BP13K595                                                  
                      BP13L675.                                                 
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
