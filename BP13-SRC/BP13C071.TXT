       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C071.                                                    
      *AUTHOR. MCC.                                                             
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *                                                             *           
      *  OBJECTIVE  :                                               *           
      *      READ IBGCPF FILE AND MATCH WITH RESALE/SMS FILE        *           
      *                                                             *           
      *  INPUT FILES:                                               *           
      *      1. BP13F121 (TRANS FILE)                               *           
      *      2. BB24F04A (CPFIBG FILE)                              *           
      *                                                             *           
      *  OUTPUT FILE:                                               *           
      *      3. P13F04A (MATCHED CASES)                             *           
      *      4. P13F04B (UNMATCHED CASES)                           *           
      * ----------------------------------------------------------- *           
      * CHG-NO   DATE     BY    DESCRIPTION                         *           
      * -------- -------- ----- -----------                         *           
      * BP132400 20030820 MCC   NEW PROGRAM                         *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F121   ASSIGN      TO BP13F121.                           
           SELECT BB24F04A   ASSIGN      TO BB24F04A.                           
           SELECT SY02F001   ASSIGN      TO SY02F001.                           
           SELECT P13F04A    ASSIGN      TO P13F04A.                            
           SELECT P13F04B    ASSIGN      TO P13F04B.                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F121                                                            
            RECORD CONTAINS 150 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F121.                                                           
                                                                                
       FD   BB24F04A                                                            
            RECORD CONTAINS 150 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BB24F04A.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   P13F04A                                                             
            RECORD CONTAINS 150  CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   P13F04A-REC.                                                        
            05 FILLER              PIC X(122).                                  
            05 F04A-CDE-MATCHED    PIC X(1).                                    
            05 F04A-PAYMENT-TYPE   PIC X(3).                                    
            05 FILLER              PIC X(24).                                   
                                                                                
       FD   P13F04B                                                             
            RECORD CONTAINS 150  CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   P13F04B-REC.                                                        
            05 FILLER              PIC X(122).                                  
            05 F04B-CDE-MATCHED    PIC X(1).                                    
            05 F04B-PAYMENT-TYPE   PIC X(3).                                    
            05 FILLER              PIC X(24).                                   
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-STATUS.                                                           
           05 WS-SY02-EOF                PIC X(01) VALUE SPACE.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F121-READ               PIC 9(08) VALUE ZEROES.                
           05 WS-F04A-READ               PIC 9(08) VALUE ZEROES.                
           05 WS-REC-MTCH                PIC 9(08) VALUE ZEROES.                
           05 WS-REC-UNMTCH              PIC 9(08) VALUE ZEROES.                
           05 WS-MTCH-DTE-DIFF           PIC 9(08) VALUE ZEROES.                
           05 WS-UNMTCH-DTE-DIFF         PIC 9(08) VALUE ZEROES.                
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY               PIC 9(4).                              
           05  WS-SYS-MM                 PIC 9(2).                              
           05  WS-SYS-DD                 PIC 9(2).                              
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN.                                                                
      *-------------------------------------------------------------            
           PERFORM 100-OPEN-FILES    THRU 100-EXIT.                             
           PERFORM 200-READ-F121     THRU 200-EXIT.                             
           PERFORM 210-READ-F04A     THRU 210-EXIT.                             
           PERFORM 300-PROCESS-REC   THRU 300-EXIT                              
                   UNTIL F04A-NUM-CPF  = HIGH-VALUES.                           
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------*                                                          
       100-OPEN-FILES.                                                          
      *--------------*                                                          
           OPEN INPUT  BP13F121                                                 
                       BB24F04A                                                 
                       SY02F001                                                 
               OUTPUT  P13F04A                                                  
                       P13F04B.                                                 
                                                                                
           PERFORM 101-READ-F001        THRU 101-EXIT.                          
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       101-READ-F001.                                                           
      *-------------------------------------------------------------            
           READ SY02F001 AT END MOVE 'Y' TO WS-SY02-EOF                         
                GO TO 101-EXIT                                                  
           END-READ.                                                            
                                                                                
           MOVE F001-DTE-CURRENT  TO WS-SYSTEM-DATE.                            
                                                                                
       101-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       200-READ-F121.                                                           
      *-------------------------------------------------------------            
           READ BP13F121 AT END                                                 
                MOVE HIGH-VALUES TO F121-NUM-NRIC                               
                GO TO 200-EXIT.                                                 
                                                                                
           ADD 1 TO WS-F121-READ.                                               
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       210-READ-F04A.                                                           
      *-------------------------------------------------------------            
           READ BB24F04A AT END                                                 
                MOVE HIGH-VALUES TO F04A-NUM-CPF                                
                GO TO 210-EXIT.                                                 
                                                                                
           ADD 1 TO WS-F04A-READ.                                               
                                                                                
       210-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       300-PROCESS-REC.                                                         
      *-------------------------------------------------------------            
           IF F121-NUM-NRIC = F04A-NUM-CPF                                      
              ADD 1 TO WS-REC-MTCH                                              
              IF WS-SYS-MM = F04A-DTE-VALUE(5:2)                                
                 MOVE SPACES            TO P13F04A-REC                          
                 MOVE BB24F04A-DETAILS      TO P13F04A-REC                      
                 MOVE F121-CDE-PAYMENT-TYPE TO F04A-PAYMENT-TYPE                
                 MOVE 'Y'                   TO F04A-CDE-MATCHED                 
                 WRITE P13F04A-REC                                              
              ELSE                                                              
                 ADD 1 TO WS-MTCH-DTE-DIFF                                      
              END-IF                                                            
                                                                                
              PERFORM 210-READ-F04A THRU 210-EXIT                               
              PERFORM 200-READ-F121 THRU 200-EXIT                               
           ELSE                                                                 
              IF F121-NUM-NRIC < F04A-NUM-CPF                                   
                 PERFORM 200-READ-F121 THRU 200-EXIT                            
              ELSE                                                              
                 ADD 1 TO WS-REC-UNMTCH                                         
                 IF WS-SYS-MM = F04A-DTE-VALUE(5:2)                             
                    MOVE SPACES            TO P13F04B-REC                       
                    MOVE BB24F04A-DETAILS  TO P13F04B-REC                       
                    MOVE SPACES            TO F04A-PAYMENT-TYPE                 
                    MOVE 'N'               TO F04B-CDE-MATCHED                  
                    WRITE P13F04B-REC                                           
                 ELSE                                                           
                    ADD 1 TO WS-UNMTCH-DTE-DIFF                                 
                 END-IF                                                         
                 PERFORM 210-READ-F04A THRU 210-EXIT                            
              END-IF                                                            
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
           DISPLAY '--- BP13C071 CONTROL LIST ------------------'.              
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECS READ FROM F121          ' WS-F121-READ.          
           DISPLAY 'NO OF RECS READ FROM F04A          ' WS-F04A-READ.          
           DISPLAY 'NO OF RECS MATCHED                 ' WS-REC-MTCH.           
           DISPLAY 'NO OF RECS MATCHED (DTE REGN DIFF) '                        
                                                 WS-MTCH-DTE-DIFF.              
           DISPLAY 'NO OF RECS UNMATCHED               ' WS-REC-UNMTCH.         
           DISPLAY 'NO OF RECS UNMTCHD (DTE REGN DIFF) '                        
                                                 WS-UNMTCH-DTE-DIFF.            
           DISPLAY '--------------------------------------------'.              
                                                                                
           CLOSE BB24F04A                                                       
                 BP13F121                                                       
                 SY02F001                                                       
                 P13F04A                                                        
                 P13F04B.                                                       
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
