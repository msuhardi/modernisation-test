       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C51A.                                                 
       AUTHOR.        SHARON DUMDUM.                                            
       DATE-WRITTEN.  03/04/2007.                                               
      *****************************************************************         
      * SYSTEM     - BP13                                                       
      *                                                                         
      * OBJECTIVE  - TO ASSIGN QUEUE NO TO BP13K740.                            
      *              SORT IN BP13F740 IN 10TH BYTE RANDOM NO SEQUENCE           
      *              QUEUE NO ALWAYS START FROM 00001                           
      *-----------------------------------------------------------------        
      * REF NO   DATE        BY    AMENDMENTS/ENHANCEMENTS                      
      * -------- ----------  ----  -----------------------                      
      * BP133010 03/04/2007  SD10  NEW PROGRAM                                  
      ******************************************************************        
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F740 ASSIGN  TO BP13F740.                                 
                                                                                
           SELECT BP13K740 ASSIGN        TO BP13K740                            
                           ACCESS MODE   IS RANDOM                              
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K740-KEY-FLD                        
                           FILE STATUS   IS WS-K740-STATUS.                     
                                                                                
           SELECT BP13L51A ASSIGN  TO BP13L51A.                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F740                      BLOCK CONTAINS 0 RECORDS               
                                         RECORD CONTAINS 500 CHARACTERS         
                                         LABEL RECORDS ARE STANDARD             
                                         RECORDING MODE IS F.                   
       COPY BP13F740.                                                           
                                                                                
       FD  BP13K740                                                             
                                         RECORD CONTAINS 500 CHARACTERS.        
       COPY BP13K740.                                                           
                                                                                
       FD  BP13L51A                      LABEL RECORDS ARE OMITTED              
                                         RECORDING MODE IS F.                   
       01  LC51A-PRINT-REPORT            PIC X(132).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K740-STATUS             PIC 99        VALUE ZEROES.           
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05 WS-CCYY                     PIC 9999.                             
           05 WS-MM                       PIC 99.                               
           05 WS-DD                       PIC 99.                               
                                                                                
       01  WS-CNT-VAR.                                                          
           05  WS-CNT-READ-F740           PIC 9(5) COMP VALUE ZEROES.           
           05  WS-CNT-UPD-K740            PIC 9(5) COMP VALUE ZEROES.           
           05  WS-CNT-PRINT               PIC 9(5) COMP VALUE ZEROES.           
           05  WS-CNT-LINE                PIC 9(2)      VALUE 56.               
           05  WS-QUEUE-NO                PIC 9(5) COMP VALUE ZEROES.           
           05  WS-FINAL-QUEUE             PIC 9(5) COMP VALUE ZEROES.           
           05  WS-PREV-FLAT-TYPE          PIC X(2) VALUE SPACES.                
                                                                                
       01  WS-PAGE-SNO.                                                         
           05  WS-PAGE                    PIC 9(5) COMP  VALUE ZEROES.          
           05  WS-SNO                     PIC 9(4) COMP  VALUE ZEROES.          
                                                                                
       01  WS-FND-FLG                     PIC X(1)       VALUE 'N'.             
           88  K740-FND                                  VALUE 'Y'.             
           88  K740-NOT-FND                              VALUE 'N'.             
                                                                                
      ***************************************************************           
      *    REPORT LAYOUT                                            *           
      ***************************************************************           
                                                                                
       01  WS-PRINT-MAIN-HD.                                                    
           05  FILLER              PIC X(08)  VALUE 'BP13L51A'.                 
           05  FILLER              PIC X(04)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'HDBCAT3'.                  
           05  FILLER              PIC X(25)  VALUE SPACES.                     
           05  FILLER              PIC X(20)  VALUE                             
                                      'S Y S T E M   O F   '.                   
           05  FILLER              PIC X(19)  VALUE                             
                                      'C O M M I T M E N T'.                    
           05  FILLER              PIC X(15)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'DATE : '.                  
           05  RPT-HEAD-DATE       PIC X(10)  VALUE SPACES.                     
           05  FILLER              PIC X(04)  VALUE SPACES.                     
           05  FILLER              PIC X(07)  VALUE 'PAGE : '.                  
           05  RPT-HEAD-PAGE       PIC Z(03)9 VALUE SPACES.                     
           05  FILLER              PIC X(12)  VALUE SPACES.                     
                                                                                
       01  WS-PRINT-REPORT-HD.                                                  
           05  FILLER              PIC X(34)  VALUE SPACES.                     
           05  FILLER              PIC X(42)  VALUE                             
               'ASSIGNMENT OF QUEUE NO FOR 3R / WIS '.                          
           05  RPT-PRINT-DATE      PIC X(08).                                   
           05  FILLER              PIC X(48)  VALUE SPACES.                     
                                                                                
       01  WS-PRINT-REPORT-CL1.                                                 
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(05)  VALUE ' S/NO'.                    
           05  FILLER              PIC X(03)  VALUE SPACES.                     
           05  FILLER              PIC X(10)  VALUE 'NUM-RANDOM'.               
           05  FILLER              PIC X(21)  VALUE SPACES.                     
           05  FILLER              PIC X(08)  VALUE 'REG-NO  '.                 
           05  FILLER              PIC X(03)  VALUE SPACES.                     
           05  FILLER              PIC X(09)  VALUE 'NRIC     '.                
           05  FILLER              PIC X(03)  VALUE SPACES.                     
           05  FILLER              PIC X(06)  VALUE 'CDE-NT'.                   
           05  FILLER              PIC X(03)  VALUE SPACES.                     
           05  FILLER              PIC X(09)  VALUE 'FLAT-TYPE'.                
           05  FILLER              PIC X(03)  VALUE SPACES.                     
           05  FILLER              PIC X(10)  VALUE 'REQ-STATUS'.               
           05  FILLER              PIC X(03)  VALUE SPACES.                     
           05  FILLER              PIC X(11)  VALUE 'FT-QUEUE'.                 
           05  FILLER              PIC X(03)  VALUE SPACES.                     
           05  FILLER              PIC X(09)  VALUE 'ALLOC-SCH'.                
           05  FILLER              PIC X(03)  VALUE SPACES.                     
           05  FILLER              PIC X(08)  VALUE 'ELIG-SCH'.                 
           05  FILLER              PIC X(01)  VALUE SPACES.                     
                                                                                
       01  WS-PRINT-REPORT-CL2.                                                 
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  RPT-SNO             PIC ZZZZ9    VALUE ZEROES.                   
           05  FILLER              PIC X(03)    VALUE SPACES.                   
           05  RPT-RANDOM          PIC X(28)    VALUE SPACES.                   
           05  FILLER              PIC X(03)    VALUE SPACES.                   
           05  RPT-REGNO           PIC X(08)    VALUE SPACES.                   
           05  FILLER              PIC X(03)    VALUE SPACES.                   
           05  RPT-NRIC            PIC X(09)    VALUE SPACES.                   
           05  FILLER              PIC X(04)    VALUE SPACES.                   
           05  RPT-NT              PIC X(03)    VALUE ZEROES.                   
           05  FILLER              PIC X(09)    VALUE SPACES.                   
           05  RPT-FT-TYP          PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(11)    VALUE SPACES.                   
           05  RPT-REQ-STATUS      PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(10)    VALUE SPACES.                   
           05  RPT-FT-QUEUE        PIC X(05)    VALUE ZEROES.                   
           05  FILLER              PIC X(09)    VALUE SPACES.                   
           05  RPT-ALL-SCH         PIC X(03)    VALUE SPACES.                   
           05  FILLER              PIC X(09)    VALUE SPACES.                   
           05  RPT-ELGI-SCH        PIC X(03)    VALUE SPACES.                   
           05  FILLER              PIC X(03)    VALUE SPACES.                   
                                                                                
       01  WS-PRINT-ROLL.                                                       
           05  FILLER              PIC X(01)  VALUE SPACES.                     
           05  FILLER              PIC X(130) VALUE ALL '-'.                    
           05  FILLER              PIC X(01)  VALUE SPACES.                     
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ************************************                                      
       0000-MAIN-LOGIC.                                                         
      ************************************                                      
                                                                                
           PERFORM 1000-START-UP          THRU 1000-EXIT.                       
           PERFORM 2000-READ-BP13F740     THRU 2000-EXIT.                       
           PERFORM 3000-UPDATE-PROCESS    THRU 3000-EXIT                        
             UNTIL F740-KEY-FLD = HIGH-VALUES.                                  
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       1000-START-UP.                                                           
      ************************************                                      
                                                                                
           OPEN INPUT  BP13F740                                                 
                I-O    BP13K740                                                 
                OUTPUT BP13L51A.                                                
                                                                                
           IF WS-K740-STATUS NOT = 0  AND                                       
              WS-K740-STATUS NOT = 97                                           
              DISPLAY ' OPENING ERROR, BP13K740 STATUS ' WS-K740-STATUS         
              MOVE WS-K740-STATUS           TO RETURN-CODE                      
              PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                        
           END-IF.                                                              
           ACCEPT WS-CURRENT-DATE FROM DATE YYYYMMDD.                           
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
                                                                                
      ************************************                                      
       2000-READ-BP13F740.                                                      
      ************************************                                      
                                                                                
           READ  BP13F740 AT END                                                
                 MOVE HIGH-VALUES TO F740-KEY-FLD                               
                 GO TO 2000-EXIT.                                               
                                                                                
           ADD 1 TO WS-CNT-READ-F740.                                           
                                                                                
       2000-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       3000-UPDATE-PROCESS.                                                     
      ************************************                                      
           PERFORM 3100-READ-BP13K740        THRU  3100-EXIT.                   
           IF K740-FND AND (F740-NUM-RANDOM NOT = SPACES AND LOW-VALUES)        
              IF F740-CDE-FLAT-TYPE NOT = WS-PREV-FLAT-TYPE                     
                 MOVE F740-CDE-FLAT-TYPE  TO WS-PREV-FLAT-TYPE                  
                 MOVE ZEROES              TO WS-QUEUE-NO                        
              END-IF                                                            
              ADD +1                  TO WS-QUEUE-NO                            
              COMPUTE WS-FINAL-QUEUE = WS-QUEUE-NO - 1                          
              MOVE SPACES             TO K740-NUM-HHTY-QUEUE-ALPHA              
              IF WS-FINAL-QUEUE = ZEROES                                        
                 MOVE SPACES             TO K740-NUM-HHTY-QUEUE-SERIAL          
              ELSE                                                              
                 MOVE WS-FINAL-QUEUE     TO K740-NUM-HHTY-QUEUE-SERIAL          
              END-IF                                                            
              PERFORM 3200-UPDATE-BP13K740   THRU  3200-EXIT                    
              PERFORM 4000-PRINT-REPORT      THRU  4000-EXIT                    
           END-IF.                                                              
           PERFORM 2000-READ-BP13F740        THRU  2000-EXIT.                   
                                                                                
       3000-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       3100-READ-BP13K740.                                                      
      ************************************                                      
           MOVE SPACES          TO BP13K740-REC.                                
           MOVE F740-NUM-RANDOM TO K740-NUM-RANDOM.                             
                                                                                
           READ BP13K740                                                        
                                                                                
           EVALUATE WS-K740-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 MOVE 'Y'    TO WS-FND-FLG                                      
              WHEN 23                                                           
                 MOVE 'N'    TO WS-FND-FLG                                      
                 DISPLAY 'RANDOM NUM NOT FOUND AT BP13K740 - '                  
                          K740-NUM-RANDOM                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR WITH READ FILE-BP13K740. STATUS IS '            
                          WS-K740-STATUS                                        
                  MOVE WS-K740-STATUS           TO RETURN-CODE                  
                 PERFORM 9999-CLOSE-ROUTINE    THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************                                      
       3200-UPDATE-BP13K740.                                                    
      ************************************                                      
           REWRITE BP13K740-REC                                                 
                                                                                
           EVALUATE WS-K740-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR WITH REWRITE FILE-BP13K740. STATUS IS '         
                          WS-K740-STATUS                                        
                 MOVE WS-K740-STATUS           TO RETURN-CODE                   
                 PERFORM 9999-CLOSE-ROUTINE    THRU 9999-EXIT                   
           END-EVALUATE.                                                        
           ADD +1              TO WS-CNT-UPD-K740.                              
                                                                                
       3200-EXIT. EXIT.                                                         
                                                                                
                                                                                
      ************************************************************              
      *    PRINTING REPORT.                                      *              
      ************************************************************              
                                                                                
      ************************************                                      
       4000-PRINT-REPORT.                                                       
      ************************************                                      
           IF WS-CNT-LINE > 55                                                  
              PERFORM 4100-PRINT-HEAD           THRU  4100-EXIT                 
           END-IF.                                                              
           ADD +1                       TO  WS-SNO.                             
                                                                                
           MOVE WS-SNO                  TO   RPT-SNO.                           
           MOVE K740-NUM-RANDOM         TO   RPT-RANDOM.                        
           MOVE K740-NUM-REGN           TO   RPT-REGNO.                         
           MOVE K740-NUM-NRIC1          TO   RPT-NRIC.                          
           MOVE K740-CDE-NT1            TO   RPT-NT.                            
           MOVE K740-CDE-FLAT-TYPE      TO   RPT-FT-TYP.                        
           MOVE K740-CDE-REQUEST-STATUS TO   RPT-REQ-STATUS.                    
           MOVE K740-NUM-HHTY-QUEUE-SERIAL                                      
                                        TO   RPT-FT-QUEUE.                      
           MOVE K740-CDE-ALLOC-SCH      TO   RPT-ALL-SCH.                       
           MOVE K740-CDE-ELIG-SCH       TO   RPT-ELGI-SCH.                      
                                                                                
           MOVE WS-PRINT-REPORT-CL2     TO LC51A-PRINT-REPORT.                  
           WRITE LC51A-PRINT-REPORT.                                            
           ADD +1                       TO   WS-CNT-PRINT.                      
           ADD +1                       TO   WS-CNT-LINE.                       
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************                                      
       4100-PRINT-HEAD.                                                         
      ************************************                                      
           MOVE SPACES                TO LC51A-PRINT-REPORT                     
           WRITE LC51A-PRINT-REPORT AFTER PAGE.                                 
           WRITE LC51A-PRINT-REPORT.                                            
                                                                                
           ADD +1                           TO   WS-PAGE.                       
           STRING WS-DD    '/'                                                  
                  WS-MM    '/'                                                  
                  WS-CCYY                                                       
              DELIMITED BY SIZE             INTO RPT-HEAD-DATE.                 
                                                                                
           MOVE WS-PAGE                     TO   RPT-HEAD-PAGE.                 
           MOVE WS-PRINT-MAIN-HD            TO   LC51A-PRINT-REPORT.            
           WRITE LC51A-PRINT-REPORT.                                            
                                                                                
           MOVE SPACES                TO LC51A-PRINT-REPORT.                    
           WRITE LC51A-PRINT-REPORT.                                            
                                                                                
      *    MOVE WS-PRINT-REPORT-HD    TO LC51A-PRINT-REPORT.                    
      *    WRITE LC51A-PRINT-REPORT.                                            
                                                                                
           MOVE SPACES                TO LC51A-PRINT-REPORT.                    
           WRITE LC51A-PRINT-REPORT.                                            
                                                                                
           MOVE WS-PRINT-REPORT-CL1   TO LC51A-PRINT-REPORT.                    
           WRITE LC51A-PRINT-REPORT.                                            
                                                                                
           MOVE WS-PRINT-ROLL         TO LC51A-PRINT-REPORT.                    
           WRITE LC51A-PRINT-REPORT.                                            
           MOVE   07                  TO  WS-CNT-LINE.                          
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************                                      
       9999-CLOSE-ROUTINE.                                                      
      ************************************                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*********** CONTROL TOTALS ***********'.                    
           DISPLAY 'PROGRAM-ID: BP13C51A'.                                      
           DISPLAY SPACES.                                                      
           DISPLAY 'TOTAL BP13F740 RECORDS READ   : '                           
                                               WS-CNT-READ-F740.                
           DISPLAY 'TOTAL BP13K740 RECORDS UPDATE : '                           
                                               WS-CNT-UPD-K740.                 
           DISPLAY 'PRINT REPORT                  : '                           
                                               WS-CNT-PRINT.                    
                                                                                
           CLOSE BP13F740                                                       
                 BP13K740                                                       
                 BP13L51A.                                                      
                                                                                
           IF WS-K740-STATUS = 97 OR 00                                         
              CONTINUE                                                          
           ELSE                                                                 
              MOVE WS-K740-STATUS TO RETURN-CODE                                
              DISPLAY 'CLOSE BP13K740 FAIL. STATUS : ' WS-K740-STATUS           
           END-IF.                                                              
                                                                                
       9999-EXIT.                                                               
           STOP RUN.                                                            
                                                                                
      **************************************************************            
      * -----------------------END-PROGRAME.-----------------------*            
      **************************************************************            
                                                                                
