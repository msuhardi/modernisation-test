      *----------------------------------------------------------------*        
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------------*        
       PROGRAM-ID.    BP13CC16.                                                 
       AUTHOR.        ABH.                                                      
       DATE-WRITTEN.  17/05/2006.                                               
                                                                                
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                   *        
      *                                                                *        
      *   SYSTEM ID   :  BP13                                          *        
      *                                                                *        
      *   OBJECTIVE   :  TO EFFECT THE NEW INSTALMENT, CASH, CPF AFTER *        
      *                  ADDING NEW LOAN.                              *        
      *                                                                *        
      *   INPUT FILES :  1.  BP13FD38                                  *        
      *                  2.  SY02F001                                  *        
      *   I-O FILES   :  1.  BP13KD00                                  *        
      *                  2.  BP13KD60                                  *        
      *   OUTPUT FILES:  1.  BP13KD38                                  *        
      *                  2.  BP13KD99 (LOG FILE FOR BP13KD00)          *        
      *                  3.  P13FD38A (RECORDS NOT PROCESSED OR        *        
      *                      FD38-DTE-INSTAL-NEW NOT = CURRENT DATE)   *        
      *                  4.  P13FD38B (RECORDS THAT ARE PROCESSED OR   *        
      *                      DELETED AFTER PROCESSING)                 *        
      *                                                                *        
      *----------------------------------------------------------------*        
      *                                                                *        
      *   CHGE-NO   BY    DATE      DESCRIPTION                        *        
      *   --------  ---  ---------- -----------                        *        
      *   BP132772  BA3  17/05/2006 NEW PROGRAM                        *        
      *   BP132888  BA3  05/12/2006 UPDATE CDE-CPF-STATUS              *        
      *   BP133091  RB12 17/10/2007 EXPAND KD00 LENGTH FR 1200 TO 1600 *        
      *   BP133091  LMS1 15/12/2007 EXPAND KD99 LENGTH FR 1250 TO 1650 *        
      *   BP133137  ED1  04/04/2007 TERMINATE PGM WHEN ERROR IN OPEN   *        
      *                             ONLY & REMOVED BP13KD63            *        
      *   BP134154  JB8  22/03/2011 TO UPDATE KD60-AMT-INST            *        
      *   BP134493  IMC1 05/05/2012 INITIALIZE KD60 READ SW BEFORE READ*        
      ******************************************************************        
                                                                                
      *----------------------------------------------------------------*        
       ENVIRONMENT DIVISION.                                                    
      *----------------------------------------------------------------*        
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FD38  ASSIGN        TO BP13FD38.                          
           SELECT SY02F001  ASSIGN        TO SY02F001.                          
                                                                                
           SELECT BP13KD38  ASSIGN        TO BP13KD38                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KD38-KEY-FLD                       
                            FILE STATUS   IS WS-KD38-STATUS.                    
                                                                                
           SELECT BP13KD00  ASSIGN        TO BP13KD00                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KD00-KEY-FLD                       
                            FILE STATUS   IS WS-KD00-STATUS.                    
                                                                                
           SELECT BP13KD60  ASSIGN        TO BP13KD60                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KD60-KEY-FLD                       
                            FILE STATUS   IS WS-KD60-STATUS.                    
                                                                                
           SELECT BP13KD99  ASSIGN        TO BP13KD99                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KD99-KEY-FLD                       
                            FILE STATUS   IS WS-KD99-STATUS.                    
                                                                                
           SELECT P13FD38A ASSIGN        TO P13FD38A.                           
           SELECT P13FD38B ASSIGN        TO P13FD38B.                           
                                                                                
      *----------------------------------------------------------------*        
       DATA DIVISION.                                                           
      *----------------------------------------------------------------*        
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13FD38                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 400 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       COPY BP13FD38.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13KD38                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
                                                                                
       COPY BP13KD38.                                                           
                                                                                
       FD  BP13KD00                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
                                                                                
       COPY BP13KD00.                                                           
                                                                                
       FD  BP13KD60                                                             
           RECORD CONTAINS 250  CHARACTERS.                                     
                                                                                
       COPY BP13KD60.                                                           
                                                                                
       FD  BP13KD99                                                             
           RECORD CONTAINS 1650 CHARACTERS.                                     
                                                                                
       COPY BP13KD99.                                                           
                                                                                
       FD  P13FD38A                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 400 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  P13FD38A-REC                 PIC X(400).                             
                                                                                
       FD  P13FD38B                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 400 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
                                                                                
       01  P13FD38B-REC                 PIC X(400).                             
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------------*        
                                                                                
       01  WS-KD38-STATUS               PIC 9(2)    VALUE 0.                    
       01  WS-KD00-STATUS               PIC 9(2)    VALUE 0.                    
       01  WS-KD60-STATUS               PIC 9(2)    VALUE 0.                    
       01  WS-KD99-STATUS               PIC 9(2)    VALUE 0.                    
       01  CNT-FD38-READ                PIC 9(8)    VALUE 0.                    
       01  CNT-KD38-DELETE              PIC 9(8)    VALUE 0.                    
       01  CNT-KD00-READ                PIC 9(8)    VALUE 0.                    
       01  CNT-KD00-UPDATE              PIC 9(8)    VALUE 0.                    
       01  CNT-KD00-NOTFOUND            PIC 9(8)    VALUE 0.                    
       01  CNT-KD60-READ                PIC 9(8)    VALUE 0.                    
       01  CNT-KD60-UPDATE              PIC 9(8)    VALUE 0.                    
       01  CNT-KD60-NOTFOUND            PIC 9(8)    VALUE 0.                    
       01  CNT-KD99-WRITE               PIC 9(8)    VALUE 0.                    
       01  WS-FD38A-WRITE               PIC 9(8)    VALUE 0.                    
       01  WS-FD38B-WRITE               PIC 9(8)    VALUE 0.                    
       01  CNT-KD38-READ                PIC 9(8)    VALUE 0.                    
       01  CNT-KD38-NOTFOUND            PIC 9(8)    VALUE 0.                    
       01  WS-NUM-SEQ                   PIC 9(3)    VALUE 0.                    
       01  WS-INTEREST                  PIC 9(2).99.                            
       01  WS-INTEREST-CHAR REDEFINES WS-INTEREST.                              
           05 WS-INTEREST-ALPHA         PIC X(5).                               
       01  WS-FD38-AMT-CPF-RATE         PIC 9(6)V99 VALUE 0.                    
                                                                                
       01  WS-SYS-DATE                  PIC X(8)    VALUE SPACES.               
       01  WS-SYS-TIME                  PIC X(8)    VALUE SPACES.               
       01  WS-EOF-F001                  PIC X(1)    VALUE SPACES.               
       01  WS-EOF-FD38                  PIC X(1)    VALUE SPACES.               
       01  WS-KD00-FD                   PIC X(1)    VALUE SPACES.               
       01  WS-KD60-FD                   PIC X(1)    VALUE SPACES.               
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------------*        
                                                                                
      *----------------------------------------------------------------*        
       0000-MAIN-ROUTINE.                                                       
      *----------------------------------------------------------------*        
           PERFORM  1000-OPEN-FILES      THRU    1000-EXIT.                     
           PERFORM  2000-READ-FD38       THRU    2000-EXIT.                     
           PERFORM  3000-PROCESS         THRU    3000-EXIT                      
                    UNTIL WS-EOF-FD38 = 'Y'.                                    
           PERFORM  9000-CLOSE-ROUTINE   THRU    9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------*        
                                                                                
           OPEN INPUT  BP13FD38                                                 
                       SY02F001                                                 
                I-O    BP13KD00                                                 
                       BP13KD38                                                 
                       BP13KD60                                                 
                       BP13KD99                                                 
                OUTPUT P13FD38A                                                 
                       P13FD38B.                                                
                                                                                
           IF WS-KD00-STATUS NOT = 00 AND 97                                    
               DISPLAY 'OPENING ERROR, BP13KD00-STATUS ' WS-KD00-STATUS         
               MOVE     WS-KD00-STATUS     TO RETURN-CODE                       
               PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
           IF WS-KD38-STATUS NOT = 00 AND 97                                    
               DISPLAY 'OPENING ERROR, BP13KD38-STATUS ' WS-KD38-STATUS         
               MOVE     WS-KD38-STATUS     TO RETURN-CODE                       
               PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
           IF WS-KD60-STATUS NOT = 00 AND 97                                    
               DISPLAY 'OPENING ERROR, BP13KD60-STATUS ' WS-KD60-STATUS         
               MOVE     WS-KD60-STATUS     TO RETURN-CODE                       
               PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
           IF WS-KD99-STATUS NOT = 00 AND 97                                    
               DISPLAY 'OPENING ERROR, BP13KD99-STATUS ' WS-KD99-STATUS         
               MOVE     WS-KD99-STATUS     TO RETURN-CODE                       
               PERFORM  9000-CLOSE-ROUTINE THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2500-READ-SY02F001   THRU 2500-EXIT.                         
           MOVE FUNCTION CURRENT-DATE(9:8) TO WS-SYS-TIME.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-READ-FD38.                                                          
      *----------------------------------------------------------------*        
                                                                                
           READ BP13FD38                                                        
                AT END MOVE 'Y' TO WS-EOF-FD38                                  
                       GO TO 2000-EXIT                                          
           END-READ.                                                            
           ADD 1 TO CNT-FD38-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       2500-READ-SY02F001.                                                      
      *-------------------------------------------------------------            
           READ SY02F001 AT END MOVE 'Y' TO WS-EOF-F001                         
                GO TO 2500-EXIT.                                                
           MOVE F001-DTE-CURRENT      TO WS-SYS-DATE.                           
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3000-PROCESS.                                                            
      *----------------------------------------------------------------*        
                                                                                
           MOVE 'N'                            TO WS-KD00-FD.                   
           PERFORM 4000-READ-KD00              THRU 4000-EXIT.                  
                                                                                
           IF WS-KD00-FD = 'Y'                                                  
              IF FD38-DTE-INSTAL-NEW(1:6) = WS-SYS-DATE(1:6)                    
                 PERFORM 4200-WRITE-KD99       THRU 4200-EXIT                   
                 IF KD00-AMT-INSTAL = 0                                         
                    MOVE FD38-AMT-INSTAL-NEW-TOT                                
                                               TO KD00-AMT-FIRST-INSTAL         
                    MOVE FD38-DTE-INSTAL-NEW   TO KD00-DTE-INSTAL-COMM          
                    MOVE 'C'                   TO KD00-CDE-ACCT-STAT            
                 END-IF                                                         
                 MOVE FD38-DTE-INSTAL-NEW   TO KD00-DTE-INSTAL-EFF              
                 MOVE FD38-AMT-INSTAL-NEW-TOT  TO KD00-AMT-INSTAL               
                 MOVE FD38-AMT-INSTAL-NEW-CASH TO KD00-AMT-INSTAL-CASH          
                 MOVE FD38-AMT-INSTAL-NEW-CPF1 TO KD00-AMT-INSTAL-CPF1          
                 MOVE FD38-AMT-INSTAL-NEW-CPF2 TO KD00-AMT-INSTAL-CPF2          
                 MOVE FD38-AMT-INSTAL-NEW-CPF3 TO KD00-AMT-INSTAL-CPF3          
                 MOVE FD38-AMT-INSTAL-NEW-CPF4 TO KD00-AMT-INSTAL-CPF4          
                 COMPUTE WS-FD38-AMT-CPF-RATE = FD38-AMT-INSTAL-NEW-CPF1        
                                              + FD38-AMT-INSTAL-NEW-CPF2        
                                              + FD38-AMT-INSTAL-NEW-CPF3        
                                              + FD38-AMT-INSTAL-NEW-CPF4        
                 MOVE WS-FD38-AMT-CPF-RATE    TO KD00-AMT-CPF-RATE              
                                                                                
                 IF WS-FD38-AMT-CPF-RATE > ZEROS                                
                    MOVE '1' TO KD00-CDE-CPF-STATUS                             
                 ELSE                                                           
                    MOVE '0' TO KD00-CDE-CPF-STATUS                             
                 END-IF                                                         
                                                                                
                 IF KD00-DTE-NEXT-INSTAL (1:6) = WS-SYS-DATE (1:6)              
                    MOVE ZEROS TO KD00-DTE-NEXT-INSTAL                          
                    MOVE ZEROS TO KD00-AMT-NEXT-INSTAL                          
                 END-IF                                                         
                 PERFORM 4100-REWRITE-KD00    THRU 4100-EXIT                    
                 PERFORM 4600-READ-KD60       THRU 4600-EXIT                    
                 IF WS-KD60-FD = 'Y'                                            
                    MOVE FD38-AMT-INSTAL-NEW-TOT  TO KD60-AMT-INST              
                    PERFORM 4700-REWRITE-KD60 THRU 4700-EXIT                    
                 END-IF                                                         
                 PERFORM 3500-READ-KD38    THRU 3500-EXIT                       
                 PERFORM 4500-WRITE-FD38B  THRU 4500-EXIT                       
                 PERFORM 5000-DELETE-KD38  THRU 5000-EXIT                       
              ELSE                                                              
                 PERFORM 4400-WRITE-FD38A     THRU 4400-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-FD38             THRU 2000-EXIT.                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       3500-READ-KD38.                                                          
      *----------------------------------------------------------------*        
                                                                                
           MOVE FD38-KEY-FLD TO KD38-KEY-FLD.                                   
                                                                                
           READ BP13KD38                                                        
           EVALUATE WS-KD38-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO CNT-KD38-READ                                         
              WHEN 23                                                           
                 ADD 1 TO CNT-KD38-NOTFOUND                                     
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN READING BP13KD38 - STATUS '                  
                             WS-KD38-STATUS ',KEY ' KD38-KEY-FLD                
                 MOVE        WS-KD38-STATUS    TO RETURN-CODE                   
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4000-READ-KD00.                                                          
      *----------------------------------------------------------------*        
           MOVE FD38-NUM-REGN TO KD00-NUM-REGN.                                 
                                                                                
           READ BP13KD00                                                        
           EVALUATE WS-KD00-STATUS                                              
              WHEN 00                                                           
                 MOVE 'Y' TO WS-KD00-FD                                         
                 ADD 1 TO CNT-KD00-READ                                         
              WHEN 23                                                           
                 ADD 1 TO CNT-KD00-NOTFOUND                                     
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN READING BP13KD00 - STATUS '                  
                             WS-KD00-STATUS ',KEY ' KD00-KEY-FLD                
                 MOVE        WS-KD00-STATUS    TO RETURN-CODE                   
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       4100-REWRITE-KD00.                                                       
      *----------------------------------------------------------------*        
                                                                                
           MOVE WS-SYS-DATE             TO KD00-DTE-UPDATE.                     
           MOVE WS-SYS-TIME             TO KD00-TME-UPDATE.                     
                                                                                
           REWRITE BP13KD00-REC.                                                
                                                                                
           EVALUATE WS-KD00-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO CNT-KD00-UPDATE                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN UPDATING BP13KD00 - STATUS '                 
                             WS-KD00-STATUS ',KEY ' KD00-KEY-FLD                
                 MOVE        WS-KD00-STATUS    TO RETURN-CODE                   
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4200-WRITE-KD99.                                                         
      *----------------------------------------------------------------*        
                                                                                
           MOVE BP13KD00-REC               TO BP13KD99-REC.                     
           MOVE FD38-NUM-REGN              TO KD99-NUM-REGN-LOG.                
           MOVE WS-SYS-DATE                TO KD99-DTE-UPDATE-LOG.              
           MOVE FUNCTION CURRENT-DATE(9:8) TO KD99-TME-UPDATE-LOG.              
                                                                                
           WRITE BP13KD99-REC.                                                  
           EVALUATE WS-KD99-STATUS                                              
              WHEN 00                                                           
              WHEN 22                                                           
                 ADD 1 TO CNT-KD99-WRITE                                        
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN WRITING  BP13KD99 - STATUS '                 
                             WS-KD99-STATUS ',KEY ' KD99-KEY-FLD                
                 MOVE        WS-KD99-STATUS    TO RETURN-CODE                   
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4400-WRITE-FD38A.                                                        
      *----------------------------------------------------------------*        
                                                                                
           MOVE BP13FD38-TRANS-REC TO P13FD38A-REC.                             
           WRITE P13FD38A-REC.                                                  
           ADD   1            TO   WS-FD38A-WRITE.                              
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4500-WRITE-FD38B.                                                        
      *----------------------------------------------------------------*        
                                                                                
           IF WS-KD38-STATUS = 00                                               
              MOVE BP13KD38-TRANS-REC TO P13FD38B-REC                           
           ELSE                                                                 
              MOVE BP13FD38-TRANS-REC TO P13FD38B-REC                           
           END-IF.                                                              
           WRITE P13FD38B-REC.                                                  
           ADD   1            TO   WS-FD38B-WRITE.                              
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       4600-READ-KD60.                                                          
      *----------------------------------------------------------------*        
           MOVE 'N'           TO WS-KD60-FD                                     
           MOVE FD38-NUM-REGN TO KD60-NUM-REGN.                                 
                                                                                
           READ BP13KD60                                                        
           EVALUATE WS-KD60-STATUS                                              
              WHEN 00                                                           
                 MOVE 'Y' TO WS-KD60-FD                                         
                 ADD 1 TO CNT-KD60-READ                                         
              WHEN 23                                                           
                 ADD 1 TO CNT-KD60-NOTFOUND                                     
                 DISPLAY 'BP13KD60 - RECORD NOTFND : ' KD60-NUM-REGN            
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN READING BP13KD60 - STATUS '                  
                             WS-KD60-STATUS ',KEY ' KD60-KEY-FLD                
                 MOVE        WS-KD60-STATUS    TO RETURN-CODE                   
           END-EVALUATE.                                                        
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------*        
       4700-REWRITE-KD60.                                                       
      *----------------------------------------------------------------*        
                                                                                
           MOVE WS-SYS-DATE             TO KD60-DTE-UPDATE.                     
           MOVE WS-SYS-TIME             TO KD60-TME-UPDATE.                     
                                                                                
           REWRITE BP13KD60-REC.                                                
                                                                                
           EVALUATE WS-KD60-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO CNT-KD60-UPDATE                                       
              WHEN 23                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN UPDATING BP13KD60 - STATUS '                 
                             WS-KD60-STATUS ',KEY ' KD60-KEY-FLD                
                 MOVE        WS-KD60-STATUS    TO RETURN-CODE                   
           END-EVALUATE.                                                        
                                                                                
                                                                                
       4700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       5000-DELETE-KD38.                                                        
      *----------------------------------------------------------------*        
                                                                                
           MOVE FD38-NUM-REGN     TO KD38-NUM-REGN.                             
           MOVE FD38-NUM-TRANS    TO KD38-NUM-TRANS.                            
           MOVE FD38-NUM-SCHEDULE TO KD38-NUM-SCHEDULE.                         
                                                                                
           DELETE BP13KD38.                                                     
           EVALUATE WS-KD38-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO CNT-KD38-DELETE                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN DELETING BP13KD38 - STATUS '                 
                             WS-KD38-STATUS ',KEY ' KD38-KEY-FLD                
                 MOVE        WS-KD38-STATUS    TO RETURN-CODE                   
           END-EVALUATE.                                                        
                                                                                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       9000-CLOSE-ROUTINE.                                                      
      *----------------------------------------------------------------*        
           CLOSE BP13FD38                                                       
                 SY02F001                                                       
                 BP13KD00                                                       
                 BP13KD38                                                       
                 BP13KD60                                                       
                 BP13KD99                                                       
                 P13FD38A                                                       
                 P13FD38B.                                                      
                                                                                
           IF WS-KD00-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR, BP13KD00-STATUS ' WS-KD00-STATUS.         
           IF WS-KD38-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR, BP13KD38-STATUS ' WS-KD38-STATUS.         
           IF WS-KD60-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR, BP13KD60-STATUS ' WS-KD60-STATUS.         
           IF WS-KD99-STATUS NOT = 00                                           
              DISPLAY 'CLOSING ERROR, BP13KD99-STATUS ' WS-KD99-STATUS.         
                                                                                
           DISPLAY '                                    '.                      
           DISPLAY '***** BP13CC16 CONTROL TOTALS ***** '.                      
           DISPLAY '                                    '.                      
           DISPLAY 'NO OF RECORDS READ FROM  (BP13FD38) = '                     
                    CNT-FD38-READ.                                              
           DISPLAY 'NO OF RECORDS DELETED FROM (BP13KD38) = '                   
                    CNT-KD38-DELETE.                                            
           DISPLAY 'NO OF RECORDS READ FROM  (BP13KD38) = '                     
                    CNT-KD38-READ.                                              
           DISPLAY 'NO OF RECORDS NOT FOUND  (BP13KD38) = '                     
                    CNT-KD38-NOTFOUND.                                          
           DISPLAY 'NO OF RECORDS FOUND FROM (BP13KD00) = '                     
                    CNT-KD00-READ.                                              
           DISPLAY 'NO OF RECORDS NOT FOUND  (BP13KD00) = '                     
                    CNT-KD00-NOTFOUND.                                          
           DISPLAY 'NO OF RECORDS UPDATED    (BP13KD00) = '                     
                    CNT-KD00-UPDATE.                                            
           DISPLAY 'NO OF RECORDS FOUND FROM (BP13KD60) = '                     
                    CNT-KD60-READ.                                              
           DISPLAY 'NO OF RECORDS NOT FOUND  (BP13KD60) = '                     
                    CNT-KD60-NOTFOUND.                                          
           DISPLAY 'NO OF RECORDS UPDATED    (BP13KD60) = '                     
                    CNT-KD60-UPDATE.                                            
           DISPLAY 'NO OF RECORDS WRITTEN    (BP13KD99) = '                     
                    CNT-KD99-WRITE.                                             
           DISPLAY 'NO OF RECORDS WRITTEN    (P13FD38A) = '                     
                    WS-FD38A-WRITE.                                             
           DISPLAY 'NO OF RECORDS WRITTEN    (P13FD38B) = '                     
                    WS-FD38B-WRITE.                                             
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
