       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF57.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  04/07/2012.                                               
      *===============================================================*         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *===============================================================*         
      *                                                               *         
      *  OBJECTIVE  :  TO EXTRACT RECORDS WITHIN THE QUEUE RANGE FROM *         
      *                BP13K205                                       *         
      *                                                               *         
      *  INPUT      :  BP13F205  BP13.K205.CTLREC (SORTED)            *         
      *                BP13F740  BP13.K740.MATUREBA (SORTED)          *         
      *  OUTPUT     :  BP13F74A  BP13.F740.ETHNIC.A633                *         
      *                                                               *         
      *===============================================================*         
      * CHG-NO   DATE     BY   DESCRIPTION                            *         
      * -------- -------- ---- -------------------------------------- *         
      * BP134581 04072012 SMR2 NEW PROGRAM                            *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205  ASSIGN       TO BP13F205.                           
           SELECT BP13F740  ASSIGN       TO BP13F740.                           
           SELECT BP13F74A  ASSIGN       TO BP13F74A.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F205                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 80  CHARACTERS.                                      
       COPY BP13F205.                                                           
                                                                                
       FD  BP13F740                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13F740.                                                           
                                                                                
       FD  BP13F74A                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 500 CHARACTERS.                                      
       01  BP13F74A-REC           PIC X(500).                                   
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01 WS-F205-EOF             PIC X(01) VALUE 'N'.                          
          88 F205-END-OF-FILE   VALUE 'Y'.                                      
       01 WS-F740-EOF             PIC X(01) VALUE 'N'.                          
          88 F740-END-OF-FILE   VALUE 'Y'.                                      
       01 WS-F205-READ-CNT        PIC 9(08) VALUE ZEROES.                       
       01 WS-F740-READ-CNT        PIC 9(08) VALUE ZEROES.                       
       01 WS-F74A-WRITE-CNT       PIC 9(08) VALUE ZEROES.                       
       01 WS-F205-NTFND-CNT       PIC 9(08) VALUE ZEROES.                       
       01 WS-F74A-OUT-RANGE       PIC 9(08) VALUE ZEROES.                       
                                                                                
       01 WS-EDT-DATE             PIC X(10) VALUE SPACES.                       
       01 WS-CUR-DATE             PIC X(08) VALUE SPACES.                       
                                                                                
       01 WS-EDIT1                PIC Z(07)9.                                   
                                                                                
       01 WS-F205-REC.                                                          
          05 WS-F205-KEY-FLD.                                                   
             10 WS-F205-NT        PIC X(03).                                    
             10 WS-F205-FT        PIC X(02).                                    
             10 WS-F205-CATG      PIC X(01).                                    
          05 WS-F205-START-Q      PIC X(05).                                    
          05 WS-F205-END-Q        PIC X(05).                                    
                                                                                
       01 WS-F740-REC.                                                          
          05 WS-F740-KEY-FLD.                                                   
             10 WS-F740-NT        PIC X(03).                                    
             10 WS-F740-FT        PIC X(02).                                    
             10 WS-F740-CATG      PIC X(01).                                    
          05 WS-F740-QUEUE        PIC X(05).                                    
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F205      THRU 2000-EXIT.                      
           PERFORM 3000-READ-BP13F740      THRU 3000-EXIT.                      
           PERFORM 4000-PROCESS-DATA       THRU 4000-EXIT                       
             UNTIL F740-END-OF-FILE.                                            
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  BP13F205                                                 
                       BP13F740                                                 
                OUTPUT BP13F74A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-CUR-DATE.                      
           STRING WS-CUR-DATE(7:2), '/',                                        
                  WS-CUR-DATE(5:2), '/',                                        
                  WS-CUR-DATE(1:4)  DELIMITED BY SIZE                           
                  INTO WS-EDT-DATE.                                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F205.                                                      
      *---------------------------------------------------------------*         
           READ BP13F205                                                        
             AT END                                                             
                MOVE 'Y'                   TO WS-F205-EOF                       
                GO TO 2000-EXIT.                                                
                                                                                
           MOVE F205-NUM-NT-ZONE           TO WS-F205-NT.                       
           MOVE F205-NUM-FLAT-TYPE         TO WS-F205-FT.                       
           MOVE F205-CDE-CATEGORY          TO WS-F205-CATG.                     
           MOVE F205-START-QUEUE           TO WS-F205-START-Q.                  
           MOVE F205-END-QUEUE             TO WS-F205-END-Q.                    
                                                                                
           ADD 1                           TO WS-F205-READ-CNT.                 
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-READ-BP13F740.                                                      
      *---------------------------------------------------------------*         
           READ BP13F740                                                        
             AT END                                                             
                MOVE 'Y'                   TO WS-F740-EOF                       
                GO TO 3000-EXIT.                                                
                                                                                
           MOVE F740-CDE-NT1               TO WS-F740-NT.                       
           MOVE F740-CDE-FLAT-TYPE         TO WS-F740-FT.                       
           IF F740-CDE-CATEGORY = '4'                                           
              MOVE '3'                     TO WS-F740-CATG                      
           ELSE                                                                 
              MOVE F740-CDE-CATEGORY       TO WS-F740-CATG                      
           END-IF.                                                              
           MOVE F740-NUM-NT-FT-QUEUE       TO WS-F740-QUEUE.                    
                                                                                
           ADD 1                           TO WS-F740-READ-CNT.                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-PROCESS-DATA.                                                       
      *---------------------------------------------------------------*         
           IF F205-END-OF-FILE                                                  
              ADD 1                        TO WS-F205-NTFND-CNT                 
              PERFORM 3000-READ-BP13F740   THRU 3000-EXIT                       
              GO TO 4000-EXIT                                                   
           END-IF.                                                              
                                                                                
           EVALUATE TRUE                                                        
           WHEN WS-F740-KEY-FLD = WS-F205-KEY-FLD                               
              IF WS-F740-QUEUE >= WS-F205-START-Q AND                           
                 WS-F740-QUEUE <= WS-F205-END-Q                                 
                 WRITE BP13F74A-REC        FROM BP13F740-REC                    
                 ADD 1                     TO WS-F74A-WRITE-CNT                 
              ELSE                                                              
                 ADD 1                     TO WS-F74A-OUT-RANGE                 
              END-IF                                                            
              PERFORM 3000-READ-BP13F740   THRU 3000-EXIT                       
                                                                                
           WHEN WS-F740-KEY-FLD < WS-F205-KEY-FLD                               
              ADD 1                        TO WS-F205-NTFND-CNT                 
              PERFORM 3000-READ-BP13F740   THRU 3000-EXIT                       
                                                                                
           WHEN WS-F740-KEY-FLD > WS-F205-KEY-FLD                               
              PERFORM 2000-READ-BP13F205   THRU 2000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
           CLOSE BP13F205                                                       
                 BP13F740                                                       
                 BP13F74A.                                                      
                                                                                
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13CF57       ' WS-EDT-DATE.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-F205-READ-CNT           TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS READ FROM BP13F205  : ' WS-EDIT1.                
           MOVE WS-F740-READ-CNT           TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS READ FROM BP13F740  : ' WS-EDIT1.                
           MOVE WS-F74A-WRITE-CNT          TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS WRITTEN TO BP13F74A : ' WS-EDIT1.                
           MOVE WS-F205-NTFND-CNT          TO WS-EDIT1.                         
           DISPLAY 'NO OF RECS NTFND FROM BP13F205 : ' WS-EDIT1.                
           MOVE WS-F74A-OUT-RANGE          TO WS-EDIT1.                         
           DISPLAY 'RECS OUT OF RANGE IN BP13F740  : ' WS-EDIT1.                
           DISPLAY ' '.                                                         
           DISPLAY '*---------------------------------------------*'.           
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
