       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C263.                                                 
      *AUTHOR.        RAINIER R. DASIL.                                         
      *DATE-WRITTEN.  28 NOV. 1995.                                             
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE : TO PRINT REPORT ON UPDATE OF ALLOCATION TAG    *            
      *                                                            *            
      *   INPUT FILE  : BM06F192                                   *            
      *   OUTPUT FILE : P13L263                                    *            
      *                                                            *            
      * CHG-NO    BY  ON    - DESCRIPTION                          *            
      * ---------------------------------                          *            
      * BP130418 LMS 981013 - Y2K CHANGES                          *            
      * BP130616 SSS 990107 - INCLUDE PRINT DATE                   *            
      * BP130818 GBR 991006 - TO REMOVE CALLING BP13C904.          *            
      * BP130818 AKT 000114 - CHANGE BM06F190 TO BM06F192.         *            
      * BP132541 LMS 041016 - EXPANSION OF ALLOCATION TAG TO X(3). *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
      *-------------------------------------------------------------            
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F192 ASSIGN TO BM06F192.                                  
                                                                                
           SELECT BP13L263 ASSIGN TO BP13L263.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
      *-------------------------------------------------------------            
       FILE SECTION.                                                            
                                                                                
       FD BM06F192                  BLOCK CONTAINS 0 RECORDS                    
                                    RECORD CONTAINS 750 CHARACTERS              
                                    RECORDING MODE IS F                         
                                    LABEL RECORDS ARE OMITTED.                  
       COPY BM06F192.                                                           
                                                                                
       FD BP13L263                  BLOCK CONTAINS 0 RECORDS                    
                                    RECORD CONTAINS 132 CHARACTERS              
                                    RECORDING MODE IS F                         
                                    LABEL RECORDS ARE OMITTED.                  
       01 BP13L263-REC              PIC X(132).                                 
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  PRINT-VARIABLES.                                                     
           05 P-TITLE-1.                                                        
              10 FILLER                 PIC X(12) VALUE 'BP13L263'.             
              10 FILLER                 PIC X(10) VALUE 'HDB3'.                 
              10 FILLER                 PIC X(77) VALUE SPACES.                 
              10 FILLER                 PIC X(7)  VALUE 'DATE :'.               
              10 P-DATE                 PIC X(10) VALUE SPACES.                 
              10 FILLER                 PIC X(11) VALUE '    PAGE :'.           
              10 P-PAGENO               PIC ZZZ9.                               
                                                                                
           05 P-TITLE-2.                                                        
              10 FILLER                 PIC X(39) VALUE SPACES.                 
              10 FILLER                 PIC X(60) VALUE                         
              'S Y S T E M   O F   C O M M I T M E N T'.                        
              10 FILLER                 PIC X(07) VALUE 'USER :'.               
              10 FILLER                 PIC X(15) VALUE                         
                 'ALLOCATION TEAM'.                                             
                                                                                
           05 P-TITLE-3.                                                        
              10 FILLER                 PIC X(41) VALUE SPACES.                 
              10 FILLER                 PIC X(60) VALUE                         
              'REPORT ON UPDATE OF ALLOCATION TAG'.                             
                                                                                
           05 P-TITLE-4.                                                        
              10 FILLER                 PIC X(1)  VALUE SPACE.                  
              10 FILLER                 PIC X(12) VALUE                         
                 'SERIAL NO'.                                                   
              10 FILLER                 PIC X(16) VALUE                         
                 'HDB-REF-NO'.                                                  
              10 FILLER                 PIC X(22) VALUE                         
                 'ADDRESS'.                                                     
              10 FILLER                 PIC X(11) VALUE                         
                 'ROOM TYPE'.                                                   
              10 FILLER                 PIC X(13) VALUE                         
                 'FLAT STATUS'.                                                 
              10 FILLER                 PIC X(20) VALUE                         
                 'ALLOC TAG (BEFORE)'.                                          
              10 FILLER                 PIC X(19) VALUE                         
                 'ALLOC TAG (AFTER)'.                                           
              10 FILLER                 PIC X(07) VALUE                         
                 'USERID'.                                                      
                                                                                
           05 P-TITLE-5.                                                        
              10 FILLER                 PIC X(1)  VALUE SPACE.                  
              10 FILLER                 PIC X(12) VALUE                         
                 '---------'.                                                   
              10 FILLER                 PIC X(16) VALUE                         
                 '--------------'.                                              
              10 FILLER                 PIC X(22) VALUE                         
                 '-------------------'.                                         
              10 FILLER                 PIC X(11) VALUE                         
                 '---------'.                                                   
              10 FILLER                 PIC X(13) VALUE                         
                 '-----------'.                                                 
              10 FILLER                 PIC X(20) VALUE                         
                 '------------------'.                                          
              10 FILLER                 PIC X(19) VALUE                         
                 '-----------------'.                                           
              10 FILLER                 PIC X(07) VALUE                         
                 '-------'.                                                     
                                                                                
           05 P-DETAIL.                                                         
              10 FILLER                 PIC X(2)  VALUE SPACES.                 
              10 P-SERIAL               PIC ZZZZ9.                              
              10 FILLER                 PIC X(6)  VALUE SPACES.                 
              10 P-HDBREF               PIC X(14).                              
              10 FILLER                 PIC X(2)  VALUE SPACES.                 
              10 P-ADDRESS              PIC X(20).                              
              10 FILLER                 PIC X(5)  VALUE SPACES.                 
              10 P-RMTYPE               PIC X(2).                               
              10 FILLER                 PIC X(6)  VALUE SPACES.                 
              10 P-FSTAT                PIC X(12).                              
              10 FILLER                 PIC X(8)  VALUE SPACES.                 
              10 P-OLD-ALLOC-TAG        PIC X(3).                               
              10 FILLER                 PIC X(17) VALUE SPACES.                 
              10 P-NEW-ALLOC-TAG        PIC X(3).                               
              10 FILLER                 PIC X(9)  VALUE SPACES.                 
              10 P-USERID               PIC X(7).                               
                                                                                
           05 P-DETAIL2.                                                        
              10 FILLER                 PIC X(29) VALUE SPACES.                 
              10 P-ADDRESS2             PIC X(20).                              
                                                                                
       01  VARIABLES.                                                           
           05 WS-COUNTERS.                                                      
              10  WS-PAGE-CTR            PIC 9(4) VALUE ZEROES.                 
              10  WS-LINE-CTR            PIC 9(2) VALUE 60.                     
           05 WS-SERIAL                  PIC 9(5) VALUE 0.                      
           05 WS-SQL-CODE                PIC S9(3).                             
           05 WS-DATE                    PIC X(8) VALUE SPACES.                 
                                                                                
           05 F192-EOF                   PIC X VALUE 'N'.                       
              88  END-OF-FILE                  VALUE 'Y'.                       
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
            PERFORM 0100-OPEN-FILES THRU 0100-EXIT.                             
                                                                                
            READ    BM06F192                                                    
            AT END  MOVE 'Y' TO F192-EOF.                                       
                                                                                
            PERFORM 1000-PROCESS    THRU 1000-EXIT                              
              UNTIL END-OF-FILE.                                                
                                                                                
            PERFORM 9000-CLOSE-FILES.                                           
                                                                                
            STOP RUN.                                                           
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       0100-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
            OPEN INPUT  BM06F192                                                
                 OUTPUT BP13L263.                                               
                                                                                
            MOVE FUNCTION CURRENT-DATE TO WS-DATE.                              
            STRING WS-DATE(7:2) '/' WS-DATE(5:2) '/' WS-DATE(1:4)               
               DELIMITED BY SIZE INTO P-DATE.                                   
                                                                                
       0100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-PROCESS.                                                            
      *-------------------------------------------------------------            
            ADD  1      TO  WS-SERIAL.                                          
                                                                                
            PERFORM 1500-MOVE-DETAILS     THRU  1500-EXIT.                      
            PERFORM 2000-WRITE-LISTING    THRU  2000-EXIT.                      
                                                                                
            READ BM06F192 AT END MOVE 'Y' TO F192-EOF.                          
                                                                                
       1000-EXIT.                                                               
             EXIT.                                                              
                                                                                
                                                                                
       1500-MOVE-DETAILS.                                                       
      *-------------------------------------------------------------            
            MOVE WS-SERIAL              TO  P-SERIAL.                           
            STRING  F192-SCHEME  '-'                                            
                    F192-ACC     '-'                                            
                    F192-CHK     '-'                                            
                    F192-ACC-TYPE  DELIMITED BY SIZE                            
            INTO  P-HDBREF.                                                     
                                                                                
            IF F192-NUM-UNIT (5:1) = SPACES OR LOW-VALUES OR ZEROES             
                                                                                
               STRING 'BLK' F192-NUM-BLOCK                                      
                      ' #'  F192-NUM-LEVEL '-'                                  
                            F192-NUM-UNIT (1:4)                                 
                            DELIMITED BY SIZE                                   
               INTO  P-ADDRESS                                                  
            ELSE                                                                
               STRING 'BLK' F192-NUM-BLOCK                                      
                      ' #'  F192-NUM-LEVEL '-'                                  
                            F192-NUM-UNIT (1:4) '-'                             
                            F192-NUM-UNIT (5:1)                                 
                            DELIMITED BY SIZE                                   
               INTO  P-ADDRESS.                                                 
                                                                                
            MOVE F192-NME-STREET       TO  P-ADDRESS2.                          
            MOVE F192-CDE-TYP-ACTUSE   TO  P-RMTYPE.                            
            MOVE F192-FLAT-STATUS      TO  P-FSTAT (1:2).                       
                                                                                
            IF F192-FLAT-STATUS      = 'RP' AND                                 
               F192-REPOSSESSED-CODE = '40'                                     
               MOVE '(BUY-BACK)'   TO  P-FSTAT (3:10)                           
            ELSE                                                                
               MOVE SPACES         TO  P-FSTAT (3:10)                           
            END-IF.                                                             
                                                                                
            MOVE F192-ALLOCN-TAG       TO  P-OLD-ALLOC-TAG.                     
            MOVE F192-NEW-ALLOCN-TAG   TO  P-NEW-ALLOC-TAG.                     
            MOVE F192-USER-ID          TO  P-USERID.                            
                                                                                
       1500-EXIT.                                                               
             EXIT.                                                              
                                                                                
                                                                                
       2000-WRITE-LISTING.                                                      
      *-------------------------------------------------------------            
            IF WS-LINE-CTR > 55                                                 
               PERFORM 2100-PRINT-HEADER  THRU  2100-EXIT                       
            END-IF.                                                             
            WRITE BP13L263-REC        FROM P-DETAIL  AFTER 2.                   
            WRITE BP13L263-REC        FROM P-DETAIL2.                           
            ADD 3                      TO  WS-LINE-CTR.                         
            MOVE SPACES                TO  P-DETAIL                             
                                           P-DETAIL2.                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       2100-PRINT-HEADER.                                                       
      *-------------------------------------------------------------            
            ADD 1             TO  WS-PAGE-CTR.                                  
            MOVE WS-PAGE-CTR  TO  P-PAGENO.                                     
            WRITE BP13L263-REC FROM P-TITLE-1  AFTER PAGE.                      
            WRITE BP13L263-REC FROM P-TITLE-2.                                  
            WRITE BP13L263-REC FROM P-TITLE-3.                                  
            WRITE BP13L263-REC FROM P-TITLE-4  AFTER 2.                         
            WRITE BP13L263-REC FROM P-TITLE-5.                                  
            MOVE 6             TO WS-LINE-CTR.                                  
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
            CLOSE BM06F192                                                      
                  BP13L263.                                                     
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
