       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C24R.                                                 
       AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  06 NOVEMVER 2013.                                         
      ***************************************************************           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      ***************************************************************           
      * OBJECTIVE: PRINT REPORT FOR FIS RECORDS WITH DIFFERENT      *           
      *            OFFER AND CURRENT SELLING PRICE                  *           
      *-------------------------------------------------------------*           
      * INPUT FILES: BM06F190                                       *           
      *              BM06K110                                       *           
      *              BM06K510                                       *           
      *              BP13K277                                       *           
      *                                                             *           
      * OUTPUT FILE: BP13L24R (REPORT)                              *           
      *-------------------------------------------------------------*           
      * CHG REF  OIC  DATE    DESCRIPTION                           *           
      *--------- ---  ------  --------------------------------------*           
      * BP135127 ESA1 061113  PRINT REPORT ON DIFF SELLING PRICE    *           
      * BP135177 ESA1 201213  CATER FOR OCS LAYOUT                  *           
      * BP135695 ESA1 070415  CATER FOR POF OPTION                  *           
      ***************************************************************           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            ALTERNATE KEY IS K110-AIX1                          
                            FILE STATUS   IS WS-K110-STATUS.                    
                                                                                
           SELECT BM06K510  ASSIGN       TO BM06K510                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K510-ESTCDE                         
                            FILE STATUS  IS WS-K510-STATUS.                     
                                                                                
           SELECT BP13K277  ASSIGN       TO BP13K277                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K277-KEY-FLD                        
                            FILE STATUS  IS WS-K277-STATUS.                     
                                                                                
           SELECT BM06F190  ASSIGN TO BM06F190.                                 
                                                                                
           SELECT BP13L24R  ASSIGN TO BP13L24R.                                 
                                                                                
       DATA DIVISION.                                                           
      *--------------------------------------------------------------           
       FILE SECTION.                                                            
      *--------------------------------------------------------------           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BM06K510                                                             
           RECORD CONTAINS 60  CHARACTERS.                                      
       COPY BM06K510.                                                           
                                                                                
       FD  BP13K277                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K277.                                                           
                                                                                
       FD  BM06F190                                                             
           RECORD CONTAINS 650 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       COPY BM06F190.                                                           
                                                                                
       FD  BP13L24R                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS OMITTED                                                
           RECORDING MODE IS F.                                                 
       01  BP13L24R-REC                   PIC X(132).                           
                                                                                
      *--------------------------------------------------------------           
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------           
       01  WS-WORK-VAR.                                                         
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-DATE            PIC X(08) VALUE SPACES.               
               10  FILLER              PIC X(13) VALUE SPACES.                  
           05  WS-DISP-DATE.                                                    
               10  WS-DISP-DD          PIC X(02) VALUE SPACES.                  
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-DISP-MM          PIC X(02) VALUE SPACES.                  
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-DISP-CCYY        PIC X(04) VALUE SPACES.                  
           05  WS-RETCODE              PIC -ZZZZ.                               
           05  WS-F190-ALT-KEY         PIC X(160).                              
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K110-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K510-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K277-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-END-OF-F190-SW       PIC X(01) VALUE SPACES.                  
               88  C-END-OF-F190       VALUE 'Y'.                               
           05  WS-END-OF-K110-SW       PIC X(01) VALUE SPACES.                  
               88  C-END-OF-K110       VALUE 'Y'.                               
           05  WS-FIRST-K110-REC-SW    PIC X(01) VALUE SPACES.                  
               88  C-FIRST-K110-REC    VALUE 'Y'.                               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F190-READ-CTR        PIC 9(06) VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(02) VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(04) VALUE ZEROES.                  
           05  WS-PRT-CTR              PIC 9(04) VALUE ZEROES.                  
           05  WS-SNO-CTR              PIC 9(04) VALUE ZEROES.                  
           05  WS-ESTATE-CTR           PIC 9(04) VALUE ZEROES.                  
                                                                                
      *-------------------------------------------------------------            
      *    BP13C913 PARAMETERS                                                  
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH-ACC.                                              
              10 WS-LINK-NUM-SCH       PIC X(4).                                
              10 WS-LINK-NUM-ACC       PIC X(5).                                
       COPY P13COMM8.                                                           
                                                                                
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01 L24R-HEADER-1.                                                        
          05 FILLER              PIC X(08)  VALUE 'BP13L24R'.                   
          05 FILLER              PIC X(05)  VALUE SPACES .                      
          05 FILLER              PIC X(08)  VALUE 'HDBCAT3'.                    
          05 FILLER              PIC X(22)  VALUE SPACES.                       
          05 FILLER              PIC X(40)  VALUE                               
             'S Y S T E M   O F   C O M M I T M E N T'.                         
          05 FILLER              PIC X(28)  VALUE SPACES.                       
          05 FILLER              PIC X(07)  VALUE 'DATE :'.                     
          05 L24R-DATE           PIC X(10)  VALUE SPACES.                       
                                                                                
       01 L24R-HEADER-2.                                                        
          05 FILLER              PIC X(30)  VALUE SPACES .                      
          05 FILLER              PIC X(46)  VALUE                               
             'REPORT ON FIS RECORDS WITH DIFFERENT OFFER AND'.                  
          05 FILLER              PIC X(26)  VALUE                               
             ' CURRENT SELLING PRICE'.                                          
          05 FILLER              PIC X(09)  VALUE SPACES.                       
          05 FILLER              PIC X(07)  VALUE 'PAGE : '.                    
          05 L24R-PAGE           PIC ZZZ9 .                                     
                                                                                
       01 L24R-HEADER-3.                                                        
          05 FILLER              PIC X(55)  VALUE                               
             'S/NO  ESTATE                NEIGH/CONT  BLK    SCHEME A'.         
          05 FILLER              PIC X(55)  VALUE                               
             'CCT NO  POD  POF  POW   COST OF  COST OF  COST OF      '.         
          05 FILLER              PIC X(20)  VALUE                               
             '   SELLING PRICE'.                                                
                                                                                
       01 L24R-HEADER-4.                                                        
          05 FILLER              PIC X(55)  VALUE SPACES.                       
          05 FILLER              PIC X(55)  VALUE                               
             '        TAG  TAG  TAG   DOORS    FLOOR    WALL         '.         
          05 FILLER              PIC X(20)  VALUE                               
             'OFFER    CURRENT'.                                                
                                                                                
       01 L24R-DETAIL.                                                          
          05 L24R-SNO                PIC ZZZZ   VALUE ZEROES.                   
          05 FILLER                  PIC X(02)  VALUE SPACES.                   
          05 L24R-NME-ESTATE         PIC X(20)  VALUE SPACES.                   
          05 FILLER                  PIC X(02)  VALUE SPACES.                   
          05 L24R-NEIGH-CONT         PIC X(09)  VALUE SPACES.                   
          05 FILLER                  PIC X(03)  VALUE SPACES.                   
          05 L24R-NUM-BLK            PIC X(05)  VALUE SPACES.                   
          05 FILLER                  PIC X(02)  VALUE SPACES.                   
          05 L24R-SCH-ACCT           PIC X(14)  VALUE SPACES.                   
          05 FILLER                  PIC X(03)  VALUE SPACES.                   
          05 L24R-POD-TAG            PIC X(01)  VALUE SPACES.                   
          05 FILLER                  PIC X(04)  VALUE SPACES.                   
          05 L24R-POF-TAG            PIC X(03)  VALUE SPACES.                   
          05 FILLER                  PIC X(01)  VALUE SPACES.                   
          05 L24R-POW-TAG            PIC X(01)  VALUE SPACES.                   
          05 FILLER                  PIC X(04)  VALUE SPACES.                   
          05 L24R-AMT-POD            PIC ZZ,ZZZ VALUE ZEROES.                   
          05 FILLER                  PIC X(03)  VALUE SPACES.                   
          05 L24R-AMT-POF            PIC ZZ,ZZZ VALUE ZEROES.                   
          05 FILLER                  PIC X(03)  VALUE SPACES.                   
          05 L24R-AMT-POW            PIC ZZ,ZZZ VALUE ZEROES.                   
          05 FILLER                  PIC X(05)  VALUE SPACES.                   
          05 L24R-OFFER-SELL-PRICE   PIC ZZ,ZZZ,ZZ9 VALUE ZEROES.               
          05 FILLER                  PIC X(02)  VALUE SPACES.                   
          05 L24R-CURR-SELL-PRICE    PIC ZZ,ZZZ,ZZ9 VALUE ZEROES.               
                                                                                
       01  L24R-LINE                 PIC X(132)  VALUE ALL '-'.                 
                                                                                
      *--------------------------------------------------------------           
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------------           
                                                                                
      *--------------------------------*                                        
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
           PERFORM 2000-PROCESS-RECORDS   THRU 2000-EXIT                        
             UNTIL C-END-OF-F190.                                               
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       1000-INITIALIZATION.                                                     
      *--------------------------------*                                        
                                                                                
           OPEN INPUT    BM06F190                                               
                         BM06K110                                               
                         BM06K510                                               
                         BP13K277                                               
                OUTPUT   BP13L24R.                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BM06K110-STATUS ' WS-K110-STATUS              
              MOVE WS-K110-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K510-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BM06K510-STATUS ' WS-K510-STATUS              
              MOVE WS-K510-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K277-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BP13K277-STATUS ' WS-K277-STATUS              
              MOVE WS-K277-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
           MOVE WS-SYS-DATE(1:4)       TO WS-DISP-CCYY.                         
           MOVE WS-SYS-DATE(5:2)       TO WS-DISP-MM.                           
           MOVE WS-SYS-DATE(7:2)       TO WS-DISP-DD.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2000-PROCESS-RECORDS.                                                    
      *--------------------------------*                                        
                                                                                
           READ BM06F190 AT END                                                 
                MOVE 'Y'     TO WS-END-OF-F190-SW                               
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1      TO WS-F190-READ-CTR.                                      
           MOVE 'Y'   TO WS-FIRST-K110-REC-SW.                                  
                                                                                
           PERFORM 2100-START-BM06K110           THRU 2100-EXIT.                
           PERFORM 2200-PROCESS-BM06K110         THRU 2200-EXIT                 
             UNTIL WS-F190-ALT-KEY NOT = K110-AIX1                              
                OR C-END-OF-K110.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2100-START-BM06K110.                                                     
      *--------------------------------*                                        
                                                                                
           MOVE F190-2-ESTATE           TO K110-ESTATE.                         
           MOVE F190-2-NEIGHBOURHOOD    TO K110-NEIGHBOURHOOD.                  
           MOVE F190-2-CONTRACT-NO      TO K110-CONTRACT-NO.                    
           MOVE F190-2-BLK-NO           TO K110-BLK-NO.                         
           MOVE K110-AIX1               TO WS-F190-ALT-KEY.                     
                                                                                
           START BM06K110 KEY NOT LESS THAN K110-AIX1.                          
           EVALUATE WS-K110-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BM06K110: ' K110-AIX1           
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BM06K110,STATUS ' WS-K110-STATUS         
                   MOVE WS-K110-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2200-PROCESS-BM06K110.                                                   
      *--------------------------------*                                        
                                                                                
           READ BM06K110 NEXT RECORD AT END                                     
                MOVE 'Y'      TO WS-END-OF-K110-SW                              
                GO TO 2200-EXIT.                                                
                                                                                
           IF WS-F190-ALT-KEY NOT = K110-AIX1                                   
              GO TO 2200-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF K110-CURR-SELLING-PRICE      NOT NUMERIC                          
              MOVE ZEROES           TO  K110-CURR-SELLING-PRICE                 
           END-IF.                                                              
                                                                                
           IF K110-1ST-OFFER-SELLING-PRICE NOT NUMERIC                          
              MOVE ZEROES           TO  K110-1ST-OFFER-SELLING-PRICE            
           END-IF.                                                              
                                                                                
           IF K110-AMT-POD NOT NUMERIC                                          
              MOVE ZEROES           TO  K110-AMT-POD                            
           END-IF.                                                              
                                                                                
           IF K110-AMT-POF NOT NUMERIC                                          
              MOVE ZEROES           TO  K110-AMT-POF                            
           END-IF.                                                              
                                                                                
           IF K110-AMT-FLEXI NOT NUMERIC                                        
              MOVE ZEROES           TO  K110-AMT-FLEXI                          
           END-IF.                                                              
                                                                                
           IF K110-AMT-POW NOT NUMERIC                                          
              MOVE ZEROES           TO  K110-AMT-POW                            
           END-IF.                                                              
                                                                                
           IF K110-AMT-OCS-DISCOUNT NOT NUMERIC                                 
              MOVE ZEROES           TO  K110-AMT-OCS-DISCOUNT                   
           END-IF.                                                              
                                                                                
           IF K110-AMT-FLAT-DISCOUNT NOT NUMERIC                                
              MOVE ZEROES           TO  K110-AMT-FLAT-DISCOUNT                  
           END-IF.                                                              
                                                                                
           IF K110-AMT-SC-SPR-PREMIUM NOT NUMERIC                               
              MOVE ZEROES           TO  K110-AMT-SC-SPR-PREMIUM                 
           END-IF.                                                              
                                                                                
           IF K110-CURR-SELLING-PRICE NOT NUMERIC                               
              MOVE ZEROES           TO  K110-CURR-SELLING-PRICE                 
           END-IF.                                                              
                                                                                
           IF K110-AMT-LBS-DIFF-PRICE NOT NUMERIC                               
              MOVE ZEROES           TO K110-AMT-LBS-DIFF-PRICE                  
           END-IF.                                                              
                                                                                
           IF K110-AMT-SINGLES-PREMIUM NOT NUMERIC                              
              MOVE ZEROES           TO K110-AMT-SINGLES-PREMIUM                 
           END-IF.                                                              
                                                                                
           IF (K110-CURR-SELLING-PRICE   NOT  = 99999999 AND ZEROES) AND        
              (K110-1ST-OFFER-SELLING-PRICE NOT = 99999999 AND ZEROES)          
               IF ((K110-1ST-OFFER-SELLING-PRICE +                              
                    K110-AMT-SINGLES-PREMIUM -                                  
                    K110-AMT-LBS-DIFF-PRICE +                                   
                    K110-AMT-POD + K110-AMT-POF + K110-AMT-FLEXI +              
                    K110-AMT-POW) -                                             
                    K110-AMT-OCS-DISCOUNT -                                     
                    K110-AMT-FLAT-DISCOUNT +                                    
                    K110-AMT-SC-SPR-PREMIUM) NOT =                              
                    K110-CURR-SELLING-PRICE                                     
                  PERFORM 3000-PRINT-REPORT       THRU 3000-EXIT                
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3000-PRINT-REPORT.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 3300-CHECK-LINE-CTR            THRU 3300-EXIT.               
                                                                                
           IF C-FIRST-K110-REC                                                  
              MOVE 'N'          TO WS-FIRST-K110-REC-SW                         
              PERFORM 3100-MOVE-ESTATE-DETAILS    THRU 3100-EXIT                
              MOVE SPACES       TO BP13L24R-REC                                 
              WRITE BP13L24R-REC                                                
              ADD   1           TO WS-LINE-CTR                                  
                                   WS-ESTATE-CTR                                
           END-IF.                                                              
                                                                                
           PERFORM 3200-MOVE-POD-POF-DETAILS      THRU 3200-EXIT.               
                                                                                
           PERFORM 3300-CHECK-LINE-CTR            THRU 3300-EXIT.               
                                                                                
           WRITE BP13L24R-REC    FROM L24R-DETAIL.                              
           ADD   1                 TO WS-LINE-CTR.                              
           MOVE SPACES             TO L24R-DETAIL.                              
           ADD   1                 TO WS-PRT-CTR.                               
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3100-MOVE-ESTATE-DETAILS.                                                
      *--------------------------------*                                        
                                                                                
           PERFORM 3110-READ-BM06K510          THRU 3110-EXIT.                  
                                                                                
           ADD  1   TO WS-SNO-CTR.                                              
           MOVE WS-SNO-CTR              TO L24R-SNO.                            
           MOVE K510-ESTATE-NAME        TO L24R-NME-ESTATE.                     
           STRING F190-2-NEIGHBOURHOOD '/' F190-2-CONTRACT-NO                   
                  DELIMITED BY SIZE   INTO L24R-NEIGH-CONT.                     
           MOVE F190-2-BLK-NO           TO L24R-NUM-BLK.                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3110-READ-BM06K510.                                                      
      *--------------------------------*                                        
                                                                                
           MOVE K110-ESTATE             TO K510-ESTCDE.                         
           READ BM06K510.                                                       
                                                                                
           EVALUATE WS-K510-STATUS                                              
              WHEN 00                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BM06K510: ' K510-ESTCDE         
                   MOVE SPACES     TO K510-REC                                  
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BM06K510,STATUS ' WS-K510-STATUS         
                   MOVE WS-K510-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3200-MOVE-POD-POF-DETAILS.                                               
      *--------------------------------*                                        
                                                                                
           PERFORM 3210-READ-BP13K277            THRU 3210-EXIT.                
           PERFORM 3220-READ-PIDB                THRU 3220-EXIT.                
                                                                                
           PERFORM 3230-MOVE-POD-POF-AMT         THRU 3230-EXIT.                
                                                                                
           STRING K110-SCH-ACC-NO (1:4) '-'                                     
                  K110-SCH-ACC-NO (5:4) '-'                                     
                  K110-SCH-ACC-NO (9:1) '-'                                     
                  K110-SCH-ACC-NO (10:2)                                        
                  DELIMITED BY SIZE  INTO L24R-SCH-ACCT.                        
           MOVE K110-CDE-POD-TAG       TO L24R-POD-TAG.                         
           IF K110-OPTION-POF NOT = SPACES AND LOW-VALUES                       
              STRING K110-CDE-POF-TAG '/'                                       
                     K110-OPTION-POF DELIMITED BY SIZE                          
                     INTO L24R-POF-TAG                                          
           ELSE                                                                 
              MOVE K110-CDE-POF-TAG    TO L24R-POF-TAG                          
           END-IF.                                                              
           MOVE K110-CDE-POW-TAG       TO L24R-POW-TAG.                         
           MOVE K110-1ST-OFFER-SELLING-PRICE  TO L24R-OFFER-SELL-PRICE.         
           MOVE K110-CURR-SELLING-PRICE       TO L24R-CURR-SELL-PRICE.          
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3210-READ-BP13K277.                                                      
      *--------------------------------*                                        
           MOVE SPACES                  TO K277-KEY-FLD.                        
           MOVE F190-2-ESTATE           TO K277-NUM-ESTATE.                     
           MOVE F190-2-NEIGHBOURHOOD    TO K277-NUM-NEIGHBOUR.                  
           MOVE F190-2-CONTRACT-NO      TO K277-NUM-CONTR.                      
           MOVE F190-2-BLK-NO           TO K277-NUM-BLK.                        
           MOVE K110-NUM-FEATURE        TO K277-NUM-FEATURE.                    
           MOVE K110-NUM-LAYOUT         TO K277-NUM-LAYOUT.                     
                                                                                
           READ BP13K277.                                                       
                                                                                
           EVALUATE WS-K277-STATUS                                              
              WHEN 00                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BP13K277, KEY: '                
                            K277-KEY-FLD                                        
                   MOVE SPACES     TO BP13K277-REC                              
                   INITIALIZE         BP13K277-REC                              
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BP13K277,STATUS ' WS-K277-STATUS         
                   MOVE WS-K277-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3220-READ-PIDB.                                                          
      *--------------------------------*                                        
                                                                                
           MOVE SPACES           TO BP13COMM8-REC.                              
           INITIALIZE               BP13COMM8-REC.                              
           MOVE K110-SCH-ACC-NO  TO WS-LINK-REC.                                
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC                                    
                                 BP13COMM8-REC.                                 
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
              WHEN +000                                                         
                 CONTINUE                                                       
              WHEN +100                                                         
                 MOVE SPACES   TO BP13COMM8-REC                                 
                 INITIALIZE       BP13COMM8-REC                                 
                 DISPLAY 'REC NOT FOUND IN PIDB: ' K110-SCH-ACC-NO              
              WHEN OTHER                                                        
                 MOVE COMM8-CDE-SYSERR      TO WS-RETCODE                       
                 DISPLAY 'ERROR IN CALLING BP13C913, RETCODE: '                 
                         WS-RETCODE                                             
                 MOVE 99                    TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT                 
           END-EVALUATE.                                                        
                                                                                
                                                                                
       3220-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3230-MOVE-POD-POF-AMT.                                                   
      *--------------------------------*                                        
                                                                                
           EVALUATE COMM8-CDE-TYP-ACTLUSE                                       
              WHEN '02'                                                         
                 IF K110-CDE-POD-TAG = 'Y'                                      
                    MOVE K277-AMT-POD-2RM   TO L24R-AMT-POD                     
                 ELSE                                                           
                    MOVE ZEROES              TO L24R-AMT-POD                    
                 END-IF                                                         
                 IF K110-CDE-POF-TAG = 'Y'                                      
                    IF K110-OPTION-POF = '2'                                    
                       MOVE K277-AMT-POF2-2RM   TO L24R-AMT-POF                 
                    ELSE                                                        
                       MOVE K277-AMT-POF-2RM    TO L24R-AMT-POF                 
                    END-IF                                                      
                 ELSE                                                           
                    MOVE ZEROES              TO L24R-AMT-POF                    
                 END-IF                                                         
                                                                                
                 IF K110-CDE-POW-TAG = 'Y'                                      
                    MOVE K277-AMT-POW-2RM    TO L24R-AMT-POW                    
                 ELSE                                                           
                    MOVE ZEROES              TO L24R-AMT-POW                    
                 END-IF                                                         
                                                                                
              WHEN '03'                                                         
                 IF K110-CDE-POD-TAG = 'Y'                                      
                    MOVE K277-AMT-POD-3RM   TO L24R-AMT-POD                     
                 ELSE                                                           
                    MOVE ZEROES             TO L24R-AMT-POD                     
                 END-IF                                                         
                 IF K110-CDE-POF-TAG = 'Y'                                      
                    IF K110-OPTION-POF = '2'                                    
                       MOVE K277-AMT-POF2-3RM  TO L24R-AMT-POF                  
                    ELSE                                                        
                       MOVE K277-AMT-POF-3RM   TO L24R-AMT-POF                  
                    END-IF                                                      
                 ELSE                                                           
                    MOVE ZEROES             TO L24R-AMT-POF                     
                 END-IF                                                         
                                                                                
                 IF K110-CDE-POW-TAG = 'Y'                                      
                    MOVE K277-AMT-POW-3RM    TO L24R-AMT-POW                    
                 ELSE                                                           
                    MOVE ZEROES              TO L24R-AMT-POW                    
                 END-IF                                                         
                                                                                
              WHEN '04'                                                         
                 IF K110-CDE-POD-TAG = 'Y'                                      
                    MOVE K277-AMT-POD-4RM  TO L24R-AMT-POD                      
                 ELSE                                                           
                    MOVE ZEROES            TO L24R-AMT-POD                      
                 END-IF                                                         
                 IF K110-CDE-POF-TAG = 'Y'                                      
                    IF K110-OPTION-POF = '2'                                    
                       MOVE K277-AMT-POF2-4RM TO L24R-AMT-POF                   
                    ELSE                                                        
                       MOVE K277-AMT-POF-4RM  TO L24R-AMT-POF                   
                    END-IF                                                      
                 ELSE                                                           
                    MOVE ZEROES            TO L24R-AMT-POF                      
                 END-IF                                                         
                                                                                
                 IF K110-CDE-POW-TAG = 'Y'                                      
                    MOVE K277-AMT-POW-4RM    TO L24R-AMT-POW                    
                 ELSE                                                           
                    MOVE ZEROES              TO L24R-AMT-POW                    
                 END-IF                                                         
                                                                                
              WHEN '05'                                                         
                 IF K110-CDE-POD-TAG = 'Y'                                      
                    MOVE K277-AMT-POD-5RM  TO L24R-AMT-POD                      
                 ELSE                                                           
                    MOVE ZEROES            TO L24R-AMT-POD                      
                 END-IF                                                         
                 IF K110-CDE-POF-TAG = 'Y'                                      
                    IF K110-OPTION-POF = '2'                                    
                       MOVE K277-AMT-POF2-5RM TO L24R-AMT-POF                   
                    ELSE                                                        
                       MOVE K277-AMT-POF-5RM  TO L24R-AMT-POF                   
                    END-IF                                                      
                 ELSE                                                           
                    MOVE ZEROES            TO L24R-AMT-POF                      
                 END-IF                                                         
                                                                                
                 IF K110-CDE-POW-TAG = 'Y'                                      
                    MOVE K277-AMT-POW-5RM    TO L24R-AMT-POW                    
                 ELSE                                                           
                    MOVE ZEROES              TO L24R-AMT-POW                    
                 END-IF                                                         
                                                                                
              WHEN OTHER                                                        
                 MOVE ZEROES                  TO L24R-AMT-POD                   
                                                 L24R-AMT-POF                   
                                                 L24R-AMT-POW                   
           END-EVALUATE.                                                        
                                                                                
       3230-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3300-CHECK-LINE-CTR.                                                     
      *--------------------------------*                                        
                                                                                
           IF WS-LINE-CTR > 56  OR                                              
              WS-LINE-CTR = 0                                                   
              PERFORM 3310-PRINT-PAGE-BREAK      THRU 3310-EXIT                 
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3310-PRINT-PAGE-BREAK.                                                   
      *--------------------------------*                                        
                                                                                
           ADD   1   TO WS-PAGE-CTR.                                            
           MOVE WS-PAGE-CTR      TO L24R-PAGE.                                  
           MOVE WS-DISP-DATE     TO L24R-DATE.                                  
           WRITE BP13L24R-REC  FROM L24R-HEADER-1 AFTER PAGE.                   
           WRITE BP13L24R-REC  FROM L24R-HEADER-2.                              
           WRITE BP13L24R-REC  FROM L24R-LINE     AFTER 2.                      
           WRITE BP13L24R-REC  FROM L24R-HEADER-3.                              
           WRITE BP13L24R-REC  FROM L24R-HEADER-4.                              
           WRITE BP13L24R-REC  FROM L24R-LINE.                                  
           MOVE 7                TO WS-LINE-CTR.                                
                                                                                
       3310-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       9000-CLOSE-FILES.                                                        
      *--------------------------------*                                        
                                                                                
           DISPLAY 'PROGRAM ID   : BP13C24R'.                                   
           DISPLAY 'CURRENT DATE : ' WS-DISP-DATE.                              
           DISPLAY SPACES.                                                      
           DISPLAY '*=============== COUNTERS ===============*'.                
           DISPLAY 'BM06F190 RECORDS READ       : ' WS-F190-READ-CTR.           
           DISPLAY 'BM06F190 RECORDS PRINTED    : ' WS-ESTATE-CTR.              
           DISPLAY 'NO. OF RECORDS PRINTED      : ' WS-PRT-CTR.                 
                                                                                
           CLOSE BM06K110                                                       
                 BM06K510                                                       
                 BP13K277                                                       
                 BM06F190                                                       
                 BP13L24R.                                                      
                                                                                
            IF WS-K110-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BM06K110-STATUS ' WS-K110-STATUS            
            END-IF.                                                             
                                                                                
            IF WS-K510-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BM06K510-STATUS ' WS-K510-STATUS            
            END-IF.                                                             
                                                                                
            IF WS-K277-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BP13K277-STATUS ' WS-K277-STATUS            
            END-IF.                                                             
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
