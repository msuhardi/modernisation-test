       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C210.                                                 
      *AUTHOR.        SAUL SIO.                                                 
      *DATE-WRITTEN.  10/09/1999.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *                                                             *           
      *  OBJECTIVE  :                                               *           
      *    1.  TO LIST OUT THOSE 3-ROOM WIS APPLICANTS WHO DID NOT  *           
      *        SELECT FLAT IN THE PREVIOUS MONTH.                   *           
      *                                                             *           
      *  INPUT    :  1. BP13F800   BP13.K800.APPLN                  *           
      *              2. BP13K060                                    *           
      *  LISTING  :  1. BP13L210   3R APPLICANT NOT SELECT FLAT     *           
      *                                                             *           
      * CHG NO   BY   ON        DESCRIPTION                         *           
      * -------- ---- --------  -----------                         *           
      * BP130712 SSS  19990910  NEW PROGRAM                         *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800  ASSIGN  TO  BP13F800.                               
                                                                                
           SELECT BP13K060  ASSIGN  TO  BP13K060                                
                  ACCESS       IS RANDOM                                        
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY   IS K060-KEY-FLD                                  
                  FILE STATUS  IS BP13K060-STATUS.                              
                                                                                
           SELECT BP13L210  ASSIGN  TO  BP13L210.                               
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F800                                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F800.                                                           
                                                                                
       FD  BP13K060                                                             
           RECORD CONTAINS  25 CHARACTERS.                                      
       COPY BP13K060.                                                           
                                                                                
       FD  BP13L210                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  L210-PRT-REC                PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  BP13K060-STATUS               PIC 9(2)   VALUE 0.                    
                                                                                
       01  MTH                           PIC 9(2)    VALUE 1.                   
                                                                                
       01  WS-TOTAL-PRTED                PIC 9(2)    VALUE 0.                   
       01  WS-LINE-CNT                   PIC 9(2)    VALUE 60.                  
       01  WS-PAGE-CNT                   PIC 9(4)    VALUE ZERO.                
       01  WS-F800-READ                  PIC 9(4)    VALUE ZERO.                
       01  WS-F800-EOF                   PIC X VALUE 'N'.                       
                                                                                
       01  WS-DTE-REGN.                                                         
           05 WS-DTE-REGN-CCYY           PIC X(4).                              
           05 WS-DTE-REGN-MM             PIC X(2).                              
           05 WS-DTE-REGN-DD             PIC X(2).                              
       01  WS-REGN-DATE REDEFINES WS-DTE-REGN.                                  
           05 WS-REGN-CCYY           PIC 9(4).                                  
           05 WS-REGN-MM             PIC 9(2).                                  
           05 WS-REGN-DD             PIC 9(2).                                  
                                                                                
       01  WS-COMPUTE-MONTH              PIC 9 VALUE ZERO.                      
       01  WS-COMPUTE-YEAR               PIC 9 VALUE ZERO.                      
                                                                                
       01  WS-DATE.                                                             
           03 WS-CCYY                    PIC 9(4).                              
           03 WS-MM                      PIC 9(2).                              
           03 WS-DD                      PIC 9(2).                              
                                                                                
       01  WS-MONTH.                                                            
           03 WS-MON                     PIC X(36) VALUE                        
             'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'.                            
           03 FILLER REDEFINES WS-MON.                                          
              05 MON           OCCURS   12 TIMES PIC X(3).                      
                                                                                
      **************************************************************            
      *           LAY-OUT FOR CONTROL LISTING                      *            
      **************************************************************            
       01  L210-HDG1.                                                           
           03  FILLER              PIC X(14)   VALUE 'BP13L210'.                
           03  FILLER              PIC X(8)    VALUE 'HDB3'.                    
           03  FILLER              PIC X(18)   VALUE SPACES.                    
           03  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           03  FILLER              PIC X(28)   VALUE SPACES.                    
           03  FILLER              PIC X(7)    VALUE 'DATE : '.                 
           03  L210-DATE           PIC X(10)   VALUE SPACES.                    
                                                                                
       01  L210-HDG2.                                                           
           03  FILLER              PIC X(26)  VALUE SPACES.                     
           03  FILLER              PIC X(48)  VALUE                             
               'LIST OF 3-ROOM WIS APPLICANTS NOT SELECTED FLATS'.              
           03  FILLER              PIC X(4)   VALUE ' IN '.                     
           03  L210-HDR-MONTH      PIC X(3)   VALUE SPACES.                     
           03  FILLER              PIC X      VALUE SPACES.                     
           03  L210-HDR-YEAR       PIC X(4)   VALUE SPACES.                     
           03  FILLER              PIC X(21)  VALUE SPACES.                     
           03  FILLER              PIC X(7)   VALUE 'PAGE : '.                  
           03  L210-PAGE           PIC ZZZZ9.                                   
                                                                                
       01  L210-HDG3.                                                           
           05  FILLER              PIC X(7)    VALUE '    S/N'.                 
           05  FILLER              PIC X(5)    VALUE SPACES.                    
           05  FILLER              PIC X(9)    VALUE 'REGN NO  '.               
           05  FILLER              PIC X(5)    VALUE SPACES.                    
           05  FILLER              PIC X(9)    VALUE 'DTE-REGN '.               
           05  FILLER              PIC X(8)    VALUE SPACES.                    
           05  FILLER              PIC X(17)   VALUE                            
               ' NRIC1 / NRIC2  '.                                              
           05  FILLER              PIC X(7)    VALUE SPACES.                    
           05  FILLER              PIC X(22)   VALUE 'NT'.                      
           05  FILLER              PIC X(8)    VALUE 'ZONE  '.                  
           05  FILLER              PIC X(9)    VALUE 'FLAT TYPE'.               
                                                                                
       01  L210-HDG4.                                                           
           05  FILLER              PIC X(125)  VALUE ALL '-'.                   
                                                                                
       01  L210-DETAILS.                                                        
           03  L210-SNO            PIC ZZZZZZ9.                                 
           03  FILLER              PIC X(4)  VALUE SPACES.                      
           03  L210-REGN           PIC X(9)  VALUE SPACES.                      
           03  FILLER              PIC X(6)  VALUE SPACES.                      
           03  L210-DTE-REGN       PIC X(10) VALUE SPACES.                      
           03  FILLER              PIC X(4)  VALUE SPACES.                      
           03  L210-NRIC1          PIC X(9)  VALUE SPACES.                      
           03  FILLER              PIC X(3)  VALUE ' / '.                       
           03  L210-NRIC2          PIC X(9)  VALUE SPACES.                      
           03  FILLER              PIC X(5)  VALUE SPACES.                      
           03  L210-NEW-TOWN       PIC X(20) VALUE SPACES.                      
           03  FILLER              PIC X(4)  VALUE SPACES.                      
           03  L210-NUM-ZONE       PIC X(2)  VALUE SPACES.                      
           03  FILLER              PIC X(9)  VALUE SPACES.                      
           03  L210-FT             PIC X(2)  VALUE SPACES.                      
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
      *-------------------------------------------------------------            
           PERFORM 100-OPEN-ROUTINE THRU 100-EXIT.                              
           PERFORM 200-READ-F800    THRU 200-EXIT.                              
           PERFORM 300-PROCESS-ROUTINE THRU 300-EXIT                            
                   UNTIL WS-F800-EOF = 'Y'.                                     
           PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                             
                                                                                
                                                                                
       100-OPEN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           OPEN INPUT BP13F800                                                  
                      BP13K060                                                  
               OUTPUT BP13L210.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
           MOVE WS-MM                 TO MTH.                                   
           IF MTH = 01                                                          
              MOVE 12 TO MTH                                                    
           ELSE                                                                 
              COMPUTE MTH = MTH - 1                                             
           END-IF.                                                              
           MOVE MON(MTH)              TO L210-HDR-MONTH.                        
           MOVE WS-CCYY               TO L210-HDR-YEAR.                         
           STRING WS-DD '/' WS-MM '/' WS-CCYY                                   
                  DELIMITED BY SIZE INTO L210-DATE.                             
                                                                                
           IF BP13K060-STATUS NOT = 00 AND 97                                   
              DISPLAY ' OPENING BP13K060 ERROR ' BP13K060-STATUS                
              MOVE     BP13K060-STATUS  TO  RETURN-CODE                         
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                          
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F800.                                                           
      *-------------------------------------------------------------            
           READ BP13F800  AT END                                                
                          MOVE 'Y' TO WS-F800-EOF                               
                          GO       TO 200-EXIT.                                 
                                                                                
           ADD  1  TO WS-F800-READ.                                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-ROUTINE.                                                     
      *-------------------------------------------------------------            
           MOVE F800-DTE-REGN TO WS-DTE-REGN.                                   
           IF WS-REGN-DATE NOT NUMERIC                                          
              MOVE ZEROS TO WS-REGN-DATE.                                       
                                                                                
           DISPLAY '300 _ REGN-DATE ' WS-REGN-DATE.                             
                                                                                
           IF WS-REGN-CCYY = WS-CCYY                                            
              COMPUTE WS-COMPUTE-MONTH =                                        
                    WS-MM  -  WS-REGN-MM                                        
              IF WS-COMPUTE-MONTH = 1                                           
                 PERFORM 400-PROCESS-PARA THRU 400-EXIT                         
              END-IF                                                            
           ELSE                                                                 
              COMPUTE WS-COMPUTE-YEAR =                                         
                    WS-CCYY  -  WS-REGN-CCYY                                    
              IF WS-COMPUTE-YEAR = 1                                            
                 PERFORM 400-PROCESS-PARA THRU 400-EXIT                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F800           THRU 200-EXIT.                       
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-PROCESS-PARA.                                                        
      *-------------------------------------------------------------            
           DISPLAY '400 _ DTE_REGN ' F800-DTE-REGN                              
                   ',  COMPUTE MONTH ' WS-COMPUTE-MONTH                         
                   ',  COMPUTE YEAR  ' WS-COMPUTE-YEAR.                         
                                                                                
           MOVE SPACES                   TO L210-DETAILS                        
                                            K060-KEY-FLD.                       
           MOVE 02                       TO K060-SERIAL-NO                      
           MOVE F800-NUM-NEW-TOWN        TO K060-CODE.                          
           READ BP13K060.                                                       
           IF BP13K060-STATUS = 00                                              
              MOVE K060-DESC TO L210-NEW-TOWN                                   
           ELSE                                                                 
           IF BP13K060-STATUS = 23                                              
              DISPLAY 'NEW TOWN NOT FOUND STATUS 23, NT CODE IS '               
                                               L210-NEW-TOWN                    
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K060, STATS ' BP13K060-STATUS          
                                 '  KEY-FLD IS ' K060-KEY-FLD                   
              MOVE     BP13K060-STATUS  TO  RETURN-CODE                         
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                          
                                                                                
           PERFORM 500-PRINT-CONTROL-LIST  THRU 500-EXIT.                       
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       500-PRINT-CONTROL-LIST.                                                  
      *-------------------------------------------------------------            
           IF WS-LINE-CNT > 55                                                  
              PERFORM 600-PRINT-CONTROL-HEADING THRU 600-EXIT.                  
                                                                                
           MOVE  SPACES            TO L210-PRT-REC.                             
           ADD 1                   TO WS-TOTAL-PRTED.                           
           MOVE WS-TOTAL-PRTED     TO L210-SNO.                                 
                                                                                
           MOVE F800-NUM-FLAT-TYPE  TO L210-FT.                                 
           MOVE F800-NUM-REGN       TO L210-REGN.                               
           STRING F800-DTE-REGN(7:2) '/' F800-DTE-REGN(5:2) '/'                 
                  F800-DTE-REGN(1:4)                                            
                  DELIMITED BY SIZE INTO L210-DTE-REGN.                         
           MOVE F800-NUM-NRIC1      TO L210-NRIC1                               
           MOVE F800-NUM-NRIC2      TO L210-NRIC2                               
           MOVE F800-NUM-ZONE       TO L210-NUM-ZONE                            
                                                                                
           WRITE L210-PRT-REC    FROM L210-DETAILS.                             
           MOVE  SPACES            TO L210-PRT-REC.                             
           WRITE L210-PRT-REC.                                                  
           ADD  2 TO WS-LINE-CNT.                                               
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       600-PRINT-CONTROL-HEADING.                                               
      *-------------------------------------------------------------            
           ADD  1                 TO   WS-PAGE-CNT                              
           MOVE 1                 TO   WS-LINE-CNT                              
           MOVE WS-PAGE-CNT       TO   L210-PAGE.                               
                                                                                
           WRITE L210-PRT-REC     FROM L210-HDG1 AFTER PAGE.                    
           WRITE L210-PRT-REC     FROM L210-HDG2 AFTER 1.                       
           WRITE L210-PRT-REC     FROM L210-HDG3 AFTER 2.                       
           WRITE L210-PRT-REC     FROM L210-HDG4.                               
                                                                                
           MOVE  SPACES           TO   L210-PRT-REC.                            
           WRITE L210-PRT-REC.                                                  
                                                                                
           ADD  6 TO WS-LINE-CNT.                                               
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
           CLOSE BP13F800                                                       
                 BP13K060                                                       
                 BP13L210.                                                      
                                                                                
           IF BP13K060-STATUS NOT = 00 AND 97                                   
              DISPLAY ' CLOSING BP13K060 ERROR ' BP13K060-STATUS                
              MOVE     BP13K060-STATUS  TO  RETURN-CODE                         
              PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT.                          
                                                                                
           DISPLAY '                                  '                         
           DISPLAY ' *----- BP13C210 CONTROL LIST ---------*'.                  
           DISPLAY '                                  '                         
           DISPLAY ' NO. OF REC READ FROM F800   = ' WS-F800-READ.              
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
