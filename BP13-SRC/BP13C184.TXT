       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C184.                                                  
      *AUTHOR.       LSB.                                                       
      *DTE-WRITTEN   12/08/1998.                                                
      ***********************************************************               
      *                                                         *               
      *  OBJECTIVE: TO DELETE SUCCESSFUL RELOCATION OCCUPIER    *               
      *             RECORD FROM REQUEST OCCUPIER FILE BP13K825  *               
      *             AFTER WRITTEN TO BP13K820.                  *               
      *                                                         *               
      *  INPUT FILE :- BP13F825                                 *               
      *  I-O    FILE:- BP13K825                                 *               
      *---------------------------------------------------------*               
      *CHGE NO  DATE      AUTHOR    REMARKS                     *               
      *-------- --------  ------    --------                    *               
      *BP130540 21081998  LSB1      NEW PROGRAM.                *               
      *BP130418 26081998  CLT1      Y2K COMPLIANCE CHANGES.     *               
      *                                                         *               
      ***********************************************************               
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F825 ASSIGN TO BP13F825.                                  
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS WS-K820-STATUS.                            
                                                                                
           SELECT BP13K825 ASSIGN TO BP13K825                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K825-KEY-FLD                               
                  FILE STATUS     IS WS-K825-STATUS.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F825   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 200 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13F825.                                                           
                                                                                
       FD  BP13K820   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 400 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13K820.                                                           
                                                                                
       FD  BP13K825   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 200 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD.                               
       COPY BP13K825.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-SWITCHES.                                                         
                                                                                
           05  WS-K820-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K825-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-F825-EOF               PIC X    VALUE 'N'.                    
               88 F825-EOF                        VALUE 'Y'.                    
           05  WS-READ-CNT-F825          PIC 9(6) VALUE ZEROS.                  
           05  WS-K825-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K820-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K820-NOTFD             PIC 9(6) VALUE ZEROS.                  
           05  WS-DELETE                 PIC 9(6) VALUE ZEROS.                  
                                                                                
                                                                                
      *-------------------*'                                                    
       PROCEDURE DIVISION.                                                      
      *-------------------*'                                                    
                                                                                
       000-MAIN-LOGIC.                                                          
            PERFORM 100-OPEN-FILES   THRU 100-EXIT.                             
            PERFORM 200-READ-F825    THRU 200-EXIT.                             
            PERFORM 500-PROCESS-K825 THRU 500-EXIT                              
                    UNTIL F825-EOF.                                             
            PERFORM 999-CLOSE-FILES  THRU 999-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
           OPEN INPUT  BP13F825                                                 
                       BP13K820                                                 
                I-O    BP13K825.                                                
                                                                                
           IF WS-K820-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K820-STATUS ' WS-K820-STATUS              
              MOVE WS-K820-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-K825-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K825-STATUS ' WS-K825-STATUS              
              MOVE WS-K825-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-F825.                                                           
           READ BP13F825 AT END                                                 
           MOVE 'Y'                      TO WS-F825-EOF                         
           GO TO 200-EXIT.                                                      
                                                                                
           ADD 1                         TO WS-READ-CNT-F825.                   
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       500-PROCESS-K825.                                                        
                                                                                
           MOVE F825-KEY-FLD  TO K820-KEY-FLD                                   
           READ BP13K820                                                        
           IF WS-K820-STATUS = 00                                               
              ADD 1 TO WS-K820-READ                                             
              PERFORM 600-READ-BP13K825 THRU 600-EXIT                           
                                                                                
           ELSE                                                                 
              IF WS-K820-STATUS = 23                                            
                 ADD 1 TO WS-K820-NOTFD                                         
              ELSE                                                              
                 DISPLAY 'RECORD NOT FOUND IN BP13K820 ' K820-KEY-FLD           
                         ' KEY = ' K820-KEY-FLD.                                
                                                                                
            PERFORM 200-READ-F825    THRU 200-EXIT.                             
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-READ-BP13K825.                                                       
                                                                                
           MOVE F825-KEY-FLD  TO K825-KEY-FLD.                                  
           READ BP13K825                                                        
           IF WS-K825-STATUS = 00                                               
              ADD 1 TO WS-K825-READ                                             
                                                                                
              PERFORM 650-DELETE-BP13K825 THRU 650-EXIT                         
                                                                                
           ELSE                                                                 
              IF WS-K825-STATUS = 23                                            
                 DISPLAY 'RECORD NOT FOUND IN BP13K825 ' WS-K825-STATUS         
                         ' KEY= ' K825-KEY-FLD                                  
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K825,STATUS ' WS-K825-STATUS           
                         ' KEY= ' K825-KEY-FLD.                                 
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       650-DELETE-BP13K825.                                                     
                                                                                
            DELETE BP13K825.                                                    
                                                                                
            IF WS-K825-STATUS = 00                                              
               ADD 1                 TO WS-DELETE                               
               DISPLAY 'DELETE REC ' K825-KEY-FLD                               
            ELSE                                                                
               DISPLAY 'ERROR DELETE BP13K825 STATUS ' WS-K825-STATUS           
                         ' KEY = ' K825-KEY-FLD                                 
            END-IF.                                                             
                                                                                
                                                                                
       650-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       999-CLOSE-FILES.                                                         
                                                                                
           CLOSE BP13F825                                                       
                 BP13K820                                                       
                 BP13K825.                                                      
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K820, STATUS ' WS-K820-STATUS          
                      ' KEY= ' K825-KEY-FLD                                     
           END-IF.                                                              
                                                                                
           IF WS-K825-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K825, STATUS ' WS-K825-STATUS          
                      ' KEY= ' K825-KEY-FLD                                     
           END-IF.                                                              
                                                                                
           DISPLAY '*------------------ BP13C184-------------------'.           
                                                                                
           DISPLAY 'NO OF REC READ      BP13F825     = '                        
                    WS-READ-CNT-F825.                                           
           DISPLAY 'NO OF REC READ      BP13K820     = '                        
                    WS-K820-READ.                                               
           DISPLAY 'NO OF REC READ      BP13K825     = '                        
                    WS-K825-READ.                                               
           DISPLAY 'NO OF REC DELETE    BP13K825     = '                        
                    WS-DELETE.                                                  
           DISPLAY 'NO OF REC NOTFD     BP13K820     = '                        
                    WS-K820-NOTFD.                                              
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
