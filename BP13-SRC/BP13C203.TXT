       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C203.                                                 
      *AUTHOR.        MTD.                                                      
      *DATE-WRITTEN.  NOV2003.                                                  
      *===============================================================*         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      *===============================================================*         
      *  OBJECTIVE   : TO PRINT PRICING REPORT FOR EVERY ESTATE       *         
      *                INDICATED IN BP13F207                          *         
      *                                                               *         
      *  INPUT FILES : BP13F207                                       *         
      *                BM06K110                                       *         
      *                                                               *         
      *  LISTINGS    : BP13L203  - REPORT                             *         
      *---------------------------------------------------------------*         
      * CHG REQ# DATE       BY    DESCRIPTION                         *         
      * -------- ---------- ----  ------------------------------------*         
      * BP132423 18/11/2003 MTD   PRINT SP REPORT                     *         
      * BP132541 04/05/2004 SK    EXPAND ALLOCN-TAG TO 3 BYTES        *         
      * BP132792 06/01/2006 LMS   ALIGN REPORT                        *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F207  ASSIGN       TO BP13F207.                           
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS DYNAMIC                             
                           RECORD KEY    IS K110-KEY-FLD                        
                           ALTERNATE KEY IS K110-AIX1                           
                           FILE STATUS   IS WS-K110-STATUS.                     
                                                                                
           SELECT BM06K100 ASSIGN TO BM06K100                                   
                           ORGANIZATION  IS INDEXED                             
                           ACCESS MODE   IS RANDOM                              
                           RECORD KEY    IS K100-KEY-FLD                        
                           FILE STATUS   IS WS-K100-STATUS.                     
                                                                                
           SELECT BP13L203  ASSIGN       TO BP13L203.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F207                                                            
            RECORDING MODE  F                                                   
            LABEL RECORDS   ARE STANDARD                                        
            BLOCK CONTAINS  0   RECORDS                                         
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13F207.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BM06K100                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K100.                                                           
                                                                                
       FD   BP13L203                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       01   BP13L203-REC                PIC X(132).                             
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-WORK-VARIABLES.                                                   
           05  WS-SYSTEM-DATE.                                                  
               10 WS-SYS-DATE          PIC X(08) VALUE SPACES.                  
               10 FILLER               PIC X(13) VALUE SPACES.                  
           05  WS-DISP-DATE.                                                    
               10  WS-DISP-DATE-DD     PIC X(02) VALUE SPACES.                  
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-DISP-DATE-MM     PIC X(02) VALUE SPACES.                  
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-DISP-DATE-YY     PIC X(04) VALUE SPACES.                  
           05  WS-SAVE-ESTCDE          PIC X(03) VALUE SPACES.                  
           05  WS-RETCODE              PIC -ZZZZ.                               
           05  WS-K110-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K100-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F207-EOF-SW          PIC X(01) VALUE SPACES.                  
               88  C-F207-EOF          VALUE 'Y'.                               
           05  WS-NEW-REPORT-SW        PIC X(01) VALUE SPACES.                  
               88  C-NEW-REPORT        VALUE 'Y'.                               
           05  WS-K110-EOF-SW          PIC X(01) VALUE SPACES.                  
               88  C-K110-EOF          VALUE 'Y'.                               
           05  WS-CHANGE-RECORD-SW     PIC X(01) VALUE SPACES.                  
               88  C-CHANGE-RECORD     VALUE 'Y'.                               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-SNO-CTR              PIC 9(05) VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(02) VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(05) VALUE ZEROES.                  
                                                                                
      *-------------------------------------------------------------            
      * BP13C913 PARAMETERS                                                     
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH-ACC.                                              
              10 WS-LINK-NUM-SCH       PIC X(4).                                
              10 WS-LINK-NUM-ACC       PIC X(5).                                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *-------------------------------------------------------------            
      * REPORT LAYOUT                                                           
      *-------------------------------------------------------------            
       01  L203-HEAD-01.                                                        
           05 FILLER                   PIC X(55)  VALUE 'BP13L203'.             
           05 FILLER                   PIC X(20)  VALUE                         
              'SYSTEM OF COMMITMENT'.                                           
           05 FILLER                   PIC X(38)  VALUE SPACES.                 
           05 FILLER                   PIC X(7)   VALUE 'DATE : '.              
           05 L203-DATE                PIC X(10)  VALUE SPACES.                 
                                                                                
       01  L203-HEAD-02.                                                        
           05 FILLER                   PIC X(50)  VALUE SPACES.                 
           05 FILLER                   PIC X(19)  VALUE                         
              'PRICING REPORT FOR '.                                            
           05 L203-ESTATE-NAME         PIC X(27)  VALUE SPACES.                 
           05 FILLER                   PIC X(17)  VALUE SPACES.                 
           05 FILLER                   PIC X(07)  VALUE 'PAGE : '.              
           05 L203-PAGE                PIC ZZZ9   VALUE ZEROS.                  
                                                                                
       01  L203-HEAD-03.                                                        
           05 FILLER                   PIC X(50) VALUE                          
              'SNO    NT  NEIG/CONT AT  ADDRESS                 F'.             
           05 FILLER                   PIC X(50) VALUE                          
              'S RM 1ST-SALE   ALLOC-DTE  PREV-SP    DISC-SP    D'.             
           05 FILLER                   PIC X(32) VALUE                          
              'TE-RPRICE OFFER-SP   SEL ALN-QTR'.                               
                                                                                
       01  L203-DETAIL-LINE.                                                    
           05 L203-SNO                 PIC Z(04) VALUE ZEROS.                   
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 L203-TAG                 PIC X(01) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 L203-NEW-TOWN            PIC X(03) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 L203-NEIGH-CONT          PIC X(09) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 L203-ALLOCN-TAG          PIC X(03) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 L203-ADDRESS             PIC X(23) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 L203-FLAT-STATUS         PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 L203-ROOM-TYPE           PIC X(02) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 L203-DTE-1ST-SALE        PIC X(10) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 L203-DTE-ALLOCN          PIC X(10) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 L203-PREV-SP             PIC ZZ,ZZZ,Z99 VALUE ZEROES.             
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 L203-CURR-SP             PIC ZZ,ZZZ,Z99 VALUE ZEROES.             
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 L203-DTE-REPRICE         PIC X(10) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 L203-1ST-OFFER-SP        PIC ZZ,ZZZ,Z99 VALUE ZEROES.             
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 L203-SEL-TYPE            PIC X(03) VALUE SPACES.                  
           05 FILLER                   PIC X(01) VALUE SPACES.                  
           05 L203-ALLOCN-QTR          PIC X(07) VALUE SPACES.                  
                                                                                
       01  L203-LINE                   PIC X(132) VALUE ALL '-'.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
            PERFORM 1000-OPEN-FILES           THRU  1000-EXIT.                  
            PERFORM 2000-PROCESS-RECORDS      THRU  2000-EXIT                   
              UNTIL C-F207-EOF.                                                 
            PERFORM 9000-CLOSE-FILES          THRU  9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
            OPEN INPUT  BP13F207                                                
                        BM06K110                                                
                        BM06K100                                                
                 OUTPUT BP13L203.                                               
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110-OPEN ERROR, STATUS: ' WS-K110-STATUS            
              MOVE     WS-K110-STATUS TO RETURN-CODE                            
              PERFORM  9000-CLOSE-FILES            THRU 9000-EXIT.              
                                                                                
           IF WS-K100-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K100-OPEN ERROR, STATUS: ' WS-K100-STATUS            
              MOVE     WS-K100-STATUS TO RETURN-CODE                            
              PERFORM  9000-CLOSE-FILES            THRU 9000-EXIT.              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE.                        
           MOVE WS-SYSTEM-DATE (1:4)  TO WS-DISP-DATE-YY.                       
           MOVE WS-SYSTEM-DATE (5:2)  TO WS-DISP-DATE-MM.                       
           MOVE WS-SYSTEM-DATE (7:2)  TO WS-DISP-DATE-DD.                       
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2000-PROCESS-RECORDS.                                                    
      *-------------------------------------------------------------            
                                                                                
           READ BP13F207                                                        
                AT END MOVE 'Y' TO WS-F207-EOF-SW                               
                GO TO 2000-EXIT.                                                
                                                                                
           IF WS-SAVE-ESTCDE NOT = F207-ESTATE-CDE                              
              MOVE 'Y'              TO WS-NEW-REPORT-SW                         
              MOVE F207-ESTATE-CDE  TO WS-SAVE-ESTCDE                           
           END-IF.                                                              
                                                                                
           MOVE SPACES        TO WS-K110-EOF-SW                                 
                                 WS-CHANGE-RECORD-SW.                           
                                                                                
           PERFORM 2100-START-BM06K110       THRU 2100-EXIT.                    
           PERFORM 2200-READNXT-BM06K110     THRU 2200-EXIT                     
             UNTIL C-K110-EOF OR                                                
                   C-CHANGE-RECORD.                                             
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2100-START-BM06K110.                                                     
      *-------------------------------------------------------------            
           MOVE LOW-VALUES               TO K110-AIX1.                          
           MOVE F207-ESTATE-CDE          TO K110-ESTATE.                        
           MOVE F207-NEIGHBOURHOOD       TO K110-NEIGHBOURHOOD.                 
           MOVE F207-CONTRACT-NO         TO K110-CONTRACT-NO.                   
           MOVE F207-BLK-NO              TO K110-BLK-NO.                        
                                                                                
           START BM06K110 KEY NOT LESS THAN K110-AIX1.                          
           IF WS-K110-STATUS = ZEROES                                           
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'START-READ ERROR FOR BM06K110, STATUS: '                 
                       WS-K110-STATUS                                           
              MOVE WS-K110-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2200-READNXT-BM06K110.                                                   
      *-------------------------------------------------------------            
                                                                                
           READ BM06K110 NEXT RECORD AT END                                     
                MOVE 'Y'      TO WS-K110-EOF-SW                                 
                GO TO 2200-EXIT.                                                
                                                                                
           IF WS-K110-STATUS = 00 OR 02                                         
              PERFORM 3000-GET-K110-RECORD        THRU 3000-EXIT                
           ELSE                                                                 
              DISPLAY 'READNEXT ERROR FOR BM06K110, STATUS: '                   
                       WS-K110-STATUS                                           
              MOVE WS-K110-STATUS        TO RETURN-CODE                         
              PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-GET-K110-RECORD.                                                    
      *-------------------------------------------------------------            
                                                                                
           PERFORM 3100-CHECK-RECORD        THRU 3100-EXIT.                     
           IF C-CHANGE-RECORD                                                   
              GO TO 3000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF K110-DTE-ACCEPTANCE = '00000000' OR SPACES OR LOW-VALUES          
              PERFORM 3200-READ-PIDB        THRU 3200-EXIT                      
              PERFORM 3300-READ-BM06K100    THRU 3300-EXIT                      
              PERFORM 4000-PRINT-REPORT     THRU 4000-EXIT                      
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3100-CHECK-RECORD.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES           TO WS-CHANGE-RECORD-SW.                        
                                                                                
           IF F207-BLK-NO = SPACES OR LOW-VALUES                                
              IF F207-ESTATE-CDE = K110-ESTATE AND                              
                 F207-NEIGHBOURHOOD = K110-NEIGHBOURHOOD AND                    
                 F207-CONTRACT-NO = K110-CONTRACT-NO                            
                 NEXT SENTENCE                                                  
              ELSE                                                              
                 MOVE 'Y'        TO WS-CHANGE-RECORD-SW                         
              END-IF                                                            
           ELSE                                                                 
              IF F207-ESTATE-CDE = K110-ESTATE AND                              
                 F207-NEIGHBOURHOOD = K110-NEIGHBOURHOOD AND                    
                 F207-CONTRACT-NO = K110-CONTRACT-NO AND                        
                 F207-BLK-NO = K110-BLK-NO                                      
                 NEXT SENTENCE                                                  
              ELSE                                                              
                 MOVE 'Y'        TO WS-CHANGE-RECORD-SW                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3200-READ-PIDB.                                                          
      *-------------------------------------------------------------            
           MOVE SPACES        TO BP13COMM8-REC.                                 
           INITIALIZE            BP13COMM8-REC.                                 
                                                                                
           MOVE K110-SCH-ACC-NO    TO WS-LINK-REC.                              
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                 BP13COMM8-REC.                                 
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
              WHEN +000                                                         
                 CONTINUE                                                       
              WHEN +100                                                         
                 MOVE SPACES   TO BP13COMM8-REC                                 
                 INITIALIZE       BP13COMM8-REC                                 
                 DISPLAY 'REC NOT FOUND IN PIDB: ' K110-SCH-ACC-NO              
              WHEN OTHER                                                        
                 MOVE COMM8-CDE-SYSERR      TO WS-RETCODE                       
                 DISPLAY 'ERROR IN CALLING BP13C913, RETCODE: '                 
                         WS-RETCODE                                             
                 MOVE 99                    TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT                 
           END-EVALUATE.                                                        
                                                                                
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3300-READ-BM06K100.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES   TO K100-REC.                                           
           INITIALIZE       K100-REC.                                           
           MOVE K110-AIX1   TO K100-KEY-FLD.                                    
                                                                                
           READ BM06K100.                                                       
           EVALUATE WS-K100-STATUS                                              
           WHEN 00                                                              
                CONTINUE                                                        
           WHEN 23                                                              
                DISPLAY 'BM06K100 RECORD NOT FOUND : ' K100-KEY-FLD             
           WHEN OTHER                                                           
                DISPLAY 'BM06K100 READ ERROR : ' WS-K100-STATUS                 
                MOVE WS-K100-STATUS       TO RETURN-CODE                        
                PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-PRINT-REPORT.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF (F207-BLK-NO NOT = SPACES AND LOW-VALUES) AND                     
              F207-BLK-NO NOT = K110-BLK-NO                                     
              GO TO 4000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF (F207-FLAT-TYPE NOT = SPACES AND LOW-VALUES) AND                  
              F207-FLAT-TYPE NOT = COMM8-CDE-TYP-ACTLUSE                        
              GO TO 4000-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE SPACES       TO L203-DETAIL-LINE.                               
                                                                                
           IF WS-LINE-CTR > 56 OR                                               
              WS-LINE-CTR = ZEROES OR                                           
              C-NEW-REPORT                                                      
              PERFORM 4100-PAGE-BREAK       THRU 4100-EXIT                      
           END-IF.                                                              
                                                                                
           ADD  1      TO WS-SNO-CTR.                                           
           MOVE WS-SNO-CTR               TO L203-SNO.                           
           MOVE F207-NT-CODE             TO L203-NEW-TOWN.                      
           MOVE K110-ALLOCN-TAG          TO L203-ALLOCN-TAG.                    
           STRING F207-NEIGHBOURHOOD ' '                                        
                  F207-CONTRACT-NO                                              
                  DELIMITED BY SIZE    INTO L203-NEIGH-CONT                     
           END-STRING.                                                          
           STRING 'BLK ' K110-BLK-NO                                            
                  ' #' COMM8-NUM-LEVEL                                          
                  '-' COMM8-NUM-UNIT-MAIN                                       
                  DELIMITED BY SIZE    INTO L203-ADDRESS                        
           END-STRING.                                                          
           MOVE K110-FLAT-STATUS         TO L203-FLAT-STATUS.                   
           MOVE COMM8-CDE-TYP-ACTLUSE    TO L203-ROOM-TYPE.                     
           MOVE K110-NUM-SELECTION       TO L203-SEL-TYPE.                      
                                                                                
      **-- SELLING PRICE                                                        
           MOVE K110-PREV-SELLING-PRICE   TO L203-PREV-SP.                      
           MOVE K110-CURR-SELLING-PRICE   TO L203-CURR-SP.                      
           MOVE K110-1ST-OFFER-SELLING-PRICE TO L203-1ST-OFFER-SP.              
                                                                                
      **-- DATES                                                                
           IF K100-DTE-1ST-SALE = SPACES OR LOW-VALUES                          
              MOVE SPACES                TO L203-DTE-1ST-SALE                   
           ELSE                                                                 
              STRING K100-DTE-1ST-SALE (7:2) '/'                                
                     K100-DTE-1ST-SALE (5:2) '/'                                
                     K100-DTE-1ST-SALE (1:4)                                    
                     DELIMITED BY SIZE INTO L203-DTE-1ST-SALE                   
              END-STRING                                                        
           END-IF.                                                              
                                                                                
           IF K110-DTE-ALLOCN = SPACES OR LOW-VALUES                            
              MOVE SPACES                TO L203-DTE-ALLOCN                     
           ELSE                                                                 
              STRING K110-DTE-ALLOCN (7:2) '/'                                  
                     K110-DTE-ALLOCN (5:2) '/'                                  
                     K110-DTE-ALLOCN (1:4)                                      
                     DELIMITED BY SIZE INTO L203-DTE-ALLOCN                     
              END-STRING                                                        
           END-IF.                                                              
                                                                                
           IF K110-DTE-REPRICE = SPACES OR LOW-VALUES                           
              MOVE SPACES                TO L203-DTE-REPRICE                    
           ELSE                                                                 
              STRING K110-DTE-REPRICE (7:2) '/'                                 
                     K110-DTE-REPRICE (5:2) '/'                                 
                     K110-DTE-REPRICE (1:4)                                     
                     DELIMITED BY SIZE INTO L203-DTE-REPRICE                    
              END-STRING                                                        
           END-IF.                                                              
                                                                                
           IF K110-NUM-ALLOCN-QTR = SPACES OR LOW-VALUES                        
              MOVE SPACES                TO L203-ALLOCN-QTR                     
           ELSE                                                                 
              STRING K110-NUM-ALLOCN-QTR (1:4) '/'                              
                     K110-NUM-ALLOCN-QTR (5:2)                                  
                     DELIMITED BY SIZE INTO L203-ALLOCN-QTR                     
              END-STRING                                                        
           END-IF.                                                              
                                                                                
      **-- TAGGING                                                              
           IF (K110-PREV-SELLING-PRICE NOT =                                    
               K110-1ST-OFFER-SELLING-PRICE) OR                                 
              (K110-CURR-SELLING-PRICE NOT =                                    
               K110-1ST-OFFER-SELLING-PRICE) OR                                 
              ((K110-PREV-SELLING-PRICE =                                       
                K110-1ST-OFFER-SELLING-PRICE)  AND                              
               (K110-DTE-REPRICE NOT = '00000000' AND SPACES AND                
                                        LOW-VALUES))                            
              MOVE '*'     TO L203-TAG                                          
           END-IF.                                                              
                                                                                
           WRITE BP13L203-REC      FROM L203-DETAIL-LINE.                       
           ADD 1          TO WS-LINE-CTR.                                       
                                                                                
      **-- STREET NAME (NEXT LINE)                                              
           IF WS-LINE-CTR > 56 OR                                               
              WS-LINE-CTR = ZEROES                                              
              PERFORM 4100-PAGE-BREAK       THRU 4100-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE SPACES               TO L203-DETAIL-LINE.                       
           MOVE COMM8-NME-STREET     TO L203-ADDRESS.                           
           WRITE BP13L203-REC      FROM L203-DETAIL-LINE.                       
           ADD 1          TO WS-LINE-CTR.                                       
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4100-PAGE-BREAK.                                                         
      *-------------------------------------------------------------            
                                                                                
           IF C-NEW-REPORT                                                      
              MOVE SPACES     TO WS-NEW-REPORT-SW                               
              MOVE ZEROES     TO WS-PAGE-CTR                                    
                                 WS-LINE-CTR                                    
                                 WS-SNO-CTR                                     
           END-IF.                                                              
                                                                                
           ADD   1   TO WS-PAGE-CTR.                                            
           MOVE WS-PAGE-CTR       TO L203-PAGE.                                 
           MOVE WS-DISP-DATE      TO L203-DATE.                                 
           MOVE F207-ESTATE-NAME  TO L203-ESTATE-NAME.                          
           WRITE BP13L203-REC   FROM L203-HEAD-01 AFTER PAGE.                   
           WRITE BP13L203-REC   FROM L203-HEAD-02.                              
           WRITE BP13L203-REC   FROM L203-LINE    AFTER 2.                      
           WRITE BP13L203-REC   FROM L203-HEAD-03.                              
           WRITE BP13L203-REC   FROM L203-LINE.                                 
           MOVE 6                 TO WS-LINE-CTR.                               
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           CLOSE BP13F207                                                       
                 BM06K110                                                       
                 BM06K100                                                       
                 BP13L203.                                                      
                                                                                
           IF WS-K110-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BM06K110, STAT ' WS-K110-STATUS            
              MOVE     WS-K110-STATUS   TO RETURN-CODE.                         
                                                                                
           IF WS-K100-STATUS   NOT = 00 AND 97                                  
              DISPLAY 'CLOSING ERROR BM06K100, STAT ' WS-K100-STATUS            
              MOVE     WS-K100-STATUS   TO RETURN-CODE.                         
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
