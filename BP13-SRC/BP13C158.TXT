       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C158.                                                 
      *AUTHOR.        ED.                                                       
      *DATE-WRITTEN.  07OCT2002.                                                
      ***************************************************************           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      ***************************************************************           
      * OBJECTIVE : SPOUSE NAME FROM SERS OCCUP FILE WHICH IS UPDATE*           
      *             BY SPU. USING SPOUSE NRIC TO MATCH WITH SPOUSE  *           
      *             NRIC OF SOC OCCUP FILE, UPDATE SPOUSE NAME.     *           
      * ----------------------------------------------------------- *           
      * CHG REF  DATE       BY   DESCRIPTION                        *           
      * -------- ---------- ---- -----------                        *           
      * BP132222 07OCT2002  ED   NEW PGM                            *           
      ***************************************************************           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.    IBM-3090.                                            
       OBJECT-COMPUTER.    IBM-3090.                                            
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F825  ASSIGN       TO BP13F825.                           
           SELECT BP17F210  ASSIGN       TO BP17F210.                           
                                                                                
           SELECT BP13K820  ASSIGN       TO BP13K820                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K820-KEY-FLD                        
                            FILE STATUS  IS WS-K820-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F825                RECORD CONTAINS 200 CHARACTERS              
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BP13F825.                                                           
                                                                                
       FD   BP17F210                RECORD CONTAINS 400 CHARACTERS              
                                    LABEL RECORDS ARE STANDARD                  
                                    RECORDING MODE IS F.                        
       COPY BP17F210.                                                           
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FLAG-N-STATUS.                                                    
           05  WS-FOUND                     PIC X     VALUE 'N'.                
           05  WS-K820-STATUS               PIC 99    VALUE ZEROS.              
           05  WS-F210-EOF-FLAG             PIC X(01)  VALUE 'N'.               
               88  WS-F210-EOF              VALUE 'Y'.                          
           05  WS-F825-EOF-FLAG             PIC X(01)  VALUE 'N'.               
               88  WS-F825-EOF              VALUE 'Y'.                          
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F825-READ                 PIC 9(05)  VALUE ZEROS.             
           05  WS-F825-NMR                  PIC 9(05)  VALUE ZEROS.             
           05  WS-F210-READ                 PIC 9(05)  VALUE ZEROS.             
           05  WS-F210-NMR                  PIC 9(05)  VALUE ZEROS.             
           05  WS-K820-UPD                  PIC 9(05)  VALUE ZEROS.             
           05  WS-K820-READ                 PIC 9(05)  VALUE ZEROS.             
           05  WS-K820-NOT-FND              PIC 9(05)  VALUE ZEROS.             
           05  WS-F210-MR-F825              PIC 9(05)  VALUE ZEROS.             
           05  WS-SKIPPED                   PIC 9(05)  VALUE ZEROS.             
           05  WS-SPOUSE-IC-BLANK           PIC 9(05)  VALUE ZEROS.             
           05  WS-SPOUSE-IC-NMR             PIC 9(05)  VALUE ZEROS.             
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-YY                    PIC X(04)  VALUE SPACES.            
           05  WS-SYS-MM                    PIC X(02)  VALUE SPACES.            
           05  WS-SYS-DD                    PIC X(02)  VALUE SPACES.            
                                                                                
       PROCEDURE DIVISION.                                                      
      ***************************                                               
       0000-MAIN.                                                               
      ***************************                                               
                                                                                
           PERFORM 1000-OPEN-FILES    THRU  1000-EXIT.                          
           PERFORM 2000-READ-BP17F210 THRU  2000-EXIT.                          
           PERFORM 2010-READ-BP13F825 THRU  2010-EXIT.                          
           PERFORM 3000-PROCESS       THRU  3000-EXIT                           
             UNTIL F210-NUM-NRIC = HIGH-VALUES                                  
             AND   F825-NUM-NRIC = HIGH-VALUES.                                 
           PERFORM 9000-CLOSE-FILES   THRU  9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       1000-OPEN-FILES.                                                         
      ***************************                                               
           OPEN INPUT BP17F210                                                  
                      BP13F825                                                  
                  I-O BP13K820.                                                 
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR K820 STATUS IS ' WS-K820-STATUS            
              MOVE     WS-K820-STATUS TO RETURN-CODE                            
              PERFORM 9000-CLOSE-FILES.                                         
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)     TO  WS-SYSTEM-DATE.              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       2000-READ-BP17F210.                                                      
      ***************************                                               
           READ BP17F210                                                        
             AT END                                                             
                MOVE 'Y'                    TO  WS-F210-EOF-FLAG                
                MOVE HIGH-VALUES            TO  F210-NUM-NRIC                   
                GO TO 2000-EXIT                                                 
            NOT AT END                                                          
                ADD   1                     TO  WS-F210-READ                    
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       2010-READ-BP13F825.                                                      
      ***************************                                               
           READ BP13F825                                                        
             AT END                                                             
                MOVE 'Y'                    TO  WS-F825-EOF-FLAG                
                MOVE HIGH-VALUES            TO  F825-NUM-NRIC                   
                GO TO 2010-EXIT                                                 
            NOT AT END                                                          
                ADD  1                      TO  WS-F825-READ                    
           END-READ.                                                            
                                                                                
       2010-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       3000-PROCESS.                                                            
      ***************************                                               
           IF F210-NUM-NRIC = F825-NUM-NRIC                                     
              ADD 1 TO WS-F210-MR-F825                                          
              IF F210-NUM-SPOUSE-IC = SPACES OR LOW-VALUES                      
                 ADD 1 TO WS-SPOUSE-IC-BLANK                                    
              ELSE                                                              
                 IF F210-NUM-SPOUSE-IC = F825-NUM-NRIC-SPOUSE                   
                    PERFORM 7000-UPDATE-K820    THRU  7000-EXIT                 
                 ELSE                                                           
                    DISPLAY 'SPOUSE-IC IN F210 ' F210-NUM-SPOUSE-IC             
                     ' NOT EQUAL TO F825 ' F825-NUM-NRIC                        
                    ADD 1    TO WS-SPOUSE-IC-NMR                                
                 END-IF                                                         
              END-IF                                                            
              PERFORM 2010-READ-BP13F825     THRU  2010-EXIT                    
              PERFORM 2000-READ-BP17F210     THRU  2000-EXIT                    
           ELSE                                                                 
              IF F210-NUM-NRIC < F825-NUM-NRIC                                  
                 ADD 1 TO WS-F210-NMR                                           
                 PERFORM 2000-READ-BP17F210 THRU 2000-EXIT                      
              ELSE                                                              
                 ADD 1 TO WS-F825-NMR                                           
                 PERFORM 2010-READ-BP13F825 THRU 2010-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       7000-UPDATE-K820.                                                        
      ***************************                                               
           MOVE SPACES               TO    BP13K820-REC.                        
           MOVE F825-NUM-REGN        TO    K820-NUM-REGN.                       
           MOVE F825-NUM-NRIC        TO    K820-NUM-NRIC.                       
                                                                                
           READ BP13K820.                                                       
           EVALUATE WS-K820-STATUS                                              
           WHEN 00                                                              
                ADD 1 TO WS-K820-READ                                           
                IF K820-NUM-NRIC-SPOUSE = F825-NUM-NRIC-SPOUSE                  
                  MOVE F210-NME-SPOUSE         TO K820-NME-SPOUSE               
                  PERFORM 7100-REWRITE-K820  THRU 7100-EXIT                     
                ELSE                                                            
                  DISPLAY 'SPOUSE IC IN K820 ' K820-NUM-NRIC-SPOUSE             
                          ' NOT = F825 SPOUSE IC ' F825-NUM-NRIC                
                  ADD 1    TO WS-SKIPPED                                        
                END-IF                                                          
           WHEN 23                                                              
                ADD 1 TO WS-K820-NOT-FND                                        
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K820 - ' WS-K820-STATUS              
                MOVE     WS-K820-STATUS TO RETURN-CODE                          
                PERFORM 9000-CLOSE-FILES                                        
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       7100-REWRITE-K820.                                                       
      ***************************                                               
           REWRITE BP13K820-REC.                                                
           EVALUATE WS-K820-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
                ADD 1    TO WS-K820-UPD                                         
           WHEN OTHER                                                           
                DISPLAY 'ERROR REWRITING BP13K820 - ' WS-K820-STATUS            
                MOVE     WS-K820-STATUS TO RETURN-CODE                          
                PERFORM 9000-CLOSE-FILES                                        
           END-EVALUATE.                                                        
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       9000-CLOSE-FILES.                                                        
      ***************************                                               
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : ' WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-YY.         
           DISPLAY '========== BP13C158 STATISTICS =============='      '.      
           DISPLAY 'NO OF RECORDS READ IN BP13F825 : ' WS-F825-READ.            
           DISPLAY 'NO OF RECORDS READ IN BP17F210 : ' WS-F210-READ.            
           DISPLAY SPACES                                                       
           DISPLAY 'NO OF F210 MR F825 BY IC       : ' WS-F210-MR-F825.         
           DISPLAY 'NO OF BLANK SPOUSE IC          : '                          
                    WS-SPOUSE-IC-BLANK.                                         
           DISPLAY 'NO OF SPOUSE IC NMR IN F825    : ' WS-SPOUSE-IC-NMR.        
           DISPLAY 'NO OF RECORDS READ IN BP13K820 : ' WS-K820-READ.            
           DISPLAY 'NO OF RECORDS UPDATED IN  K820 : ' WS-K820-UPD.             
           DISPLAY 'NO OF RECORDS NOT IN BP13K820  : ' WS-K820-NOT-FND.         
           DISPLAY SPACES                                                       
           DISPLAY 'NO OF F825 NOT FOUND IN F210   : ' WS-F825-NMR.             
           DISPLAY 'NO OF F210 NOT FOUND IN F825   : ' WS-F210-NMR.             
           DISPLAY 'NO OF SPOUSE IC NMR IN K820    : ' WS-SKIPPED.              
           DISPLAY SPACES.                                                      
           DISPLAY '============================================='.             
                                                                                
           CLOSE BP13F825                                                       
                 BP17F210                                                       
                 BP13K820.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
      ********************** END OF PROGRAM ***************************         
