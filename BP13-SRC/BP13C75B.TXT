      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C75B.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  19 MAY 2016.                                              
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : E-APPLICATION IAMEND ALLOW PUBLIC TO UPDATE *           
      *                 BP13K757 VIA INTERNET. BATCH PROGRAM TO     *           
      *                 UPDATE DTE-UPDATE-SOC/IAMEND IN BP13K757 *              
      *   INPUT FILES :                                             *           
      *   1.  BP13F757                                              *           
      *                                                             *           
      *   I-O FILES                                                 *           
      *   1.  BP13K757                                              *           
      *                                                             *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP135497 19/05/2016 FNP1 NEW PROGRAM                        *           
      * BP139280 07/11/2023 EAA2 EXPAND BP13F757 FROM 3000 TO 4000  *           
      *                          EXPAND BP13K757 FROM 3000 TO 4000  *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F757   ASSIGN       TO BP13F757.                          
                                                                                
           SELECT BP13K757  ASSIGN        TO BP13K757                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K757-KEY-FLD                       
                            FILE STATUS   IS WS-K757-STATUS.                    
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F757                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS 4000 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F757.                                                           
                                                                                
       FD   BP13K757                                                            
            RECORD CONTAINS 4000 CHARACTERS.                                    
       COPY BP13K757.                                                           
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-F757-EOF                PIC X       VALUE SPACE.              
           05  WS-K757-STATUS             PIC 9(2)    VALUE ZEROES.             
                                                                                
       01  WS-COUNTERS.                                                         
           05  CNT-F757-READ              PIC 9(8)    VALUE ZEROES.             
           05  CNT-K757-FND               PIC 9(8)    VALUE ZEROES.             
           05  CNT-K757-UPDATED           PIC 9(8)    VALUE ZEROES.             
                                                                                
       01  WS-DATE                        PIC X(08)   VALUE SPACES.             
       01  WS-CURRENT-DATE.                                                     
           02  WS-CURRENT-DATE1.                                                
               05  WS-DTE-CCYY            PIC X(4).                             
               05  WS-DTE-MM              PIC X(2).                             
               05  WS-DTE-DD              PIC X(2).                             
           02  WS-CURRENT-TIME            PIC X(9).                             
           02  FILLER                     PIC X(4).                             
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-F757    THRU 2000-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-F757-EOF = 'Y'                                      
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13F757                                                 
                I-O    BP13K757.                                                
                                                                                
           IF WS-K757-STATUS   NOT =  00 AND 97                                 
              DISPLAY 'ERROR IN OPENING BP13K757 STATUS - '                     
                  WS-K757-STATUS                                                
              MOVE WS-K757-STATUS   TO RETURN-CODE                              
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F757.                                                          
      *---------------*                                                         
                                                                                
           READ BP13F757 AT END                                                 
                MOVE HIGH-VALUES TO F757-KEY-FLD                                
                MOVE 'Y' TO WS-F757-EOF                                         
                GO TO 2000-EXIT.                                                
           ADD 1 TO CNT-F757-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           IF F757-NUM-TAG-I-AMEND = 'P'                                        
              PERFORM 3500-READ-BP13K757  THRU 3500-EXIT                        
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F757         THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3500-READ-BP13K757.                                                      
      *-----------------*                                                       
           MOVE SPACES        TO K757-KEY-FLD.                                  
           MOVE F757-NUM-REGN TO K757-KEY-FLD.                                  
                                                                                
           READ BP13K757.                                                       
                                                                                
           EVALUATE WS-K757-STATUS                                              
             WHEN 00                                                            
                ADD 1 TO CNT-K757-FND                                           
                PERFORM 3600-REWRITE-BP13K757   THRU 3600-EXIT                  
             WHEN 23                                                            
                 DISPLAY 'RECORD NOT FOUND IN BP13K757, ' K757-KEY-FLD          
             WHEN OTHER                                                         
                 DISPLAY 'ERROR READING BP13K757 : ' WS-K757-STATUS             
                 MOVE WS-K757-STATUS      TO RETURN-CODE                        
                 PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3600-REWRITE-BP13K757.                                                   
      *-----------------*                                                       
           MOVE 'Y'           TO K757-NUM-TAG-I-AMEND.                          
           MOVE SPACES        TO K757-DTE-SOC-UPDATED.                          
           MOVE 'BP13C75B'    TO K757-NUM-USER-UPDATE.                          
                                                                                
           REWRITE BP13K757-REC.                                                
                                                                                
           EVALUATE WS-K757-STATUS                                              
              WHEN 00                                                           
                 ADD 1 TO CNT-K757-UPDATED                                      
              WHEN OTHER                                                        
                 DISPLAY 'ERROR WITH UPDATING BP13K757. STATUS IS : '           
                     WS-K757-STATUS                                             
                 MOVE WS-K757-STATUS    TO RETURN-CODE                          
                 PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13C75B *************'.                            
           DISPLAY 'NO OF RECS READ FROM  BP13F757 : ' CNT-F757-READ.           
           DISPLAY 'NO OF RECS FOUND IN   BP13K757 : ' CNT-K757-FND.            
           DISPLAY 'NO OF RECS UPDATED IN BP13K757 : ' CNT-K757-UPDATED.        
                                                                                
           CLOSE BP13F757                                                       
                 BP13K757.                                                      
                                                                                
           IF WS-K757-STATUS   NOT =  00 AND 97                                 
              DISPLAY 'ERROR IN OPENING BP13K757 STATUS - '                     
                  WS-K757-STATUS                                                
              MOVE WS-K757-STATUS   TO RETURN-CODE                              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
