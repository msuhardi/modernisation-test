       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CD13.                                                 
      *AUTHOR.        MSD1.                                                     
      *DATE-WRITTEN.  31/01/2008.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      * OBJECTIVES: UPLOAD DBSS RESULT TO SOC OCCUP (BP13K820)     *            
      *                                                                         
      *                                                            *            
      *  INPUT FILE :   BP13F820 -                                 *            
      *  OUTPUT FILE:   BP13K820 -                                 *            
      * ---------------------------------------------------------- *            
      * CHG REF   BY    DATE      DETAILS                          *            
      * -------   ====  ========= ================================ *            
      * BP133244  MSD1  20080131  NEW PGM                          *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F820 ASSIGN TO BP13F820.                                  
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS K820-STATUS.                               
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F820                                                            
            RECORD CONTAINS 400 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F820.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD    CONTAINS 400 CHARACTERS.                                   
       COPY BP13K820.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01 WS-COUNTERS.                                                          
          05 WS-F820-READ         PIC 9(08) VALUE ZEROES.                       
          05 WS-K820-WRITE        PIC 9(08) VALUE ZEROES.                       
       01 K820-STATUS             PIC 9(02) VALUE ZEROES.                       
      *01 WS-CUR-DATE             PIC X(08) VALUE SPACES.                       
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                          
           PERFORM 2000-READ-BP13F820  THRU 2000-EXIT.                          
           PERFORM 3000-PROCESS-DATA   THRU 3000-EXIT                           
              UNTIL K820-KEY-FLD = HIGH-VALUES.                                 
           PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT.                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F820                                                 
                I-O    BP13K820.                                                
                                                                                
           IF K820-STATUS NOT = ZEROS AND 97                                    
              DISPLAY ' ERROR OPENING BP13K820  FILE, STATUS '                  
                  K820-STATUS                                                   
              MOVE K820-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                        
                                                                                
      *    MOVE FUNCTION CURRENT-DATE      TO WS-CUR-DATE.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F820.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F820                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO K820-KEY-FLD                             
                NOT AT END                                                      
                   ADD 1    TO WS-F820-READ                                     
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-DATA.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF F820-KEY-FLD NOT = SPACES AND LOW-VALUES                          
                                                                                
           MOVE  BP13F820-REC    TO BP13K820-REC                                
           WRITE BP13K820-REC                                                   
                                                                                
           EVALUATE  K820-STATUS                                                
           WHEN 00                                                              
           WHEN 02                                                              
                ADD 1     TO WS-K820-WRITE                                      
           WHEN 22                                                              
                DISPLAY ' ERROR WRITING TO BP13K820, FOUND DUPLICATE'           
                        ' REC - ' K820-KEY-FLD                                  
           WHEN OTHER                                                           
              DISPLAY ' ERROR WRITING BP13K820, STATUS ' K820-STATUS            
              DISPLAY 'K820-NUM-REGN - ' K820-NUM-REGN                          
              MOVE K820-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-EVALUATE                                                         
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F820  THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY ' BP13CD13 CONTROL TOTAL '.                                  
           DISPLAY '------------------------------------------'.                
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECS READ  FROM BP13F820 : ' WS-F820-READ.            
           DISPLAY 'NO OF RECS WRITTEN TO BP13K820 : ' WS-K820-WRITE.           
                                                                                
           CLOSE BP13F820                                                       
                 BP13K820.                                                      
                                                                                
           IF K820-STATUS NOT = ZEROES                                          
              DISPLAY 'ERROR CLOSING BP13K820 : ' K820-STATUS                   
              MOVE K820-STATUS                 TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
