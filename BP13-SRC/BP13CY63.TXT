      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CY63.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  12/11/2009.                                               
      *****************************************************************         
      * OBJECTIVE -                                                   *         
      *   MATCH WITH BP13F205 TO EXTRACT HIST REC THAT MATCHED        *         
      *   THE LAUNCH DATE, CLOSE-DATE, MODE OF SALES.                 *         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13F205                                  *         
      *                  2. BP13F800                                  *         
      *                                                               *         
      *     OUTPUT FILE:                                              *         
      *                  1. P13F800A                                  *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * REF NO    DATE      BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------   --------  ---  ------------------------             *         
      * BP133792  12112009  PCL3 NEW PROGRAM.                         *         
      * BP133792  24122009  PCL3 ADD CODE FOR CHECKING F205-DTE-REQUEST         
      * BP134011  27102010  PCL3 ADD 'NS', 'SC' & 'SF' FOR ALLOC-TAG. *         
      * BP134504  06062012  LSB1 CATER FOR SBF IN JOB AY0C            *         
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F205  ASSIGN        TO BP13F205.                         
           SELECT  BP13F800  ASSIGN        TO BP13F800.                         
           SELECT  P13F800A  ASSIGN        TO P13F800A.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD   BP13F205                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 80 CHARACTERS            
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F800                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 2000 CHARACTERS          
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       COPY BP13F800.                                                           
                                                                                
       FD   P13F800A                   BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 2000 CHARACTERS          
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
       01  P13F800A-REC                PIC X(2000).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  WORK-FILE-VARIABLE.                                                  
           05  WS-EOF-BP13F800         PIC X     VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-BP13F800-READ        PIC 9(7)  VALUE ZEROES.                  
           05  WS-P13F800A-WRITTEN     PIC 9(7)  VALUE ZEROES.                  
           05  WS-CUTOFF               PIC 9(7)  VALUE ZEROES.                  
           05  WS-NO-BKAPPT            PIC 9(7)  VALUE ZEROES.                  
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F205      THRU 2000-EXIT.                      
           PERFORM 2100-READ-BP13F800      THRU 2100-EXIT                       
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
                   UNTIL WS-EOF-BP13F800   = 'Y'                                
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F205                                                 
                       BP13F800                                                 
                OUTPUT P13F800A.                                                
                                                                                
       1000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2000-READ-BP13F205.                                                      
      ******************************************************************        
                                                                                
           READ BP13F205           AT END                                       
                GO                 TO 9999-CLOSE-ROUTINE.                       
                                                                                
       2000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       2100-READ-BP13F800.                                                      
      ******************************************************************        
                                                                                
           READ BP13F800           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-BP13F800                         
                GO                 TO   2100-EXIT.                              
                                                                                
           ADD  1                  TO   WS-BP13F800-READ.                       
                                                                                
       2100-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
      ** CUTOFF-DATE IS KEEP F205-DTE-REQUEST                                   
      ** BK-APP > CUT-OFF-DATE ==> OMIT                                         
      ** BK-APP = SPACE, RQ/NC/NF/NE ==> INCLUDE                                
                                                                                
           IF (F800-DTE-BALLOT  >= F205-DTE-ALLOCN(1:6)) AND                    
              (F800-DTE-BALLOT  <= F205-DTE-END(1:6))                           
                                                                                
            IF F205-NUM-OPTION  = 'YC'                                          
               IF F205-NUM-SELECTION = 'BTO'                                    
                 IF F800-NUM-ALLO-CAT = 'BTO'                                   
                    PERFORM 3100-OUTPUT-FILE THRU 3100-EXIT                     
                 END-IF                                                         
               ELSE                                                             
                    PERFORM 3100-OUTPUT-FILE THRU 3100-EXIT                     
               END-IF                                                           
            ELSE                                                                
                                                                                
               IF F205-NUM-OPTION  = 'YI'                                       
                  PERFORM 3100-OUTPUT-FILE THRU 3100-EXIT                       
               ELSE                                                             
                  IF F800-NUM-ALLO-CAT = F205-NUM-SELECTION                     
                     PERFORM 3100-OUTPUT-FILE THRU 3100-EXIT                    
                                                                                
                  END-IF                                                        
               END-IF                                                           
            END-IF.                                                             
                                                                                
           PERFORM 2100-READ-BP13F800                   THRU 2100-EXIT.         
                                                                                
       3000-EXIT.  EXIT.                                                        
                                                                                
       3100-OUTPUT-FILE.                                                        
                                                                                
              IF F205-DTE-REQUEST = SPACES OR LOW-VALUES OR ZEROES              
                 PERFORM 4000-WRITE-P13F800A            THRU 4000-EXIT          
              ELSE                                                              
                 IF F800-DTE-BALLOT = F205-DTE-END(1:6)                         
                    PERFORM 3500-CHECK-CUTOFF-DATE      THRU 3500-EXIT          
                 ELSE                                                           
                    PERFORM 4000-WRITE-P13F800A         THRU 4000-EXIT          
                 END-IF                                                         
              END-IF.                                                           
                                                                                
       3100-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       3500-CHECK-CUTOFF-DATE.                                                  
      ******************************************************************        
                                                                                
           IF F800-DTE-BK-APPT = SPACES                                         
              IF F800-NUM-ALLOC-TAG  = 'RQ' OR 'NC' OR 'NF' OR                  
                                       'NE' OR 'NS' OR 'SC' OR 'SF'             
                 PERFORM 4000-WRITE-P13F800A      THRU 4000-EXIT                
              ELSE                                                              
                 MOVE '99999999'                    TO F800-DTE-BK-APPT         
                 PERFORM 4000-WRITE-P13F800A      THRU 4000-EXIT                
                 ADD 1                              TO WS-NO-BKAPPT             
              END-IF                                                            
           ELSE                                                                 
              IF F800-DTE-BK-APPT > F205-DTE-REQUEST                            
                 MOVE '99999999'                    TO F800-DTE-BK-APPT         
                 PERFORM 4000-WRITE-P13F800A      THRU 4000-EXIT                
                 ADD 1                              TO WS-CUTOFF                
              ELSE                                                              
                 PERFORM 4000-WRITE-P13F800A      THRU 4000-EXIT                
              END-IF                                                            
            END-IF.                                                             
                                                                                
       3500-EXIT.  EXIT.                                                        
                                                                                
      *********************                                                     
       4000-WRITE-P13F800A.                                                     
      *********************                                                     
                                                                                
           WRITE P13F800A-REC          FROM BP13F800-MASTER.                    
           ADD 1                         TO WS-P13F800A-WRITTEN.                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CY63'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13F800 RECORDS READ..........: '                    
                    WS-BP13F800-READ.                                           
           DISPLAY 'NO OF P13F800A RECORDS WRITTEN.......: '                    
                    WS-P13F800A-WRITTEN.                                        
           DISPLAY 'NO OF RECORD OMIT FROM CUTOFF DATE...: '                    
                    WS-CUTOFF.                                                  
           DISPLAY 'NO OF RECORD OMIT FROM BLANK BKAPPT..: '                    
                    WS-NO-BKAPPT.                                               
                                                                                
           CLOSE BP13F205                                                       
                 BP13F800                                                       
                 P13F800A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
