       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C164.                                                 
      *AUTHOR.        SHARON DUMDUM.                                            
      *DATE-WRITTEN.  06/12/2004.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *   OBJECTIVE   :  PROGRAM TO COMPARE BP13F310 AND           *            
      *                  BP13K757 FILES                            *            
      *                                                            *            
      *   INPUT FILES  :  1. BP13F310                              *            
      *                   2. BP13K757                              *            
      *                                                            *            
      *   OUTPUT FILES :  1. BP13F757                              *            
      *                                                            *            
      * CHG REF  DATE      OIC   REMARKS                           *            
      * -------- --------  ----  -----------------------------     *            
      * BP132626 20041206  SSD   NEW PROGRAM                       *            
      * BP133030 20070326  BA2   TO ENHANCE THE REFERRENCE FLD     *            
      * BP139280 30102023  EAA2  EXPAND BP13F757 FROM 3000 TO 4000 *            
      *                          EXPAND BP13K757 FROM 3000 TO 4000 *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F310  ASSIGN       TO BP13F310.                           
                                                                                
           SELECT BP13F757  ASSIGN       TO BP13F757.                           
                                                                                
           SELECT BP13K757  ASSIGN       TO BP13K757                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K757-KEY-FLD                        
                            FILE STATUS  IS WS-K757-STATUS.                     
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F310           BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 150 CHARACTERS                   
                               LABEL RECORD IS STANDARD                         
                               RECORDING MODE  IS  F.                           
       COPY BP13F310.                                                           
                                                                                
       FD   BP13F757           BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 4000 CHARACTERS                  
                               LABEL RECORD IS STANDARD                         
                               RECORDING MODE  IS  F.                           
       COPY BP13F757.                                                           
                                                                                
       FD   BP13K757           RECORD CONTAINS 4000 CHARACTERS.                 
       COPY BP13K757.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K757-STATUS           PIC 9(2)   VALUE 00.                    
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F310-READ             PIC 9(4)   VALUE ZEROES.                
           05  WS-K757-READ             PIC 9(4)   VALUE ZEROES.                
           05  WS-K757-NOTFND           PIC 9(4)   VALUE ZEROES.                
           05  WS-F757-REC-WRT          PIC 9(4)   VALUE ZEROES.                
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05  WS-F310-EOF              PIC X      VALUE 'N'.                   
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 1500-F310-READ    THRU 1500-EXIT.                            
           PERFORM 2000-MAIN-ROUTINE THRU 2000-EXIT                             
                       UNTIL WS-F310-EOF = 'Y'.                                 
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F310                                                 
                       BP13K757                                                 
                OUTPUT BP13F757.                                                
                                                                                
           IF WS-K757-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K757 ERROR ' WS-K757-STATUS                  
              MOVE     WS-K757-STATUS TO RETURN-CODE                            
              PERFORM  9000-CLOSE-FILES THRU 9000-EXIT.                         
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1500-F310-READ.                                                          
      *-------------------------------------------------------------            
           READ BP13F310 AT END MOVE 'Y'  TO WS-F310-EOF                        
                            GO TO 1500-EXIT.                                    
                                                                                
           ADD  1                         TO WS-F310-READ.                      
                                                                                
       1500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE F310-REGN-NO               TO K757-NUM-REGN.                    
           READ BP13K757.                                                       
           EVALUATE WS-K757-STATUS                                              
           WHEN 00                                                              
                ADD 1                      TO WS-K757-READ                      
                MOVE SPACES                TO BP13F757-REC                      
                INITIALIZE                    BP13F757-REC                      
                MOVE BP13K757-REC          TO BP13F757-REC                      
                WRITE BP13F757-REC                                              
                ADD 1 TO WS-F757-REC-WRT                                        
           WHEN 23                                                              
                ADD 1                      TO WS-K757-NOTFND                    
                DISPLAY "K757 REC NOT FND, STATUS:" WS-K757-STATUS              
                DISPLAY "K757-KEY-FLD  = "  F310-REGN-NO                        
           WHEN OTHER                                                           
                MOVE WS-K757-STATUS        TO RETURN-CODE                       
                DISPLAY "BP13K757 READ FILE ERROR:" WS-K757-STATUS              
                PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
           PERFORM 1500-F310-READ         THRU 1500-EXIT.                       
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13C164 CONTROL '.                                         
           DISPLAY '-----------------------------------'.                       
           DISPLAY 'TOTAL F310 REC READ      = ' WS-F310-READ.                  
           DISPLAY 'TOTAL K757 REC READ      = ' WS-K757-READ.                  
           DISPLAY 'TOTAL F757 REC WRITE     = ' WS-F757-REC-WRT.               
           DISPLAY 'TOTAL K757 REC RECNFND   = ' WS-K757-NOTFND.                
                                                                                
           CLOSE      BP13F310                                                  
                      BP13F757                                                  
                      BP13K757.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
