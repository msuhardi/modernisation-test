       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C758.                                                  
       AUTHOR.       AM25.                                                      
       DATE-WRITTEN. 27/11/2019.                                                
      *****************************************************************         
      *                SOC - SYSTEM OF COMMITMENT (BP13)              *         
      *===============================================================*         
      *  OBJECTIVES : TO ADJUST MAX QUOTA BASED ON TOTAL SUPPLY FOR PA*         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                                                               *         
      *                ACCESS     REC                                 *         
      * DD NAME  I/O/E MODE       LEN  KEY FIELDS    REMARKS          *         
      * -------- ----- ---------- ---- ------------- ---------------- *         
      * BP13K010 I/O   SEQUENTIAL 600  K010-KEY-FLD                   *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      *                                                               *         
      * CHG-REQ#  DD/MM/YY   BY   REMARKS                             *         
      * --------  --------  ----  ----------------------------------- *         
      * BP138098  27/11/19  AM25  NEW PROGRAM                         *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K010 ASSIGN      TO BP13K010                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS SEQUENTIAL                                       
              RECORD KEY    IS K010-KEY-FLD                                     
              FILE STATUS   IS WS-K010-STATUS.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13K010                                                             
           RECORD CONTAINS 600 CHARACTERS.                                      
       COPY BP13K010.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K010-STATUS          PIC 9(2)        VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-K010-READ            PIC 9(5)        VALUE ZEROES.            
           05  WS-K010-UPDATE          PIC 9(5)        VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES-FLAGS.                                                   
           05  WS-K010-EOF             PIC X           VALUE 'N'.               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13K010    THRU 2000-EXIT                         
              UNTIL WS-K010-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN I-O    BP13K010.                                                
                                                                                
           IF (WS-K010-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K010 FAIL STATUS=' WS-K010-STATUS               
              MOVE WS-K010-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13K010.                                                      
      *****************************************************************         
                                                                                
           READ BP13K010.                                                       
                                                                                
           EVALUATE WS-K010-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K010-READ                          
                                                                                
                 IF (K010-KEY-FLD NOT = SPACES AND LOW-VALUES)                  
                    PERFORM 3000-MAIN-PROCESSING THRU 3000-EXIT                 
                    PERFORM 4000-UPDATE-BP13K010 THRU 4000-EXIT                 
                 END-IF                                                         
              WHEN 10                                                           
                 MOVE 'Y'              TO WS-K010-EOF                           
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K010 FAIL STATUS=' WS-K010-STATUS            
                 MOVE WS-K010-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-MAIN-PROCESSING.                                                    
      *****************************************************************         
                                                                                
           IF K010-NUM-TOT-PA NOT NUMERIC                                       
              MOVE ZEROES              TO K010-NUM-TOT-PA                       
           END-IF.                                                              
           IF K010-NUM-MAX-PA1 NOT NUMERIC                                      
              MOVE ZEROES              TO K010-NUM-MAX-PA1                      
           END-IF.                                                              
           IF K010-NUM-MAX-PA2 NOT NUMERIC                                      
              MOVE ZEROES              TO K010-NUM-MAX-PA2                      
           END-IF.                                                              
           IF K010-NUM-MAX-PA3 NOT NUMERIC                                      
              MOVE ZEROES              TO K010-NUM-MAX-PA3                      
           END-IF.                                                              
                                                                                
           IF K010-NUM-MAX-PA1 > 0                                              
              MOVE K010-NUM-TOT-PA     TO K010-NUM-MAX-PA1                      
           END-IF.                                                              
           IF K010-NUM-MAX-PA2 > 0                                              
              MOVE K010-NUM-TOT-PA     TO K010-NUM-MAX-PA2                      
           END-IF.                                                              
           IF K010-NUM-MAX-PA3 > 0                                              
              MOVE K010-NUM-TOT-PA     TO K010-NUM-MAX-PA3                      
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4000-UPDATE-BP13K010.                                                    
      *****************************************************************         
                                                                                
           REWRITE BP13K010-TENDER.                                             
                                                                                
           IF WS-K010-STATUS = ZEROES                                           
              ADD 1                    TO WS-K010-UPDATE                        
           ELSE                                                                 
              DISPLAY 'REWRITE BP13K010 FAIL STATUS=' WS-K010-STATUS            
              MOVE WS-K010-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BP13K010.                                                      
                                                                                
           IF WS-K010-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K010 FAIL STATUS=' WS-K010-STATUS              
              MOVE WS-K010-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           DISPLAY '*****************************'.                             
           DISPLAY '* PROGRAM BP13C758          *'.                             
           DISPLAY '* CONTROL COUNTS            *'.                             
           DISPLAY '*---------------------------*'.                             
           DISPLAY '* REC READ K010     : ' WS-K010-READ ' *'.                  
           DISPLAY '* REC UPDATE K010   : ' WS-K010-UPDATE ' *'.                
           DISPLAY '*****************************'.                             
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
