       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C522.                                                 
       AUTHOR.        LMS.                                                      
       DATE-WRITTEN.  13/11/2008.                                               
      * ========================================================= *             
      *             CREATE APPOINTMENT SCHEDULES IN K225          *             
      * ========================================================= *             
      *  PURPOSE OF THIS PROGRAM: CREATE APPOINTMENT SCHEDULES    *             
      *                           IN BP13K225 FROM BOOKING APPMT  *             
      *                           DETAILS IN BP13K223             *             
      *                                                           *             
      *  INPUT        :  BP13K223                                 *             
      *  OUTPUT       :  BP13K225                                 *             
      *                                                           *             
      * ----------------------------------------------------------*             
      * CHG REF  DATE     BY   DESCRIPTION                        *             
      * -------- -------- --- ------------------------------------*             
      * BP133403 13/11/08 LMS1 NEW PROGRAM                        *             
      * ==========================================================*             
                                                                                
       ENVIRONMENT    DIVISION.                                                 
       CONFIGURATION  SECTION.                                                  
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BP13K223  ASSIGN        TO BP13K223                           
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K223-KEY-FLD                       
                            ACCESS MODE   IS DYNAMIC                            
                            FILE STATUS   IS K223-FILE-STATUS.                  
                                                                                
           SELECT BP13K225  ASSIGN        TO BP13K225                           
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K225-KEY-FLD                       
                            ACCESS MODE   IS RANDOM                             
                            FILE STATUS   IS K225-FILE-STATUS.                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13K223                                                            
            RECORD CONTAINS 80 CHARACTERS.                                      
       COPY BP13K223.                                                           
                                                                                
       FD   BP13K225                                                            
            RECORD CONTAINS 150 CHARACTERS.                                     
       COPY BP13K225.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  K223-FILE-STATUS               PIC 9(2) VALUE 0.                     
       01  K225-FILE-STATUS               PIC 9(2) VALUE 0.                     
                                                                                
       01  WS-SAVE-DATE                   PIC X(8) VALUE SPACES.                
       01  WS-NUM-FIRST-BATCH             PIC 9(6) VALUE ZEROES.                
       01  WS-PREFIX                      PIC X    VALUE SPACES.                
       01  WS-NEXT-BATCH                  PIC X    VALUE 'N'.                   
       01  WS-CTR                         PIC 9(2) VALUE ZEROES.                
                                                                                
       01  WS-SAVE-SALES-MODE             PIC X(3) VALUE SPACES.                
       01  WS-SAVE-SALES-MODE-CNT         PIC 9(5) VALUE ZEROES.                
                                                                                
       01  WS-QUEUE-NUM REDEFINES WS-SAVE-SALES-MODE-CNT.                       
           05 FILLER                      PIC X.                                
           05 WS-QUEUE-NUM-DIGITS         PIC X(4).                             
                                                                                
       01  WS-K223-NUM-CASE-ALPHA         PIC X(2).                             
       01  WS-K223-NUM-CASE REDEFINES WS-K223-NUM-CASE-ALPHA                    
                                          PIC 9(2).                             
                                                                                
       01  WS-K223-READ-CNT               PIC 9(8) VALUE ZEROES.                
       01  WS-K225-WRITE-CNT              PIC 9(8) VALUE ZEROES.                
       01  WS-K225-WRITE-ERR              PIC 9(8) VALUE ZEROES.                
                                                                                
       01  WS-DTE-CURRENT.                                                      
           05  WS-DTE-CCYY                PIC 9(4) VALUE ZEROES.                
           05  WS-DTE-MM                  PIC 9(2) VALUE ZEROES.                
           05  WS-DTE-DD                  PIC 9(2) VALUE ZEROES.                
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-K223-EOF-FLAG           PIC X VALUE 'N'.                      
               88 WS-K223-EOF-YES               VALUE 'Y'.                      
               88 WS-K223-EOF-NO                VALUE 'N'.                      
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 1000-PRE-PROCESS         THRU 1000-EXIT.                     
           PERFORM 2000-PROCESS-MASTER      THRU 2000-EXIT.                     
           PERFORM 9000-CLOSE-ROUTINE       THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       1000-PRE-PROCESS.                                                        
      *-------------------------------------------------------------            
           OPEN INPUT  BP13K223                                                 
                I-O    BP13K225.                                                
                                                                                
           IF K223-FILE-STATUS NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING BP13K223 : ' K223-FILE-STATUS              
              MOVE     K223-FILE-STATUS    TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
           IF K225-FILE-STATUS NOT = 00 AND 97                                  
              DISPLAY 'ERROR OPENING BP13K225 : ' K225-FILE-STATUS              
              MOVE     K225-FILE-STATUS    TO RETURN-CODE                       
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-PROCESS-MASTER.                                                     
      *-------------------------------------------------------------            
                                                                                
           SET WS-K223-EOF-NO        TO TRUE.                                   
                                                                                
           MOVE SPACES               TO K223-KEY-FLD.                           
                                                                                
           START BP13K223 KEY IS GREATER OR EQUAL K223-KEY-FLD                  
              END-START.                                                        
                                                                                
           EVALUATE K223-FILE-STATUS                                            
                                                                                
           WHEN 00                                                              
              PERFORM 2100-PROCESS-K223 THRU 2100-EXIT                          
                UNTIL WS-K223-EOF-YES                                           
                                                                                
           WHEN 10                                                              
              DISPLAY 'K223 END OF FILE'                                        
                                                                                
           WHEN 23                                                              
              DISPLAY 'K223 KEY NOT FOUND IN START OF K223: '                   
                          K223-KEY-FLD                                          
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'ERROR STARTING BP13K223'                                 
              DISPLAY 'K223-FILE-STATUS: ' K223-FILE-STATUS                     
                                                                                
           END-EVALUATE.                                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2100-PROCESS-K223.                                                       
      *-------------------------------------------------------------            
                                                                                
           READ BP13K223 NEXT RECORD                                            
             AT END                                                             
                SET WS-K223-EOF-YES  TO   TRUE                                  
                GO TO 2100-EXIT.                                                
                                                                                
           IF K223-NUM-SALES-MODE = SPACES OR LOW-VALUES                        
              GO TO 2100-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE K223-NUM-CASES TO WS-K223-NUM-CASE-ALPHA.                       
                                                                                
           PERFORM 3000-CREATE-K225 VARYING WS-CTR FROM 1 BY 1                  
             UNTIL WS-CTR > WS-K223-NUM-CASE.                                   
                                                                                
           ADD 1 TO WS-K223-READ-CNT.                                           
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-CREATE-K225.                                                        
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES TO BP13K225-REC.                                         
           INITIALIZE     BP13K225-REC.                                         
                                                                                
           ADD 1 TO WS-SAVE-SALES-MODE-CNT.                                     
                                                                                
           EVALUATE K223-NUM-SALES-MODE                                         
               WHEN 'DBS'                                                       
                  MOVE ' ' TO WS-PREFIX                                         
           END-EVALUATE.                                                        
                                                                                
           STRING WS-PREFIX WS-QUEUE-NUM-DIGITS                                 
              DELIMITED BY SIZE INTO K225-NUM-QUEUE.                            
                                                                                
           STRING '##' K225-NUM-QUEUE                                           
              DELIMITED BY SIZE     INTO K225-NUM-NRIC.                         
                                                                                
           MOVE K223-NUM-SALES-MODE   TO K225-NUM-SALES-MODE.                   
           MOVE K223-DTE-APPOINTMENT  TO K225-DTE-APPOINTMENT                   
                                         WS-SAVE-DATE.                          
           MOVE K223-TME-APPOINTMENT  TO K225-TME-APPOINTMENT.                  
                                                                                
           MOVE 'BP13C522'            TO K225-NUM-USERID.                       
           MOVE FUNCTION CURRENT-DATE TO K225-DTE-UPDATE.                       
           ACCEPT K225-TME-UPDATE   FROM TIME.                                  
                                                                                
           WRITE BP13K225-REC.                                                  
                                                                                
           IF K225-FILE-STATUS EQUAL 00 OR 02                                   
              ADD 1 TO WS-K225-WRITE-CNT                                        
           ELSE                                                                 
              DISPLAY 'ERROR WRITING K225 - ' K225-FILE-STATUS                  
                      ' APPMT NOT WRITTEN: ' K225-KEY-FLD K225-ALT-KEY          
              ADD 1 TO WS-K225-WRITE-ERR                                        
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
                                                                                
           DISPLAY '******  BP13C522  *************'.                           
           DISPLAY 'RECS READ   FROM BP13K223   : ' WS-K223-READ-CNT.           
           DISPLAY '                              '.                            
           DISPLAY 'RECS WRITTEN  IN BP13K225   : ' WS-K225-WRITE-CNT.          
           DISPLAY 'RECS ERROR WRITE BP13K225   : ' WS-K225-WRITE-ERR.          
                                                                                
           CLOSE   BP13K223                                                     
                   BP13K225.                                                    
                                                                                
           IF K223-FILE-STATUS NOT = 00                                         
              DISPLAY 'ERROR CLOSING BP13K223 : ' K223-FILE-STATUS              
              MOVE     K223-FILE-STATUS    TO RETURN-CODE.                      
                                                                                
           IF K225-FILE-STATUS NOT = 00                                         
              DISPLAY 'ERROR CLOSING BP13K225 : ' K225-FILE-STATUS              
              MOVE     K225-FILE-STATUS    TO RETURN-CODE.                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
