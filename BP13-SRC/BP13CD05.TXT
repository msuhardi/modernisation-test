      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13CD05.                                                 
       AUTHOR.        TEREZA SALONGA.                                           
      *-----------------------------------------------------------*             
                                                                                
      *===========================================================*             
      *        SOC - S Y S T E M  O F  C O M M I T M E N T        *             
      *===========================================================*             
      *  OBJECTIVES :                                             *             
      *                                                           *             
      *    1.  TO TRANSFER DATA FROM BP13FB20 TO BP13KB25         *             
      *                                                           *             
      *    INPUT FILES : BP13FB20 - BALLOT FILE FOR MATURE NT     *             
      *                                                           *             
      *    INPUT-OUTPUT: BP13KB25 - DBS REQUEST FILE              *             
      *                                                           *             
      *    SUBROUTINES :                                          *             
      *                                                           *             
      *---------------------------------------------------------- *             
      *===========================================================*             
      *  REVISION HISTORY :                                       *             
      *  CHG-REQ#  BY    DATE        DESCRIPTION                  *             
      *  --------  ----  ----------  ---------------------------- *             
      *  BP132875  TRS   05/09/06    NEW PROGRAM                  *             
      *  BP133333  CT2   27/05/08    MOVE FB20 DATA TO REGN4 AND  *             
      *                              DBSS-ELIG2 DUE TO ALT-KEY3 IN*             
      *                              BP13KB25                     *             
      *  BP133518  PCL3  22/04/09    MOVE ONLY (1-3171) RECORDS OF*             
      *                              BP13FB20 TO BP13KB25-KB20-REC*             
      *                              WHEN UPDATING/WRITING THE    *             
      *                              FILE.                        *             
      *  BP182120  ZDD1  15/12/10    CHANGE FB20-DTE-UPDATE TO    *             
      *                              FB20-DTE-ACCEPT WHEN MOVING  *             
      *                              VALUE TO KB25-DTE-BOOKING    *             
      *  BP134215  JB8   28/06/11    MOVE DTE-ACCEPT / BALLOT     *             
      *                                   PAYMENT-SCHEME          *             
      *  BP134396  JB8   28/11/11    TO CATER FOR ALT-KEY3        *             
      *  BP134272  EFC   28/06/2012  UPD KB25-DTE-8WK-FROM-OPTION *             
      *************************************************************             
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FB20 ASSIGN TO BP13FB20.                                  
                                                                                
           SELECT BP13KB25 ASSIGN TO BP13KB25                                   
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS KB25-KEY-FLD                         
                           ALTERNATE RECORD KEY IS KB25-ALT-KEY2                
                           FILE STATUS  IS KB25-FILE-STATUS.                    
                                                                                
           SELECT BP13K022 ASSIGN TO BP13K022                                   
                           ACCESS MODE     IS DYNAMIC                           
                           ORGANIZATION    IS INDEXED                           
                           RECORD KEY      IS K022-KEY-FLD                      
                           ALTERNATE KEY   IS K022-NUM-REGN                     
                           FILE STATUS     IS K022-FILE-STATUS.                 
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
                                                                                
       FD  BP13FB20    BLOCK  CONTAINS 0 RECORDS                                
                       RECORD CONTAINS 3300 CHARACTERS                          
                       LABEL RECORD IS STANDARD                                 
                       RECORDING MODE IS F.                                     
       COPY BP13FB20.                                                           
                                                                                
       FD   BP13KB25                                                            
            RECORD     CONTAINS 3400 CHARACTERS.                                
       COPY BP13KB25.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
       01  WS-8WK-VARIABLES.                                                    
           05  WS-ACCEPT-DTE              PIC 9(8) VALUE ZEROES.                
           05  WS-8WK-DTE                 PIC 9(8) VALUE ZEROES.                
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  K825-FILE-STATUS           PIC 9(2)    VALUE  0.                 
           05  K830-FILE-STATUS           PIC 9(2)    VALUE  0.                 
           05  KB25-FILE-STATUS           PIC 9(2)    VALUE  0.                 
           05  K022-FILE-STATUS           PIC 9(2)    VALUE  0.                 
           05  KB78-FILE-STATUS           PIC 9(2)    VALUE  0.                 
           05  WS-FB20-EOF-SW             PIC X(1)    VALUE 'N'.                
           05  WS-END-OF-K825             PIC X(1)    VALUE 'N'.                
           05  WS-FOUND                   PIC X(1)    VALUE 'N'.                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FB20-READ-CTR           PIC 9(4)    VALUE  0.                 
           05  WS-FB20-SKIP-CTR           PIC 9(4)    VALUE  0.                 
           05  WS-KB25-WRITE-CTR          PIC 9(4)    VALUE  0.                 
           05  WS-KB25-DUP-CTR            PIC 9(4)    VALUE  0.                 
           05  WS-KB25-ERR-CTR            PIC 9(4)    VALUE  0.                 
           05  WS-CTR                     PIC 9(2)    VALUE  0.                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-SYSTEM-DATE             PIC X(8)    VALUE SPACES.             
           05  WS-TEMP-MSTATUS            PIC X(1).                             
           05  WS-TEMP-RACE               PIC X(1).                             
           05  WS-TEMP-SEX                PIC X(1).                             
           05  WS-TMP-EDN                 PIC 9(1)    VALUE ZEROES.             
                                                                                
       01  WS-CONSTANT-TEXTS.                                                   
           05  WS-ERROR-MESSAGES.                                               
               10  K-FB20-BLANK           PIC X(40)   VALUE                     
                   'FB20-NUM-REGN IS BLANK      '.                              
               10  K-K825-NOTFND          PIC X(40)   VALUE                     
                   'RECORD NOT FOUND IN BP13K825'.                              
               10  K-K830-NOTFND          PIC X(40)   VALUE                     
                   'RECORD NOT FOUND IN BP13K830'.                              
               10  K-KB25-DUPLICATE       PIC X(40)   VALUE                     
                   'DUPLICATE RECORD IN BP13KB25'.                              
               10  K-KB25-ERROR           PIC X(40)   VALUE                     
                   'ERROR WRITING TO BP13KB25'.                                 
           05  WS-TOTAL-DESC.                                                   
               10  K-FB20-RECORDS-READ    PIC X(50)   VALUE                     
                      'TOTAL RECORDS READ IN BP13FB20            : '.           
               10  K-FB20-RECORDS-SKIP    PIC X(50)   VALUE                     
                      'TOTAL RECORDS SKIPPED IN BP13FB20         : '.           
               10  K-KB25-RECORDS-WRITE   PIC X(50)   VALUE                     
                      'TOTAL RECORDS WRITE TO BP13KB25           : '.           
               10  K-KB25-RECORDS-DUP     PIC X(50)   VALUE                     
                      'TOTAL DUPLICATES ENCOUNTERED IN BP13KB25  : '.           
               10  K-KB25-UNSUCC-WRITE    PIC X(50)   VALUE                     
                      'TOTAL UNSUCCESSFUL WRITES TO BP13KB25     : '.           
                                                                                
      *-----------------------*                                                 
       PROCEDURE DIVISION.                                                      
      *-----------------------*                                                 
                                                                                
      *-------------*                                                           
       0000-CONTROL.                                                            
      *-------------*                                                           
            PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                       
            PERFORM 4000-PROCESS-FB20-REC THRU 4000-EXIT                        
               UNTIL WS-FB20-EOF-SW = 'Y'.                                      
            PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
           OPEN INPUT  BP13FB20                                                 
                       BP13K022                                                 
                I-O    BP13KB25.                                                
                                                                                
           IF KB25-FILE-STATUS NOT = 00 AND 97                                  
              DISPLAY 'OPENING BP13KB25 ERROR ' KB25-FILE-STATUS                
              MOVE     KB25-FILE-STATUS    TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF K022-FILE-STATUS NOT = 00 AND 97                                  
              DISPLAY 'OPENING BP13K022 ERROR ' K022-FILE-STATUS                
              MOVE     K022-FILE-STATUS    TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM  3000-READ-FB20 THRU 3000-EXIT.                              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------*                                                         
       3000-READ-FB20.                                                          
      *---------------*                                                         
           READ BP13FB20                                                        
              AT END MOVE 'Y' TO WS-FB20-EOF-SW                                 
              GO TO 3000-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD +1             TO WS-FB20-READ-CTR.                              
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------*                                                  
       4000-PROCESS-FB20-REC.                                                   
      *----------------------*                                                  
           IF FB20-NUM-REGN NOT = SPACES AND LOW-VALUES                         
              PERFORM 4300-READ-BP13KB25   THRU 4300-EXIT                       
              IF KB25-DTE-ENTRY = SPACES OR LOW-VALUES                          
                 PERFORM 4500-BP13KB25-WRITE  THRU 4500-EXIT                    
              END-IF                                                            
           ELSE                                                                 
              ADD  1                       TO   WS-FB20-SKIP-CTR                
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-FB20          THRU 3000-EXIT.                      
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------*                                                 
       4300-READ-BP13KB25.                                                      
      *-----------------------*                                                 
           MOVE SPACES TO BP13KB25-REC                                          
           INITIALIZE     BP13KB25-REC.                                         
                                                                                
           MOVE FB20-NUM-PROJECT TO KB25-NUM-PROJECT3.                          
           MOVE 'Y'              TO KB25-NUM-CPF-HG.                            
           MOVE FB20-NUM-REGN    TO KB25-NUM-REGN2.                             
                                                                                
           READ BP13KB25 KEY IS KB25-ALT-KEY2.                                  
                                                                                
           IF KB25-FILE-STATUS = 00 OR 02                                       
              DISPLAY 'FND IN Y : ' KB25-NUM-REGN2                              
              CONTINUE                                                          
           ELSE                                                                 
           IF KB25-FILE-STATUS = 23                                             
              PERFORM 4305-READ-BP13KB25   THRU 4305-EXIT                       
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13KB25 FOR REC ' FB20-NUM-REGN           
              MOVE     KB25-FILE-STATUS   TO   RETURN-CODE                      
              PERFORM  9000-CLOSE-FILES   THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------*                                                 
       4305-READ-BP13KB25.                                                      
      *-----------------------*                                                 
           MOVE SPACES TO BP13KB25-REC                                          
           INITIALIZE     BP13KB25-REC.                                         
                                                                                
           MOVE FB20-NUM-PROJECT TO KB25-NUM-PROJECT3.                          
           MOVE 'N'              TO KB25-NUM-CPF-HG.                            
           MOVE FB20-NUM-REGN    TO KB25-NUM-REGN2.                             
                                                                                
           READ BP13KB25 KEY IS KB25-ALT-KEY2.                                  
                                                                                
           IF KB25-FILE-STATUS = 00 OR 02                                       
              DISPLAY 'FND IN N : ' KB25-NUM-REGN2                              
              CONTINUE                                                          
           ELSE                                                                 
              MOVE FB20-NUM-PROJECT   TO KB25-NUM-PROJECT1                      
                                         KB25-NUM-PROJECT2                      
                                         KB25-NUM-PROJECT3                      
              MOVE FB20-NUM-REGN      TO KB25-NUM-REGN                          
              MOVE FB20-NUM-REGN      TO KB25-NUM-REGN2                         
              MOVE FB20-NUM-REGN      TO KB25-NUM-REGN4                         
              MOVE FB20-DTE-ACCEPT    TO KB25-DTE-BOOKING                       
              MOVE FB20-NUM-CPFHG-REQ TO KB25-NUM-CPF-HG                        
              MOVE FB20-NUM-DBSS-ELIG TO KB25-NUM-DBSS-ELIG2                    
              PERFORM 4400-READ-BP13K022    THRU 4400-EXIT                      
              IF WS-FOUND = 'Y'                                                 
                 MOVE K022-NUM-OIC       TO KB25-NUM-OIC                        
              ELSE                                                              
                 MOVE 'KKL1'             TO KB25-NUM-OIC                        
              END-IF                                                            
              PERFORM 4510-WRITE-BP13KB25   THRU 4510-EXIT                      
           END-IF.                                                              
                                                                                
       4305-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ****************************************************************          
       4400-READ-BP13K022.                                                      
      ****************************************************************          
                                                                                
           MOVE 'N'                  TO WS-FOUND.                               
           MOVE LOW-VALUES           TO K022-KEY-FLD.                           
           MOVE FB20-NUM-REGN        TO K022-NUM-REGN.                          
                                                                                
           START BP13K022 KEY >= K022-NUM-REGN.                                 
                                                                                
           IF K022-FILE-STATUS = 00 OR 02                                       
              READ BP13K022 NEXT RECORD                                         
              IF K022-FILE-STATUS = 00 OR 02                                    
                 IF (K022-NUM-REGN = FB20-NUM-REGN)                             
                    MOVE 'Y' TO WS-FOUND                                        
                 ELSE                                                           
                    MOVE 'N' TO WS-FOUND                                        
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
           IF K022-FILE-STATUS = 23                                             
              MOVE 'N' TO WS-FOUND                                              
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'ERROR READING BP13K022 FOR REC ' FB20-NUM-REGN           
              MOVE     K022-FILE-STATUS   TO   RETURN-CODE                      
              PERFORM  9000-CLOSE-FILES   THRU 9000-EXIT                        
           END-IF.                                                              
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       4500-BP13KB25-WRITE.                                                     
      *--------------------*                                                    
                                                                                
           MOVE SPACES                     TO BP13KB25-KB20-REC.                
           MOVE FUNCTION CURRENT-DATE(1:8) TO KB25-DTE-ENTRY.                   
           MOVE BP13FB20-REC(1:3171)       TO BP13KB25-KB20-REC.                
           MOVE FB20-DTE-ACCEPT            TO KB25-DTE-ACCEPT                   
           MOVE FB20-NUM-PAYMENT-SCHEME    TO KB25-NUM-PAYMENT-SCHEME           
           MOVE FB20-DTE-BALLOT            TO KB25-DTE-BALLOT                   
           MOVE FB20-NUM-USERID            TO KB25-NUM-USERID                   
           MOVE FB20-DTE-UPDATE            TO KB25-DTE-UPDATE.                  
           MOVE FB20-TME-UPDATE            TO KB25-TME-UPDATE.                  
           MOVE FB20-DTE-REQUESTED         TO KB25-DTE-REQUESTED.               
           MOVE FB20-TME-REQUESTED         TO KB25-TME-REQUESTED.               
                                                                                
           IF KB25-DTE-8WK-FROM-OPTION  =  SPACES                               
              PERFORM 5000-COMPUTE-8WK     THRU 5000-EXIT                       
           END-IF.                                                              
                                                                                
           REWRITE BP13KB25-REC.                                                
                                                                                
           IF KB25-FILE-STATUS = 00 OR 02                                       
              ADD 1 TO WS-KB25-WRITE-CTR                                        
              DISPLAY 'OLD CASE UPDATE : ' KB25-NUM-REGN                        
           ELSE                                                                 
              DISPLAY 'ERROR IN UPDATING : ' FB20-NUM-REGN                      
           END-IF.                                                              
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------*                                                    
       4510-WRITE-BP13KB25.                                                     
      *--------------------*                                                    
                                                                                
           MOVE SPACES                     TO BP13KB25-KB20-REC.                
           MOVE FUNCTION CURRENT-DATE(1:8) TO KB25-DTE-ENTRY.                   
           MOVE BP13FB20-REC(1:3171)       TO BP13KB25-KB20-REC.                
           MOVE FB20-DTE-UPDATE            TO KB25-DTE-UPDATE.                  
           MOVE FB20-TME-UPDATE            TO KB25-TME-UPDATE.                  
           MOVE FB20-DTE-REQUESTED         TO KB25-DTE-REQUESTED.               
           MOVE FB20-TME-REQUESTED         TO KB25-TME-REQUESTED.               
           MOVE FB20-NUM-PAYMENT-SCHEME    TO KB25-NUM-PAYMENT-SCHEME.          
           MOVE FB20-DTE-ACCEPT            TO KB25-DTE-ACCEPT.                  
           MOVE FB20-DTE-BALLOT            TO KB25-DTE-BALLOT.                  
           MOVE FB20-NUM-USERID            TO KB25-NUM-USERID.                  
                                                                                
           PERFORM 5000-COMPUTE-8WK        THRU 5000-EXIT.                      
                                                                                
           WRITE BP13KB25-REC.                                                  
                                                                                
           IF KB25-FILE-STATUS = 00 OR 02                                       
              ADD 1 TO WS-KB25-WRITE-CTR                                        
              DISPLAY 'NEW CASE WRITE : ' KB25-NUM-REGN                         
           ELSE                                                                 
              DISPLAY 'ERROR IN WRITING : ' FB20-NUM-REGN                       
           END-IF.                                                              
                                                                                
       4510-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------*                                                       
       5000-COMPUTE-8WK.                                                        
      *-----------------*                                                       
           IF KB25-DTE-ACCEPT NOT =  SPACES AND ZEROS                           
               PERFORM 5100-56DAYS-TO-DTE-ACCEPT  THRU 5100-EXIT                
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------*                                              
       5100-56DAYS-TO-DTE-ACCEPT.                                               
      *--------------------------*                                              
           INITIALIZE                    WS-8WK-VARIABLES.                      
                                                                                
           MOVE KB25-DTE-ACCEPT          TO WS-ACCEPT-DTE.                      
                                                                                
              COMPUTE WS-8WK-DTE =                                              
                      FUNCTION INTEGER-OF-DATE (WS-ACCEPT-DTE)                  
                    + 56.                                                       
                                                                                
              COMPUTE WS-8WK-DTE =                                              
                      FUNCTION DATE-OF-INTEGER (WS-8WK-DTE).                    
                                                                                
              MOVE    WS-8WK-DTE         TO KB25-DTE-8WK-FROM-OPTION.           
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
           CLOSE BP13FB20                                                       
                 BP13K022                                                       
                 BP13KB25.                                                      
                                                                                
           IF KB25-FILE-STATUS NOT = 00                                         
              DISPLAY 'BP13KB25 CLOSE ERROR,STATUS IS ' KB25-FILE-STATUS        
              MOVE KB25-FILE-STATUS TO RETURN-CODE                              
           END-IF.                                                              
                                                                                
           IF K022-FILE-STATUS NOT = 00                                         
              DISPLAY 'BP13K022 CLOSE ERROR,STATUS IS ' K022-FILE-STATUS        
              MOVE K022-FILE-STATUS TO RETURN-CODE                              
           END-IF.                                                              
                                                                                
           DISPLAY 'TOTAL WRITE : ' WS-KB25-WRITE-CTR.                          
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
