       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C52A.                                                 
      *AUTHOR.        SHEILA MAE PATIAG.                                        
      *DATE-WRITTEN.  12/04/2012.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT  (BP13)                               *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  TO UPDATE THE QUEUE POSITIONS FOR MGPS      *            
      *                (CHILD) APPLICATION                         *            
      *                                                            *            
      *  INPUT      :  BP13F740  BP13.F740.MATUREBA.MGP            *            
      *  I-O        :  BP13K740  BP13.K740.MATUREBA                *            
      *  OUTPUT     :  BP13L52A  REPORT ON QUEUE POSITIONS UPDATED *            
      *                          FOR MGPS (CHILD)                  *            
      *                                                            *            
      * ========================================================== *            
      * CHG-REF#  BY  DDMMCCYY DESCRIPTION                         *            
      * -------- ---- -------- ----------------------------------- *            
      * BP134483 SMR2 12042012 NEW PROGRAM                         *            
      * BP134536 SMR2 19062012 CHECK K740-KEY-FLD BEFORE STARTBR   *            
      * BP134627 SMR2 21082012 GET ORIG-QUEUE FROM CHILD RECORD    *            
      *                        AND NEW-QUEUE FROM PARENT RECORD    *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
           SELECT BP13K740 ASSIGN TO BP13K740                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K740-KEY-FLD                               
                  ALTERNATE RECORD KEY IS K740-ALT1-KEY                         
                  FILE STATUS     IS WS-K740-STATUS.                            
           SELECT BP13L52A ASSIGN TO BP13L52A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F740                                                            
            RECORD CONTAINS 500  CHARACTERS                                     
            BLOCK CONTAINS    0  RECORDS                                        
            RECORDING MODE   IS  F                                              
            LABEL RECORDS        STANDARD.                                      
       COPY BP13F740.                                                           
                                                                                
       FD   BP13K740                                                            
            RECORD CONTAINS 500  CHARACTERS.                                    
       COPY BP13K740.                                                           
                                                                                
       FD   BP13L52A                                                            
            RECORD CONTAINS 132  CHARACTERS                                     
            RECORDING MODE   IS  F                                              
            LABEL RECORDS        OMITTED.                                       
       01   BP13L52A-REC         PIC X(132).                                    
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-FILE-INDICATORS.                                                  
           05  WS-F740-EOF                PIC X(01) VALUE 'N'.                  
           05  WS-K740-EOF                PIC X(01) VALUE 'N'.                  
           05  WS-F740-READ               PIC 9(06) VALUE ZEROES.               
           05  WS-K740-FOUND              PIC X(01) VALUE 'N'.                  
           05  WS-F740-NTFND-CNT          PIC 9(06) VALUE ZEROES.               
           05  WS-K740-STATUS             PIC 9(02) VALUE ZEROES.               
           05  WS-K740-REWRITE            PIC 9(06) VALUE ZEROES.               
                                                                                
       01  WS-CNT-LINE-ON-PAGE            PIC 9(08) VALUE 66.                   
       01  WS-CNT-PRINT-L52A              PIC 9(08) VALUE 1.                    
       01  WS-CNT-WRITE-L52A              PIC 9(06) VALUE 0.                    
       01  WS-NO-OF-PAGE                  PIC 9(08) VALUE 0.                    
       01  WS-ORIG-QUEUE                  PIC X(05) VALUE SPACES.               
       01  WS-NEW-QUEUE                   PIC X(05) VALUE SPACES.               
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY                PIC 9(04).                            
           05  WS-SYS-MM                  PIC 9(02).                            
           05  WS-SYS-DD                  PIC 9(02).                            
                                                                                
       01  WS-EDITED-DATE                 PIC X(10).                            
                                                                                
      *--------------------------------------------------------*                
      *         BP13L52A -  R E P O R T    L A Y O U T        *                 
      *--------------------------------------------------------*                
                                                                                
       01  L52A-HEAD-01.                                                        
           05  FILLER                   PIC X(14) VALUE 'BP13L52A'.             
           05  FILLER                   PIC X(08) VALUE 'HDBCAT 3'.             
           05  FILLER                   PIC X(25) VALUE SPACES.                 
           05  FILLER                   PIC X(39) VALUE                         
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                   PIC X(13) VALUE SPACES.                 
           05  FILLER                   PIC X(07) VALUE 'DATE: '.               
           05  L52A-RUN-DATE            PIC X(10) VALUE SPACES.                 
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  FILLER                   PIC X(07) VALUE 'PAGE: '.               
           05  L52A-PAGE-NO             PIC ZZZ9.                               
                                                                                
       01  L52A-HEAD-02.                                                        
           05  FILLER                   PIC X(47) VALUE SPACES.                 
           05  FILLER                   PIC X(40) VALUE                         
               'UPDATED QUEUE POSITIONS FOR MGPS (CHILD)'.                      
                                                                                
       01  L52A-HEAD-03.                                                        
           05  FILLER          PIC X(05)  VALUE ' S-NO'.                        
           05  FILLER          PIC X(05)  VALUE SPACES.                         
           05  FILLER          PIC X(08)  VALUE 'REGN NO.'.                     
           05  FILLER          PIC X(05)  VALUE SPACES.                         
           05  FILLER          PIC X(08)  VALUE 'NEW TOWN'.                     
           05  FILLER          PIC X(05)  VALUE SPACES.                         
           05  FILLER          PIC X(09)  VALUE 'FLAT TYPE'.                    
           05  FILLER          PIC X(05)  VALUE SPACES.                         
           05  FILLER          PIC X(10)  VALUE 'ORIG QUEUE'.                   
           05  FILLER          PIC X(05)  VALUE SPACES.                         
           05  FILLER          PIC X(09)  VALUE 'NEW QUEUE'.                    
           05  FILLER          PIC X(05)  VALUE SPACES.                         
           05  FILLER          PIC X(15)  VALUE 'PARENT REGN NO.'.              
                                                                                
       01  L52A-HEAD-04.                                                        
           05  FILLER          PIC X(95)  VALUE ALL '-'.                        
                                                                                
       01  L52A-DETAILS.                                                        
           05  L52A-NUMBER           PIC ZZZZ9  BLANK WHEN ZERO.                
           05  FILLER                PIC X(05)  VALUE SPACES.                   
           05  L52A-REGN             PIC X(08)  VALUE SPACES.                   
           05  FILLER                PIC X(05)  VALUE SPACES.                   
           05  L52A-NT               PIC X(03)  VALUE SPACES.                   
           05  FILLER                PIC X(10)  VALUE SPACES.                   
           05  L52A-FT               PIC X(02)  VALUE SPACES.                   
           05  FILLER                PIC X(12)  VALUE SPACES.                   
           05  L52A-ORIG-Q           PIC X(05)  VALUE SPACES.                   
           05  FILLER                PIC X(10)  VALUE SPACES.                   
           05  L52A-NEW-Q            PIC X(05)  VALUE SPACES.                   
           05  FILLER                PIC X(09)  VALUE SPACES.                   
           05  L52A-PARENT-REGN      PIC X(08)  VALUE SPACES.                   
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-F740          THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-F740       THRU 3000-EXIT                       
             UNTIL WS-F740-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BP13F740                                                 
                  I-O  BP13K740                                                 
               OUTPUT  BP13L52A.                                                
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K740 OPEN ERROR ' WS-K740-STATUS                
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE      TO WS-SYSTEM-DATE.                   
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-F740.                                                          
      *-------------------------------------------------------------            
                                                                                
           READ BP13F740 AT END                                                 
                MOVE 'Y'                   TO WS-F740-EOF                       
                GO TO 2000-EXIT                                                 
           END-READ.                                                            
                                                                                
           ADD 1                           TO WS-F740-READ.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-F740.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 3100-READ-K740          THRU 3100-EXIT.                      
                                                                                
           IF WS-K740-FOUND = 'Y'                                               
              PERFORM 4000-STARTBR-K740    THRU 4000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F740          THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3100-READ-K740.                                                          
      *--------------------------------------------------------*                
                                                                                
           MOVE LOW-VALUES                 TO K740-KEY-FLD.                     
           MOVE F740-KEY-FLD               TO K740-KEY-FLD.                     
                                                                                
           READ BP13K740.                                                       
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROES                                                          
              MOVE 'Y'                     TO WS-K740-FOUND                     
              MOVE K740-NUM-NT-FT-QUEUE    TO WS-NEW-QUEUE                      
           WHEN 23                                                              
              MOVE 'N'                     TO WS-K740-FOUND                     
              ADD 1                        TO WS-F740-NTFND-CNT                 
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN READ OF BP13K740. STATUS IS '                   
                      WS-K740-STATUS                                            
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-STARTBR-K740.                                                       
      *--------------------------------------------------------*                
                                                                                
           MOVE LOW-VALUES                 TO K740-ALT1-KEY.                    
           MOVE F740-NUM-JB-REGN           TO K740-ALT1-KEY.                    
                                                                                
           START BP13K740 KEY IS = K740-ALT1-KEY.                               
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROES                                                          
              PERFORM 5000-READNEXT-K740   THRU 5000-EXIT                       
                UNTIL K740-ALT1-KEY NOT = F740-NUM-JB-REGN                      
                   OR WS-K740-EOF = 'Y'                                         
           WHEN 10                                                              
           WHEN 23                                                              
              ADD 1                        TO WS-F740-NTFND-CNT                 
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN STARTBR OF BP13K740. STATUS IS '                
                      WS-K740-STATUS                                            
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       5000-READNEXT-K740.                                                      
      *--------------------------------------------------------*                
                                                                                
           READ BP13K740 NEXT RECORD                                            
                AT END MOVE 'Y'            TO WS-K740-EOF.                      
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN ZEROES                                                          
           WHEN 02                                                              
              IF K740-NUM-MGPS = 'C' AND                                        
                 K740-NUM-JB-REGN = F740-NUM-REGN                               
                 PERFORM 6000-REWRITE-K740 THRU 6000-EXIT                       
                 PERFORM 7000-WRITE-L52A   THRU 7000-EXIT                       
              END-IF                                                            
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                     TO WS-K740-EOF                       
           WHEN OTHER                                                           
              DISPLAY 'ERROR IN READ NEXT OF BP13K740. STATUS IS '              
                      WS-K740-STATUS                                            
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-REWRITE-K740.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE K740-NUM-NT-FT-QUEUE       TO WS-ORIG-QUEUE.                    
           MOVE WS-NEW-QUEUE               TO K740-NUM-NT-FT-QUEUE.             
                                                                                
           REWRITE BP13K740-REC.                                                
                                                                                
           EVALUATE WS-K740-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO WS-K740-REWRITE                   
           WHEN OTHER                                                           
              DISPLAY 'ERROR WRITING TO BP13K740. STATUS IS - '                 
                      WS-K740-STATUS                                            
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       7000-WRITE-L52A.                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                     TO L52A-DETAILS.                     
           INITIALIZE L52A-DETAILS.                                             
                                                                                
           MOVE K740-NUM-REGN              TO L52A-REGN.                        
           MOVE K740-CDE-NT1               TO L52A-NT.                          
           MOVE K740-CDE-FLAT-TYPE         TO L52A-FT.                          
           MOVE WS-ORIG-QUEUE              TO L52A-ORIG-Q.                      
           MOVE WS-NEW-QUEUE               TO L52A-NEW-Q.                       
           MOVE K740-NUM-JB-REGN           TO L52A-PARENT-REGN.                 
                                                                                
           IF WS-CNT-LINE-ON-PAGE > 55                                          
              PERFORM 8000-PRINT-HEADING   THRU 8000-EXIT                       
              MOVE 5                       TO WS-CNT-LINE-ON-PAGE               
           END-IF.                                                              
                                                                                
           MOVE WS-CNT-PRINT-L52A          TO L52A-NUMBER                       
                                                                                
           WRITE BP13L52A-REC              FROM L52A-DETAILS AFTER 1.           
           ADD 1                           TO WS-CNT-LINE-ON-PAGE               
                                              WS-CNT-PRINT-L52A                 
                                              WS-CNT-WRITE-L52A.                
           MOVE SPACES                     TO L52A-DETAILS.                     
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       8000-PRINT-HEADING.                                                      
      *-------------------------------------------------------------            
                                                                                
           ADD 1                           TO WS-NO-OF-PAGE.                    
           MOVE WS-NO-OF-PAGE              TO L52A-PAGE-NO.                     
           MOVE WS-EDITED-DATE             TO L52A-RUN-DATE.                    
                                                                                
           MOVE SPACES                     TO BP13L52A-REC.                     
                                                                                
           WRITE BP13L52A-REC              AFTER PAGE.                          
           WRITE BP13L52A-REC              FROM L52A-HEAD-01 AFTER 1.           
           WRITE BP13L52A-REC              FROM L52A-HEAD-02 AFTER 1.           
           WRITE BP13L52A-REC              FROM L52A-HEAD-03 AFTER 2.           
           WRITE BP13L52A-REC              FROM L52A-HEAD-04 AFTER 1.           
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
                                                                                
           CLOSE BP13F740                                                       
                 BP13K740                                                       
                 BP13L52A.                                                      
                                                                                
           IF WS-K740-STATUS NOT = 00 AND 97                                    
              DISPLAY 'FILE BP13K740 CLOSE ERROR ' WS-K740-STATUS               
              MOVE WS-K740-STATUS          TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           DISPLAY '==============================================='.           
           DISPLAY '      PROGRAM : BP13C52A       ' WS-EDITED-DATE.            
           DISPLAY '==============================================='.           
           DISPLAY ' '.                                                         
           DISPLAY 'BP13F740 RECORDS READ       ' WS-F740-READ.                 
           DISPLAY 'BP13F740 RECORDS NOT FOUND  ' WS-F740-NTFND-CNT.            
           DISPLAY 'BP13K740 RECORDS REWRITTEN  ' WS-K740-REWRITE.              
           DISPLAY 'BP13L52A RECORDS WRITTEN    ' WS-CNT-WRITE-L52A.            
           DISPLAY ' '.                                                         
           DISPLAY '==============================================='.           
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
