      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C575.                                                 
      *AUTHOR.        KARL ANDRUS CABUG.                                        
      *DATE-WRITTEN.  28/02/23.                                                 
      * ====================================================== *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *     1. TO GENERATE A DAILY NON-CUMULATIVE E-REPORT OF  *                
      *        THE CASES THAT HAVE BEEN UPDATED VIA PANEL M327 *                
      *     2. TO DEPOSIT THE DAILY REPORT INTO EAPG PORTAL >  *                
      *        NEW SALES>DOCUMENT MANAGEMENT>BP13 REPORTS>AGT  *                
      *                                                        *                
      *     INPUT FILES :                                      *                
      *       1. BP13F650                                      *                
      *                                                        *                
      *     OUTPUT FILE :                                      *                
      *       1.                                               *                
      * ====================================================== *                
      * CHG REF   BY    DATE   DESCRIPTION                     *                
      * -------- ---- -------- ----------                      *                
      * BP139526 KAC1 28/02/23 NEW PROGRAM                     *                
      * ====================================================== *                
                                                                                
      ***********************                                                   
       ENVIRONMENT DIVISION.                                                    
      ***********************                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F650 ASSIGN TO BP13F650.                                  
      *    SELECT BP13K650 ASSIGN TO BP13K650                                   
      *              ORGANIZATION IS INDEXED                                    
      *              ACCESS MODE  IS RANDOM                                     
      *              RECORD KEY   IS K650-NUM-REGN                              
      *              FILE STATUS  IS K650-STATUS.                               
                                                                                
           SELECT P13L575A ASSIGN TO P13L575A.                                  
                                                                                
      ***********************                                                   
       DATA DIVISION.                                                           
      ***********************                                                   
       FILE SECTION.                                                            
      *FD  BP13K650         RECORD CONTAINS 500 CHARACTERS.                     
      *COPY BP13K650.                                                           
       FD  BP13F650         BLOCK CONTAINS 0 RECORDS                            
                            RECORD CONTAINS 500 CHARACTERS                      
                            RECORDING MODE IS F                                 
                            LABEL RECORDS ARE STANDARD.                         
       COPY BP13F650.                                                           
                                                                                
       FD  P13L575A                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE OMITTED.                                           
       01  PRINT-REC-1                 PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           03  WS-PAGE             PIC 9(4)    VALUE ZEROS.                     
           03  WS-CNT              PIC 9(2)    VALUE ZEROS.                     
           03  WS-SEQ-NO           PIC 9(4)    VALUE ZEROS.                     
           03  WS-SEQ-NO-TOT       PIC 9(5)    VALUE ZEROS.                     
                                                                                
       01  WS-FLAGS.                                                            
           03  EOF-F650            PIC X       VALUE SPACES.                    
           03  DTL1-SW             PIC X       VALUE SPACES.                    
                                                                                
       01  WS-EDITED-DATE.                                                      
           03  WS-DTE-DD           PIC X(02) VALUE SPACES.                      
           03  FILLER              PIC X(01) VALUE '/'.                         
           03  WS-DTE-MM           PIC X(02) VALUE SPACES.                      
           03  FILLER              PIC X(01) VALUE '/'.                         
           03  WS-DTE-CCYY         PIC X(04) VALUE SPACES.                      
                                                                                
       01  WS-CUR-DATE.                                                         
           03  WS-YY               PIC 9(4).                                    
           03  WS-MM               PIC 99.                                      
           03  WS-DD               PIC 99.                                      
                                                                                
       01  L575-PR-HEAD-1.                                                      
           03  L575-ID             PIC X(8).                                    
           03  FILLER              PIC X(6)   VALUE SPACES.                     
           03  FILLER              PIC X(25)  VALUE 'HDB3'.                     
           03  FILLER              PIC X(62)  VALUE                             
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           03  FILLER              PIC X(6)   VALUE 'DATE: '.                   
           03  L575-DATE           PIC X(10).                                   
           03  FILLER              PIC X(10)  VALUE '    PAGE: '.               
           03  L575-PAGE           PIC ZZZ9.                                    
                                                                                
       01  L575-PR-HEAD-2.                                                      
           03  FILLER              PIC X(02)  VALUE SPACES.                     
           03  FILLER              PIC X(45)  VALUE                             
              'Daily Report of Settlement Agreement Received'.                  
           03  FILLER              PIC X(85)  VALUE SPACES.                     
                                                                                
       01  L575-PR-HEAD-3.                                                      
           03  FILLER                   PIC X(02)  VALUE SPACES.                
           03  FILLER                   PIC X(06)  VALUE 'Date: '.              
           03  L575-DTE-DDMMCCYY        PIC X(10)  VALUE SPACES.                
                                                                                
       01  L575-PR-HEAD-4.                                                      
           03  FILLER                   PIC X(02) VALUE SPACES.                 
           03  FILLER                   PIC X(42) VALUE                         
               'Date Update    Regn Number       Update by'.                    
           03  FILLER                   PIC X(88) VALUE SPACES.                 
                                                                                
       01  L575-PR-DETAIL-1.                                                    
           03  FILLER              PIC X(02) VALUE SPACES.                      
           03  L575-DATE-UPDT      PIC X(10) VALUE SPACES.                      
           03  FILLER              PIC X(06) VALUE SPACES.                      
           03  L575-REGN-NO        PIC X(08) VALUE SPACES.                      
           03  FILLER              PIC X(09) VALUE SPACES.                      
           03  L575-UPDATED-BY     PIC X(08) VALUE SPACES.                      
           03  FILLER              PIC X(89) VALUE SPACES.                      
                                                                                
       01  L578-PR-TRAILER.                                                     
           03  FILLER              PIC X(24)  VALUE SPACES.                     
           03  FILLER              PIC X(15)  VALUE 'TOTAL CASES : '.           
           03  L575-TOTAL-CASE-NO  PIC ZZZZ9.                                   
                                                                                
      *********************************************************                 
       PROCEDURE DIVISION.                                                      
      *********************************************************                 
                                                                                
       0000-MAIN.                                                               
                                                                                
           MOVE ZEROES                       TO WS-SEQ-NO.                      
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 1500-READ-BP13F650      THRU 1500-EXIT.                      
           PERFORM 1600-PR-HEADER          THRU 1600-EXIT.                      
           PERFORM 2000-PROCESS-RTN        THRU 2000-EXIT                       
             UNTIL EOF-F650 = 'Y'.                                              
                                                                                
           PERFORM 3000-PR-TOTAL           THRU 3000-EXIT.                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
           OPEN INPUT BP13F650                                                  
               OUTPUT P13L575A.                                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1500-READ-BP13F650.                                                      
            READ BP13F650 AT END                                                
                 MOVE 'Y' TO EOF-F650.                                          
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1600-PR-HEADER.                                                          
            ADD 1                            TO WS-PAGE.                        
            MOVE WS-PAGE                     TO L575-PAGE.                      
            MOVE 'P13L575A'                  TO L575-ID.                        
            MOVE FUNCTION CURRENT-DATE       TO WS-CUR-DATE.                    
            MOVE WS-CUR-DATE(7:2)            TO WS-DTE-DD.                      
            MOVE WS-CUR-DATE(5:2)            TO WS-DTE-MM.                      
            MOVE WS-CUR-DATE(1:4)            TO WS-DTE-CCYY.                    
            MOVE WS-EDITED-DATE              TO L575-DTE-DDMMCCYY.              
      *     WRITE PRINT-REC-1 FROM L575-PR-HEAD-1 AFTER PAGE.                   
            MOVE SPACES                      TO PRINT-REC-1                     
            WRITE PRINT-REC-1                                                   
            WRITE PRINT-REC-1                                                   
            WRITE PRINT-REC-1 FROM L575-PR-HEAD-2 AFTER 1.                      
            MOVE SPACES                      TO PRINT-REC-1                     
            WRITE PRINT-REC-1                                                   
            WRITE PRINT-REC-1 FROM L575-PR-HEAD-3 AFTER 2.                      
            MOVE SPACES                      TO PRINT-REC-1                     
            WRITE PRINT-REC-1                                                   
            WRITE PRINT-REC-1 FROM L575-PR-HEAD-4 AFTER 1.                      
                                                                                
       1600-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-PROCESS-RTN.                                                        
           IF WS-SEQ-NO = 50                                                    
              PERFORM 1600-PR-HEADER       THRU 1600-EXIT                       
              MOVE ZEROES                    TO WS-SEQ-NO                       
           END-IF.                                                              
           ADD 1                             TO WS-SEQ-NO                       
                                                WS-SEQ-NO-TOT.                  
                                                                                
           MOVE F650-NUM-REGN                TO L575-REGN-NO.                   
                                                                                
      *    STRING F650-DTE-SALES-AGREEMENT(7:2) '/'                             
      *           F650-DTE-SALES-AGREEMENT(5:2) '/'                             
      *           F650-DTE-SALES-AGREEMENT(1:4)                                 
      *           DELIMITED BY SIZE INTO L575-TP-DATE                           
      *    END-STRING.                                                          
                                                                                
           STRING F650-DTE-UPDATE(7:2) '/'                                      
                  F650-DTE-UPDATE(5:2) '/'                                      
                  F650-DTE-UPDATE(1:4)                                          
                  DELIMITED BY SIZE INTO L575-DATE-UPDT                         
           END-STRING.                                                          
                                                                                
           MOVE F650-USERID                  TO L575-UPDATED-BY.                
                                                                                
           MOVE SPACES                       TO PRINT-REC-1.                    
           WRITE PRINT-REC-1 FROM L575-PR-DETAIL-1 AFTER 2.                     
                                                                                
           PERFORM 1500-READ-BP13F650      THRU 1500-EXIT.                      
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3000-PR-TOTAL.                                                           
           IF WS-SEQ-NO-TOT > 0 THEN                                            
              MOVE WS-SEQ-NO-TOT             TO L575-TOTAL-CASE-NO              
              MOVE SPACES TO PRINT-REC-1                                        
              WRITE PRINT-REC-1                                                 
              WRITE PRINT-REC-1                                                 
              WRITE PRINT-REC-1 FROM L578-PR-TRAILER AFTER 2                    
           END-IF.                                                              
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
           CLOSE BP13F650                                                       
                 P13L575A.                                                      
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
