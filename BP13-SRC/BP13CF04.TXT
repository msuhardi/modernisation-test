       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF04.                                                 
      *AUTHOR.        ZDD1                                                      
      *DATE-WRITTEN.  23APR2010.                                                
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1. EXTRACT NRIC FOR MCP  APPLICATIONS               *                
      *                                                        *                
      *    INPUT FILES  : BP13F730                             *                
      *                   BP13K825                             *                
      *    OUTPUT FILE  : P13F730A                             *                
      *--------------------------------------------------------*                
      * CHG-NO  BY  DATE      DETAILS                          *                
      *=======  === ========= =================================*                
      *BP133893 ZDD 20100423  NEW PGM                          *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN TO BP13F730.                                  
                                                                                
           SELECT BP13K825 ASSIGN TO BP13K825                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K825-KEY-FLD                               
                  FILE STATUS     IS WS-K825-STATUS.                            
                                                                                
           SELECT P13F730A ASSIGN TO P13F730A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K825.                                                           
                                                                                
       FD   P13F730A                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 500 CHARACTERS.                                     
       01   P13F730A-REC.                                                       
            05  FILLER      PIC X(40).                                          
            05  WS-NRIC     PIC X(09).                                          
            05  FILLER      PIC X(73).                                          
            05  WS-NUM-REGN PIC X(08).                                          
            05  FILLER      PIC X(370).                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F730-EOF                    PIC X(1)  VALUE 'N'.              
           05  WS-K825-EOF                    PIC X(1)  VALUE 'N'.              
           05  WS-K825-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-CNT-READ-F730               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-NOT-FND-K825            PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-WRTE-F730               PIC 9(6)  VALUE ZEROS.            
           05  WS-FOUND                       PIC X(1)  VALUE 'N'.              
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
                                                                                
           PERFORM 3000-PROCESS-F730      THRU 3000-EXIT                        
                  UNTIL WS-F730-EOF    = 'Y'.                                   
                                                                                
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13K825                                                 
                OUTPUT P13F730A.                                                
                                                                                
           IF WS-K825-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K825 OPEN ERROR, STATUS IS ' WS-K825-STATUS          
              MOVE WS-K825-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-F730.                                                          
      ****************************************************************          
                                                                                
           READ BP13F730                                                        
              AT END MOVE 'Y' TO WS-F730-EOF                                    
              GO TO 3000-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD  +1                TO WS-CNT-READ-F730.                          
                                                                                
                                                                                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2100-READ-K825.                                                          
      ****************************************************************          
                                                                                
           START BP13K825 KEY IS >= K825-NUM-REGN.                              
                                                                                
           EVALUATE WS-K825-STATUS                                              
            WHEN 00                                                             
            WHEN 02                                                             
            WHEN 22                                                             
                 PERFORM 2200-READNEXT-K825   THRU 2200-EXIT                    
                   UNTIL F730-NUM-REGN NOT = K825-NUM-REGN OR                   
                          WS-K825-EOF = 'Y'                                     
            WHEN 23                                                             
               MOVE 'N' TO WS-FOUND                                             
               ADD 1 TO WS-CNT-NOT-FND-K825                                     
            WHEN OTHER                                                          
              DISPLAY 'ERROR READING OF BP13K825. STATUS IS '                   
                       WS-K825-STATUS                                           
              MOVE WS-K825-STATUS              TO RETURN-CODE                   
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2200-READNEXT-K825.                                                      
      ****************************************************************          
                                                                                
           READ BP13K825 NEXT RECORD                                            
                                                                                
           EVALUATE  WS-K825-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
               WHEN 22                                                          
                    IF F730-NUM-REGN  = K825-NUM-REGN                           
                       MOVE F730-NUM-REGN  TO WS-NUM-REGN                       
                       MOVE K825-NUM-NRIC  TO WS-NRIC                           
                       WRITE P13F730A-REC                                       
                       ADD 1 TO WS-CNT-WRTE-F730                                
                       MOVE 'Y' TO WS-FOUND                                     
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                  IF WS-FOUND = 'N'                                             
                    ADD 1 TO WS-CNT-NOT-FND-K825                                
                  END-IF                                                        
                  MOVE 'Y' TO WS-K825-EOF                                       
               WHEN OTHER                                                       
                 DISPLAY 'ERROR READING OF BP13K825. STATUS IS '                
                         WS-K825-STATUS                                         
                 MOVE WS-K825-STATUS              TO RETURN-CODE                
                 PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-F730.                                                       
      ****************************************************************          
                                                                                
           PERFORM 2000-READ-F730    THRU 2000-EXIT.                            
                                                                                
           MOVE 'N'                  TO WS-FOUND                                
                                        WS-K825-EOF.                            
                                                                                
           MOVE SPACES               TO BP13K825-REC                            
                                        P13F730A-REC.                           
                                                                                
           MOVE F730-NUM-REGN        TO K825-NUM-REGN                           
                                                                                
           PERFORM 2100-READ-K825 THRU 2100-EXIT.                               
                                                                                
           IF F730-NUM-RES-NRIC1    NOT = SPACES AND LOW-VALUES                 
              MOVE F730-NUM-RES-NRIC1     TO WS-NRIC                            
              MOVE F730-NUM-REGN          TO WS-NUM-REGN                        
              WRITE P13F730A-REC                                                
              ADD 1 TO WS-CNT-WRTE-F730                                         
           END-IF.                                                              
                                                                                
           IF F730-NUM-RES-NRIC2    NOT = SPACES AND LOW-VALUES                 
              MOVE F730-NUM-RES-NRIC2     TO WS-NRIC                            
              MOVE F730-NUM-REGN          TO WS-NUM-REGN                        
              WRITE P13F730A-REC                                                
              ADD 1 TO WS-CNT-WRTE-F730                                         
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13CF04 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F730 RECORDS READ      = ' WS-CNT-READ-F730.            
           DISPLAY 'NO OF CASES WRITTEN        = ' WS-CNT-WRTE-F730.            
           DISPLAY 'NO OF NOT FOUND ON K825    = ' WS-CNT-NOT-FND-K825.         
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           CLOSE BP13F730                                                       
                 BP13K825                                                       
                 P13F730A.                                                      
                                                                                
           IF WS-K825-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K825 CLOSE ERROR, STATUS IS ' WS-K825-STATUS         
              MOVE WS-K825-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
                                                                                
           STOP RUN.                                                            
       9999-EXIT.                                                               
           EXIT.                                                                
