      *-------------------------------------------------------------*           
       IDENTIFICATION DIVISION.                                                 
      *-------------------------------------------------------------*           
       PROGRAM-ID.    BP13C542.                                                 
      *AUTHOR.        SK.                                                       
      *DATE-WRITTEN.  21/04/2004.                                               
      *-------------------------------------------------------------*           
      * SYSTEM OF COMMITMENT  (BP13)                                *           
      *-------------------------------------------------------------*           
      *                                                             *           
      *  OBJECTIVE   : 1.  MERGE TWO RECORDS (RANDOM NUMBERS)       *           
      *                                                             *           
      *  INPUT FILES : 1.  BP13F747  -  SUCC. REGN                  *           
      *                                                             *           
      *  OUTPUT      : 1.  P13F747A  -  SUCC. REGN (MREGED FILE)    *           
      *                                                             *           
      *-------------------------------------------------------------*           
      * CHG REF  BY    DATE    DESCRIPTION                          *           
      * -------- ---- -------- -----------                          *           
      * BP132496 SK   20040421 NEW PGM                              *           
      *-------------------------------------------------------------*           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *-------------------------------------------------------------*           
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------*           
                                                                                
           SELECT BP13F747  ASSIGN       TO BP13F747.                           
           SELECT P13F747A  ASSIGN       TO P13F747A.                           
                                                                                
      *-------------------------------------------------------------*           
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------*           
                                                                                
       FD   BP13F747                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 150  CHARACTERS.                                    
       COPY BP13F747.                                                           
                                                                                
       FD   P13F747A                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 150  CHARACTERS.                                    
                                                                                
       01   P13F747A-REC     PIC X(150).                                        
                                                                                
      *-------------------------------------------------------------*           
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------*           
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F747-EOF                  PIC X(01)  VALUE 'N'.               
           05  WS-F747-READ                 PIC 9(6)   VALUE ZEROS.             
           05  WS-F747A-WRITE               PIC 9(6)   VALUE ZEROS.             
           05  WS-COUNT                     PIC 9(1)   VALUE ZEROS.             
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY                  PIC 9(4).                           
           05  WS-SYS-MM                    PIC 9(2).                           
           05  WS-SYS-DD                    PIC 9(2).                           
                                                                                
       01  WS-EDITED-DATE                   PIC X(10).                          
       01  WS-P13F747A-REC1                 PIC X(150) VALUE SPACES.            
       01  WS-P13F747A-REC2                 PIC X(150) VALUE SPACES.            
       01  WS-F747-KEY                      PIC X(16)  VALUE SPACES.            
                                                                                
      *-------------------------------------------------------------*           
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------*           
                                                                                
      *-------------------------------------------------------------*           
       0000-MAIN.                                                               
      *-------------------------------------------------------------*           
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-F747     THRU 2000-EXIT.                           
                                                                                
           PERFORM 3000-PROCESS       THRU 3000-EXIT                            
                   UNTIL WS-F747-EOF = 'Y'.                                     
                                                                                
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------*           
                                                                                
           OPEN INPUT  BP13F747                                                 
                OUTPUT P13F747A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       2000-READ-F747.                                                          
      *-------------------------------------------------------------*           
                                                                                
           READ BP13F747                                                        
                AT END                                                          
                   MOVE 'Y' TO WS-F747-EOF                                      
                NOT AT END                                                      
                   ADD 1 TO WS-F747-READ                                        
           END-READ.                                                            
                                                                                
           IF WS-F747-KEY = SPACES OR LOW-VALUES                                
              MOVE F747-KEY-FLD  TO WS-F747-KEY                                 
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       3000-PROCESS.                                                            
      *-------------------------------------------------------------*           
                                                                                
           MOVE 0         TO WS-COUNT.                                          
           MOVE SPACES    TO WS-P13F747A-REC1                                   
                             WS-P13F747A-REC2.                                  
                                                                                
           PERFORM UNTIL F747-KEY-FLD NOT = WS-F747-KEY                         
                   OR WS-COUNT > 1 OR WS-F747-EOF = 'Y'                         
                                                                                
              IF F747-KEY-FLD = WS-F747-KEY                                     
                 ADD 1 TO WS-COUNT                                              
                 IF WS-COUNT = 1                                                
                    MOVE BP13F747-REC TO WS-P13F747A-REC1                       
                 ELSE                                                           
                    MOVE BP13F747-REC TO WS-P13F747A-REC2                       
                 END-IF                                                         
              END-IF                                                            
              PERFORM 2000-READ-F747  THRU 2000-EXIT                            
           END-PERFORM                                                          
                                                                                
           IF WS-COUNT > 1                                                      
              IF (WS-P13F747A-REC1(54:28) NOT = SPACES AND LOW-VALUES)          
                 AND                                                            
                 (WS-P13F747A-REC2(54:28)  = SPACES OR LOW-VALUES)              
                                                                                
                 MOVE WS-P13F747A-REC2(82:28) TO                                
                                           WS-P13F747A-REC1(82:28)              
                 PERFORM 4000-WRITE-F747A     THRU 4000-EXIT                    
              ELSE                                                              
              IF (WS-P13F747A-REC2(54:28) NOT = SPACES AND LOW-VALUES)          
                 AND                                                            
                 (WS-P13F747A-REC1(54:28)  = SPACES OR LOW-VALUES)              
                                                                                
                 MOVE WS-P13F747A-REC2(54:28) TO                                
                                           WS-P13F747A-REC1(54:28)              
                 PERFORM 4000-WRITE-F747A     THRU 4000-EXIT                    
              ELSE                                                              
              IF (WS-P13F747A-REC1(82:28)  = SPACES OR LOW-VALUES)              
                 AND                                                            
                 (WS-P13F747A-REC2(82:28)  = SPACES OR LOW-VALUES)              
                                                                                
                 MOVE WS-P13F747A-REC2(54:28) TO                                
                                           WS-P13F747A-REC1(82:28)              
                 PERFORM 4000-WRITE-F747A     THRU 4000-EXIT                    
              ELSE                                                              
              IF (WS-P13F747A-REC1(54:28)  = SPACES OR LOW-VALUES)              
                 AND                                                            
                 (WS-P13F747A-REC2(54:28)  = SPACES OR LOW-VALUES)              
                                                                                
                 MOVE WS-P13F747A-REC2(82:28) TO                                
                                           WS-P13F747A-REC1(54:28)              
                 PERFORM 4000-WRITE-F747A     THRU 4000-EXIT                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-COUNT = 1                                                      
                 PERFORM 4000-WRITE-F747A     THRU 4000-EXIT                    
           END-IF.                                                              
                                                                                
           MOVE F747-KEY-FLD    TO WS-F747-KEY.                                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       4000-WRITE-F747A.                                                        
      *-------------------------------------------------------------*           
                                                                                
           WRITE P13F747A-REC FROM WS-P13F747A-REC1.                            
           ADD 1 TO WS-F747A-WRITE.                                             
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------*           
                                                                                
           DISPLAY '*---------- BP13C542 -------------*'.                       
           DISPLAY SPACES.                                                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECORDS READ    :'.                                   
           DISPLAY '   BP13F747           : '  WS-F747-READ.                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF RECORDS WRITE   : '.                                  
           DISPLAY '   P13F747A           : '  WS-F747A-WRITE.                  
           DISPLAY SPACES.                                                      
           DISPLAY '*---------------------------------*'.                       
                                                                                
           CLOSE BP13F747                                                       
                 P13F747A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
      *                        END OF PROGRAM                       *           
      *-------------------------------------------------------------*           
