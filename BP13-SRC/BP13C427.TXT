       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C427.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  14/06/2011.                                               
      *****************************************************************         
      **              SYSTEM OF COMMITMENT  (BP13)                   **         
      ** =========================================================== **         
      ** OBJECTIVE    : TO COMPARE CPF SAVING AND IDP                **         
      **                                                             **         
      ** INPUT FILES  : BP13F800 (MATCH BY REGN)                     **         
      **                BP13F680                                     **         
      ** OUTPUT FILES : P13F800A                                     **         
      **                (COL 37 : Y - CPF SAVING < IDP               **         
      **                          N - CPF SAVING < IDP)              **         
      ** =========================================================== **         
      ** CHG REQ   DATE        BY    DETAIL                          **         
      ** ========  ==========  ====  =============================== **         
      ** BP134267  14/06/2011  JB8   NEW PROGRAM                     **         
      ** BP136460  02/03/2017  KSJ3  EXPAND BP13F680 (1500-1600)     **         
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *-------------------------------------------------------------            
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800  ASSIGN        TO BP13F800.                          
           SELECT BP13F680  ASSIGN        TO BP13F680.                          
           SELECT P13F800A  ASSIGN        TO P13F800A.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BP13F800                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F800.                                                           
                                                                                
       FD   BP13F680                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 1600 CHARACTERS.                                    
       COPY BP13F680.                                                           
                                                                                
       FD   P13F800A                                                            
            RECORDING MODE  IS   F                                              
            RECORD CONTAINS 2000 CHARACTERS.                                    
       01   P13F800A-REC  PIC X(2000).                                          
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-VARIABLES.                                                        
           05  CNT-F800-READ                PIC 9(06)  VALUE ZEROS.             
           05  CNT-F680-READ                PIC 9(06)  VALUE ZEROS.             
           05  CNT-F800A-WRITE              PIC 9(06)  VALUE ZEROS.             
           05  CNT-F800-NOTMAT              PIC 9(06)  VALUE ZEROS.             
           05  CNT-F680-NOTMAT              PIC 9(06)  VALUE ZEROS.             
           05  WS-SYS-DATE                  PIC X(08)  VALUE SPACES.            
                                                                                
      *=============================================================            
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-FILES             THRU 1000-EXIT.                  
           PERFORM 2100-READ-BP13F800          THRU 2100-EXIT.                  
           PERFORM 2200-READ-BP13F680          THRU 2200-EXIT.                  
           PERFORM 3000-PROCESS-RECORDS        THRU 3000-EXIT                   
             UNTIL F800-NUM-REGN = HIGH-VALUES                                  
               AND F680-NUM-REGN = HIGH-VALUES.                                 
           PERFORM 9000-CLOSE-ROUTINE          THRU 9000-EXIT.                  
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       1000-OPEN-FILES.                                                         
      *=============================================================            
                                                                                
           OPEN INPUT BP13F800                                                  
                      BP13F680                                                  
               OUTPUT P13F800A.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-SYS-DATE.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       2100-READ-BP13F800.                                                      
      *=============================================================            
                                                                                
           READ BP13F800                                                        
             AT END                                                             
                MOVE HIGH-VALUES TO F800-NUM-REGN                               
                GO               TO 2100-EXIT                                   
           END-READ.                                                            
                                                                                
           ADD 1                 TO CNT-F800-READ.                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       2200-READ-BP13F680.                                                      
      *=============================================================            
                                                                                
           READ BP13F680                                                        
             AT END                                                             
                MOVE HIGH-VALUES TO F680-NUM-REGN                               
                GO               TO 2200-EXIT                                   
           END-READ.                                                            
                                                                                
           ADD 1                 TO CNT-F680-READ.                              
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=============================================================            
       3000-PROCESS-RECORDS.                                                    
      *=============================================================            
                                                                                
           IF F800-NUM-REGN = F680-NUM-REGN                                     
              IF F680-AMT-CPF-NET-LSE1 < F800-AMT-CD                            
                 MOVE 'Y'                      TO BP13F800-MASTER(37:1)         
              ELSE                                                              
                 MOVE 'N'                      TO BP13F800-MASTER(37:1)         
              END-IF                                                            
              WRITE P13F800A-REC FROM BP13F800-MASTER                           
              ADD 1                            TO CNT-F800A-WRITE               
              PERFORM 2100-READ-BP13F800     THRU 2100-EXIT                     
              PERFORM 2200-READ-BP13F680     THRU 2200-EXIT                     
           ELSE                                                                 
              IF F800-NUM-REGN < F680-NUM-REGN                                  
                 PERFORM 2100-READ-BP13F800  THRU 2100-EXIT                     
                 ADD 1                         TO CNT-F800-NOTMAT               
              ELSE                                                              
                 PERFORM 2200-READ-BP13F680  THRU 2200-EXIT                     
                 ADD 1                         TO CNT-F680-NOTMAT               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
       9000-CLOSE-ROUTINE.                                                      
      **************************************************************            
           DISPLAY ' '.                                                         
           DISPLAY ' ====  BP13C427 CONTROL TOTALS  ======'.                    
           DISPLAY ' '.                                                         
           DISPLAY ' F800 RECORDS READ         : ' CNT-F800-READ.               
           DISPLAY ' F680 RECORDS READ         : ' CNT-F680-READ.               
           DISPLAY ' F800A RECORDS WRITTEN     : ' CNT-F800A-WRITE.             
           DISPLAY ' F800 RECORDS NOT MATCH    : ' CNT-F800-NOTMAT.             
           DISPLAY ' F680 RECORDS NOT MATCH    : ' CNT-F680-NOTMAT.             
                                                                                
           CLOSE BP13F800                                                       
                 BP13F680                                                       
                 P13F800A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
