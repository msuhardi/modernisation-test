      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C233.                                                 
       AUTHOR.        CHYEO.                                                    
      *DATE-WRITTEN.  08/07/98.                                                 
                                                                                
      *===============================================================*         
      *  SOC (BP13)      DAILY PROGRAM                                *         
      *===============================================================*         
      *  OBJECTIVES : CHANGE THE OBJECTIVE TO OUTPUT ONLY SERS CASES  *         
      *               WITHOUT ANY UPDATING                            *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                ACCESS      REC                                *         
      * DD NAME  I/O/E  MODE       LEN  KEY          REMARKS          *         
      * -------  ----- -------     ---  ---          --------         *         
      * BP13F410 I      SEQUNETIAL 1200                               *         
      * BP17F000 I      SEQUENTIAL  80                                *         
      * BP17K120 I-O    DYNAMIC     80                                *         
      *===============================================================*         
      * CHG NO   DATE      AUTHOR  REMARKS                            *         
      * -------- --------  ------  -------                            *         
      * 0491     09/07/98   YCH2   NEW PROGRAM                        *         
      * BP130760 25022000   YCH2   UPDATE CONTRA AMT TO K120 SERS     *         
      * BP131036 14062001   RAS    TO CATER FOR NEW ALLOC CODES: SFC  *         
      *                            AND SRC                            *         
      * BP132031 03102001   MALC   SALE OF SERS FLAT TO SINGLES       *         
      * BP132118 13022002   CLT    SER-CONTRA-MISC-FEE FOR STDAPT CASE*         
      * BP132604 11102004   YCH2   EXPAND BP17F000 FROM 80 TO 200     *         
      * BP132708 07092005   GAN1   USE OF SERS TABLE (SERSCH)         *         
      * BP135364 11112013   YCH2   TO LIFT OUT THE PAID AMOUNT        *         
      *                            UPDATE SRS WHEN VR RECEIVED BY SRS *         
      * BP135887 30042015   YCH2   REMOVE UPDATE OF CONTRA            *         
      *===============================================================*         
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410 ASSIGN TO BP13F410.                                  
           SELECT BP17F000 ASSIGN TO BP17F000.                                  
           SELECT BP13K820  ASSIGN TO BP13K820                                  
                  ORGANIZATION     IS INDEXED                                   
                  ACCESS MODE      IS DYNAMIC                                   
                  RECORD KEY       IS K820-KEY-FLD                              
                  FILE STATUS      IS WS-K820-STATUS.                           
                                                                                
           SELECT P13F410O ASSIGN TO P13F410O.                                  
           SELECT BP13F415 ASSIGN TO BP13F415.                                  
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
      *------------------------*                                                
                                                                                
       FD  BP17F000    BLOCK  CONTAINS 0 RECORDS                                
                       RECORD CONTAINS 200 CHARACTERS                           
                       LABEL RECORD IS STANDARD                                 
                       RECORDING MODE IS F.                                     
       COPY BP17F000.                                                           
                                                                                
       FD  BP13F410    RECORD CONTAINS 2000 CHARACTERS                          
                       BLOCK CONTAINS 0 RECORDS                                 
                       LABEL RECORD IS STANDARD                                 
                       RECORDING MODE IS F.                                     
       COPY BP13F410.                                                           
                                                                                
       FD BP13K820    RECORD CONTAINS 400 CHARACTERS.                           
       COPY BP13K820.                                                           
                                                                                
       FD  P13F410O    RECORD CONTAINS 2000 CHARACTERS                          
                       BLOCK CONTAINS 0 RECORDS                                 
                       LABEL RECORD IS STANDARD                                 
                       RECORDING MODE IS F.                                     
       01  P13F410O-REC   PIC X(2000).                                          
                                                                                
       FD  BP13F415    RECORD CONTAINS 100 CHARACTERS                           
                       BLOCK CONTAINS 0 RECORDS                                 
                       LABEL RECORD IS STANDARD                                 
                       RECORDING MODE IS F.                                     
       COPY BP13F415.                                                           
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01 WS-FILE-FLAGS.                                                        
           05 WS-K820-STATUS       PIC XX        VALUE SPACES.                  
           05 WS-EOF-K820          PIC X(01)     VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F410-READ-CNT     PIC 9(08)     VALUE ZEROES.                  
           05 WS-F000-READ-CNT     PIC 9(08)     VALUE ZEROES.                  
           05 WS-F410-OUT-CNT      PIC 9(08)     VALUE ZEROES.                  
           05 WS-F415-OUT-CNT      PIC 9(08)     VALUE ZEROES.                  
           05 WS-F415-SRS-CNT      PIC 9(08)     VALUE ZEROES.                  
           05 WS-SRS-CNT           PIC 9(08)     VALUE ZEROES.                  
           05 WS-NRIC-CNT          PIC 9(01)     VALUE ZEROES.                  
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURRENT-CC       PIC X(2).                                    
           05  WS-CURRENT-YY       PIC X(2).                                    
           05  WS-CURRENT-MM       PIC X(2).                                    
           05  WS-CURRENT-DD       PIC X(2).                                    
                                                                                
       01  SW-SERS-ALLO-SCHEME     PIC X(01) VALUE SPACES.                      
           88  C-SERS-ALLO-SCHEME  VALUE 'Y'.                                   
                                                                                
       COPY SERSCH.                                                             
                                                                                
      *------------------------*                                                
       PROCEDURE DIVISION.                                                      
      *------------------------*                                                
                                                                                
      *----------------------------------------------------------------         
       0000-CONTROL.                                                            
      *----------------------------------------------------------------         
            PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                        
            PERFORM 2000-READ-F410       THRU 2000-EXIT.                        
            PERFORM 2100-READ-F000       THRU 2100-EXIT.                        
                                                                                
            PERFORM 3000-MAIN-ROUTINE    THRU 3000-EXIT                         
                    UNTIL F410-NUM-REGN = HIGH-VALUES                           
                     AND  F000-NUM-REGNO = HIGH-VALUES.                         
                                                                                
            PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT.                        
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *----------------------------------------------------------------         
       1000-OPEN-FILES.                                                         
      *----------------------------------------------------------------         
            OPEN INPUT  BP13F410                                                
                        BP17F000                                                
                        BP13K820                                                
                 OUTPUT P13F410O                                                
                        BP13F415.                                               
                                                                                
            IF WS-K820-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING BP13K820 ERROR ' WS-K820-STATUS                 
               MOVE     WS-K820-STATUS      TO RETURN-CODE                      
               PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                      
            END-IF.                                                             
                                                                                
            MOVE FUNCTION CURRENT-DATE (1:8) TO WS-CURRENT-DATE.                
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *----------------------------------------------------------------         
       2000-READ-F410.                                                          
      *----------------------------------------------------------------         
            READ BP13F410 AT END                                                
                 MOVE HIGH-VALUES TO F410-NUM-REGN                              
                 GO       TO 2000-EXIT.                                         
                                                                                
             ADD  1   TO WS-F410-READ-CNT.                                      
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *----------------------------------------------------------------         
       2100-READ-F000.                                                          
      *----------------------------------------------------------------         
            READ BP17F000 AT END                                                
                 MOVE HIGH-VALUES TO F000-NUM-REGNO                             
                 GO       TO 2100-EXIT.                                         
                                                                                
             ADD  1   TO WS-F000-READ-CNT.                                      
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *----------------------------------------------------------------         
       3000-MAIN-ROUTINE.                                                       
      *----------------------------------------------------------------         
            IF  F410-NUM-REGN = F000-NUM-REGNO                                  
                PERFORM 3100-PROCESS-F410 THRU 3100-EXIT                        
                PERFORM 2000-READ-F410 THRU 2000-EXIT                           
                PERFORM 2100-READ-F000 THRU 2100-EXIT                           
            ELSE                                                                
                IF F410-NUM-REGN < F000-NUM-REGNO                               
                   PERFORM 2000-READ-F410 THRU 2000-EXIT                        
                ELSE                                                            
                   PERFORM 2100-READ-F000 THRU 2100-EXIT                        
                END-IF                                                          
            END-IF.                                                             
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *----------------------------------------------------------------         
       3100-PROCESS-F410.                                                       
      *----------------------------------------------------------------         
           PERFORM 8000-CHK-SERS-ALSCH  THRU 8000-EXIT.                         
           IF C-SERS-ALLO-SCHEME                                                
              ADD 1                            TO WS-SRS-CNT                    
              MOVE ZEROES                      TO WS-NRIC-CNT                   
              PERFORM 3110-STARTBR-BP13K820  THRU 3110-EXIT                     
              PERFORM 3150-WRITE-P13F410O    THRU 3150-EXIT                     
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------------         
       3110-STARTBR-BP13K820.                                                   
      *----------------------------------------------------------------         
           MOVE SPACES                     TO BP13K820-REC.                     
           INITIALIZE                         BP13K820-REC.                     
                                                                                
           MOVE 'N'                        TO WS-EOF-K820.                      
                                                                                
           MOVE F410-NUM-REGN              TO K820-NUM-REGN.                    
                                                                                
           START BP13K820 KEY >= K820-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K820-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3120-READNEXT-BP13K820 THRU 3120-EXIT               
                    PERFORM 3130-PROCESS-BP13K820  THRU 3130-EXIT               
                      UNTIL WS-EOF-K820 = 'Y'                                   
               WHEN 20                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
                                                                                
               WHEN OTHER                                                       
                  DISPLAY 'START ERROR BP13K820 : ' WS-K820-STATUS              
                          ' REGN : ' K820-NUM-REGN                              
                  PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT               
           END-EVALUATE.                                                        
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------         
       3120-READNEXT-BP13K820.                                                  
      *----------------------------------------------------------------         
           READ BP13K820 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-EOF-K820.                        
                                                                                
       3120-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------         
       3130-PROCESS-BP13K820.                                                   
      *----------------------------------------------------------------         
           IF K820-NUM-REGN = F410-NUM-REGN                                     
              IF K820-NUM-NRIC NOT = SPACES AND LOW-VALUES                      
                 ADD 1                         TO WS-NRIC-CNT                   
                 PERFORM 3140-WRITE-BP13F415 THRU 3140-EXIT                     
              END-IF                                                            
              PERFORM 3120-READNEXT-BP13K820 THRU 3120-EXIT                     
           ELSE                                                                 
              MOVE 'Y'                         TO WS-EOF-K820                   
           END-IF.                                                              
                                                                                
       3130-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------         
       3140-WRITE-BP13F415.                                                     
      *----------------------------------------------------------------         
            MOVE SPACES               TO BP13F415-REC.                          
            INITIALIZE                   BP13F415-REC.                          
                                                                                
            MOVE K820-NUM-REGN        TO F415-NUM-REGN.                         
            MOVE K820-NUM-NRIC        TO F415-NRIC.                             
            MOVE F410-NUM-SCH-ACC     TO F415-NUM-NEWFLAT-SCHACCT.              
            MOVE F410-DTE-KEY-ISSUE   TO F415-DTE-ACCEPT.                       
                                                                                
            WRITE BP13F415-REC.                                                 
            ADD 1 TO WS-F415-OUT-CNT.                                           
                                                                                
            IF WS-NRIC-CNT = 1                                                  
               ADD 1                  TO WS-F415-SRS-CNT                        
            END-IF.                                                             
                                                                                
       3140-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3150-WRITE-P13F410O.                                                     
            WRITE P13F410O-REC FROM BP13F410-REC.                               
            ADD 1 TO WS-F410-OUT-CNT.                                           
                                                                                
       3150-EXIT.                                                               
            EXIT.                                                               
                                                                                
       8000-CHK-SERS-ALSCH.                                                     
      *-------------------------------------------------------------            
      *----------------------------------------------------------------         
      * SEARCH SERS TABLE TO CHECK ALLOC SCHEME                                 
      *----------------------------------------------------------------         
           SET WS-SERS-PTR TO 1.                                                
           SEARCH WS-SERS-SCH                                                   
              AT END                                                            
                 MOVE 'N'      TO SW-SERS-ALLO-SCHEME                           
              WHEN WS-SERS-SCHEME (WS-SERS-PTR) = F410-CDE-ALLOC-SCH            
                 MOVE 'Y'      TO SW-SERS-ALLO-SCHEME                           
           END-SEARCH.                                                          
                                                                                
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *----------------------------------------------------------------         
       9000-CLOSE-FILES.                                                        
      *----------------------------------------------------------------         
            DISPLAY '         BP13C233  '.                                      
            DISPLAY '         ~~~~~~~~  '.                                      
            DISPLAY '          '                                                
                    'CURR-DATE : '  WS-CURRENT-DATE.                            
            DISPLAY ' '.                                                        
            DISPLAY '     NO. OF RECS READ  FROM F410 -> '                      
                                                    WS-F410-READ-CNT            
            DISPLAY '     NO. OF RECS READ  FROM F000 -> '                      
                                                    WS-F000-READ-CNT            
            DISPLAY '     NO. OF RECS SRS CASES       -> '                      
                                                    WS-SRS-CNT                  
            DISPLAY '     NO. OF RECS F415 OUTPUT     -> '                      
                                                    WS-F415-OUT-CNT.            
            DISPLAY '     NO. OF CASE F415 OUTPUT     -> '                      
                                                    WS-F415-SRS-CNT.            
            DISPLAY '     NO. OF RECS F410 OUTPUT     -> '                      
                                                    WS-F410-OUT-CNT.            
                                                                                
            CLOSE BP13F410                                                      
                  BP17F000                                                      
                  BP13F415                                                      
                  BP13K820                                                      
                  P13F410O.                                                     
                                                                                
            IF WS-K820-STATUS NOT = 00                                          
               DISPLAY 'CLOSING BP13K820 ERROR ' WS-K820-STATUS                 
            END-IF.                                                             
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------- END OF PROGRAM ----------------------*             
                                                                                
