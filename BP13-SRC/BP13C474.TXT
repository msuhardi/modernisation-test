       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C474.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  23/03/2017.                                               
      * ========================================================== *            
      * OBJECTIVE    : TO EXTRACT UPDATED BP13F60A EHPS RECORDS    *            
      *                AND SYNC TO BP13K600.                       *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13F60A                               *            
      * I-O    FILES :  1.) BP13K600                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP136692 KSJ3 23/03/2017 NEW PROGRAM                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F60A  ASSIGN        TO BP13F60A.                          
                                                                                
           SELECT BP13K600  ASSIGN        TO BP13K600                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K600-KEY-FLD                       
                            FILE STATUS   IS WS-K600-STATUS.                    
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F60A                                                             
           RECORD CONTAINS 1800 CHARACTERS.                                     
           COPY BP13F60A.                                                       
                                                                                
       FD  BP13K600.                                                            
           COPY BP13K600.                                                       
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-K600-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F60A-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K600-UPD           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F60B-WRITE         PIC 9(6)   VALUE ZEROES.               
           05  WS-I                      PIC 9(2)   VALUE ZEROES.               
           05  WS-I-MAX                  PIC 9(2)   VALUE 50.                   
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F60A-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-K600-FND               PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE ZEROES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
           PERFORM 2000-BP13F60A-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F60A-EOF = 'Y'                                      
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13F60A                                                 
                I-O    BP13K600.                                                
                                                                                
           IF WS-K600-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K600 ERROR ' WS-K600-STATUS                  
              MOVE     WS-K600-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    GET CURRENT DATE                                                     
      *-------------------------------------------------------------            
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13F60A-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13F60A NEXT                                                   
              AT END MOVE 'Y'              TO   WS-F60A-EOF.                    
                                                                                
           IF WS-F60A-EOF NOT = 'Y'                                             
              ADD 1                        TO   WS-CNT-F60A-READ                
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           IF F60A-DATE-EHPS-SUB = SPACES OR LOW-VALUES OR ZEROES               
              GO TO 3000-CONTINUE-NEXT-REC.                                     
                                                                                
           MOVE SPACES                     TO   BP13K600-REC.                   
           MOVE F60A-KEY-FLD               TO   K600-KEY-FLD                    
                                                                                
           PERFORM 3010-BP13K600-READ      THRU 3010-EXIT.                      
                                                                                
           IF WS-K600-FND NOT = 'Y'                                             
              GO TO 3000-CONTINUE-NEXT-REC.                                     
                                                                                
           IF K600-DATE-EHPS-SUB NOT = SPACES AND LOW-VALUES AND                
                                       ZEROES                                   
              GO TO 3000-CONTINUE-NEXT-REC.                                     
                                                                                
           MOVE F60A-EHPS-VER              TO   K600-EHPS-VER.                  
           MOVE F60A-DATE-EHPS-SUB         TO   K600-DATE-EHPS-SUB.             
           MOVE F60A-TIME-EHPS-SUB         TO   K600-TIME-EHPS-SUB.             
           MOVE F60A-CDE-EHPS-QUE1         TO   K600-CDE-EHPS-QUE1.             
           MOVE F60A-CDE-EHPS-QUE2         TO   K600-CDE-EHPS-QUE2.             
           MOVE F60A-CDE-EHPS-QUE3         TO   K600-CDE-EHPS-QUE3.             
           MOVE F60A-TEXT-EHPS-QUE1        TO   K600-TEXT-EHPS-QUE1.            
           MOVE F60A-TEXT-EHPS-QUE2        TO   K600-TEXT-EHPS-QUE2.            
           MOVE F60A-TEXT-EHPS-QUE3        TO   K600-TEXT-EHPS-QUE3.            
           MOVE F60A-CDE-EHPS-SUB-STAT     TO   K600-CDE-EHPS-SUB-STAT.         
           MOVE F60A-NUM-HP                TO   K600-NUM-HP.                    
                                                                                
           PERFORM 3020-BP13K600-REWRITE   THRU 3020-EXIT.                      
                                                                                
       3000-CONTINUE-NEXT-REC.                                                  
                                                                                
           PERFORM 2000-BP13F60A-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3010-BP13K600-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13K600.                                                       
                                                                                
           EVALUATE WS-K600-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K600-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K600-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ BP13K600 ERROR '    WS-K600-STATUS                  
                      ' / '                     K600-KEY-FLD                    
              MOVE     WS-K600-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3020-BP13K600-REWRITE.                                                   
      *=============================================================            
                                                                                
           REWRITE BP13K600-REC.                                                
                                                                                
           EVALUATE WS-K600-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-K600-UPD                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'REWRITE BP13K600 ERROR ' WS-K600-STATUS                  
                      ' / '                     K600-KEY-FLD                    
              MOVE     WS-K600-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3020-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13F60A                                                       
                 BP13K600.                                                      
                                                                                
           IF WS-K600-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K600 ERROR ' WS-K600-STATUS                  
              MOVE     WS-K600-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C474 CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13F60A READ      : ' WS-CNT-F60A-READ.          
           DISPLAY ' 2. NO OF BP13K600 UPDATED   : ' WS-CNT-K600-UPD.           
           DISPLAY ' 3. NO OF BP13F60B WRITTEN   : ' WS-CNT-F60B-WRITE.         
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
