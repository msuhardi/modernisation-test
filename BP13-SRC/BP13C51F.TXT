       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C51F.                                                  
       AUTHOR.       KARL ANDRUS MAGALONA-CABUG.                                
       DATE-WRITTEN. 05/10/2023.                                                
      *****************************************************************         
      *                SOC - SYSTEM OF COMMITMENT (BP13)              *         
      *===============================================================*         
      *  THIS PROGRAM IS SIMILAR TO BP13C51E BUT WITH DIFFERENT INPUT *         
      *  OBJECTIVES :                                                 *         
      *     - TO EXTRACT DATA FROM FLAT_ELGBTY_CASE TABLE BY NUM-HLA  *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                                                               *         
      *                ACCESS     REC                                 *         
      * DD NAME  I/O/E MODE       LEN  KEY FIELDS    REMARKS          *         
      * -------- ----- ---------- ---- ------------- ---------------- *         
      * BP13F800 I     SEQUENTIAL 2000                                *         
      * P13VFECA I                                   FLAT_ELGBTY_CASE *         
      * BP13FECA O     SEQUENTIAL 500                                 *         
      *===============================================================*         
      * REVISION HISTORY :                                            *         
      *                                                               *         
      * CHG-REQ#  DDMMYYYY   BY   DESCRIPTION                         *         
      * --------  --------  ----  ----------------------------------- *         
      * BP139635  05102023  KAC1  NEW PROGRAM                         *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F800 ASSIGN      TO BP13F800.                             
           SELECT BP13FECA ASSIGN      TO BP13FECA.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F800                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F800.                                                           
                                                                                
       FD  BP13FECA                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       COPY BP13FECA.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-SQLCODE              PIC -9(3)       VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F800-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-VFECA-READ           PIC 9(6)        VALUE ZEROES.            
           05  WS-FECA-WRITE           PIC 9(6)        VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES-FLAGS.                                                   
           05  WS-F800-EOF             PIC X           VALUE 'N'.               
                                                                                
           EXEC SQL INCLUDE SQLCA    END-EXEC.                                  
           EXEC SQL INCLUDE P13VFECA END-EXEC.                                  
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13F800    THRU 2000-EXIT.                        
           PERFORM 3000-MAIN-PROCESSING  THRU 3000-EXIT                         
              UNTIL WS-F800-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13F800                                                 
                OUTPUT BP13FECA.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13F800.                                                      
      *****************************************************************         
                                                                                
           READ BP13F800                                                        
              AT END                                                            
                 MOVE 'Y'              TO WS-F800-EOF                           
              NOT AT END                                                        
                 ADD 1                 TO WS-F800-READ                          
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-MAIN-PROCESSING.                                                    
      *****************************************************************         
                                                                                
           MOVE F800-NUM-HLA           TO NUM-ELGBTY-CASE.                      
                                                                                
           EXEC SQL                                                             
              SELECT NUM_STATUS                                                 
                    ,TME_SUBMIT                                                 
                    ,TME_UPDATE                                                 
                    ,NUM_FAMILY_STRCTR                                          
                    ,NUM_HSHLD_TYPE                                             
                    ,AMT_HSHLD_INCOME                                           
                    ,NUM_ALCTN_SCHEME                                           
                    ,NUM_ELGBTY_SCHEME                                          
                    ,NUM_FIRST_TIMER_SECOND_TIMER                               
                    ,NUM_APLCNT_ENJOY_MAX_SBSDY                                 
                    ,NUM_LOAN_TYPE                                              
                    ,NUM_CASE_STATUS                                            
                    ,NUM_PARENT_CHILD                                           
                    ,NUM_5_YEAR_OCPTN                                           
                    ,NUM_ELDRLY                                                 
                    ,DTE_APRV                                                   
                    ,DTE_REJECT                                                 
                    ,DTE_CANCEL                                                 
                    ,NUM_RGSTRN_1                                               
                    ,NUM_RGSTRN_2                                               
                    ,NUM_RGSTRN_3                                               
                    ,NUM_RGSTRN_4                                               
                    ,NUM_RGSTRN_5                                               
                    ,NUM_RGSTRN_6                                               
                    ,NUM_FLAT_ELGBTY                                            
                    ,NUM_MULTI_NUCLEI_FAMILY                                    
                INTO :NUM-STATUS                                                
                    ,:TME-SUBMIT                                                
                    ,:TME-UPDATE                                                
                    ,:NUM-FAMILY-STRCTR                                         
                    ,:NUM-HSHLD-TYPE                                            
                    ,:AMT-HSHLD-INCOME                                          
                    ,:NUM-ALCTN-SCHEME                                          
                    ,:NUM-ELGBTY-SCHEME                                         
                    ,:NUM-FIRST-TIMER-SECOND-TIMER                              
                    ,:NUM-APLCNT-ENJOY-MAX-SBSDY                                
                    ,:NUM-LOAN-TYPE                                             
                    ,:NUM-CASE-STATUS                                           
                    ,:NUM-PARENT-CHILD                                          
                    ,:NUM-5-YEAR-OCPTN                                          
                    ,:NUM-ELDRLY                                                
                    ,:DTE-APRV                                                  
                    ,:DTE-REJECT                                                
                    ,:DTE-CANCEL                                                
                    ,:NUM-RGSTRN-1                                              
                    ,:NUM-RGSTRN-2                                              
                    ,:NUM-RGSTRN-3                                              
                    ,:NUM-RGSTRN-4                                              
                    ,:NUM-RGSTRN-5                                              
                    ,:NUM-RGSTRN-6                                              
                    ,:NUM-FLAT-ELGBTY                                           
                    ,:NUM-MULTI-NUCLEI-FAMILY                                   
                FROM FLAT_ELGBTY_CASE                                           
                WHERE NUM_ELGBTY_CASE = :NUM-ELGBTY-CASE                        
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN ZEROES                                                       
                 ADD 1                 TO WS-VFECA-READ                         
                                                                                
                 PERFORM 3100-WRITE-BP13FECA THRU 3100-EXIT                     
              WHEN +100                                                         
                 DISPLAY 'REC NOT FOUND IN FLAT_ELGBTY_CASE TABLE: '            
                                                        F800-NUM-HLA            
              WHEN OTHER                                                        
                 MOVE SQLCODE          TO WS-SQLCODE                            
                 DISPLAY 'READ ERROR P13VFECA: ' WS-SQLCODE                     
                         ' - HLA=' F800-NUM-HLA                                 
                 MOVE 99               TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2000-READ-BP13F800 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3100-WRITE-BP13FECA.                                                     
      *****************************************************************         
                                                                                
           MOVE SPACES                 TO BP13FECA-REC.                         
           INITIALIZE BP13FECA-REC.                                             
                                                                                
           MOVE F800-NUM-REGN          TO FECA-NUM-REGN.                        
           MOVE F800-NUM-HLA           TO FECA-HFE-NO.                          
           MOVE NUM-STATUS             TO FECA-HFE-STATUS.                      
           MOVE NUM-FAMILY-STRCTR      TO FECA-HFE-FAMILY-STRCTR.               
           MOVE NUM-HSHLD-TYPE         TO FECA-HFE-HH-TYPE.                     
           MOVE AMT-HSHLD-INCOME       TO FECA-HFE-HH-INCOME.                   
           MOVE NUM-ALCTN-SCHEME       TO FECA-HFE-ALCTN-SCHEME.                
           MOVE NUM-ELGBTY-SCHEME      TO FECA-HFE-ELGBTY-SCHEME.               
           MOVE NUM-FIRST-TIMER-SECOND-TIMER                                    
                                       TO FECA-HFE-FT2T.                        
           MOVE NUM-APLCNT-ENJOY-MAX-SBSDY                                      
                                       TO FECA-HFE-NTIMER.                      
           MOVE NUM-LOAN-TYPE          TO FECA-HFE-LOAN-TYPE.                   
           MOVE NUM-CASE-STATUS        TO FECA-HFE-CASE-STATUS.                 
           MOVE NUM-PARENT-CHILD       TO FECA-HFE-PARENT-CHILD.                
           MOVE NUM-5-YEAR-OCPTN       TO FECA-HFE-5YR-OCCUPN.                  
           MOVE NUM-ELDRLY             TO FECA-HFE-ELDERLY.                     
           MOVE NUM-RGSTRN-1           TO FECA-HFE-RGSTRN-1.                    
           MOVE NUM-RGSTRN-2           TO FECA-HFE-RGSTRN-2.                    
           MOVE NUM-RGSTRN-3           TO FECA-HFE-RGSTRN-3.                    
           MOVE NUM-RGSTRN-4           TO FECA-HFE-RGSTRN-4.                    
           MOVE NUM-RGSTRN-5           TO FECA-HFE-RGSTRN-5.                    
           MOVE NUM-RGSTRN-6           TO FECA-HFE-RGSTRN-6.                    
           MOVE NUM-FLAT-ELGBTY        TO FECA-HFE-FLAT-ELGBTY.                 
           MOVE NUM-MULTI-NUCLEI-FAMILY                                         
                                       TO FECA-HFE-3GEN.                        
                                                                                
           STRING TME-SUBMIT(1:4)                                               
                  TME-SUBMIT(6:2)                                               
                  TME-SUBMIT(9:2)                                               
              DELIMITED BY SIZE INTO FECA-HFE-DTE-SUBMIT.                       
                                                                                
           STRING TME-UPDATE(1:4)                                               
                  TME-UPDATE(6:2)                                               
                  TME-UPDATE(9:2)                                               
              DELIMITED BY SIZE INTO FECA-HFE-DTE-UPDATE.                       
                                                                                
           STRING DTE-APRV(1:4)                                                 
                  DTE-APRV(6:2)                                                 
                  DTE-APRV(9:2)                                                 
              DELIMITED BY SIZE INTO FECA-HFE-DTE-APRV.                         
                                                                                
           STRING DTE-REJECT(1:4)                                               
                  DTE-REJECT(6:2)                                               
                  DTE-REJECT(9:2)                                               
              DELIMITED BY SIZE INTO FECA-HFE-DTE-REJECT.                       
                                                                                
           STRING DTE-CANCEL(1:4)                                               
                  DTE-CANCEL(6:2)                                               
                  DTE-CANCEL(9:2)                                               
              DELIMITED BY SIZE INTO FECA-HFE-DTE-CANCEL.                       
                                                                                
           WRITE BP13FECA-REC.                                                  
           ADD 1                       TO WS-FECA-WRITE.                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BP13F800                                                       
                 BP13FECA.                                                      
                                                                                
           DISPLAY '******************************'.                            
           DISPLAY '* PROGRAM BP13C51F           *'.                            
           DISPLAY '* CONTROL COUNTS             *'.                            
           DISPLAY '*----------------------------*'.                            
           DISPLAY '* REC READ F800     : ' WS-F800-READ ' *'.                  
           DISPLAY '* REC READ          : ' WS-VFECA-READ ' *'.                 
           DISPLAY '* FLAT_ELGBTY_CASE           *'.                            
           DISPLAY '* REC WRITTEN FECA  : ' WS-FECA-WRITE ' *'.                 
           DISPLAY '******************************'.                            
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
