       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C885.                                                    
      *AUTHOR. RHB.                                                             
       DATE-WRITTEN.  07/08/96.                                         00040081
       DATE-COMPILED.                                                   00050061
      *REMARKS.                                                                 
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1.  TO EXTRACT PURE JB SUCC CASES UNDER JB QUOTA    *                
      *        ONLY.                                           *                
      *                                                        *                
      *    INPUT FILES : BP13F500                              *                
      *                : BP13K520                              *                
      *                                                        *                
      *    OUTPUT      : BP13F500                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE     DETAILS                         *                
      * ======   ==   ====     =============================== *                
      * BP130150 RHB  07/08/96 NEW PROGRAM                     *                
      * BP130630 NOR  19/02/99 EXPAND F500 & K520 RECL 300-500 *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F500 ASSIGN TO BP13F500.                                  
                                                                                
           SELECT P13F500O ASSIGN TO P13F500O.                                  
                                                                                
           SELECT BP13K520 ASSIGN TO BP13K520                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K520-NUM-REGN                                 
                  FILE STATUS  IS WS-K520-STATUS.                               
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F500                                                            
            RECORDING MODE  F                                                   
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F500.                                                           
                                                                                
       FD   P13F500O                                                            
            RECORDING MODE  F                                                   
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 500 CHARACTERS.                                     
                                                                                
       01   P13F500O-REC.                                                       
            05  FILLER          PIC X(500).                                     
                                                                                
       FD   BP13K520                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K520.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-K520-STATUS                 PIC 9(2)  VALUE ZEROS.            
           05  WS-F500-EOF-SW                 PIC X(1)  VALUE 'N'.              
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-F500-READ-CTR               PIC 9(5)  VALUE ZEROS.            
           05  WS-F500-WRITE-CTR              PIC 9(5)  VALUE ZEROS.            
           05  WS-K520-DUP-CTR                PIC 9(5)  VALUE ZEROS.            
           05  WS-K520-FOUND-CTR              PIC 9(5)  VALUE ZEROS.            
           05  WS-K520-NOTFND-CTR             PIC 9(5)  VALUE ZEROS.            
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-YY                  PIC 9(4).                         
               10  WS-SYS-MM                  PIC 9(2).                         
               10  WS-SYS-DD                  PIC 9(2).                         
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-MM               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-YY               PIC 9(4).                         
                                                                                
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-INITIALIZATION THRU 1000-EXIT.                          
                                                                                
           PERFORM 2000-READ-F500      THRU 2000-EXIT.                          
                                                                                
           PERFORM 3000-PROCESS-F500   THRU 3000-EXIT                           
              UNTIL WS-F500-EOF-SW = 'Y'.                                       
                                                                                
           GO TO 9999-CLOSE-FILES.                                              
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-INITIALIZATION.                                                     
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F500                                                 
                       BP13K520                                                 
                OUTPUT P13F500O.                                                
                                                                                
           IF WS-K520-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K520 OPEN ERROR, STATUS IS ' WS-K520-STATUS          
              MOVE WS-K520-STATUS TO RETURN-CODE                                
              PERFORM 9999-CLOSE-FILES                                          
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-READ-F500.                                                          
      ****************************************************************          
                                                                                
           READ BP13F500                                                        
              AT END                                                            
                 MOVE 'Y' TO WS-F500-EOF-SW                                     
                 GO  TO 2000-EXIT                                               
           END-READ.                                                            
                                                                                
           ADD  1                      TO WS-F500-READ-CTR.                     
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-F500.                                                       
      ****************************************************************          
                                                                                
           MOVE F500-NUM-REGN          TO K520-NUM-REGN.                        
                                                                                
           PERFORM 4000-PROCESS-K520 THRU 4000-EXIT.                            
                                                                                
           PERFORM 2000-READ-F500    THRU 2000-EXIT.                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4000-PROCESS-K520.                                                       
      ****************************************************************          
                                                                                
           READ BP13K520  KEY IS K520-NUM-REGN.                                 
                                                                                
           IF WS-K520-STATUS = ZEROS                                            
              ADD  +1                   TO   WS-K520-FOUND-CTR                  
           ELSE                                                                 
           IF WS-K520-STATUS = 02                                               
              ADD  +1                   TO   WS-K520-DUP-CTR                    
           ELSE                                                                 
           IF WS-K520-STATUS = 23                                               
              ADD  +1                   TO WS-K520-NOTFND-CTR                   
              PERFORM 4100-WRITE-F500O  THRU 4100-EXIT                          
           ELSE                                                                 
              DISPLAY 'BP13K520 READ ERROR, STATUS IS ' WS-K520-STATUS          
              DISPLAY 'BP13K520 RECORD KEY IS '         K520-KEY-FLD            
              GO TO 9999-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4100-WRITE-F500O.                                                        
      ****************************************************************          
                                                                                
           WRITE P13F500O-REC       FROM BP13F500-REC                           
                                                                                
           ADD   1                  TO   WS-F500-WRITE-CTR.                     
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-FILES.                                                        
      ****************************************************************          
                                                                                
           CLOSE BP13F500                                                       
                 P13F500O                                                       
                 BP13K520.                                                      
                                                                                
           IF WS-K520-STATUS NOT = 00                                           
              DISPLAY 'BP13K520 CLOSE ERROR, STATUS IS ' WS-K520-STATUS         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO WS-SYSTEM-DATE.                  
                                                                                
           MOVE   WS-SYS-DD           TO WS-EDITED-DD.                          
           MOVE   WS-SYS-MM           TO WS-EDITED-MM.                          
           MOVE   WS-SYS-YY           TO WS-EDITED-YY.                          
                                                                                
           DISPLAY 'RUN DATE : ' WS-EDITED-DATE.                                
           DISPLAY SPACES.                                                      
           DISPLAY '*=========BP13C885  CONTROL TOTALS============*'.           
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F500 RECORDS READ      = ' WS-F500-READ-CTR.            
           DISPLAY 'P13F500O RECORDS WRITE     = ' WS-F500-WRITE-CTR.           
           DISPLAY 'BP13K520 RECORDS DUPLICATE = ' WS-K520-DUP-CTR.             
           DISPLAY 'BP13K520 RECORDS FOUND     = ' WS-K520-FOUND-CTR.           
           DISPLAY 'BP13K520 RECORDS NOT FOUND = ' WS-K520-NOTFND-CTR.          
           DISPLAY SPACES.                                                      
           DISPLAY '*=============================================*'.           
                                                                                
           STOP RUN.                                                            
                                                                                
