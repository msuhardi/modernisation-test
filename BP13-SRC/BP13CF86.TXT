      ************************************************                          
       IDENTIFICATION DIVISION.                                                 
      ************************************************                          
       PROGRAM-ID.   BP13CF86.                                                  
       AUTHOR.       SMR2.                                                      
      *DATE-WRITTEN. 15/05/2013                                                 
                                                                                
      ****************************************************************          
      * SYSTEM NAME : SYSTEM OF COMMITMENT (BP13)                    *          
      *==============================================================*          
      * OBJECTIVE : BASE ON SALES SITE FILE BP13K764, RETRIEVE THE   *          
      *             SALES MODE AND DTE-BALLOT TO USE TO EXTRACT DATA *          
      *             FROM BP13K757                                    *          
      ****************************************************************          
      * REQ-NO    OIC   DATE      DESCRIPTION                        *          
      * --------  ----  --------  ---------------------------------- *          
      * BP134906  SMR2  15052013  NEW PROGRAM (SIMILAR TO BP13CK10)  *          
      * BP134996  SMR2  02082013  USE FUNCTION CURRENT-DATE          *          
      * BP136852  SMR2  22062017  CATER FOR SALES-MODE = 'ROF'       *          
      * BP139280  EAA2  30102023  EXPAND BP13K757 FROM 3000 TO 4000  *          
      ****************************************************************          
       ENVIRONMENT DIVISION.                                                    
      ************************************************                          
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K764  ASSIGN        TO BP13K764                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K764-KEY-FLD                       
                            FILE STATUS   IS WS-K764-STATUS.                    
                                                                                
           SELECT BP13K757  ASSIGN        TO BP13K757                           
                            ACCESS MODE   IS SEQUENTIAL                         
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K757-KEY-FLD                       
                            FILE STATUS   IS WS-K757-STATUS.                    
                                                                                
           SELECT SY02F001  ASSIGN       TO SY02F001.                           
           SELECT BP13F757  ASSIGN       TO BP13F757.                           
           SELECT BP13LF86  ASSIGN       TO BP13LF86.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13K764                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K764.                                                           
                                                                                
       FD  BP13K757                                                             
           RECORD CONTAINS 4000 CHARACTERS.                                     
       COPY BP13K757.                                                           
                                                                                
       FD   BP13F757                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 4000 CHARACTERS.                                    
       COPY BP13F757.                                                           
                                                                                
       FD   BP13LF86                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 133 CHARACTERS.                                     
       01   BP13LF86-REC               PIC X(133).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-STATUS.                                                           
           05 WS-K764-STATUS               PIC 9(02)  VALUE ZEROES.             
           05 WS-K757-STATUS               PIC 9(02)  VALUE ZEROES.             
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-K757-READ                     PIC 9(08) VALUE ZEROES.          
           05 WS-K757-MR-BTO                   PIC 9(08) VALUE ZEROES.          
           05 WS-K757-MR-SBF                   PIC 9(08) VALUE ZEROES.          
           05 WS-K757-MR-ROF                   PIC 9(08) VALUE ZEROES.          
           05 WS-F757-WRITE                    PIC 9(08) VALUE ZEROES.          
                                                                                
       01  WS-FLAGS.                                                            
           05 WS-K757-EOF                      PIC X(01) VALUE 'N'.             
           05 WS-EXTRACT-BTO                   PIC X(01) VALUE 'N'.             
           05 WS-EXTRACT-SBF                   PIC X(01) VALUE 'N'.             
           05 WS-EXTRACT-ROF                   PIC X(01) VALUE 'N'.             
           05 WS-WITHIN-CUTOFF                 PIC X(01) VALUE 'N'.             
                                                                                
       01  WS-CURR-DATE.                                                        
           05 WS-CURRENT-DATE                  PIC 9(8).                        
           05 WS-CURRENT-TIME                  PIC 9(8).                        
                                                                                
       01  WS-LF86-REC.                                                         
           05 FILLER                           PIC X(5)  VALUE SPACES.          
           05 WS-LF86-DESC                     PIC X(80) VALUE SPACES.          
           05 FILLER                           PIC X(15) VALUE SPACES.          
                                                                                
       01  WS-K764-BTO-DATA.                                                    
           05 WS-BTO-SALES-MODE                PIC X(3).                        
           05 WS-BTO-DTE-BALLOT                PIC X(6).                        
           05 WS-BTO-DTE-SALES-START           PIC X(8).                        
           05 WS-BTO-DTE-SALES-END             PIC X(8).                        
                                                                                
       01  WS-K764-SBF-DATA.                                                    
           05 WS-SBF-SALES-MODE                PIC X(3).                        
           05 WS-SBF-DTE-BALLOT                PIC X(6).                        
           05 WS-SBF-DTE-SALES-START           PIC X(8).                        
           05 WS-SBF-DTE-SALES-END             PIC X(8).                        
                                                                                
       01  WS-K764-ROF-DATA.                                                    
           05 WS-ROF-SALES-MODE                PIC X(3).                        
           05 WS-ROF-DTE-BALLOT                PIC X(6).                        
           05 WS-ROF-DTE-SALES-START           PIC X(8).                        
           05 WS-ROF-DTE-SALES-END             PIC X(8).                        
                                                                                
       01  WS-DTE-SALES-END-INT                PIC 9(8).                        
       01  WS-INTEGER                          PIC 9(7).                        
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
                                                                                
           PERFORM 0500-OPEN-FILES        THRU 0500-EXIT.                       
                                                                                
           MOVE 'N'                       TO WS-EXTRACT-BTO                     
                                             WS-EXTRACT-SBF                     
                                             WS-EXTRACT-ROF.                    
                                                                                
           MOVE 'BTO'                     TO K764-NUM-SALES-MODE.               
           PERFORM 1000-READ-BP13K764     THRU 1000-EXIT.                       
           MOVE 'SBF'                     TO K764-NUM-SALES-MODE.               
           PERFORM 1000-READ-BP13K764     THRU 1000-EXIT.                       
           MOVE 'ROF'                     TO K764-NUM-SALES-MODE.               
           PERFORM 1000-READ-BP13K764     THRU 1000-EXIT.                       
                                                                                
           IF WS-EXTRACT-BTO = 'Y' OR WS-EXTRACT-SBF = 'Y' OR                   
              WS-EXTRACT-ROF = 'Y'                                              
              PERFORM 3000-EXTRACT-K757  THRU 3000-EXIT                         
                      UNTIL WS-K757-EOF = 'Y'                                   
           END-IF.                                                              
                                                                                
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       0500-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT BP13K764                                                  
                      BP13K757                                                  
                      SY02F001                                                  
               OUTPUT BP13F757                                                  
                      BP13LF86.                                                 
                                                                                
           IF WS-K764-STATUS  NOT =  00 AND 97                                  
              DISPLAY 'ERROR OPENING - BP13K764 : ' WS-K764-STATUS              
              MOVE     WS-K764-STATUS          TO RETURN-CODE                   
              PERFORM  9000-CLOSE-FILES        THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF WS-K757-STATUS  NOT =  00 AND 97                                  
              DISPLAY 'ERROR OPENING - BP13K757 : ' WS-K757-STATUS              
              MOVE     WS-K757-STATUS          TO RETURN-CODE                   
              PERFORM  9000-CLOSE-FILES        THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:16) TO WS-CURR-DATE.                    
                                                                                
           READ SY02F001.                                                       
                                                                                
       0500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-READ-BP13K764.                                                      
      *---------------------------------------------------------------*         
           READ BP13K764.                                                       
           EVALUATE WS-K764-STATUS                                              
           WHEN 00                                                              
                EVALUATE K764-NUM-SALES-MODE                                    
                WHEN 'BTO'                                                      
                   PERFORM 1100-RETRIEVE-BTO-DATA THRU 1100-EXIT                
                WHEN 'SBF'                                                      
                   PERFORM 1200-RETRIEVE-SBF-DATA THRU 1200-EXIT                
                WHEN 'ROF'                                                      
                   PERFORM 1300-RETRIEVE-ROF-DATA THRU 1300-EXIT                
                END-EVALUATE                                                    
           WHEN 23                                                              
                DISPLAY 'BP13K764 REC NOT FOUND: ' K764-NUM-SALES-MODE          
           WHEN OTHER                                                           
                DISPLAY 'BP13K764 READ ERROR : ' WS-K764-STATUS                 
                DISPLAY 'K764-NUM-SALES-MODE : ' K764-NUM-SALES-MODE            
                MOVE WS-K764-STATUS       TO RETURN-CODE                        
                PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1100-RETRIEVE-BTO-DATA.                                                  
      *-------------------------------------------------------------            
           MOVE K764-NUM-SALES-MODE  TO WS-BTO-SALES-MODE.                      
           MOVE K764-DTE-BALLOT      TO WS-BTO-DTE-BALLOT.                      
           MOVE K764-DTE-SALES-START TO WS-BTO-DTE-SALES-START.                 
           MOVE K764-DTE-SALES-END   TO WS-DTE-SALES-END-INT.                   
                                                                                
           COMPUTE WS-INTEGER =                                                 
                   FUNCTION INTEGER-OF-DATE(WS-DTE-SALES-END-INT).              
           ADD 14 TO WS-INTEGER.                                                
           COMPUTE WS-DTE-SALES-END-INT =                                       
                   FUNCTION DATE-OF-INTEGER(WS-INTEGER).                        
           MOVE WS-DTE-SALES-END-INT TO WS-BTO-DTE-SALES-END.                   
                                                                                
           IF (WS-CURRENT-DATE >= WS-BTO-DTE-SALES-START) AND                   
              (WS-CURRENT-DATE <= WS-BTO-DTE-SALES-END)                         
              MOVE 'Y' TO WS-EXTRACT-BTO                                        
           ELSE                                                                 
              MOVE 'N' TO WS-EXTRACT-BTO                                        
           END-IF.                                                              
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1200-RETRIEVE-SBF-DATA.                                                  
      *-------------------------------------------------------------            
           MOVE K764-NUM-SALES-MODE  TO WS-SBF-SALES-MODE.                      
           MOVE K764-DTE-BALLOT      TO WS-SBF-DTE-BALLOT.                      
           MOVE K764-DTE-SALES-START TO WS-SBF-DTE-SALES-START.                 
           MOVE K764-DTE-SALES-END   TO WS-DTE-SALES-END-INT.                   
                                                                                
           COMPUTE WS-INTEGER =                                                 
                   FUNCTION INTEGER-OF-DATE(WS-DTE-SALES-END-INT).              
           ADD 14 TO WS-INTEGER.                                                
           COMPUTE WS-DTE-SALES-END-INT =                                       
                   FUNCTION DATE-OF-INTEGER(WS-INTEGER).                        
           MOVE WS-DTE-SALES-END-INT TO WS-SBF-DTE-SALES-END.                   
                                                                                
           IF (WS-CURRENT-DATE >= WS-SBF-DTE-SALES-START) AND                   
              (WS-CURRENT-DATE <= WS-SBF-DTE-SALES-END)                         
              MOVE 'Y' TO WS-EXTRACT-SBF                                        
           ELSE                                                                 
              MOVE 'N' TO WS-EXTRACT-SBF                                        
           END-IF.                                                              
                                                                                
       1200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1300-RETRIEVE-ROF-DATA.                                                  
      *-------------------------------------------------------------            
           MOVE K764-NUM-SALES-MODE  TO WS-ROF-SALES-MODE.                      
           MOVE K764-DTE-BALLOT      TO WS-ROF-DTE-BALLOT.                      
           MOVE K764-DTE-SALES-START TO WS-ROF-DTE-SALES-START.                 
           MOVE K764-DTE-SALES-END   TO WS-DTE-SALES-END-INT.                   
                                                                                
           COMPUTE WS-INTEGER =                                                 
                   FUNCTION INTEGER-OF-DATE(WS-DTE-SALES-END-INT).              
           ADD 14 TO WS-INTEGER.                                                
           COMPUTE WS-DTE-SALES-END-INT =                                       
                   FUNCTION DATE-OF-INTEGER(WS-INTEGER).                        
           MOVE WS-DTE-SALES-END-INT TO WS-ROF-DTE-SALES-END.                   
                                                                                
           IF (WS-CURRENT-DATE >= WS-ROF-DTE-SALES-START) AND                   
              (WS-CURRENT-DATE <= WS-ROF-DTE-SALES-END)                         
              MOVE 'Y' TO WS-EXTRACT-ROF                                        
           ELSE                                                                 
              MOVE 'N' TO WS-EXTRACT-ROF                                        
           END-IF.                                                              
                                                                                
       1300-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3000-EXTRACT-K757.                                                       
      *-------------------------------------------------------------            
           READ BP13K757 AT END                                                 
                         MOVE 'Y' TO WS-K757-EOF                                
                         GO TO 3000-EXIT.                                       
                                                                                
           IF WS-K757-STATUS NOT = ZEROES                                       
              DISPLAY 'BP13K757 READ ERROR, STATUS : ' WS-K757-STATUS           
              MOVE     WS-K757-STATUS          TO RETURN-CODE                   
              PERFORM  9000-CLOSE-FILES        THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF K757-DTE-REQUESTED <= WS-CURRENT-DATE                             
              MOVE 'Y' TO WS-WITHIN-CUTOFF                                      
           ELSE                                                                 
              MOVE 'N' TO WS-WITHIN-CUTOFF                                      
           END-IF.                                                              
                                                                                
           IF WS-K757-EOF = 'N'                                                 
            ADD 1 TO WS-K757-READ                                               
            IF WS-WITHIN-CUTOFF = 'Y'                                           
               EVALUATE K757-NUM-SALES-MODE                                     
               WHEN 'BTO'                                                       
                 IF (WS-BTO-DTE-BALLOT = K757-DTE-BALLOT) AND                   
                    (WS-EXTRACT-BTO = 'Y') AND                                  
                    (K757-DTE-REQUESTED >= WS-BTO-DTE-SALES-START) AND          
                    (K757-DTE-REQUESTED <= WS-BTO-DTE-SALES-END)                
                     ADD 1 TO WS-K757-MR-BTO                                    
                     WRITE BP13F757-REC FROM BP13K757-REC                       
                     ADD 1 TO WS-F757-WRITE                                     
                 END-IF                                                         
               WHEN 'SBF'                                                       
                 IF (WS-SBF-DTE-BALLOT = K757-DTE-BALLOT) AND                   
                    (WS-EXTRACT-SBF = 'Y') AND                                  
                    (K757-DTE-REQUESTED >= WS-SBF-DTE-SALES-START) AND          
                    (K757-DTE-REQUESTED <= WS-SBF-DTE-SALES-END)                
                    ADD 1 TO WS-K757-MR-SBF                                     
                    WRITE BP13F757-REC FROM BP13K757-REC                        
                    ADD 1 TO WS-F757-WRITE                                      
                 END-IF                                                         
               WHEN 'ROF'                                                       
                 IF (WS-ROF-DTE-BALLOT = K757-DTE-BALLOT) AND                   
                    (WS-EXTRACT-ROF = 'Y') AND                                  
                    (K757-DTE-REQUESTED >= WS-ROF-DTE-SALES-START) AND          
                    (K757-DTE-REQUESTED <= WS-ROF-DTE-SALES-END)                
                    ADD 1 TO WS-K757-MR-ROF                                     
                    WRITE BP13F757-REC FROM BP13K757-REC                        
                    ADD 1 TO WS-F757-WRITE                                      
                 END-IF                                                         
              END-EVALUATE                                                      
            END-IF                                                              
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
           MOVE SPACES TO WS-LF86-REC                                           
           MOVE    'PROGRAM-ID              :BP13CF86'                          
                    TO WS-LF86-DESC.                                            
           WRITE BP13LF86-REC FROM WS-LF86-REC.                                 
                                                                                
           MOVE SPACES TO WS-LF86-REC                                           
           STRING  'DATE & TME PROCESS      :' WS-CURR-DATE                     
                    DELIMITED BY SIZE                                           
                    INTO WS-LF86-DESC.                                          
           WRITE BP13LF86-REC FROM WS-LF86-REC.                                 
                                                                                
           MOVE SPACES TO WS-LF86-REC                                           
           WRITE BP13LF86-REC FROM WS-LF86-REC.                                 
                                                                                
           MOVE SPACES TO WS-LF86-REC                                           
           STRING  'NO OF BP13K757 READ     :' WS-K757-READ                     
                    DELIMITED BY SIZE                                           
                    INTO WS-LF86-DESC.                                          
           WRITE BP13LF86-REC FROM WS-LF86-REC.                                 
                                                                                
           MOVE SPACES TO WS-LF86-REC                                           
           STRING  'NO OF BP13K757 BTO CASE :' WS-K757-MR-BTO                   
                    DELIMITED BY SIZE                                           
                    INTO WS-LF86-DESC.                                          
           WRITE BP13LF86-REC FROM WS-LF86-REC.                                 
                                                                                
           MOVE SPACES TO WS-LF86-REC                                           
           STRING  'NO OF BP13K757 SBF CASE :' WS-K757-MR-SBF                   
                    DELIMITED BY SIZE                                           
                    INTO WS-LF86-DESC.                                          
           WRITE BP13LF86-REC FROM WS-LF86-REC.                                 
                                                                                
           MOVE SPACES TO WS-LF86-REC                                           
           STRING  'NO OF BP13K757 ROF CASE :' WS-K757-MR-ROF                   
                    DELIMITED BY SIZE                                           
                    INTO WS-LF86-DESC.                                          
           WRITE BP13LF86-REC FROM WS-LF86-REC.                                 
                                                                                
           MOVE SPACES TO WS-LF86-REC                                           
           STRING  'NO OF BP13F757 WRITE    :' WS-F757-WRITE                    
                    DELIMITED BY SIZE                                           
                    INTO WS-LF86-DESC.                                          
           WRITE BP13LF86-REC FROM WS-LF86-REC.                                 
                                                                                
           MOVE SPACES TO WS-LF86-REC                                           
           WRITE BP13LF86-REC FROM WS-LF86-REC.                                 
                                                                                
           MOVE SPACES TO WS-LF86-REC                                           
           MOVE FUNCTION CURRENT-DATE(1:16) TO WS-CURR-DATE.                    
           STRING  'TIME END PROCESSED      :' WS-CURR-DATE                     
                    DELIMITED BY SIZE                                           
                    INTO WS-LF86-DESC.                                          
           WRITE BP13LF86-REC FROM WS-LF86-REC.                                 
                                                                                
           CLOSE BP13K764                                                       
                 BP13K757                                                       
                 SY02F001                                                       
                 BP13F757                                                       
                 BP13LF86.                                                      
                                                                                
           IF WS-K764-STATUS  NOT =  00 AND 97                                  
              DISPLAY 'ERROR CLOSING - BP13K764 : ' WS-K764-STATUS              
              MOVE     WS-K764-STATUS            TO RETURN-CODE                 
           END-IF.                                                              
                                                                                
           IF WS-K757-STATUS  NOT =  00 AND 97                                  
              DISPLAY 'ERROR CLOSING - BP13K757 : ' WS-K757-STATUS              
              MOVE     WS-K757-STATUS            TO RETURN-CODE                 
           END-IF.                                                              
                                                                                
           IF WS-EXTRACT-BTO = 'N' AND WS-EXTRACT-SBF = 'N' AND                 
              WS-EXTRACT-ROF = 'N'                                              
              MOVE 99 TO RETURN-CODE                                            
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
