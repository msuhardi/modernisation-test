      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C39R.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  DECEMBER 19, 2017.                                        
      *===============================================================*         
      * OBJECTIVE  :  REPORT ON TP CASES                              *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F410                                     *         
      *                                                                         
      * OUTPUT FILE:  1. BP13L39R                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP137140 18/12/2017 PCL4 NEW PROGRAM.                         *         
      * BP137142 21/12/2017 PCL4 TO ADD DISPLAY ERROR MSSG            *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F410  ASSIGN        TO BP13F410.                         
                                                                                
           SELECT  BM06K110  ASSIGN       TO BM06K110                           
                             ACCESS MODE  IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K110-KEY-FLD OF K110-REC           
                             FILE STATUS  IS WS-K110-STATUS.                    
                                                                                
           SELECT  BP13K800  ASSIGN       TO BP13K800                           
                             ACCESS MODE  IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K800-NUM-REGN                      
                             FILE STATUS  IS WS-K800-STATUS.                    
                                                                                
           SELECT  BP13K893  ASSIGN       TO BP13K893                           
                             ACCESS MODE  IS DYNAMIC                            
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K893-KEY-FLD                       
                             FILE STATUS  IS WS-K893-STATUS.                    
                                                                                
           SELECT  BP13L39R  ASSIGN        TO BP13L39R.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F410                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F410.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP13L39R                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS  133 CHARACTERS.                                    
       01   BP13L39R-REC               PIC X(132).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F410             PIC X(1)  VALUE 'N'.                     
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K893-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K110-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-ADDRESS.                                                          
           05 WS-BLK-NO                PIC X(05) VALUE SPACES.                  
           05 FILLER                   PIC X     VALUE SPACES.                  
           05 WS-FLAT-NO.                                                       
              10 FILLER                PIC X     VALUE '#'.                     
              10 WS-LVL-NO             PIC X(2)  VALUE SPACES.                  
              10 FILLER                PIC X     VALUE '-'.                     
              10 WS-UNIT-NO            PIC X(4)  VALUE SPACES.                  
           05 FILLER                   PIC X(1)  VALUE SPACES.                  
           05 WS-STREET                PIC X(25) VALUE SPACES.                  
           05 WS-SINGAPORE             PIC X(01) VALUE 'S'.                     
           05 FILLER                   PIC X(1)  VALUE SPACES.                  
           05 WS-OPEN                  PIC X(01) VALUE '('.                     
           05 WS-POSTAL-CODE           PIC X(6)  VALUE SPACES.                  
           05 WS-CLOSE                 PIC X(01) VALUE ')'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                    PIC 9(1)  VALUE ZEROES.                  
           05  WS-F410-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
           05  WS-CNT-L39R-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(5)  VALUE 2.                       
           05  WS-CNT-BYPASS           PIC 9(7)  VALUE ZEROES.                  
           05  WS-MAX-LINE             PIC 9(2)  VALUE 55.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-SNO-ZZZZ9             PIC ZZZZ9 VALUE ZEROES.                  
           05 WS-FIRST-WRITE           PIC X(1)  VALUE 'Y'.                     
           05 WS-LODGE-FLAG            PIC X(1)  VALUE 'N'.                     
                                                                                
       01 WS-PRIO-DTE.                                                          
          05  WS-PRIO-CCYY                 PIC 9(04).                           
          05  WS-PRIO-MM                   PIC 9(02).                           
          05  WS-PRIO-DD                   PIC 9(02).                           
                                                                                
       01 WS-CA-DTE.                                                            
          05  WS-CA-CCYY                   PIC 9(04).                           
          05  WS-CA-MM                     PIC 9(02).                           
          05  WS-CA-DD                     PIC 9(02).                           
                                                                                
       01 WS-CURR-DATE.                                                         
          05  WS-CURR-CCYY                 PIC 9(04).                           
          05  WS-CURR-MM                   PIC 9(02).                           
          05  WS-CURR-DD                   PIC 9(02).                           
                                                                                
       01  L39R-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13L39R'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  L39R-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L39R-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  L39R-HEADER02.                                                       
           05  FILLER              PIC X(22)    VALUE SPACES.                   
           05  FILLER              PIC X(31)     VALUE                          
                   'REPORT ON TP CASES             '.                           
                                                                                
       01  L39R-HEADER03.                                                       
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(05)    VALUE '  S/N'.                  
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(08)    VALUE 'REGN NO '.               
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(02)    VALUE 'LT'.                     
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(02)    VALUE 'FT'.                     
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(10)    VALUE 'DTE-BK'.                 
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(10)    VALUE 'DTE-TP'.                 
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(50)    VALUE 'FLAT ADDRESS'.           
                                                                                
       01  L39R-DETAIL01.                                                       
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L39R-SN             PIC X(05)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L39R-REGN           PIC X(08)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L39R-LT             PIC X(02)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L39R-FT             PIC X(02)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L39R-DTE-BK         PIC X(10)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L39R-DTE-TP         PIC X(10)        VALUE SPACES.               
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  L39R-FLAT-ADDRESS   PIC X(50)        VALUE SPACES.               
                                                                                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
       01  WS-LINK-BP13C913.                                                    
           05  LINK-NUM-SCH-ACC.                                                
               10  LINK-NUM-SCH        PIC X(4).                                
               10  LINK-NUM-ACC        PIC X(5).                                
                                                                                
      *-------------------------------------------------------*                 
      *    LINKAGE VARIABLES                                  *                 
      *-------------------------------------------------------*                 
       01  BB14C187-AREA.                                                       
           05  C187-INPUT.                                                      
               10  C187-NUM-SCH-ACCT       PIC X(9).                            
           05  C187-OUTPUT.                                                     
               10  C187-NUM-CODE-STATUS    PIC X(1).                            
               10  C187-NUM-EST-ORGNL      PIC X(4).                            
               10  C187-NUM-NGH-ORGNL      PIC X(8).                            
               10  C187-NUM-CNTRCT-ORGNL   PIC X(6).                            
               10  C187-DTE-UPDATE         PIC X(26).                           
               10  C187-CDE-SYSERR         PIC S9(3).                           
               10  C187-SYSERR-MESG        PIC X(43).                           
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F410      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F410 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F410                                                 
                       BM06K110                                                 
                       BP13K800                                                 
                       BP13K893                                                 
              OUTPUT   BP13L39R.                                                
                                                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K893 - ERROR OPENING : ' WS-K893-STATUS              
              MOVE WS-K893-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR OPENING : ' WS-K110-STATUS              
              MOVE WS-K110-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 6000-PRINT-HEADER       THRU 6000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F410.                                                      
      ******************************************************************        
                                                                                
           READ BP13F410           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F410                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F410-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
                                                                                
           IF F410-DTE-KEY-ISSUE NOT = SPACES AND LOW-VALUES AND ZEROES         
              PERFORM 6200-CALL-BB14C187              THRU 6200-EXIT            
                                                                                
              IF WS-LODGE-FLAG  = 'Y'                                           
                 PERFORM 3100-READ-BM06K110           THRU 3100-EXIT            
                 PERFORM 3200-READ-BP13K800           THRU 3200-EXIT            
                                                                                
                 PERFORM 4000-PRINT-REPORT            THRU 4000-EXIT            
              ELSE                                                              
                 ADD 1                                TO WS-CNT-BYPASS          
              END-IF                                                            
           ELSE                                                                 
              ADD 1                                   TO WS-CNT-BYPASS          
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F410               THRU 2000-EXIT.             
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-READ-BM06K110.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                   TO K110-REC.                           
           INITIALIZE                       K110-REC.                           
                                                                                
           MOVE F410-NUM-SCH-ACC         TO K110-KEY-FLD OF K110-REC.           
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
                                                                                
               WHEN 23                                                          
                    MOVE SPACES                           TO K110-REC           
                    INITIALIZE                               K110-REC           
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BM06K110 : ' WS-K110-STATUS          
                            ' KEY  = ' K110-KEY-FLD OF K110-REC                 
                    PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3200-READ-BP13K800.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                   TO BP13K800-MASTER.                    
           INITIALIZE                       BP13K800-MASTER.                    
                                                                                
           MOVE F410-NUM-REGN            TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
                                                                                
               WHEN 23                                                          
                    PERFORM 3300-START-BP13K893   THRU 3300-EXIT                
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K800 : ' WS-K800-STATUS          
                            ' KEY  = ' K800-NUM-REGN                            
                    PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT             
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3300-START-BP13K893.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                           TO BP13K893-MASTER             
                                                    BP13K800-MASTER.            
           INITIALIZE                               BP13K893-MASTER             
                                                    BP13K800-MASTER.            
                                                                                
           MOVE SPACES                           TO K893-KEY-FLD.               
           MOVE F410-NUM-REGN                    TO K893-NUM-REGN-HIST.         
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K893-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 3400-READ-BP13K893  THRU 3400-EXIT                  
                                                                                
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K893: ' WS-K893-STATUS          
                            ' REGN = ' F410-NUM-REGN                            
                    PERFORM  9999-CLOSE-ROUTINE THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3400-READ-BP13K893.                                                      
      ******************************************************************        
                                                                                
            READ  BP13K893 NEXT AT END                                          
                  MOVE SPACES               TO K893-KEY-FLD.                    
                                                                                
            EVALUATE WS-K893-STATUS                                             
                WHEN 00                                                         
                WHEN 02                                                         
                     IF K893-NUM-REGN-HIST = F410-NUM-REGN                      
                        MOVE BP13K893-MASTER(1:2000) TO BP13K800-MASTER         
                     END-IF                                                     
                WHEN 23                                                         
                     CONTINUE                                                   
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K893 : ' WS-K893-STATUS         
                             ' REGN = ' K893-NUM-REGN-HIST                      
                     PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                
            END-EVALUATE.                                                       
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-PRINT-REPORT.                                                       
      ******************************************************************        
                                                                                
           MOVE SPACES                    TO L39R-DETAIL01.                     
           INITIALIZE                        L39R-DETAIL01.                     
                                                                                
           ADD 1                          TO WS-SNO.                            
           MOVE WS-SNO                    TO WS-SNO-ZZZZ9.                      
           MOVE WS-SNO-ZZZZ9              TO L39R-SN.                           
           MOVE F410-NUM-REGN             TO L39R-REGN.                         
           MOVE K800-NUM-LOAN-TAG         TO L39R-LT.                           
           MOVE K800-NUM-FLAT-TYPE        TO L39R-FT.                           
                                                                                
           IF F410-DTE-KEY-ISSUE NOT = SPACES AND LOW-VALUES AND ZEROES         
              STRING F410-DTE-KEY-ISSUE(7:2) '/'                                
                     F410-DTE-KEY-ISSUE(5:2) '/'                                
                     F410-DTE-KEY-ISSUE(1:4)                                    
                   DELIMITED BY SIZE INTO L39R-DTE-TP                           
           END-IF.                                                              
                                                                                
           IF K110-DTE-ACCEPTANCE NOT = SPACES AND LOW-VALUES                   
                                               AND ZEROES                       
              STRING K110-DTE-ACCEPTANCE(7:2) '/'                               
                     K110-DTE-ACCEPTANCE(5:2) '/'                               
                     K110-DTE-ACCEPTANCE(1:4)                                   
                   DELIMITED BY SIZE INTO L39R-DTE-BK                           
           END-IF.                                                              
                                                                                
           PERFORM 6100-CALL-BP13C913         THRU 6100-EXIT.                   
                                                                                
           WRITE BP13L39R-REC                 FROM L39R-DETAIL01.               
           ADD 1                                TO WS-CNT-L39R-WRITE.           
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6000-PRINT-HEADER.                                                       
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)      TO WS-SYSTEM-DATE.              
                                                                                
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
              DELIMITED BY SIZE INTO L39R-DATE-HDR.                             
                                                                                
           ADD 1                               TO WS-PAGE-CTR.                  
           MOVE ZEROES                         TO L39R-PAGE-HDR.                
           MOVE WS-PAGE-CTR                    TO L39R-PAGE-HDR.                
                                                                                
            WRITE BP13L39R-REC FROM L39R-HEADER01.                              
            WRITE BP13L39R-REC FROM L39R-HEADER02.                              
                                                                                
            MOVE SPACES TO BP13L39R-REC.                                        
            WRITE BP13L39R-REC.                                                 
                                                                                
            WRITE BP13L39R-REC FROM L39R-HEADER03.                              
                                                                                
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6100-CALL-BP13C913.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                       TO BP13COMM8-REC.                  
           INITIALIZE                           BP13COMM8-REC.                  
                                                                                
           MOVE SPACES                       TO WS-BLK-NO                       
                                                WS-LVL-NO                       
                                                WS-UNIT-NO                      
                                                WS-STREET                       
                                                WS-POSTAL-CODE.                 
                                                                                
                                                                                
           MOVE F410-NUM-SCH-ACC             TO LINK-NUM-SCH-ACC.               
                                                                                
           CALL 'BP13C913' USING WS-LINK-BP13C913,                              
                                 BP13COMM8-REC.                                 
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE'                       
                         COMM8-CDE-SYSERR                                       
              ELSE                                                              
                 DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR             
              END-IF                                                            
              MOVE SPACES        TO BP13COMM8-REC                               
           END-IF.                                                              
                                                                                
           MOVE COMM8-NUM-BLK                TO WS-BLK-NO.                      
           MOVE COMM8-NUM-LEVEL              TO WS-LVL-NO.                      
           MOVE COMM8-NUM-UNIT-MAIN          TO WS-UNIT-NO.                     
           MOVE COMM8-NME-STREET             TO WS-STREET.                      
           MOVE COMM8-NUM-POSTAL-CODE        TO WS-POSTAL-CODE.                 
                                                                                
           MOVE WS-ADDRESS                   TO L39R-FLAT-ADDRESS.              
                                                                                
       6100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       6200-CALL-BB14C187.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                 TO BB14C187-AREA.                        
           INITIALIZE                     BB14C187-AREA.                        
                                                                                
           MOVE F410-NUM-SCH-ACC       TO C187-NUM-SCH-ACCT.                    
           MOVE SPACES                 TO WS-LODGE-FLAG.                        
                                                                                
           CALL 'BB14C187' USING BB14C187-AREA.                                 
                                                                                
           EVALUATE C187-CDE-SYSERR                                             
               WHEN 0                                                           
                    MOVE  'Y'   TO WS-LODGE-FLAG                                
               WHEN 100                                                         
               WHEN 023                                                         
               WHEN 999                                                         
                    CONTINUE                                                    
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BB14 ERROR : ' C187-CDE-SYSERR                     
           END-EVALUATE.                                                        
                                                                                
                                                                                
       6200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C39R'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F410 RECORDS READ............. : '            
                    WS-F410-READ.                                               
           DISPLAY '(2) NO OF BP13L39R RECORDS WRITTEN.......... : '            
                    WS-CNT-L39R-WRITE.                                          
           DISPLAY '(3) NO OF BP13F410 RECORDS BYPASS .......... : '            
                    WS-CNT-BYPASS.                                              
                                                                                
           CLOSE    BP13F410                                                    
                    BP13K800                                                    
                    BP13K893                                                    
                    BM06K110                                                    
                    BP13L39R.                                                   
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR CLOSING : ' WS-K800-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K893 - ERROR CLOSING : ' WS-K893-STATUS              
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR CLOSING : ' WS-K110-STATUS              
           END-IF.                                                              
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
