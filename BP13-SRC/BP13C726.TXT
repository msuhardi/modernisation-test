       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13C726.                                                  
       AUTHOR.       JAI.                                                       
      *DATE-WRITTEN. 07.01.95.                                                  
      ************************************************************              
      *  SYSTEM NAME : SYSTEM OF COMMITMENT                      *              
      *  SYSTEM ID   : BP13                                      *              
      *  OBJECTIVE   : TO UPDATE CATEGORY                        *              
      *  BE01F515    : APPLICANT/OCCUPIER INFO FROM MHA          *              
      *  BP13K825    : SOC MASTER'S OCCUPIER FILE                *              
      *  P13L726B    : ERROR LIST FOR INVALID INFO ENCOUNTERED   *              
      *                                                          *              
      * CHG-NO  BY   DATE    DESCRIPTION                         *              
      * ------  ---  ------  ---------------------------------   *              
      * BP130418 SIA 9810--  Y2K CHANGES                         *              
      * BP130652 NOR 990219  EXPAND K500 & K730 RECL FR 300 - 500*              
      * BP132376 MJ16030729  CHANGE BP13F726 TO BE01F515                        
      * BP133010 ED1 070404  ADD BP13K800 REWRITE                               
      * BP132887 BA2 070710  CHANGE THE LENGTH OF BE01F515                      
      * BP138285 AM25200515  CHANGE REPORT ID TO BP13L726 FROM P13L726B         
      ************************************************************              
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BE01F515 ASSIGN       TO BE01F515.                            
                                                                                
           SELECT BP13K500 ASSIGN       TO BP13K500                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K500-KEY-NEW                         
                           FILE STATUS  IS K500-STATUS.                         
                                                                                
           SELECT BP13K825 ASSIGN       TO BP13K825                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS DYNAMIC                              
                           RECORD KEY   IS K825-KEY-FLD                         
                           FILE STATUS  IS K825-STATUS.                         
                                                                                
           SELECT BP13K730 ASSIGN       TO BP13K730                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K730-KEY-NEW                         
                           FILE STATUS  IS K730-STATUS.                         
                                                                                
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS K800-STATUS.                         
                                                                                
           SELECT P13L726B ASSIGN       TO P13L726B.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BE01F515                                                             
           BLOCK  CONTAINS 0 RECORDS                                            
           RECORD CONTAINS 580 CHARACTERS                                       
           RECORDING  MODE IS  F                                                
           LABEL  RECORDS  ARE STANDARD.                                        
       COPY BE01F515.                                                           
                                                                                
       FD   BP13K500                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K500.                                                           
                                                                                
       FD   BP13K825                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K825.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K730.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD  P13L726B                                                             
           BLOCK  CONTAINS 0   RECORDS                                          
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING  MODE IS  F                                                
           LABEL  RECORDS  ARE STANDARD.                                        
       01  P13L726B-REC                 PIC X(132).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 K825-STATUS               PIC 9(02) VALUE ZERO.                   
           05 K730-STATUS               PIC 9(02) VALUE ZERO.                   
           05 K500-STATUS               PIC 9(02) VALUE ZERO.                   
           05 K800-STATUS               PIC 9(02) VALUE ZERO.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F515-READ              PIC 9(08) VALUE ZERO.                   
           05 WS-K825-READ              PIC 9(08) VALUE ZERO.                   
           05 WS-K500-REWRITE           PIC 9(08) VALUE ZERO.                   
           05 WS-K730-REWRITE           PIC 9(08) VALUE ZERO.                   
           05 WS-K800-REWRITE           PIC 9(08) VALUE ZERO.                   
           05 WS-REC-NOTFND             PIC 9(08) VALUE ZERO.                   
           05 WS-REC-UPDATED            PIC 9(08) VALUE ZERO.                   
           05 WS-REC-PRINTED            PIC 9(08) VALUE ZERO.                   
           05 WS-ERR-CTR                PIC 9(08) VALUE ZERO.                   
           05 WS-ERROR-LCTR             PIC 9(04) VALUE 56.                     
           05 WS-LINE-CTR               PIC 9(04) VALUE 56.                     
           05 WS-ERROR-PCTR             PIC 9(04) VALUE ZERO.                   
           05 WS-PAGE-CTR               PIC 9(04) VALUE ZERO.                   
                                                                                
       01  WS-K825-ETHNIC-TABLE.                                                
           05 WS-K825-REGN-NO           PIC X(08) VALUE SPACES.                 
           05 WS-ETHNIC-TABLE               OCCURS 99 TIMES.                    
              10 WS-K825-ETHNIC         PIC X(01).                              
              10 WS-K825-NRIC           PIC X(09).                              
              10 WS-K825-REMARKS        PIC X(30).                              
                                                                                
       01  WS-SWITCHES.                                                         
           05 F515-EOF                  PIC X(01) VALUE 'N'.                    
           05 WS-EOF-K825               PIC X(01) VALUE 'N'.                    
                                                                                
       01  WS-CURR-DATE.                                                        
           05 WS-YY                     PIC 9(04).                              
           05 WS-MM                     PIC 9(02).                              
           05 WS-DD                     PIC 9(02).                              
                                                                                
       01  WS-J                         PIC S9(4) COMP.                         
       01  WS-I                         PIC S9(4) COMP.                         
       01  WS-ETHNIC                    PIC X(01) VALUE SPACES.                 
       01  WS-ETHNIC-PREV               PIC X(01) VALUE SPACES.                 
       01  WS-ETHNIC-ERR                PIC X(01) VALUE SPACES.                 
       01  WS-CDE-CAT                   PIC X(01) VALUE SPACES.                 
       01  WS-MSG-1                     PIC X(30) VALUE                         
           ' ETHNIC CODE NOT SAME IN OCCUP'.                                    
       01  WS-MSG-2                     PIC X(30) VALUE                         
           ' ETHNIC CODE NOT 0 5 6 OR 7   '.                                    
       01  WS-REMARKS                   PIC X(30) VALUE SPACES.                 
                                                                                
       01  PRINT-VARIABLES.                                                     
           05 P-ERROR-TITLE-1.                                                  
              10 FILLER                 PIC X(11) VALUE 'BP13L726'.             
              10 FILLER                 PIC X(44) VALUE                         
                'ERROR LIST FOR INVALID INFORMATION'.                           
              10 FILLER                 PIC X(05) VALUE 'DATE '.                
              10 P-ERROR-DATE.                                                  
                 15 WS-DD               PIC 9(02).                              
                 15 FILLER              PIC X(01) VALUE '/'.                    
                 15 WS-MM               PIC 9(02).                              
                 15 FILLER              PIC X(01) VALUE '/'.                    
                 15 WS-YY               PIC 9(04).                              
              10 FILLER                 PIC X(06) VALUE ' PAGE ' .              
              10 P-ERROR-PAGENO         PIC ZZZ9.                               
           05 P-ERROR-TITLE-2.                                                  
              10 FILLER                 PIC X(07) VALUE 'S/N'.                  
              10 FILLER                 PIC X(10) VALUE 'REGN'.                 
              10 FILLER                 PIC X(05) VALUE SPACES.                 
              10 FILLER                 PIC X(09) VALUE ' NRIC NO '.            
              10 FILLER                 PIC X(02) VALUE SPACES.                 
              10 FILLER                 PIC X(06) VALUE 'ETHNIC'.               
              10 FILLER                 PIC X(02) VALUE SPACES.                 
              10 FILLER                 PIC X(13) VALUE SPACES.                 
              10 FILLER                 PIC X(07) VALUE 'REMARKS'.              
              10 FILLER                 PIC X(13) VALUE SPACES.                 
           05 P-ERROR-DETAIL.                                                   
              10 P-SERIAL-ERR           PIC ZZZZ9.                              
              10 FILLER                 PIC X(02) VALUE SPACES.                 
              10 P-REGN-ERR             PIC X(08) VALUE SPACES.                 
              10 FILLER                 PIC X(05) VALUE SPACES.                 
              10 P-NRIC-ERR             PIC X(09) VALUE SPACES.                 
              10 FILLER                 PIC X(05) VALUE SPACES.                 
              10 P-ETHNIC-ERR           PIC X(01) VALUE SPACES.                 
              10 FILLER                 PIC X(05) VALUE SPACES.                 
              10 P-REMARKS-ERR          PIC X(30) VALUE SPACES.                 
           05 P-TOTAL.                                                          
              10 FILLER                 PIC X(50) VALUE                         
                '********* BP13C726 CONTROL TOTALS *********'.                  
           05 P-TOTAL-1.                                                        
              10 FILLER                 PIC X(35) VALUE                         
                'TOTAL RECORDS READ FROM BE01F515 = '.                          
              10 P-F515-READ            PIC ZZZZZZZ9.                           
           05 P-TOTAL-2.                                                        
               10 FILLER                PIC X(35) VALUE                         
                 'TOTAL RECORDS READ FROM BP13K825 = '.                         
               10 P-K825-READ           PIC ZZZZZZZ9.                           
           05 P-TOTAL-3.                                                        
              10 FILLER                 PIC X(35) VALUE                         
                'TOTAL RECORDS IN K500 REWRITTEN  = '.                          
              10 P-K500-REWRITE         PIC ZZZZZZZ9.                           
           05 P-TOTAL-4.                                                        
              10 FILLER                 PIC X(35) VALUE                         
                'TOTAL RECORDS IN K730 REWRITTEN  = '.                          
              10 P-K730-REWRITE         PIC ZZZZZZZ9.                           
           05 P-BLANK                   PIC X(132) VALUE SPACES.                
           05 P-ERROR-LINE.                                                     
              10 FILLER                 PIC X(07) VALUE '-----  '.              
              10 FILLER                 PIC X(10) VALUE '--------  '.           
              10 FILLER                 PIC X(05) VALUE SPACES.                 
              10 FILLER                 PIC X(09) VALUE ' ------- '.            
              10 FILLER                 PIC X(02) VALUE SPACES.                 
              10 FILLER                 PIC X(06) VALUE '------'.               
              10 FILLER                 PIC X(02) VALUE SPACES.                 
              10 FILLER                 PIC X(30) VALUE ALL '-'.                
                                                                                
      *------------------*                                                      
       PROCEDURE DIVISION.                                                      
      *------------------*                                                      
                                                                                
      *---------------*                                                         
       0000-MAIN-LOGIC.                                                         
      *---------------*                                                         
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-F515     THRU 2000-EXIT.                           
           PERFORM 3000-PROCESS-RTN   THRU 3000-EXIT                            
             UNTIL F515-EOF = 'Y'.                                              
           PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------*                                                         
       1000-OPEN-FILES.                                                         
      *---------------*                                                         
                                                                                
           OPEN  INPUT BE01F515                                                 
                       BP13K825                                                 
                   I-O BP13K500                                                 
                       BP13K730                                                 
                       BP13K800                                                 
                OUTPUT P13L726B.                                                
                                                                                
           IF K825-STATUS NOT = 0 AND 97                                        
              DISPLAY '* ERROR IN OPENING BP13K825 FILE, STATUS IS '            
                       K825-STATUS                                              
              MOVE K825-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-FILES  THRU  9000-EXIT                         
           END-IF                                                               
                                                                                
           IF K500-STATUS NOT = 0 AND 97                                        
              DISPLAY '* ERROR IN OPENING BP13K500 FILE, STATUS IS '            
                       K500-STATUS                                              
              MOVE K500-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-FILES  THRU  9000-EXIT                         
           END-IF                                                               
                                                                                
           IF K730-STATUS NOT = 0 AND 97                                        
              DISPLAY '* ERROR IN OPENING BP13K730 FILE, STATUS IS '            
                       K730-STATUS                                              
              MOVE K730-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-FILES  THRU  9000-EXIT                         
           END-IF                                                               
                                                                                
           IF K800-STATUS NOT = 0 AND 97                                        
              DISPLAY '* ERROR IN OPENING BP13K800 FILE, STATUS IS '            
                       K800-STATUS                                              
              MOVE K800-STATUS TO RETURN-CODE                                   
              PERFORM 9000-CLOSE-FILES  THRU  9000-EXIT                         
           END-IF                                                               
                                                                                
           INITIALIZE WS-COUNTERS.                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
           MOVE   CORR WS-CURR-DATE TO   P-ERROR-DATE.                          
           MOVE   56   TO   WS-LINE-CTR  WS-ERROR-LCTR.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------*                                                          
       2000-READ-F515.                                                          
      *--------------*                                                          
                                                                                
           READ BE01F515                                                        
             AT END                                                             
                MOVE 'Y' TO F515-EOF                                            
                GO TO 2000-EXIT                                                 
           END-READ                                                             
                                                                                
           ADD 1 TO WS-F515-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       3000-PROCESS-RTN.                                                        
      *----------------*                                                        
                                                                                
           MOVE SPACES             TO BP13K825-REC.                             
           MOVE SPACES                  TO WS-K825-ETHNIC-TABLE.                
           MOVE BE01F515-REF-NO(1:8)    TO K825-NUM-REGN.                       
           MOVE LOW-VALUES              TO K825-NUM-NRIC.                       
                                                                                
           MOVE ZERO               TO WS-J.                                     
           MOVE SPACES             TO WS-ETHNIC WS-ETHNIC-PREV.                 
           MOVE SPACES             TO WS-ETHNIC-ERR.                            
           MOVE SPACES             TO WS-CDE-CAT.                               
           MOVE SPACES             TO WS-EOF-K825.                              
                                                                                
           START BP13K825 KEY IS > K825-KEY-FLD.                                
                                                                                
           EVALUATE K825-STATUS                                                 
           WHEN 00                                                              
                PERFORM 4000-READ-K825       THRU 4000-EXIT                     
                  UNTIL WS-EOF-K825  = 'Y'                                      
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN BP13K825, KEY IS '                 
                         K825-KEY-FLD                                           
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING IN BP13K825, STATUS IS '                 
                         K825-STATUS                                            
                PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
           IF WS-ETHNIC-ERR = 'Y'                                               
              PERFORM 4500-PRINT-ERROR       THRU 4500-EXIT                     
           ELSE                                                                 
              IF WS-J > ZERO                                                    
                                                                                
                 EVALUATE WS-ETHNIC                                             
                 WHEN '0'                                                       
                       MOVE '2'  TO WS-CDE-CAT                                  
                 WHEN '5'                                                       
                       MOVE '1'  TO WS-CDE-CAT                                  
                 WHEN '6'                                                       
                       MOVE '3'  TO WS-CDE-CAT                                  
                 WHEN '7'                                                       
                       MOVE '4'  TO WS-CDE-CAT                                  
                 END-EVALUATE                                                   
                                                                                
                 IF WS-CDE-CAT EQUAL '1' OR '2' OR '3' OR '4'                   
                    PERFORM 5000-REWRITE-K500-REC THRU 5000-EXIT                
                    PERFORM 7000-REWRITE-K800-REC THRU 7000-EXIT                
                 END-IF                                                         
                                                                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
           ADD  WS-J                         TO WS-K825-READ.                   
                                                                                
           PERFORM 2000-READ-F515            THRU 2000-EXIT.                    
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------*                                                          
       4000-READ-K825.                                                          
      *--------------*                                                          
                                                                                
           READ BP13K825 NEXT                                                   
                END-READ.                                                       
                                                                                
           EVALUATE K825-STATUS                                                 
           WHEN  00                                                             
           WHEN  02                                                             
                 IF K825-NUM-REGN EQUAL BE01F515-REF-NO(1:8)                    
                    PERFORM 4200-CHECK-REGN-ETHNIC  THRU 4200-EXIT              
                 ELSE                                                           
                    MOVE 'Y'                TO WS-EOF-K825                      
                 END-IF                                                         
           WHEN 10                                                              
                MOVE 'Y'                    TO WS-EOF-K825                      
           WHEN OTHER                                                           
              DISPLAY 'ERROR READING BP13K825, STATUS IS '                      
                       K825-STATUS                                              
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------*                                                  
       4200-CHECK-REGN-ETHNIC.                                                  
      *----------------------*                                                  
                                                                                
           ADD  1                  TO WS-J                                      
           MOVE K825-NUM-REGN      TO WS-K825-REGN-NO                           
           MOVE K825-NUM-ETHNIC    TO WS-K825-ETHNIC  (WS-J)                    
           MOVE SPACES             TO WS-K825-REMARKS (WS-J)                    
           MOVE K825-NUM-NRIC      TO WS-K825-NRIC    (WS-J)                    
           MOVE K825-NUM-ETHNIC    TO WS-ETHNIC                                 
                                                                                
           IF WS-J EQUAL 1                                                      
              MOVE K825-NUM-ETHNIC TO WS-ETHNIC WS-ETHNIC-PREV                  
           END-IF                                                               
                                                                                
           IF WS-ETHNIC EQUAL '0' OR '5' OR '6' OR '7'                          
              IF WS-ETHNIC EQUAL WS-ETHNIC-PREV                                 
                 CONTINUE                                                       
              ELSE                                                              
                 MOVE  'Y'            TO WS-ETHNIC-ERR                          
                 MOVE  WS-MSG-1       TO WS-K825-REMARKS (WS-J)                 
              END-IF                                                            
           ELSE                                                                 
              MOVE  'Y'               TO WS-ETHNIC-ERR                          
              MOVE  WS-MSG-2          TO WS-K825-REMARKS (WS-J)                 
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       4500-PRINT-ERROR.                                                        
      *----------------*                                                        
                                                                                
            IF WS-ERROR-LCTR > 54                                               
               PERFORM 4600-PRINT-ERROR-HDG  THRU  4600-EXIT                    
            END-IF.                                                             
                                                                                
            PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > WS-J                  
               ADD   1                        TO WS-ERR-CTR                     
               MOVE  WS-ERR-CTR               TO P-SERIAL-ERR                   
               MOVE  BE01F515-REF-NO(1:8)     TO P-REGN-ERR                     
               MOVE  WS-K825-NRIC    (WS-I)   TO P-NRIC-ERR                     
               EVALUATE WS-K825-ETHNIC (WS-I)                                   
               WHEN '0'                                                         
                    MOVE  'C'                      TO P-ETHNIC-ERR              
               WHEN '5'                                                         
                    MOVE  'M'                      TO P-ETHNIC-ERR              
               WHEN '6'                                                         
                    MOVE  'I'                      TO P-ETHNIC-ERR              
               WHEN '7'                                                         
                    MOVE  'O'                      TO P-ETHNIC-ERR              
               WHEN OTHER                                                       
                    MOVE  WS-K825-ETHNIC  (WS-I)   TO P-ETHNIC-ERR              
               END-EVALUATE                                                     
               MOVE  WS-K825-REMARKS (WS-I)   TO P-REMARKS-ERR                  
               WRITE P13L726B-REC   FROM P-ERROR-DETAIL AFTER 1                 
               ADD   1                        TO WS-ERROR-LCTR                  
            END-PERFORM.                                                        
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------*                                                   
       4600-PRINT-ERROR-HDG.                                                    
      *---------------------*                                                   
                                                                                
            ADD   1              TO WS-ERROR-PCTR.                              
            MOVE  WS-ERROR-PCTR  TO P-ERROR-PAGENO.                             
            WRITE P13L726B-REC FROM P-ERROR-TITLE-1 AFTER ADVANCING PAGE        
            WRITE P13L726B-REC FROM P-ERROR-TITLE-2 AFTER ADVANCING 2.          
            WRITE P13L726B-REC FROM P-ERROR-LINE    AFTER ADVANCING 1.          
            WRITE P13L726B-REC FROM P-BLANK         AFTER ADVANCING 1.          
            MOVE  5              TO WS-ERROR-LCTR.                              
                                                                                
       4600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------*                                                   
       5000-REWRITE-K500-REC.                                                   
      *---------------------*                                                   
                                                                                
           MOVE SPACES                       TO BP13K500-REC.                   
           MOVE BE01F515-REF-NO(1:8)         TO K500-NUM-REGN.                  
                                                                                
           READ BP13K500.                                                       
                                                                                
           EVALUATE K500-STATUS                                                 
           WHEN 00                                                              
                MOVE  WS-CDE-CAT            TO K500-CDE-CATEGORY                
                REWRITE BP13K500-REC                                            
                EVALUATE K500-STATUS                                            
                WHEN 00                                                         
                     ADD   1                TO WS-K500-REWRITE                  
                WHEN OTHER                                                      
                     DISPLAY 'ERROR REWRITING  BP13K500, STATUS IS '            
                              K500-STATUS                                       
                     PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                   
                END-EVALUATE                                                    
           WHEN 23                                                              
                PERFORM 6000-REWRITE-K730      THRU 6000-EXIT                   
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K500, STATUS IS '                    
                         K500-STATUS                                            
                PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------*                                                       
       6000-REWRITE-K730.                                                       
      *-----------------*                                                       
                                                                                
           MOVE SPACES                TO BP13K730-REC.                          
           MOVE BE01F515-REF-NO(1:8)  TO K730-NUM-REGN.                         
                                                                                
           READ BP13K730.                                                       
                                                                                
           EVALUATE K730-STATUS                                                 
           WHEN 00                                                              
                MOVE  WS-CDE-CAT            TO K730-CDE-CATEGORY                
                REWRITE BP13K730-REC                                            
                EVALUATE K730-STATUS                                            
                WHEN 00                                                         
                     ADD   1                TO WS-K730-REWRITE                  
                WHEN OTHER                                                      
                     DISPLAY 'ERROR REWRITING  BP13K730, STATUS IS '            
                              K730-STATUS                                       
                     PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                   
                END-EVALUATE                                                    
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN K730 ' K730-NUM-REGN               
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K730, STATUS IS '                    
                         K730-STATUS                                            
                PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *---------------------*                                                   
       7000-REWRITE-K800-REC.                                                   
      *---------------------*                                                   
                                                                                
           MOVE SPACES                       TO BP13K800-MASTER.                
           MOVE BE01F515-REF-NO(1:8)         TO K800-NUM-REGN.                  
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE K800-STATUS                                                 
           WHEN 00                                                              
                MOVE  WS-CDE-CAT            TO K800-NUM-CAT                     
                REWRITE BP13K800-MASTER                                         
                EVALUATE K800-STATUS                                            
                WHEN 00                                                         
                     ADD   1                TO WS-K800-REWRITE                  
                WHEN OTHER                                                      
                     DISPLAY 'ERROR REWRITING  BP13K800, STATUS IS '            
                              K800-STATUS                                       
                     PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                   
                END-EVALUATE                                                    
           WHEN 23                                                              
                CONTINUE                                                        
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K800, STATUS IS '                    
                         K800-STATUS                                            
                PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------*                                                        
       9000-CLOSE-FILES.                                                        
      *----------------*                                                        
                                                                                
           DISPLAY '  '.                                                        
           DISPLAY '********* BP13C726 CONTROL TOTALS *********'.               
           DISPLAY '  '.                                                        
           DISPLAY 'TOTAL RECORDS READ FROM BE01F515 = ' WS-F515-READ.          
           DISPLAY 'TOTAL RECORDS READ FROM BP13K825 = ' WS-K825-READ.          
           DISPLAY 'TOTAL ERRORS  ENCOUNTERED        = ' WS-ERR-CTR.            
           DISPLAY 'TOTAL K500 RECORDS RE-WRITTEN    = '                        
                    WS-K500-REWRITE.                                            
           DISPLAY 'TOTAL K730 RECORDS RE-WRITTEN    = '                        
                    WS-K730-REWRITE.                                            
           DISPLAY 'TOTAL K800 RECORDS RE-WRITTEN    = '                        
                    WS-K800-REWRITE.                                            
                                                                                
           CLOSE BE01F515                                                       
                 BP13K825                                                       
                 BP13K500                                                       
                 BP13K800                                                       
                 BP13K730                                                       
                 P13L726B.                                                      
                                                                                
           IF K825-STATUS NOT = 0                                               
              DISPLAY '* ERROR IN CLOSING BP13K825 FILE, STATUS IS '            
                       K825-STATUS                                              
              MOVE K825-STATUS   TO  RETURN-CODE                                
           END-IF                                                               
                                                                                
           IF K730-STATUS NOT = 0                                               
              DISPLAY '* ERROR IN CLOSING BP13K730 FILE, STATUS IS '            
                       K730-STATUS                                              
              MOVE K730-STATUS   TO  RETURN-CODE                                
           END-IF                                                               
                                                                                
           IF K500-STATUS NOT = 0                                               
              DISPLAY '* ERROR IN CLOSING BP13K500 FILE, STATUS IS '            
                       K500-STATUS                                              
              MOVE K500-STATUS   TO  RETURN-CODE                                
           END-IF                                                               
                                                                                
           IF K800-STATUS NOT = 0                                               
              DISPLAY '* ERROR IN CLOSING BP13K800 FILE, STATUS IS '            
                       K800-STATUS                                              
              MOVE K800-STATUS   TO  RETURN-CODE                                
           END-IF                                                               
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
