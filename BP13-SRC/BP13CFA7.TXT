      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13CFA7.                                                 
       AUTHOR.        ZARITA DANE SARMIENTO.                                    
       DATE-WRITTEN.  13 JULY 2017.                                             
      *===============================================================*         
      * OBJECTIVE  :  TO CREATE BP13F23A FORMAT                       *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F740                                     *         
      *               2. BP13K857                                     *         
      *                                                               *         
      * OUTPUT FILE:                                                            
      *               5. BP13F23A                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP136922 13/07/2017 ZDD1 NEW PROGRAM.                         *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F740  ASSIGN        TO BP13F740.                         
                                                                                
           SELECT  BP13K857  ASSIGN        TO BP13K857                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K857-KEY-FLD                      
                             FILE STATUS   IS WS-K857-STATUS.                   
                                                                                
           SELECT BP13K800   ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
                                                                                
           SELECT  BP13K813  ASSIGN        TO BP13K813                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K813-KEY-FLD                      
                             FILE STATUS   IS WS-K813-STATUS.                   
                                                                                
                                                                                
           SELECT  BP13F23A  ASSIGN        TO BP13F23A.                         
           SELECT  BP13F74A  ASSIGN        TO BP13F74A.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F740                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F740.                                                           
                                                                                
       FD   BP13F74A                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   BP13F74A-REC  PIC X(500).                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13K857                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13K857.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13F23A                                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F23A.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
                                                                                
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F740             PIC X(1)  VALUE 'N'.                     
           05  WS-K857-FOUND           PIC X(1)  VALUE 'N'.                     
           05  WS-K857-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K813-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-K857-FND             PIC 9(7)  VALUE ZEROES.                  
           05  WS-K857-NFND            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-F23A             PIC 9(7)  VALUE ZEROES.                  
           05  WS-BLNK-EMAIL           PIC 9(7)  VALUE ZEROES.                  
           05  WS-K857-EMAIL           PIC 9(7)  VALUE ZEROES.                  
           05  WS-K800-FND             PIC 9(7)  VALUE ZEROES.                  
           05  WS-K800-NFND            PIC 9(7)  VALUE ZEROES.                  
           05  WS-K813-FND             PIC 9(7)  VALUE ZEROES.                  
           05  WS-K813-NFND            PIC 9(7)  VALUE ZEROES.                  
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F740      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F740 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F740                                                 
                       BP13K857                                                 
                       BP13K813                                                 
                       BP13K800                                                 
               OUTPUT  BP13F23A                                                 
                       BP13F74A.                                                
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K857 - ERROR OPENING : ' WS-K857-STATUS              
              MOVE WS-K857-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE         THRU 9999-EXIT                
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K813 - ERROR OPENING : ' WS-K813-STATUS              
              MOVE WS-K813-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE         THRU 9999-EXIT                
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE         THRU 9999-EXIT                
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F740.                                                      
      ******************************************************************        
                                                                                
           READ BP13F740           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F740                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                    TO WS-F740-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
           MOVE SPACES                                TO BP13K857-REC.          
           INITIALIZE                                    BP13K857-REC.          
                                                                                
           MOVE F740-NUM-REGN  TO K800-NUM-REGN                                 
           PERFORM 4100-READ-BP13K800             THRU 4100-EXIT.               
                                                                                
           MOVE F740-NUM-REGN  TO K857-NUM-REGN                                 
           PERFORM 4000-READ-BP13K857  THRU 4000-EXIT                           
                                                                                
           PERFORM 5000-READ-BP13K813          THRU 5000-EXIT                   
                                                                                
           IF K857-NUM-EMAIL NOT = SPACES AND LOW-VALUES                        
             PERFORM 6000-MOVE-BP13F23A          THRU 6000-EXIT                 
             PERFORM 6500-WRITE-ROFF23AA         THRU 6500-EXIT                 
           ELSE                                                                 
             WRITE BP13F74A-REC   FROM BP13F740-REC                             
             ADD 1 TO WS-BLNK-EMAIL                                             
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F740                   THRU 2000-EXIT.         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-READ-BP13K857.                                                      
      ******************************************************************        
                                                                                
           READ BP13K857.                                                       
                                                                                
           EVALUATE WS-K857-STATUS                                              
               WHEN 00                                                          
                    ADD 1                             TO WS-K857-FND            
               WHEN 23                                                          
                    ADD 1                             TO WS-K857-NFND           
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K857 : ' WS-K857-STATUS          
                            ' REGN = ' K857-NUM-REGN                            
                    PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       4100-READ-BP13K800.                                                      
      *********************                                                     
           INITIALIZE BP13K800-MASTER.                                          
                                                                                
           MOVE F740-NUM-REGN   TO K800-NUM-REGN.                               
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-STATUS = ZEROS OR 02                                      
              ADD 1 TO WS-K800-FND                                              
           ELSE                                                                 
              IF WS-K800-STATUS = 23                                            
                 ADD 1 TO WS-K800-NFND                                          
                 DISPLAY 'READ BP13K800 NOT FD = ' F740-NUM-REGN                
              ELSE                                                              
                 DISPLAY 'READ ERROR, BP13K800 STATUS = '                       
                 WS-K800-STATUS                                                 
                 MOVE WS-K800-STATUS TO RETURN-CODE                             
                 PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-READ-BP13K813.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                            TO BP13K813-REC.              
           INITIALIZE                                BP13K813-REC.              
                                                                                
      *    MOVE ZEROES                            TO WS-K813-MID                
      *                                              WS-K813-END                
      *                                              WS-K813-START.             
                                                                                
           MOVE 'ROF'                          TO K813-NUM-ZONE                 
           MOVE 'RF'                           TO K813-NUM-FLAT-TYPE            
                                                                                
           MOVE K800-DTE-BALLOT                   TO K813-DTE-BALLOT.           
                                                                                
           READ BP13K813.                                                       
                                                                                
           EVALUATE WS-K813-STATUS                                              
               WHEN 00                                                          
                    ADD 1                         TO WS-K813-FND                
                    IF K813-NUM-QUEUE-START NOT NUMERIC                         
                       MOVE ZEROES              TO K813-NUM-QUEUE-START         
                    END-IF                                                      
                                                                                
                    IF K813-NUM-QUEUE-MID NOT NUMERIC                           
                       MOVE ZEROES                TO K813-NUM-QUEUE-MID         
                    END-IF                                                      
                                                                                
                    IF K813-NUM-QUEUE-END NOT NUMERIC                           
                       MOVE ZEROES                TO K813-NUM-QUEUE-END         
                    END-IF                                                      
                                                                                
               WHEN 23                                                          
                    ADD 1                         TO WS-K813-NFND               
                    DISPLAY 'BP13K813 NOT FND : ' K813-KEY-FLD                  
                            ' REGN : ' F740-NUM-REGN                            
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K813 : ' WS-K813-STATUS          
                            ' ZONE = ' K813-NUM-ZONE                            
                    PERFORM  9999-CLOSE-ROUTINE     THRU 9999-EXIT              
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       6000-MOVE-BP13F23A.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                        TO BP13F23A-REC.                  
           INITIALIZE                            BP13F23A-REC.                  
                                                                                
           MOVE F740-NUM-REGN                 TO F23A-NUM-REGN.                 
           MOVE F740-CDE-FLAT-TYPE         TO F23A-NUM-FLAT-TYPE                
                                                                                
           MOVE F740-DTE-BALLOT               TO F23A-DTE-BALLOT.               
                                                                                
           MOVE F740-NUM-NT-FT-QUEUE       TO F23A-NUM-QUEUE.                   
           IF F740-NUM-NT-FT-QUEUE >  K813-NUM-QUEUE-END                        
              MOVE 'R2'            TO F23A-TYPE-APPMT                           
           ELSE                                                                 
              MOVE 'R1'            TO F23A-TYPE-APPMT                           
           END-IF.                                                              
           MOVE K857-NUM-EMAIL             TO F23A-NUM-EMAIL.                   
                                                                                
           MOVE K813-NME-PROJECT-LTR          TO F23A-NUM-PRJ-NME.              
           MOVE K813-TXT-SEL-PERIOD-LTR       TO F23A-NUM-SEL-PERIOD.           
           MOVE K813-NUM-MATURE-EST-TAG     TO F23A-NUM-MATURE-EST-TAG.         
           MOVE K813-NUM-UNIT-OFFER         TO F23A-NUM-UNIT-OFFER.             
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6500-WRITE-ROFF23AA.                                                     
      ******************************************************************        
                                                                                
           PERFORM 6000-MOVE-BP13F23A       THRU 6000-EXIT.                     
                                                                                
           WRITE BP13F23A-REC.                                                  
                                                                                
           ADD 1  TO WS-CNT-F23A.                                               
                                                                                
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CFA7'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY ' (1) NO OF BP13F740 RECORDS READ............. : '           
                    WS-F740-READ.                                               
           DISPLAY ' (2) NO OF BP13K857 RECORDS FOUND WITH EMAIL. : '           
                    WS-K857-FND.                                                
           DISPLAY ' (3) NO OF BP13K857 RECORDS NOT FOUND........ : '           
                    WS-K857-NFND.                                               
           DISPLAY ' (4) NO OF BP13K857 RECORDS W/ BLANK EMAIL .. : '           
                    WS-BLNK-EMAIL.                                              
           DISPLAY 'NO OF REC WRITTEN ON F23A            ........ : '           
                    WS-CNT-F23A.                                                
           DISPLAY 'NO OF REC FOUND BP13K800             ........ : '           
                    WS-K800-FND.                                                
           DISPLAY 'NO OF REC NOT FOUND BP13K800         ........ : '           
                    WS-K800-NFND.                                               
           DISPLAY 'NO OF REC FOUND BP13K813             ........ : '           
                    WS-K813-FND.                                                
           DISPLAY 'NO OF REC NOT FOUND BP13K800         ........ : '           
                    WS-K813-NFND.                                               
           DISPLAY ' '.                                                         
                                                                                
           CLOSE    BP13F740                                                    
                    BP13K857                                                    
                    BP13K813                                                    
                    BP13F23A.                                                   
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K857 - ERROR CLOSING : ' WS-K857-STATUS              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
