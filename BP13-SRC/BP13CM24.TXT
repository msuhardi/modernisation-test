       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CM24.                                                 
       AUTHOR.        CS94317.                                                  
      *DATE-WRITTEN.  31/07/23.                                                 
                                                                                
      ******************************************************************        
      *   OBJECTIVE : TO PROCESS FIRST TIMER APPLICATION                        
      *================================================================*        
      * FILES DESCRIPTION :                                            *        
      *                ACCESS     REC                                  *        
      * DD NAME  I/O/E  MODE      LEN  KEY          REMARKS            *        
      * -------  ----- -------    ---  ---          -------            *        
      * CHG REF    DATE     BY    DESCRIPTION                          *        
      * -------- -------- ------- -----------                          *        
      * BP139530 08092023 CS94317 NEW PROGRAM TO DETERMINE FAMILY AND  *        
      *                           PERSON INFORMATION FOR FT(PMC)       *        
      *                           ELIGIBILITY                          *        
      ******************************************************************        
      * BP139530 16102023 CS94317 TO RUN THE PROGRAM SUCCESSFULLY      *        
      *                           AND WRITE INTO OUTPUT WITH HEADER AND*        
      *                           FOOTER RECORD ONLY                   *        
      *                           WHEN BP13FIN1 FILE IS EMPTY          *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT SY02F001 ASSIGN TO SY02F001.                                  
           SELECT BP13FIN1 ASSIGN TO BP13FIN1.                                  
           SELECT BP13FIN2 ASSIGN TO BP13FIN2.                                  
           SELECT BP13FOT1 ASSIGN TO BP13FOT1.                                  
           SELECT BP13FOT2 ASSIGN TO BP13FOT2.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *** INPUT RECORD LAYOUT ***                                               
                                                                                
       FD  BP13FIN1 LABEL RECORDS STANDARD.                                     
       01  BP13FIN1-REC.                                                        
           05 FIN1-NUM-REGN             PIC X(8).                               
           05 FIN1-CDE-ELIG-SCH         PIC X(3).                               
           05 FIN1-NUM-ALLO-CAT         PIC X(3).                               
           05 FIN1-CDE-HOUSEHOLD        PIC X.                                  
           05 FIN1-NUM-FT2T             PIC X.                                  
           05 FIN1-DTE-REQUEST          PIC X(8).                               
           05 FIN1-DTE-BALLOT           PIC X(6).                               
           05 FIN1-NUM-HLA              PIC X(9).                               
           05 FIN1-HFE-APPLN-DTE        PIC X(8).                               
                                                                                
       FD  BP13FIN2 LABEL RECORDS STANDARD.                                     
       01  BP13FIN2-REC.                                                        
           05 FIN2-NUM-REGN             PIC X(8).                               
           05 FIN2-NUM-NRIC             PIC X(9).                               
           05 FIN2-NUM-MARITAL-STATUS   PIC X.                                  
           05 FIN2-DTE-BIRTH            PIC X(8).                               
           05 FIN2-NUM-RELATIONSHIP     PIC X(2).                               
           05 FIN2-NUM-LESSEE-CHECK     PIC X.                                  
           05 FIN2-NUM-PPS-PREGNANT     PIC X.                                  
           05 FIN2-NUM-CITIZENSHIP      PIC X(2).                               
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  BP13FOT1                                                             
           BLOCK CONTAINS     0 RECORDS                                         
           RECORD CONTAINS  100 CHARACTERS                                      
           RECORDING MODE    IS F                                               
           LABEL RECORD      IS STANDARD.                                       
       01  BP13FOT1-REC.                                                        
           05 FOT1-HDR-REC.                                                     
              10 FOT1-HDR-DTE-BALLOT-QTR PIC 9(6).                              
              10 FOT1-HDR-LOW-VAL        PIC X(8).                              
              10 FOT1-DTE-CREATE         PIC 9(8).                              
              10 FOT1-TIME-CREATE        PIC 9(6).                              
              10 FOT1-HDR-FILLER         PIC X(72).                             
                                                                                
           05 FOT1-DTL-REC REDEFINES FOT1-HDR-REC.                              
              10 FOT1-DTL-DTE-BALLOT-QTR PIC 9(6).                              
              10 FOT1-NUM-REGN           PIC X(8).                              
              10 FOT1-CDE-ELIG-SCH       PIC X(3).                              
              10 FOT1-NUM-ALLO-CAT       PIC X(3).                              
              10 FOT1-CDE-HOUSEHOLD      PIC X.                                 
              10 FOT1-NUM-FT2T           PIC X.                                 
              10 FOT1-DTE-REQUEST        PIC X(8).                              
              10 FOT1-NUM-HFE            PIC X(9).                              
              10 FOT1-DTE-HFE-APPLN      PIC X(8).                              
              10 FOT1-CDE-FAMILY-OUTCOME PIC X.                                 
              10 FOT1-CDE-RES-OUTCOME    PIC X.                                 
              10 FOT1-DTL-FILLER-1       PIC X(5).                              
              10 FOT1-CDE-CHANCE-OUTCOME PIC X.                                 
              10 FOT1-CDE-FTPMC          PIC X.                                 
              10 FOT1-REMARK             PIC X(30).                             
              10 FOT1-DTL-FILLER-2       PIC X(14).                             
                                                                                
           05 FOT1-CTL-REC REDEFINES FOT1-HDR-REC.                              
              10 FOT1-CTL-DTE-BALLOT-QTR PIC 9(6).                              
              10 FOT1-CTL-HIGH-VAL       PIC X(8).                              
              10 FOT1-RCD-CNT            PIC 9(8).                              
              10 FOT1-CTL-FILLER         PIC X(78).                             
                                                                                
       FD  BP13FOT2                                                             
           BLOCK CONTAINS     0 RECORDS                                         
           RECORD CONTAINS  100 CHARACTERS                                      
           RECORDING MODE    IS F                                               
           LABEL RECORD      IS STANDARD.                                       
       01  BP13FOT2-REC.                                                        
           05 FOT2-HDR-REC.                                                     
              10 FOT2-HDR-DTE-BALLOT-QTR PIC 9(6).                              
              10 FOT2-HDR-LOW-VAL        PIC X(8).                              
              10 FOT2-DTE-CREATE         PIC 9(8).                              
              10 FOT2-TIME-CREATE        PIC 9(6).                              
              10 FOT2-HDR-FILLER         PIC X(72).                             
                                                                                
           05 FOT2-DTL-REC REDEFINES FOT2-HDR-REC.                              
              10 FOT2-DTL-DTE-BALLOT-QTR PIC 9(6).                              
              10 FOT2-NUM-REGN           PIC X(8).                              
              10 FOT2-CDE-ELIG-SCH       PIC X(3).                              
              10 FOT2-NUM-ALLO-CAT       PIC X(3).                              
              10 FOT2-CDE-HOUSEHOLD      PIC X.                                 
              10 FOT2-NUM-FT2T           PIC X.                                 
              10 FOT2-DTE-REQUEST        PIC X(8).                              
              10 FOT2-NUM-HFE            PIC X(9).                              
              10 FOT2-DTE-HFE-APPLN      PIC X(8).                              
              10 FOT2-CDE-FAMILY-OUTCOME PIC X.                                 
              10 FOT2-CDE-RES-OUTCOME    PIC X.                                 
              10 FOT2-DTL-FILLER-1       PIC X(5).                              
              10 FOT2-CDE-CHANCE-OUTCOME PIC X.                                 
              10 FOT2-CDE-FTPMC          PIC X.                                 
              10 FOT2-REMARK             PIC X(30).                             
              10 FOT2-DTL-FILLER-2       PIC X(14).                             
                                                                                
           05 FOT2-CTL-REC REDEFINES FOT2-HDR-REC.                              
              10 FOT2-CTL-DTE-BALLOT-QTR PIC 9(6).                              
              10 FOT2-CTL-HIGH-VAL       PIC X(8).                              
              10 FOT2-RCD-CNT            PIC 9(8).                              
              10 FOT2-CTL-FILLER         PIC X(78).                             
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-FIN1-EOF             PIC X(1)    VALUE 'N'.                    
           05 WS-FIN2-EOF             PIC X(1)    VALUE 'N'.                    
           05 WS-OUT-CNT              PIC 9(9)    VALUE ZERO.                   
           05 WS-FIN1-CNT             PIC 9(9)    VALUE ZERO.                   
           05 WS-FIN2-CNT             PIC 9(9)    VALUE ZERO.                   
           05 WS-ACP-CNT              PIC 9(8)    VALUE ZERO.                   
           05 WS-REJ-CNT              PIC 9(8)    VALUE ZERO.                   
                                                                                
      **** MAIN APPLICANT                                                       
           05 WS-HA1-FND-TAG          PIC X    VALUE SPACES.                    
           05 WS-HA1-NRIC             PIC X(9) VALUE SPACES.                    
           05 WS-HA1-CN-TAG           PIC X    VALUE SPACES.                    
           05 WS-HA1-RLS              PIC X(9) VALUE SPACES.                    
           05 WS-HA1-MS               PIC X    VALUE SPACES.                    
           05 WS-HA1-DOB              PIC X(8) VALUE SPACES.                    
           05 WS-HA1-AGE-YR           PIC 9(3) VALUE ZERO.                      
           05 WS-HA1-PREGNANT         PIC X    VALUE SPACES.                    
                                                                                
      **** SPOUSE                                                               
           05 WS-SPS-FND-TAG          PIC X    VALUE SPACES.                    
           05 WS-SPS-NRIC             PIC X(9) VALUE SPACES.                    
           05 WS-SPS-CN-TAG           PIC X    VALUE SPACES.                    
           05 WS-SPS-RLS              PIC X(9) VALUE SPACES.                    
           05 WS-SPS-MS               PIC X    VALUE SPACES.                    
           05 WS-SPS-DOB              PIC X(8) VALUE SPACES.                    
           05 WS-SPS-AGE-YR           PIC 9(3) VALUE ZERO.                      
           05 WS-SPS-PREGNANT         PIC X    VALUE SPACES.                    
                                                                                
      **** YOUNGEST CHILD                                                       
           05 WS-YCH-FND-TAG          PIC X    VALUE SPACES.                    
           05 WS-YCH-NRIC             PIC X(9) VALUE SPACES.                    
           05 WS-YCH-CN-TAG           PIC X    VALUE SPACES.                    
           05 WS-YCH-RLS              PIC X(9) VALUE SPACES.                    
           05 WS-YCH-MS               PIC X    VALUE SPACES.                    
           05 WS-YCH-DOB              PIC X(8) VALUE SPACES.                    
           05 WS-YCH-AGE-YR           PIC 9(3) VALUE ZERO.                      
           05 WS-YCH-PREGNANT         PIC X    VALUE SPACES.                    
           05 WS-HAS-CHILD     PIC X    VALUE SPACES.                           
                                                                                
      **** YOUNGEST GRANDCHILD                                                  
           05 WS-YGC-FND-TAG          PIC X    VALUE SPACES.                    
           05 WS-YGC-NRIC             PIC X(9) VALUE SPACES.                    
           05 WS-YGC-CN-TAG           PIC X    VALUE SPACES.                    
           05 WS-YGC-RLS              PIC X(9) VALUE SPACES.                    
           05 WS-YGC-MS               PIC X    VALUE SPACES.                    
           05 WS-YGC-DOB              PIC X(8) VALUE SPACES.                    
           05 WS-YGC-AGE-YR           PIC 9(3) VALUE ZERO.                      
           05 WS-YGC-PREGNANT         PIC X    VALUE SPACES.                    
                                                                                
      **** YOUNGEST ORPHAN                                                      
           05 WS-YOR-FND-TAG          PIC X    VALUE SPACES.                    
           05 WS-YOR-NRIC             PIC X(9) VALUE SPACES.                    
           05 WS-YOR-CN-TAG           PIC X    VALUE SPACES.                    
           05 WS-YOR-RLS              PIC X(9) VALUE SPACES.                    
           05 WS-YOR-MS               PIC X    VALUE SPACES.                    
           05 WS-YOR-DOB              PIC X(8) VALUE SPACES.                    
           05 WS-YOR-AGE-YR           PIC 9(3) VALUE ZERO.                      
           05 WS-YOR-PREGNANT         PIC X    VALUE SPACES.                    
                                                                                
           05 WS-CURR-DTETIME.                                                  
              10 WS-CURR-CDTE.                                                  
                 15 WS-CURR-CDTE-CCYY   PIC X(04) VALUE SPACES.                 
                 15 WS-CURR-CDTE-MM     PIC X(02) VALUE SPACES.                 
                 15 WS-CURR-CDTE-DD     PIC X(02) VALUE SPACES.                 
              10 WS-CURR-TME            PIC X(06) VALUE SPACES.                 
                                                                                
      ***   START CR9530 PKG015228   ***                                        
      *    05 WS-DTE-CURR               PIC 9(08) VALUE ZEROES.                 
           05 WS-DTE-CURR.                                                      
              10 WS-DTE-CURR-CCYYMM     PIC 9(06) VALUE ZEROES.                 
              10 WS-DTE-CURR-DD         PIC 9(02) VALUE ZEROES.                 
      ***   END   CR9530 PKG015228   ***                                        
           05 WS-TME-CURR               PIC 9(06) VALUE ZEROES.                 
           05 WS-DTE-CREATE             PIC 9(08) VALUE ZEROES.                 
           05 WS-TIME-CREATE            PIC 9(06) VALUE ZEROES.                 
           05 WS-DTL-BALLOT-QTR         PIC 9(06) VALUE ZEROES.                 
           05 WS-HDL-BALLOT-QTR         PIC 9(06) VALUE ZEROES.                 
           05 WS-CDE-FAMILY-OUTCOME     PIC X VALUE SPACES.                     
           05 WS-ERR-MSG                PIC X(50) VALUE SPACES.                 
           05 WS-REJ-TAG                PIC X     VALUE SPACES.                 
           05 WS-DTE-HFE-APPLN.                                                 
              10 WS-DTE-HFE-APPLN-CCYY  PIC 9(4)  VALUE ZEROES.                 
              10 WS-DTE-HFE-APPLN-MM    PIC 9(2)  VALUE ZEROES.                 
              10 WS-DTE-HFE-APPLN-DD    PIC 9(2)  VALUE ZEROES.                 
           05 WS-FIN1-NUM-HFE           PIC 9(8)  VALUE ZEROES.                 
           05 WS-REJ-REMARK             PIC X(30) VALUE SPACES.                 
           05 WS-CURR-NUM-REGN          PIC X(9) VALUE SPACES.                  
           05 WS-EOF-FIN1               PIC X    VALUE 'N'.                     
           05 WS-EOF-FIN2               PIC X    VALUE 'N'.                     
           05 WS-AGE                    PIC 9(3) VALUE ZEROES.                  
           05 WS-AGE-DAY.                                                       
              10 WS-YRS                 PIC 9(3) VALUE ZEROES.                  
              10 WS-MTH                 PIC S9(2) VALUE ZEROES.                 
              10 WS-DAY                 PIC S9(2) VALUE ZEROES.                 
           05 WS-FIN2-DOB.                                                      
              10 WS-FIN2-DOB-CCYY       PIC 9(4).                               
              10 WS-FIN2-DOB-MM         PIC 9(2).                               
              10 WS-FIN2-DOB-DD         PIC 9(2).                               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ****************                                                          
       0000-MAIN-PROC.                                                          
      ****************                                                          
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT                         
           PERFORM 1500-GET-CURRENT-DATE THRU 1500-EXIT                         
           PERFORM 2000-VALIDATE-INPUT   THRU 2000-EXIT                         
           PERFORM 3000-MAIN-PROCESS     THRU 3000-EXIT                         
                   UNTIL WS-FIN1-EOF = 'Y' OR WS-FIN2-EOF = 'Y'                 
                                                                                
           IF WS-FIN1-EOF = 'N' AND WS-FIN2-EOF = 'Y'                           
              PERFORM 4000-REST-FIN1    THRU 4000-EXIT                          
                                        UNTIL WS-FIN1-EOF = 'Y'                 
           END-IF                                                               
                                                                                
           IF WS-FIN1-EOF = 'Y' AND WS-FIN2-EOF = 'N'                           
              PERFORM 5000-REST-FIN2    THRU 5000-EXIT                          
                                        UNTIL WS-FIN2-EOF = 'Y'                 
           END-IF                                                               
                                                                                
           PERFORM 6000-CTL-SUMMARY      THRU 6000-EXIT                         
           PERFORM 7000-CTL-REC          THRU 7000-EXIT                         
           PERFORM 9998-CLOSE-FILE THRU 9998-EXIT                               
           STOP RUN                                                             
           .                                                                    
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *******************************************************                   
       1000-OPEN-FILES.                                                         
      *******************************************************                   
           OPEN INPUT  BP13FIN1                                                 
                       BP13FIN2                                                 
                       SY02F001                                                 
                OUTPUT BP13FOT1                                                 
                       BP13FOT2                                                 
           .                                                                    
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       1500-GET-CURRENT-DATE.                                                   
      ***********************                                                   
           READ SY02F001                                                        
             AT END                                                             
                DISPLAY 'BP13CM24-E01: SY02F001 IS EMPTY'                       
                MOVE 'BP13CM24-E01: SY02F001 IS EMPTY' TO WS-ERR-MSG            
                PERFORM 9998-CLOSE-FILE THRU 9998-EXIT                          
                PERFORM 9999-CANCEL-JOB THRU 9999-EXIT                          
             NOT AT END                                                         
                MOVE F001-DTE-CURRENT  TO WS-DTE-CURR.                          
           MOVE FUNCTION CURRENT-DATE  TO WS-CURR-DTETIME                       
           MOVE WS-CURR-TME            TO WS-TME-CURR                           
           .                                                                    
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************                                                            
       2300-HDR-REC.                                                            
      **************                                                            
           INITIALIZE FOT1-HDR-REC                                              
           INITIALIZE FOT2-HDR-REC                                              
           MOVE LOW-VALUES          TO FOT1-HDR-LOW-VAL                         
                                       FOT2-HDR-LOW-VAL                         
           MOVE WS-DTE-CURR         TO FOT1-DTE-CREATE                          
                                       FOT2-DTE-CREATE                          
           MOVE WS-TME-CURR         TO FOT1-TIME-CREATE                         
                                       FOT2-TIME-CREATE                         
           MOVE WS-HDL-BALLOT-QTR   TO FOT1-HDR-DTE-BALLOT-QTR                  
                                       FOT2-HDR-DTE-BALLOT-QTR                  
           MOVE SPACES              TO FOT1-HDR-FILLER                          
                                       FOT2-HDR-FILLER                          
           WRITE BP13FOT1-REC                                                   
           WRITE BP13FOT2-REC                                                   
           .                                                                    
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       2000-VALIDATE-INPUT.                                                     
      **********************                                                    
           READ BP13FIN1                                                        
             AT END                                                             
                DISPLAY 'BP13CM24-I02: BP13FIN1 IS EMPTY'                       
                MOVE 'BP13CM24-I02: BP13FIN1 IS EMPTY' TO WS-ERR-MSG            
      ***   START CR9530 PKG015228   ***                                        
                MOVE WS-DTE-CURR-CCYYMM   TO   WS-HDL-BALLOT-QTR                
                PERFORM 2300-HDR-REC          THRU 2300-EXIT                    
                PERFORM 7000-CTL-REC          THRU 7000-EXIT                    
                PERFORM 9998-CLOSE-FILE THRU 9998-EXIT                          
                STOP RUN                                                        
      *         PERFORM 9999-CANCEL-JOB THRU 9999-EXIT                          
      ***   END   CR9530 PKG015228   ***                                        
             NOT AT END                                                         
                ADD 1 TO WS-FIN1-CNT                                            
                MOVE FIN1-DTE-BALLOT    TO WS-HDL-BALLOT-QTR                    
           END-READ                                                             
                                                                                
           READ BP13FIN2                                                        
             AT END                                                             
                DISPLAY 'BP13CM24-E03: BP13FIN2 IS EMPTY'                       
                MOVE 'BP13CM24-E03: BP13FIN2 IS EMPTY' TO WS-ERR-MSG            
                PERFORM 9998-CLOSE-FILE THRU 9998-EXIT                          
                PERFORM 9999-CANCEL-JOB THRU 9999-EXIT                          
             NOT AT END                                                         
                ADD 1 TO WS-FIN2-CNT.                                           
                                                                                
           PERFORM 2300-HDR-REC          THRU 2300-EXIT                         
           .                                                                    
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *******************                                                       
       3000-MAIN-PROCESS.                                                       
      *******************                                                       
           MOVE 'N'               TO WS-REJ-TAG                                 
           MOVE SPACE             TO WS-CDE-FAMILY-OUTCOME                      
           MOVE ZEROES            TO WS-DTE-HFE-APPLN                           
           MOVE FIN1-DTE-BALLOT    TO WS-DTL-BALLOT-QTR                         
                                                                                
           IF FIN1-NUM-HLA = SPACES OR                                          
              FIN1-HFE-APPLN-DTE = SPACES OR                                    
              FIN1-HFE-APPLN-DTE = ZEROES                                       
              MOVE 'Y'                  TO WS-REJ-TAG                           
              MOVE 'N'                  TO WS-CDE-FAMILY-OUTCOME                
              MOVE 'HFE DATA ERROR'     TO WS-REJ-REMARK                        
              PERFORM 9000-OUT-REJECT   THRU 9000-EXIT                          
              PERFORM 2100-READ-FIN1    THRU 2100-EXIT                          
           END-IF                                                               
                                                                                
           IF FIN1-CDE-ELIG-SCH = 'NCS' AND WS-REJ-TAG = 'N'                    
              MOVE 'Y'                  TO WS-REJ-TAG                           
              MOVE 'N'                  TO WS-CDE-FAMILY-OUTCOME                
              MOVE 'NCS CASE'           TO WS-REJ-REMARK                        
              PERFORM 9000-OUT-REJECT   THRU 9000-EXIT                          
              PERFORM 2100-READ-FIN1    THRU 2100-EXIT                          
           END-IF                                                               
                                                                                
           IF WS-REJ-TAG = 'N'                                                  
              MOVE FIN1-HFE-APPLN-DTE TO WS-DTE-HFE-APPLN                       
              MOVE FIN1-NUM-REGN      TO WS-CURR-NUM-REGN                       
              IF WS-CURR-NUM-REGN = FIN2-NUM-REGN                               
                 PERFORM 3100-INITIAL-FAMILY      THRU 3100-EXIT                
                 PERFORM 3200-DETERMINE-FAMILY    THRU 3200-EXIT                
                      UNTIL WS-CURR-NUM-REGN NOT = FIN2-NUM-REGN OR             
                            WS-FIN2-EOF = 'Y'                                   
                 PERFORM 3300-DETERMINE-FTPMC-ELG THRU 3300-EXIT                
                 PERFORM 2100-READ-FIN1           THRU 2100-EXIT                
              ELSE                                                              
                 IF WS-CURR-NUM-REGN > FIN2-NUM-REGN                            
                    PERFORM 2200-READ-FIN2    THRU 2200-EXIT                    
                 ELSE                                                           
                    MOVE 'Y'                  TO WS-REJ-TAG                     
                    MOVE 'N'                  TO WS-CDE-FAMILY-OUTCOME          
                    MOVE 'PERSON NOT FOUND'   TO WS-REJ-REMARK                  
                    PERFORM 9000-OUT-REJECT   THRU 9000-EXIT                    
                    PERFORM 2100-READ-FIN1    THRU 2100-EXIT                    
                 END-IF                                                         
              END-IF                                                            
           END-IF                                                               
           .                                                                    
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************                                                          
       2100-READ-FIN1.                                                          
      ****************                                                          
           READ BP13FIN1                                                        
             AT END                                                             
                MOVE 'Y'              TO WS-FIN1-EOF                            
             NOT AT END                                                         
                ADD 1 TO WS-FIN1-CNT.                                           
           .                                                                    
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************                                                          
       2200-READ-FIN2.                                                          
      ****************                                                          
           READ BP13FIN2                                                        
             AT END                                                             
                MOVE 'Y'              TO WS-FIN2-EOF                            
             NOT AT END                                                         
                ADD 1 TO WS-FIN2-CNT.                                           
           .                                                                    
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       3100-INITIAL-FAMILY.                                                     
      *********************                                                     
           MOVE 'N'               TO WS-HA1-FND-TAG                             
                                     WS-SPS-FND-TAG                             
                                     WS-YCH-FND-TAG                             
                                     WS-YGC-FND-TAG                             
                                     WS-YOR-FND-TAG                             
                                     WS-HAS-CHILD                               
                                                                                
           MOVE SPACES            TO WS-HA1-NRIC                                
                                     WS-SPS-NRIC                                
                                     WS-YCH-NRIC                                
                                     WS-YGC-NRIC                                
                                     WS-YOR-NRIC                                
                                     WS-HA1-CN-TAG                              
                                     WS-SPS-CN-TAG                              
                                     WS-YCH-CN-TAG                              
                                     WS-YGC-CN-TAG                              
                                     WS-YOR-CN-TAG                              
                                     WS-HA1-RLS                                 
                                     WS-SPS-RLS                                 
                                     WS-YCH-RLS                                 
                                     WS-YGC-RLS                                 
                                     WS-YOR-RLS                                 
                                     WS-HA1-MS                                  
                                     WS-SPS-MS                                  
                                     WS-YCH-MS                                  
                                     WS-YGC-MS                                  
                                     WS-YOR-MS                                  
                                     WS-HA1-PREGNANT                            
                                     WS-SPS-PREGNANT                            
                                     WS-YCH-PREGNANT                            
                                     WS-YGC-PREGNANT                            
                                     WS-YOR-PREGNANT                            
                                                                                
           MOVE ZEROS             TO WS-HA1-DOB                                 
                                     WS-SPS-DOB                                 
                                     WS-YCH-DOB                                 
                                     WS-YGC-DOB                                 
                                     WS-YOR-DOB                                 
                                                                                
           MOVE 0                 TO WS-HA1-AGE-YR                              
                                     WS-SPS-AGE-YR                              
                                     WS-YCH-AGE-YR                              
                                     WS-YGC-AGE-YR                              
                                     WS-YOR-AGE-YR                              
           .                                                                    
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***********************                                                   
       3200-DETERMINE-FAMILY.                                                   
      ***********************                                                   
           EVALUATE FIN2-NUM-RELATIONSHIP                                       
           WHEN '00'                                                            
              IF WS-HA1-FND-TAG = 'N'                                           
                 MOVE 'Y'                      TO WS-HA1-FND-TAG                
                 MOVE FIN2-NUM-LESSEE-CHECK    TO WS-HA1-CN-TAG                 
                 MOVE FIN2-NUM-NRIC            TO WS-HA1-NRIC                   
                 MOVE FIN2-NUM-MARITAL-STATUS  TO WS-HA1-MS                     
                 MOVE FIN2-DTE-BIRTH           TO WS-HA1-DOB                    
                 MOVE FIN2-NUM-PPS-PREGNANT    TO WS-HA1-PREGNANT               
                 PERFORM 3400-COMPUTE-AGE      THRU 3400-EXIT                   
                 MOVE WS-AGE                   TO WS-HA1-AGE-YR                 
              END-IF                                                            
           WHEN '20'                                                            
              IF WS-SPS-FND-TAG = 'N'                                           
                 MOVE 'Y'                      TO WS-SPS-FND-TAG                
                 MOVE FIN2-NUM-LESSEE-CHECK    TO WS-SPS-CN-TAG                 
                 MOVE FIN2-NUM-NRIC            TO WS-SPS-NRIC                   
                 MOVE FIN2-NUM-MARITAL-STATUS  TO WS-SPS-MS                     
                 MOVE FIN2-DTE-BIRTH           TO WS-SPS-DOB                    
                 MOVE FIN2-NUM-PPS-PREGNANT    TO WS-SPS-PREGNANT               
                 PERFORM 3400-COMPUTE-AGE      THRU 3400-EXIT                   
                 MOVE WS-AGE                   TO WS-SPS-AGE-YR                 
              END-IF                                                            
           WHEN '39'                                                            
           WHEN '41'                                                            
           WHEN '51'                                                            
              MOVE 'Y'                   TO WS-HAS-CHILD                        
              IF WS-YCH-FND-TAG = 'N'                                           
                 IF FIN2-NUM-CITIZENSHIP = '10'                                 
                    MOVE 'Y'                      TO WS-YCH-FND-TAG             
                    MOVE FIN2-NUM-LESSEE-CHECK    TO WS-YCH-CN-TAG              
                    MOVE FIN2-NUM-NRIC            TO WS-YCH-NRIC                
                    MOVE FIN2-NUM-MARITAL-STATUS  TO WS-YCH-MS                  
                    MOVE FIN2-DTE-BIRTH           TO WS-YCH-DOB                 
                    MOVE FIN2-NUM-PPS-PREGNANT    TO WS-YCH-PREGNANT            
                    PERFORM 3400-COMPUTE-AGE      THRU 3400-EXIT                
                    MOVE WS-AGE                   TO WS-YCH-AGE-YR              
                 END-IF                                                         
              END-IF                                                            
           WHEN '43'                                                            
              IF WS-YGC-FND-TAG = 'N'                                           
                 MOVE 'Y'                      TO WS-YGC-FND-TAG                
                 MOVE FIN2-NUM-LESSEE-CHECK    TO WS-YGC-CN-TAG                 
                 MOVE FIN2-NUM-NRIC            TO WS-YGC-NRIC                   
                 MOVE FIN2-NUM-MARITAL-STATUS  TO WS-YGC-MS                     
                 MOVE FIN2-DTE-BIRTH           TO WS-YGC-DOB                    
                 MOVE FIN2-NUM-PPS-PREGNANT    TO WS-YGC-PREGNANT               
                 PERFORM 3400-COMPUTE-AGE      THRU 3400-EXIT                   
                 MOVE WS-AGE                   TO WS-YGC-AGE-YR                 
              END-IF                                                            
           WHEN '06'                                                            
              IF WS-YOR-FND-TAG = 'N'                                           
                 MOVE 'Y'                      TO WS-YOR-FND-TAG                
                 MOVE FIN2-NUM-LESSEE-CHECK    TO WS-YOR-CN-TAG                 
                 MOVE FIN2-NUM-NRIC            TO WS-YOR-NRIC                   
                 MOVE FIN2-NUM-MARITAL-STATUS  TO WS-YOR-MS                     
                 MOVE FIN2-DTE-BIRTH           TO WS-YOR-DOB                    
                 MOVE FIN2-NUM-PPS-PREGNANT    TO WS-YOR-PREGNANT               
                 PERFORM 3400-COMPUTE-AGE      THRU 3400-EXIT                   
                 MOVE WS-AGE                   TO WS-YOR-AGE-YR                 
              END-IF                                                            
           WHEN OTHER                                                           
              CONTINUE                                                          
           END-EVALUATE                                                         
                                                                                
           PERFORM 2200-READ-FIN2        THRU 2200-EXIT                         
           .                                                                    
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************                                                
       3300-DETERMINE-FTPMC-ELG.                                                
      **************************                                                
           IF (WS-HA1-FND-TAG = 'Y' AND                                         
              (WS-HA1-CN-TAG = 'N' OR SPACES)) OR                               
              (WS-SPS-FND-TAG = 'Y' AND                                         
              (WS-SPS-CN-TAG = 'N' OR SPACES))                                  
              MOVE 'CORE NUCLEUS ERROR' TO WS-REJ-REMARK                        
              MOVE 'N'                  TO WS-CDE-FAMILY-OUTCOME                
              PERFORM 9000-OUT-REJECT   THRU 9000-EXIT                          
              GO TO 3300-EXIT                                                   
           END-IF                                                               
                                                                                
           IF (WS-HA1-FND-TAG = 'Y' AND WS-HA1-MS = SPACE) OR                   
              (WS-SPS-FND-TAG = 'Y' AND WS-SPS-MS = SPACE) OR                   
              (WS-YCH-FND-TAG = 'Y' AND WS-YCH-MS = SPACE) OR                   
              (WS-YGC-FND-TAG = 'Y' AND WS-YGC-MS = SPACE)                      
              MOVE 'MARITAL STATUS ERROR' TO WS-REJ-REMARK                      
              MOVE 'N'                    TO WS-CDE-FAMILY-OUTCOME              
              PERFORM 9000-OUT-REJECT     THRU 9000-EXIT                        
              GO TO 3300-EXIT                                                   
           END-IF                                                               
                                                                                
           IF (WS-HA1-FND-TAG = 'Y' AND WS-HA1-AGE-YR = 0) OR                   
              (WS-SPS-FND-TAG = 'Y' AND WS-SPS-AGE-YR = 0)                      
              MOVE 'Y'                     TO WS-REJ-TAG                        
              MOVE 'AGE ERROR'             TO WS-REJ-REMARK                     
              MOVE 'N'                     TO WS-CDE-FAMILY-OUTCOME             
              PERFORM 9000-OUT-REJECT      THRU 9000-EXIT                       
              GO TO 3300-EXIT                                                   
           END-IF                                                               
                                                                                
           IF WS-HA1-FND-TAG = 'Y' AND WS-SPS-FND-TAG = 'Y' AND                 
              WS-HA1-MS = '2' AND WS-SPS-MS = '2' AND                           
              WS-HA1-CN-TAG = 'Y' AND WS-SPS-CN-TAG = 'Y'                       
              IF WS-HA1-PREGNANT = 'Y' OR WS-SPS-PREGNANT = 'Y'                 
                 MOVE 'C'                   TO WS-CDE-FAMILY-OUTCOME            
                 PERFORM 8000-OUT-ACCEPT    THRU 8000-EXIT                      
              ELSE                                                              
                 IF WS-YCH-FND-TAG = 'Y' AND WS-YCH-AGE-YR < 19                 
                    MOVE 'C'                TO WS-CDE-FAMILY-OUTCOME            
                    PERFORM 8000-OUT-ACCEPT THRU 8000-EXIT                      
                 ELSE                                                           
                    PERFORM 3500-DTRMINE-YCHGC THRU 3500-EXIT                   
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              PERFORM 3600-DTRMINE-OTHER    THRU 3600-EXIT                      
           END-IF                                                               
                                                                                
           IF WS-CDE-FAMILY-OUTCOME = SPACE                                     
              MOVE 'Y'                    TO WS-REJ-TAG                         
              MOVE 'N'                    TO WS-CDE-FAMILY-OUTCOME              
              MOVE 'INELIGIBLE FAMILY'    TO WS-REJ-REMARK                      
              PERFORM 9000-OUT-REJECT     THRU 9000-EXIT                        
           END-IF                                                               
           .                                                                    
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       3400-COMPUTE-AGE.                                                        
      ******************                                                        
           MOVE 0                     TO WS-AGE                                 
           MOVE FIN2-DTE-BIRTH        TO WS-FIN2-DOB                            
                                                                                
                                                                                
           IF FIN2-DTE-BIRTH = ZEROES                                           
              COMPUTE WS-AGE = 0                                                
           ELSE                                                                 
              COMPUTE WS-DAY = WS-DTE-HFE-APPLN-DD - WS-FIN2-DOB-DD             
              COMPUTE WS-MTH = WS-DTE-HFE-APPLN-MM - WS-FIN2-DOB-MM             
              COMPUTE WS-YRS = WS-DTE-HFE-APPLN-CCYY -                          
                               WS-FIN2-DOB-CCYY                                 
                                                                                
              IF WS-DAY < 0                                                     
                 COMPUTE WS-MTH = WS-MTH - 1                                    
              END-IF                                                            
                                                                                
              IF WS-MTH < 0                                                     
                 COMPUTE WS-YRS = WS-YRS - 1                                    
              END-IF                                                            
                                                                                
              COMPUTE WS-AGE = WS-YRS                                           
           END-IF                                                               
           .                                                                    
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *********************                                                     
       3500-DTRMINE-YCHGC.                                                      
      *********************                                                     
           IF WS-YCH-FND-TAG = 'Y' AND                                          
              (WS-YCH-AGE-YR > 19 OR WS-YCH-AGE-YR = 19) AND                    
              WS-YCH-CN-TAG = 'Y' AND WS-YCH-MS = '1'                           
              IF WS-YGC-FND-TAG = 'Y' AND WS-YGC-AGE-YR < 19                    
                 MOVE 'O'                    TO WS-CDE-FAMILY-OUTCOME           
                 PERFORM 8000-OUT-ACCEPT     THRU 8000-EXIT                     
              ELSE                                                              
                 MOVE 'Y'                    TO WS-REJ-TAG                      
                 MOVE 'N'                    TO WS-CDE-FAMILY-OUTCOME           
                 MOVE 'INELIGIBLE MULTI-GEN' TO WS-REJ-REMARK                   
                 PERFORM 9000-OUT-REJECT     THRU 9000-EXIT                     
              END-IF                                                            
           ELSE                                                                 
              IF WS-YCH-FND-TAG = 'Y' AND                                       
                 (WS-YCH-AGE-YR > 19 OR WS-YCH-AGE-YR = 19) AND                 
                 WS-YCH-CN-TAG = 'Y' AND WS-YCH-MS = '2'                        
                 MOVE 'Y'                    TO WS-REJ-TAG                      
                 MOVE 'N'                    TO WS-CDE-FAMILY-OUTCOME           
                 MOVE 'CHILD >= 19'          TO WS-REJ-REMARK                   
                 PERFORM 9000-OUT-REJECT     THRU 9000-EXIT                     
              END-IF                                                            
           END-IF                                                               
                                                                                
           IF WS-YCH-FND-TAG = 'N' AND WS-HAS-CHILD = 'Y'                       
              IF WS-HA1-AGE-YR < 41 AND                                         
                 WS-SPS-AGE-YR < 41 AND                                         
                 WS-HA1-MS = '2' AND WS-SPS-MS = '2' AND                        
                 WS-HA1-CN-TAG = 'Y' AND WS-SPS-CN-TAG = 'Y'                    
                 MOVE 'M'                   TO WS-CDE-FAMILY-OUTCOME            
                 PERFORM 8000-OUT-ACCEPT    THRU 8000-EXIT                      
              ELSE                                                              
                 MOVE 'Y'                   TO WS-REJ-TAG                       
                 MOVE 'PARENT >=41 WITH NC CHILD'                               
                                            TO WS-REJ-REMARK                    
                 MOVE 'N'                   TO WS-CDE-FAMILY-OUTCOME            
                 PERFORM 9000-OUT-REJECT    THRU 9000-EXIT                      
              END-IF                                                            
           END-IF                                                               
                                                                                
           IF WS-YCH-FND-TAG = 'N' AND WS-HAS-CHILD = 'N'                       
              IF WS-HA1-AGE-YR < 41 AND                                         
                 WS-SPS-AGE-YR < 41 AND                                         
                 WS-HA1-MS = '2' AND WS-SPS-MS = '2' AND                        
                 WS-HA1-CN-TAG = 'Y' AND WS-SPS-CN-TAG = 'Y'                    
                 MOVE 'M'                   TO WS-CDE-FAMILY-OUTCOME            
                 PERFORM 8000-OUT-ACCEPT    THRU 8000-EXIT                      
              ELSE                                                              
                 MOVE 'Y'                   TO WS-REJ-TAG                       
                 MOVE 'N'                   TO WS-CDE-FAMILY-OUTCOME            
                 MOVE 'PARENT >=41 WITH NO CHILD'                               
                                            TO WS-REJ-REMARK                    
                 PERFORM 9000-OUT-REJECT    THRU 9000-EXIT                      
              END-IF                                                            
           END-IF                                                               
           .                                                                    
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ********************                                                      
       3600-DTRMINE-OTHER.                                                      
      ********************                                                      
           IF WS-HA1-FND-TAG = 'Y' AND                                          
              (WS-HA1-MS = '3' OR '4' OR '5') AND                               
              WS-HA1-CN-TAG = 'Y'                                               
              IF WS-YCH-FND-TAG = 'Y' AND WS-YCH-AGE-YR < 19                    
                 MOVE 'O'                   TO WS-CDE-FAMILY-OUTCOME            
                 PERFORM 8000-OUT-ACCEPT    THRU 8000-EXIT                      
              ELSE                                                              
                 IF WS-YCH-FND-TAG = 'Y' AND WS-YCH-AGE-YR >= 19                
                    MOVE 'Y'  TO WS-REJ-TAG                                     
                    MOVE 'D/W/P WITH CHILD >= 19' TO WS-REJ-REMARK              
                    MOVE 'N'                  TO WS-CDE-FAMILY-OUTCOME          
                    PERFORM 9000-OUT-REJECT   THRU 9000-EXIT                    
                    GO TO 3600-EXIT                                             
                 END-IF                                                         
                                                                                
                 IF FIN1-CDE-ELIG-SCH = 'OR' AND WS-YOR-AGE-YR < 19             
                    MOVE 'O'                 TO WS-CDE-FAMILY-OUTCOME           
                    PERFORM 8000-OUT-ACCEPT  THRU 8000-EXIT                     
                 ELSE                                                           
                    MOVE 'Y'                  TO WS-REJ-TAG                     
                    MOVE 'INELIGIBLE D/W/P'   TO WS-REJ-REMARK                  
                    MOVE 'N'                  TO WS-CDE-FAMILY-OUTCOME          
                    PERFORM 9000-OUT-REJECT   THRU 9000-EXIT                    
                 END-IF                                                         
              END-IF                                                            
           END-IF                                                               
                                                                                
           IF WS-HA1-FND-TAG = 'Y' AND WS-HA1-MS = '1' AND                      
              FIN1-CDE-ELIG-SCH = 'OR'                                          
              IF WS-YOR-FND-TAG = 'Y' AND WS-YOR-AGE-YR < 19                    
                 MOVE 'O'                    TO WS-CDE-FAMILY-OUTCOME           
                 PERFORM 8000-OUT-ACCEPT     THRU 8000-EXIT                     
              ELSE                                                              
                 MOVE 'Y'                    TO WS-REJ-TAG                      
                 MOVE 'ORPHAN SIBLING >= 19' TO WS-REJ-REMARK                   
                 MOVE 'N'                    TO WS-CDE-FAMILY-OUTCOME           
                 PERFORM 9000-OUT-REJECT     THRU 9000-EXIT                     
              END-IF                                                            
           END-IF                                                               
           .                                                                    
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************                                                          
       4000-REST-FIN1.                                                          
      ****************                                                          
           MOVE 'PERSON NOT FOUND'       TO WS-REJ-REMARK                       
           MOVE 'N'                      TO WS-CDE-FAMILY-OUTCOME               
           PERFORM 9000-OUT-REJECT       THRU 9000-EXIT                         
           PERFORM 2100-READ-FIN1        THRU 2100-EXIT                         
           .                                                                    
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************                                                          
       5000-REST-FIN2.                                                          
      ****************                                                          
           PERFORM 2200-READ-FIN2   THRU 2200-EXIT                              
           .                                                                    
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************                                                        
       6000-CTL-SUMMARY.                                                        
      ******************                                                        
           DISPLAY 'BP13CM24-I01: CONTROL SUMMARY'                              
           DISPLAY 'SYSF001 DATE : ' F001-DTE-CURRENT                           
           DISPLAY 'CURRENT-DATE : ' WS-CURR-CDTE                               
           DISPLAY 'CURRENT-TIME : ' WS-TME-CURR                                
           DISPLAY 'BP13FIN1 PROCESS CNT : ' WS-FIN1-CNT                        
           DISPLAY 'BP13FIN2 PROCESS CNT : ' WS-FIN2-CNT                        
           DISPLAY 'ACCEPTED CNT         : ' WS-ACP-CNT                         
           DISPLAY 'REJECTED CNT         : ' WS-REJ-CNT                         
           .                                                                    
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************                                                           
       7000-CTL-REC.                                                            
      ***************                                                           
           INITIALIZE FOT1-CTL-REC                                              
           INITIALIZE FOT2-CTL-REC                                              
           MOVE WS-HDL-BALLOT-QTR   TO FOT1-CTL-DTE-BALLOT-QTR                  
                                       FOT2-CTL-DTE-BALLOT-QTR                  
           MOVE HIGH-VALUES         TO FOT1-CTL-HIGH-VAL                        
                                       FOT2-CTL-HIGH-VAL                        
           COMPUTE FOT1-RCD-CNT = WS-ACP-CNT                                    
           COMPUTE FOT2-RCD-CNT = WS-REJ-CNT                                    
           MOVE SPACES              TO FOT1-CTL-FILLER                          
                                       FOT2-CTL-FILLER                          
           WRITE BP13FOT1-REC                                                   
           WRITE BP13FOT2-REC                                                   
           .                                                                    
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************                                                          
       8000-OUT-ACCEPT.                                                         
      ****************                                                          
           MOVE WS-DTL-BALLOT-QTR        TO FOT1-DTL-DTE-BALLOT-QTR             
           MOVE FIN1-NUM-REGN            TO FOT1-NUM-REGN                       
           MOVE FIN1-CDE-ELIG-SCH        TO FOT1-CDE-ELIG-SCH                   
           MOVE FIN1-NUM-ALLO-CAT        TO FOT1-NUM-ALLO-CAT                   
           MOVE FIN1-CDE-HOUSEHOLD       TO FOT1-CDE-HOUSEHOLD                  
           MOVE FIN1-NUM-FT2T            TO FOT1-NUM-FT2T                       
           MOVE FIN1-DTE-REQUEST         TO FOT1-DTE-REQUEST                    
           MOVE FIN1-NUM-HLA             TO FOT1-NUM-HFE                        
           MOVE FIN1-HFE-APPLN-DTE       TO FOT1-DTE-HFE-APPLN                  
           MOVE WS-CDE-FAMILY-OUTCOME    TO FOT1-CDE-FAMILY-OUTCOME             
           MOVE SPACES                   TO FOT1-CDE-RES-OUTCOME                
           MOVE SPACES                   TO FOT1-CDE-CHANCE-OUTCOME             
           MOVE SPACES                   TO FOT1-CDE-FTPMC                      
           MOVE SPACES                   TO FOT1-REMARK                         
           MOVE SPACES                   TO FOT1-DTL-FILLER-1                   
           MOVE SPACES                   TO FOT1-DTL-FILLER-2                   
           ADD  1                        TO WS-ACP-CNT                          
           WRITE BP13FOT1-REC                                                   
           .                                                                    
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************                                                         
       9000-OUT-REJECT.                                                         
      *****************                                                         
           MOVE WS-DTL-BALLOT-QTR        TO FOT2-DTL-DTE-BALLOT-QTR             
           MOVE FIN1-NUM-REGN            TO FOT2-NUM-REGN                       
           MOVE FIN1-CDE-ELIG-SCH        TO FOT2-CDE-ELIG-SCH                   
           MOVE FIN1-NUM-ALLO-CAT        TO FOT2-NUM-ALLO-CAT                   
           MOVE FIN1-CDE-HOUSEHOLD       TO FOT2-CDE-HOUSEHOLD                  
           MOVE FIN1-NUM-FT2T            TO FOT2-NUM-FT2T                       
           MOVE FIN1-DTE-REQUEST         TO FOT2-DTE-REQUEST                    
           MOVE FIN1-NUM-HLA             TO FOT2-NUM-HFE                        
           MOVE FIN1-HFE-APPLN-DTE       TO FOT2-DTE-HFE-APPLN                  
           MOVE WS-CDE-FAMILY-OUTCOME    TO FOT2-CDE-FAMILY-OUTCOME             
           MOVE WS-CDE-FAMILY-OUTCOME    TO FOT2-CDE-FTPMC                      
           MOVE SPACES                   TO FOT2-CDE-RES-OUTCOME                
           MOVE SPACES                   TO FOT2-CDE-CHANCE-OUTCOME             
           MOVE WS-REJ-REMARK            TO FOT2-REMARK                         
           MOVE SPACES                   TO FOT2-DTL-FILLER-1                   
           MOVE SPACES                   TO FOT2-DTL-FILLER-2                   
           ADD  1                        TO WS-REJ-CNT                          
           WRITE BP13FOT2-REC                                                   
           .                                                                    
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************                                                         
       9998-CLOSE-FILE.                                                         
      *****************                                                         
           CLOSE BP13FIN1 BP13FIN2 BP13FOT1 BP13FOT2 SY02F001                   
           .                                                                    
       9998-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************                                                         
       9999-CANCEL-JOB.                                                         
      *****************                                                         
           MOVE '111' TO RETURN-CODE                                            
           DISPLAY WS-ERR-MSG                                                   
           STOP RUN                                                             
           .                                                                    
       9999-EXIT.                                                               
           EXIT.                                                                
