      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.   BP13CH24.                                                  
      *AUTHOR.       IRVIN CHING.                                               
      *DATE-WRITTEN. 12/11/13.                                                  
                                                                                
      ***********************************************************               
      *  OBJECTIVE  : CREATE DEBARMENT FILE FOR ST/FTS CASES    *               
      *             : IF THEY DID NOT SELECTION FLAT WITH       *               
      *             : NON-SELECTION=02                          *               
      *  INPUT FILE :- BP04F010 (ST/FTS CASES FROM CH03)        *               
      *  I-O           BP13K665                                 *               
      *  OUTPUT FILE:- BP04FOUT                                 *               
      ***********************************************************               
      *                                                         *               
      * CHG NO   BY  DATE        REMARKS                        *               
      * ------   --- ----------  -----------------------------  *               
      * BP135039 IMC 12/11/2013  NEW PROGRAM.                   *               
      * BP138645 KR13 15/04/2021  GET K820 CSTMR_SRC FOR F010.  *               
      ***********************************************************               
                                                                                
      *************************                                                 
       ENVIRONMENT DIVISION.                                                    
      *************************                                                 
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *************************                                                 
       INPUT-OUTPUT SECTION.                                                    
      *************************                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP04F010 ASSIGN TO BP04F010.                                  
                                                                                
           SELECT BP13K665 ASSIGN TO BP13K665                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS WS-K665-KEY-FLD                            
                  FILE STATUS     IS WS-K665-STATUS.                            
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K820-KEY-FLD                               
                  ALTERNATE RECORD KEY IS K820-NUM-NRIC                         
                                  WITH DUPLICATES                               
                  FILE STATUS     IS WS-K820-STATUS.                            
                                                                                
           SELECT BP04FOUT ASSIGN TO BP04FOUT.                                  
                                                                                
      *************************                                                 
       DATA DIVISION.                                                           
      *************************                                                 
       FILE SECTION.                                                            
                                                                                
       FD BP04F010                                                              
                      RECORD CONTAINS 300 CHARACTERS.                           
       COPY P04F010.                                                            
                                                                                
       FD  BP13K665                                                             
                      RECORD CONTAINS 50 CHARACTERS                             
                      LABEL RECORDS ARE STANDARD.                               
       01  WS-BP13K665-REC.                                                     
           05  WS-K665-KEY-FLD         PIC X(08).                               
           05  FILLER                  PIC X(42).                               
                                                                                
       FD   BP13K820                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K820.                                                           
                                                                                
       FD  BP04FOUT                                                             
                      RECORD CONTAINS 300 CHARACTERS.                           
       01  BP04FOUT-REC          PIC X(300).                                    
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
       01  WS-SWITCHES.                                                         
           05  WS-F010-EOF               PIC X    VALUE 'N'.                    
               88 F010-EOF                        VALUE 'Y'.                    
           05  WS-FOUND                  PIC X    VALUE SPACES.                 
           05  WS-DEBARMENT-TAG          PIC X    VALUE SPACES.                 
           05  WS-DEBAR-TAG              PIC X    VALUE SPACES.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-READ-CNT-F010          PIC 9(6) VALUE ZEROS.                  
           05  WS-K665-READ              PIC 9(6) VALUE ZEROS.                  
           05  WS-K665-WRITE             PIC 9(6) VALUE ZEROS.                  
           05  WS-FOUT-WRITE             PIC 9(6) VALUE ZEROS.                  
           05  WS-LINE-CTR               PIC 9(6) VALUE 60.                     
           05  WS-LINE-CTR1              PIC 9(6) VALUE 60.                     
           05  WS-PAGE-CTR               PIC 9(6) VALUE ZEROS.                  
           05  WS-PAGE-CTR1              PIC 9(6) VALUE ZEROS.                  
           05  WS-SEQ-NO                 PIC 9(6) VALUE ZEROS.                  
           05  WS-SEQ-NO1                PIC 9(6) VALUE ZEROS.                  
           05  WS-DIRECT-PURCHASE        PIC 9(6) VALUE ZEROS.                  
           05  WS-PERIOD                 PIC 9(3) VALUE ZEROS.                  
                                                                                
       01  WS-TEMPORARY-VARS.                                                   
           05  WS-PREV-REGN               PIC X(8) VALUE SPACES.                
           05  WS-K665-NUM-LAST-DEBARMENT PIC X(13) VALUE SPACES.               
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K665-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-F010-STATUS            PIC 99   VALUE ZEROS.                  
           05  WS-K820-STATUS            PIC 99   VALUE ZEROS.                  
                                                                                
       01  WS-CURR-DATE.                                                        
           05 WS-CURR-CC           PIC 9(02).                                   
           05 WS-YYMMDD.                                                        
              10 WS-CURR-YY        PIC 9(02).                                   
              10 WS-CURR-MM        PIC 9(02).                                   
              10 WS-CURR-DD        PIC 9(02).                                   
                                                                                
       COPY BP13K665.                                                           
                                                                                
      *************************                                                 
       PROCEDURE DIVISION.                                                      
      *************************                                                 
                                                                                
      *-----------------                                                        
       000-MAIN-LOGIC.                                                          
      *-----------------                                                        
            PERFORM 100-OPEN-FILES   THRU 100-EXIT.                             
            PERFORM 200-READ-F010    THRU 200-EXIT.                             
            PERFORM 300-PROCESS-K665 THRU 300-EXIT                              
                    UNTIL F010-EOF.                                             
            PERFORM 999-CLOSE-FILES  THRU 999-EXIT.                             
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------                                                        
       100-OPEN-FILES.                                                          
      *-----------------                                                        
           OPEN INPUT  BP04F010                                                 
                       BP13K820                                                 
                I-O    BP13K665                                                 
                OUTPUT BP04FOUT.                                                
                                                                                
           IF WS-K665-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K665-STATUS ' WS-K665-STATUS              
              MOVE WS-K665-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-K820-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'OPENING ERROR, K820-STATUS ' WS-K820-STATUS              
              MOVE WS-K820-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURR-DATE.                          
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------                                                        
       200-READ-F010.                                                           
      *-----------------                                                        
           READ BP04F010 AT END                                                 
                MOVE 'Y' TO WS-F010-EOF                                         
                GO TO 200-EXIT.                                                 
                                                                                
           ADD   1     TO WS-READ-CNT-F010.                                     
           MOVE SPACES TO WS-FOUND.                                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------                                                        
       300-PROCESS-K665.                                                        
      *-----------------                                                        
                                                                                
           PERFORM 400-GET-DEBARMENT-NO         THRU 400-EXIT                   
           PERFORM 500-PREPARE-DEBARMENT-REC    THRU 500-EXIT                   
           PERFORM 200-READ-F010  THRU 200-EXIT.                                
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------                                                     
       400-GET-DEBARMENT-NO.                                                    
      *--------------------                                                     
           MOVE  LOW-VALUES    TO WS-K665-KEY-FLD.                              
           START BP13K665 KEY IS >= WS-K665-KEY-FLD                             
                 INVALID KEY                                                    
                 DISPLAY 'READING ERROR, K665-STATUS ' WS-K665-STATUS           
                 MOVE WS-K665-STATUS       TO RETURN-CODE                       
                 PERFORM 999-CLOSE-FILES THRU 999-EXIT.                         
                                                                                
           PERFORM 410-READ-BP13K665 THRU 410-EXIT                              
                   UNTIL WS-K665-KEY-FLD = HIGH-VALUES.                         
                                                                                
           IF K665-KEY-FLD = SPACES OR LOW-VALUES                               
              MOVE 0                TO  K665-NUM-LAST-DEBREF-ID                 
           END-IF.                                                              
                                                                                
           ADD   1                  TO  K665-NUM-LAST-DEBREF-ID.                
           MOVE  'SL'               TO  K665-NUM-DEBREF-AO                      
                                        K665-NUM-LAST-DEBREF-AO.                
           MOVE  WS-CURR-DATE       TO  K665-DTE-ISSUE-DEBARMENT.               
           MOVE  WS-CURR-DATE(1:4)  TO  K665-NUM-LAST-DEBREF-YR.                
           MOVE  'P13CH24'          TO  K665-NUM-USERID.                        
           MOVE K665-NUM-LAST-DEBARMENT TO WS-K665-NUM-LAST-DEBARMENT           
                                                                                
           IF WS-DEBARMENT-TAG = 'Y'                                            
              REWRITE WS-BP13K665-REC FROM BP13K665-REC                         
           ELSE                                                                 
              WRITE   WS-BP13K665-REC FROM BP13K665-REC.                        
                                                                                
           EVALUATE WS-K665-STATUS                                              
           WHEN 00                                                              
              MOVE  'Y'               TO  WS-DEBARMENT-TAG                      
              ADD   1                 TO  WS-K665-WRITE                         
                                                                                
           WHEN 22                                                              
              REWRITE WS-BP13K665-REC FROM BP13K665-REC                         
                      INVALID KEY                                               
                      DISPLAY 'REWRITING ERROR, K665-STATUS '                   
                      WS-K665-STATUS                                            
                      MOVE WS-K665-STATUS       TO RETURN-CODE                  
                      PERFORM 999-CLOSE-FILES THRU 999-EXIT                     
              END-REWRITE                                                       
              ADD   1                 TO  WS-K665-WRITE                         
              MOVE  'Y'               TO   WS-DEBARMENT-TAG                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'WRITING ERROR, K665-STATUS ' WS-K665-STATUS              
              MOVE WS-K665-STATUS     TO   RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------                                                     
       410-READ-BP13K665.                                                       
      *--------------------                                                     
           READ BP13K665 NEXT                                                   
                AT END MOVE HIGH-VALUES TO WS-K665-KEY-FLD                      
                GO TO 410-EXIT.                                                 
                                                                                
           ADD 1 TO WS-K665-READ.                                               
                                                                                
           IF WS-K665-KEY-FLD NOT = SPACES AND LOW-VALUES                       
              MOVE WS-BP13K665-REC TO BP13K665-REC.                             
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------                                                     
       500-PREPARE-DEBARMENT-REC.                                               
      *--------------------                                                     
           MOVE SPACES TO BP04FOUT-REC.                                         
                                                                                
           PERFORM 530-WRITE-BP04F010   THRU 530-EXIT.                          
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *--------------------                                                     
       530-WRITE-BP04F010.                                                      
      *--------------------                                                     
                                                                                
           MOVE WS-K665-NUM-LAST-DEBARMENT  TO F010-DEB-REF-NO.                 
                                                                                
           PERFORM 535-READ-BP13K820        THRU 535-EXIT                       
                                                                                
           WRITE BP04FOUT-REC FROM P04F010-REC.                                 
           ADD 1 TO WS-FOUT-WRITE.                                              
                                                                                
       530-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       535-READ-BP13K820.                                                       
      ******************************************************************        
           INITIALIZE  BP13K820-REC                                             
                                                                                
           MOVE SPACES                     TO K820-KEY-FLD                      
           MOVE F010-NUM-UINFIN(1:9)       TO K820-NUM-NRIC                     
                                                                                
           START BP13K820 KEY >= K820-NUM-NRIC                                  
                                                                                
           EVALUATE WS-K820-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    READ BP13K820 NEXT                                          
                    IF WS-K820-STATUS = 00 OR 02                                
                       IF K820-NUM-NRIC = F010-NUM-UINFIN(1:9)                  
                          MOVE K820-NUM-CSTMR-SOURCE                            
                                            TO F010-NUM-CSTMR-SOURCE            
                       END-IF                                                   
                    END-IF                                                      
               WHEN 23                                                          
                    DISPLAY 'KEY NOT FOUND IN BP13K820!' K820-KEY-FLD           
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K820!'                          
                             ' / KEY : ' K820-KEY-FLD                           
                             ' / FILE STATUS :' WS-K820-STATUS                  
                    PERFORM 999-CLOSE-FILES  THRU 999-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       535-EXIT.                                                                
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------------*        
       999-CLOSE-FILES.                                                         
      *----------------------------------------------------------------*        
           CLOSE BP04F010                                                       
                 BP13K665                                                       
                 BP13K820                                                       
                 BP04FOUT.                                                      
                                                                                
           IF WS-K665-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K665, STATUS ' WS-K665-STATUS          
              MOVE WS-K665-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-F010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP04F010, STATUS ' WS-F010-STATUS          
              MOVE WS-F010-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K820, STATUS ' WS-K820-STATUS          
              MOVE WS-K820-STATUS        TO RETURN-CODE.                        
                                                                                
           DISPLAY '*------------------ BP13CH24 -------------------'.          
           DISPLAY '                  ' WS-CURR-DATE                            
           DISPLAY 'NO OF REC READ        BP13FDBR   = '                        
                    WS-READ-CNT-F010.                                           
           DISPLAY 'NO OF REC READ        BP13K665   = '                        
                    WS-K665-READ                                                
           DISPLAY 'NO OF REC WRITTEN     BP13K665   = '                        
                    WS-K665-WRITE                                               
           DISPLAY 'NO OF REC WRITTEN     BP04F010   = '                        
                    WS-FOUT-WRITE                                               
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
