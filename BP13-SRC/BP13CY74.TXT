      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13CY74.                                                 
      *AUTHOR.        PAULO CAMIA LEGASPI.                                      
      *DATE-WRITTEN.  12/08/10.                                                 
      *****************************************************************         
      *OBJECTIVE - TO UPDATE BP13K558 AND UPDATE F558-NUM-PARENT-CHILD*         
      *                                                               *         
      *     INPUT FILE : 1. BP13F558                                  *         
      *       I-O  FILE: 1. BP13K558                                  *         
      *                                                               *         
      *---------------------------------------------------------------*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP133792 12/08/2010 PCL3 NEW PROGRAM                          *         
      * BP134598 31/07/2012 ZDD1 TO UPDATE HH TO 'H'                  *         
      *****************************************************************         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F558 ASSIGN TO BP13F558.                                 
           SELECT  BP13K558 ASSIGN TO BP13K558                                  
                            ORGANIZATION   IS INDEXED                           
                            ACCESS MODE    IS RANDOM                            
                            RECORD KEY     IS K558-KEY-FLD                      
                            FILE STATUS    IS WS-K558-STATUS.                   
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13K558                                                            
            RECORD CONTAINS  600 CHARACTERS.                                    
       COPY BP13K558.                                                           
                                                                                
       FD   BP13F558                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F558.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F558             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K558             PIC X(1)  VALUE 'N'.                     
           05  WS-K558-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F558-READ            PIC 9(07) VALUE ZEROES.                  
           05  WS-CNT-FND              PIC 9(07) VALUE ZEROES.                  
           05  WS-CNT-NFND             PIC 9(07) VALUE ZEROES.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-SYSTEM-DATE          PIC 9(08) VALUE ZEROES.                  
           05  WS-K558-FOUND           PIC X(01) VALUE 'N'.                     
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F558      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL F558-KEY-FLD = HIGH-VALUES.                                  
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.  EXIT.                                                        
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F558                                                 
                I-O    BP13K558.                                                
                                                                                
           IF WS-K558-STATUS  NOT =  00 AND 97                                  
              DISPLAY 'ERROR OPENING - BP13K558 : ' WS-K558-STATUS              
              MOVE     WS-K558-STATUS            TO RETURN-CODE                 
              PERFORM  9999-CLOSE-ROUTINE      THRU 9999-EXIT                   
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F558.                                                      
      ******************************************************************        
                                                                                
           READ BP13F558           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F558                             
                MOVE HIGH-VALUES   TO   F558-KEY-FLD                            
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO   WS-F558-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           PERFORM 4000-READ-BP13K558          THRU 4000-EXIT.                  
                                                                                
           IF WS-K558-FOUND = 'Y'                                               
              IF K558-NUM-PARENT-CHILD NOT = 'P'                                
                 PERFORM 5000-REWRITE-BP13K558 THRU 5000-EXIT                   
              END-IF                                                            
           ELSE                                                                 
              PERFORM 6000-WRITE-BP13K558      THRU 6000-EXIT                   
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F558          THRU 2000-EXIT.                  
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-READ-BP13K558.                                                      
      ******************************************************************        
                                                                                
           MOVE SPACES                                TO BP13K558-REC.          
           INITIALIZE                                    BP13K558-REC.          
                                                                                
           MOVE 'N'                                   TO WS-K558-FOUND.         
           MOVE F558-KEY-FLD                          TO K558-KEY-FLD.          
                                                                                
           READ BP13K558.                                                       
                                                                                
           EVALUATE WS-K558-STATUS                                              
               WHEN 00                                                          
                    MOVE 'Y'                          TO WS-K558-FOUND          
                                                                                
               WHEN 23                                                          
                    CONTINUE                                                    
                                                                                
               WHEN OTHER                                                       
                    MOVE WS-K558-STATUS               TO RETURN-CODE            
                    DISPLAY 'ERROR READING BP13K558: ' WS-K558-STATUS           
                            ' KEY : ' K558-KEY-FLD                              
                    PERFORM  9999-CLOSE-ROUTINE THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-REWRITE-BP13K558.                                                   
      *-------------------------------------------------------------            
                                                                                
           MOVE 'P'                           TO K558-NUM-PARENT-CHILD.         
           MOVE 'H'                           TO K558-CDE-HOUSEHOLD             
                                                                                
           REWRITE BP13K558-REC.                                                
                                                                                
           EVALUATE WS-K558-STATUS                                              
               WHEN 00                                                          
                    ADD 1                          TO WS-CNT-FND                
                                                                                
               WHEN OTHER                                                       
                 DISPLAY 'BP13K558 REWRITE ERR-STATUS ' WS-K558-STATUS          
                       ' KEY IS ' K558-KEY-FLD                                  
                 MOVE WS-K558-STATUS               TO RETURN-CODE               
                 PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-WRITE-BP13K558.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE BP13F558-REC                  TO BP13K558-REC.                  
           MOVE 'P'                           TO K558-NUM-PARENT-CHILD.         
                                                                                
           WRITE BP13K558-REC.                                                  
                                                                                
           EVALUATE WS-K558-STATUS                                              
               WHEN 00                                                          
                    ADD 1                          TO WS-CNT-NFND               
                                                                                
               WHEN OTHER                                                       
                 DISPLAY 'BP13K558 WRITE ERR-STATUS ' WS-K558-STATUS            
                       ' KEY IS ' K558-KEY-FLD                                  
                 MOVE WS-K558-STATUS               TO RETURN-CODE               
                 PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CY74.'                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F558 RECORDS READ ........ : '                
                    WS-F558-READ.                                               
           DISPLAY '(2) NO OF BP13K558 RECORDS UPDATED ..... : '                
                    WS-CNT-FND.                                                 
           DISPLAY '(2) NO OF BP13K558 RECORDS WRITTEN ..... : '                
                    WS-CNT-NFND.                                                
                                                                                
           CLOSE BP13F558                                                       
                 BP13K558.                                                      
                                                                                
           IF WS-K558-STATUS  NOT =  00 AND 97                                  
              DISPLAY 'ERROR CLOSING - BP13K558 : ' WS-K558-STATUS              
              MOVE     WS-K558-STATUS            TO RETURN-CODE                 
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.  EXIT.                                                        
