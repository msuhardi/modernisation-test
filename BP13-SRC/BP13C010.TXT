       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C010.                                                 
      *AUTHOR.        SATHAPPAN.                                                
      *DATE-WRITTEN.  05/07/02.                                                 
      *===============================================================*         
      *               SYSTEM OF COMMITMENT (BP13) *                   *         
      *===============================================================*         
      *                                                               *         
      * OBJECTIVE :  THIS PROGRAM IS USED TO GET THE MARRIAGE DATE FROM         
      *              CDB AND WRITTEN IT INTO P13F800A AND             *         
      *              NOT CASES IN CDB AND WRITTEN  INTO P13F800B.     *         
      *1. FILE DETAILS :                                              *         
      *    INPUT   :  BP13F800  ( MASTER FILE   )                     *         
      *    OUTPUT  :  P13F800A  ( FOUND CASES IN CDB )                *         
      *               P13F800B  ( NOT FOUND CASES IN CDB )            *         
      * --------------------------------------------------------------*         
      * CHG-NO    CHG-DATE     BY   DESCRIPTION                       *         
      * ------    --------     --   -----------                       *         
      * BP132172  05/07/2002   AS1  NEW PROGRAM                       *         
      * BP132313  28/04/2003   MJ16 INCLUDED SUBROUTINE AV02C013      *         
      *****************************************************************         
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
      *----------------------*                                                  
       CONFIGURATION SECTION.                                                   
      *----------------------*                                                  
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *----------------------*                                                  
       INPUT-OUTPUT SECTION.                                                    
      *----------------------*                                                  
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT    BP13F800   ASSIGN       TO   BP13F800.                     
                                                                                
           SELECT    P13F800A   ASSIGN       TO   P13F800A.                     
                                                                                
           SELECT    P13F800B   ASSIGN       TO   P13F800B.                     
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
      *-------------*                                                           
       FILE SECTION.                                                            
      *-------------*                                                           
                                                                                
       FD  BP13F800                                                             
           RECORD    CONTAINS 2000 CHARACTERS                                   
           BLOCK     CONTAINS   0 RECORDS                                       
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE     IS  F.                                            
       COPY BP13F800.                                                           
                                                                                
       FD  P13F800A                                                             
           RECORD    CONTAINS 2000 CHARACTERS                                   
           BLOCK     CONTAINS   0 RECORDS                                       
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE     IS  F.                                            
       01  WS-P13F800A-REC.                                                     
           05 WS-F800A-INFO1           PIC X(1992).                             
           05 WS-F800A-DTE-MRG         PIC X(08).                               
                                                                                
       FD  P13F800B                                                             
           RECORD    CONTAINS 2000 CHARACTERS                                   
           BLOCK     CONTAINS   0 RECORDS                                       
           LABEL     RECORDS  ARE STANDARD                                      
           RECORDING MODE     IS  F.                                            
       01  WS-P13F800B-REC.                                                     
           05 WS-F800B-INFO1           PIC X(1992).                             
           05 WS-F800B-DTE-MRG         PIC X(08).                               
                                                                                
      *-------------------------*                                               
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
       01  WS-SWITCHES.                                                         
           03  F800-EOF-IND            PIC X        VALUE SPACES.               
               88 F800-EOF             VALUE 'Y'.                               
                                                                                
       01  WS-ACCUMULATORS.                                                     
           03  WS-NO-WRT-F800A         PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-WRT-F800B         PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-READ-F800         PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-READ-MRG          PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-NOTFND-MRG        PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-READ-IDNTY        PIC 9(6)     VALUE ZEROES.               
           03  WS-NO-NOTFND-IDNTY      PIC 9(6)     VALUE ZEROES.               
           03  WS-COUNT                PIC 9(1)     VALUE ZERO.                 
                                                                                
           03  WS-ED-WRT-F800A         PIC Z,ZZ,ZZ9 VALUE ZEROES.               
           03  WS-ED-WRT-F800B         PIC Z,ZZ,ZZ9 VALUE ZEROES.               
           03  WS-ED-READ-F800         PIC Z,ZZ,ZZ9 VALUE ZEROES.               
           03  WS-ED-READ-MRG          PIC Z,ZZ,ZZ9 VALUE ZEROES.               
           03  WS-ED-NOTFND-MRG        PIC Z,ZZ,ZZ9 VALUE ZEROES.               
           03  WS-ED-READ-IDNTY        PIC Z,ZZ,ZZ9 VALUE ZEROES.               
           03  WS-ED-NOTFND-IDNTY      PIC Z,ZZ,ZZ9 VALUE ZEROES.               
                                                                                
       01  WS-TEMP.                                                             
           05  WS-SYS-DATE             PIC X(08)    VALUE SPACES.               
           05  WS-DATE-RUN             PIC X(10)    VALUE SPACES.               
           05  WS-NUM-NRIC             PIC X(09)    VALUE SPACES.               
           05  WS-NUM-CSTMR            PIC X(09)    VALUE SPACES.               
           05  WS-NUM-DTE-MRG          PIC X(09)    VALUE SPACES.               
           05  WS-NUM-NRIC-TYPE        PIC X(03)    VALUE SPACES.               
                                                                                
      ***************************************************************           
      *   AV02C013 SUBROUTINE VARIABLES                             *           
      ***************************************************************           
                                                                                
        01  WS-NUM-UINFIN            PIC  X(09).                                
        01  WS-VALIDATION-FLAG       PIC  X(01).                                
        01  WS-NUM-UFIN-TYPE         PIC  X(03).                                
                                                                                
      *COPY BE01W902.                                                           
                                                                                
            EXEC SQL INCLUDE SQLCA     END-EXEC.                                
            EXEC SQL INCLUDE P13IDNO   END-EXEC.                                
            EXEC SQL INCLUDE P13VMRG   END-EXEC.                                
                                                                                
      ******************************************************************        
      *            DEFINE THE CURSOR SNMEMRG                           *        
      ******************************************************************        
                                                                                
            EXEC SQL                                                            
                 DECLARE SNMEMRG  CURSOR FOR                                    
                 SELECT  NUM_CSTMR_HSBND ,                                      
                         NUM_CSTMR_WIFE ,                                       
                         DTE_MRG                                                
                   FROM  MRG                                                    
                   WHERE ( NUM_CSTMR_HSBND = :WS-NUM-CSTMR ) OR                 
                         ( NUM_CSTMR_WIFE  = :WS-NUM-CSTMR )                    
                   ORDER BY DTE_MRG DESC                                        
            END-EXEC.                                                           
                                                                                
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *----------------*                                                        
       0000-MAIN-PROCESS.                                                       
      *----------------*                                                        
                                                                                
             PERFORM 1000-OPEN-ROUTINE        THRU 1000-EXIT.                   
             PERFORM 2000-READ-BP13F800       THRU 2000-EXIT.                   
                                                                                
             PERFORM 3000-MAIN-PROCESS        THRU 3000-EXIT                    
               UNTIL F800-EOF.                                                  
                                                                                
             PERFORM 9999-CLOSE-ROUTINE       THRU 9999-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-ROUTINE.                                                       
      *----------------*                                                        
                                                                                
           OPEN       INPUT   BP13F800                                          
                      OUTPUT  P13F800A                                          
                              P13F800B.                                         
                                                                                
           MOVE   FUNCTION CURRENT-DATE(1:8) TO   WS-SYS-DATE.                  
           STRING WS-SYS-DATE(7:2) '/'                                          
                  WS-SYS-DATE(5:2) '/'                                          
                  WS-SYS-DATE(1:4)                                              
                  DELIMITED BY SIZE          INTO WS-DATE-RUN.                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       2000-READ-BP13F800.                                                      
      *-----------------*                                                       
                                                                                
           READ BP13F800 AT END  MOVE 'Y' TO F800-EOF-IND                       
                         GO TO 2000-EXIT.                                       
                                                                                
           ADD 1 TO WS-NO-READ-F800.                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------*                                                   
       3000-MAIN-PROCESS.                                                       
      *---------------------*                                                   
                                                                                
           MOVE   SPACES              TO    WS-NUM-NRIC                         
                                            WS-NUM-NRIC-TYPE                    
                                            WS-NUM-CSTMR.                       
                                                                                
      *    MOVE   SPACES              TO    BE01W902-COMMAREA.                  
      *    INITIALIZE BE01W902-COMMAREA.                                        
                                                                                
      *    MOVE F800-NUM-NRIC1        TO   BE01W902-IN-NUM-IDNTY-NO.            
                                                                                
           MOVE SPACES TO WS-NUM-UINFIN.                                        
           MOVE SPACES TO WS-VALIDATION-FLAG.                                   
           MOVE SPACES TO WS-NUM-UFIN-TYPE.                                     
                                                                                
           MOVE F800-NUM-NRIC1        TO   WS-NUM-UINFIN.                       
           PERFORM 3050-CONVERT-IDNTY-TYPE THRU 3050-EXIT.                      
                                                                                
           MOVE   F800-NUM-NRIC1      TO    WS-NUM-NRIC.                        
                                                                                
            EXEC SQL                                                            
                 SELECT NUM_CSTMR                                               
                  INTO :DCLIDNTY-NO.NUM-CSTMR                                   
                  FROM  IDNTY_NO                                                
                  WHERE NUM_IDNTY_NO = :WS-NUM-NRIC                             
                    AND NUM_IDNTY_NO_TYPE = :WS-NUM-NRIC-TYPE                   
                                                                                
            END-EXEC.                                                           
                                                                                
           EVALUATE SQLCODE                                                     
               WHEN 000                                                         
                    ADD        1                   TO  WS-NO-READ-IDNTY         
                    MOVE  NUM-CSTMR OF DCLIDNTY-NO TO   WS-NUM-CSTMR            
                    PERFORM 4000-READ-MRG-TABLE THRU 4000-EXIT                  
               WHEN 100                                                         
                    PERFORM 5100-WRITE-P13F800B  THRU 5100-EXIT                 
                    ADD        1                 TO  WS-NO-NOTFND-IDNTY         
                    DISPLAY  "REC. NOT FOUND IN IDNTY_NO TABLE  : "             
                              WS-NUM-NRIC                                       
               WHEN OTHER                                                       
                    DISPLAY 'SQL ERROR IN IDNTY_NO TABLE (APPL)'                
                    DISPLAY 'F800-NUM-NRIC1   :  ' F800-NUM-NRIC1               
                    DISPLAY 'SQL ERROR CODE  :  '  SQLCODE                      
                    DISPLAY SPACES                                              
                    PERFORM 9000-SQL-ERROR  THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
           PERFORM   2000-READ-BP13F800     THRU 2000-EXIT.                     
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       3050-CONVERT-IDNTY-TYPE.                                                 
      *-----------------------*                                                 
                                                                                
      *     CALL 'BE01C902' USING BE01W902-COMMAREA                             
                                                                                
            IF WS-NUM-UINFIN NOT = SPACES AND LOW-VALUES                        
               CALL 'AV02C013' USING WS-NUM-UINFIN  WS-VALIDATION-FLAG          
                                 WS-NUM-UFIN-TYPE                               
                                                                                
               PERFORM  3075-EVALUATE-UFIN-TYPE  THRU 3075-EXIT                 
            ELSE                                                                
               DISPLAY 'INVALID NUM-NRIC'                                       
            END-IF.                                                             
                                                                                
                                                                                
                                                                                
      *     IF BE01W902-OUT-RTN-MSGNO = SPACES                                  
      *        MOVE BE01W902-OUT-NUM-IDNTY-NO-TYPE  TO WS-NUM-NRIC-TYPE         
      *     ELSE                                                                
      *      IF BE01W902-OUT-RTN-MSGNO  = '0127'                                
      *        MOVE        'XIN'                    TO WS-NUM-NRIC-TYPE         
      *      ELSE                                                               
      *        DISPLAY ' ERROR IN BE01C902' ' ' 'NRIC : ' F800-NUM-NRIC1        
      *        DISPLAY 'BE01W902-OUT-RTN-MSGNO'                                 
      *      END-IF                                                             
      *     END-IF.                                                             
                                                                                
       3050-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------*                                                 
       3075-EVALUATE-UFIN-TYPE.                                                 
      *-----------------------*                                                 
                                                                                
           EVALUATE WS-NUM-UFIN-TYPE                                            
            WHEN 'UIN'                                                          
      *        MOVE WS-NUM-UFIN-TYPE TO BE01W902-OUT-NUM-IDNTY-NO-TYPE          
               MOVE WS-NUM-UFIN-TYPE TO WS-NUM-NRIC-TYPE                        
            WHEN 'FIN'                                                          
      *        MOVE WS-NUM-UFIN-TYPE TO BE01W902-OUT-NUM-IDNTY-NO-TYPE          
               MOVE WS-NUM-UFIN-TYPE TO WS-NUM-NRIC-TYPE                        
            WHEN OTHER                                                          
      *        MOVE WS-NUM-UFIN-TYPE TO BE01W902-OUT-NUM-IDNTY-NO-TYPE          
               MOVE 'XIN'            TO WS-NUM-NRIC-TYPE                        
      *        MOVE '0127'           TO BE01W902-OUT-RTN-MSGNO                  
      *        MOVE 'AV02C013'       TO BE01W902-OUT-RTN-PGMID                  
           END-EVALUATE.                                                        
                                                                                
       3075-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------*                                                     
       4000-READ-MRG-TABLE.                                                     
      *-------------------*                                                     
                                                                                
            EXEC SQL                                                            
                 SELECT  NUM_CSTMR_HSBND ,                                      
                         NUM_CSTMR_WIFE ,                                       
                         DTE_MRG                                                
                 INTO   :DCLMRG.NUM-CSTMR-HSBND ,                               
                        :DCLMRG.NUM-CSTMR-WIFE  ,                               
                        :DCLMRG.DTE-MRG                                         
                                                                                
                 FROM    MRG                                                    
                 WHERE   (NUM_CSTMR_HSBND = :WS-NUM-CSTMR) OR                   
                         (NUM_CSTMR_WIFE  = :WS-NUM-CSTMR)                      
                                                                                
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
                WHEN 000                                                        
                     ADD     1                 TO   WS-NO-READ-MRG              
                     PERFORM  4250-DTE-MRG-DETAILS  THRU  4250-EXIT             
                WHEN 100                                                        
                     PERFORM 5100-WRITE-P13F800B    THRU  5100-EXIT             
                     ADD     1                 TO   WS-NO-NOTFND-MRG            
                     DISPLAY "REC. NOT FOUND IN MRG TABLE FOR : "               
                              F800-NUM-NRIC1                                    
                WHEN - 811                                                      
                     PERFORM  4100-RETRIEVE-DUPLICATE  THRU  4100-EXIT          
                WHEN OTHER                                                      
                     DISPLAY 'SQL ERROR IN MRG      TABLE       '               
                     DISPLAY 'F800-NUM-NRIC1   :  ' F800-NUM-NRIC1              
                     DISPLAY 'SQL ERROR CODE  :  '  SQLCODE                     
                     DISPLAY SPACES                                             
                                                                                
                     PERFORM  9000-SQL-ERROR  THRU  9000-EXIT                   
            END-EVALUATE.                                                       
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-----------------------*                                                 
       4100-RETRIEVE-DUPLICATE.                                                 
      *-----------------------*                                                 
                                                                                
            EXEC SQL OPEN SNMEMRG END-EXEC.                                     
                                                                                
            IF SQLCODE NOT = 000                                                
               DISPLAY 'SQL ERROR IN OPEN CURSOR MRG    TABLE       '           
               DISPLAY 'F800-NUM-NRIC1   :  ' F800-NUM-NRIC1                    
               DISPLAY 'SQL ERROR CODE  :  '  SQLCODE                           
               DISPLAY SPACES                                                   
                                                                                
               PERFORM  9000-SQL-ERROR  THRU  9000-EXIT                         
            END-IF.                                                             
                                                                                
            MOVE   ZERO                 TO WS-COUNT.                            
            PERFORM 4200-FETCH-SNMEMRG  THRU  4200-EXIT UNTIL                   
                    SQLCODE NOT = 000 OR WS-COUNT >= 1                          
                                                                                
            EVALUATE SQLCODE                                                    
                WHEN 000                                                        
                WHEN 100                                                        
                     CONTINUE                                                   
                WHEN OTHER                                                      
                     DISPLAY 'SQL ERROR IN FETCH CURSOR MRG    TABLE '          
                     DISPLAY 'F800-NUM-NRIC1   :  ' F800-NUM-NRIC1              
                     DISPLAY 'SQL ERROR CODE  :  '  SQLCODE                     
                     DISPLAY SPACES                                             
                     PERFORM 9000-SQL-ERROR  THRU 9000-EXIT                     
            END-EVALUATE.                                                       
                                                                                
            EXEC SQL CLOSE SNMEMRG END-EXEC.                                    
                                                                                
            IF SQLCODE NOT = 000                                                
               DISPLAY 'SQL ERROR IN CLOSE CURSOR MRG    TABLE '                
               DISPLAY 'F800-NUM-NRIC1   :  ' F800-NUM-NRIC1                    
               DISPLAY 'SQL ERROR CODE  :  '  SQLCODE                           
               DISPLAY SPACES                                                   
                                                                                
               PERFORM  9000-SQL-ERROR  THRU  9000-EXIT                         
            END-IF.                                                             
                                                                                
       4100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------*                                                      
       4200-FETCH-SNMEMRG.                                                      
      *------------------*                                                      
                                                                                
            MOVE   SPACES    TO   DCLMRG.                                       
                                                                                
            EXEC   SQL  FETCH  SNMEMRG                                          
                        INTO   :DCLMRG.NUM-CSTMR-HSBND ,                        
                               :DCLMRG.NUM-CSTMR-WIFE  ,                        
                               :DCLMRG.DTE-MRG                                  
            END-EXEC.                                                           
                                                                                
            IF  SQLCODE = 000                                                   
                PERFORM   4250-DTE-MRG-DETAILS THRU 4250-EXIT                   
                ADD   1   TO   WS-NO-READ-MRG  WS-COUNT                         
            END-IF.                                                             
                                                                                
       4200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *--------------------*                                                    
       4250-DTE-MRG-DETAILS.                                                    
      *--------------------*                                                    
                                                                                
            MOVE DTE-MRG OF DCLMRG(1:4)  TO WS-NUM-DTE-MRG   (1:4)              
            MOVE DTE-MRG OF DCLMRG(6:2)  TO WS-NUM-DTE-MRG   (5:2)              
            MOVE DTE-MRG OF DCLMRG(9:2)  TO WS-NUM-DTE-MRG   (7:2)              
                                                                                
            PERFORM 5000-WRITE-P13F800A   THRU 5000-EXIT.                       
                                                                                
       4250-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------*                                                     
       5000-WRITE-P13F800A.                                                     
      *-------------------*                                                     
                                                                                
            MOVE SPACES                 TO    WS-P13F800A-REC.                  
            INITIALIZE WS-P13F800A-REC.                                         
                                                                                
            MOVE BP13F800-MASTER(1:1992)  TO    WS-F800A-INFO1.                 
            MOVE WS-NUM-DTE-MRG           TO    WS-F800A-DTE-MRG.               
                                                                                
            WRITE WS-P13F800A-REC.                                              
                                                                                
            ADD   1                       TO    WS-NO-WRT-F800A.                
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------*                                                     
       5100-WRITE-P13F800B.                                                     
      *-------------------*                                                     
                                                                                
            MOVE SPACES                 TO    WS-P13F800B-REC.                  
            INITIALIZE WS-P13F800B-REC.                                         
                                                                                
            MOVE BP13F800-MASTER(1:1992)  TO    WS-F800B-INFO1.                 
            MOVE '00000000'               TO    WS-F800B-DTE-MRG.               
                                                                                
            WRITE WS-P13F800B-REC.                                              
            ADD   1                     TO    WS-NO-WRT-F800B.                  
                                                                                
       5100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *****************************************************************         
      *   ABORT PROCESSING DUE TO SQL ERRORS                          *         
      *****************************************************************         
      *--------------*                                                          
       9000-SQL-ERROR.                                                          
      *--------------*                                                          
                                                                                
           DISPLAY ' *****   BP13C010 ABEND   *****'.                           
                                                                                
           CALL 'DBATIAR'  USING SQLCA.                                         
           EXEC SQL   ROLLBACK   END-EXEC.                                      
           PERFORM SQL-INITIAL UNTIL SQL-INIT-DONE                              
           CALL 'DSNHLI' USING SQL-PLIST8.                                      
           EVALUATE SQLCODE                                                     
               WHEN 0                                                           
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ROLLBACK FAILED, SQLCODE IS : ' SQLCODE            
           END-EVALUATE.                                                        
                                                                                
           MOVE 12                        TO   RETURN-CODE.                     
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *------------------*                                                      
       9999-CLOSE-ROUTINE.                                                      
      *------------------*                                                      
                                                                                
           MOVE  WS-NO-READ-F800          TO  WS-ED-READ-F800.                  
           MOVE  WS-NO-READ-IDNTY         TO  WS-ED-READ-IDNTY.                 
           MOVE  WS-NO-NOTFND-IDNTY       TO  WS-ED-NOTFND-IDNTY.               
           MOVE  WS-NO-READ-MRG           TO  WS-ED-READ-MRG.                   
           MOVE  WS-NO-NOTFND-MRG         TO  WS-ED-NOTFND-MRG.                 
           MOVE  WS-NO-WRT-F800A          TO  WS-ED-WRT-F800A.                  
           MOVE  WS-NO-WRT-F800B          TO  WS-ED-WRT-F800B.                  
                                                                                
           DISPLAY  SPACES.                                                     
                                                                                
           DISPLAY  '**************************************************'        
           DISPLAY  'DATE : ' WS-DATE-RUN '       ' 'BP13C010'.                 
           DISPLAY  '**************************************************'        
           DISPLAY  SPACES.                                                     
                                                                                
           DISPLAY  ' 1. NO. OF REC. READ  FROM BP13F800            : '         
                     WS-ED-READ-F800                                            
           DISPLAY  ' 2. NO. OF REC. READ  FROM IDNTY   TBL (APPL)  : '         
                     WS-ED-READ-IDNTY                                           
           DISPLAY  ' 3. NO. OF REC. NOTFND IN  IDNTY   TBL (APPL)  : '         
                     WS-ED-NOTFND-IDNTY                                         
           DISPLAY  ' 4. NO. OF REC. READ  FROM MRG     TBL         : '         
                     WS-ED-READ-MRG                                             
           DISPLAY  ' 5. NO. OF NOTFND   IN     MRG     TBL         : '         
                     WS-ED-NOTFND-MRG                                           
           DISPLAY  ' 6. NO. OF REC. WRITTEN INTO P13F800A }        : '         
           DISPLAY  '    ( FOUND IN CDB MARRIAGE TABLE )   }        : '         
                     WS-ED-WRT-F800A                                            
           DISPLAY  ' 7. NO. OF REC. WRITTEN INTO P13F800B }        : '         
           DISPLAY  '    ( NOT FOUNF IN CDB MARRIAGE TABLE)}        : '         
                     WS-ED-WRT-F800B                                            
           DISPLAY  SPACES.                                                     
           DISPLAY  ' NOTE:   1  =  6  + 7'.                                    
                                                                                
           CLOSE BP13F800                                                       
                 P13F800A                                                       
                 P13F800B.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
            EXIT.                                                               
