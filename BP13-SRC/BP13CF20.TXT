      ******************************************************************        
       IDENTIFICATION DIVISION.                                                 
      ******************************************************************        
       PROGRAM-ID.    BP13CF20.                                                 
      *AUTHOR.        ZARITA DANE SARMIENTO.                                    
      *DATE-WRITTEN.  24/11/10.                                                 
                                                                                
      ******************************************************************        
      *  PROGRAM NAME : BP13CF20                                       *        
      *  OBJECTIVE    : OUTPUT CASES ON BP13F740                       *        
      *                                                                *        
      *  INPUT/OUTPUT FILES :                                          *        
      *  ====================                                          *        
      *  TYPE      FILE NAME                                           *        
      *  ------    ---------                                           *        
      *  INPUT     BP13F740                                            *        
      *  OUTPUT    BP13OUT1                                            *        
      *================================================================*        
      * MODIFICATION HISTORY:                                          *        
      * ---------------------                                          *        
      * CHG REQ. AUTHOR DATE      DESCRIPTIONS                         *        
      * ======== ====== =======   =====================================*        
      * BP134071 ZDD    20101124  NEW PROGRAM.                         *        
      * BP135100 SMR2   20131031  STATUS = 'K' HAS SAME TREATMENT AS 'U'        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
      ******************************************************************        
      *================================================================*        
       CONFIGURATION SECTION.                                                   
      *================================================================*        
       SOURCE-COMPUTER.  IBM-4341.                                              
       OBJECT-COMPUTER.  IBM-4341.                                              
                                                                                
      *================================================================*        
       INPUT-OUTPUT SECTION.                                                    
      *================================================================*        
      *----------------------------------------------------------------*        
       FILE-CONTROL.                                                            
      *----------------------------------------------------------------*        
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
                                                                                
           SELECT BP13OUT1 ASSIGN TO BP13OUT1.                                  
                                                                                
      ******************************************************************        
       DATA DIVISION.                                                           
      ******************************************************************        
      *================================================================*        
       FILE SECTION.                                                            
      *================================================================*        
                                                                                
       FD   BP13F740                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F740.                                                           
                                                                                
       FD   BP13OUT1                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 120 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   BP13OUT1-REC.                                                       
            05  OUT1-REGN           PIC X(08).                                  
            05  FILLER1             PIC X(01).                                  
            05  OUT1-RANDOM-NUM     PIC X(28).                                  
            05  FILLER2             PIC X(01).                                  
            05  OUT1-NRIC-HA1       PIC X(09).                                  
            05  FILLER3             PIC X(01).                                  
            05  OUT1-NRIC-HA2       PIC X(09).                                  
            05  FILLER4             PIC X(01).                                  
            05  OUT1-NT             PIC X(03).                                  
            05  FILLER5             PIC X(01).                                  
            05  OUT1-FT             PIC X(02).                                  
            05  FILLER6             PIC X(01).                                  
            05  OUT1-BALLOT-HH      PIC X(12).                                  
            05  FILLER7             PIC X(01).                                  
            05  OUT1-COHORT         PIC X(12).                                  
            05  FILLER8             PIC X(01).                                  
            05  OUT1-CATEGORY       PIC X(04).                                  
            05  FILLER9             PIC X(01).                                  
            05  OUT1-ALLOC-SCH      PIC X(08).                                  
            05  FILLER10            PIC X(01).                                  
            05  OUT1-ALLOC-SCH-REQ  PIC X(08).                                  
            05  FILLER              PIC X(07).                                  
                                                                                
                                                                                
      *================================================================*        
       WORKING-STORAGE SECTION.                                                 
      *================================================================*        
      *----------------------------------------------------------------*        
      *    FLAG VARIABLES                                              *        
      *----------------------------------------------------------------*        
       01 WS-FILE-VARIBLES.                                                     
          05 WS-F740-READ                PIC 9(07) VALUE ZEROES.                
          05 WS-EOF-F740                 PIC X(01) VALUE 'N'.                   
          05 WS-OUT1-WRT                 PIC 9(07) VALUE ZEROES.                
                                                                                
       01 WS-OTHER-VARIABLES.                                                   
          05 WS-COHORT                   PIC 9(05) VALUE ZEROES.                
          05 WS-COHORT-FMT               PIC ZZZ9.                              
          05 WS-COHORT-INP               PIC 9(02) VALUE ZEROES.                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *================================================================*        
       0000-MAIN-ROUTINE.                                                       
      *================================================================*        
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 3000-PROCESS-RECORDS     THRU 3000-EXIT                      
                   UNTIL WS-EOF-F740 = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       1000-OPEN-FILES.                                                         
      *================================================================*        
                                                                                
           OPEN INPUT  BP13F740                                                 
                OUTPUT BP13OUT1.                                                
                                                                                
           PERFORM 2000-READ-BP13F740       THRU 2000-EXIT.                     
                                                                                
           MOVE SPACES TO BP13OUT1-REC.                                         
           INITIALIZE     BP13OUT1-REC.                                         
                                                                                
           STRING 'REGN;RANDOM NUMBER;'                                         
             'HA1 NRIC;HA2 NRIC;NT ;FT;HOUSEHOLD;COHORT;'                       
             'CATEGORY;ALLOCATION SCHEME; '                                     
             'REQUESTED ALLOCATION SCHEME' DELIMITED BY SIZE INTO               
                   BP13OUT1-REC.                                                
           WRITE BP13OUT1-REC.                                                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       2000-READ-BP13F740.                                                      
      *================================================================*        
                                                                                
           READ BP13F740                                                        
             AT END                                                             
                MOVE 'Y' TO WS-EOF-F740                                         
                GO TO 2000-EXIT                                                 
           END-READ                                                             
                                                                                
           ADD 1 TO WS-F740-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       3000-PROCESS-RECORDS.                                                    
      *================================================================*        
                                                                                
           MOVE SPACES                           TO BP13OUT1-REC.               
           INITIALIZE                               BP13OUT1-REC.               
                                                                                
                                                                                
           PERFORM 4000-MOVE-DELIMITER         THRU 4000-EXIT.                  
           PERFORM 5000-MOVE-DETAILS           THRU 5000-EXIT.                  
                                                                                
           WRITE BP13OUT1-REC.                                                  
           ADD 1 TO WS-OUT1-WRT.                                                
                                                                                
           PERFORM 2000-READ-BP13F740          THRU 2000-EXIT.                  
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       4000-MOVE-DELIMITER.                                                     
      *================================================================*        
                                                                                
           MOVE     ';'            TO FILLER1   FILLER2                         
                                      FILLER3   FILLER4                         
                                      FILLER5   FILLER6                         
                                      FILLER7   FILLER8                         
                                      FILLER9   FILLER10.                       
                                                                                
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       5000-MOVE-DETAILS.                                                       
      *================================================================*        
                                                                                
           MOVE F740-NUM-REGN          TO OUT1-REGN.                            
           MOVE F740-NUM-RANDOM        TO OUT1-RANDOM-NUM.                      
           MOVE F740-NUM-NRIC1         TO OUT1-NRIC-HA1.                        
                                                                                
           IF F740-NUM-NRIC2(1:1) NOT = '#'                                     
             MOVE F740-NUM-NRIC2         TO OUT1-NRIC-HA2                       
           END-IF.                                                              
                                                                                
           MOVE F740-CDE-NT1           TO OUT1-NT.                              
           MOVE F740-CDE-FLAT-TYPE     TO OUT1-FT.                              
                                                                                
      ****CHECK FOR ALLOC-SCHEME*****                                           
           EVALUATE F740-CDE-ALLOC-SCH                                          
             WHEN 'TCP'                                                         
                MOVE F740-CDE-ALLOC-SCH     TO OUT1-ALLOC-SCH                   
             WHEN 'TPS'                                                         
                MOVE F740-CDE-ALLOC-SCH     TO OUT1-ALLOC-SCH                   
             WHEN 'PA '                                                         
                IF F740-CDE-RESIDENT-HA1 = '1' OR 'A' OR 'T'                    
                   MOVE 'MCPS'              TO OUT1-ALLOC-SCH                   
                ELSE                                                            
                   MOVE 'NON-MCPS'          TO OUT1-ALLOC-SCH                   
                END-IF                                                          
             WHEN OTHER                                                         
                IF F740-NUM-WARD NOT = SPACES                                   
                   MOVE 'GRO'               TO OUT1-ALLOC-SCH                   
                ELSE                                                            
                   MOVE F740-CDE-ALLOC-SCH  TO OUT1-ALLOC-SCH                   
                END-IF                                                          
           END-EVALUATE.                                                        
                                                                                
      ****CHECK FOR CHECK FOR ALLOC-SCHEME REQUESTED**                          
           IF F740-CDE-REQ-ALLOC-SCH NOT =  SPACES AND LOW-VALUES               
             EVALUATE F740-CDE-REQ-ALLOC-SCH                                    
               WHEN 'TCP'                                                       
                  MOVE F740-CDE-REQ-ALLOC-SCH TO OUT1-ALLOC-SCH-REQ             
               WHEN 'TPS'                                                       
                  MOVE F740-CDE-REQ-ALLOC-SCH TO OUT1-ALLOC-SCH-REQ             
               WHEN 'PA '                                                       
                  IF F740-CDE-RESIDENT-HA1 = '1' OR 'A' OR 'T'                  
                     MOVE 'MCPS'              TO OUT1-ALLOC-SCH-REQ             
                  ELSE                                                          
                     MOVE 'NON-MCPS'          TO OUT1-ALLOC-SCH-REQ             
                  END-IF                                                        
               WHEN OTHER                                                       
                  IF F740-NUM-WARD NOT = SPACES                                 
                    MOVE 'GRO'               TO OUT1-ALLOC-SCH-REQ              
                  ELSE                                                          
                   MOVE F740-CDE-ALLOC-SCH  TO OUT1-ALLOC-SCH-REQ               
                  END-IF                                                        
             END-EVALUATE                                                       
           ELSE                                                                 
              MOVE OUT1-ALLOC-SCH  TO OUT1-ALLOC-SCH-REQ                        
           END-IF.                                                              
                                                                                
      ****CHECK FOR BALLOT HOUSEHOLD****                                        
           IF F740-CDE-BALLOT-HOUSEHOLD = 'H' OR 'T'                            
              MOVE 'FIRST-TIMER'          TO OUT1-BALLOT-HH                     
           END-IF.                                                              
                                                                                
           IF F740-CDE-BALLOT-HOUSEHOLD = 'G'                                   
              MOVE 'SECOND-TIMER'         TO OUT1-BALLOT-HH                     
           END-IF.                                                              
                                                                                
      ****CHECK FOR COHORT****                                                  
           IF F740-CDE-REQUEST-STATUS = 'S'                                     
              MOVE '100% ' TO OUT1-COHORT                                       
           END-IF.                                                              
                                                                                
           IF F740-CDE-REQUEST-STATUS = 'R'                                     
              MOVE '200% ' TO OUT1-COHORT                                       
           END-IF.                                                              
                                                                                
           IF F740-CDE-REQUEST-STATUS = 'W'                                     
             IF F740-CDE-COHORT = SPACES OR LOW-VALUES                          
                MOVE SPACES TO OUT1-COHORT                                      
             ELSE                                                               
               MOVE F740-CDE-COHORT  TO WS-COHORT-INP                           
               COMPUTE WS-COHORT = WS-COHORT-INP   * 100 + 200                  
               IF WS-COHORT > 300                                               
                  MOVE 'SHORTFALL'  TO   OUT1-COHORT                            
               ELSE                                                             
                  MOVE WS-COHORT   TO WS-COHORT-FMT                             
                  IF WS-COHORT-FMT(1:1) = SPACE  OR LOW-VALUES                  
                     STRING  WS-COHORT-FMT(2:3)                                 
                          '%' DELIMITED BY SIZE INTO  OUT1-COHORT               
                  ELSE                                                          
                     STRING  WS-COHORT-FMT '%' DELIMITED BY SIZE INTO           
                          OUT1-COHORT                                           
                  END-IF                                                        
               END-IF                                                           
             END-IF                                                             
           END-IF.                                                              
                                                                                
           IF F740-CDE-REQUEST-STATUS = 'U' OR 'K'                              
              MOVE 'UNSUCCESSFULL' TO OUT1-COHORT                               
           END-IF.                                                              
                                                                                
      ****CHECK FOR CATEGORY****                                                
           IF F740-CDE-CATEGORY = '1'                                           
              MOVE 'CAT1'                TO OUT1-CATEGORY                       
           END-IF.                                                              
                                                                                
           IF F740-CDE-CATEGORY = '2'                                           
              MOVE 'CAT2'                TO OUT1-CATEGORY                       
           END-IF.                                                              
                                                                                
           IF F740-CDE-CATEGORY = '3' OR '4'                                    
              MOVE 'CAT3'                TO OUT1-CATEGORY                       
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-FILES.                                                        
      *================================================================*        
                                                                                
           CLOSE   BP13F740                                                     
                   BP13OUT1.                                                    
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '********* BP13CF20 CONTROL TOTALS *********'.               
           DISPLAY '*'.                                                         
           DISPLAY '* NO. OF F740 READ .............. : '                       
                    WS-F740-READ.                                               
           DISPLAY '* NO. OF OUT1 WRITTEN............ : '                       
                    WS-OUT1-WRT.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
