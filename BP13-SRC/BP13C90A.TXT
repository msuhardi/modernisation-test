      *************************                                                 
       IDENTIFICATION DIVISION.                                                 
      *************************                                                 
       PROGRAM-ID.    BP13C90A.                                                 
       AUTHOR.        J MOHAMED SHAMSUDEEN.                                     
      *DATE-WRITTEN.  12/07/99.                                                 
                                                                                
      *===============================================================*         
      *                                                               *         
      *   SYSTEM NAME :  SYSTEM OF COMMITMENT  (SOC)                  *         
      *                                                               *         
      *   SYSTEM ID   :  BP13                                         *         
      *                                                               *         
      *   OBJECTIVE   :  1.  A STANDARD ROUTINE CALLED BY BATCH       *         
      *                      PROGRAMS TO CALCULATE PREMIUM STAMP      *         
      *                      FEES, SURVEY STAMP FEE, MORTGAGE STAMP   *         
      *                      FEE, ETC...                              *         
      *                                                               *         
      *                  2.  THIS SUBROUTINE IS CALLED ONLY FOR CA = Y*         
      *                      CASES WHOSE LIP FROM BP13K640 IS LESSER  *         
      *                      THAN THE LOAN COMPUTED USING BP13C900 OR *         
      *                      BP13C90N.                                *         
      *                                                               *         
      *  CHGE-NO  BY    DATE      DESCRIPTION.                        *         
      *  -------  ---  ------     ----------------------------------  *         
      *  BP130690 JMS  12/07/1999 NEW PROGRAM                         *         
      *  BP132037 CKK  16/10/2001 TO REDUCE 30% IN STAMP FEE DUE TO   *         
      *                           2001 OFF-BUDGET MEASURE             *         
      *  BP132037 CKK  07/11/2001 PREMIUM STAMP FEE 1ST TIER 1260     *         
      *                                             2ND TIER 3780     *         
      *  BP132125 MCC  07/04/2002 ROUNDING OF ODD CENTS               *         
      *  BP132191 MCC  02/08/2002 CHGE REGN FEE FROM 30 TO 35.30      *         
      *  BP132237 MALC 28/10/2002 RESTORE ORIGINAL FEES AND TO SHOW   *         
      *                           30% OFF-BUDGET DISC IN BRACKET      *         
      *  BP132263 MALC 14/11/2002 TO CATER FOR CHANGE OF GST          *         
      *  BP132429 MALC 14/11/2003 TO HARDCODE 5% GST                  *         
      *  BP132666 JF4  17/02/2005 CHGE REGN-FEE FROM 35.30 TO 38.30   *         
      *  BP139485 KAC1 02/01/2024 TO CALL BP13C90D FOR 9 % GST RATE   *         
      *                           WEF 20240101                        *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
      *=======================*                                                 
       WORKING-STORAGE SECTION.                                                 
      *=======================*                                                 
                                                                                
       01  INT-LOAN                      PIC 9(7)       VALUE 0.                
       01  INT-CONVEY-FEE                PIC 9(3)       VALUE 0.                
       01  WS-TIMES                      PIC 9(5)V9(5)  VALUE 0.                
       01  INT-TIMES                     PIC 9(5)       VALUE 0.                
       01  TEMP-DEC-SPRICE               PIC 9(8)V99    VALUE 0.                
       01  TEMP-INT-SPRICE               PIC 9(8)       VALUE 0.                
       01  WS-DEC-SPRICE                 PIC 9(8)V99    VALUE 0.                
       01  WS-INT-SPRICE                 PIC 9(8)       VALUE 0.                
       01  PREMIUM-ST-FEE-NEW            PIC 9(5)V99    VALUE 0.                
       01  PREMIUM-ST-FEE-ORIG           PIC 9(5)V99    VALUE 0.                
       01  WS-CONVEY-FEE1                PIC 9(3)V99    VALUE 0.                
       01  WS-CONVEY-FEE2                PIC 9(3)V99    VALUE 0.                
       01  WS-SURVEY-FEE-GST             PIC 9(8)V99    VALUE 0.                
       01  WS-CONVEY-FEE-GST             PIC 9(8)V99    VALUE 0.                
       01  WS-FEE-GST                    PIC S9(8)V99   VALUE 0.                
                                                                                
      *-----------------------------------------------------------*             
      *    WS VARIABLES PASSED TO SUB-PROGRAM "AV02C021"          *             
      *-----------------------------------------------------------*             
       COPY P13GST01.                                                           
                                                                                
      *-----------------------------------------------------------*             
      *    WS VARIABLES PASSED TO SUB-PROGRAM "BP13C90B"          *             
      *-----------------------------------------------------------*             
       COPY P13GST02.                                                           
                                                                                
      *----------------------------------------------------------------*        
      *    WS VARIABLES RECEIVED FROM CALLING PROGRAM                  *        
      *----------------------------------------------------------------*        
       01  WS-COMM-AREA.                                                        
           05  SALE-PRICE                PIC 9(8)       VALUE ZEROES.           
           05  CDE-CLS-ACTUSE            PIC X(1)       VALUE SPACES.           
           05  CDE-TYP-ACTUSE            PIC X(2)       VALUE SPACES.           
           05  ROOM-TY                   PIC X(2)       VALUE SPACES.           
           05  CD-PREV-PAID              PIC 9(7)V99    VALUE ZEROES.           
           05  ICP-TOTAL-AMT             PIC 9(7)V99    VALUE ZEROES.           
           05  REPAY-PD                  PIC 99         VALUE ZEROES.           
           05  PREMIUM-ST-FEE            PIC 9(5)V99    VALUE ZEROES.           
           05  SURVEY-FEE                PIC 9(3)V99    VALUE ZEROES.           
           05  LEASE-ESCROW-FEE          PIC 9(3)V99    VALUE ZEROES.           
           05  LEASE-REGN-FEE            PIC 9(3)V99    VALUE ZEROES.           
           05  CONVEY-FEE                PIC 9(3)V99    VALUE ZEROES.           
           05  MORT-ESCROW-FEE           PIC 9(3)V99    VALUE ZEROES.           
           05  MORT-ST-FEE               PIC 9(5)V99    VALUE ZEROES.           
           05  MORT-REGN-FEE             PIC 9(3)V99    VALUE ZEROES.           
           05  LOAN-AMT                  PIC S9(7)V99   VALUE ZEROES.           
           05  ORIG-SELL                 PIC 9(8)       VALUE ZEROES.           
           05  SALE-TYPE                 PIC X(2)       VALUE SPACES.           
           05  PREMIUM-CODE              PIC X(2)       VALUE SPACES.           
           05  SURVEY-FEE-GST            PIC 9(3)V99    VALUE ZEROES.           
           05  CONVEY-FEE-GST            PIC 9(3)V99    VALUE ZEROES.           
           05  SALES-TYPE-SURVEY         PIC X(2)       VALUE SPACES.           
                                                                                
                                                                                
      *===============*                                                         
       LINKAGE SECTION.                                                         
      *===============*                                                         
                                                                                
       01  WS-LINK-AREA.                                                        
           05  LINK-SALE-PRICE           PIC 9(8).                              
           05  LINK-CDE-CLS-ACTUSE       PIC X(1).                              
           05  LINK-CDE-TYP-ACTUSE       PIC X(2).                              
           05  LINK-ROOM-TY              PIC X(2).                              
           05  LINK-CD-PREV-PAID         PIC 9(7)V99.                           
           05  LINK-ICP-TOTAL-AMT        PIC 9(7)V99.                           
           05  LINK-REPAY-PD             PIC 99.                                
           05  LINK-PREMIUM-ST-FEE       PIC 9(5)V99.                           
           05  LINK-SURVEY-FEE           PIC 9(3)V99.                           
           05  LINK-LEASE-ESCROW-FEE     PIC 9(3)V99.                           
           05  LINK-LEASE-REGN-FEE       PIC 9(3)V99.                           
           05  LINK-CONVEY-FEE           PIC 9(3)V99.                           
           05  LINK-MORT-ESCROW-FEE      PIC 9(3)V99.                           
           05  LINK-MORT-ST-FEE          PIC 9(5)V99.                           
           05  LINK-MORT-REGN-FEE        PIC 9(3)V99.                           
           05  LINK-LOAN-AMT             PIC S9(7)V99.                          
           05  LINK-ORIG-SELL            PIC 9(8).                              
           05  LINK-SALE-TYPE            PIC X(2).                              
           05  LINK-PREMIUM-CODE         PIC X(2).                              
           05  LINK-SURVEY-FEE-GST       PIC 9(3)V99.                           
           05  LINK-CONVEY-FEE-GST       PIC 9(3)V99.                           
           05  LINK-SALES-TYPE-SURVEY    PIC X(2).                              
                                                                                
                                                                                
      ***************************************                                   
       PROCEDURE DIVISION USING WS-LINK-AREA.                                   
      ***************************************                                   
      ******************************************************************        
       MAIN-CONTROL.                                                            
      ******************************************************************        
            MOVE WS-LINK-AREA  TO  WS-COMM-AREA.                                
                                                                                
      *------------------------------------------------------------*            
      *     COMPUTATION OF LEASE STAMP FEES BASED ON NEW RATE      *            
      *        FIRST    $150,000       3%                          *            
      *        FIRST    $ 90,000       1%                          *            
      *        NEXT     $ 60,000       2.0%                        *            
      *        SUBSEQUENT AMT          3.0%                        *            
      *                                                            *            
      *     EXTRA CHARGES (AD-VALOREM ON THE SC/CC) BASED ON RM-TY *            
      *        3R & 4R        ADD $22                              *            
      *        5R & E         ADD $30                              *            
      *        8R             ADD $30                              *            
      *     THE LEASE STAMP FEE(ONLY) SHOULD BE BASED ON THE       *            
      *     PREMIUM AMOUNT ROUNEDED TO NEAREST 100 DOLLARS.        *            
      *------------------------------------------------------------*            
      *     CHG ON 26/06/92 BY LJL                                 *            
      *     FOR SALE TYPE 11 (SA-TP)                               *            
      *     IF PREMIUM = 0, SUPPL STAMP FEE = 0                    *            
      *     IF PREMIUM = 5 OR 10,                                  *            
      *        SUPPL STAMP FEE = LSF (COMPUTE WITH PREMIUM)  -     *            
      *                         LSF ( COMPUTE WITHOUT PREMIUM).    *            
      *     NOTE NO 2 $ !!!                                        *            
      *     CHG ON 24/11/89 BY LSB                                 *            
      *     FOR SALE TYPE 10                                       *            
      *     IF PREMIUM = 0, SUPPL STAMP FEE = 0                    *            
      *     IF PREMIUM = 5 OR 10,                                  *            
      *        SUPPL STAMP FEE = LSF (COMPUTE WITH PREMIUM)     -  *            
      *                          LSF ( COMPUTE WITHOUT PREMIUM) +  *            
      *                          $2 DUP FEE                        *            
      *     FOR SALE TYPE 01                                       *            
      *         LSF BASE ON SELLING PRICE WITH OR WITHOUT PREMIUM  *            
      *                  PLUS FEE (DEPEND ON RM-TY)                *            
      *------------------------------------------------------------*            
                                                                                
      ******************************************************************        
       MOVE-GST-RATE.                                                           
      ******************************************************************        
      *-----------------------------------------------------------*             
      *    CALL BP13C90D TO GET GST-RATE                          *             
      *-----------------------------------------------------------*             
           CALL 'BP13C90D' USING WS-COMM-AREA-GST.                              
                                                                                
      ******************************************************************        
       PREMIUM-ST-FEE-ROUTINE.                                                  
      ******************************************************************        
            IF SALE-TYPE = '10'  OR '11'                                        
               PERFORM STAMP-FEE-FOR-TYPE10                                     
            ELSE                                                                
                PERFORM SELLING-PREMIUM-ST-FEE                                  
                ADD     PREMIUM-ST-FEE-NEW TO PREMIUM-ST-FEE.                   
                                                                                
                                                                                
      ******************************************************************        
       STAMP-FEE-FOR-TYPE10.                                                    
      ******************************************************************        
            IF PREMIUM-CODE =  ZEROES  OR SPACES                                
                MOVE ZEROS  TO PREMIUM-ST-FEE                                   
            ELSE                                                                
            IF SALE-TYPE = '11'                                                 
               PERFORM SELLING-PREMIUM-ST-FEE                                   
               PERFORM ORIG-SELL-ST-FEE                                         
               COMPUTE PREMIUM-ST-FEE =                                         
                      (PREMIUM-ST-FEE-NEW - PREMIUM-ST-FEE-ORIG)                
            ELSE                                                                
               PERFORM SELLING-PREMIUM-ST-FEE                                   
               PERFORM ORIG-SELL-ST-FEE                                         
               COMPUTE PREMIUM-ST-FEE =                                         
                    (PREMIUM-ST-FEE-NEW - PREMIUM-ST-FEE-ORIG) + 2.             
                                                                                
                                                                                
      ******************************************************************        
       SELLING-PREMIUM-ST-FEE.                                                  
      ******************************************************************        
            COMPUTE TEMP-DEC-SPRICE = SALE-PRICE / 100.                         
            MOVE TEMP-DEC-SPRICE  TO  TEMP-INT-SPRICE.                          
                                                                                
            IF  (TEMP-DEC-SPRICE  -   TEMP-INT-SPRICE)  >  0                    
                 ADD 1 TO TEMP-INT-SPRICE.                                      
            COMPUTE TEMP-INT-SPRICE = TEMP-INT-SPRICE  *  100.                  
                                                                                
            IF TEMP-INT-SPRICE > 360000 OR = 360000                             
               COMPUTE  PREMIUM-ST-FEE-NEW =                                    
                       (TEMP-INT-SPRICE - 360000)  *  0.03 +  5400              
            ELSE                                                                
            IF TEMP-INT-SPRICE > 180000                                         
               COMPUTE  PREMIUM-ST-FEE-NEW =                                    
                       (TEMP-INT-SPRICE - 180000)  *  0.02 +  1800              
           ELSE                                                                 
               COMPUTE  PREMIUM-ST-FEE-NEW  =  TEMP-INT-SPRICE * 0.01.          
                                                                                
                                                                                
      ******************************************************************        
       ORIG-SELL-ST-FEE.                                                        
      ******************************************************************        
           COMPUTE WS-DEC-SPRICE = ORIG-SELL  / 100.                            
           MOVE WS-DEC-SPRICE  TO  WS-INT-SPRICE.                               
                                                                                
           IF  (WS-DEC-SPRICE  -   WS-INT-SPRICE)  >  0                         
                ADD 1 TO WS-INT-SPRICE.                                         
           COMPUTE WS-INT-SPRICE = WS-INT-SPRICE  *  100.                       
                                                                                
           IF WS-INT-SPRICE > 360000 OR = 360000                                
              COMPUTE PREMIUM-ST-FEE-ORIG =                                     
                     (WS-INT-SPRICE - 360000)   *  0.03 +  5400                 
           ELSE                                                                 
           IF WS-INT-SPRICE > 180000                                            
              COMPUTE PREMIUM-ST-FEE-ORIG =                                     
                     (WS-INT-SPRICE - 180000)  *  0.02 +  1800                  
           ELSE                                                                 
              COMPUTE PREMIUM-ST-FEE-ORIG  =  WS-INT-SPRICE  *  0.01.           
                                                                                
                                                                                
      ******************************************************************        
       SURVEY-SC-ROUTINE.                                                       
      ******************************************************************        
      *------------------------------------------------------------*            
      *------------------------------------------------------------*            
      *    SURVEY FEE AND SERVICE CONSERVANCY CHARGES IS           *            
      *    DEPENDENT ON ROOM TYPE.                                 *            
      *------------------------------------------------------------*            
           IF ROOM-TY   =   '01'                                                
              MOVE 150  TO  SURVEY-FEE                                          
           ELSE                                                                 
           IF ROOM-TY   =   '02'                                                
              MOVE 150  TO  SURVEY-FEE                                          
           ELSE                                                                 
           IF ROOM-TY   =  '03'                                                 
              MOVE 212.50 TO  SURVEY-FEE                                        
           ELSE                                                                 
           IF ROOM-TY   =  '04'                                                 
              MOVE 275  TO  SURVEY-FEE                                          
           ELSE                                                                 
           IF ROOM-TY   =  '05'                                                 
              MOVE 325  TO  SURVEY-FEE                                          
           ELSE                                                                 
           IF ROOM-TY   =  '06'                                                 
              MOVE 375  TO  SURVEY-FEE.                                         
                                                                                
      * COMPUTE THE GST OF SURVEY FEE TO BE PAID.                               
      * IF SALES TYPE 3 OR 3A , SURVEY FEES = ZEROES *                          
                                                                                
           IF  SALES-TYPE-SURVEY  = '03' OR '3A'                                
               MOVE   ZEROES TO SURVEY-FEE.                                     
                                                                                
           MOVE   SURVEY-FEE TO WS-AMT-NETT.                                    
                                                                                
           CALL 'AV02C021' USING WS-COMM-AREA-GST.                              
                                                                                
           MOVE WS-AMT-GST  TO WS-SURVEY-FEE-GST SURVEY-FEE-GST.                
                                                                                
      * TO REMOVE ODD CENTS                                                     
           ADD WS-AMT-GST TO SURVEY-FEE GIVING WS-FEE-GST.                      
                                                                                
           MOVE SPACES             TO   WS-COMM-AREA-ODDCENTS.                  
           INITIALIZE                   WS-COMM-AREA-ODDCENTS.                  
                                                                                
           MOVE  WS-FEE-GST        TO   WS-COMM-ODD-TOTAL-FEE.                  
           MOVE  WS-NUM-GST-RATE   TO   WS-COMM-ODD-GST-RATE.                   
                                                                                
           CALL 'BP13C90B' USING WS-COMM-AREA-ODDCENTS.                         
                                                                                
           IF WS-COMM-ODD-CENTS-SW = 'Y'                                        
              MOVE WS-COMM-ODD-GST       TO WS-AMT-GST                          
                                            WS-SURVEY-FEE-GST                   
                                            SURVEY-FEE-GST                      
              MOVE WS-COMM-ODD-BASE-FEE  TO SURVEY-FEE                          
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
       LEASE-ESCROW-FEE-ROUTINE.                                                
      ******************************************************************        
      *------------------------------------------------------------*            
      *    STANDARD FEE OF $ 4/=                                   *            
      *------------------------------------------------------------*            
           MOVE 4 TO LEASE-ESCROW-FEE.                                          
                                                                                
                                                                                
      ******************************************************************        
       LEASE-REGN-FEE-ROUTINE.                                                  
      ******************************************************************        
      *------------------------------------------------------------*            
      *    STANDARD FEE OF $ 38.30                                 *            
      *------------------------------------------------------------*            
           MOVE 38.30 TO LEASE-REGN-FEE.                                        
                                                                                
                                                                                
      ******************************************************************        
       CONVEY-FEE-ROUTINE.                                                      
      ******************************************************************        
      *------------------------------------------------------------*            
      *    CHECK IF EITHER OLD OR NEW CONVEYANCING TYPE.           *            
      *    IF OLD                                                  *            
      *       CONVEYANCING TYPE THEN STANDARD RATE OF $ 20/=       *            
      *    ELSE                                                    *            
      *       CONVEYANCING FEE :                                   *            
      *           FIRST    $30,000 @ $0.09  PER $100 ($27/-)       *            
      *           NEXT     $30,000 @ $0.072 PER $100 ($21.60)      *            
      *          THERAFTER         @ $0.06  PER $100               *            
      *    MINIMUM CONVEYANCING FEE CHARGEABLE IS $20/-            *            
      *------------------------------------------------------------*            
                                                                                
      *------------------------------------------------------------*            
      *    CALCULATE CONVEY FEE FOR SALE PRICE + PREMIUM           *            
      *------------------------------------------------------------*            
           IF PREMIUM-ST-FEE = ZEROES                                           
              MOVE ZEROES  TO CONVEY-FEE                                        
              GO TO MORT-ESCROW-FEE-ROUTINE.                                    
                                                                                
           IF SALE-PRICE > 60000                                                
              COMPUTE WS-CONVEY-FEE1 = 48.6  +  (SALE-PRICE - 60000)  *         
                                       0.01  *  0.06                            
           ELSE                                                                 
           IF SALE-PRICE > 30000                                                
              COMPUTE WS-CONVEY-FEE1 = 27    +  (SALE-PRICE - 30000)   *        
                                       0.01  *   0.072                          
           ELSE                                                                 
              COMPUTE WS-CONVEY-FEE1 = SALE-PRICE  *  0.01  *  0.09.            
                                                                                
           IF WS-CONVEY-FEE1  <   20                                            
              MOVE   20 TO WS-CONVEY-FEE1.                                      
                                                                                
           MOVE WS-CONVEY-FEE1 TO INT-CONVEY-FEE.                               
           IF   WS-CONVEY-FEE1 -  INT-CONVEY-FEE > 0                            
                COMPUTE WS-CONVEY-FEE1 = INT-CONVEY-FEE  +  1.                  
                                                                                
      *------------------------------------------------------------*            
      *    CALCULATE CONVEY FEE FOR SALE PRICE W/O PREMIUM         *            
      *------------------------------------------------------------*            
           IF ORIG-SELL > 60000                                                 
              COMPUTE WS-CONVEY-FEE2 = 48.6  +  (ORIG-SELL - 60000)  *          
                                       0.01  *  0.06                            
           ELSE                                                                 
           IF ORIG-SELL > 30000                                                 
              COMPUTE WS-CONVEY-FEE2 = 27    +  (ORIG-SELL - 30000)  *          
                                       0.01  *  0.072                           
           ELSE                                                                 
              COMPUTE WS-CONVEY-FEE2 = ORIG-SELL  *  0.01  *  0.09.             
                                                                                
           IF WS-CONVEY-FEE2  <   20                                            
              MOVE   20 TO WS-CONVEY-FEE2.                                      
                                                                                
           MOVE WS-CONVEY-FEE2 TO INT-CONVEY-FEE.                               
           IF   WS-CONVEY-FEE2 -  INT-CONVEY-FEE > 0                            
                COMPUTE WS-CONVEY-FEE2 = INT-CONVEY-FEE  +  1.                  
                                                                                
      *------------------------------------------------------------*            
      *    COMPUTE THE DIFFERENCE IN CONVEY FEE TO BE PAID         *            
      *------------------------------------------------------------*            
           COMPUTE CONVEY-FEE = WS-CONVEY-FEE1 - WS-CONVEY-FEE2.                
                                                                                
      * COMPUTE THE GST OF CONVEY FEE TO BE PAID.                               
                                                                                
           MOVE    CONVEY-FEE TO WS-AMT-NETT.                                   
                                                                                
           CALL 'AV02C021' USING WS-COMM-AREA-GST.                              
                                                                                
           MOVE WS-AMT-GST    TO WS-CONVEY-FEE-GST CONVEY-FEE-GST.              
                                                                                
      * TO REMOVE ODD CENTS                                                     
           ADD WS-AMT-GST TO CONVEY-FEE GIVING WS-FEE-GST.                      
                                                                                
           MOVE SPACES             TO   WS-COMM-AREA-ODDCENTS.                  
           INITIALIZE                   WS-COMM-AREA-ODDCENTS.                  
                                                                                
           MOVE  WS-FEE-GST        TO   WS-COMM-ODD-TOTAL-FEE.                  
           MOVE  WS-NUM-GST-RATE   TO   WS-COMM-ODD-GST-RATE.                   
                                                                                
           CALL 'BP13C90B' USING WS-COMM-AREA-ODDCENTS.                         
                                                                                
           IF WS-COMM-ODD-CENTS-SW = 'Y'                                        
              MOVE WS-COMM-ODD-GST       TO WS-AMT-GST                          
                                            WS-CONVEY-FEE-GST                   
                                            CONVEY-FEE-GST                      
              MOVE WS-COMM-ODD-BASE-FEE  TO CONVEY-FEE                          
           END-IF.                                                              
                                                                                
      ******************************************************************        
       MORT-ESCROW-FEE-ROUTINE.                                                 
      ******************************************************************        
      *------------------------------------------------------------*            
      *    STANDARD FEE OF $ 10/=                                  *            
      *------------------------------------------------------------*            
           IF REPAY-PD =  ZEROES OR LOAN-AMT = ZEROES                           
              MOVE  0  TO MORT-ESCROW-FEE                                       
           ELSE                                                                 
              MOVE  10 TO MORT-ESCROW-FEE                                       
           END-IF.                                                              
                                                                                
                                                                                
      ******************************************************************        
       MORT-REGN-ROUTINE.                                                       
      ******************************************************************        
      *------------------------------------------------------------*            
      *    MORTGAGE REGISTRATION/MICROFILMING FEE AND              *            
      *    MORT STAMP FEE EQUALS ZERO WHEN THERE IS NO LOAN        *            
      *------------------------------------------------------------*            
           MOVE  0 TO MORT-ST-FEE, MORT-REGN-FEE.                               
                                                                                
           IF (LOAN-AMT < 0 OR = 0) OR REPAY-PD = 0                             
               NEXT SENTENCE                                                    
           ELSE                                                                 
      *------------------------------------------------------------*            
      *    MORTGAGE REGISTRATION/MICROFILMING FEE IS FIXED AT      *            
      *    $38.30 IF THERE IS A LOAN.                              *            
      *                                                            *            
      *    MAXIMUN LIMIT FOR MORTGAGE STAMP FEE IS $ 500           *            
      *    PLUS DUP $4 , IE TOTAL $502                             *            
      *    PLUS STAMP DUTY FIXED AT $10.- CAL AT BATCH PROGRAM     *            
      *    REMOVE  $ 2 DUE TO DISPENSATING OF DUPLICATES FOR       *            
      *    MORTGAGE (EFFECTIVE 1ST JULY 1996).                     *            
      *------------------------------------------------------------*            
               MOVE 38.30 TO MORT-REGN-FEE                                      
                                                                                
               IF  LOAN-AMT > 125000 OR = 125000                                
                   MOVE 502 TO MORT-ST-FEE                                      
               ELSE                                                             
               IF  LOAN-AMT NOT LESS THAN 1000                                  
      *------------------------------------------------------------*            
      *    FOR FIRST $1000 LOAN, MORTAGAGE STAMP FEE- $5           *            
      *        NEXT & SUBSEQUENT $1000 LOAN  $4/-                  *            
      *        PLUS STAMP DUTY FOR POA       $10/-                 *            
      *             - COMPUTE AT BATCH PROGRAM AT THE              *            
      *               TIME PRINTING STAMPIMG LIST                  *            
      *               CHG ON 23/11/89                              *            
      *    PLUS STAMP DUTY FOR DOA           $2/-                  *            
      *    REMOVE  $ 2 DUE TO DISPENSATING OF DUPLICATES FOR       *            
      *    MORTGAGE (EFFECTIVE 1ST JULY 1996).                     *            
      *------------------------------------------------------------*            
                   COMPUTE WS-TIMES = (LOAN-AMT) / 1000                         
                   PERFORM COMPUTE-MULT-FACTOR                                  
                   COMPUTE MORT-ST-FEE = INT-TIMES * 4 + 2                      
               ELSE                                                             
                   MOVE 16 TO MORT-ST-FEE.                                      
                                                                                
      *------------------------------------------------------------*            
      *    COMPUTE AMOUNT OF LOAN TAKING INTO CONSIDERATION THE    *            
      *    AMOUNT DUE TO THE ESTIMATED MORT REGN/MICROFILMING FEE  *            
      *    AND MORT STAMP FEE, SO AS TO RECALCULATE THE ACTUAL     *            
      *    MORTAGE STAMP FEE.                                      *            
      *    FOR OUTRIGHT PURCHASE CASE, REPAY-PD EQ 0 AND           *            
      *    MORT REGN FEE AND MORT STAMP FEE SHOULD EQ 0            *            
      *------------------------------------------------------------*            
                                                                                
           IF LOAN-AMT > 0 AND REPAY-PD NOT = 0                                 
              COMPUTE WS-TIMES = LOAN-AMT / 1000                                
              PERFORM COMPUTE-MULT-FACTOR                                       
      *------------------------------------------------------------*            
      *    FOR CASES WHERE FEE IS 502, LET IT REMAINS AS IT IS.    *            
      *    OTHERWISE,                                              *            
      *    RECALCULATE ACTUAL LOAN AMOUNT TAKING INTO CONSIDERA-   *            
      *    TION THE MORT REGN/MICROFILMING FEE AND MORT STAMP FEE. *            
      *------------------------------------------------------------*            
            IF MORT-ST-FEE NOT = 502                                            
                PERFORM FINAL-MORT-ST-FEE                                       
            END-IF.                                                             
                                                                                
                                                                                
      ******************************************************************        
       END-ROUTINE.                                                             
      ******************************************************************        
           MOVE SALE-PRICE         TO  LINK-SALE-PRICE.                         
           MOVE CDE-CLS-ACTUSE     TO  LINK-CDE-CLS-ACTUSE.                     
           MOVE CDE-TYP-ACTUSE     TO  LINK-CDE-TYP-ACTUSE.                     
           MOVE ROOM-TY            TO  LINK-ROOM-TY.                            
           MOVE CD-PREV-PAID       TO  LINK-CD-PREV-PAID.                       
           MOVE ICP-TOTAL-AMT      TO  LINK-ICP-TOTAL-AMT.                      
           MOVE REPAY-PD           TO  LINK-REPAY-PD.                           
           MOVE PREMIUM-ST-FEE     TO  LINK-PREMIUM-ST-FEE.                     
           MOVE SURVEY-FEE         TO  LINK-SURVEY-FEE.                         
           MOVE LEASE-ESCROW-FEE   TO  LINK-LEASE-ESCROW-FEE.                   
           MOVE LEASE-REGN-FEE     TO  LINK-LEASE-REGN-FEE.                     
           MOVE CONVEY-FEE         TO  LINK-CONVEY-FEE.                         
           MOVE MORT-ESCROW-FEE    TO  LINK-MORT-ESCROW-FEE.                    
           MOVE MORT-ST-FEE        TO  LINK-MORT-ST-FEE.                        
           MOVE MORT-REGN-FEE      TO  LINK-MORT-REGN-FEE.                      
           MOVE LOAN-AMT           TO  LINK-LOAN-AMT.                           
           MOVE ORIG-SELL          TO  LINK-ORIG-SELL.                          
           MOVE SALE-TYPE          TO  LINK-SALE-TYPE.                          
           MOVE PREMIUM-CODE       TO  LINK-PREMIUM-CODE.                       
           MOVE SURVEY-FEE-GST     TO  LINK-SURVEY-FEE-GST.                     
           MOVE CONVEY-FEE-GST     TO  LINK-CONVEY-FEE-GST.                     
                                                                                
           GO TO 999-EXIT-PROGRAM.                                              
                                                                                
                                                                                
      ******************************************************************        
       FINAL-MORT-ST-FEE.                                                       
      ******************************************************************        
      *------------------------------------------------------------*            
      *    FOR CASES WHERE MORTGAGE STAMP FEE IS MORE THAN $ 500   *            
      *    SET AT LIMIT OF $ 502                                   *            
      *    CHG ON 24/11 - (*5 + 11) CHG TO (*5+1)                  *            
      *    CHG ON 01/03/96 - (*5 + 1) CHG TO (*4 + 2)                           
      *------------------------------------------------------------*            
           COMPUTE MORT-ST-FEE  =  INT-TIMES  *  4 + 2.                         
                                                                                
           IF MORT-ST-FEE > 502                                                 
              MOVE 502 TO MORT-ST-FEE.                                          
                                                                                
                                                                                
      ******************************************************************        
       COMPUTE-MULT-FACTOR.                                                     
      ******************************************************************        
      *------------------------------------------------------------*            
      *    CALCULATE NUMBER OF THOUSANDS IN THE LOAN               *            
      *------------------------------------------------------------*            
           MOVE WS-TIMES TO INT-TIMES.                                          
                                                                                
           IF WS-TIMES - INT-TIMES > 0                                          
              ADD 1 TO   INT-TIMES.                                             
                                                                                
                                                                                
      ******************************************************************        
       999-EXIT-PROGRAM.                                                        
      ******************************************************************        
           GOBACK.                                                              
