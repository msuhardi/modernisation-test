       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CB18.                                                 
       AUTHOR.        EDEN ROSE DINGAL.                                         
       DATE-WRITTEN.  03/07/2007.                                               
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : THIS PGM REPLICATES BP13F730          *                
      *                  ACCORDINGLY                           *                
      *                 MCPS FIRST TIMER    - OUTPUT 4 REC     *                
      *                 MCPS UPGRADER       - OUTPUT 2 REC     *                
      *                 NON-MCPS FIRST TIMER - OUTPUT 2 REC    *                
      *                 NON-MCPS UPGRADER    - OUTPUT 1 REC    *                
      *                                                        *                
      *                 FOR FIRST TIMER : ADD ON OUTPUT :      *                
      *                 4 UNSUCC ATTEMPT    - OUTPUT 1 REC     *                
      *                 5 UNSUCC ATTEMPT    - OUTPUT 2 REC     *                
      *                 AND ADD ON..                           *                
      *                                                        *                
      *    INPUT  FILE  : BP13F730  - CASES PER NRIC           *                
      *                   BP13K593                             *                
      *                   BP13K816                             *                
      *    OUTPUT FILE  : BP13F740  - F730 REPLICATED          *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE     DETAILS                         *                
      * BP133142 ED1 130707    NEW PROGRAM                     *                
      * BP133256 ED1 050208    ADD NRIC1 IN REPORT             *                
      * BP133292 ED1 250208    MCPS FLAT-TYP '1A' OUTPUT 2 RECS*                
      *                    NON-MCPS FLAT-TYP '1A' OUTPUT 1 REC *                
      * BP133316 IL5 150409    RENAME REPORT HEADER FROM       *                
      *                        UNSUCC ATTEMPT TO BALLOT CHANCES*                
      * BP133298 ED1 150409    ADD OUTFILE P13LB18B FOR >= 3   *                
      *                        BALLOT ATTEMPTS                 *                
      * BP133248 ED1 260508    CHECK IF FT WS-MAX-UNSUCC > 1   *                
      *                        THEN ADD (WS-MAX-UNSUCC-1) RECS *                
      * BP133380 IL5 110708    CHG OUTFILE P13LB18B FOR >= 2   *                
      *                        BALLOT CHANCES                  *                
      * BP133432 ED1 160908    GET MAX-UNSUCC FROM BP13K593    *                
      *                        IF ALLO-CAT NOT 'BE'            *                
      *                        CHG OUTFILE P13LB18B FOR >= 4   *                
      *                        CHANCES                         *                
      * BP133511 IL5 200209    DO NOT COUNT UNSUCC ATTEMPTS    *                
      *                        FOR FLAT TYPE 1A                *                
      * BP133610 ESA1 240409   TO REPLACE BP13K767 W/ BP13K816 *                
      * BP133838 ZDD1 020310   TO CHECK NT/FT BEFORE WRITING   *                
      *                        ON LOG FILE                     *                
      * BP134024 ZDD1 220910   INCLUDE HA2-HA4 ON BP13LB18     *                
      * BP134536 ZDD1 070612   INCLUDE APPS TAG                *                
      * BP134857 SMR2 250313   REMOVE EXTRA CHANCES ASSIGNED TO*                
      *                        STUDIO APTS FOR APPS AND MCPS   *                
      * BP134903 SMR2 190413   REDUCE RECORDS PRINTED PER PAGE *                
      * BP134996 SMR2 220713   BAL-HH 'F' TO BE ACCORDED THE   *                
      *                        SAME TREATMENT AS 'H' AND 'T'   *                
      * BP134996 SMR2 060813   ONLY 1 CHANCE FOR SINGLES SCHEME*                
      * BP135254 SMR2 210414   TO REPLACE BP13K816 W/ BP13K813 *                
      * BP135392 IL5  040714   CATER FOR FT 3P                 *                
      * BP135492 SMR2 220814   MODIFY CHANCES FOR MCPS CASES,  *                
      *                        SAME TREATMENT AS PA CASES      *                
      * BP135998 SMR2 061115   CATER FOR ELDERLY               *                
      * BP136106 SMR2 250116   CATER FOR ELDERLY SINGLES       *                
      * BP136360 SMR2 230816   CHG OUTFILE P13LB18B FOR >= 3   *                
      *                        BALLOT CHANCES / INCLUDE NRIC2  *                
      * BP136852 SMR2 270617   1 CHANCE FOR APPLN RECEIVED     *                
      *                        UNDER 'ROF'                     *                
      * BP137786  KV6 180419   TO CATER FOR 'OBF'              *                
      * BP138038 SMP3 231019   CATER FOR APPLN WITH UNSUCC     *                
      *                        ATTEMPTS TO HAVE SAME VALUES    *                
      *                        PER INDIVIDUAL RECORDS          *                
      * BP138591 AM25 200121   CATER FOR FT 1F                 *                
      * BP139670 AM25 250823   CATER FOR FT(PMC)               *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN TO BP13F740.                                  
           SELECT BP13F730 ASSIGN TO BP13F730.                                  
           SELECT BP13LB18 ASSIGN TO BP13LB18.                                  
           SELECT P13LB18B ASSIGN TO P13LB18B.                                  
                                                                                
           SELECT BP13K593 ASSIGN TO BP13K593                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K593-KEY-FLD                               
                  FILE STATUS     IS WS-K593-STATUS.                            
                                                                                
           SELECT BP13K813 ASSIGN TO BP13K813                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K813-KEY-FLD                               
                  FILE STATUS     IS WS-K813-STATUS.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F740                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F740.                                                           
                                                                                
       FD   BP13F730                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORD IS STANDARD.                                           
       COPY BP13F730.                                                           
                                                                                
       FD   BP13K593                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K593.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13LB18                                                            
            RECORD CONTAINS   132 CHARACTERS                                    
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01   LB18-PRTREC           PIC X(132).                                   
                                                                                
       FD   P13LB18B                                                            
            RECORD CONTAINS   100 CHARACTERS                                    
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01   LB18B-PRTREC.                                                       
           05  LB18B-REG-NO        PIC X(08).                                   
           05  LB18B-NRIC          PIC X(09).                                   
           05  LB18B-MCPS-TAG      PIC X(01).                                   
           05  LB18B-HH-TYPE       PIC X(01).                                   
           05  LB18B-ETHNIC        PIC X(01).                                   
           05  LB18B-UNS-CNT       PIC X(02).                                   
           05  LB18B-TOTAL         PIC X(02).                                   
           05  LB18B-MODE          PIC X(03).                                   
           05  LB18B-DTEBAL        PIC X(07).                                   
           05  LB18B-CDE-FT        PIC X(07).                                   
           05  LB18B-CDE-NT        PIC X(03).                                   
           05  LB18B-APPS-TAG      PIC X(01).                                   
           05  LB18B-NRIC2         PIC X(09).                                   
           05  LB18B-FTPMC         PIC X(01).                                   
           05  FILLER              PIC X(44).                                   
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F730-READ               PIC 9(5)  VALUE ZEROS.                 
           05 WS-F740-WRITE              PIC 9(5)  VALUE ZEROS.                 
           05 WS-LB18B-WRITE             PIC 9(5)  VALUE ZEROS.                 
           05 WS-SNO                     PIC 9(5)  VALUE 0.                     
           05 WS-PAGE                    PIC 9(4)  VALUE 0.                     
           05 WS-LINE-CTR                PIC 9(3)  VALUE 0.                     
           05 WS-COUNT                   PIC 99    VALUE 0.                     
           05 WS-TOTAL                   PIC 99    VALUE 0.                     
           05 WS-GRAND-TOTAL             PIC 9(5)  VALUE 0.                     
                                                                                
       01  WS-NUM-PRIO                   PIC 99    VALUE 0.                     
       01  WS-NUM-UNSUC                  PIC 99    VALUE 0.                     
      *01  WS-MCPS-TAG                   PIC X     VALUE SPACE.                 
       01  WS-MAX-LINE                   PIC 9(2)  VALUE 55.                    
       01  WS-K593-STATUS                PIC 9(2)  VALUE 0.                     
       01  WS-K813-STATUS                PIC 9(2)  VALUE 0.                     
       01  WS-CNT                        PIC 9(2)  VALUE 0.                     
       01  WS-MAX-UNSUCC                 PIC 9(2)  VALUE 0.                     
       01  WS-NRIC-UNSUC                 PIC X(9)  VALUE SPACES.                
       01  WS-MAX-CTR                    PIC 9(2)  VALUE 0.                     
       01  WS-EDITED-DATE                PIC X(10) VALUE SPACES.                
       01  WS-SYSTEM-DATE-TIME.                                                 
           05  WS-SYS-DATE               PIC X(08) VALUE SPACES.                
           05  WS-SYS-TIME               PIC X(08) VALUE SPACES.                
           05  FILLER                    PIC X(05) VALUE SPACES.                
                                                                                
       01  WS-PREV-KEY.                                                         
           05  WS-PREV-NT                PIC X(03) VALUE SPACES.                
           05  WS-PREV-FT                PIC X(02) VALUE SPACES.                
                                                                                
       01  WS-PREV-DATA.                                                        
           05  WS-PREV-MODE              PIC X(03) VALUE SPACES.                
           05  WS-PREV-DTEBAL            PIC X(06) VALUE SPACES.                
                                                                                
       01  WS-NEW-KEY.                                                          
           05  WS-NEW-NT                 PIC X(03) VALUE SPACES.                
           05  WS-NEW-FT                 PIC X(02) VALUE SPACES.                
                                                                                
       01  WS-EOF-F730                   PIC 9       VALUE ZERO.                
           88 EOF-F730                   VALUE 1.                               
                                                                                
       01  WS-BLANK-LINE  PIC X(132)             VALUE SPACES.                  
       01  WS-DASH-LINE   PIC X(132)             VALUE ALL '-'.                 
                                                                                
       01  LB18-PR-HEAD-01.                                                     
           05  FILLER             PIC X(08) VALUE 'BP13LB18'.                   
           05  FILLER             PIC X(05) VALUE SPACES.                       
           05  FILLER             PIC X(08) VALUE 'HDBCAT 3'.                   
           05  FILLER             PIC X(24) VALUE SPACES.                       
           05  FILLER             PIC X(39) VALUE                               
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER             PIC X(17) VALUE SPACES.                       
           05  FILLER             PIC X(06) VALUE 'DATE: '.                     
           05  LB18-DATE          PIC X(10).                                    
           05  FILLER             PIC X(03) VALUE SPACES.                       
           05  FILLER             PIC X(06) VALUE 'PAGE :'.                     
           05  LB18-PAGE          PIC ZZZ9.                                     
                                                                                
       01  LB18-PR-HEAD-02.                                                     
           05  FILLER             PIC X(45) VALUE SPACES.                       
           05  FILLER             PIC X(40) VALUE                               
               'REPORT DETAILS ON RANDOM NUMBER ISSUED'.                        
                                                                                
       01  LB18-PR-HEAD-3A.                                                     
           05  FILLER      PIC X(02) VALUE SPACES.                              
           05  FILLER      PIC X(17) VALUE 'MODE OF SALE   : '.                 
           05  LB18-MODE   PIC X(3).                                            
                                                                                
       01  LB18-PR-HEAD-3B.                                                     
           05  FILLER      PIC X(02) VALUE SPACES.                              
           05  FILLER      PIC X(17) VALUE 'BALLOT QUARTER : '.                 
           05  LB18-DTEBAL PIC X(7).                                            
                                                                                
       01  LB18-PR-HEAD-3C.                                                     
           05  FILLER      PIC X(02) VALUE SPACES.                              
           05  FILLER      PIC X(17) VALUE 'FLAT-TYPE      : '.                 
           05  LB18-CDE-FT PIC X(7).                                            
                                                                                
       01  LB18-PR-HEAD-3D.                                                     
           05  FILLER      PIC X(02) VALUE SPACES.                              
           05  FILLER      PIC X(17) VALUE 'NEW TOWN       : '.                 
           05  LB18-CDE-NT PIC X(30).                                           
                                                                                
       01  LB18-PR-HEAD-04.                                                     
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  FILLER             PIC X(49) VALUE                               
               'S/NO  REGN NO.  NRIC       NRIC       NRIC       '.             
           05  FILLER             PIC X(50) VALUE                               
               'NRIC       MCPS  BALLOT   ETHNIC  UNSUCC     FT   '.            
           05  FILLER             PIC X(29) VALUE                               
               ' NO. OF   APPS  FTS  ELD  PPO'.                                 
                                                                                
       01  LB18-PR-HEAD-05.                                                     
           05  FILLER             PIC X(18) VALUE SPACES.                       
           05  FILLER             PIC X(50) VALUE                               
               'HA1        HA2        HA3        HA4              '.            
           05  FILLER             PIC X(46) VALUE                               
               'HH TYPE  GROUP   ATTEMPTS  (PMC)  CHANCES  TAG'.                
                                                                                
       01  LB18-PR-HEAD-06.                                                     
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  FILLER             PIC X(42) VALUE                               
               'TOTAL NUMBER OF RANDOM NUMBERS ASSIGNED : '.                    
           05  LB18-GRAND-TOTAL   PIC ZZZZ9.                                    
                                                                                
       01  LB18-PR-DETAILS.                                                     
           05  FILLER             PIC X(01) VALUE SPACES.                       
           05  LB18-SNO           PIC ZZZZ9.                                    
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LB18-REG-NO        PIC X(08).                                    
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LB18-NRIC1         PIC X(09).                                    
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LB18-NRIC2         PIC X(09).                                    
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LB18-NRIC3         PIC X(09).                                    
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LB18-NRIC4         PIC X(09).                                    
           05  FILLER             PIC X(03) VALUE SPACES.                       
           05  LB18-MCPS-TAG      PIC X(01).                                    
           05  FILLER             PIC X(07) VALUE SPACES.                       
           05  LB18-HH-TYPE       PIC X(01).                                    
           05  FILLER             PIC X(07) VALUE SPACES.                       
           05  LB18-ETHNIC        PIC X(01).                                    
           05  FILLER             PIC X(08) VALUE SPACES.                       
           05  LB18-UNS-CNT       PIC  Z9.                                      
           05  LB18-UNS-CNT-R REDEFINES LB18-UNS-CNT.                           
               10  LB18-UNS-CNT-X PIC XX.                                       
           05  FILLER             PIC X(07) VALUE SPACES.                       
           05  LB18-FTPMC         PIC X(01).                                    
           05  FILLER             PIC X(06) VALUE SPACES.                       
           05  LB18-TOTAL         PIC  Z9.                                      
           05  LB18-TOTAL-R REDEFINES LB18-TOTAL.                               
               10  LB18-TOTAL-X   PIC XX.                                       
           05  FILLER             PIC X(06) VALUE SPACES.                       
           05  LB18-APPS-TAG      PIC X(01).                                    
           05  FILLER             PIC X(05) VALUE SPACES.                       
           05  LB18-FTS-TAG       PIC X(01).                                    
           05  FILLER             PIC X(04) VALUE SPACES.                       
           05  LB18-ELDERLY       PIC X(01).                                    
           05  FILLER             PIC X(04) VALUE SPACES.                       
           05  LB18-PPO           PIC X(01).                                    
                                                                                
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************                                                        
       000-MAIN-ROUTINE.                                                        
      ******************                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F730      THRU 200-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL EOF-F730.                                                    
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ********************                                                      
       100-INITIALIZATION.                                                      
      ********************                                                      
           OPEN INPUT  BP13F730                                                 
                       BP13K593                                                 
                       BP13K813                                                 
                OUTPUT BP13F740                                                 
                       BP13LB18                                                 
                       P13LB18B.                                                
                                                                                
           IF WS-K593-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K593 FILE ERROR ' WS-K593-STATUS             
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K813 FILE ERROR ' WS-K813-STATUS             
           END-IF.                                                              
                                                                                
           MOVE ZEROS                 TO WS-COUNTERS.                           
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE-TIME.                   
                                                                                
           STRING WS-SYS-DATE(7:2) '/'                                          
                  WS-SYS-DATE(5:2) '/'                                          
                  WS-SYS-DATE(1:4)                                              
           DELIMITED BY SIZE INTO WS-EDITED-DATE.                               
                                                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *****************                                                         
       200-READ-F730.                                                           
      *****************                                                         
           READ BP13F730 AT END                                                 
                MOVE 1               TO WS-EOF-F730                             
                GO TO 200-EXIT.                                                 
                                                                                
           MOVE  F730-CDE-NT1       TO WS-NEW-NT.                               
           MOVE  F730-CDE-FLAT-TYPE TO WS-NEW-FT.                               
                                                                                
           ADD  1                    TO WS-F730-READ.                           
                                                                                
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *****************                                                         
       300-PROCESS-RTN.                                                         
      *****************                                                         
           MOVE  0      TO WS-COUNT.                                            
           MOVE  0      TO WS-MAX-CTR.                                          
           MOVE  0      TO WS-MAX-UNSUCC.                                       
           MOVE  0      TO WS-TOTAL.                                            
           MOVE  0      TO WS-NUM-PRIO.                                         
           MOVE  0      TO WS-NUM-UNSUC.                                        
      *    MOVE  SPACE  TO WS-MCPS-TAG.                                         
           MOVE  SPACES TO WS-NRIC-UNSUC.                                       
                                                                                
           IF WS-PREV-KEY = SPACES                                              
              MOVE  F730-CDE-NT1       TO WS-PREV-NT                            
              MOVE  F730-CDE-FLAT-TYPE TO WS-PREV-FT                            
           END-IF.                                                              
                                                                                
      *    IF (F730-NUM-MCPS NOT = SPACE AND LOW-VALUE) AND                     
      *       (F730-CDE-RESIDENT-HA1 NOT = 'H' AND                              
      *        F730-CDE-RESIDENT-HA1 NOT = 'P' AND                              
      *        F730-CDE-RESIDENT-HA1 NOT = 'M' )                                
      *        MOVE 'Y' TO WS-MCPS-TAG                                          
      *        PERFORM 400-PROCESS-MCPS    THRU 400-EXIT                        
      *    ELSE                                                                 
      *        MOVE 'N' TO WS-MCPS-TAG                                          
      *    END-IF.                                                              
                                                                                
           IF F730-NUM-ALLO-CAT = 'BTO'                                         
              PERFORM 7200-READ-BP13K813 THRU 7200-EXIT                         
              IF F730-CDE-BALLOT-HOUSEHOLD = 'H' OR 'T' OR 'F'                  
                 IF (F730-CDE-FLAT-TYPE NOT = '1A' AND '2A' AND 'SP')           
                     AND (F730-CDE-ALLOC-SCH NOT = 'FTS')                       
                    IF K813-NUM-MATURE-EST-TAG NOT = 'Y'                        
                       PERFORM 7050-GET-MAX-UNSUC THRU 7050-EXIT                
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F730-CDE-BALLOT-HOUSEHOLD = 'H' OR 'T' OR 'F'                     
              IF (F730-CDE-FLAT-TYPE NOT = '1A' AND '2A' AND 'SP'               
                                       AND '1F') AND                            
                 (F730-CDE-ALLOC-SCH NOT = 'FTS')                               
                 PERFORM 7000-PROCESS-FT    THRU 7000-EXIT                      
              END-IF                                                            
           END-IF                                                               
                                                                                
           IF F730-NUM-ALLO-CAT = 'ROF' OR 'OBF'                                
              PERFORM 310-WRITE-BP13F740  THRU 310-EXIT                         
              MOVE 1 TO WS-COUNT                                                
           ELSE                                                                 
              PERFORM 500-PROCESS-NONMCPS THRU 500-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-PREV-KEY NOT = WS-NEW-KEY                                      
              PERFORM 320-PROCESS-DIFFREC  THRU 320-EXIT                        
           END-IF.                                                              
                                                                                
           PERFORM 600-LOG-REPORT  THRU 600-EXIT.                               
                                                                                
           IF WS-TOTAL >= 3                                                     
              PERFORM 640-WRITE-P13LB18B THRU 640-EXIT                          
           END-IF.                                                              
                                                                                
           PERFORM 200-READ-F730   THRU 200-EXIT.                               
                                                                                
           IF  EOF-F730                                                         
              PERFORM 320-PROCESS-DIFFREC  THRU 320-EXIT                        
           END-IF.                                                              
                                                                                
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ********************                                                      
       310-WRITE-BP13F740.                                                      
      ********************                                                      
           ADD  1               TO WS-NUM-PRIO.                                 
           MOVE BP13F730-REC    TO BP13F740-REC.                                
           MOVE WS-NUM-PRIO     TO F740-NUM-RANDOM-PRIORITY.                    
           MOVE WS-NUM-UNSUC    TO F740-NUM-WAIT-TIME.                          
                                                                                
           WRITE                   BP13F740-REC.                                
           ADD  1               TO WS-F740-WRITE.                               
                                                                                
                                                                                
       310-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *********************                                                     
       320-PROCESS-DIFFREC.                                                     
      *********************                                                     
           MOVE  ZEROS               TO WS-SNO.                                 
           MOVE  F730-CDE-NT1        TO WS-PREV-NT.                             
           MOVE  F730-CDE-FLAT-TYPE  TO WS-PREV-FT.                             
                                                                                
           PERFORM 620-LOG-TOTAL   THRU 620-EXIT.                               
                                                                                
           MOVE  0                   TO WS-GRAND-TOTAL.                         
                                                                                
           IF NOT EOF-F730                                                      
              PERFORM 630-PRINT-CURR-HDR THRU 630-EXIT                          
           END-IF.                                                              
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************                                                        
      *400-PROCESS-MCPS.                                                        
      ******************                                                        
      *    IF (F730-CDE-FLAT-TYPE = '1A' OR '2A' OR 'SP') OR                    
      *       (F730-CDE-ALLOC-SCH = 'FTS')                                      
      *       PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                          
      *       MOVE 1 TO WS-COUNT                                                
      *    ELSE                                                                 
      *                                                                         
      *       IF F730-CDE-BALLOT-HOUSEHOLD = 'H' OR 'T' OR 'F'                  
      *    FIRST TIMERS                                                         
      * -----------------                                                       
      *         IF F730-NUM-MCPS = 'M'                                          
      *           PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                      
      *           PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                      
      *           PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                      
      *           PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                      
      *           PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                      
      *           PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                      
      *           MOVE 6 TO WS-COUNT                                            
      *         ELSE                                                            
      *           PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                      
      *           PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                      
      *           PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                      
      *           PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                      
      *           MOVE 4 TO WS-COUNT                                            
      *         END-IF                                                          
      *                                                                         
      *       ELSE                                                              
      *      UPGRADERS                                                          
      * -----------------                                                       
      *         IF F730-NUM-MCPS = 'M'                                          
      *           PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                      
      *           PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                      
      *           PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                      
      *           MOVE 3 TO WS-COUNT                                            
      *         ELSE                                                            
      *           PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                      
      *           PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                      
      *           MOVE 2 TO WS-COUNT                                            
      *         END-IF                                                          
      *       END-IF                                                            
      *                                                                         
      *    END-IF.                                                              
      *                                                                         
      *                                                                         
      *400-EXIT.                                                                
      *    EXIT.                                                                
                                                                                
                                                                                
      *********************                                                     
       500-PROCESS-NONMCPS.                                                     
      *********************                                                     
           IF F730-NUM-ELDERLY = 'F'                                            
              IF F730-CDE-BALLOT-HOUSEHOLD = 'H' OR 'T' OR 'F'                  
                 IF F730-NUM-PPO = SPACES OR 'E' OR 'N'                         
                    PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                    
                    PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                    
                    MOVE 2 TO WS-COUNT                                          
                 ELSE                                                           
                    PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                    
                    MOVE 1 TO WS-COUNT                                          
                 END-IF                                                         
              ELSE                                                              
                 PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                       
                 MOVE 1 TO WS-COUNT                                             
              END-IF                                                            
           ELSE                                                                 
              IF F730-NUM-ELDERLY = 'S'                                         
                 PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                       
                 MOVE 1 TO WS-COUNT                                             
              ELSE                                                              
                 IF (F730-CDE-FLAT-TYPE = '1A' OR '2A' OR 'SP') OR              
                    (F730-CDE-ALLOC-SCH = 'FTS')                                
                    PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                    
                    MOVE 1 TO WS-COUNT                                          
                 ELSE                                                           
                    IF F730-CDE-BALLOT-HOUSEHOLD = 'H' OR 'T' OR 'F'            
      *    FIRST TIMERS                                                         
      * -----------------                                                       
                       IF F730-NUM-FTPMC = 'M' OR 'C' OR 'O' OR 'A'             
                          PERFORM 310-WRITE-BP13F740 THRU 310-EXIT              
                          PERFORM 310-WRITE-BP13F740 THRU 310-EXIT              
                          PERFORM 310-WRITE-BP13F740 THRU 310-EXIT              
                          MOVE 3 TO WS-COUNT                                    
                       ELSE                                                     
                          PERFORM 310-WRITE-BP13F740 THRU 310-EXIT              
                          PERFORM 310-WRITE-BP13F740 THRU 310-EXIT              
                          MOVE 2 TO WS-COUNT                                    
                       END-IF                                                   
                    ELSE                                                        
      *      UPGRADERS                                                          
      * -----------------                                                       
                       PERFORM 310-WRITE-BP13F740 THRU 310-EXIT                 
                       MOVE 1 TO WS-COUNT                                       
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ****************                                                          
       600-LOG-REPORT.                                                          
      ****************                                                          
           MOVE SPACES  TO LB18-PR-DETAILS.                                     
           INITIALIZE      LB18-PR-DETAILS.                                     
                                                                                
           COMPUTE WS-TOTAL       = WS-COUNT       + WS-MAX-CTR.                
           COMPUTE WS-GRAND-TOTAL = WS-GRAND-TOTAL + WS-TOTAL.                  
                                                                                
           MOVE F730-NUM-REGN               TO LB18-REG-NO.                     
           MOVE F730-NUM-NRIC1              TO LB18-NRIC1.                      
                                                                                
           IF F730-NUM-NRIC2(1:1) NOT = '#'                                     
             MOVE F730-NUM-NRIC2            TO LB18-NRIC2                       
           ELSE                                                                 
             MOVE SPACES                    TO LB18-NRIC2                       
           END-IF.                                                              
                                                                                
           IF F730-NUM-NRIC3(1:1) NOT  = '#'                                    
             MOVE F730-NUM-NRIC3              TO LB18-NRIC3                     
           ELSE                                                                 
             MOVE SPACES                      TO LB18-NRIC3                     
           END-IF.                                                              
                                                                                
           IF F730-NUM-NRIC4(1:1) NOT = '#'                                     
             MOVE F730-NUM-NRIC4              TO LB18-NRIC4                     
           ELSE                                                                 
             MOVE SPACES                      TO LB18-NRIC4                     
           END-IF.                                                              
                                                                                
           MOVE F730-CDE-BALLOT-HOUSEHOLD   TO LB18-HH-TYPE.                    
           MOVE F730-CDE-CATEGORY           TO LB18-ETHNIC.                     
           MOVE WS-TOTAL                    TO LB18-TOTAL.                      
                                                                                
           MOVE SPACES                      TO LB18-MCPS-TAG                    
           IF (F730-NUM-MCPS NOT = SPACE AND LOW-VALUE) AND                     
              (F730-CDE-RESIDENT-HA1 NOT = 'H' AND                              
               F730-CDE-RESIDENT-HA1 NOT = 'P' AND                              
               F730-CDE-RESIDENT-HA1 NOT = 'M' )                                
               MOVE F730-NUM-MCPS               TO LB18-MCPS-TAG                
           END-IF.                                                              
                                                                                
           MOVE 'N'                         TO LB18-APPS-TAG                    
           IF F730-NUM-APPS NOT  = SPACES AND LOW-VALUES                        
              IF F730-NUM-APPS-ELIG-TAG NOT = 'H' AND 'P'                       
                MOVE 'Y'                         TO LB18-APPS-TAG               
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F730-CDE-ALLOC-SCH = 'FTS'                                        
              MOVE 'Y'                      TO LB18-FTS-TAG                     
           ELSE                                                                 
              MOVE 'N'                      TO LB18-FTS-TAG                     
           END-IF.                                                              
                                                                                
           MOVE F730-NUM-FTPMC              TO LB18-FTPMC.                      
           MOVE F730-NUM-ELDERLY            TO LB18-ELDERLY.                    
           MOVE F730-NUM-PPO                TO LB18-PPO.                        
                                                                                
           IF F730-CDE-BALLOT-HOUSEHOLD = 'H' OR 'T' OR 'F'                     
               MOVE WS-MAX-UNSUCC TO LB18-UNS-CNT                               
           ELSE                                                                 
               MOVE SPACES        TO LB18-UNS-CNT-X                             
           END-IF.                                                              
                                                                                
           ADD   1                TO  WS-SNO.                                   
           MOVE  WS-SNO           TO  LB18-SNO.                                 
           MOVE  WS-EDITED-DATE   TO  LB18-DATE.                                
                                                                                
           IF WS-LINE-CTR > WS-MAX-LINE OR                                      
              WS-F730-READ = 1                                                  
              ADD          1   TO WS-PAGE                                       
              MOVE ZEROES      TO LB18-PAGE                                     
              MOVE WS-PAGE     TO LB18-PAGE                                     
              PERFORM 610-PRT-HEADING THRU 610-EXIT                             
           END-IF.                                                              
                                                                                
           WRITE LB18-PRTREC FROM LB18-PR-DETAILS.                              
                                                                                
           ADD   1   TO   WS-LINE-CTR.                                          
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *****************                                                         
       610-PRT-HEADING.                                                         
      *****************                                                         
      * POPULATE HEADER FIELDS                                                  
      *-------------------------------                                          
           MOVE F730-NUM-ALLO-CAT           TO LB18-MODE.                       
           IF F730-NUM-ALLO-CAT NOT  = 'BTO'                                    
               MOVE F730-CDE-NT1  TO LB18-CDE-NT                                
           ELSE                                                                 
               MOVE K813-NME-EST TO LB18-CDE-NT                                 
           END-IF.                                                              
                                                                                
           IF F730-DTE-BALLOT NOT = SPACES AND LOW-VALUES AND ZEROS             
               STRING F730-DTE-BALLOT(5:2) '/'                                  
                      F730-DTE-BALLOT(1:4)                                      
               DELIMITED BY SIZE INTO LB18-DTEBAL                               
           END-IF.                                                              
                                                                                
           IF F730-CDE-FLAT-TYPE NOT = SPACES AND LOW-VALUES AND ZEROS          
               STRING F730-CDE-FLAT-TYPE DELIMITED BY ' '                       
                                         '-ROOM '                               
               DELIMITED BY SIZE INTO LB18-CDE-FT                               
           END-IF.                                                              
                                                                                
      * PRINT REPORT                                                            
      *-------------------------------                                          
           WRITE LB18-PRTREC FROM WS-BLANK-LINE AFTER PAGE.                     
           WRITE LB18-PRTREC FROM WS-BLANK-LINE.                                
           WRITE LB18-PRTREC FROM WS-BLANK-LINE.                                
           WRITE LB18-PRTREC FROM LB18-PR-HEAD-01.                              
           WRITE LB18-PRTREC FROM LB18-PR-HEAD-02.                              
           WRITE LB18-PRTREC FROM WS-BLANK-LINE.                                
           WRITE LB18-PRTREC FROM LB18-PR-HEAD-3A.                              
           WRITE LB18-PRTREC FROM LB18-PR-HEAD-3B.                              
           WRITE LB18-PRTREC FROM LB18-PR-HEAD-3C.                              
           WRITE LB18-PRTREC FROM LB18-PR-HEAD-3D.                              
           WRITE LB18-PRTREC FROM WS-BLANK-LINE.                                
           WRITE LB18-PRTREC FROM LB18-PR-HEAD-04.                              
           WRITE LB18-PRTREC FROM LB18-PR-HEAD-05.                              
           WRITE LB18-PRTREC FROM WS-DASH-LINE.                                 
                                                                                
           INITIALIZE WS-LINE-CTR.                                              
           MOVE 14 TO WS-LINE-CTR.                                              
                                                                                
       610-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***************                                                           
       620-LOG-TOTAL.                                                           
      ***************                                                           
           MOVE WS-GRAND-TOTAL  TO LB18-GRAND-TOTAL.                            
           WRITE LB18-PRTREC FROM WS-BLANK-LINE.                                
           WRITE LB18-PRTREC FROM LB18-PR-HEAD-06.                              
           ADD 2 TO WS-LINE-CTR.                                                
                                                                                
       620-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ********************                                                      
       630-PRINT-CURR-HDR.                                                      
      ********************                                                      
           ADD          1      TO WS-PAGE.                                      
           MOVE ZEROES         TO LB18-PAGE.                                    
           MOVE WS-PAGE        TO LB18-PAGE.                                    
           MOVE F730-NUM-ALLO-CAT           TO LB18-MODE.                       
                                                                                
           IF F730-NUM-ALLO-CAT NOT  = 'BTO'                                    
               MOVE F730-CDE-NT1  TO LB18-CDE-NT                                
           ELSE                                                                 
               MOVE K813-NME-EST TO LB18-CDE-NT                                 
           END-IF.                                                              
                                                                                
           IF F730-DTE-BALLOT NOT = SPACES AND LOW-VALUES AND ZEROS             
               STRING F730-DTE-BALLOT(5:2) '/'                                  
                      F730-DTE-BALLOT(1:4)                                      
               DELIMITED BY SIZE INTO LB18-DTEBAL                               
           END-IF.                                                              
                                                                                
           IF F730-CDE-FLAT-TYPE NOT = SPACES AND LOW-VALUES AND ZEROS          
               STRING F730-CDE-FLAT-TYPE DELIMITED BY ' '                       
                                         '-ROOM '                               
               DELIMITED BY SIZE INTO LB18-CDE-FT                               
           END-IF.                                                              
                                                                                
           PERFORM 610-PRT-HEADING  THRU 610-EXIT.                              
                                                                                
                                                                                
       630-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *********************                                                     
       640-WRITE-P13LB18B.                                                      
      *********************                                                     
           INITIALIZE     LB18B-PRTREC.                                         
           MOVE SPACES TO LB18B-PRTREC.                                         
           ADD 1               TO WS-LB18B-WRITE.                               
           MOVE LB18-REG-NO    TO LB18B-REG-NO.                                 
           MOVE LB18-NRIC1     TO LB18B-NRIC.                                   
           MOVE LB18-NRIC2     TO LB18B-NRIC2.                                  
           MOVE LB18-FTPMC     TO LB18B-FTPMC.                                  
           MOVE LB18-MCPS-TAG  TO LB18B-MCPS-TAG.                               
           MOVE LB18-HH-TYPE   TO LB18B-HH-TYPE.                                
           MOVE LB18-ETHNIC    TO LB18B-ETHNIC.                                 
           IF LB18-TOTAL-X  NOT = SPACES AND LOW-VALUES AND ZEROS               
              MOVE LB18-TOTAL-X   TO LB18B-TOTAL                                
           END-IF.                                                              
           IF LB18-UNS-CNT-X NOT = SPACES AND LOW-VALUES AND ZEROS              
              MOVE LB18-UNS-CNT-X TO LB18B-UNS-CNT                              
           END-IF.                                                              
           MOVE LB18-MODE      TO LB18B-MODE.                                   
           MOVE LB18-DTEBAL    TO LB18B-DTEBAL.                                 
           MOVE LB18-CDE-FT    TO LB18B-CDE-FT.                                 
           MOVE F730-CDE-NT1   TO LB18B-CDE-NT.                                 
           IF F730-NUM-APPS-ELIG-TAG = '1' OR 'T'                               
              MOVE 'Y'                      TO LB18B-APPS-TAG                   
           ELSE                                                                 
              MOVE SPACE                    TO LB18B-APPS-TAG                   
           END-IF.                                                              
           WRITE LB18B-PRTREC.                                                  
                                                                                
       640-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************                                                         
       7000-PROCESS-FT.                                                         
      *****************                                                         
           IF WS-MAX-UNSUCC > 1                                                 
              COMPUTE WS-MAX-CTR = WS-MAX-UNSUCC - 1                            
                                                                                
              PERFORM VARYING WS-CNT FROM 1 BY 1                                
                        UNTIL WS-CNT > WS-MAX-CTR                               
                PERFORM 310-WRITE-BP13F740   THRU 310-EXIT                      
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ********************                                                      
       7050-GET-MAX-UNSUC.                                                      
      *********************                                                     
           IF (F730-NUM-NRIC1 NOT = SPACES AND LOW-VALUES AND ZEROS)            
              AND F730-NUM-NRIC1(1:1) NOT = '#'                                 
              MOVE F730-NUM-NRIC1 TO K593-KEY-FLD                               
              PERFORM 7100-READ-BP13K593  THRU 7100-EXIT                        
           END-IF.                                                              
           IF (F730-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND ZEROS)            
              AND F730-NUM-NRIC2(1:1) NOT = '#'                                 
              MOVE F730-NUM-NRIC2 TO K593-KEY-FLD                               
              PERFORM 7100-READ-BP13K593  THRU 7100-EXIT                        
           END-IF.                                                              
           IF (F730-NUM-NRIC3 NOT = SPACES AND LOW-VALUES AND ZEROS)            
              AND F730-NUM-NRIC3(1:1) NOT = '#'                                 
              MOVE F730-NUM-NRIC3 TO K593-KEY-FLD                               
              PERFORM 7100-READ-BP13K593  THRU 7100-EXIT                        
           END-IF.                                                              
           IF (F730-NUM-NRIC4 NOT = SPACES AND LOW-VALUES AND ZEROS)            
              AND F730-NUM-NRIC4(1:1) NOT = '#'                                 
              MOVE F730-NUM-NRIC4 TO K593-KEY-FLD                               
              PERFORM 7100-READ-BP13K593  THRU 7100-EXIT                        
           END-IF.                                                              
                                                                                
           MOVE WS-MAX-UNSUCC TO  WS-NUM-UNSUC.                                 
                                                                                
       7050-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ********************                                                      
       7100-READ-BP13K593.                                                      
      ********************                                                      
           READ BP13K593.                                                       
                                                                                
           IF WS-K593-STATUS = 00                                               
              IF K593-NUM-UNSUCC-TOT NOT NUMERIC                                
                 MOVE 0 TO K593-NUM-UNSUCC-TOT                                  
              END-IF                                                            
                                                                                
              IF K593-NUM-UNSUCC-TOT > WS-MAX-UNSUCC                            
                 MOVE K593-NUM-UNSUCC-TOT TO WS-MAX-UNSUCC                      
                 MOVE K593-NUM-NRIC       TO WS-NRIC-UNSUC                      
              END-IF                                                            
                                                                                
           ELSE                                                                 
             IF WS-K593-STATUS = 23                                             
                CONTINUE                                                        
             ELSE                                                               
                DISPLAY 'ERROR READING K593, STATUS ' WS-K593-STATUS            
                        ', KEY IS '  K593-KEY-FLD                               
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       7100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ********************                                                      
       7200-READ-BP13K813.                                                      
      ********************                                                      
           MOVE SPACES                     TO BP13K813-REC.                     
           INITIALIZE                         BP13K813-REC.                     
           MOVE F730-CDE-NT1               TO K813-NUM-ZONE.                    
           MOVE F730-CDE-FLAT-TYPE         TO K813-NUM-FLAT-TYPE.               
           MOVE F730-DTE-BALLOT            TO K813-DTE-BALLOT.                  
                                                                                
           READ BP13K813.                                                       
                                                                                
           IF WS-K813-STATUS = 00 OR 23                                         
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'ERROR READING K813, STATUS ' WS-K813-STATUS              
                      ', KEY IS '  K813-KEY-FLD                                 
           END-IF.                                                              
                                                                                
       7200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       999-CLOSE-FILES.                                                         
           CLOSE BP13F740                                                       
                 BP13F730                                                       
                 BP13K593                                                       
                 BP13K813                                                       
                 BP13LB18                                                       
                 P13LB18B.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '* RUN DATE :  ' WS-EDITED-DATE.                             
           DISPLAY '*================ BP13CB18 ================*'.              
           DISPLAY '  NO. OF F730 RECORDS READ    = ' WS-F730-READ.             
           DISPLAY '  NO. OF F740 RECORDS WRITTEN = ' WS-F740-WRITE.            
           DISPLAY '*==========================================*'.              
                                                                                
           IF WS-K593-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K593 FILE ERROR ' WS-K593-STATUS             
           END-IF.                                                              
                                                                                
           IF WS-K813-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K813 FILE ERROR ' WS-K813-STATUS             
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
                                                                                
