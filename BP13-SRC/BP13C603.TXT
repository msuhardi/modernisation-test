      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.    BP13C603.                                                 
      *AUTHOR.        LOU JL.                                                   
      *DATE-WRITTEN.  17/08/92.                                                 
                                                                                
      * ========================================================= *             
      *               SYSTEM OF COMMENCEMENT (BP13)               *             
      * ========================================================= *             
      *    OBJECTIVE :                                            *             
      *      1. THIS PROGRAM WILL PRINT OUT THE SUMMARY LIST OF   *             
      *         SALES ORDER RAISED AND KEY ISSUED, FOR SA-TP      *             
      *         CASES ONLY.                                       *             
      *                                                           *             
      *    INPUT FILE :                                           *             
      *      1. BP13F410.                                         *             
      *                                                           *             
      *    LISTING :                                              *             
      *      1. BP13L603 - S.O. RAISED FOR KEY ISSUED SA-TP CASES *             
      *                                                           *             
      *    SUB-PROGRAM :                                          *             
      *       1. BP13C913 - TO GET FLAT INFO FROM PIDB TABLE      *             
      *                                                           *             
      * ========================================================= *             
      * CHG-NO   BY  DATE    DESCRIPTION                          *             
      * -------- --- ------  -----------                          *             
      * SOC-PH8  JEP 930703  CALL SUB-PROGRAM BP13C904            *             
      * BP130020 SSS 951010  CHG FORMAT OF RPT-ID                 *             
      * BP130418 NOR 980608  MAKE PRGM Y2K COMPLIANT              *             
      * BP130883 RAS 000808  TO CHANGE BP13C904 TO BP13C913       *             
      * BP134779 PCL4130613  TO CONVERT THE REPORT TO SENDMAIL    *             
      * BP135537 RJB1230215  CHANGE  THE EMAIL RECIPIENT FROM     *             
      *                      NICKNAME INTO COMPLETE NAME          *             
      * BP136339 PCL4120716  TO ADD NEW EMAIL RECIPIENTS          *             
      * BP136477 RJB 181016  TO REPLACE KIM HUAT TO YEE MIEN      *             
      * BP136625 RJB 010217  REMOVE NORMALA BUANG, INCLUDE CHANDRA*             
      *                      ARUMUGAM AND MEENACHI                *             
      * BP136685 PCL 030417  TO UPDATE EMAIL/REPORT FORMAT        *             
      * ========================================================= *             
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F410   ASSIGN  TO  BP13F410.                              
           SELECT BP13L603   ASSIGN  TO  BP13L603.                              
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
       FILE SECTION.                                                            
                                                                                
       FD   BP13F410                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            RECORDING MODE  IS   F.                                             
       COPY BP13F410.                                                           
                                                                                
       FD  BP13L603                                                             
                                                                                
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORDING MODE IS V                              
                               LABEL RECORD IS STANDARD.                        
       01  PRINT-REC-1                   PIC X(200).                            
                                                                                
                                                                                
      ************************                                                  
       WORKING-STORAGE SECTION.                                                 
      ************************                                                  
                                                                                
       01  WS-FLAGS.                                                            
           05  EOF-F410               PIC X      VALUE SPACE.                   
           05  WS-REC-FOUND-IND       PIC X      VALUE SPACE.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  CNT1                   PIC 9      VALUE 0.                       
           05  LINE-CNT1              PIC 9(2)   VALUE 30.                      
           05  NO-OF-PAGE1            PIC 9(4)   VALUE ZEROS.                   
           05  SEQUENCE-NO1           PIC 9(4)   VALUE ZEROS.                   
           05  TOT-LOAN-AMT1          PIC S9(8)V99 VALUE ZEROS.                 
                                                                                
       01  WS-OTHER-VARIABLES.                                                  
           05  WS-PREV-SALES-TYPE     PIC X(2)   VALUE SPACES.                  
           05  WS-NAME3               PIC X(60)  VALUE SPACES.                  
           05  WS-NAME4               PIC X(60)  VALUE SPACES.                  
           05  WS-HSE-SUB.                                                      
               10  WS-HSE-SUB1        PIC X(1)   VALUE SPACE.                   
               10  WS-HSE-SUB2        PIC X(2)   VALUE SPACES.                  
           05  WS-SQL-CODE            PIC 9(2)   VALUE ZEROES.                  
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURRENT-YY          PIC 9999   VALUE ZEROES.                  
           05  WS-CURRENT-MM          PIC 99     VALUE ZEROES.                  
           05  WS-CURRENT-DD          PIC 99     VALUE ZEROES.                  
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-EDITED-DD           PIC 99     VALUE ZEROES.                  
           05  FILLER                 PIC X      VALUE '/'.                     
           05  WS-EDITED-MM           PIC 99     VALUE ZEROES.                  
           05  FILLER                 PIC X      VALUE '/'.                     
           05  WS-EDITED-YY           PIC 9999   VALUE ZEROES.                  
                                                                                
       01  WS-SO-DATE.                                                          
           05  WS-SO-YY               PIC X(4)   VALUE SPACES.                  
           05  WS-SO-MM               PIC X(2)   VALUE SPACES.                  
           05  WS-SO-DD               PIC X(2)   VALUE SPACES.                  
                                                                                
       01  WS-AGMT-DATE.                                                        
           05  WS-AGMT-YY             PIC X(4)   VALUE SPACES.                  
           05  WS-AGMT-MM             PIC X(2)   VALUE SPACES.                  
           05  WS-AGMT-DD             PIC X(2)   VALUE SPACES.                  
                                                                                
       01  WS-SYSTEM-DATE-TIME.                                                 
           05  WS-SYSTEM-DATE         PIC X(08).                                
           05  WS-SYSTEM-TIME         PIC X(08).                                
           05  WS-GMT                 PIC X(05).                                
                                                                                
      *----------------------------------------------------------------*        
      *        LINKAGE FOR SUB-PROGRAM BP13C913                        *        
      *----------------------------------------------------------------*        
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH    PIC X(4).                                 
               10  WS-LINK-NUM-SCH    PIC X(5).                                 
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *----------------------------------------------------------------*        
      *        PRINT HEADINGS LINES                                    *        
      *----------------------------------------------------------------*        
       01  L603-PR-HEAD-1.                                                      
           05  RPT-ID                 PIC X(8)   VALUE 'BP13L603'.              
           05  FILLER                 PIC X(10)  VALUE '      HDB3'.            
           05  FILLER                 PIC X(21)  VALUE SPACES.                  
           05  FILLER                 PIC X(63)  VALUE                          
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                 PIC X(7)   VALUE 'DATE : '.               
           05  WS-PRINT-DATE          PIC X(11)  VALUE SPACES.                  
           05  FILLER                 PIC X(7)   VALUE 'PAGE : '.               
           05  WS-PAGE                PIC ZZZ9.                                 
                                                                                
       01  L603-PR-HEAD-2.                                                      
           05  FILLER                 PIC X(33)  VALUE SPACES.                  
           05  FILLER                 PIC X(35)  VALUE                          
               'SUMMARY LIST OF SALES ORDER RAISED'.                            
           05  WS-DESC                PIC X(18)  VALUE 'FOR KEY ISSUED'.        
           05  FILLER                 PIC X(16)  VALUE SPACES.                  
           05  FILLER                 PIC X(29)  VALUE                          
               'USER : AGREEMENT TEAM (SALES)'.                                 
                                                                                
       01  L603-PR-HEAD-4.                                                      
           05  FILLER                 PIC X(132) VALUE ALL '-'.                 
                                                                                
       01  L603-PR-HEAD-5.                                                      
           05  FILLER                 PIC X(118) VALUE 'T O T A L : '.          
           05  WS-TOT-AMT             PIC ZZ,ZZZ,ZZ9.99.                        
                                                                                
       01  L603-PR-HEAD-6.                                                      
           05  FILLER                 PIC X(30)  VALUE                          
               ' S\N    SCH-ACC-NO'.                                            
           05  FILLER                 PIC X(30)  VALUE 'APPLICANT (S)'.         
           05  FILLER                 PIC X(33)  VALUE 'FLAT ADDRESS'.          
           05  FILLER                 PIC X(39)  VALUE                          
               'SALES AGRMT DATE  PREMIUM   LOAN AMOUNT'.                       
                                                                                
       01  L603-PR-HEAD-7.                                                      
           05  FILLER                 PIC X(8)   VALUE SPACES.                  
           05  FILLER                 PIC X(25)  VALUE 'REGN NO'.               
           05  FILLER                 PIC X(60)  VALUE 'NAME'.                  
           05  FILLER                 PIC X(40)  VALUE                          
               'SALES ORDER DATE    (%)         ($)'.                           
                                                                                
       01  L603-PR-HEAD-B.                                                      
           05  FILLER                 PIC X(45)  VALUE SPACES.                  
           05  FILLER                 PIC X(60)  VALUE                          
               'FOR SYSTEM OF COMMMITMENT FLAT SA-TP CASES'.                    
                                                                                
      *----------------------------------------------------------------*        
      *        PRINT DETAIL LINES                                      *        
      *----------------------------------------------------------------*        
       01  L603-PR-DETAIL-1.                                                    
           05  FILLER                 PIC X      VALUE SPACES.                  
           05  603-SEQ-NO             PIC ZZZ9.                                 
           05  FILLER                 PIC X(3)   VALUE SPACES.                  
           05  603-SCH-ACC-NO         PIC X(14)  VALUE SPACES.                  
           05  FILLER                 PIC X(3)   VALUE SPACES.                  
           05  603-NAME1              PIC X(32)  VALUE SPACES.                  
           05  FILLER                 PIC X(3)   VALUE SPACES.                  
           05  603-BLK-DESC           PIC X(4)   VALUE SPACES.                  
           05  603-BLK-NO             PIC X(5)   VALUE SPACES.                  
           05  FILLER                 PIC X(1)   VALUE SPACES.                  
           05  603-HEX                PIC X(1)   VALUE '#'.                     
           05  603-HOUSE-NO           PIC X(7)   VALUE SPACES.                  
           05  FILLER                 PIC X(18)  VALUE SPACES.                  
           05  603-AGMT-DATE          PIC X(10)  VALUE SPACES.                  
           05  FILLER                 PIC X(10)  VALUE SPACES.                  
           05  603-PREMIUM-RATE       PIC X(2)   VALUE SPACES.                  
           05  FILLER                 PIC X(3)   VALUE SPACES.                  
           05  603-LOAN-AMT           PIC ZZZ,ZZ9.99-.                          
                                                                                
       01  L603-PR-DETAIL-2.                                                    
           05  FILLER                 PIC X(8)   VALUE SPACES.                  
           05  603-REGN-NO            PIC X(8)   VALUE SPACES.                  
           05  FILLER                 PIC X(9)   VALUE SPACES.                  
           05  603-NAME2              PIC X(32)  VALUE SPACES.                  
           05  FILLER                 PIC X(3)   VALUE SPACES.                  
           05  603-STREET             PIC X(30)  VALUE SPACES.                  
           05  FILLER                 PIC X(6)   VALUE SPACES.                  
           05  603-SO-DATE            PIC X(10)  VALUE SPACES.                  
                                                                                
       01  L603-PR-DETAIL-3.                                                    
           05  FILLER                 PIC X(25)  VALUE SPACES.                  
           05  603-NAME3              PIC X(32)  VALUE SPACES.                  
                                                                                
       01  L603-PR-DETAIL-4.                                                    
           05  FILLER                 PIC X(25)  VALUE SPACES.                  
           05  603-NAME4              PIC X(32)  VALUE SPACES.                  
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
      *    MAIN LOGIC PROCESS ROUTINE                                  *        
      ******************************************************************        
       000-MAIN-ROUTINE.                                                        
                                                                                
           PERFORM  100-OPEN-ROUTINE        THRU  100-EXIT.                     
           PERFORM  200-READ-BP13F410       THRU  200-EXIT.                     
           MOVE     F410-CDE-SALES-TYPE     TO    WS-PREV-SALES-TYPE.           
           PERFORM  300-PRINT-SUMMARY-LIST  THRU  300-EXIT                      
                        UNTIL EOF-F410 = 'Y'.                                   
           PERFORM  800-PRINT-TOTAL         THRU  800-EXIT.                     
           PERFORM  900-CLOSE-ROUTINE       THRU  900-EXIT.                     
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    OPEN FILES SECTION                                          *        
      ******************************************************************        
       100-OPEN-ROUTINE.                                                        
           OPEN INPUT  BP13F410                                                 
                OUTPUT BP13L603.                                                
                                                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    READ SEQUENTIAL FILE BP13F410 AT END SET EOF-IND TO 'Y'     *        
      ******************************************************************        
       200-READ-BP13F410.                                                       
           READ BP13F410 AT END                                                 
                         MOVE 'Y' TO EOF-F410                                   
                         GO TO 200-EXIT.                                        
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    PRINT OUT THE SUMMARY LIST OF SALES ORDER RAISED            *        
      ******************************************************************        
       300-PRINT-SUMMARY-LIST.                                                  
           IF  F410-CDE-SA-TP  =  'Y'                                           
               NEXT SENTENCE                                                    
           ELSE                                                                 
               MOVE 'Y' TO EOF-F410                                             
               GO TO 300-EXIT.                                                  
                                                                                
           MOVE    'Y'                   TO    WS-REC-FOUND-IND.                
           PERFORM 400-CALL-BP13C913     THRU  400-EXIT.                        
           PERFORM 500-PROCESS-PRINTING  THRU  500-EXIT.                        
           PERFORM 200-READ-BP13F410     THRU  200-EXIT.                        
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    CALL BP13C913 USING F410-NUM-SCH-ACC VALUES AS A KEY TO     *        
      *                  GET FLAT INFO FROM PIDB TABLE                 *        
      ******************************************************************        
       400-CALL-BP13C913.                                                       
           MOVE  'Y'               TO     WS-REC-FOUND-IND.                     
           MOVE  SPACES            TO     BP13COMM8-REC.                        
           INITIALIZE                     BP13COMM8-REC.                        
                                                                                
           MOVE  F410-NUM-SCH-ACC  TO     WS-LINK-REC.                          
                                                                                
           CALL 'BP13C913'         USING  WS-LINK-REC,                          
                                          BP13COMM8-REC.                        
                                                                                
           IF COMM8-CDE-SYSERR NOT = 0                                          
              MOVE 'N'               TO WS-REC-FOUND-IND                        
              MOVE  COMM8-CDE-SYSERR TO WS-SQL-CODE                             
              DISPLAY 'F410-NUM-SCH-ACC = ' F410-NUM-SCH-ACC                    
              IF COMM8-CDE-SYSERR = 100                                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PIDB TABLE ' WS-SQL-CODE         
              ELSE                                                              
                 DISPLAY 'ERROR READING PIDB TABLE   ' WS-SQL-CODE              
           ELSE                                                                 
              MOVE COMM8-NUM-UNIT-SUB TO WS-HSE-SUB.                            
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    CHECK F410-CDE-SALES-TYPE WITH WS-PREV-SALES-TYPE           *        
      ******************************************************************        
       500-PROCESS-PRINTING.                                                    
           IF F410-CDE-SALES-TYPE = WS-PREV-SALES-TYPE                          
              NEXT SENTENCE                                                     
           ELSE                                                                 
              PERFORM 800-PRINT-TOTAL     THRU 800-EXIT                         
              MOVE    F410-CDE-SALES-TYPE TO   WS-PREV-SALES-TYPE               
              MOVE    30                  TO   LINE-CNT1                        
              MOVE    0                   TO   SEQUENCE-NO1.                    
                                                                                
           PERFORM 600-PRINT-DETAIL-LINE  THRU 600-EXIT.                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    PRINT DETAIL LINE FOR KEY ISSUED DATE NOT = ZEROS OR SPACES *        
      ******************************************************************        
       600-PRINT-DETAIL-LINE.                                                   
           ADD    1            TO SEQUENCE-NO1.                                 
           MOVE   SEQUENCE-NO1 TO 603-SEQ-NO.                                   
           STRING F410-SCHEME '-' F410-ACC '-' F410-CHK '-'                     
                  F410-LESSEE-CODE DELIMITED BY SIZE                            
                  INTO 603-SCH-ACC-NO.                                          
                                                                                
           MOVE   F410-NAME1            TO  603-NAME1.                          
           MOVE   F410-NAME2            TO  603-NAME2.                          
           MOVE   F410-NAME3            TO  603-NAME3.                          
           MOVE   F410-NAME4            TO  603-NAME4.                          
           MOVE   F410-NUM-REGN         TO  603-REGN-NO.                        
           MOVE   F410-NUM-PREMIUM-RATE TO  603-PREMIUM-RATE.                   
           MOVE   F410-DTE-SO-EXECUTION TO  WS-SO-DATE.                         
           STRING WS-SO-DD '/' WS-SO-MM '/' WS-SO-YY DELIMITED BY SIZE          
                  INTO 603-SO-DATE.                                             
                                                                                
           MOVE ZEROES       TO  WS-AGMT-DATE.                                  
           STRING WS-AGMT-DD '/' WS-AGMT-MM '/' WS-AGMT-YY                      
                  DELIMITED BY SIZE INTO 603-AGMT-DATE.                         
                                                                                
           MOVE  F410-AMT-LOAN TO 603-LOAN-AMT.                                 
           IF    WS-REC-FOUND-IND = 'N'                                         
                 MOVE SPACES TO 603-BLK-NO 603-HOUSE-NO 603-BLK-DESC            
                 MOVE SPACES TO 603-HEX    603-STREET                           
           ELSE                                                                 
                 MOVE 'BLK '              TO  603-BLK-DESC                      
                 MOVE COMM8-NUM-BLK       TO  603-BLK-NO                        
                 MOVE '#'                 TO  603-HEX                           
                 MOVE COMM8-NME-STREET    TO  603-STREET                        
                 STRING COMM8-NUM-LEVEL   '-' COMM8-NUM-UNIT-MAIN               
                        WS-HSE-SUB1 DELIMITED BY SIZE                           
                        INTO   603-HOUSE-NO.                                    
                                                                                
           IF LINE-CNT1 > 19                                                    
              PERFORM 700-PRINT-HEADER THRU 700-EXIT.                           
                                                                                
           MOVE  SPACES      TO    PRINT-REC-1.                                 
           WRITE PRINT-REC-1 AFTER 1.                                           
           WRITE PRINT-REC-1 FROM  L603-PR-DETAIL-1 AFTER 1.                    
           MOVE  SPACES      TO    PRINT-REC-1.                                 
           WRITE PRINT-REC-1 FROM  L603-PR-DETAIL-2 AFTER 1.                    
           ADD   1           TO    LINE-CNT1.                                   
                                                                                
           MOVE    F410-NAME3 TO WS-NAME3.                                      
           INSPECT WS-NAME3 REPLACING ALL LOW-VALUES BY ' '.                    
           IF WS-NAME3 = SPACES                                                 
              NEXT SENTENCE                                                     
           ELSE                                                                 
              ADD   1           TO   CNT1                                       
              MOVE  SPACES      TO   PRINT-REC-1                                
              WRITE PRINT-REC-1 FROM L603-PR-DETAIL-3 AFTER 1.                  
                                                                                
           MOVE F410-NAME4 TO WS-NAME4.                                         
           INSPECT WS-NAME4 REPLACING ALL LOW-VALUES BY ' '.                    
           IF WS-NAME4 = SPACES                                                 
              NEXT SENTENCE                                                     
           ELSE                                                                 
              ADD   1           TO   CNT1                                       
              MOVE  SPACES      TO   PRINT-REC-1                                
              WRITE PRINT-REC-1 FROM L603-PR-DETAIL-4 AFTER 1.                  
                                                                                
           IF CNT1 = 2                                                          
              ADD    1   TO LINE-CNT1                                           
              MOVE   0   TO CNT1.                                               
           COMPUTE TOT-LOAN-AMT1 = TOT-LOAN-AMT1 + F410-AMT-LOAN.               
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    PRINT HEADING LINES SECTION                                 *        
      ******************************************************************        
       700-PRINT-HEADER.                                                        
           ADD    1                TO   NO-OF-PAGE1.                            
           MOVE   NO-OF-PAGE1      TO   WS-PAGE.                                
           MOVE   FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE.                      
           MOVE   WS-SYSTEM-DATE   TO   WS-CURRENT-DATE.                        
           MOVE   WS-CURRENT-DD    TO   WS-EDITED-DD.                           
           MOVE   WS-CURRENT-MM    TO   WS-EDITED-MM.                           
           MOVE   WS-CURRENT-YY    TO   WS-EDITED-YY.                           
           MOVE   WS-EDITED-DATE   TO   WS-PRINT-DATE.                          
           WRITE  PRINT-REC-1 FROM L603-PR-HEAD-1 AFTER PAGE.                   
           WRITE  PRINT-REC-1 FROM L603-PR-HEAD-2 AFTER 1.                      
                                                                                
           IF WS-PREV-SALES-TYPE = '10'                                         
              WRITE PRINT-REC-1 FROM L603-PR-HEAD-B AFTER 1.                    
                                                                                
           WRITE PRINT-REC-1 FROM L603-PR-HEAD-6 AFTER 2.                       
           WRITE PRINT-REC-1 FROM L603-PR-HEAD-7 AFTER 1.                       
           MOVE  SPACES      TO   PRINT-REC-1.                                  
           WRITE PRINT-REC-1.                                                   
           MOVE  0           TO   LINE-CNT1.                                    
                                                                                
       700-EXIT.                                                                
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
      *    PRINT TOTAL LINES SECTION                                   *        
      ******************************************************************        
       800-PRINT-TOTAL.                                                         
           IF SEQUENCE-NO1 > 0                                                  
              MOVE  TOT-LOAN-AMT1 TO   WS-TOT-AMT                               
              WRITE PRINT-REC-1   FROM L603-PR-HEAD-4 AFTER 2                   
              MOVE  SPACES        TO   PRINT-REC-1                              
              WRITE PRINT-REC-1   FROM L603-PR-HEAD-5 AFTER 1                   
              WRITE PRINT-REC-1   FROM L603-PR-HEAD-4 AFTER 1.                  
                                                                                
           MOVE ZEROES TO TOT-LOAN-AMT1.                                        
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
      *    CLOSE FILES   AND   STOP RUN                                *        
      ******************************************************************        
       900-CLOSE-ROUTINE.                                                       
           CLOSE BP13F410                                                       
                 BP13L603.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
