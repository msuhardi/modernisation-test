      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C21O.                                                 
      *AUTHOR.        RACHELLE SAN BUENAVENTURA.                                
      *DATE-WRITTEN.  13 OCT 2009.                                              
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : CONVERT F240 FILE FORMAT TO K249            *           
      *                 ADD RECORDS TO BP13K249                     *           
      *                 (FOR SBF - SALE OF BALANCE FLATS)           *           
      *   INPUT FILES :                                             *           
      *   1.  BP13F240                                              *           
      *                                                             *           
      *   INPUT/OUTPUT:                                             *           
      *   1.  BP13K249                                              *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP133771 08/12/2009 RB12 NEW PROGRAM                        *           
      * BP135111 29/10/2013 ESA1 CATER FOR 3GEN                     *           
      * BP135135 14/11/2013 ESA1 CATER FOR BONUS FLATS              *           
      * BP135415 13/08/2014 ESA1 EXTEND BP13K249 TO 450             *           
      * BP139545 31/10/2023 SC54 POLICY CHANGE STARTING FROM 202310 *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F240 ASSIGN       TO BP13F240.                            
                                                                                
           SELECT BP13K246 ASSIGN       TO BP13K246                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K246-KEY-FLD                         
                           FILE STATUS  IS WS-K246-STATUS                       
                           ACCESS MODE  IS DYNAMIC.                             
                                                                                
           SELECT BP13K249 ASSIGN       TO BP13K249                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K249-KEY-FLD                         
                           FILE STATUS  IS WS-K249-STATUS                       
                           ACCESS MODE  IS DYNAMIC.                             
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F240                                                            
            RECORD CONTAINS 450 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13K240.                                                           
                                                                                
       FD   BP13K246                                                            
            RECORD CONTAINS 80  CHARACTERS.                                     
       COPY BP13K246.                                                           
                                                                                
       FD   BP13K249                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K249.                                                           
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY         PIC 9999.                                    
           05  WS-CUR-MM           PIC 99.                                      
           05  WS-CUR-DD           PIC 99.                                      
                                                                                
       01  WS-K246-STATUS           PIC 9(02)  VALUE ZEROES.                    
       01  WS-K249-STATUS           PIC 9(02)  VALUE ZEROES.                    
       01  WS-COMBINED-IND          PIC X      VALUE SPACES.                    
       01  WS-K249-FND              PIC X      VALUE SPACES.                    
       01  WS-F240-EOF              PIC X      VALUE SPACES.                    
       01  WS-F240-READ-CNT         PIC 9(08)  VALUE ZEROES.                    
       01  WS-F240-READ-ERR         PIC 9(08)  VALUE ZEROES.                    
       01  WS-K246-READ-CNT         PIC 9(08)  VALUE ZEROES.                    
       01  WS-K249-REWRITE-CNT      PIC 9(08)  VALUE ZEROES.                    
       01  WS-K249-REWRITE-ERR      PIC 9(08)  VALUE ZEROES.                    
       01  WS-PREV-KEY.                                                         
           05  WS-NUM-NEW-TOWN      PIC X(20).                                  
           05  WS-NUM-FLAT-TYPE     PIC X(09).                                  
           05  WS-NUM-SELECTION     PIC X(03).                                  
           05  WS-DTE-BALLOT        PIC X(06).                                  
           05  WS-NUM-BONUS         PIC X(06).                                  
       01  WS-AVAIL-TOT             PIC 9(04)  VALUE ZEROES.                    
       01  WS-AVAIL-TOTAL           PIC 9(04)  VALUE ZEROES.                    
       01  WS-AVAIL-CAT             PIC 9(03)  VALUE ZEROES.                    
       01  WS-AVAIL-CAT1            PIC 9(03)  VALUE ZEROES.                    
       01  WS-AVAIL-CAT2            PIC 9(03)  VALUE ZEROES.                    
       01  WS-AVAIL-CAT3            PIC 9(03)  VALUE ZEROES.                    
       01  WS-NUM-TOTAL             PIC 9(04)  VALUE ZEROES.                    
       01  WS-NUM-TOTAL-GEN3        PIC 9(04)  VALUE ZEROES.                    
       01  WS-NUM-UNIT-CAT1         PIC 9(04)  VALUE ZEROES.                    
       01  WS-NUM-UNIT-CAT2         PIC 9(04)  VALUE ZEROES.                    
       01  WS-NUM-UNIT-CAT3         PIC 9(04)  VALUE ZEROES.                    
       01  WS-NUM-LIMIT             PIC 9(03)V99 VALUE ZEROES.                  
       01  WS-NUM-LIMIT-REDEF REDEFINES WS-NUM-LIMIT.                           
           05  WS-NUM-LIMIT-UNIT    PIC 9(03).                                  
           05  WS-NUM-LIMIT-DECIMAL PIC 9(02).                                  
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-PRE-PROCESS      THRU 1000-EXIT.                        
           PERFORM 2000-READ-F240        THRU 2000-EXIT.                        
           PERFORM 3000-PROCESS-REC      THRU 3000-EXIT                         
                   UNTIL WS-F240-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-PRE-PROCESS.                                                        
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13F240                                                 
                       BP13K246                                                 
                I-O    BP13K249.                                                
                                                                                
           IF WS-K246-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BP13K246 - OPEN ERROR: ' WS-K246-STATUS                  
              MOVE WS-K246-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-K249-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BP13K249 - OPEN ERROR: ' WS-K249-STATUS                  
              MOVE WS-K249-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-F240.                                                          
      *---------------*                                                         
                                                                                
           READ BP13F240 AT END                                                 
                PERFORM 3050-UPD-BP13K249 THRU 3050-EXIT                        
                MOVE 'Y' TO WS-F240-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-F240-READ-CNT.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           PERFORM 3600-READ-BP13K246          THRU 3600-EXIT.                  
                                                                                
           IF (WS-PREV-KEY NOT = K240-ALT-KEY5)                                 
              IF (WS-PREV-KEY NOT = SPACES AND LOW-VALUES) AND                  
                 ((WS-COMBINED-IND = SPACE) OR                                  
                  ((WS-COMBINED-IND = 'Y') AND                                  
                   (WS-NUM-FLAT-TYPE = '4-ROOM   ' OR '5-ROOM   ')))            
                 PERFORM 3050-UPD-BP13K249     THRU 3050-EXIT                   
                 MOVE ZEROES                   TO   WS-AVAIL-TOTAL              
                                                    WS-AVAIL-CAT1               
                                                    WS-AVAIL-CAT2               
                                                    WS-AVAIL-CAT3               
                                                    WS-NUM-TOTAL                
                                                    WS-NUM-TOTAL-GEN3           
                                                    WS-NUM-UNIT-CAT1            
                                                    WS-NUM-UNIT-CAT2            
                                                    WS-NUM-UNIT-CAT3            
              END-IF                                                            
              MOVE K240-ALT-KEY5               TO   WS-PREV-KEY                 
              PERFORM 3500-COMPUTE-TOTALS      THRU 3500-EXIT                   
           ELSE                                                                 
              PERFORM 3500-COMPUTE-TOTALS      THRU 3500-EXIT                   
              IF WS-PREV-KEY = SPACES OR LOW-VALUES                             
                 MOVE K240-ALT-KEY5            TO   WS-PREV-KEY                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-F240              THRU 2000-EXIT.                  
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3050-UPD-BP13K249.                                                       
      *-------------------*                                                     
                                                                                
           IF WS-PREV-KEY = SPACES OR LOW-VALUES                                
              GO TO 3050-EXIT                                                   
           END-IF.                                                              
                                                                                
           PERFORM 3100-READ-BP13K249          THRU 3100-EXIT.                  
           PERFORM 3200-MOVE-FIELDS            THRU 3200-EXIT.                  
           IF WS-K249-FND = 'Y'                                                 
              PERFORM 3300-REWRITE-BP13K249    THRU 3300-EXIT                   
           END-IF.                                                              
                                                                                
           IF WS-COMBINED-IND = 'Y'                                             
              PERFORM 3150-READ-BP13K249       THRU 3150-EXIT                   
              PERFORM 3200-MOVE-FIELDS         THRU 3200-EXIT                   
              IF WS-K249-FND = 'Y'                                              
                 PERFORM 3300-REWRITE-BP13K249 THRU 3300-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3100-READ-BP13K249.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                     TO BP13K249-REC.                     
           INITIALIZE                         BP13K249-REC.                     
                                                                                
           MOVE WS-NUM-NEW-TOWN            TO K249-NUM-NEW-TOWN.                
           MOVE WS-NUM-FLAT-TYPE           TO K249-NUM-FLAT-TYPE.               
           MOVE WS-NUM-SELECTION           TO K249-NUM-SELECTION.               
           MOVE WS-DTE-BALLOT              TO K249-DTE-BALLOT.                  
           MOVE WS-NUM-BONUS               TO K249-NUM-BONUS.                   
           MOVE 'N'                        TO WS-K249-FND.                      
                                                                                
           READ BP13K249 KEY IS K249-KEY-FLD.                                   
                                                                                
           EVALUATE WS-K249-STATUS                                              
             WHEN 00                                                            
                MOVE 'Y' TO WS-K249-FND                                         
             WHEN 23                                                            
                CONTINUE                                                        
             WHEN OTHER                                                         
                DISPLAY 'BP13K249 - READ ERROR: ' WS-K249-STATUS                
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3150-READ-BP13K249.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                     TO BP13K249-REC.                     
           INITIALIZE                         BP13K249-REC.                     
                                                                                
           MOVE WS-NUM-NEW-TOWN            TO K249-NUM-NEW-TOWN.                
           MOVE WS-NUM-SELECTION           TO K249-NUM-SELECTION.               
           MOVE WS-DTE-BALLOT              TO K249-DTE-BALLOT.                  
           MOVE WS-NUM-BONUS               TO K249-NUM-BONUS.                   
           MOVE 'N'                        TO WS-K249-FND.                      
                                                                                
           IF WS-NUM-FLAT-TYPE = '4-ROOM   '                                    
              MOVE '3-ROOM   '             TO K249-NUM-FLAT-TYPE                
           END-IF.                                                              
                                                                                
           IF WS-NUM-FLAT-TYPE = '5-ROOM   '                                    
              MOVE 'EXECUTIVE'             TO K249-NUM-FLAT-TYPE                
           END-IF.                                                              
                                                                                
           READ BP13K249 KEY IS K249-KEY-FLD.                                   
                                                                                
           EVALUATE WS-K249-STATUS                                              
             WHEN 00                                                            
                MOVE 'Y' TO WS-K249-FND                                         
             WHEN 23                                                            
                CONTINUE                                                        
             WHEN OTHER                                                         
                DISPLAY 'BP13K249 - READ ERROR: ' WS-K249-STATUS                
           END-EVALUATE.                                                        
                                                                                
       3150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3200-MOVE-FIELDS.                                                        
      *-----------------*                                                       
                                                                                
      ** CR - BP139545 START                                                    
           IF K249-DTE-BALLOT < '202310'                                        
                                                                                
              COMPUTE WS-NUM-LIMIT = ((WS-NUM-TOTAL - WS-NUM-TOTAL-GEN3)        
                                       * 0.05)                                  
                                                                                
              IF WS-NUM-LIMIT-DECIMAL > 0                                       
                 ADD 1                  TO   WS-NUM-LIMIT                       
              END-IF                                                            
                                                                                
              IF WS-NUM-LIMIT < 10                                              
                 MOVE 10                TO   K249-NUM-LIMIT                     
              ELSE                                                              
                 MOVE WS-NUM-LIMIT      TO   K249-NUM-LIMIT                     
              END-IF                                                            
                                                                                
           ELSE                                                                 
                                                                                
              PERFORM 3210-COMPUTE-LIMIT-202310 THRU 3210-EXIT                  
                                                                                
           END-IF.                                                              
      ** CR - BP139545 END.                                                     
                                                                                
           MOVE WS-CUR-DATE          TO   K249-DTE-UPDATE.                      
                                                                                
           ACCEPT K249-TME-UPDATE    FROM TIME.                                 
                                                                                
           MOVE 'BP13C21O'           TO   K249-NUM-USERID.                      
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ** CR - BP139545 START                                                    
      *-------------------------*                                               
       3210-COMPUTE-LIMIT-202310.                                               
      *-------------------------*                                               
                                                                                
      *NOTE: IN BP13CH03, IF NUMBER OF FLAT IS >= TO LIMIT IS                   
      * CONSIDER AS WITHIN LIMIT, HENCE, IN ORDER TO WAIVE,                     
      * WHEN THE NUMBER OF FLAT IS < = 5, THIS PROGRAM                          
      * WILL NEED TO SET THE LIMIT TO 6.                                        
                                                                                
      *    MOVE 5 TO WS-NUM-LIMIT.                                              
           MOVE 6 TO WS-NUM-LIMIT.                                              
                                                                                
           IF K249-NUM-FLAT-TYPE = '5-ROOM   '                                  
                                                                                
              COMPUTE WS-NUM-LIMIT = WS-NUM-LIMIT + K249-NUM-TOTAL-GEN3         
                                                                                
           END-IF.                                                              
                                                                                
           MOVE WS-NUM-LIMIT TO K249-NUM-LIMIT.                                 
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ** CR - BP139545 END.                                                     
      *---------------------*                                                   
       3300-REWRITE-BP13K249.                                                   
      *---------------------*                                                   
                                                                                
           REWRITE BP13K249-REC.                                                
                                                                                
           IF WS-K249-STATUS = 00                                               
              ADD 1 TO WS-K249-REWRITE-CNT                                      
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING BP13K249, KEY: '                         
                       K249-KEY-FLD                                             
              ADD 1 TO WS-K249-REWRITE-ERR                                      
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       3500-COMPUTE-TOTALS.                                                     
      *--------------------*                                                    
                                                                                
           IF K240-NUM-TOTAL NOT NUMERIC                                        
              MOVE ZEROES       TO K240-NUM-TOTAL                               
           END-IF.                                                              
                                                                                
           IF K240-NUM-TOT-CAT1 NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-TOT-CAT1                            
           END-IF.                                                              
                                                                                
           IF K240-NUM-TOT-CAT2 NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-TOT-CAT2                            
           END-IF.                                                              
                                                                                
           IF K240-NUM-TOT-CAT3 NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-TOT-CAT3                            
           END-IF.                                                              
                                                                                
           IF K240-NUM-MAX-CAT1 NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-MAX-CAT1                            
           END-IF.                                                              
                                                                                
           IF K240-NUM-MAX-CAT2 NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-MAX-CAT2                            
           END-IF.                                                              
                                                                                
           IF K240-NUM-MAX-CAT3 NOT NUMERIC                                     
              MOVE ZEROES       TO K240-NUM-MAX-CAT3                            
           END-IF.                                                              
                                                                                
           ADD K240-NUM-TOTAL TO WS-NUM-TOTAL.                                  
           ADD K240-NUM-TOTAL-GEN3 TO WS-NUM-TOTAL-GEN3.                        
                                                                                
           COMPUTE WS-AVAIL-TOT  = K240-NUM-TOTAL                               
                                 - K240-NUM-TOT-CAT1                            
                                 - K240-NUM-TOT-CAT2                            
                                 - K240-NUM-TOT-CAT3.                           
                                                                                
           ADD WS-AVAIL-TOT     TO WS-AVAIL-TOTAL.                              
                                                                                
           COMPUTE WS-AVAIL-CAT  = K240-NUM-MAX-CAT1                            
                                 - K240-NUM-TOT-CAT1.                           
                                                                                
           IF WS-AVAIL-CAT > WS-AVAIL-TOT                                       
              MOVE WS-AVAIL-TOT TO WS-AVAIL-CAT                                 
           END-IF.                                                              
                                                                                
           ADD WS-AVAIL-CAT     TO WS-AVAIL-CAT1.                               
                                                                                
           IF WS-AVAIL-CAT > 0                                                  
              ADD WS-AVAIL-TOT  TO WS-NUM-UNIT-CAT1                             
           END-IF.                                                              
                                                                                
           COMPUTE WS-AVAIL-CAT  = K240-NUM-MAX-CAT2                            
                                 - K240-NUM-TOT-CAT2.                           
                                                                                
           IF WS-AVAIL-CAT > WS-AVAIL-TOT                                       
              MOVE WS-AVAIL-TOT TO WS-AVAIL-CAT                                 
           END-IF.                                                              
                                                                                
           ADD WS-AVAIL-CAT     TO WS-AVAIL-CAT2.                               
                                                                                
           IF WS-AVAIL-CAT > 0                                                  
              ADD WS-AVAIL-TOT  TO WS-NUM-UNIT-CAT2                             
           END-IF.                                                              
                                                                                
           COMPUTE WS-AVAIL-CAT  = K240-NUM-MAX-CAT3                            
                                 - K240-NUM-TOT-CAT3.                           
                                                                                
           IF WS-AVAIL-CAT > WS-AVAIL-TOT                                       
              MOVE WS-AVAIL-TOT TO WS-AVAIL-CAT                                 
           END-IF.                                                              
                                                                                
           ADD WS-AVAIL-CAT     TO WS-AVAIL-CAT3.                               
                                                                                
           IF WS-AVAIL-CAT > 0                                                  
              ADD WS-AVAIL-TOT  TO WS-NUM-UNIT-CAT3                             
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3600-READ-BP13K246.                                                      
      *-------------------*                                                     
                                                                                
           MOVE SPACES                     TO BP13K246-REC.                     
           INITIALIZE                         BP13K246-REC.                     
                                                                                
           MOVE WS-NUM-NEW-TOWN            TO K246-NUM-NEW-TOWN.                
           MOVE WS-NUM-FLAT-TYPE           TO K246-NUM-FLAT-TYPE.               
           MOVE WS-DTE-BALLOT              TO K246-DTE-BALLOT.                  
           MOVE SPACE                      TO WS-COMBINED-IND.                  
                                                                                
                                                                                
           READ BP13K246 KEY IS K246-KEY-FLD.                                   
                                                                                
           EVALUATE WS-K246-STATUS                                              
             WHEN 00                                                            
                IF K246-COMBINED-FT NOT = SPACES AND LOW-VALUES                 
                   MOVE 'Y'             TO WS-COMBINED-IND                      
                   ADD 1 TO WS-K246-READ-CNT                                    
                END-IF                                                          
             WHEN 23                                                            
                CONTINUE                                                        
             WHEN OTHER                                                         
                DISPLAY 'BP13K246 - READ ERROR: ' WS-K246-STATUS                
           END-EVALUATE.                                                        
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13C21O  *************'.                           
           DISPLAY 'RECS READ    FROM BP13F240 : ' WS-F240-READ-CNT.            
           DISPLAY 'RECS READ    FROM BP13K246 : ' WS-K246-READ-CNT.            
           DISPLAY '                              '.                            
           DISPLAY 'RECS REWRITTEN IN BP13K249 : ' WS-K249-REWRITE-CNT.         
           DISPLAY 'REWRITE ERRORS IN BP13K249 : ' WS-K249-REWRITE-ERR.         
                                                                                
           CLOSE BP13F240                                                       
                 BP13K246                                                       
                 BP13K249.                                                      
                                                                                
           IF WS-K246-STATUS NOT EQUAL 00                                       
              DISPLAY 'BP13K246 - CLOSE ERROR: ' WS-K246-STATUS                 
              MOVE WS-K246-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-K249-STATUS NOT EQUAL 00                                       
              DISPLAY 'BP13K249 - CLOSE ERROR: ' WS-K249-STATUS                 
              MOVE WS-K249-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
