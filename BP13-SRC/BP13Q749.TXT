       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13Q749.                                                    
       AUTHOR.     SURESH.                                                      
       INSTALLATION. HDB.                                                       
       DATE-WRITTEN. 03-02-1999.                                                
      *-----------------------------------------------------------*             
      *        SOC  - BP13                                        *             
      *-----------------------------------------------------------*             
      * OBJECTIVES :  TO EXTRACT RECS OF APPLICANT (WITH THAT OF  *             
      *               FAMILY MEMBERS) FORM RDB II USING NRIC      *             
      *                                                           *             
      * INPUT FILES   : BP13F716                                  *             
      *                 AB03K030                                  *             
      *                 BP13K060                                  *             
      *                                                           *             
      * DATABASE      : RDB2                                      *             
      *                                                           *             
      * OUTPUT FILE   : P13F716A                                  *             
      *                                                           *             
      * REQ NO.   DATE       CHG BY  DESCRIPTION                  *             
      * -------   ----       ------  -----------                  *             
      * BP130630 03-02-1999   SRN    NEW PROGRAM                  *             
      * BP130630 17071999     SRN    ADD IN ERROR REPORT          *             
      * BP130790 02022000     SCK    TO ADD IN IN-SOURCE DOCUMEN- *             
      *                              TATION                       *             
      * BP130825 07042000     SRN    OUTPUT MARITAL STATUS        *             
      * BP130825 27042000     SRN    READ CURSOR 2 WHEN -811      *             
      * BP136989 26072013     IMC1   FIX READING OF RNTL TABLE    *             
      *-----------------------------------------------------------*             
       ENVIRONMENT DIVISION.                                                    
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F716   ASSIGN TO BP13F716.                                
                                                                                
           SELECT P13F716A   ASSIGN TO P13F716A.                                
                                                                                
           SELECT AB03K030   ASSIGN TO AB03K030                                 
                             ORGANIZATION INDEXED                               
                             ACCESS MODE  RANDOM                                
                             RECORD KEY   K030-NUM-HDB-REF                      
                             FILE STATUS  WS-K030-STATUS.                       
                                                                                
           SELECT BP13K060   ASSIGN TO BP13K060                                 
                             ORGANIZATION INDEXED                               
                             ACCESS MODE  RANDOM                                
                             RECORD KEY   K060-KEY-FLD                          
                             FILE STATUS  WS-K060-STATUS.                       
                                                                                
                                                                                
           SELECT BP13L749   ASSIGN TO BP13L749.                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD BP13F716.                                                             
       COPY BP13F716.                                                           
                                                                                
       FD P13F716A.                                                             
       01 P13F716A-REC                  PIC X(350).                             
                                                                                
       FD AB03K030.                                                             
       COPY AB03K030.                                                           
                                                                                
       FD BP13K060.                                                             
       COPY BP13K060.                                                           
                                                                                
       FD BP13L749.                                                             
       01 BP13L749-REC                  PIC X(132).                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
           EXEC SQL INCLUDE P13RSROL  END-EXEC.                                 
                                                                                
           EXEC SQL INCLUDE P13IDNO   END-EXEC.                                 
                                                                                
           EXEC SQL INCLUDE P13RSDNT  END-EXEC.                                 
                                                                                
           EXEC SQL INCLUDE P13CSID   END-EXEC.                                 
                                                                                
           EXEC SQL INCLUDE P13RNTL   END-EXEC.                                 
                                                                                
           EXEC SQL INCLUDE P13PERSN  END-EXEC.                                 
                                                                                
           EXEC SQL INCLUDE SQLCA     END-EXEC.                                 
                                                                                
           EXEC SQL                                                             
             DECLARE CURSOR1 CURSOR FOR                                         
                SELECT                                                          
                    T1.NUM_RLTNSP,                                              
                    T2.NUM_CSTMR,                                               
                    T3.NUM_SCHEME,                                              
                    T3.NUM_ACNT,                                                
                    T3.NUM_TNCY,                                                
                    T3.NUM_ROLE,                                                
                    T3.DTE_ROLE_START,                                          
                    T4.NUM_MRTL_STATUS,                                         
                    T4.NUM_SEX                                                  
                FROM                                                            
                    RSDNT         T1,                                           
                    CSTMR_ID      T2,                                           
                    RSDNT_ROLE    T3,                                           
                    PERSONS       T4,                                           
                    RSDNT_ROLE    T5,                                           
                    CSTMR_ID      T6,                                           
                    IDNTY_NO      T7                                            
                WHERE                                                           
                  T1.NUM_CSTMR_SOURCE   = T3.NUM_CSTMR_SOURCE       AND         
                  T1.NUM_SCHEME         = T3.NUM_SCHEME             AND         
                  T1.NUM_ACNT           = T3.NUM_ACNT               AND         
                  T1.NUM_TNCY           = T3.NUM_TNCY               AND         
                  T4.NUM_CSTMR          = T2.NUM_CSTMR              AND         
                  T2.NUM_CSTMR_SOURCE   = T3.NUM_CSTMR_SOURCE       AND         
                  T3.NUM_SCHEME         = T5.NUM_SCHEME             AND         
                  T3.NUM_ACNT           = T5.NUM_ACNT               AND         
                  T3.NUM_TNCY           = T5.NUM_TNCY               AND         
                  T3.DTE_ROLE_END       = '0001-01-01'              AND         
                  T5.NUM_CSTMR_SOURCE   = T6.NUM_CSTMR_SOURCE       AND         
                  T5.DTE_ROLE_END       = '0001-01-01'              AND         
                  T6.NUM_CSTMR          = T7.NUM_CSTMR              AND         
                  T7.NUM_IDNTY_NO       = :DCLIDNTY-NO.NUM-IDNTY-NO AND         
                  T7.NUM_IDNTY_NO_TYPE  = :DCLIDNTY-NO.NUM-IDNTY-NO-TYPE        
           END-EXEC.                                                            
                                                                                
           EXEC SQL                                                             
             DECLARE CURSOR2 CURSOR FOR                                         
               SELECT                                                           
                 T1.NUM_IDNTY_NO,                                               
                 T1.NUM_IDNTY_NO_TYPE                                           
               FROM                                                             
                 IDNTY_NO       T1                                              
               WHERE                                                            
                 T1.NUM_CSTMR          = :DCLCSTMR-ID.NUM-CSTMR AND             
                 T1.NUM_IDNTY_NO_TYPE  = 'UIN'                                  
               UNION ALL                                                        
               SELECT                                                           
                 T1.NUM_IDNTY_NO,                                               
                 T1.NUM_IDNTY_NO_TYPE                                           
               FROM                                                             
                 IDNTY_NO     T1                                                
               WHERE                                                            
                 T1.NUM_CSTMR          = :DCLCSTMR-ID.NUM-CSTMR AND             
                 T1.NUM_IDNTY_NO_TYPE  = 'FIN'                                  
               UNION ALL                                                        
               SELECT                                                           
                 T1.NUM_IDNTY_NO,                                               
                 T1.NUM_IDNTY_NO_TYPE                                           
               FROM                                                             
                 IDNTY_NO     T1                                                
               WHERE                                                            
                 T1.NUM_CSTMR          = :DCLCSTMR-ID.NUM-CSTMR AND             
                 T1.NUM_IDNTY_NO_TYPE  NOT IN ('UIN','FIN')                     
           END-EXEC.                                                            
                                                                                
      *-------------------------------------------------------------*           
      * COMMAREA TO CALL AV02C013 FOR FINDING THE NRIC NO TYPE      *           
      *-------------------------------------------------------------*           
       01  WS-COM-AREA.                                                         
           05  WS-COM-NRIC             PIC X(9)    VALUE SPACES.                
           05  WS-COM-RFLAG            PIC X(1)    VALUE SPACES.                
           05  WS-COM-NUMBER-TYPE      PIC X(3)    VALUE SPACES.                
                                                                                
       01  WS-CONTROL-COUNT.                                                    
           05  WS-F716-READ            PIC 9(8)    VALUE ZEROS.                 
           05  WS-INVALID-UINFIN       PIC 9(8)    VALUE ZEROS.                 
           05  WS-RDB2-READ            PIC 9(8)    VALUE ZEROS.                 
           05  WS-RDB2-NFND            PIC 9(8)    VALUE ZEROS.                 
           05  WS-IDNTY-READ           PIC 9(8)    VALUE ZEROS.                 
           05  WS-IDNTY-NFND           PIC 9(8)    VALUE ZEROS.                 
           05  WS-K060-READ            PIC 9(8)    VALUE ZEROS.                 
           05  WS-K060-NFND            PIC 9(8)    VALUE ZEROS.                 
           05  WS-K030-READ            PIC 9(8)    VALUE ZEROS.                 
           05  WS-K030-NFND            PIC 9(8)    VALUE ZEROS.                 
           05  WS-RNTL-READ            PIC 9(8)    VALUE ZEROS.                 
           05  WS-RNTL-NFND            PIC 9(8)    VALUE ZEROS.                 
           05  WS-F716-WRITE           PIC 9(8)    VALUE ZEROS.                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-EOF-F717                 PIC X   VALUE 'N'.                   
               88 EOF-F717                         VALUE 'Y'.                   
           05  WS-K060-STATUS          PIC 9(2)    VALUE ZEROS.         .       
           05  WS-K030-STATUS          PIC 9(2)    VALUE ZEROS.         .       
                                                                                
       01  WS-EOC                      PIC X(1)    VALUE 'N'.                   
           88 END-OF-CURSOR                        VALUE 'Y'.                   
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-CSTMR            PIC X(9)    VALUE SPACES.                
           05  WS-APPLN-NRIC           PIC X(9)    VALUE SPACES.                
           05  WS-PRT-APPLN-NRIC       PIC X(9)    VALUE SPACES.                
           05  WS-REGN-NO              PIC X(8)    VALUE SPACES.                
           05  WS-NUM-HDBREF           PIC X(11)   VALUE SPACES.                
                                                                                
       01  WS-DTE-ACCEPTED             PIC X(8)    VALUE SPACES.                
                                                                                
       01  WS-DUP-REC                  PIC X(1)    VALUE 'N'.                   
                                                                                
       01  WS-SQLCODE                  PIC 9(4)    VALUE ZEROS.                 
                                                                                
       01  WS-PRT-SQLCODE              PIC -9(3).                               
                                                                                
       01  WS-SYS-DATE                 PIC X(8)    VALUE SPACES.                
                                                                                
       01  WS-PRT-CTR.                                                          
           05 WS-LINE-CTR              PIC 9(2)    VALUE 99.                    
           05 WS-SNO                   PIC 9(8)    VALUE ZEROS.                 
           05 WS-PAGE-CTR              PIC 9(4)    VALUE ZEROS.                 
           05 WS-EDT-SNO               PIC ZZZZ9.                               
                                                                                
                                                                                
       01  L749-PRT-HEAD1.                                                      
           05 FILLER                   PIC X(8)    VALUE 'BP13L749'.            
           05 FILLER                   PIC X(40)   VALUE SPACES.                
           05 FILLER                   PIC X(20)   VALUE                        
              'SYSTEM OF COMMITMENT'.                                           
           05 FILLER                   PIC X(25)   VALUE SPACES.                
           05 FILLER                   PIC X(7)    VALUE 'DATE : '.             
           05 L749-PRT-DATE            PIC X(10)   VALUE SPACES.                
           05 FILLER                   PIC X(4)    VALUE SPACES.                
           05 FILLER                   PIC X(7)    VALUE 'PAGE : '.             
           05 L749-PRT-PAGE            PIC ZZZ9.                                
                                                                                
       01  L749-PRT-HEAD2.                                                      
           05 FILLER                   PIC X(33)   VALUE SPACES.                
           05 FILLER                   PIC X(55)   VALUE                        
              'ERROR REPORT ON PEOPLE WITH SAME IDNTY-NO FOUND IN CDB'.         
                                                                                
       01  L749-PRT-HEAD3.                                                      
           05 FILLER                   PIC X(5)    VALUE 'SNO  '.               
           05 FILLER                   PIC X(3)    VALUE SPACES.                
           05 FILLER                   PIC X(9)    VALUE 'REGN-NO'.             
           05 FILLER                   PIC X(3)    VALUE SPACES.                
           05 FILLER                   PIC X(11)   VALUE 'APPLN-NRIC'.          
           05 FILLER                   PIC X(3)    VALUE SPACES.                
           05 FILLER                   PIC X(14)   VALUE 'HDBREF'.              
           05 FILLER                   PIC X(3)    VALUE SPACES.                
           05 FILLER                   PIC X(9)    VALUE 'NUM-CSTMR'.           
           05 FILLER                   PIC X(3)    VALUE SPACES.                
           05 FILLER                   PIC X(9)    VALUE 'IDNTY_NO'.            
           05 FILLER                   PIC X(3)    VALUE SPACES.                
           05 FILLER                   PIC X(13)   VALUE 'NAME'.                
                                                                                
       01  L749-PRT-LINE               PIC X(131)  VALUE ALL '_'.               
                                                                                
       01  L749-PRT-DETAIL.                                                     
           05 L749-PRT-SNO             PIC X(5)    VALUE SPACES.                
           05 FILLER                   PIC X(3)    VALUE SPACES.                
           05 L749-REGN-NO             PIC X(8)    VALUE SPACES.                
           05 FILLER                   PIC X(4)    VALUE SPACES.                
           05 L749-APPLN-NRIC          PIC X(9)    VALUE SPACES.                
           05 FILLER                   PIC X(5)    VALUE SPACES.                
           05 L749-HDBREF              PIC X(14)   VALUE SPACES.                
           05 FILLER                   PIC X(3)    VALUE SPACES.                
           05 L749-NUM-CSTMR           PIC X(9)    VALUE SPACES.                
           05 FILLER                   PIC X(3)    VALUE SPACES.                
           05 L749-IDNTY-NO            PIC X(9)    VALUE SPACES.                
           05 FILLER                   PIC X(3)    VALUE SPACES.                
           05 L749-NAME                PIC X(66)   VALUE SPACES.                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
       0000-MAIN.                                                               
                                                                                
            PERFORM 0500-OPEN-FILES        THRU 0500-EXIT.                      
                                                                                
            PERFORM 1000-READ-BP13F716     THRU 1000-EXIT.                      
                                                                                
            PERFORM 2000-RETRIEVE-RDBINFO  THRU 2000-EXIT                       
                    UNTIL EOF-F717.                                             
                                                                                
            PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                      
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       0500-OPEN-FILES.                                                         
            OPEN INPUT  BP13F716                                                
                        BP13K060                                                
                        AB03K030                                                
                 OUTPUT P13F716A                                                
                        BP13L749.                                               
                                                                                
            MOVE FUNCTION CURRENT-DATE(1:8)   TO WS-SYS-DATE.                   
                                                                                
            STRING WS-SYS-DATE(7:2) DELIMITED BY SIZE,                          
                   '-'                                                          
                   WS-SYS-DATE(5:2) DELIMITED BY SIZE,                          
                   '-'                                                          
                   WS-SYS-DATE(1:4) DELIMITED BY SIZE                           
                   INTO                                                         
                   L749-PRT-DATE.                                               
                                                                                
            IF WS-K060-STATUS NOT = 0 AND 97                                    
               DISPLAY 'ERROR OPENING BP13K060 '                                
               DISPLAY 'FILE-STATUS :- ' WS-K060-STATUS                         
               MOVE WS-K060-STATUS            TO RETURN-CODE                    
               GO TO 9000-CLOSE-FILES                                           
            END-IF.                                                             
                                                                                
            IF WS-K030-STATUS NOT = 0 AND 97                                    
               DISPLAY 'ERROR OPENING AB03K030 '                                
               DISPLAY 'FILE-STATUS :- ' WS-K030-STATUS                         
               MOVE WS-K030-STATUS            TO RETURN-CODE                    
               GO TO 9000-CLOSE-FILES                                           
            END-IF.                                                             
                                                                                
       0500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-READ-BP13F716.                                                      
            READ BP13F716 AT END MOVE 'Y'     TO WS-EOF-F717                    
                 GO TO 1000-EXIT.                                               
                                                                                
            MOVE F716-NUM-NRIC                TO WS-APPLN-NRIC.                 
                                                                                
            ADD 1 TO WS-F716-READ.                                              
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2000-RETRIEVE-RDBINFO.                                                   
            MOVE SPACES                  TO WS-COM-AREA.                        
                                                                                
            MOVE F716-NUM-NRIC           TO WS-COM-NRIC                         
                                                                                
            IF WS-COM-NRIC(1:1) = 'X'                                           
               MOVE 'XIN'                TO WS-COM-NUMBER-TYPE                  
               MOVE 'Y'                  TO WS-COM-RFLAG                        
            ELSE                                                                
               CALL 'AV02C013'           USING WS-COM-NRIC,                     
                                               WS-COM-RFLAG,                    
                                               WS-COM-NUMBER-TYPE               
            END-IF.                                                             
                                                                                
            IF WS-COM-RFLAG = 'Y'                                               
               PERFORM 3000-READ-RDB     THRU 3000-EXIT                         
            ELSE                                                                
               ADD 1                     TO WS-INVALID-UINFIN                   
            END-IF.                                                             
                                                                                
            PERFORM 1000-READ-BP13F716   THRU 1000-EXIT.                        
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
       3000-READ-RDB.                                                           
            MOVE 'N'                             TO WS-EOC                      
                                                    WS-DUP-REC.                 
                                                                                
            MOVE WS-COM-NUMBER-TYPE              TO NUM-IDNTY-NO-TYPE           
                                                                                
            IF WS-COM-NUMBER-TYPE = 'XIN'                                       
               MOVE F716-NUM-NRIC(2:8)           TO NUM-IDNTY-NO                
            ELSE                                                                
               MOVE F716-NUM-NRIC                TO NUM-IDNTY-NO                
            END-IF                                                              
                                                                                
            EXEC SQL                                                            
               SELECT                                                           
                    NUM_CSTMR                                                   
               INTO                                                             
                   :WS-NUM-CSTMR                                                
               FROM                                                             
                 IDNTY_NO                                                       
               WHERE                                                            
                 NUM_IDNTY_NO       = :DCLIDNTY-NO.NUM-IDNTY-NO  AND            
                 NUM_IDNTY_NO_TYPE  = :DCLIDNTY-NO.NUM-IDNTY-NO-TYPE            
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
              WHEN 0                                                            
                   CONTINUE                                                     
              WHEN 100                                                          
                   ADD 1    TO WS-RDB2-NFND                                     
                   GO TO 3000-EXIT                                              
              WHEN -811                                                         
      **-----------------------------------------------------------**           
      **  THIS CONDITION CAN HAPPEN ONLY FOR NON UIN FIN IDNTY_NOS **           
      **  IDNTY_NO TYPE WILL NOT BE 'UIN' OR 'FIN'                 **           
      **-----------------------------------------------------------**           
                   MOVE SPACES           TO WS-NUM-CSTMR                        
                   DISPLAY '************************************ '              
                   DISPLAY 'DUPLICATE RECORD FOUND IN IDNTY_NO '                
                   DISPLAY 'IDNTY NO      :- ' F716-NUM-NRIC                    
                   DISPLAY 'IDNTY NO TYPE :- ' WS-COM-NUMBER-TYPE               
                   DISPLAY '************************************ '              
                   MOVE 'Y'              TO WS-DUP-REC                          
              WHEN OTHER                                                        
                   MOVE SQLCODE          TO WS-SQLCODE                          
                                            WS-PRT-SQLCODE                      
                   DISPLAY 'ERROR READING RDBII'                                
                   DISPLAY 'SQLCODE :- ' WS-PRT-SQLCODE                         
                   MOVE WS-SQLCODE       TO RETURN-CODE                         
                   GO TO 9000-CLOSE-FILES                                       
            END-EVALUATE.                                                       
                                                                                
            PERFORM 3100-OPEN-CURSOR     THRU 3100-EXIT                         
                                                                                
            PERFORM 3200-FETCH-CURSOR    THRU 3200-EXIT                         
                                                                                
            IF END-OF-CURSOR                                                    
               ADD 1    TO WS-RDB2-NFND                                         
            END-IF.                                                             
                                                                                
            PERFORM 3200-FETCH-CURSOR    THRU 3200-EXIT                         
                    UNTIL END-OF-CURSOR.                                        
                                                                                
            PERFORM 3300-CLOSE-CURSOR    THRU 3300-EXIT.                        
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3100-OPEN-CURSOR.                                                        
            EXEC SQL                                                            
              OPEN CURSOR1                                                      
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
              WHEN 0                                                            
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   MOVE SQLCODE          TO WS-SQLCODE                          
                                            WS-PRT-SQLCODE                      
                   DISPLAY 'ERROR OPENING CURSOR1 '                             
                   DISPLAY 'SQLCODE :- ' WS-PRT-SQLCODE                         
                   MOVE WS-SQLCODE       TO RETURN-CODE                         
                   GO TO 9000-CLOSE-FILES                                       
            END-EVALUATE.                                                       
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3200-FETCH-CURSOR.                                                       
            EXEC SQL                                                            
              FETCH CURSOR1                                                     
                INTO                                                            
                  :DCLRSDNT.NUM-RLTNSP,                                         
                  :DCLCSTMR-ID.NUM-CSTMR,                                       
                  :DCLRSDNT-ROLE.NUM-SCHEME,                                    
                  :DCLRSDNT-ROLE.NUM-ACNT,                                      
                  :DCLRSDNT-ROLE.NUM-TNCY,                                      
                  :DCLRSDNT-ROLE.NUM-ROLE,                                      
                  :DCLRSDNT-ROLE.DTE-ROLE-START,                                
                  :DCLPERSONS.NUM-MRTL-STATUS,                                  
                  :DCLPERSONS.NUM-SEX                                           
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
              WHEN 0                                                            
                ADD 1    TO WS-RDB2-READ                                        
                PERFORM 4000-WRITE-P13F716A  THRU 4000-EXIT                     
              WHEN 100                                                          
                 MOVE 'Y'                    TO WS-EOC                          
              WHEN OTHER                                                        
                 MOVE SQLCODE          TO WS-SQLCODE                            
                                          WS-PRT-SQLCODE                        
                 DISPLAY 'ERROR READING RDBII'                                  
                 DISPLAY 'SQLCODE :- ' WS-PRT-SQLCODE                           
                 MOVE WS-SQLCODE       TO RETURN-CODE                           
                 GO TO 9000-CLOSE-FILES                                         
            END-EVALUATE.                                                       
       3200-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3300-CLOSE-CURSOR.                                                       
            EXEC SQL                                                            
              CLOSE CURSOR1                                                     
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
              WHEN 0                                                            
                CONTINUE                                                        
              WHEN OTHER                                                        
                 MOVE SQLCODE          TO WS-SQLCODE                            
                                          WS-PRT-SQLCODE                        
                 DISPLAY 'ERROR CLOSING CURSOR1 '                               
                 DISPLAY 'SQLCODE :- ' WS-PRT-SQLCODE                           
                 MOVE WS-SQLCODE       TO RETURN-CODE                           
                 GO TO 9000-CLOSE-FILES                                         
            END-EVALUATE.                                                       
       3300-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4000-WRITE-P13F716A.                                                     
            STRING NUM-SCHEME OF DCLRSDNT-ROLE,                                 
                   NUM-ACNT   OF DCLRSDNT-ROLE,                                 
                   NUM-TNCY   OF DCLRSDNT-ROLE DELIMITED BY SIZE                
                   INTO                                                         
                   F716-NUM-HDB-REF                                             
                                                                                
      **---------------------------------------------------------**             
      ** THIS IS TO CATER FOR CHANGE IN IDNTY_NO                 **             
      ** EG: FIN -> UIN OR XIN -> FIN, XIN                       **             
      **---------------------------------------------------------**             
                                                                                
            IF NUM-CSTMR OF DCLCSTMR-ID  = WS-NUM-CSTMR                         
               MOVE WS-COM-NRIC               TO F716-NUM-NRIC                  
            ELSE                                                                
               PERFORM 5000-READ-IDNTY        THRU 5000-EXIT                    
            END-IF.                                                             
                                                                                
            IF F716-NUM-HDB-REF NOT = WS-NUM-HDBREF                             
                                                                                
               IF F716-NUM-HDB-REF < '70000000000'                              
                  PERFORM 6000-READ-RNTL      THRU 6000-EXIT                    
               ELSE                                                             
                  PERFORM 6500-READ-AB03K030  THRU 6500-EXIT                    
               END-IF                                                           
                                                                                
               MOVE F716-NUM-HDB-REF          TO WS-NUM-HDBREF                  
            END-IF.                                                             
                                                                                
            MOVE WS-DTE-ACCEPTED              TO F716-DTE-ACCEPTED              
                                                                                
            IF DTE-ROLE-START OF DCLRSDNT-ROLE = '0001-01-01'                   
               MOVE SPACES                    TO F716-DTE-MOVING-IN             
            ELSE                                                                
               STRING DTE-ROLE-START OF DCLRSDNT-ROLE(1:4)                      
                      DTE-ROLE-START OF DCLRSDNT-ROLE(6:2)                      
                      DTE-ROLE-START OF DCLRSDNT-ROLE(9:2)                      
                                     DELIMITED BY SIZE,                         
                      INTO                                                      
                      F716-DTE-MOVING-IN                                        
            END-IF.                                                             
                                                                                
            MOVE NUM-ROLE  OF DCLRSDNT-ROLE   TO F716-NUM-ROLE                  
                                                                                
                                                                                
            MOVE SPACES                       TO BP13K060-REC                   
            MOVE '50'                         TO K060-SERIAL-NO                 
            MOVE NUM-ROLE  OF DCLRSDNT-ROLE   TO K060-CODE                      
            PERFORM 4500-READ-BP13K060        THRU 4500-EXIT                    
            MOVE K060-DESC1(1:1)              TO F716-CDE-OCCUPIER              
                                                                                
            MOVE SPACES                       TO BP13K060-REC                   
            MOVE '49'                         TO K060-SERIAL-NO                 
            IF NUM-RLTNSP = '01' OR '21'                                        
               STRING NUM-RLTNSP,  NUM-SEX                                      
                      DELIMITED BY SIZE                                         
                      INTO                                                      
                      K060-CODE                                                 
            ELSE                                                                
               MOVE NUM-RLTNSP OF DCLRSDNT    TO K060-CODE                      
            END-IF.                                                             
                                                                                
            PERFORM 4500-READ-BP13K060        THRU 4500-EXIT                    
            MOVE K060-DESC1(1:2)              TO F716-CDE-RELATIONSHIP          
                                                                                
            EVALUATE NUM-MRTL-STATUS                                            
              WHEN 'S'                                                          
                MOVE '1'                      TO                                
                                          F716-NUM-RDB-MARITAL-STATUS           
              WHEN 'M'                                                          
                MOVE '2'                      TO                                
                                          F716-NUM-RDB-MARITAL-STATUS           
              WHEN 'W'                                                          
                MOVE '3'                      TO                                
                                          F716-NUM-RDB-MARITAL-STATUS           
              WHEN 'D'                                                          
                MOVE '4'                      TO                                
                                          F716-NUM-RDB-MARITAL-STATUS           
              WHEN 'P'                                                          
                MOVE '5'                      TO                                
                                          F716-NUM-RDB-MARITAL-STATUS           
            END-EVALUATE.                                                       
                                                                                
            WRITE P13F716A-REC FROM BP13F716-REC.                               
            ADD 1   TO WS-F716-WRITE.                                           
                                                                                
            IF WS-DUP-REC = 'Y'                                                 
               PERFORM 7000-PRINT-ERROR-RPT     THRU 7000-EXIT                  
            END-IF.                                                             
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4500-READ-BP13K060.                                                      
            READ BP13K060.                                                      
                                                                                
            EVALUATE WS-K060-STATUS                                             
              WHEN 0                                                            
                ADD 1                        TO WS-K060-READ                    
              WHEN 23                                                           
                ADD 1                        TO WS-K060-NFND                    
                MOVE SPACES                  TO K060-DESC1                      
                DISPLAY 'ERROR !! RECORD NOT FOUND IN BP13K060 '                
                DISPLAY 'K060-KEY-FLD :- ' K060-KEY-FLD                         
              WHEN OTHER                                                        
                DISPLAY 'ERROR READING BP13K060 '                               
                DISPLAY 'FILE-STATUS :- ' WS-K060-STATUS                        
                MOVE WS-K060-STATUS          TO RETURN-CODE                     
                GO TO 9000-CLOSE-FILES                                          
            END-EVALUATE.                                                       
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       5000-READ-IDNTY.                                                         
            EXEC SQL                                                            
               SELECT                                                           
                 NUM_IDNTY_NO,                                                  
                 NUM_IDNTY_NO_TYPE                                              
               INTO                                                             
                 :DCLIDNTY-NO.NUM-IDNTY-NO,                                     
                 :DCLIDNTY-NO.NUM-IDNTY-NO-TYPE                                 
               FROM                                                             
                 IDNTY_NO                                                       
               WHERE                                                            
                 NUM_CSTMR = :DCLCSTMR-ID.NUM-CSTMR                             
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
              WHEN 0                                                            
                ADD 1                            TO WS-IDNTY-READ               
                IF NUM-IDNTY-NO-TYPE = 'UIN' OR 'FIN'                           
                   MOVE NUM-IDNTY-NO             TO F716-NUM-NRIC               
                ELSE                                                            
                   STRING 'X' , NUM-IDNTY-NO                                    
                          DELIMITED BY SIZE                                     
                          INTO                                                  
                          F716-NUM-NRIC                                         
                END-IF                                                          
              WHEN -811                                                         
                PERFORM 5100-PROCESS-CURSOR2    THRU 5100-EXIT                  
              WHEN 100                                                          
                ADD 1                            TO WS-IDNTY-NFND               
              WHEN OTHER                                                        
                 MOVE SQLCODE          TO WS-SQLCODE                            
                                          WS-PRT-SQLCODE                        
                 DISPLAY 'ERROR READING IDNTY_NO'                               
                 DISPLAY 'SQLCODE :- ' WS-PRT-SQLCODE                           
                 MOVE WS-SQLCODE       TO RETURN-CODE                           
                 GO TO 9000-CLOSE-FILES                                         
            END-EVALUATE.                                                       
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       5100-PROCESS-CURSOR2.                                                    
            PERFORM 5110-OPEN-CURSOR2        THRU 5110-EXIT                     
                                                                                
            PERFORM 5120-FETCH-CURSOR2       THRU 5120-EXIT                     
                                                                                
            PERFORM 5130-CLOSE-CURSOR2       THRU 5130-EXIT.                    
       5100-EXIT.                                                               
            EXIT.                                                               
                                                                                
       5110-OPEN-CURSOR2.                                                       
            EXEC SQL                                                            
                OPEN CURSOR2                                                    
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
              WHEN 0                                                            
                CONTINUE                                                        
              WHEN OTHER                                                        
                MOVE SQLCODE          TO WS-SQLCODE                             
                                         WS-PRT-SQLCODE                         
                DISPLAY 'ERROR READING CURSOR2 '                                
                DISPLAY 'SQLCODE :- ' WS-PRT-SQLCODE                            
                MOVE WS-SQLCODE       TO RETURN-CODE                            
                GO TO 9000-CLOSE-FILES                                          
            END-EVALUATE.                                                       
                                                                                
       5110-EXIT.                                                               
            EXIT.                                                               
                                                                                
       5120-FETCH-CURSOR2.                                                      
            EXEC SQL                                                            
                FETCH CURSOR2                                                   
                  INTO :DCLIDNTY-NO.NUM-IDNTY-NO,                               
                       :DCLIDNTY-NO.NUM-IDNTY-NO-TYPE                           
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
              WHEN 0                                                            
                ADD 1                            TO WS-IDNTY-READ               
                IF NUM-IDNTY-NO-TYPE = 'UIN' OR 'FIN'                           
                   MOVE NUM-IDNTY-NO             TO F716-NUM-NRIC               
                ELSE                                                            
                   STRING 'X' , NUM-IDNTY-NO                                    
                          DELIMITED BY SIZE                                     
                          INTO                                                  
                          F716-NUM-NRIC                                         
                END-IF                                                          
              WHEN 100                                                          
                ADD 1                            TO WS-IDNTY-NFND               
              WHEN OTHER                                                        
                MOVE SQLCODE          TO WS-SQLCODE                             
                                         WS-PRT-SQLCODE                         
                DISPLAY 'ERROR READING CURSOR2 '                                
                DISPLAY 'SQLCODE :- ' WS-PRT-SQLCODE                            
                MOVE WS-SQLCODE       TO RETURN-CODE                            
                GO TO 9000-CLOSE-FILES                                          
            END-EVALUATE.                                                       
                                                                                
       5120-EXIT.                                                               
            EXIT.                                                               
                                                                                
       5130-CLOSE-CURSOR2.                                                      
                                                                                
            EXEC SQL                                                            
                CLOSE CURSOR2                                                   
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
              WHEN 0                                                            
                CONTINUE                                                        
              WHEN OTHER                                                        
                 MOVE SQLCODE          TO WS-SQLCODE                            
                                          WS-PRT-SQLCODE                        
                 DISPLAY 'ERROR CLOSING CURSOR2 '                               
                 DISPLAY 'SQLCODE :- ' WS-PRT-SQLCODE                           
                 MOVE WS-SQLCODE       TO RETURN-CODE                           
                 GO TO 9000-CLOSE-FILES                                         
            END-EVALUATE.                                                       
                                                                                
       5130-EXIT.                                                               
            EXIT.                                                               
                                                                                
       6000-READ-RNTL.                                                          
                                                                                
            MOVE NUM-SCHEME OF DCLRSDNT-ROLE     TO                             
                                            NUM-SCHEME OF DCLRNTL               
            MOVE NUM-ACNT   OF DCLRSDNT-ROLE     TO                             
                                            NUM-ACNT OF DCLRNTL                 
            MOVE NUM-TNCY   OF DCLRSDNT-ROLE     TO                             
                                            NUM-TNCY OF DCLRNTL                 
                                                                                
            EXEC SQL                                                            
                 SELECT                                                         
                    DTE_CMCMNT,                                                 
                    NUM_HSNG_SCHEME                                             
                 INTO                                                           
                    :DTE-CMCMNT,                                                
                    :NUM-HSNG-SCHEME                                            
                 FROM                                                           
                    RNTL                                                        
                 WHERE                                                          
                    NUM_SCHEME = :DCLRNTL.NUM-SCHEME   AND                      
                    NUM_ACNT   = :DCLRNTL.NUM-ACNT     AND                      
                    NUM_TNCY   = :DCLRNTL.NUM-TNCY                              
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
               WHEN 0                                                           
                 IF DTE-CMCMNT = '0001-01-01'                                   
                    MOVE SPACES           TO WS-DTE-ACCEPTED                    
                 ELSE                                                           
                    STRING DTE-CMCMNT(1:4),                                     
                           DTE-CMCMNT(6:2),                                     
                           DTE-CMCMNT(9:2)                                      
                           DELIMITED BY SIZE                                    
                           INTO                                                 
                           WS-DTE-ACCEPTED                                      
                 END-IF                                                         
                 MOVE NUM-HSNG-SCHEME  OF DCLRNTL  TO                           
                                          F716-NUM-HSNG-SCH-RENT                
                                                                                
                 ADD 1                    TO WS-RNTL-READ                       
               WHEN 100                                                         
                 DISPLAY 'SQL NTFND '                                           
                 ADD 1                    TO WS-RNTL-NFND                       
                 MOVE SPACES              TO WS-DTE-ACCEPTED                    
               WHEN OTHER                                                       
                 MOVE SQLCODE          TO WS-SQLCODE                            
                                          WS-PRT-SQLCODE                        
                 DISPLAY 'ERROR READING RNTL '                                  
                 DISPLAY 'SQLCODE : ' WS-PRT-SQLCODE                            
                 MOVE WS-SQLCODE          TO RETURN-CODE                        
                 GO TO 9000-CLOSE-FILES                                         
            END-EVALUATE.                                                       
       6000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       6500-READ-AB03K030.                                                      
            MOVE F716-NUM-HDB-REF             TO K030-NUM-HDB-REF               
                                                                                
            READ AB03K030.                                                      
                                                                                
            EVALUATE WS-K030-STATUS                                             
              WHEN 0                                                            
                ADD 1                         TO WS-K030-READ                   
                                                                                
                IF K030-DTE-EXE-SALE-AGRMT = SPACES OR LOW-VALUES               
                   MOVE K030-DTE-SALE-EFF       TO WS-DTE-ACCEPTED              
                ELSE                                                            
                   MOVE K030-DTE-EXE-SALE-AGRMT TO WS-DTE-ACCEPTED              
                END-IF                                                          
                                                                                
              WHEN 23                                                           
                ADD 1                         TO WS-K030-NFND                   
                MOVE SPACES                   TO WS-DTE-ACCEPTED                
              WHEN OTHER                                                        
                DISPLAY 'ERROR READING AB03K030'                                
                DISPLAY 'FILE-STATUS :- ' WS-K030-STATUS                        
                MOVE WS-K030-STATUS           TO RETURN-CODE                    
                GO TO 9000-CLOSE-FILES                                          
            END-EVALUATE.                                                       
       6500-EXIT.                                                               
            EXIT.                                                               
                                                                                
       7000-PRINT-ERROR-RPT.                                                    
            EXEC SQL                                                            
               SELECT                                                           
                 NME_PERSON                                                     
               INTO                                                             
                 :DCLPERSONS.NME-PERSON                                         
               FROM                                                             
                 PERSONS                                                        
               WHERE                                                            
                 NUM_CSTMR = :DCLCSTMR-ID.NUM-CSTMR                             
            END-EXEC.                                                           
                                                                                
            EVALUATE SQLCODE                                                    
              WHEN 0                                                            
                MOVE NME-PERSON         TO L749-NAME                            
              WHEN 100                                                          
      ** THIS CONDITION WILL NOT ARISE                                          
                MOVE SPACES             TO L749-NAME                            
              WHEN OTHER                                                        
                MOVE SQLCODE            TO WS-SQLCODE                           
                                           WS-PRT-SQLCODE                       
                DISPLAY 'ERROR READING PERSONS '                                
                DISPLAY 'SQLCODE : ' WS-PRT-SQLCODE                             
                MOVE WS-SQLCODE         TO RETURN-CODE                          
                GO TO 9000-CLOSE-FILES                                          
            END-EVALUATE.                                                       
                                                                                
            STRING NUM-SCHEME OF DCLRSDNT-ROLE,      '-',                       
                   NUM-ACNT   OF DCLRSDNT-ROLE(1:4), '-',                       
                   NUM-ACNT   OF DCLRSDNT-ROLE(5:1), '-',                       
                   NUM-TNCY   OF DCLRSDNT-ROLE                                  
                   DELIMITED BY SIZE                                            
                   INTO                                                         
                   L749-HDBREF.                                                 
                                                                                
            MOVE NUM-IDNTY-NO             TO L749-IDNTY-NO                      
                                                                                
            MOVE NUM-CSTMR OF DCLCSTMR-ID TO L749-NUM-CSTMR                     
                                                                                
                                                                                
            IF WS-LINE-CTR > 55                                                 
               ADD 1                      TO WS-PAGE-CTR                        
               MOVE WS-PAGE-CTR           TO L749-PRT-PAGE                      
               WRITE BP13L749-REC         FROM L749-PRT-HEAD1 AFTER PAGE        
               WRITE BP13L749-REC         FROM L749-PRT-HEAD2 AFTER 2           
               WRITE BP13L749-REC         FROM L749-PRT-HEAD3 AFTER 2           
               WRITE BP13L749-REC         FROM L749-PRT-LINE                    
               MOVE SPACES                TO BP13L749-REC                       
               WRITE BP13L749-REC                                               
               MOVE 7                     TO WS-LINE-CTR                        
            END-IF.                                                             
                                                                                
            IF F716-NUM-REGN = WS-REGN-NO AND                                   
               WS-APPLN-NRIC = WS-PRT-APPLN-NRIC                                
               MOVE SPACES                TO L749-PRT-SNO                       
                                             L749-APPLN-NRIC                    
                                             L749-REGN-NO                       
            ELSE                                                                
               ADD 1                      TO WS-SNO                             
               MOVE WS-SNO                TO WS-EDT-SNO                         
               MOVE WS-EDT-SNO            TO L749-PRT-SNO                       
               MOVE F716-NUM-REGN         TO L749-REGN-NO                       
                                             WS-REGN-NO                         
               MOVE WS-APPLN-NRIC         TO L749-APPLN-NRIC                    
                                             WS-PRT-APPLN-NRIC                  
               IF WS-LINE-CTR > 7                                               
                  MOVE SPACES                TO BP13L749-REC                    
                  WRITE BP13L749-REC                                            
                  ADD 1                      TO WS-LINE-CTR                     
               END-IF                                                           
                                                                                
            END-IF.                                                             
            ADD 1                         TO WS-LINE-CTR                        
                                                                                
            WRITE BP13L749-REC            FROM L749-PRT-DETAIL.                 
                                                                                
       7000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       9000-CLOSE-FILES.                                                        
                                                                                
            DISPLAY '   '.                                                      
            DISPLAY 'SUMMARY REPORT   BP13T749   RUNDATE ' L749-PRT-DATE        
            DISPLAY '______________________________________________'.           
            DISPLAY '   '.                                                      
            DISPLAY 'NO OF RECORDS READ FROM BP13F716       : '                 
                                                WS-F716-READ                    
            DISPLAY 'NO OF RECORDS WITH INVALID UINFIN      : '                 
                                                WS-INVALID-UINFIN               
            DISPLAY 'NO OF RECORDS READ FROM RDB II         : '                 
                                                WS-RDB2-READ                    
            DISPLAY 'NO OF RECORDS NOT FOUND IN RDB II      : '                 
                                                WS-RDB2-NFND                    
            DISPLAY 'NO OF RECORDS READ FROM IDNTY_NO       : '                 
                                                WS-IDNTY-READ                   
            DISPLAY 'NO OF RECORDS NOT FOUND IN IDNTY_NO    : '                 
                                                WS-IDNTY-NFND                   
            DISPLAY 'NO OF RECORDS READ FROM AB03K030       : '                 
                                                WS-K030-READ                    
            DISPLAY 'NO OF RECORDS NOT FOUND IN AB03K030    : '                 
                                                WS-K030-NFND                    
            DISPLAY "NO OF RECORDS READ FROM RNTL D'BASE    : "                 
                                                WS-RNTL-READ                    
            DISPLAY "NO OF RECORDS NOT FOUND IN RNTL D'BASE : "                 
                                                WS-RNTL-NFND                    
            DISPLAY 'NO OF RECORDS READ FROM BP13K060       : '                 
                                                WS-K060-READ                    
            DISPLAY 'NO OF RECORDS NOT FOUND IN BP13K060    : '                 
                                                WS-K060-NFND                    
            DISPLAY 'NO OF RECORDS WRITTEN TO BP17F716      : '                 
                                                WS-F716-WRITE                   
            DISPLAY 'NO OF RECORDS PRINTED                  : '                 
                                                WS-SNO                          
                                                                                
            CLOSE BP13F716                                                      
                  BP13K060                                                      
                  AB03K030                                                      
                  P13F716A                                                      
                  BP13L749.                                                     
                                                                                
            IF WS-K060-STATUS NOT = 0                                           
               DISPLAY 'ERROR CLOSING BP13K060 '                                
               DISPLAY 'FILE-STATUS :- ' WS-K060-STATUS                         
               MOVE WS-K060-STATUS       TO RETURN-CODE                         
            END-IF.                                                             
                                                                                
            IF WS-K030-STATUS NOT = 0                                           
               DISPLAY 'ERROR CLOSING AB03K030 '                                
               DISPLAY 'FILE-STATUS :- ' WS-K030-STATUS                         
               MOVE WS-K030-STATUS       TO RETURN-CODE                         
            END-IF.                                                             
                                                                                
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *9900-ABORT-PROCESS.                                                      
      *     CALL 'DBATIAR' USING SQLCA.                                         
      *     PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT.                         
                                                                                
