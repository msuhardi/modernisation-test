       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C994.                                                 
      *AUTHOR.        LLOYD CAPARRO.                                            
      *                                                                         
      ************************************************************              
      *                   SOC SYSTEM (SOC)                       *              
      *==========================================================*              
      * DATE WRITTEN      : 20 FEB 2005                          *              
      *                                                          *              
      * 1. OBJECTIVE      : CREATE 2 FILES FROM BP13F595 WITH    *              
      *                   : FOUND AND NOT FOUND CASES IN         *              
      *                   : BB24K020 FILE                        *              
      *                                                          *              
      * 2. INPUT          : BP13F595 - BP13.F595.C995.NOTFOUND   *              
      *                   : BB24K020 - BB24.K020.OCCUPIER        *              
      *                                                          *              
      * 3. OUTPUT         : P13F595F - BP13.F595.C994.FOUND      *              
      *                   : P13F595N - BP13.F595.C994.NOTFOUND   *              
      *==========================================================*              
      *  MODIFICATIONS:                                          *              
      *                                                          *              
      *  CHGE-NO  DATE       OIC  DESCRIPTION                    *              
      *  -------- ---------- ---- ------------------------------ *              
      *  BP132667 21/02/2005 CG7  NEW PROGRAM.                   *              
      *  BP134255 15/06/2011 PCL3 TO ADD CHECK ON NRIC           *              
      ************************************************************              
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F595     ASSIGN        TO BP13F595.                       
                                                                                
           SELECT BB24K020     ASSIGN        TO BB24K020                        
                               ACCESS MODE   IS DYNAMIC                         
                               ORGANIZATION  IS INDEXED                         
                               RECORD KEY    IS K020-KEY-FLD                    
                               ALTERNATE RECORD KEY IS K020-NUM-UIN             
                                                WITH DUPLICATES                 
                               FILE STATUS   IS WS-K020-STATUS.                 
                                                                                
           SELECT P13F595F     ASSIGN        TO P13F595F.                       
                                                                                
           SELECT P13F595N     ASSIGN        TO P13F595N.                       
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F595                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       COPY BP13F595.                                                           
                                                                                
       FD  BB24K020                                                             
                               RECORD CONTAINS 200 CHARACTERS.                  
       COPY BB24K020.                                                           
                                                                                
       FD  P13F595F                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F595F-REC                  PIC X(500).                            
                                                                                
       FD  P13F595N                                                             
                               BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               RECORDING MODE IS F                              
                               LABEL RECORD IS STANDARD.                        
       01  P13F595N-REC                  PIC X(500).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  FILE-STATUS.                                                         
           05  WS-K020-STATUS            PIC 9(2)  VALUE ZEROES.                
                                                                                
       01  END-OF-FILE-FLAGS.                                                   
           05  WS-F595-EOF               PIC X     VALUE 'N'.                   
           05  WS-K020-EOF               PIC X     VALUE 'N'.                   
                                                                                
       01  COUNTERS.                                                            
           05  WS-F595-READ-CNT          PIC 9(6)  VALUE ZEROES.                
           05  WS-595F-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-595N-WRITE-CNT         PIC 9(6)  VALUE ZEROES.                
           05  WS-NRIC-LIST-CNT          PIC 9(1)  VALUE ZEROES.                
           05  WS-CNT                    PIC 9(1)  VALUE ZERO.                  
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURRENT-DATE1          PIC X(8) VALUE SPACES.                 
           05  WS-CURRENT-TIME           PIC X(9) VALUE SPACES.                 
               05  WS-TIME REDEFINES WS-CURRENT-TIME.                           
                   10  WS-HHMMSSMS       PIC X(8).                              
                   10  WS-GM             PIC X(1).                              
           05  FILLER                    PIC X(4) VALUE SPACES.                 
                                                                                
       01  TEMPORARY-VARIABLES.                                                 
           05  WS-NRIC OCCURS 4 TIMES.                                          
               10 WS-NRIC-LIST           PIC X(9)  VALUE SPACES.                
           05  WS-RUN-DATE               PIC X(10) VALUE SPACES.                
           05  WS-NRIC-FOUND             PIC X(1)  VALUE 'N'.                   
           05  WS-NRIC-FND               PIC X(1)  VALUE 'N'.                   
           05  WS-CNT-FOUND              PIC 9(1)  VALUE ZEROES.                
           05  WS-K020-REC-PROCESS       PIC X(1)  VALUE 'N'.                   
           05  WS-K020-NRIC-PREV         PIC X(9)  VALUE SPACES.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *************************************************************             
       0000-MAIN.                                                               
      *************************************************************             
           PERFORM 1000-OPEN-ROUTINE THRU 1000-EXIT.                            
           PERFORM 2000-PROCESS-RECORDS THRU 2000-EXIT                          
             UNTIL WS-F595-EOF = 'Y'.                                           
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       1000-OPEN-ROUTINE.                                                       
      *************************************************************             
           OPEN INPUT  BP13F595                                                 
                       BB24K020                                                 
                OUTPUT P13F595F                                                 
                       P13F595N.                                                
                                                                                
           IF WS-K020-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BB24K020 OPEN ERROR ' WS-K020-STATUS                     
              MOVE WS-K020-STATUS        TO RETURN-CODE                         
                                                                                
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CURRENT-DATE.                    
                                                                                
           STRING WS-CURRENT-DATE1(7:2) '/'                                     
                  WS-CURRENT-DATE1(5:2) '/'                                     
                  WS-CURRENT-DATE1(1:4)                                         
                  DELIMITED BY SIZE                                             
             INTO WS-RUN-DATE                                                   
                                                                                
           PERFORM 1100-READ-BP13F595 THRU 1100-EXIT.                           
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       1100-READ-BP13F595.                                                      
      *************************************************************             
           READ BP13F595                                                        
             AT END                                                             
                MOVE 'Y'                 TO WS-F595-EOF                         
            NOT AT END                                                          
                ADD 1                    TO WS-F595-READ-CNT                    
           END-READ.                                                            
                                                                                
       1100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       2000-PROCESS-RECORDS.                                                    
      *************************************************************             
           MOVE SPACES                   TO WS-NRIC-LIST(1)                     
                                            WS-NRIC-LIST(2)                     
                                            WS-NRIC-LIST(3)                     
                                            WS-NRIC-LIST(4).                    
                                                                                
           MOVE F595-NUM-NRIC1           TO WS-NRIC-LIST(1).                    
           MOVE 1                        TO WS-NRIC-LIST-CNT.                   
                                                                                
           IF F595-NUM-NRIC2 NOT = SPACES AND LOW-VALUES AND                    
              F595-NUM-NRIC2(1:1) NOT = '#'                                     
              MOVE F595-NUM-NRIC2        TO WS-NRIC-LIST(2)                     
              ADD 1                      TO WS-NRIC-LIST-CNT                    
           END-IF.                                                              
                                                                                
           IF F595-NUM-NRIC3 NOT = SPACES AND LOW-VALUES AND                    
              F595-NUM-NRIC3(1:1) NOT = '#'                                     
              MOVE F595-NUM-NRIC3        TO WS-NRIC-LIST(3)                     
              ADD 1                      TO WS-NRIC-LIST-CNT                    
           END-IF.                                                              
                                                                                
           IF F595-NUM-NRIC4 NOT = SPACES AND LOW-VALUES AND                    
              F595-NUM-NRIC4(1:1) NOT = '#'                                     
              MOVE F595-NUM-NRIC4        TO WS-NRIC-LIST(4)                     
              ADD 1                      TO WS-NRIC-LIST-CNT                    
           END-IF.                                                              
                                                                                
           MOVE 'N'                      TO WS-NRIC-FOUND.                      
           MOVE 0                        TO WS-CNT-FOUND.                       
                                                                                
           PERFORM VARYING WS-CNT FROM 1 BY 1                                   
             UNTIL WS-CNT > WS-NRIC-LIST-CNT                                    
                   MOVE SPACES           TO P13F595F-REC                        
                                            P13F595N-REC                        
                                                                                
                   PERFORM 3000-START-BB24K020 THRU 3000-EXIT                   
           END-PERFORM.                                                         
                                                                                
           IF WS-CNT-FOUND = WS-NRIC-LIST-CNT                                   
              MOVE 'Y'                   TO WS-NRIC-FOUND                       
           END-IF.                                                              
                                                                                
           IF WS-NRIC-FOUND = 'Y'                                               
              MOVE BP13F595-REC          TO P13F595F-REC                        
              MOVE SPACES                TO P13F595F-REC(493:8)                 
              MOVE 'E'                   TO P13F595F-REC(500:1)                 
                                                                                
              WRITE P13F595F-REC                                                
                                                                                
              ADD 1             TO WS-595F-WRITE-CNT                            
           ELSE                                                                 
              MOVE BP13F595-REC          TO P13F595N-REC                        
                                                                                
              WRITE P13F595N-REC                                                
                                                                                
              ADD 1                      TO WS-595N-WRITE-CNT                   
           END-IF.                                                              
                                                                                
           PERFORM 1100-READ-BP13F595 THRU 1100-EXIT.                           
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3000-START-BB24K020.                                                     
      *************************************************************             
           MOVE SPACES                   TO BB24K020-REC.                       
           INITIALIZE                       BB24K020-REC.                       
                                                                                
           MOVE WS-NRIC-LIST(WS-CNT)     TO K020-NUM-UIN                        
                                            WS-K020-NRIC-PREV.                  
           MOVE 'N'                      TO WS-K020-EOF                         
                                            WS-NRIC-FND                         
                                            WS-K020-REC-PROCESS.                
                                                                                
           START BB24K020 KEY >= K020-NUM-UIN.                                  
                                                                                
           EVALUATE WS-K020-STATUS                                              
               WHEN 00                                                          
                    PERFORM 3100-READNEXT-BB24K020 THRU 3100-EXIT               
                      UNTIL K020-NUM-UIN NOT = WS-K020-NRIC-PREV                
                         OR WS-K020-EOF = 'Y'                                   
                         OR WS-NRIC-FND = 'Y'                                   
               WHEN 10                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K020 START ERROR ' WS-K020-STATUS              
                    DISPLAY 'K020 KEY: ' K020-KEY-FLD                           
                    MOVE WS-K020-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       3100-READNEXT-BB24K020.                                                  
      *************************************************************             
           READ BB24K020 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                 TO WS-K020-EOF                         
           END-READ.                                                            
                                                                                
           EVALUATE WS-K020-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    IF K020-NUM-UIN = WS-K020-NRIC-PREV                         
                       MOVE 'Y'          TO WS-NRIC-FND                         
                                            WS-K020-REC-PROCESS                 
                       ADD 1             TO WS-CNT-FOUND                        
                    END-IF                                                      
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE 'Y'             TO WS-K020-EOF                         
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K020 READ NEXT ERROR ' WS-K020-STATUS          
                    DISPLAY 'K020 KEY: ' K020-KEY-FLD                           
                    MOVE WS-K020-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *************************************************************             
       9000-CLOSE-ROUTINE.                                                      
      *************************************************************             
           DISPLAY '   '.                                                       
           DISPLAY 'RUN DATE : ' WS-RUN-DATE.                                   
           DISPLAY '*--------- BP13C994 CONTROL TOTAL ---------*'.              
           DISPLAY '   '.                                                       
           DISPLAY 'NO. OF READS FROM F595   : ' WS-F595-READ-CNT.              
           DISPLAY 'RECS WRITTEN TO P13F595F : ' WS-595F-WRITE-CNT.             
           DISPLAY 'RECS WRITTEN TO P13F595N : ' WS-595N-WRITE-CNT.             
           DISPLAY '  '.                                                        
                                                                                
           CLOSE BP13F595                                                       
                 BB24K020                                                       
                 P13F595F                                                       
                 P13F595N.                                                      
                                                                                
           IF WS-K020-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'BB24K020 CLOSE ERROR ' WS-K020-STATUS                    
              MOVE WS-K020-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
