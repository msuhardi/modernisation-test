       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C045.                                                    
      *AUTHOR.     SURESH.                                                      
      *DATE-WRITTEN. 24/08/1999.                                                
      * ====================================================== *                
      *              SYSTEM OF COMMITMENT (BP13)               *                
      * ====================================================== *                
      * PURPOSE     : OUTPUT MATCHED BP13F520-REC TO BP13F510  *                
      *               AFTER MATCHING BP13F520 WITH BP13F205    *                
      *               FOR PRINTING REPORT BP13S541             *                
      *                                                        *                
      * INPUT FILE  : BP13F520, BP13F205                       *                
      *                                                        *                
      * OUTPUT FILE : P13F520A                                 *                
      * ------------------------------------------------------ *                
      * CHG-NO     OIC    DATE      DESCRIPTION                *                
      * ======    =====   ====      ============               *                
      * BP130732   SRN  24/08/1999  NEW PROGRAM                *                
      * ====================================================== *                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205   ASSIGN       TO BP13F205.                          
                                                                                
           SELECT BP13F520   ASSIGN       TO BP13F520.                          
                                                                                
           SELECT BP13F510   ASSIGN       TO BP13F510.                          
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F205                                                            
            RECORD CONTAINS 80 CHARACTERS                                       
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F205.                                                           
                                                                                
       FD   BP13F520                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F520.                                                           
                                                                                
       FD   BP13F510                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F510.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01 WS-COUNTERS.                                                          
          05 WS-F520-READ          PIC 9(8)    VALUE 0.                         
          05 WS-F205-READ          PIC 9(8)    VALUE 0.                         
          05 WS-F510-WRITE         PIC 9(8)    VALUE 0.                         
                                                                                
       01 WS-F205-KEYFLD.                                                       
          05 WS-F205-NUM-NT-ZONE   PIC X(3)    VALUE SPACES.                    
          05 WS-F205-NUM-FLAT-TYPE PIC X(3)    VALUE SPACES.                    
          05 WS-F205-NUM-HOUSEHOLD PIC X(1)    VALUE SPACES.                    
          05 WS-F205-NUM-CATEGORY  PIC X(1)    VALUE SPACES.                    
          05 WS-F205-TME-QUEUE     PIC X(2)    VALUE SPACES.                    
                                                                                
       01 WS-F520-KEYFLD.                                                       
          05 WS-F520-CDE-ZONE      PIC X(3)    VALUE SPACES.                    
          05 WS-F520-CDE-FLAT-TYPE PIC X(3)    VALUE SPACES.                    
          05 WS-F520-CDE-HOUSEHOLD PIC X(1)    VALUE SPACES.                    
          05 WS-F520-CDE-CATEGORY  PIC X(1)    VALUE SPACES.                    
          05 WS-F520-TME-QUEUE     PIC X(2)    VALUE SPACES.                    
                                                                                
       01 WS-SYS-DATE              PIC X(8)    VALUE SPACES.                    
                                                                                
       01 WS-EDIT-DATE             PIC X(10)   VALUE SPACES.                    
                                                                                
      *------------------*                                                      
       PROCEDURE DIVISION.                                                      
      *------------------*                                                      
       0000-MAIN.                                                               
                                                                                
            PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                         
                                                                                
            PERFORM 2000-READ-BP13F205  THRU 2000-EXIT.                         
                                                                                
            PERFORM 3000-READ-BP13F520  THRU 3000-EXIT.                         
                                                                                
            PERFORM 4000-PROCESS        THRU 4000-EXIT UNTIL                    
                    WS-F520-KEYFLD = HIGH-VALUES OR                             
                    WS-F205-KEYFLD = HIGH-VALUES.                               
                                                                                
            PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPEN-FILES.                                                         
            OPEN INPUT  BP13F520                                                
                        BP13F205                                                
                 OUTPUT BP13F510.                                               
                                                                                
            ACCEPT WS-SYS-DATE   FROM DATE YYYYMMDD.                            
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       2000-READ-BP13F205.                                                      
            READ BP13F205  AT END                                               
                           MOVE HIGH-VALUES  TO WS-F205-KEYFLD                  
                           GO         TO  2000-EXIT.                            
                                                                                
            ADD 1                     TO  WS-F205-READ.                         
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       3000-READ-BP13F520.                                                      
            READ BP13F520  AT END                                               
                           MOVE HIGH-VALUES  TO WS-F520-KEYFLD                  
                           GO          TO  3000-EXIT.                           
                                                                                
            ADD 1                      TO  WS-F520-READ.                        
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       4000-PROCESS.                                                            
            MOVE SPACES                TO  WS-F205-KEYFLD                       
                                           WS-F520-KEYFLD.                      
                                                                                
            MOVE F205-NUM-NT-ZONE      TO  WS-F205-NUM-NT-ZONE                  
            MOVE F205-NUM-FLAT-TYPE    TO  WS-F205-NUM-FLAT-TYPE.               
                                                                                
            MOVE F520-CDE-ZONE         TO  WS-F520-CDE-ZONE                     
            MOVE F520-CDE-FLAT-TYPE    TO  WS-F520-CDE-FLAT-TYPE.               
                                                                                
            EVALUATE F205-NUM-OPTION                                            
              WHEN 'R1'                                                         
      ** MATCH BY CATEGORY                                                      
                MOVE F205-CDE-CATEGORY  TO WS-F205-NUM-CATEGORY                 
                                                                                
                MOVE F520-CDE-CATEGORY  TO WS-F520-CDE-CATEGORY                 
              WHEN 'R2'                                                         
              WHEN 'R4'                                                         
      ** MATCH BY CATEGORY AND HOUSEHOLD-TYPE                                   
                MOVE F205-CDE-CATEGORY  TO WS-F205-NUM-CATEGORY                 
                MOVE F205-CDE-HOUSEHOLD TO WS-F205-NUM-HOUSEHOLD                
                                                                                
                MOVE F520-CDE-CATEGORY  TO WS-F520-CDE-CATEGORY                 
                MOVE F520-CDE-HOUSEHOLD TO WS-F520-CDE-HOUSEHOLD                
              WHEN 'R3'                                                         
      ** MATCH BY HOUSEHOLD-TYPE                                                
                MOVE F205-CDE-HOUSEHOLD TO WS-F205-NUM-HOUSEHOLD                
                                                                                
                MOVE F520-CDE-HOUSEHOLD TO WS-F520-CDE-HOUSEHOLD                
              WHEN 'R5'                                                         
      ** MATCH BY HOUSEHOLD-TYPE AND TIME-QUEUE                                 
                MOVE F205-CDE-HOUSEHOLD TO WS-F205-NUM-HOUSEHOLD                
                MOVE F205-TME-QUEUE     TO WS-F205-TME-QUEUE                    
                                                                                
                MOVE F520-CDE-HOUSEHOLD TO WS-F520-CDE-HOUSEHOLD                
                MOVE F520-TME-QUEUE     TO WS-F520-TME-QUEUE                    
            END-EVALUATE.                                                       
                                                                                
                                                                                
            IF WS-F520-KEYFLD = WS-F205-KEYFLD                                  
               PERFORM 5000-WRITE-BP13F510   THRU 5000-EXIT                     
               PERFORM 3000-READ-BP13F520    THRU 3000-EXIT                     
            ELSE                                                                
               IF WS-F520-KEYFLD > WS-F205-KEYFLD                               
                  PERFORM 2000-READ-BP13F205 THRU 2000-EXIT                     
               ELSE                                                             
                  IF WS-F520-KEYFLD < WS-F205-KEYFLD                            
                      PERFORM 3000-READ-BP13F520                                
                                             THRU 3000-EXIT                     
                  END-IF                                                        
               END-IF                                                           
            END-IF.                                                             
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       5000-WRITE-BP13F510.                                                     
      ** IF PRIORITY - 'Y' ALLOC-SCHM SHOULD NOT BE 'PA '                       
      ** IF PRIORITY - 'N' ALLOC-SCHM SHOULD BE 'PA '                           
                                                                                
            EVALUATE F205-CDE-PRIORITY                                          
              WHEN 'Y'                                                          
                IF F520-CDE-ALLOC-SCH = 'PA '                                   
                   GO TO 5000-EXIT                                              
                END-IF                                                          
              WHEN 'N'                                                          
                IF F520-CDE-ALLOC-SCH NOT = 'PA '                               
                   GO TO 5000-EXIT                                              
                END-IF                                                          
            END-EVALUATE.                                                       
                                                                                
            WRITE BP13F510-REC      FROM BP13F520-REC                           
            ADD 1                   TO WS-F510-WRITE.                           
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       9000-CLOSE-FILES.                                                        
            STRING WS-SYS-DATE(7:2) DELIMITED BY SIZE,                          
                   '/',                                                         
                   WS-SYS-DATE(5:2) DELIMITED BY SIZE,                          
                   '/',                                                         
                   WS-SYS-DATE(1:4) DELIMITED BY SIZE                           
                   INTO                                                         
                   WS-EDIT-DATE.                                                
                                                                                
            DISPLAY ' '                                                         
            DISPLAY '____________________________________________'              
            DISPLAY ' '                                                         
            DISPLAY 'CONTROL TOTALS  PGM:- BP13C045 DATE ' WS-EDIT-DATE         
            DISPLAY ' '                                                         
            DISPLAY 'RECORDS READ FROM BP13F520  - ' WS-F520-READ               
            DISPLAY 'RECORDS READ FROM BP13F205  - ' WS-F205-READ               
            DISPLAY 'RECORDS WRITTEN TO BP13F510 - ' WS-F510-WRITE.             
            DISPLAY '____________________________________________'              
                                                                                
            CLOSE      BP13F520                                                 
                       BP13F205                                                 
                       BP13F510.                                                
            STOP RUN.                                                           
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
