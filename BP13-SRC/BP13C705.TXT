       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C705.                                                 
       AUTHOR.        LSB.                                                      
       DATE-WRITTEN.  30/09/91.                                                 
       DATE-COMPILED.                                                           
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  TO UPDATE THE DATE-BALLOT BASE ON THE CUURENT *                
      *          DATE. DETERMINE THE DATE-BALLOT AS            *                
      *          1ST QTR = YY01                                *                
      *          2ND QTR = YY04                                *                
      *          3RD QTR = YY07                                *                
      *          4TH QTR = YY10                                *                
      *                                                        *                
      *      2.  TO UPDATE NT-WITH-LAND BASE ON THE REQUESTED  *                
      *          NT-WITH-LAND. IE. CCK, JW, PR, TAP AND WL.    *                
      *                                                        *                
      *     INPUT FILES:                                       *                
      *      1. BP13F500                                       *                
      *      2. BP13K703                                       *                
      *                                                        *                
      *     OUTPUT FILES :                                     *                
      *      1. BP13F520                                       *                
      *                                                        *                
      * CHG-NO   BY   ON      DESCRIPTION                      *                
      * ---------------------------------                      *                
      * BP130030 LSB 950823 - FOR 3RM, DTE-BALLOT IS MTHLY NOT *                
      *                       PERVIOUSLY QUARTERLY, YYMM       *                
      * BP130652 NOR 990219 - EXPAND F500 & F520 RECL 300 - 500*                
      * BP131035 SN  010613 - TO ADD F520-CDE-FLAT-TYPE = '2 ' *                
      * BP132255 CKK 021215 - HARCODE 200211 FOR BE BALLOTING  *                
      * BP132293 SK  080203 - READ BP13K703                    *                
      **********************************************************                
      *                                                                         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *                                                                         
       INPUT-OUTPUT SECTION.                                                    
      *                                                                         
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K703  ASSIGN        TO BP13K703                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K703-KEY-FLD                       
                            FILE STATUS   IS BP13K703-STATUS.                   
                                                                                
           SELECT BP13F500 ASSIGN TO BP13F500.                                  
                                                                                
           SELECT BP13F520 ASSIGN TO BP13F520.                                  
                                                                                
      *                                                                         
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *                                                                         
       FD   BP13K703                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K703.                                                           
                                                                                
       FD   BP13F500                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F500.                                                           
      *                                                                         
       FD   BP13F520                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13F520.                                                           
      *                                                                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-ACCESS-STATUS.                                               
           05  BP13K703-STATUS         PIC 9(2)    VALUE ZEROS.                 
           05  WS-K703-STATUS          PIC 9(2)    VALUE ZEROS.                 
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-YY                   PIC 9(4).                                
           05  WS-MM                   PIC 9(2).                                
           05  WS-DD                   PIC 9(2).                                
                                                                                
       01  WS-BALLOT-DATE.                                                      
           05  WS-BALLOT-YY            PIC 9(4).                                
           05  WS-BALLOT-MM            PIC 9(2).                                
                                                                                
       01  WS-BALLOT-DATE-3RM.                                                  
           05  WS-BALLOT-YY-3RM        PIC 9(4).                                
           05  WS-BALLOT-MM-3RM        PIC 9(2).                                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F500-READ        PIC 9(5)    VALUE ZEROS.                 
           05  WS-CNT-F520-WRITE       PIC 9(5)    VALUE ZEROS.                 
           05  WS-CNT-K703-READ        PIC 9(5)    VALUE ZEROS.                 
           05  WS-CNT-K703-FND         PIC 9(5)    VALUE ZEROS.                 
           05  WS-CNT-NT-WITH-LAND     PIC 9(5)    VALUE ZEROS.                 
           05  WS-CNT-REDEVT-AREA      PIC 9(5)    VALUE ZEROS.                 
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F500-STATUS          PIC X       VALUE 'N'.                   
               88  WS-F500-EOF                     VALUE 'Y'.                   
      *                                                                         
       PROCEDURE DIVISION.                                                      
      *                                                                         
                                                                                
      ***************************************************************           
       000-MAIN-ROUTINE.                                                        
      ***************************************************************           
                                                                                
           PERFORM 100-OPEN-FILES                                               
              THRU 100-EXIT.                                                    
                                                                                
           PERFORM 150-COMPUTE-BALLOT-DATE                                      
              THRU 150-EXIT.                                                    
                                                                                
                                                                                
           PERFORM 200-PROCESS-BP13F500                                         
              THRU 200-EXIT                                                     
             UNTIL (WS-F500-EOF)                                                
                                                                                
           PERFORM 900-CLOSE-ROUTINE                                            
              THRU 900-EXIT.                                                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ***************************************************************           
       100-OPEN-FILES.                                                          
      ***************************************************************           
                                                                                
           OPEN INPUT  BP13F500                                                 
                       BP13K703                                                 
                OUTPUT BP13F520.                                                
                                                                                
           EVALUATE WS-K703-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   MOVE WS-K703-STATUS TO RETURN-CODE                           
                   DISPLAY 'OPEN BP13K703 FAIL. STATUS=' WS-K703-STATUS         
                   PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       150-COMPUTE-BALLOT-DATE.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-CURRENT-DATE.                
           MOVE WS-MM TO WS-BALLOT-MM-3RM.                                      
           MOVE WS-YY    TO WS-BALLOT-YY-3RM.                                   
           MOVE WS-YY    TO WS-BALLOT-YY.                                       
                                                                                
           IF WS-MM > 0 AND < 4                                                 
               MOVE '01' TO WS-BALLOT-MM                                        
           ELSE                                                                 
           IF WS-MM > 3 AND < 7                                                 
               MOVE '04' TO WS-BALLOT-MM                                        
           ELSE                                                                 
           IF WS-MM > 6 AND < 10                                                
               MOVE '07' TO WS-BALLOT-MM                                        
           ELSE                                                                 
           IF WS-MM > 9                                                         
               MOVE '10' TO WS-BALLOT-MM.                                       
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***************************************************************           
       200-PROCESS-BP13F500.                                                    
      ***************************************************************           
                                                                                
           READ BP13F500                                                        
                AT END                                                          
                   MOVE 'Y' TO  WS-F500-STATUS                                  
                   GO TO 200-EXIT.                                              
           ADD 1 TO WS-CNT-F500-READ.                                           
           PERFORM 300-WRITE-OUTPUT-REC THRU 300-EXIT.                          
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ***************************************************************           
       300-WRITE-OUTPUT-REC.                                                    
      ***************************************************************           
                                                                                
           MOVE BP13F500-REC   TO  BP13F520-REC.                                
                                                                                
           IF F520-CDE-FLAT-TYPE = '3 ' OR F520-CDE-FLAT-TYPE = '2 ' OR         
              F520-NUM-REF(1:1)  = 'M'                                          
               MOVE WS-BALLOT-DATE-3RM TO F520-DTE-BALLOT                       
           ELSE                                                                 
               MOVE WS-BALLOT-DATE     TO F520-DTE-BALLOT.                      
                                                                                
           IF F520-NUM-ALLO-CAT = 'BE ' OR 'BTO'                                
              PERFORM 400-READ-BP13K703 THRU 400-EXIT                           
           END-IF.                                                              
                                                                                
           ADD     1           TO  WS-CNT-F520-WRITE.                           
           WRITE BP13F520-REC.                                                  
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ****************************************************************          
       400-READ-BP13K703.                                                       
      ****************************************************************          
           MOVE SPACES             TO BP13K703-REC.                             
           INITIALIZE                 BP13K703-REC.                             
           MOVE F520-NUM-ALLO-CAT  TO K703-NUM-SOCLIST.                         
           MOVE F520-NUM-ALLO-CAT  TO K703-NUM-NT.                              
           MOVE '00'               TO K703-NUM-FLAT.                            
           ADD  1                  TO WS-CNT-K703-READ.                         
           READ BP13K703.                                                       
                                                                                
           EVALUATE WS-K703-STATUS                                              
              WHEN 00                                                           
              WHEN 97                                                           
                   ADD 1 TO WS-CNT-K703-FND                                     
                   MOVE K703-DTE-BALLOT-ORIGIN TO F520-DTE-BALLOT               
                   CONTINUE                                                     
              WHEN OTHER                                                        
                   MOVE WS-K703-STATUS TO RETURN-CODE                           
                   DISPLAY 'OPEN BP13K703 FAIL. STATUS=' WS-K703-STATUS         
                   PERFORM 900-CLOSE-ROUTINE THRU 900-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ****************************************************************          
       900-CLOSE-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           CLOSE BP13F500                                                       
                 BP13F520                                                       
                 BP13K703.                                                      
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '  ******* BP13C705 ******* '.                               
           DISPLAY '  '.                                                        
           DISPLAY ' CURRENT-DATE        = ' WS-CURRENT-DATE.                   
           DISPLAY ' UPDATE BALLOT-DATE  = ' WS-BALLOT-DATE.                    
           DISPLAY ' NO OF READ BP13K703 = ' WS-CNT-K703-READ.                  
           DISPLAY ' NO OF FND BP13K703  = ' WS-CNT-K703-FND.                   
           DISPLAY '  '.                                                        
           DISPLAY                                                              
           'TOTAL NO. OF REC READ FROM EXTRACT FILE (BP13F500) : '              
                   WS-CNT-F500-READ.                                            
           DISPLAY                                                              
           'TOTAL NO. OF REC WRITTEN TO BALLOT FILE (BP13F520) : '              
                   WS-CNT-F520-WRITE.                                           
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
