       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C481.                                                 
      *AUTHOR.        GBR.                                                      
      *DATE-WRITTEN.  25/07/2000.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  DESCRIPTION : TO WRITE P13F410A THE CASES WITH REPURCHASED*            
      *                FLATS, REPOSSESSED CODE = '40' AND NO COST. *            
      *                                                            *            
      *  INPUT FILE  :  BP13F410                                   *            
      *                 BM06K110                                   *            
      *  OUTPUT FILE :  P13F410A                                   *            
      *                 BP13F417                                   *            
      * ---------------------------------------------------------- *            
      * CHG-NO.  BY  ON          DESCRIPTION                       *            
      * -------- --- ---------- ---------------------------------- *            
      * BP130862 GBR 25/07/2000 NEW PROGRAM                        *            
      * BP130862 LSB 29/09/2000 -CHG RP-CODE FROM RP TO ST         *            
      * BP131002 LMS 28/03/2001 TO PASS ALL EX-RENTAL FLATS INSTEAD*            
      *              OF CHECKING ST 40.                            *            
      * BP134021 ESA104/11/2010 TO CREATE BP13F417 FOR FTP         *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410   ASSIGN TO BP13F410.                                
                                                                                
           SELECT BM06K110   ASSIGN TO BM06K110                                 
                  ACCESS MODE  IS RANDOM                                        
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY   IS K110-KEY-FLD                                  
                  FILE STATUS IS BM06K110-STATUS.                               
                                                                                
           SELECT P13F410A   ASSIGN TO P13F410A.                                
           SELECT BP13F417   ASSIGN TO BP13F417.                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F410                                                             
           BLOCK  CONTAINS    0  RECORDS                                        
           RECORD CONTAINS  2000  CHARACTERS                                    
           LABEL  RECORD     IS  STANDARD                                       
           RECORDING MODE    IS  F.                                             
       COPY BP13F410.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  P13F410A                                                             
           BLOCK  CONTAINS    0  RECORDS                                        
           RECORD CONTAINS  2000  CHARACTERS                                    
           LABEL  RECORD     IS  STANDARD                                       
           RECORDING MODE    IS  F.                                             
       01  P13F410A-REC              PIC X(2000).                               
                                                                                
       FD  BP13F417                                                             
           BLOCK  CONTAINS    0  RECORDS                                        
           RECORD CONTAINS  200  CHARACTERS                                     
           LABEL  RECORD     IS  STANDARD                                       
           RECORDING MODE    IS  F.                                             
       COPY BP13F417.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  BM06K110-STATUS           PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  F410-READ-CTR             PIC 9(8)   VALUE ZEROES.               
           05  K110-READ-CTR             PIC 9(8)   VALUE ZEROES.               
           05  K110-NOTFND-CTR           PIC 9(8)   VALUE ZEROES.               
           05  F410A-WRITE-CTR           PIC 9(8)   VALUE ZEROES.               
           05  F417A-WRITE-CTR           PIC 9(8)   VALUE ZEROES.               
           05  WS-EDT-TMP                PIC Z(9)9  VALUE ZEROES.               
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K110-FOUND             PIC X(1)   VALUE SPACES.               
           05  WS-F410-FLAG              PIC X(1)   VALUE SPACES.               
               88 WS-F410-EOF            VALUE 'Y'.                             
                                                                                
       01  WS-EDT-DATE.                                                         
           05  WS-EDT-DD           PIC 99.                                      
           05  FILLER              PIC X      VALUE '/'.                        
           05  WS-EDT-MM           PIC 99.                                      
           05  FILLER              PIC X      VALUE '/'.                        
           05  WS-EDT-CCYY         PIC 9999.                                    
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY         PIC 9999.                                    
           05  WS-CUR-MM           PIC 99.                                      
           05  WS-CUR-DD           PIC 99.                                      
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       MAIN-ROUTINE SECTION.                                                    
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F410      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-BM06K110   THRU 3000-EXIT                       
                       UNTIL WS-F410-EOF.                                       
                                                                                
           PERFORM 9000-CLOSE-RTN          THRU 9000-EXIT.                      
                                                                                
       MAIN-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F410                                                 
                       BM06K110                                                 
                OUTPUT P13F410A                                                 
                       BP13F417.                                                
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING BM06K110 : ' BM06K110-STATUS               
              MOVE BM06K110-STATUS             TO RETURN-CODE                   
              PERFORM 9000-CLOSE-RTN           THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
           MOVE WS-CUR-CCYY           TO WS-EDT-CCYY.                           
           MOVE WS-CUR-MM             TO WS-EDT-MM.                             
           MOVE WS-CUR-DD             TO WS-EDT-DD.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13F410.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F410 AT END MOVE 'Y' TO WS-F410-FLAG                        
                                GO TO 2000-EXIT.                                
                                                                                
           ADD 1    TO F410-READ-CTR.                                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-PROCESS-BM06K110.                                                   
      *-------------------------------------------------------------            
                                                                                
           PERFORM 5000-READ-BM06K110   THRU 5000-EXIT.                         
                                                                                
           IF WS-K110-FOUND = 'Y'                                               
             IF (K110-FLAT-STATUS = 'ST')  AND                                  
                (K110-COST-PRICE  = ZEROES)                                     
                PERFORM 5100-WRITE-P13F410A   THRU 5100-EXIT                    
                PERFORM 5200-WRITE-P13F417A   THRU 5200-EXIT                    
             ELSE                                                               
                IF F410-CDE-SALES-TYPE = '2B'                                   
                   PERFORM 5200-WRITE-P13F417A   THRU 5200-EXIT                 
                END-IF                                                          
             END-IF                                                             
           ELSE                                                                 
             IF F410-CDE-SALES-TYPE = '2B'                                      
                PERFORM 5200-WRITE-P13F417A   THRU 5200-EXIT                    
             END-IF                                                             
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F410   THRU 2000-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5000-READ-BM06K110.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES            TO K110-KEY-FLD.                              
           MOVE F410-NUM-SCH-ACC  TO K110-SCH-ACC-NO.                           
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE BM06K110-STATUS                                             
           WHEN 00                                                              
              MOVE 'Y'        TO WS-K110-FOUND                                  
              ADD 1           TO K110-READ-CTR                                  
           WHEN 23                                                              
              MOVE 'N'        TO WS-K110-FOUND                                  
              ADD 1           TO K110-NOTFND-CTR                                
              DISPLAY 'RECORD NOT FOUND IN BM06K110 : ' K110-KEY-FLD            
           WHEN OTHER                                                           
              DISPLAY 'BM06K110 READ ERROR : ' BM06K110-STATUS                  
              MOVE BM06K110-STATUS      TO RETURN-CODE                          
              PERFORM 9000-CLOSE-RTN    THRU 9000-EXIT                          
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5100-WRITE-P13F410A.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES           TO P13F410A-REC.                               
                                                                                
           WRITE P13F410A-REC    FROM BP13F410-REC.                             
                                                                                
           ADD 1                 TO F410A-WRITE-CTR.                            
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
       5200-WRITE-P13F417A.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES             TO BP13F417-REC.                             
           MOVE 'BP13'             TO F417-SYSTEM-ID.                           
           MOVE WS-CUR-CCYY        TO F417-DTE-DOC(5:4)                         
                                      F417-DTE-POST(5:4).                       
           MOVE WS-CUR-MM          TO F417-DTE-DOC(3:2)                         
                                      F417-DTE-POST(3:2).                       
           MOVE WS-CUR-DD          TO F417-DTE-DOC(1:2)                         
                                      F417-DTE-POST(1:2).                       
           MOVE 'XXXXXXXX'         TO F417-NUM-ASSET.                           
           MOVE F410-NUM-SCH-ACC   TO F417-NUM-SCH-ACC.                         
           MOVE 'Z24'              TO F417-NUM-ASSET-TYPE.                      
           MOVE F410-DTE-KEY-ISSUE(7:2) TO F417-DTE-KEY-ISSUED(1:2)             
           MOVE F410-DTE-KEY-ISSUE(5:2) TO F417-DTE-KEY-ISSUED(3:2)             
           MOVE F410-DTE-KEY-ISSUE(1:4) TO F417-DTE-KEY-ISSUED(5:4)             
                                                                                
           WRITE BP13F417-REC.                                                  
                                                                                
           ADD 1                 TO F417A-WRITE-CTR.                            
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-RTN.                                                          
      *-------------------------------------------------------------            
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
           DISPLAY ' PROGRAM : BP13C481             ' WS-EDT-DATE.              
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'.             
           DISPLAY ' '.                                                         
           MOVE F410-READ-CTR        TO WS-EDT-TMP.                             
           DISPLAY 'NO OF RECS READ FROM BP13F410    : ' WS-EDT-TMP.            
           MOVE K110-READ-CTR        TO WS-EDT-TMP.                             
           DISPLAY 'NO OF RECS FOUND FROM BM06K110   : ' WS-EDT-TMP.            
           MOVE F410A-WRITE-CTR      TO WS-EDT-TMP.                             
           DISPLAY 'NO OF RECS WRITTEN TO P13F410A   : ' WS-EDT-TMP.            
           MOVE F417A-WRITE-CTR      TO WS-EDT-TMP.                             
           DISPLAY 'NO OF RECS WRITTEN TO BP13F417   : ' WS-EDT-TMP.            
           MOVE K110-NOTFND-CTR      TO WS-EDT-TMP.                             
           DISPLAY 'NO OF RECS NOT FOUND IN BM06K110 : ' WS-EDT-TMP.            
           DISPLAY ' '.                                                         
           DISPLAY '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'              
                                                                                
           CLOSE BM06K110                                                       
                 BP13F410                                                       
                 P13F410A                                                       
                 BP13F417.                                                      
                                                                                
           IF BM06K110-STATUS NOT = 0 AND 97                                    
              MOVE BM06K110-STATUS       TO RETURN-CODE                         
              DISPLAY 'BM06K110 CLOSE ERROR : ' BM06K110-STATUS                 
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
