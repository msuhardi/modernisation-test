       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY84.                                                 
      *AUTHOR.        PAULO CAMIA LEGASPI.                                      
      *DATE-WRITTEN.  14/06/2011.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE : TO READ BP13F595 AND START BROWSE BP13K828         *        
      *                                                                *        
      * INPUT  FILES : BP13F595                                        *        
      *                BP13K828                                        *        
      *                BP13K716                                        *        
      * OUTPUT FILES : BP13LY84                                        *        
      *                P13F595A                                        *        
      *                                                                *        
      * CHG-NO   BY    ON    - DESCRIPTION                             *        
      * -------------------------------------------------------------- *        
      * BP134255 PCL3 14/06/11 NEW PROGRAM                             *        
      * BP135002 IMC1 15/07/13 CATER FOR CHANGE OF NRIC OF SPOUSE      *        
      * BP135350 SMR2 30/05/14 BYPASS MOTHER / BROSIS-IN-LAW FOUND AS  *        
      *                        OCCUPIER IN DP FLAT                     *        
      * BP138285 AM25 15/05/20 FIX PAGINATION ISSUE                    *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT   BP13F595      ASSIGN TO BP13F595.                           
           SELECT   P13F595A      ASSIGN TO P13F595A.                           
                                                                                
           SELECT   BP13K828      ASSIGN TO BP13K828                            
                            ORGANIZATION IS INDEXED                             
                             ACCESS MODE IS DYNAMIC                             
                              RECORD KEY IS K828-KEY-FLD                        
                             FILE STATUS IS K828-STATUS.                        
                                                                                
           SELECT   BP13K716      ASSIGN TO BP13K716                            
                            ORGANIZATION IS INDEXED                             
                             ACCESS MODE IS DYNAMIC                             
                              RECORD KEY IS K716-KEY-FLD                        
                             FILE STATUS IS K716-STATUS.                        
                                                                                
           SELECT   BP13LY84      ASSIGN TO BP13LY84.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F595                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F595.                                                           
                                                                                
       FD  BP13K828                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13K828.                                                           
                                                                                
       FD  BP13K716                                                             
           RECORD CONTAINS 350 CHARACTERS.                                      
       COPY BP13K716.                                                           
                                                                                
       FD   BP13LY84                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01 BP13LY84-REC  PIC X(132).                                             
                                                                                
       FD   P13F595A                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01 P13F595A-REC  PIC X(500).                                             
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  LY84-HEADER01.                                                       
           05  LY84-TITLE-HDR      PIC X(17)     VALUE 'BP13LY84'.              
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(24)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  LY84-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  LY84-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  LY84-HEADER02.                                                       
           05  FILLER              PIC X(45)     VALUE SPACES.                  
           05  FILLER              PIC X(52)     VALUE                          
               'REPORT ON NON-CITIZEN SPOUSE'.                                  
                                                                                
       01  LY84-HEADER03.                                                       
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(07)    VALUE '    S/N'.                
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(08)    VALUE 'REGN NO '.               
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(09)    VALUE 'NRIC'.                   
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(05)    VALUE 'RELN '.                  
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(03)    VALUE 'CTZ'.                    
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(04)    VALUE 'ROLE'.                   
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(11)    VALUE 'SCH ACC'.                
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER              PIC X(08)    VALUE 'RDB NRIC'.               
                                                                                
       01  LY84-DETAIL01.                                                       
           05  FILLER              PIC X(01)        VALUE SPACES.               
           05  LY84-SN             PIC ZZZZZZ9      VALUE ZEROES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LY84-REGN           PIC X(08)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LY84-NRIC           PIC X(09)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LY84-RELN           PIC X(05)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LY84-CTZ            PIC X(03)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LY84-ROLE           PIC X(04)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LY84-SCH-ACC        PIC X(11)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
           05  LY84-NRIC-RDB       PIC X(09)        VALUE SPACES.               
           05  FILLER              PIC X(02)        VALUE SPACES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  CNT-F595-READ                PIC 9(7)  VALUE ZEROS.              
           05  CNT-WRITE                    PIC 9(7)  VALUE ZEROS.              
           05  CNT-WRITE1                   PIC 9(7)  VALUE ZEROS.              
           05  WS-SNO                       PIC 9(7)  VALUE ZEROS.              
           05  WS-PAGE-CNT                  PIC 9(4)  VALUE ZEROS.              
           05  WS-LINE-CNT                  PIC 9(2)  VALUE 60.                 
                                                                                
                                                                                
       01  WS-VARIABLES.                                                        
           05  K828-STATUS                  PIC 99    VALUE ZEROES.             
           05  K716-STATUS                  PIC 99    VALUE ZEROES.             
           05  WS-CNT                       PIC 9(1)  VALUE 0.                  
           05  WS-SPOUSE-FD                 PIC X(1)  VALUE 'N'.                
           05  WS-MOTHER-FD                 PIC X(1)  VALUE 'N'.                
           05  WS-INLAW-FD                  PIC X(1)  VALUE 'N'.                
           05  WS-K828-EOF                  PIC X(1)  VALUE 'N'.                
           05  WS-K716-EOF                  PIC X(1)  VALUE 'N'.                
           05  WS-F595-EOF                  PIC X(1)  VALUE 'N'.                
           05  WS-K828-FND                  PIC X(01) VALUE SPACE.              
               88  K828-FND                 VALUE 'Y'.                          
           05  WS-K716-FND                  PIC X(01) VALUE SPACE.              
               88  K716-FND                 VALUE 'Y'.                          
           05  WS-SYSTEM-DATE               PIC 9(8)  VALUE ZEROES.             
                                                                                
      *** 1 - APPLICANT, 2 - SPOUSE, 3 - FATHER, 4 - MOTHER, ***                
      *** 5 - BRO/SIS, 6 - BRO/SIS-IN-LAW                    ***                
       01  WS-ARRAY-VARIABLES.                                                  
           05  WS-ARRAY-VARS OCCURS 6 TIMES.                                    
               10 WS-NUM-REGN               PIC X(08) VALUE SPACES.             
               10 WS-NUM-NRIC               PIC X(09) VALUE SPACES.             
               10 WS-NUM-RELN               PIC X(02) VALUE SPACES.             
               10 WS-NUM-CTZ                PIC X(02) VALUE SPACES.             
               10 WS-NUM-SCHACC             PIC X(11) VALUE SPACES.             
               10 WS-NUM-ROLE               PIC X(03) VALUE SPACES.             
               10 WS-NUM-NRIC-RDB           PIC X(09) VALUE SPACES.             
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
                                                                                
      *===============================================================*         
       0000-MAIN.                                                               
      *===============================================================*         
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13F595       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECORD      THRU 3000-EXIT                      
             UNTIL WS-F595-EOF = 'Y'.                                           
                                                                                
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       1000-OPEN-FILES.                                                         
      *===============================================================*         
                                                                                
           OPEN INPUT BP13F595                                                  
                      BP13K828                                                  
                      BP13K716                                                  
               OUTPUT P13F595A                                                  
                      BP13LY84.                                                 
                                                                                
                                                                                
           IF K828-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K828 , STATUS = ' K828-STATUS          
              MOVE K828-STATUS               TO RETURN-CODE                     
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
           IF K716-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR OPENING BP13K716 , STATUS = ' K716-STATUS          
              MOVE K716-STATUS               TO RETURN-CODE                     
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)          TO WS-SYSTEM-DATE.          
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
                  DELIMITED BY SIZE INTO LY84-DATE-HDR.                         
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       2000-READ-BP13F595.                                                      
      *===============================================================*         
                                                                                
           READ BP13F595                                                        
             AT END                                                             
                MOVE 'Y'                TO WS-F595-EOF                          
                GO                      TO 2000-EXIT                            
           END-READ.                                                            
                                                                                
           ADD  1                       TO CNT-F595-READ.                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       3000-PROCESS-RECORD.                                                     
      *===============================================================*         
                                                                                
           PERFORM 3100-START-BP13K828               THRU 3100-EXIT.            
           IF K828-FND                                                          
              PERFORM UNTIL WS-K828-EOF = 'Y'                                   
                 PERFORM 3200-READNEXT-BP13K828      THRU 3200-EXIT             
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
           IF ((WS-NUM-NRIC(1) NOT = SPACES AND LOW-VALUES) AND                 
               (WS-NUM-NRIC(2) NOT = SPACES AND LOW-VALUES)) OR                 
              ((WS-NUM-NRIC(3) NOT = SPACES AND LOW-VALUES) AND                 
               (WS-NUM-NRIC(4) NOT = SPACES AND LOW-VALUES)) OR                 
              ((WS-NUM-NRIC(5) NOT = SPACES AND LOW-VALUES) AND                 
               (WS-NUM-NRIC(6) NOT = SPACES AND LOW-VALUES))                    
              PERFORM 3500-START-BP13K716            THRU 3500-EXIT             
              IF K716-FND                                                       
      *          PERFORM VARYING WS-CNT FROM 1 BY 1                             
      *            UNTIL WS-CNT > 2                                             
                 PERFORM UNTIL WS-K716-EOF = 'Y'                                
      *                  PERFORM 3300-READ-BP13K716     THRU 3300-EXIT          
                         PERFORM 3600-READNEXT-BP13K716 THRU 3600-EXIT          
                 END-PERFORM                                                    
              END-IF                                                            
                                                                                
              PERFORM 3400-CHECK-NUM-ROLE            THRU 3400-EXIT             
           ELSE                                                                 
              PERFORM 5000-WRITE-P13F595A            THRU 5000-EXIT             
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F595                THRU 2000-EXIT.            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       3100-START-BP13K828.                                                     
      *===============================================================*         
                                                                                
           MOVE SPACES                           TO BP13K828-REC                
           INITIALIZE                               BP13K828-REC.               
                                                                                
           MOVE SPACES                           TO WS-ARRAY-VARIABLES.         
           INITIALIZE                               WS-ARRAY-VARIABLES.         
                                                                                
           MOVE 'N'                              TO WS-K828-EOF                 
                                                    WS-K828-FND.                
                                                                                
           MOVE F595-NUM-REGN                     TO K828-NUM-REGN              
                                                                                
           START BP13K828 KEY >= K828-KEY-FLD.                                  
                                                                                
           IF K828-STATUS = 00 OR 02                                            
              MOVE 'Y'                            TO WS-K828-FND                
           ELSE                                                                 
              IF K828-STATUS = 23 OR 10                                         
                 MOVE 'N'                         TO WS-K828-FND                
              ELSE                                                              
                 DISPLAY 'BP13K828 - ERROR STARTING : ' K828-STATUS             
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       3200-READNEXT-BP13K828.                                                  
      *===============================================================*         
                                                                                
           READ BP13K828 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'    TO WS-K828-EOF                                      
                GO          TO 3200-EXIT                                        
           END-READ.                                                            
                                                                                
           IF K828-STATUS = 00 OR 02                                            
              IF K828-NUM-REGN = F595-NUM-REGN                                  
                 IF K828-NUM-RELATIONSHIP  = '00'                               
                    MOVE K828-NUM-REGN            TO WS-NUM-REGN (1)            
                    MOVE K828-NUM-NRIC            TO WS-NUM-NRIC (1)            
                    MOVE K828-NUM-RELATIONSHIP    TO WS-NUM-RELN (1)            
                    MOVE K828-NUM-CITIZENSHIP     TO WS-NUM-CTZ  (1)            
                 END-IF                                                         
                                                                                
                 IF K828-NUM-RELATIONSHIP  = '20'                               
                    MOVE K828-NUM-REGN            TO WS-NUM-REGN (2)            
                    MOVE K828-NUM-NRIC            TO WS-NUM-NRIC (2)            
                    MOVE K828-NUM-RELATIONSHIP    TO WS-NUM-RELN (2)            
                    MOVE K828-NUM-CITIZENSHIP     TO WS-NUM-CTZ  (2)            
                 END-IF                                                         
                                                                                
                 IF K828-NUM-RELATIONSHIP  = '02'                               
                    MOVE K828-NUM-REGN            TO WS-NUM-REGN (3)            
                    MOVE K828-NUM-NRIC            TO WS-NUM-NRIC (3)            
                    MOVE K828-NUM-RELATIONSHIP    TO WS-NUM-RELN (3)            
                    MOVE K828-NUM-CITIZENSHIP     TO WS-NUM-CTZ  (3)            
                 END-IF                                                         
                                                                                
                 IF K828-NUM-RELATIONSHIP  = '12'                               
                    MOVE K828-NUM-REGN            TO WS-NUM-REGN (4)            
                    MOVE K828-NUM-NRIC            TO WS-NUM-NRIC (4)            
                    MOVE K828-NUM-RELATIONSHIP    TO WS-NUM-RELN (4)            
                    MOVE K828-NUM-CITIZENSHIP     TO WS-NUM-CTZ  (4)            
                 END-IF                                                         
                                                                                
                 IF K828-NUM-RELATIONSHIP  = '06'                               
                    MOVE K828-NUM-REGN            TO WS-NUM-REGN (5)            
                    MOVE K828-NUM-NRIC            TO WS-NUM-NRIC (5)            
                    MOVE K828-NUM-RELATIONSHIP    TO WS-NUM-RELN (5)            
                    MOVE K828-NUM-CITIZENSHIP     TO WS-NUM-CTZ  (5)            
                 END-IF                                                         
                                                                                
                 IF K828-NUM-RELATIONSHIP  = '07'                               
                    MOVE K828-NUM-REGN            TO WS-NUM-REGN (6)            
                    MOVE K828-NUM-NRIC            TO WS-NUM-NRIC (6)            
                    MOVE K828-NUM-RELATIONSHIP    TO WS-NUM-RELN (6)            
                    MOVE K828-NUM-CITIZENSHIP     TO WS-NUM-CTZ  (6)            
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'Y'                         TO WS-K828-EOF                
              END-IF                                                            
           ELSE                                                                 
              IF K828-STATUS = 23 OR 10                                         
                 MOVE 'Y'                         TO WS-K828-EOF                
              ELSE                                                              
                 DISPLAY 'BP13K828 - ERROR READING NEXT : ' K828-STATUS         
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       3300-READ-BP13K716.                                                      
      *===============================================================*         
                                                                                
           MOVE SPACES                            TO BP13K716-REC.              
           INITIALIZE                                BP13K716-REC.              
                                                                                
           MOVE F595-NUM-REGN                     TO K716-NUM-REGN.             
           MOVE WS-NUM-NRIC(WS-CNT)               TO K716-NUM-NRIC.             
                                                                                
           READ BP13K716.                                                       
                                                                                
           EVALUATE K716-STATUS                                                 
               WHEN 00                                                          
                    MOVE  K716-NUM-ROLE        TO WS-NUM-ROLE(WS-CNT)           
                    MOVE  K716-NUM-HDB-REF     TO WS-NUM-SCHACC(WS-CNT)         
               WHEN 23                                                          
                    MOVE 3                     TO WS-CNT                        
                                                                                
               WHEN OTHER                                                       
                    MOVE K716-STATUS           TO RETURN-CODE                   
                    DISPLAY 'ERROR READING BP13K716: ' K716-STATUS              
                            ' KEY : ' K716-KEY-FLD                              
                    PERFORM  9000-CLOSE-FILES  THRU 9000-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       3400-CHECK-NUM-ROLE.                                                     
      *===============================================================*         
                                                                                
           MOVE 'N'                            TO WS-SPOUSE-FD                  
                                                  WS-MOTHER-FD                  
                                                  WS-INLAW-FD.                  
                                                                                
           IF (WS-NUM-SCHACC(1) NOT = SPACES AND LOW-VALUES) AND                
               WS-NUM-SCHACC(1) = WS-NUM-SCHACC(2)                              
              IF (WS-NUM-ROLE(1) = '023' OR '024') AND                          
                 (WS-NUM-ROLE(2) = '025')                                       
                 MOVE  'Y'                     TO WS-SPOUSE-FD                  
              END-IF                                                            
                                                                                
              IF (WS-NUM-ROLE(1) = '025') AND                                   
                 (WS-NUM-ROLE(2) = '023' OR '024')                              
                 MOVE  'Y'                     TO WS-SPOUSE-FD                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF (WS-NUM-SCHACC(3) NOT = SPACES AND LOW-VALUES) AND                
               WS-NUM-SCHACC(3) = WS-NUM-SCHACC(4)                              
              IF (WS-NUM-ROLE(3) = '023' OR '024') AND                          
                 (WS-NUM-ROLE(4) = '025')                                       
                 MOVE  'Y'                     TO WS-MOTHER-FD                  
              END-IF                                                            
              IF (WS-NUM-ROLE(3) = '025') AND                                   
                 (WS-NUM-ROLE(4) = '023' OR '024')                              
                 MOVE  'Y'                     TO WS-MOTHER-FD                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF (WS-NUM-SCHACC(5) NOT = SPACES AND LOW-VALUES) AND                
               WS-NUM-SCHACC(5) = WS-NUM-SCHACC(6)                              
              IF (WS-NUM-ROLE(5) = '023' OR '024') AND                          
                 (WS-NUM-ROLE(6) = '025')                                       
                 MOVE  'Y'                     TO WS-INLAW-FD                   
              END-IF                                                            
                                                                                
              IF (WS-NUM-ROLE(5) = '025') AND                                   
                 (WS-NUM-ROLE(6) = '023' OR '024')                              
                 MOVE  'Y'                     TO WS-INLAW-FD                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-SPOUSE-FD = 'Y' OR WS-MOTHER-FD = 'Y' OR                       
              WS-INLAW-FD = 'Y'                                                 
              ADD 1                                    TO WS-SNO                
              IF WS-SPOUSE-FD = 'Y'                                             
                 PERFORM VARYING WS-CNT FROM 1 BY 1                             
                   UNTIL WS-CNT > 2                                             
                    PERFORM 4000-WRITE-BP13LY84    THRU 4000-EXIT               
                 END-PERFORM                                                    
              ELSE                                                              
                 IF WS-MOTHER-FD = 'Y'                                          
                    PERFORM VARYING WS-CNT FROM 3 BY 1                          
                      UNTIL WS-CNT > 4                                          
                       PERFORM 4000-WRITE-BP13LY84    THRU 4000-EXIT            
                    END-PERFORM                                                 
                 ELSE                                                           
                    IF WS-INLAW-FD = 'Y'                                        
                       PERFORM VARYING WS-CNT FROM 5 BY 1                       
                         UNTIL WS-CNT > 6                                       
                          PERFORM 4000-WRITE-BP13LY84    THRU 4000-EXIT         
                       END-PERFORM                                              
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              PERFORM 5000-WRITE-P13F595A            THRU 5000-EXIT             
           END-IF.                                                              
                                                                                
       3400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       3500-START-BP13K716.                                                     
      *===============================================================*         
                                                                                
           MOVE SPACES                           TO BP13K716-REC                
           INITIALIZE                               BP13K716-REC.               
                                                                                
           MOVE 'N'                              TO WS-K716-EOF                 
                                                    WS-K716-FND.                
                                                                                
           MOVE F595-NUM-REGN                     TO K716-NUM-REGN              
                                                                                
           START BP13K716 KEY >= K716-KEY-FLD.                                  
                                                                                
           IF K716-STATUS = 00 OR 02                                            
              MOVE 'Y'                            TO WS-K716-FND                
           ELSE                                                                 
              IF K716-STATUS = 23 OR 10                                         
                 MOVE 'N'                         TO WS-K716-FND                
              ELSE                                                              
                 DISPLAY 'BP13K716 - ERROR STARTING : ' K716-STATUS             
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       3600-READNEXT-BP13K716.                                                  
      *===============================================================*         
                                                                                
           READ BP13K716 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'    TO WS-K716-EOF                                      
                GO          TO 3600-EXIT                                        
           END-READ.                                                            
                                                                                
           IF K716-STATUS = 00 OR 02                                            
              IF K716-NUM-REGN = F595-NUM-REGN                                  
                 IF K716-CDE-RELATIONSHIP  = '00'                               
                    MOVE  K716-NUM-ROLE        TO WS-NUM-ROLE(1)                
                    MOVE  K716-NUM-HDB-REF     TO WS-NUM-SCHACC(1)              
                    IF K716-NUM-NRIC NOT = WS-NUM-NRIC(1)                       
                       MOVE K716-NUM-NRIC   TO WS-NUM-NRIC-RDB(1)               
                    END-IF                                                      
                 END-IF                                                         
                                                                                
                 IF K716-CDE-RELATIONSHIP  = '20'                               
                    MOVE  K716-NUM-ROLE        TO WS-NUM-ROLE(2)                
                    MOVE  K716-NUM-HDB-REF     TO WS-NUM-SCHACC(2)              
                    IF K716-NUM-NRIC NOT = WS-NUM-NRIC(2)                       
                       MOVE K716-NUM-NRIC   TO WS-NUM-NRIC-RDB(2)               
                    END-IF                                                      
                 END-IF                                                         
                                                                                
                 IF K716-CDE-RELATIONSHIP  = '02'                               
                    MOVE  K716-NUM-ROLE        TO WS-NUM-ROLE(3)                
                    MOVE  K716-NUM-HDB-REF     TO WS-NUM-SCHACC(3)              
                    IF K716-NUM-NRIC NOT = WS-NUM-NRIC(3)                       
                       MOVE K716-NUM-NRIC   TO WS-NUM-NRIC-RDB(3)               
                    END-IF                                                      
                 END-IF                                                         
                                                                                
                 IF K716-CDE-RELATIONSHIP  = '12'                               
                    MOVE  K716-NUM-ROLE        TO WS-NUM-ROLE(4)                
                    MOVE  K716-NUM-HDB-REF     TO WS-NUM-SCHACC(4)              
                    IF K716-NUM-NRIC NOT = WS-NUM-NRIC(4)                       
                       MOVE K716-NUM-NRIC   TO WS-NUM-NRIC-RDB(4)               
                    END-IF                                                      
                 END-IF                                                         
                                                                                
                 IF K716-CDE-RELATIONSHIP  = '06'                               
                    MOVE  K716-NUM-ROLE        TO WS-NUM-ROLE(5)                
                    MOVE  K716-NUM-HDB-REF     TO WS-NUM-SCHACC(5)              
                    IF K716-NUM-NRIC NOT = WS-NUM-NRIC(5)                       
                       MOVE K716-NUM-NRIC   TO WS-NUM-NRIC-RDB(5)               
                    END-IF                                                      
                 END-IF                                                         
                                                                                
                 IF K716-CDE-RELATIONSHIP  = '07'                               
                    MOVE  K716-NUM-ROLE        TO WS-NUM-ROLE(6)                
                    MOVE  K716-NUM-HDB-REF     TO WS-NUM-SCHACC(6)              
                    IF K716-NUM-NRIC NOT = WS-NUM-NRIC(6)                       
                       MOVE K716-NUM-NRIC   TO WS-NUM-NRIC-RDB(6)               
                    END-IF                                                      
                 END-IF                                                         
              ELSE                                                              
                 MOVE 'Y'                         TO WS-K716-EOF                
              END-IF                                                            
           ELSE                                                                 
              IF K716-STATUS = 23 OR 10                                         
                 MOVE 'Y'                         TO WS-K716-EOF                
              ELSE                                                              
                 DISPLAY 'BP13K716 - ERROR READING NEXT : ' K716-STATUS         
                 GO TO 9000-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       4000-WRITE-BP13LY84.                                                     
      *===============================================================*         
                                                                                
           IF WS-LINE-CNT > 59                                                  
              ADD 1                    TO WS-PAGE-CNT                           
              MOVE WS-PAGE-CNT         TO LY84-PAGE-HDR                         
                                                                                
              WRITE BP13LY84-REC    FROM LY84-HEADER01 AFTER PAGE               
              WRITE BP13LY84-REC    FROM LY84-HEADER02 AFTER 2                  
              WRITE BP13LY84-REC    FROM LY84-HEADER03                          
                                                                                
              MOVE 4                   TO WS-LINE-CNT                           
           END-IF.                                                              
                                                                                
           MOVE SPACES                               TO BP13LY84-REC.           
           INITIALIZE                                   BP13LY84-REC.           
                                                                                
           MOVE WS-SNO                               TO LY84-SN.                
           MOVE F595-NUM-REGN                        TO LY84-REGN.              
           MOVE WS-NUM-NRIC(WS-CNT)                  TO LY84-NRIC.              
           MOVE WS-NUM-RELN(WS-CNT)                  TO LY84-RELN.              
           MOVE WS-NUM-CTZ(WS-CNT)                   TO LY84-CTZ.               
           MOVE WS-NUM-ROLE(WS-CNT)                  TO LY84-ROLE.              
           MOVE WS-NUM-SCHACC(WS-CNT)                TO LY84-SCH-ACC.           
           MOVE WS-NUM-NRIC-RDB(WS-CNT)              TO LY84-NRIC-RDB.          
                                                                                
           WRITE BP13LY84-REC                      FROM LY84-DETAIL01.          
           ADD 1                                     TO CNT-WRITE               
                                                        WS-LINE-CNT.            
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       5000-WRITE-P13F595A.                                                     
      *===============================================================*         
                                                                                
           MOVE SPACES                               TO P13F595A-REC.           
           INITIALIZE                                   P13F595A-REC.           
                                                                                
           WRITE P13F595A-REC                      FROM BP13F595-REC.           
           ADD 1                                     TO CNT-WRITE1.             
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===============================================================*         
       9000-CLOSE-FILES.                                                        
      *===============================================================*         
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CY84'.                                
           DISPLAY '***************************************'.                   
           DISPLAY 'NO. OF BP13F595 READ          : ' CNT-F595-READ.            
           DISPLAY 'NO. OF RECS SPOUSE FOUND      : ' CNT-WRITE.                
           DISPLAY 'NO. OF RECS WRITTEN (P13F595A): ' CNT-WRITE1.               
                                                                                
           CLOSE BP13F595                                                       
                 BP13K828                                                       
                 BP13K716                                                       
                 P13F595A                                                       
                 BP13LY84.                                                      
                                                                                
           IF K716-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BP13K716 , STATUS = ' K716-STATUS          
           END-IF.                                                              
                                                                                
           IF K828-STATUS NOT = 00 AND 97                                       
              DISPLAY 'ERROR CLOSING BP13K828 , STATUS = ' K828-STATUS          
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
