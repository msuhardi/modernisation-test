       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C688.                                                 
       AUTHOR.        RAYMOND H. BUNADO                                         
      *DATE-WRITTEN.  04/03/97.                                                 
      ***************************************************************           
      *                      SERS SYSTEM  (BP17)                    *           
      *=============================================================*           
      *                                                             *           
      *  OBJECTIVE  :AFTER EXTRACT LESSEE AND NRIC FROM SMS MASTER  *           
      *              PROGRAM TO CHECK, IF THERE ARE MULTIPLE DP SMS *           
      *              RECORD OUTPUT REC TO FILE B. IF THERE IS ONE   *           
      *              DP FLAT, OUTPUT TO FILE A.                     *           
      *                                                             *           
      *  I/O FILE                                                   *           
      *  --------                                                   *           
      *         I   :  BP13F559                                     *           
      *         O   :  P13F559A                                     *           
      *         O   :  P13F559B                                     *           
      *                                                             *           
      *-------------------------------------------------------------*           
      * CHG-NO   BY  DATE     DESCRIPTION                           *           
      * -------- --- ------   ------------                          *           
      * BP130258 RHB 970205 - NEW PGM                               *           
      * BP130393 LSB 971016 - EXPAND FILE STATUS-RSL TO 3 BYTES     *           
      ***************************************************************           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F559 ASSIGN TO BP13F559.                                  
                                                                                
           SELECT P13F559A ASSIGN TO P13F559A.                                  
                                                                                
           SELECT P13F559B ASSIGN TO P13F559B.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F559                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE F.                                                    
       COPY BP13F559.                                                           
                                                                                
       FD  P13F559A                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE F.                                                    
       01 P13F559A-REC   PIC X(500).                                            
                                                                                
       FD  P13F559B                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE F.                                                    
       01 P13F559B-REC   PIC X(500).                                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  WS-F559-READ-CNT              PIC 9(8)  VALUE ZEROS.             
           05  WS-F559A-WRITE-CNT            PIC 9(8)  VALUE ZEROS.             
           05  WS-F559B-WRITE-CNT            PIC 9(8)  VALUE ZEROS.             
           05  WS-MATCH-CNT                  PIC 9(8)  VALUE ZEROS.             
           05  WS-F559B-CNT                  PIC 9(8)  VALUE ZEROS.             
           05  WS-B-CNT                  PIC 9(8)  VALUE ZEROS.                 
           05  WS-A-CNT                  PIC 9(8)  VALUE ZEROS.                 
                                                                                
       01  WS-GEN-REC.                                                          
          02 WS-PREV-KEY-FLD.                                                   
             05 WS-PREV-REGN                 PIC X(8)  VALUE SPACES.            
             05 WS-PREV-NRIC                 PIC X(9)  VALUE SPACES.            
          02 WS-CURR-KEY-FLD.                                                   
             05 WS-CURR-REGN                 PIC X(8)  VALUE SPACES.            
             05 WS-CURR-NRIC                 PIC X(9)  VALUE SPACES.            
          02 WS-OM-FLAT                      PIC 9(8)  VALUE ZEROS.             
          02 WS-DP-FLAT                      PIC 9(8)  VALUE ZEROS.             
          02  WS-F559-EOF                    PIC X(1)  VALUE  'N'.              
          02  WS-F559-STATUS                 PIC 9(2)  VALUE ZEROS.             
          02  WS-IDX                         PIC 9(2)  VALUE ZEROS.             
          02  WS-MR-IDX                         PIC 9(2)  VALUE ZEROS.          
          02  WS-CNT                         PIC 9(2)  VALUE ZEROS.             
          02  WS-SUB-CNT                     PIC 9(2)  VALUE ZEROS.             
          02  WS-MR                          PIC X(1)  VALUE 'N'.               
                                                                                
       01  WS-BP13F559-RECORD.                                                  
         03  WS-BP13F559-REC             OCCURS  11 TIMES.                      
           05 WS-F559-KEY-FLD.                                                  
              10 WS-F559-NUM-REGN                 PIC X(8).                     
              10 WS-F559-NUM-MATURE-KEY REDEFINES WS-F559-NUM-REGN.             
                 15 WS-F559-NUM-REF               PIC X(6).                     
                 15 FILLER                        PIC X(2).                     
              10 WS-F559-NUM-NRIC.                                              
                 15 WS-F559-NUM-NRIC-PREFIX       PIC X(1).                     
                 15 WS-F559-NUM-NRIC-OLD          PIC X(8).                     
           05 FILLER                              PIC X(19).                    
           05 WS-F559-DTE-RESALE-EFF              PIC X(8).                     
           05 WS-F559-DTE-TRSF-SMS                PIC X(8).                     
           05 WS-F559-DTE-FLAT-SOLD-SMS           PIC X(8).                     
           05 WS-F559-DTE-CDE-SALES-TYPE-SMS      PIC  X(2).                    
           05 WS-F559-DTE-ISSUE-REGN              PIC X(8).                     
           05 WS-F559-NUM-HDB-REF-RSL             PIC X(11).                    
           05 WS-F559-CDE-RELATIONSHIP            PIC X(2).                     
           05 WS-F559-CDE-LESSEE-CHECK            PIC X(1).                     
           05 WS-F559-DTE-UPDATE                  PIC X(8).                     
           05 WS-F559-CDE-HOUSEHOLD               PIC X(1).                     
           05 WS-F559-NUM-5YR-OCCUPN              PIC X(1).                     
           05 FILLER                              PIC X(4).                     
           05 WS-F559-CDE-OWNER-STATUS            PIC X(1).                     
           05 WS-F559-CDE-FILE                    PIC X(1).                     
           05 WS-F559-NUM-HDB-REF-SMS             PIC X(11).                    
           05 WS-F559-CDE-FILE-STATUS-RSL         PIC X(3).                     
           05 WS-F559-CDE-FLAT-TYPE-SMS           PIC X(02).                    
           05 WS-F559-CDE-FLAT-TYPE-RESALE        PIC X(01).                    
           05 WS-F559-NUM-MTHLY-INCOME            PIC 9(5).                     
           05 WS-F559-CDE-SOLE-LESSEE             PIC X(1).                     
           05 WS-F559-CDE-FILE-STATUS-SMS         PIC X(2).                     
           05 WS-F559-NUM-ETHNIC                  PIC X(1).                     
           05 WS-F559-NUM-SEX                     PIC X(1).                     
           05 WS-F559-NUM-MARITAL-STATUS          PIC X(1).                     
           05 WS-F559-DTE-BIRTH.                                                
              10 WS-F559-DTE-BIRTH-CC             PIC X(2).                     
              10 WS-F559-DTE-BIRTH-YY             PIC X(2).                     
              10 WS-F559-DTE-BIRTH-MM             PIC X(2).                     
              10 WS-F559-DTE-BIRTH-DD             PIC X(2).                     
           05 WS-F559-NUM-CITIZENSHIP             PIC X(2).                     
           05 FILLER                              PIC X(12).                    
           05 FILLER                              PIC X(250).                   
           05 WS-F559-NUM-UIN-SMS1                PIC X(9).                     
           05 WS-F559-NUM-UIN-SMS2                PIC X(9).                     
           05 WS-F559-NUM-UIN-SMS3                PIC X(9).                     
           05 WS-F559-NUM-UIN-SMS4                PIC X(9).                     
           05 WS-F559-NUM-ACCT-SMS                PIC X(1).                     
           05 FILLER                              PIC X(63).                    
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
      *=========================================================*               
       0000-MAIN-LOGIC.                                                         
      *=========================================================*               
                                                                                
           PERFORM  1000-OPEN-FILES             THRU 1000-EXIT.                 
           PERFORM  2000-READ-F559              THRU 2000-EXIT.                 
           MOVE F559-NUM-REGN TO WS-PREV-REGN.                                  
           MOVE F559-NUM-NRIC TO WS-PREV-NRIC.                                  
           PERFORM  2100-PROCESS                THRU 2100-EXIT                  
                 UNTIL WS-F559-EOF = 'Y'.                                       
           PERFORM  9000-CLOSE-FILES            THRU 9000-EXIT.                 
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=========================================================*               
       1000-OPEN-FILES.                                                         
      *=========================================================*               
                                                                                
           OPEN INPUT  BP13F559                                                 
                OUTPUT P13F559A                                                 
                OUTPUT P13F559B.                                                
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=========================================================*               
       2000-READ-F559.                                                          
      *=========================================================*               
           READ BP13F559 AT END                                                 
                MOVE 'Y'                    TO WS-F559-EOF                      
                GO TO 2000-EXIT                                                 
           END-READ.                                                            
           MOVE F559-NUM-REGN TO WS-CURR-REGN.                                  
           MOVE F559-NUM-NRIC TO WS-CURR-NRIC.                                  
           ADD 1                            TO WS-F559-READ-CNT.                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=========================================================*               
       2100-PROCESS.                                                            
      *=========================================================*               
                                                                                
           PERFORM  2160-INITIALISE-WS THRU 2160-EXIT                           
                   VARYING WS-IDX FROM 1 BY 1 UNTIL WS-IDX > 10.                
                                                                                
           MOVE ZEROS                            TO WS-OM-FLAT.                 
           MOVE ZEROS                            TO WS-DP-FLAT.                 
           MOVE ZEROS                            TO WS-MATCH-CNT.               
           MOVE ZEROS                            TO WS-A-CNT.                   
           MOVE ZEROS                            TO WS-B-CNT.                   
                                                                                
           PERFORM  2150-PROCESS-DP-OM THRU 2150-EXIT                           
               UNTIL WS-CURR-KEY-FLD NOT = WS-PREV-KEY-FLD                      
                     OR WS-F559-EOF = 'Y'.                                      
                                                                                
           MOVE F559-NUM-REGN TO WS-PREV-REGN.                                  
           MOVE F559-NUM-NRIC TO WS-PREV-NRIC.                                  
                                                                                
           MOVE WS-MATCH-CNT  TO WS-SUB-CNT.                                    
                                                                                
           IF WS-DP-FLAT > 1                                                    
              PERFORM 2300-WRITE-F559B THRU 2300-EXIT                           
                      VARYING WS-B-CNT FROM 1 BY 1 UNTIL                        
                      WS-B-CNT > WS-SUB-CNT                                     
           ELSE                                                                 
           IF WS-DP-FLAT = 1 AND  WS-OM-FLAT > 0                                
              PERFORM 2200-WRITE-F559A THRU 2200-EXIT                           
                      VARYING WS-A-CNT FROM 1 BY 1 UNTIL                        
                      WS-A-CNT > WS-SUB-CNT                                     
           END-IF.                                                              
                                                                                
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=========================================================*               
       2150-PROCESS-DP-OM.                                                      
      *=========================================================*               
                                                                                
            ADD   1                        TO WS-MATCH-CNT                      
            MOVE BP13F559-REC TO WS-BP13F559-REC(WS-MATCH-CNT)                  
            IF F559-CDE-FILE-STATUS-SMS  = 'OM'                                 
               ADD 1                        TO WS-OM-FLAT                       
            ELSE                                                                
               ADD 1                        TO WS-DP-FLAT                       
            END-IF.                                                             
                                                                                
            PERFORM 2000-READ-F559         THRU 2000-EXIT.                      
                                                                                
       2150-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=========================================================*               
       2160-INITIALISE-WS.                                                      
      *=========================================================*               
                                                                                
           MOVE SPACES TO WS-BP13F559-REC(WS-IDX).                              
                                                                                
       2160-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *==========================================================*              
       2200-WRITE-F559A.                                                        
      *==========================================================*              
           IF WS-F559-CDE-FILE-STATUS-SMS(WS-A-CNT) NOT = 'OM'                  
              MOVE WS-BP13F559-REC(WS-A-CNT) TO P13F559A-REC                    
              WRITE P13F559A-REC                                                
              ADD 1                       TO WS-F559A-WRITE-CNT                 
              MOVE 99 TO WS-A-CNT                                               
           END-IF.                                                              
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *==========================================================*              
       2300-WRITE-F559B.                                                        
      *==========================================================*              
            MOVE WS-BP13F559-REC(WS-B-CNT) TO P13F559B-REC.                     
            WRITE P13F559B-REC.                                                 
            ADD 1                       TO WS-F559B-WRITE-CNT.                  
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *==========================================================*              
       9000-CLOSE-FILES.                                                        
      *==========================================================*              
           CLOSE BP13F559                                                       
                 P13F559A                                                       
                 P13F559B.                                                      
                                                                                
                                                                                
           DISPLAY '***** BP13C688 TOTAL *****'.                                
           DISPLAY 'NO OF BP13F559 READ    : ' WS-F559-READ-CNT.                
           DISPLAY 'NO OF P13F559A WRITTEN : ' WS-F559A-WRITE-CNT.              
           DISPLAY 'NO OF P13F559B WRITTEN : ' WS-F559B-WRITE-CNT.              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
