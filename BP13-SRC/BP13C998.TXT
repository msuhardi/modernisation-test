      *----------------------------------------------------------------*        
       IDENTIFICATION DIVISION.                                                 
      *----------------------------------------------------------------*        
       PROGRAM-ID.    BP13C998.                                                 
      *AUTHOR.        SK.                                                       
      *DATE-WRITTEN.  13-09-2004.                                               
      *----------------------------------------------------------------*        
      *        SOC  - BP13                                             *        
      *----------------------------------------------------------------*        
      *                                                                *        
      * OBJECTIVES :  SUB-RTN TO TO EXTRACT THE MOBILITY DATE FROM RDB *        
      *               TABLES FOR COMPUTE THE SUBLET MONTH FOR EX-FLAT  *        
      *               OF SMS, RESALE, TRANSFER FLAT.                   *        
      *                                                                *        
      * DATABASE   : RDB2                                              *        
      *                                                                *        
      * CHG REQ# DATE     BY    DESCRIPTION                            *        
      * -------- -------- ----  ---------------------------------------*        
      * BP132589 13092004 SK    NEW PROGRAM                            *        
      *----------------------------------------------------------------*        
       ENVIRONMENT DIVISION.                                                    
       DATA DIVISION.                                                           
       WORKING-STORAGE SECTION.                                                 
      *----------------------------------------------------------------*        
                                                                                
      *----------------------------------------------------------------*        
      * COMAREA FOR CALLING AV02C013 FOR FINDING NRIC NO TYPE          *        
      *----------------------------------------------------------------*        
       01  WS-COM-AREA.                                                         
           05  WS-COM-NRIC             PIC X(9)    VALUE SPACES.                
           05  WS-COM-RFLAG            PIC X(1)    VALUE SPACES.                
           05  WS-COM-NUMBER-TYPE      PIC X(3)    VALUE SPACES.                
                                                                                
           EXEC SQL INCLUDE P13IDNO   END-EXEC.                                 
           EXEC SQL INCLUDE P13CSROL  END-EXEC.                                 
           EXEC SQL INCLUDE P13CXROL  END-EXEC.                                 
           EXEC SQL INCLUDE SQLCA     END-EXEC.                                 
                                                                                
           EXEC SQL                                                             
             DECLARE CURSOR1 CURSOR FOR                                         
               SELECT T2.NUM_BSNS_REF,                                          
                      T2.DTE_ROLE_START,                                        
                      DATE('0001-01-01'),                                       
                      T2.NUM_ROLE                                               
               FROM                                                             
                 IDNTY_NO       T1,                                             
                 CSTMR_ROLE     T2                                              
               WHERE                                                            
                 T1.NUM_CSTMR         = T2.NUM_CSTMR                 AND        
                 T2.NUM_ROLE IN ('023','024')                        AND        
                 T1.NUM_IDNTY_NO      = :DCLIDNTY-NO.NUM-IDNTY-NO    AND        
                 T1.NUM_IDNTY_NO_TYPE = :DCLIDNTY-NO.NUM-IDNTY-NO-TYPE          
                                                                                
               UNION                                                            
                                                                                
               SELECT T2.NUM_BSNS_REF,                                          
                      T2.DTE_ROLE_START,                                        
                      T2.DTE_ROLE_END,                                          
                      T2.NUM_ROLE                                               
               FROM                                                             
                 IDNTY_NO       T1,                                             
                 CSTMR_EX_ROLE  T2                                              
               WHERE                                                            
                 T1.NUM_CSTMR         = T2.NUM_CSTMR                 AND        
                 T2.NUM_ROLE IN ('023','024')                        AND        
                 T1.NUM_IDNTY_NO      = :DCLIDNTY-NO.NUM-IDNTY-NO    AND        
                 T1.NUM_IDNTY_NO_TYPE = :DCLIDNTY-NO.NUM-IDNTY-NO-TYPE          
                                                                                
                 ORDER BY 2 DESC                                                
           END-EXEC                                                             
                                                                                
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CTR                  PIC 9(1) VALUE ZEROS.                    
           05  WS-SQL-CODE             PIC 9(4).                                
           05  WS-SQL-CODE-X REDEFINES WS-SQL-CODE                              
                                       PIC X(4).                                
                                                                                
       01  WS-SWITCHES.                                                         
           05 WS-CURSOR-EOF            PIC X(1) VALUE 'N'.                      
                                                                                
      *----------------------------------------------------------------*        
       LINKAGE SECTION.                                                         
      *----------------------------------------------------------------*        
       01  LINK-REC.                                                            
           05  LINK-NUM-NRIC.                                                   
               10  LINK-NUM-NRIC-PRFIX      PIC X(1).                           
               10  LINK-NUM-NRIC-NUMBER     PIC X(8).                           
                                                                                
       COPY P13COM91.                                                           
                                                                                
      *----------------------------------------------------------------*        
       PROCEDURE DIVISION USING LINK-REC, BP13COM91-REC.                        
      *----------------------------------------------------------------*        
                                                                                
           EXEC SQL WHENEVER NOT FOUND                                          
                CONTINUE                                                        
           END-EXEC.                                                            
                                                                                
           EXEC SQL WHENEVER SQLERROR                                           
                GO TO 9000-ABORT-PROCESS                                        
           END-EXEC.                                                            
                                                                                
           EXEC SQL WHENEVER SQLWARNING                                         
                GO TO 9000-ABORT-PROCESS                                        
           END-EXEC.                                                            
                                                                                
      *----------------------------------------------------------------*        
       0000-MAIN.                                                               
      *----------------------------------------------------------------*        
                                                                                
           MOVE SPACES TO  BP13COM91-REC.                                       
           INITIALIZE      BP13COM91-REC.                                       
                                                                                
           PERFORM 1000-PROCESS-NRIC    THRU 1000-EXIT                          
           PERFORM 9100-GOBACK          THRU 9100-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           GOBACK.                                                              
                                                                                
      *----------------------------------------------------------------*        
       1000-PROCESS-NRIC.                                                       
      *----------------------------------------------------------------*        
                                                                                
           MOVE LINK-NUM-NRIC   TO  WS-COM-NRIC.                                
                                                                                
           IF WS-COM-NRIC(2:8) = SPACES OR LOW-VALUES OR                        
                                       ALL 'X'                                  
              MOVE '1000'     TO WS-SQL-CODE                                    
              GO TO 9100-GOBACK                                                 
           END-IF.                                                              
                                                                                
           IF WS-COM-NRIC(1:1) = 'X'                                            
              MOVE 'XIN'                TO WS-COM-NUMBER-TYPE                   
              MOVE 'Y'                  TO WS-COM-RFLAG                         
           ELSE                                                                 
              CALL 'AV02C013'           USING WS-COM-NRIC                       
                                              WS-COM-RFLAG                      
                                              WS-COM-NUMBER-TYPE                
           END-IF.                                                              
                                                                                
           IF WS-COM-RFLAG = 'Y'                                                
              PERFORM 2000-READ-CDB        THRU 2000-EXIT                       
           ELSE                                                                 
             MOVE 'I000' TO COM91-ERR-CODE                                      
             GO TO 9100-GOBACK                                                  
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       2000-READ-CDB.                                                           
      *----------------------------------------------------------------*        
                                                                                
           MOVE WS-COM-NUMBER-TYPE    TO NUM-IDNTY-NO-TYPE.                     
           MOVE WS-COM-NRIC           TO NUM-IDNTY-NO OF DCLIDNTY-NO.           
                                                                                
           MOVE 'N'                   TO WS-CURSOR-EOF.                         
           PERFORM 2100-OPEN-CURSOR   THRU 2100-EXIT.                           
                                                                                
           MOVE 1  TO  WS-CTR.                                                  
           PERFORM UNTIL WS-CURSOR-EOF = 'Y' OR  WS-CTR > 6                     
              PERFORM 2200-FETCH-CURSOR  THRU 2200-EXIT                         
              IF WS-CURSOR-EOF NOT = 'Y'                                        
                 MOVE NUM-ROLE     OF DCLCSTMR-ROLE TO                          
                                   COM91-NUM-ROLE(WS-CTR)                       
                 MOVE NUM-BSNS-REF OF DCLCSTMR-ROLE TO                          
                                   COM91-SCH-ACCT(WS-CTR)                       
                 ADD  1  TO  WS-CTR                                             
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           PERFORM 4000-CLOSE-CURSOR  THRU 4000-EXIT.                           
                                                                                
           IF WS-CTR  =  1                                                      
              MOVE '0100'    TO COM91-ERR-CODE                                  
           ELSE                                                                 
              MOVE '0000'    TO COM91-ERR-CODE                                  
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------------*        
       2100-OPEN-CURSOR.                                                        
      *----------------------------------------------------------------*        
                                                                                
           EXEC SQL                                                             
             OPEN CURSOR1                                                       
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN 0                                                            
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE SQLCODE            TO WS-SQL-CODE                         
                 MOVE WS-SQL-CODE-X      TO COM91-ERR-CODE                      
                 IF SQLCODE < ZEROES                                            
                    MOVE '-'             TO COM91-ERR-CODE(1:1)                 
                 END-IF                                                         
                 GO TO 9000-ABORT-PROCESS                                       
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------------*        
       2200-FETCH-CURSOR.                                                       
      *----------------------------------------------------------------*        
                                                                                
           EXEC SQL                                                             
             FETCH CURSOR1                                                      
               INTO                                                             
                 :DCLCSTMR-ROLE.NUM-BSNS-REF,                                   
                 :DCLCSTMR-ROLE.DTE-ROLE-START,                                 
                 :DCLCSTMR-EX-ROLE.DTE-ROLE-END,                                
                 :DCLCSTMR-ROLE.NUM-ROLE                                        
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN 0                                                            
                 CONTINUE                                                       
              WHEN 100                                                          
                 MOVE 'Y'             TO WS-CURSOR-EOF                          
              WHEN OTHER                                                        
                 MOVE SQLCODE            TO WS-SQL-CODE                         
                 MOVE WS-SQL-CODE-X      TO COM91-ERR-CODE                      
                 IF SQLCODE < ZEROES                                            
                    MOVE '-'             TO COM91-ERR-CODE(1:1)                 
                 END-IF                                                         
                 GO TO 9000-ABORT-PROCESS                                       
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------------*        
       4000-CLOSE-CURSOR.                                                       
      *----------------------------------------------------------------*        
                                                                                
           EXEC SQL                                                             
             CLOSE CURSOR1                                                      
           END-EXEC.                                                            
                                                                                
           EVALUATE SQLCODE                                                     
              WHEN 0                                                            
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 MOVE SQLCODE            TO WS-SQL-CODE                         
                 MOVE WS-SQL-CODE-X      TO COM91-ERR-CODE                      
                 IF SQLCODE < ZEROES                                            
                    MOVE '-'             TO COM91-ERR-CODE(1:1)                 
                 END-IF                                                         
                 GO TO 9000-ABORT-PROCESS                                       
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------------*        
       9000-ABORT-PROCESS.                                                      
      *----------------------------------------------------------------*        
                                                                                
           CALL 'DBATIAR' USING SQLCA.                                          
           GOBACK.                                                              
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *----------------------------------------------------------------*        
       9100-GOBACK.                                                             
      *----------------------------------------------------------------*        
                                                                                
           GOBACK.                                                              
                                                                                
       9100-EXIT.                                                               
            EXIT.                                                               
                                                                                
