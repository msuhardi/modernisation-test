       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CF7O.                                                  
       AUTHOR.       AM25.                                                      
       DATE-WRITTEN. 03/01/2024.                                                
      *****************************************************************         
      *                SOC - SYSTEM OF COMMITMENT (BP13)              *         
      *===============================================================*         
      *  OBJECTIVES : UPDATE NUM-BALLOT-OVERWRITE IN BP13K800 BASED   *         
      *               ON INPUT REGISTRATION NUMBER FOR EIP            *         
      *===============================================================*         
      * FILES DESCRIPTION :-                                          *         
      *                                                               *         
      *                ACCESS     REC                                 *         
      * DD NAME  I/O/E MODE       LEN  KEY FIELDS    REMARKS          *         
      * -------- ----- ---------- ---- ------------- ---------------- *         
      * BP13F740 I     SEQUENTIAL 500                                 *         
      * BP13K800 I/O   RANDOM     2000 K800-NUM-REGN                  *         
      *===============================================================*         
      * MODIFICATIONS :                                               *         
      *                                                               *         
      * CHG-REQ#  DDMMCCYY   BY   REMARKS                             *         
      * --------  --------  ----  ----------------------------------- *         
      * BP139852  03012024  AM25  NEW PROGRAM                         *         
      *****************************************************************         
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F740 ASSIGN      TO BP13F740.                             
                                                                                
           SELECT BP13K800 ASSIGN      TO BP13K800                              
              ORGANIZATION  IS INDEXED                                          
              ACCESS MODE   IS RANDOM                                           
              RECORD KEY    IS K800-NUM-REGN                                    
              FILE STATUS   IS WS-K800-STATUS.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F740                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F740.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-K800-STATUS          PIC 9(2)        VALUE ZEROES.            
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F740-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-K800-READ            PIC 9(6)        VALUE ZEROES.            
           05  WS-K800-UPDT            PIC 9(6)        VALUE ZEROES.            
                                                                                
       01  WS-SWITCHES-FLAGS.                                                   
           05  WS-F740-EOF             PIC X           VALUE 'N'.               
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-BP13F740 THRU 2000-EXIT.                           
           PERFORM 3000-READ-BP13K800 THRU 3000-EXIT                            
              UNTIL WS-F740-EOF = 'Y'.                                          
           PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13F740                                                 
                I-O    BP13K800.                                                
                                                                                
           IF (WS-K800-STATUS NOT = ZEROES AND 97)                              
              DISPLAY 'OPEN BP13K800 FAIL STATUS=' WS-K800-STATUS               
              MOVE WS-K800-STATUS      TO RETURN-CODE                           
              GO TO 9000-EXIT                                                   
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-READ-BP13F740.                                                      
      *****************************************************************         
                                                                                
           READ BP13F740                                                        
              AT END                                                            
                 MOVE 'Y'              TO WS-F740-EOF                           
              NOT AT END                                                        
                 ADD 1                 TO WS-F740-READ                          
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-READ-BP13K800.                                                      
      *****************************************************************         
                                                                                
           MOVE F740-NUM-REGN          TO K800-NUM-REGN.                        
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
              WHEN 00                                                           
                 ADD 1                 TO WS-K800-READ                          
                                                                                
                 PERFORM 3100-UPDATE-BP13K800 THRU 3100-EXIT                    
              WHEN 23                                                           
                 DISPLAY 'REC NOT FOUND IN BP13K800: ' F740-NUM-REGN            
              WHEN OTHER                                                        
                 DISPLAY 'READ BP13K800 FAIL STATUS=' WS-K800-STATUS            
                 MOVE WS-K800-STATUS   TO RETURN-CODE                           
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2000-READ-BP13F740 THRU 2000-EXIT.                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3100-UPDATE-BP13K800.                                                    
      *****************************************************************         
                                                                                
           MOVE 'E'                    TO K800-NUM-BALLOT-OVERWRITE.            
                                                                                
           REWRITE BP13K800-MASTER.                                             
                                                                                
           IF WS-K800-STATUS = ZEROES                                           
              ADD 1                    TO WS-K800-UPDT                          
           ELSE                                                                 
              DISPLAY 'REWRITE BP13K800 FAIL STATUS=' WS-K800-STATUS            
              MOVE WS-K800-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-FILES.                                                        
      *****************************************************************         
                                                                                
           CLOSE BP13F740                                                       
                 BP13K800.                                                      
                                                                                
           IF WS-K800-STATUS NOT = ZEROES                                       
              DISPLAY 'CLOSE BP13K800 FAIL STATUS=' WS-K800-STATUS              
              MOVE WS-K800-STATUS      TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           DISPLAY '*******************************'.                           
           DISPLAY '* PROGRAM BP13CF7O            *'.                           
           DISPLAY '* CONTROL COUNTS              *'.                           
           DISPLAY '*-----------------------------*'.                           
           DISPLAY '* REC READ F740      : ' WS-F740-READ ' *'.                 
           DISPLAY '* REC READ K800      : ' WS-K800-READ ' *'.                 
           DISPLAY '* REC UPDT K800      : ' WS-K800-UPDT ' *'.                 
           DISPLAY '*******************************'.                           
                                                                                
       9000-EXIT.                                                               
           STOP RUN.                                                            
           EXIT.                                                                
                                                                                
      *======================  END OF PROGRAM  ======================*          
