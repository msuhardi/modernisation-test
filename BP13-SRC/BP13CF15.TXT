       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CF15.                                                 
      *AUTHOR.        ZARITA DANE SARMIENTO.                                    
      *DATE-WRITTEN.  22/06/10.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  TO DELETE CASES THAT MC SUBM DATE IS        *            
      *              AFTER 1 YR                                    *            
      *  INPUT FILE :  1. BP13F399                                 *            
      *  I-O        :  1. BP13K399                                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * BP133681 ZDD 20100622   NEW PROGRAM                        *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BP13F399 ASSIGN TO BP13F399.                                  
                                                                                
            SELECT BP13K399 ASSIGN TO BP13K399                                  
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K399-KEY-FLD                               
                  FILE STATUS     IS WS-K399-STATUS.                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F399                                                             
           RECORD CONTAINS 800 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F399.                                                           
                                                                                
       FD   BP13K399                                                            
            RECORD CONTAINS 800 CHARACTERS.                                     
       COPY BP13K399.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  NO-F399-READ        PIC 9(5)    VALUE ZEROS.                     
           05  NO-K399-DELETED     PIC 9(5)    VALUE ZEROS.                     
           05  WS-BYPASS           PIC 9(5)    VALUE ZEROS.                     
           05  WS-DATE             PIC X(8)    VALUE SPACES.                    
           05  WS-DATE-AFT         PIC 9(8)    VALUE ZEROS.                     
           05  WS-EOF              PIC X(1)    VALUE 'N'.                       
           05  WS-K399-STATUS      PIC 9(2)    VALUE ZEROS.                     
           05  WS-FOUND            PIC X(1)    VALUE 'N'.                       
       01  WS-SUBM-DATE.                                                        
           05  WS-YEAR             PIC 9(4).                                    
           05  WS-MON              PIC 9(2).                                    
           05  WS-DAY              PIC 9(2).                                    
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
           PERFORM 1000-OPENING-ROUTINE  THRU 1000-EXIT.                        
           PERFORM 2000-READ-F399        THRU 2000-EXIT.                        
           PERFORM 4000-PROCESS-FILES    THRU 4000-EXIT                         
                   UNTIL WS-EOF = 'Y'.                                          
           PERFORM 9000-CLOSING-ROUTINE   THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
       1000-OPENING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F399                                                 
                I-O    BP13K399.                                                
                                                                                
           IF WS-K399-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K399 OPEN ERROR, STATUS IS ' WS-K399-STATUS          
              MOVE WS-K399-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
            MOVE FUNCTION CURRENT-DATE TO WS-DATE.                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-F399.                                                          
      *-------------------------------------------------------------            
            READ BP13F399 AT END                                                
                 MOVE 'Y' TO WS-EOF                                             
                 GO TO 2000-EXIT.                                               
                                                                                
            ADD 1 TO NO-F399-READ.                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-PROCESS-FILES.                                                      
      *-------------------------------------------------------------            
           PERFORM 4200-PROCESS-RECORD  THRU 4200-EXIT.                         
           PERFORM 2000-READ-F399 THRU 2000-EXIT.                               
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4200-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
                                                                                
           MOVE  SPACES                 TO BP13K399-REC                         
                                           WS-FOUND.                            
           MOVE  F399-KEY-FLD           TO K399-KEY-FLD                         
           PERFORM 4300-READ-BP13K399   THRU 4300-EXIT                          
                                                                                
           IF WS-FOUND = 'Y'                                                    
             IF K399-DTE-MC-SUBM NOT = ZEROES   AND LOW-VALUES                  
                MOVE K399-DTE-MC-SUBM   TO WS-SUBM-DATE                         
                  ADD 1 TO WS-YEAR                                              
                  IF WS-SUBM-DATE < WS-DATE                                     
                     PERFORM 4400-DELETE-REC   THRU 4400-EXIT                   
                     ADD 1 TO NO-K399-DELETED                                   
                  ELSE                                                          
                     ADD 1 TO WS-BYPASS                                         
                  END-IF                                                        
             ELSE                                                               
                 ADD 1 TO WS-BYPASS                                             
             END-IF                                                             
           ELSE                                                                 
              ADD 1 TO WS-BYPASS                                                
           END-IF.                                                              
                                                                                
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       4300-READ-BP13K399.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13K399                                                        
                                                                                
           EVALUATE WS-K399-STATUS                                              
            WHEN 00                                                             
               MOVE 'Y' TO WS-FOUND                                             
            WHEN 23                                                             
               MOVE 'N' TO WS-FOUND                                             
            WHEN OTHER                                                          
              DISPLAY 'ERROR READING OF BP13K399. STATUS IS '                   
                       WS-K399-STATUS                                           
              MOVE WS-K399-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-EVALUATE.                                                        
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4400-DELETE-REC.                                                         
      *-------------------------------------------------------------            
                                                                                
           DELETE BP13K399                                                      
                                                                                
           IF WS-K399-STATUS NOT  = 00                                          
              DISPLAY 'ERROR DELETING  BP13K399. STATUS IS '                    
                       WS-K399-STATUS                                           
              MOVE WS-K399-STATUS              TO RETURN-CODE                   
              PERFORM  9000-CLOSING-ROUTINE  THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
       4400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       9000-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           CLOSE BP13K399                                                       
                 BP13F399.                                                      
                                                                                
           DISPLAY 'NO. OF F399 REC READ   > ' NO-F399-READ.                    
           DISPLAY 'NO. OF F399 REC DELETED> ' NO-K399-DELETED.                 
           DISPLAY 'NO. OF F399 REC BYPASS > ' WS-BYPASS.                       
                                                                                
           IF WS-K399-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K399 CLOSE ERROR, STATUS IS ' WS-K399-STATUS         
              MOVE WS-K399-STATUS              TO RETURN-CODE                   
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
