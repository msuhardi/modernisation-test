       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C699.                                                 
       AUTHOR.        CVG.                                                      
      *DATE-WRITTEN.  03/02/97.                                                 
                                                                                
      ***************************************************************           
      *                      BP13 SYSTEM                            *           
      *=============================================================*           
      *                                                             *           
      *  OBJECTIVE  :  TO GET THE HOUSEHOLD SIZE OF THE APPLICATION *           
      *                UNDER THE WAITING LIST AND AGE OF HA1        *           
      *  I/O FILE                                                   *           
      *  --------                                                   *           
      *         I   :  BP13F716                                     *           
      *                BP13K820                                     *           
      *                                                             *           
      *         O   :  P13F716O                                    *            
      *                                                             *           
      *-------------------------------------------------------------*           
      *  MODIFICATIONS                                              *           
      *  -------------                                              *           
      *  CHGREQ   OIC   DATE  DESCRIPTION                           *           
      *  ------   ---   ---- ------------                           *           
      * BP130391 YCH2 10101997 AGE PROFILE                          *           
      * BP130418 SGK  31081998 TO CATER FOR Y2K.                    *           
      * BP130553 YCH  06011999 COMPUTE AGE BASED ON DTE-REGN        *           
      * BP130630 NOR  14011999 CHGE F715 F716 EXPAND TO 350         *           
      ***************************************************************           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K820                                                      
                       ASSIGN        TO BP13K820                                
                       ACCESS MODE   IS DYNAMIC                                 
                       ORGANIZATION  IS INDEXED                                 
                       RECORD KEY    IS K820-KEY-FLD                            
                       ALTERNATE KEY IS K820-NUM-NRIC                           
                       WITH DUPLICATES                                          
                       FILE STATUS   IS WS-K820-STATUS.                         
                                                                                
           SELECT BP13F716 ASSIGN TO BP13F716.                                  
                                                                                
           SELECT P13F716O ASSIGN TO P13F716O.                                  
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
                                                                                
       FD  BP13F716                                                             
           BLOCK  CONTAINS 0   RECORDS                                          
           RECORD CONTAINS 350 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F716.                                                           
                                                                                
       FD  P13F716O                                                             
           BLOCK  CONTAINS 0   RECORDS                                          
           RECORD CONTAINS 350 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01  P13F716O-REC           PIC X(350).                                   
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F716-READ-CNT             PIC 9(8)  VALUE ZEROS.              
           05  WS-K820-READ-CNT             PIC 9(8)  VALUE ZEROS.              
           05  WS-K820-NOTFND-CNT           PIC 9(8)  VALUE ZEROS.              
           05  WS-F716O-WRITE-CNT           PIC 9(8)  VALUE ZEROS.              
                                                                                
       01  WS-K820-STATUS                   PIC 9(2)  VALUE ZEROES.             
       01  WS-NO-OCCUP                      PIC 9(2)  VALUE ZEROES.             
       01  WS-F716-EOF                      PIC X     VALUE 'N'.                
       01  WS-FLAG                          PIC X     VALUE 'N'.                
       01  WS-FIRST-IND                     PIC X     VALUE 'Y'.                
       01  WS-HA1-AGE                       PIC 9(4)  VALUE ZEROES.             
       01  WS-K820-BIRTH-YEAR               PIC 9(4)  VALUE ZEROES.             
                                                                                
       01  WS-TIME-CURRENT.                                                     
           05  WS-TME-HH                    PIC XX.                             
           05  WS-TME-MM                    PIC XX.                             
           05  WS-TME-SS                    PIC XX.                             
                                                                                
       01  WS-DTE-CURRENT.                                                      
           05  WS-DTE-CCYY                  PIC X(4).                           
           05  WS-DTE-MM                    PIC XX.                             
           05  WS-DTE-DD                    PIC XX.                             
                                                                                
       01  WS-DTE-REC.                                                          
           05  WS-DTE-REC-CCYY              PIC 9(4).                           
           05  WS-DTE-REC-MM                PIC 99.                             
           05  WS-DTE-REC-DD                PIC 99.                             
                                                                                
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
                                                                                
           PERFORM  1000-OPEN-FILES             THRU 1000-EXIT.                 
           PERFORM  2000-READ-F716              THRU 2000-EXIT                  
                    UNTIL  WS-F716-EOF = 'Y'.                                   
           PERFORM  9000-CLOSE-FILES            THRU 9000-EXIT.                 
                                                                                
                                                                                
      *===========================================================*             
       1000-OPEN-FILES.                                                         
      *===========================================================*             
                                                                                
           OPEN INPUT  BP13F716                                                 
                       BP13K820                                                 
                OUTPUT P13F716O.                                                
                                                                                
           IF WS-K820-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'ERROR IN OPEN BP13K820 : ' WS-K820-STATUS                
              MOVE WS-K820-STATUS           TO RETURN-CODE                      
              PERFORM  9000-CLOSE-FILES     THRU 9000-EXIT                      
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO WS-DTE-CURRENT.                  
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *    ------------------------------------                                 
      *    READ BP13F716 SEQUENTIALLY UNTIL EOF                                 
      *    ------------------------------------                                 
      *=========================================================*               
       2000-READ-F716.                                                          
      *=========================================================*               
                                                                                
           READ BP13F716 AT END                                                 
                MOVE 'Y'                    TO WS-F716-EOF                      
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F716-READ-CNT.                
           MOVE ZEROES                      TO WS-HA1-AGE.                      
           MOVE SPACES                      TO K820-KEY-FLD.                    
           MOVE F716-NUM-REGN               TO K820-NUM-REGN.                   
           MOVE 'N'                         TO WS-FLAG.                         
           MOVE 'Y'                         TO WS-FIRST-IND.                    
                                                                                
           MOVE F716-DTE-REGN(7:2)          TO WS-DTE-REC-DD.                   
           MOVE F716-DTE-REGN(5:2)          TO WS-DTE-REC-MM.                   
           MOVE F716-DTE-REGN(1:4)          TO WS-DTE-REC-CCYY.                 
                                                                                
           START BP13K820 KEY IS  >=  K820-KEY-FLD                              
                                                                                
           IF WS-K820-STATUS = 00                                               
              MOVE  ZEROES             TO WS-NO-OCCUP                           
              PERFORM 3000-READ-K820           THRU 3000-EXIT                   
                   UNTIL F716-NUM-REGN  NOT =  K820-NUM-REGN                    
                      OR  WS-FLAG =  'Y'                                        
           ELSE                                                                 
             IF WS-K820-STATUS = 23 OR 10                                       
      *-----   RECORD NOT FOUND IN BP13K820....  F716-NUM-REGN ---              
                ADD 1 TO WS-K820-NOTFND-CNT                                     
             END-IF                                                             
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=========================================================*               
       3000-READ-K820.                                                          
      *=========================================================*               
                                                                                
           READ BP13K820  NEXT RECORD.                                          
                                                                                
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
                  IF F716-NUM-REGN = K820-NUM-REGN                              
                     IF WS-FIRST-IND = 'Y'                                      
                        MOVE  'N'             TO WS-FIRST-IND                   
                        ADD    1              TO WS-K820-READ-CNT               
                     END-IF                                                     
                     PERFORM 4000-CHECK-HHSIZE THRU 4000-EXIT                   
                  ELSE                                                          
                     PERFORM 6000-WRITE-F716O THRU 6000-EXIT                    
                     IF WS-NO-OCCUP = 0                                         
                        ADD 1                 TO WS-K820-NOTFND-CNT             
                     END-IF                                                     
                     MOVE  ZEROES          TO WS-NO-OCCUP                       
                  END-IF                                                        
              WHEN 23                                                           
              WHEN 10                                                           
                  PERFORM 6000-WRITE-F716O THRU 6000-EXIT                       
                  MOVE  ZEROES          TO WS-NO-OCCUP                          
                  MOVE 'Y'                    TO WS-FLAG                        
                  GO TO 3000-EXIT                                               
              WHEN OTHER                                                        
      *           MOVE 'Y'                    TO WS-FLAG                        
                  DISPLAY 'SERIOUS ERROR READING BP13K820'                      
                  DISPLAY 'K820-STATUS = ' WS-K820-STATUS                       
                  GO TO 9000-CLOSE-FILES                                        
           END-EVALUATE.                                                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=========================================================*               
       4000-CHECK-HHSIZE.                                                       
      *=========================================================*               
                                                                                
           ADD  1  TO  WS-NO-OCCUP.                                             
           IF K820-NUM-RELATIONSHIP = '00'                                      
              IF K820-DTE-BIRTH NOT = SPACES AND LOW-VALUES                     
                 MOVE K820-DTE-BIRTH (1:4) TO WS-K820-BIRTH-YEAR                
                 COMPUTE WS-HA1-AGE = WS-DTE-REC-CCYY -                         
                                      WS-K820-BIRTH-YEAR                        
                 MOVE WS-HA1-AGE (2:3) TO F716-NUM-HA1-AGE                      
              ELSE                                                              
                 MOVE SPACES TO F716-NUM-HA1-AGE                                
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=========================================================*               
       6000-WRITE-F716O.                                                        
      *=========================================================*               
                                                                                
           MOVE  SPACES                     TO P13F716O-REC.                    
           INITIALIZE                       P13F716O-REC.                       
           MOVE  WS-NO-OCCUP                TO F716-NUM-HHSIZE.                 
           MOVE  BP13F716-REC               TO P13F716O-REC.                    
           WRITE P13F716O-REC.                                                  
           ADD 1                            TO WS-F716O-WRITE-CNT.              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *==========================================================*              
       9000-CLOSE-FILES.                                                        
      *==========================================================*              
                                                                                
           CLOSE BP13F716                                                       
                 BP13K820                                                       
                 P13F716O.                                                      
                                                                                
           IF WS-K820-STATUS NOT = ZEROES                                       
              DISPLAY 'ERROR IN CLOSE BP13K820 : ' WS-K820-STATUS               
              MOVE WS-K820-STATUS            TO RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '     ***** BP13C699 TOTAL *****'.                           
                                                                                
           DISPLAY 'NO OF BP13F716 READ      ' WS-F716-READ-CNT.                
           DISPLAY 'NO OF BP13K820 FOUND     ' WS-K820-READ-CNT.                
           DISPLAY 'NO OF BP13K820 NOT FOUND ' WS-K820-NOTFND-CNT.              
           DISPLAY '      P13F716O WRITTEN   ' WS-F716O-WRITE-CNT.              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
