      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C02J.                                                 
      *AUTHOR.        BALA AMUTHAN.                                             
      *DTE WRITTEN.   21/07/2006.                                               
      **********************************************************                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      ***********************************************************               
      *                                                         *               
      *    OBJECTIVES  :                                        *               
      *                                                         *               
      *    1.  TO CREATE APPLICATION DATA ON DBSS REQUEST FOR   *               
      *         DEVELOPER VIA SFTP.                             *               
      *                                                         *               
      *    2.  TO EXTRACT APPL DATA FROM PREV DAY 7PM TO TODAY  *               
      *        7.59AM FOR THE 1ST JOB AT 8.05AM.                *               
      *                                                         *               
      *    3.  TO EXTRACT APPL DATA HOURLY FOR 9.05AM TO 7.05PM *               
      *        I.E. 9.05AM JOB WILL CONTAIN 8AM TO 8.59AM CASES *               
      *                                                         *               
      *    INPUT FILES : BP13FB78 - DBS FILE                    *               
      *                                                         *               
      *    OUTPUT FILE : P13FB78A - TIME SLOT RECORDS           *               
      *                                                         *               
      *---------------------------------------------------------*               
      * CHG-NO   BY    DATE   DETAILS                           *               
      * ======= ====  ======  ==================================*               
      * BP132888 BA2  210706  NEW PROGRAM                       *               
      * BP132886 CKK7 210806  CHANGE THE TIME FROM 1800 TO 1900 *               
      * ========================================================*               
                                                                                
      *-----------------------*                                                 
       ENVIRONMENT DIVISION.                                                    
      *-----------------------*                                                 
       CONFIGURATION SECTION.                                                   
      *-----------------------*                                                 
                                                                                
       SOURCE-COMPUTER. IBM-9121.                                               
       OBJECT-COMPUTER. IBM-9121.                                               
                                                                                
      *-----------------------*                                                 
       INPUT-OUTPUT SECTION.                                                    
      *-----------------------*                                                 
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FB78 ASSIGN    TO BP13FB78.                               
                                                                                
           SELECT P13FB78A ASSIGN    TO P13FB78A.                               
                                                                                
                                                                                
      *-----------------------*                                                 
       DATA DIVISION.                                                           
      *-----------------------*                                                 
       FILE SECTION.                                                            
      *-----------------------*                                                 
                                                                                
       FD   BP13FB78                                                            
            BLOCK      CONTAINS 0    RECORDS                                    
            RECORD     CONTAINS 3300 CHARACTERS                                 
            LABEL      RECORDS  ARE  STANDARD                                   
            RECORDING  MODE     IS  F.                                          
       COPY BP13FB78.                                                           
                                                                                
                                                                                
       FD   P13FB78A                                                            
            BLOCK     CONTAINS 0   RECORDS                                      
            RECORD    CONTAINS 3300 CHARACTERS                                  
            LABEL     RECORDS  ARE OMITTED                                      
            RECORDING MODE     IS  F.                                           
       01  P13FB78A-REC                PIC X(3300).                             
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-FB78-EOF-SW             PIC X(1)    VALUE 'N'.                
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-CNT-FB38A               PIC 9(5) COMP  VALUE 0.               
           05  WS-CNT-FB38                PIC 9(5) COMP  VALUE 0.               
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-TODAY-DATE.                                                   
               10  WS-SYS-YY              PIC 9(4).                             
               10  WS-SYS-MM              PIC 9(2).                             
               10  WS-SYS-DD              PIC 9(2).                             
           05  WS-TODAY-DATE1 REDEFINES WS-TODAY-DATE PIC 9(8).                 
           05  WS-TIME-NUM                PIC 9(9).                             
           05  WS-TIME REDEFINES WS-TIME-NUM.                                   
               10  WS-HH                  PIC 9(2).                             
               10  WS-MM                  PIC 9(2).                             
               10  WS-SS                  PIC 9(2).                             
               10  WS-GG                  PIC 9(2).                             
                                                                                
       01  WS-DATE-COMPUTE.                                                     
           05  WS-DATE-INT                PIC S9(9).                            
                                                                                
       01  WS-HHMM-START.                                                       
           05  WS-START-HH                PIC 9(2).                             
           05  WS-START-MM                PIC 9(2).                             
                                                                                
       01  WS-HHMM-END.                                                         
           05  WS-END-HH                  PIC 9(2).                             
           05  WS-END-MM                  PIC 9(2).                             
                                                                                
       01  WS-CHECK-DATE.                                                       
           05  WS-FB78-DTE-REQUESTED      PIC X(8).                             
           05  WS-DATE-CHAR REDEFINES WS-FB78-DTE-REQUESTED.                    
               10  WS-FB78-DATE           PIC 9(8).                             
                                                                                
       01  WS-CHECK-TIME.                                                       
           05  WS-FB78-TME-REQUESTED      PIC X(8).                             
           05  WS-HH-CHAR2 REDEFINES WS-FB78-TME-REQUESTED.                     
               10  WS-FB78-HHMM           PIC 9(4).                             
               10  WS-FB78-SS             PIC 9(2).                             
               10  WS-FB78-GG             PIC 9(2).                             
                                                                                
       77  WS-PREV-DATE                   PIC 9(8).                             
                                                                                
      *-----------------------*                                                 
       PROCEDURE DIVISION.                                                      
      *-----------------------*                                                 
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
           PERFORM  1000-OPEN-FILES         THRU 1000-EXIT.                     
           PERFORM  2000-MAIN-ROUTINE       THRU 2000-EXIT.                     
           PERFORM  9000-CLOSE-FILES        THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-FILES.                                                         
      ******************************************************************        
           OPEN INPUT  BP13FB78                                                 
                OUTPUT P13FB78A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYSTEM-DATE                         
                                                                                
           IF WS-HH < 09                                                        
              MOVE 19     TO WS-START-HH                                        
              MOVE 00     TO WS-START-MM                                        
              MOVE WS-HH  TO WS-END-HH                                          
              MOVE 00     TO WS-END-MM                                          
              COMPUTE WS-DATE-INT  =                                            
                               FUNCTION INTEGER-OF-DATE(WS-TODAY-DATE1)         
              COMPUTE WS-DATE-INT  = WS-DATE-INT - 1                            
              COMPUTE WS-PREV-DATE =                                            
                               FUNCTION DATE-OF-INTEGER(WS-DATE-INT)            
           ELSE                                                                 
              COMPUTE WS-START-HH = WS-HH - 1                                   
              MOVE 00       TO WS-START-MM                                      
              MOVE    WS-HH TO WS-END-HH                                        
              MOVE 00       TO WS-END-MM                                        
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2000-MAIN-ROUTINE.                                                       
      ******************************************************************        
           PERFORM  3000-BP13FB78-READ     THRU 3000-EXIT.                      
                                                                                
           PERFORM  4000-PROCESS-FB78-REC  THRU 4000-EXIT                       
              UNTIL WS-FB78-EOF-SW = 'Y'.                                       
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       3000-BP13FB78-READ.                                                      
      ******************************************************************        
           READ BP13FB78                                                        
              AT END MOVE 'Y'     TO WS-FB78-EOF-SW                             
              GO TO  3000-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD +1                 TO WS-CNT-FB38.                               
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      **********************************************************                
       4000-PROCESS-FB78-REC.                                                   
      **********************************************************                
                                                                                
           IF FB78-NUM-REGN      = SPACE OR LOW-VALUES                          
              PERFORM 3000-BP13FB78-READ THRU 3000-EXIT                         
              GO TO 4000-EXIT.                                                  
                                                                                
           MOVE FB78-TME-REQUESTED  TO WS-FB78-TME-REQUESTED.                   
           MOVE FB78-DTE-REQUESTED  TO WS-FB78-DTE-REQUESTED.                   
                                                                                
           IF WS-HH < 09                                                        
              IF WS-PREV-DATE   =  WS-FB78-DATE  AND                            
                 WS-HHMM-START <=  WS-FB78-HHMM                                 
                 PERFORM 5000-FB60-WRITE         THRU 5000-EXIT                 
                                                                                
              ELSE                                                              
                IF  WS-TODAY-DATE  =  WS-FB78-DATE AND                          
                    (0000         <=  WS-FB78-HHMM AND                          
                    WS-HHMM-END   >   WS-FB78-HHMM)                             
                    PERFORM 5000-FB60-WRITE         THRU 5000-EXIT              
                END-IF                                                          
              END-IF                                                            
           ELSE                                                                 
              IF (WS-TODAY-DATE  = WS-FB78-DATE) AND                            
                 (WS-HHMM-START <= WS-FB78-HHMM  AND                            
                 WS-HHMM-END   >  WS-FB78-HHMM)                                 
                 PERFORM 5000-FB60-WRITE         THRU 5000-EXIT                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3000-BP13FB78-READ      THRU 3000-EXIT.                      
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************************************************                
       5000-FB60-WRITE.                                                         
      **********************************************************                
           MOVE    BP13FB78-REC  TO  P13FB78A-REC.                              
           WRITE   P13FB78A-REC.                                                
                                                                                
           ADD +1                 TO WS-CNT-FB38A.                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************************************************                
       9000-CLOSE-FILES.                                                        
      **********************************************************                
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13C02J'.                                     
           DISPLAY '---------------------'.                                     
           DISPLAY 'NO OF BP13FB38 RECORDS READ..........: '                    
                    WS-CNT-FB38.                                                
           DISPLAY 'NO OF BP13FB38 RECORDS WRITTEN.......: '                    
                    WS-CNT-FB38A.                                               
                                                                                
           CLOSE BP13FB78                                                       
                 P13FB78A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
