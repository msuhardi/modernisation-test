       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C223.                                                 
       AUTHOR.        LMS.                                                      
       DATE-WRITTEN.  16/03/98.                                                 
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO READ FIS INVENTORY FILE AND EXTRACT*                
      *                  THE LATEST SCHEME ACCT.               *                
      *                                                        *                
      *    INPUT FILE  : BM06F110                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY    DATE     DETAILS                         *                
      *-------- --- --------   -------                         *                
      *BP130418 LMS 10/07/98   TO CHANGE BM06F211 TO BM06F110. *                
      *BP130418 LMS 18/11/98   TO AMEND RECORD LENGTH.         *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06F110 ASSIGN TO BM06F110.                                  
           SELECT BM6F110A ASSIGN TO BM6F110A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BM06F110                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BM06F110.                                                           
                                                                                
       FD  BM6F110A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  BM6F110A-REC                   PIC X(500).                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F110-READ                PIC 9(5).                             
           05 WS-F110-WRITE               PIC 9(5).                             
                                                                                
       01  WS-EOF-F110                    PIC 9.                                
           88 EOF-F110                    VALUE 1.                              
                                                                                
       01  WS-PREV-REC.                                                         
           05 WS-PREV-SCH-ACCNT           PIC X(9).                             
           05 FILLER                      PIC X(491).                           
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      ******************************************************************        
       000-MAIN-ROUTINE.                                                        
      ******************************************************************        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F110      THRU 200-EXIT.                            
           MOVE F110-REC              TO   WS-PREV-REC.                         
           PERFORM 300-PROCESS-F110   THRU 300-EXIT                             
             UNTIL EOF-F110.                                                    
           PERFORM 400-WRITE-LASTREC  THRU 400-EXIT.                            
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       100-INITIALIZATION.                                                      
      ******************************************************************        
                                                                                
           OPEN INPUT  BM06F110                                                 
                OUTPUT BM6F110A.                                                
                                                                                
           MOVE ZEROS                   TO WS-COUNTERS                          
                                           WS-EOF-F110.                         
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       200-READ-F110.                                                           
      ******************************************************************        
                                                                                
           READ BM06F110                                                        
                AT END                                                          
                   MOVE 1 TO WS-EOF-F110                                        
                   GO TO 200-EXIT.                                              
                                                                                
           ADD 1          TO WS-F110-READ.                                      
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-F110.                                                        
      ******************************************************************        
                                                                                
           IF WS-PREV-SCH-ACCNT NOT = F110-KEY-FLD (1:9)                        
              WRITE BM6F110A-REC FROM WS-PREV-REC                               
              ADD 1              TO   WS-F110-WRITE                             
           END-IF.                                                              
                                                                                
           MOVE F110-REC         TO   WS-PREV-REC.                              
                                                                                
           PERFORM 200-READ-F110 THRU 200-EXIT.                                 
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       400-WRITE-LASTREC.                                                       
      ******************************************************************        
                                                                                
           WRITE BM6F110A-REC FROM WS-PREV-REC.                                 
           ADD 1              TO   WS-F110-WRITE.                               
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       999-CLOSE-FILES.                                                         
      ******************************************************************        
                                                                                
           CLOSE BM06F110                                                       
                 BM6F110A.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*=========== BP13C223 ===========*'.                        
           DISPLAY 'NO. OF F110 RECORDS READ  = ' WS-F110-READ.                 
           DISPLAY 'NO. OF F110 RECORDS WRITE = ' WS-F110-WRITE.                
           DISPLAY '*================================*'.                        
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
            EXIT.                                                               
                                                                                
