       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C93K.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  25/05/2017.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *                                                             *           
      *   OBJECTIVE   : ADD MGPS TOTAL BY PROJECT, WRITE TO  OUTPUT *           
      *                 BP13FW3C FOR RECORD TOTAL-APPL NOT =99999   *           
      *                                                             *           
      *   NOTE        : FOR 2-ROOM, WRITE 2 RECORDS (2A & 2B)       *           
      *                    2A - FLOOR AREA <= 40                    *           
      *                    2B - FLOOR AREA >  40                    *           
      *                 FOR 5-ROOM, WRITE 2 RECORDS (00 & 5G)       *           
      *                    00 - MODL DESIGN = OTHERS                *           
      *                    5G - MODL DESIGN = 90 OR 91              *           
      *                                                             *           
      *   INPUT FILES : BP13FW3C - BOOKING SUMMARY                  *           
      *   OUTPUT FILE : P13FW3CA - UPDATED FILE                     *           
      *                                                             *           
      * =========================================================== *           
      * CHQ REQ  DATE       BY   DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136299 25/05/2017 SMR2 NEW PROGRAM                        *           
      * BP136299 30/05/2017 SMR2 CATER FOR NUM-FLAT-MGPS            *           
      * BP136832 02/06/2017 SMR2 CATER FOR FLAT-TYPE '3P'           *           
      * BP137073 13/11/2017 FNP1 WRITE FW3CA AFTER MATCHING IS DONE *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FW3C ASSIGN       TO BP13FW3C.                            
                                                                                
           SELECT P13FW3CA ASSIGN       TO P13FW3CA.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13FW3C                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD     IS STANDARD                                        
            RECORDING MODE   IS F.                                              
       COPY BP13FW3C.                                                           
                                                                                
       FD   P13FW3CA                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 500 CHARACTERS                                      
            LABEL RECORD     IS STANDARD                                        
            RECORDING MODE   IS F.                                              
       01   P13FW3CA-REC                    PIC X(500).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FW3C-READ                 PIC 9(06)   VALUE ZEROES.           
           05  WS-FW3CA-UPDATE              PIC 9(06)   VALUE ZEROES.           
           05  WS-FW3CA-WRITE               PIC 9(06)   VALUE ZEROES.           
                                                                                
       01  WS-FW3C-COUNTERS.                                                    
           05  WS-FLAT-PROJ                 PIC 9(03)   VALUE ZEROES.           
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-CURR-DATE                 PIC X(08)   VALUE SPACES.           
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-FW3C-KEY-FLD              PIC X(14)   VALUE SPACES.           
           05  WS-PREV-FW3C-KEY             PIC X(14)   VALUE SPACES.           
           05  WS-BP13FW3C-REC OCCURS 10 TIMES.                                 
               10  WS-FW3C-REC              PIC X(500)  VALUE SPACES.           
           05  WS-CNT                       PIC 99      VALUE ZEROES.           
           05  WS-I                         PIC 99      VALUE ZEROES.           
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2500-READ-BP13FW3C       THRU 2500-EXIT.                     
           MOVE WS-FW3C-KEY-FLD             TO WS-PREV-FW3C-KEY.                
           PERFORM 3000-PROCESS-RECS        THRU 3000-EXIT                      
             UNTIL WS-FW3C-KEY-FLD = HIGH-VALUES.                               
           IF WS-FW3C-KEY-FLD = HIGH-VALUES                                     
              PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > WS-CNT              
                 PERFORM 7000-WRITE-P13FW3CA     THRU 7000-EXIT                 
              END-PERFORM                                                       
           END-IF.                                                              
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13FW3C                                                 
                OUTPUT P13FW3CA.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-CURR-DATE.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------*                                                     
       2500-READ-BP13FW3C.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13FW3C AT END                                                 
                MOVE HIGH-VALUES            TO WS-FW3C-KEY-FLD                  
                GO TO 2500-EXIT.                                                
                                                                                
           ADD 1                            TO WS-FW3C-READ.                    
                                                                                
           MOVE FW3C-KEY-FLD(1:14)          TO WS-FW3C-KEY-FLD.                 
                                                                                
           IF FW3C-NUM-FLAT-MGPS IS NOT NUMERIC                                 
              MOVE ZEROS TO FW3C-NUM-FLAT-MGPS                                  
           END-IF.                                                              
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-RECS.                                                       
      *-----------------*                                                       
                                                                                
                                                                                
           IF WS-FW3C-KEY-FLD = WS-PREV-FW3C-KEY                                
              PERFORM 4000-COMPUTE-MGPS-FLAT THRU 4000-EXIT                     
              PERFORM 2500-READ-BP13FW3C     THRU 2500-EXIT                     
           ELSE                                                                 
              PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > WS-CNT              
                 PERFORM 7000-WRITE-P13FW3CA     THRU 7000-EXIT                 
              END-PERFORM                                                       
              PERFORM VARYING WS-I FROM 1 BY 1 UNTIL WS-I > 10                  
                 MOVE SPACES       TO WS-FW3C-REC(WS-I)                         
              END-PERFORM                                                       
              MOVE WS-FW3C-KEY-FLD  TO WS-PREV-FW3C-KEY                         
              MOVE ZEROS        TO WS-FLAT-PROJ                                 
                                   WS-CNT                                       
              PERFORM 4000-COMPUTE-MGPS-FLAT THRU 4000-EXIT                     
              PERFORM 2500-READ-BP13FW3C       THRU 2500-EXIT                   
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4000-COMPUTE-MGPS-FLAT.                                                  
      *------------------*                                                      
                                                                                
           COMPUTE WS-FLAT-PROJ = WS-FLAT-PROJ + FW3C-NUM-FLAT-MGPS.            
                                                                                
           IF FW3C-NUM-TOT-APPL = 99999                                         
              MOVE BP13FW3C-REC        TO P13FW3CA-REC                          
              WRITE P13FW3CA-REC                                                
              ADD 1                    TO WS-FW3CA-WRITE                        
           ELSE                                                                 
              ADD 1                    TO WS-CNT                                
              MOVE BP13FW3C-REC        TO WS-FW3C-REC(WS-CNT)                   
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       7000-WRITE-P13FW3CA.                                                     
      *--------------------*                                                    
                                                                                
           MOVE WS-FLAT-PROJ        TO WS-FW3C-REC(WS-I)(352:3).                
           ADD 1                    TO WS-FW3CA-UPDATE                          
                                                                                
           MOVE WS-FW3C-REC(WS-I)   TO P13FW3CA-REC.                            
           WRITE P13FW3CA-REC.                                                  
           ADD 1                       TO WS-FW3CA-WRITE.                       
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '*===========================================*'.             
           DISPLAY '  BP13C93K CONTROL TOTAL     : ' WS-CURR-DATE.              
           DISPLAY '*-------------------------------------------*'.             
           DISPLAY '  RECS READ FROM BP13FW3C    : ' WS-FW3C-READ.              
           DISPLAY '  RECS UPDATE  IN P13FW3CA   : ' WS-FW3CA-UPDATE.           
           DISPLAY '  RECS WRITTEN IN P13FW3CA   : ' WS-FW3CA-WRITE.            
           DISPLAY '*===========================================*'.             
                                                                                
           CLOSE BP13FW3C                                                       
                 P13FW3CA.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
