      *--------------------------------------------------------*                
       IDENTIFICATION DIVISION.                                                 
      *--------------------------------------------------------*                
       PROGRAM-ID. BP13C169.                                                    
      *AUTHOR. SK.                                                              
      *DATE-WRITTEN.   18/11/2004.                                              
      *--------------------------------------------------------*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730   ASSIGN TO BP13F730.                                
           SELECT BP13F558   ASSIGN TO BP13F558.                                
           SELECT P13F730A   ASSIGN TO P13F730A.                                
                                                                                
      *--------------------------------------------------------*                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------*                
                                                                                
       FD   BP13F558                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       COPY BP13F558.                                                           
                                                                                
       FD   BP13F730                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       01  BP13F730-REC.                                                        
           05  FILLER                             PIC X(4).                     
           05  F730-ZW                            PIC X(1).                     
           05  F730-ZP                            PIC X(1).                     
           05  F730-LS                            PIC X(1).                     
           05  F730-XS                            PIC X(1).                     
           05  FILLER                             PIC X(1).                     
           05  F730-TRF                           PIC X(1).                     
           05  F730-SLS                           PIC X(1).                     
           05  F730-MAMS                          PIC X(1).                     
           05  FILLER  REDEFINES F730-MAMS.                                     
               10 F730-FL                         PIC X(1).                     
           05  F730-PC                            PIC X(1).                     
           05  F730-SB                            PIC X(1).                     
           05  F730-S7                            PIC X(1).                     
           05  F730-AS                            PIC X(1).                     
           05  F730-HZ                            PIC X(1).                     
           05  FILLER                             PIC X(11).                    
           05  F730-NUM-SEQ-REF                   PIC X(6).                     
           05  F730-KEY-FLD.                                                    
               10  F730-NUM-REF                   PIC X(6).                     
           05  F730-ALT1-KEY.                                                   
               10  F730-NUM-NRIC1                 PIC X(9).                     
           05  F730-ALT3-KEY.                                                   
               10  F730-NUM-NRIC3                 PIC X(9).                     
           05  F730-ALT4-KEY.                                                   
               10  F730-NUM-NRIC4                 PIC X(9).                     
           05  F730-NUM-MTH-RESIDE                PIC 9(03).                    
           05  F730-NUM-LOW-FLOOR                 PIC X(01).                    
           05  F730-NUM-PREV-REF                  PIC X(06).                    
           05  F730-CDE-ZONE                      PIC X(02).                    
           05  F730-CDE-COHORT                    PIC X(02).                    
           05  F730-ALT2-KEY.                                                   
               10  F730-NUM-NRIC2                 PIC X(9).                     
           05  F730-NUM-SHORTLIST-PA-HHTY         PIC X(5).                     
           05  F730-NUM-SHORTLIST-PA-CAT          PIC X(5).                     
           05  F730-TME-QUEUE                     PIC X(2).                     
           05  F730-NUM-SHORTLIST-PRIO-HHTY       PIC X(5).                     
           05  F730-NUM-SHORTLIST-PRIO-CAT        PIC X(5).                     
           05  F730-CDE-NT-WITH-LAND              PIC X(1).                     
           05  F730-NUM-WAIT-TIME                 PIC 9(2).                     
           05  F730-CDE-HOUSEHOLD                 PIC X(1).                     
           05  F730-NUM-TWO-ROOMER                PIC X(1).                     
           05  F730-NUM-MCPS                      PIC X(1).                     
           05  F730-CDE-SERVE                     PIC X(1).                     
           05  F730-NUM-HOUSEHOLD-FREEZE          PIC X(1).                     
           05  F730-CDE-RESIDENT-HA3              PIC X(1).                     
           05  F730-CDE-RESIDENT-HA4              PIC X(1).                     
           05  F730-KEY-NEW.                                                    
               10  F730-NUM-REGN                  PIC X(8).                     
           05  F730-NUM-SEQUENCE                  PIC X(8).                     
           05  F730-NUM-CUR-REGN                  PIC X(8).                     
           05  F730-NUM-EXIST-REGN                PIC X(8).                     
           05  F730-NUM-JB-PARENT-REGN            PIC X(8).                     
           05  F730-NUM-GRO-RANK                  PIC X(2).                     
           05  F730-TME-PIGEONHOLE                PIC X(2).                     
           05  F730-TME-QUEUE-PRIO-IFPA           PIC X(2).                     
           05  F730-NUM-HHTY-QUEUE.                                             
               10  F730-NUM-HHTY-QUEUE-ALPHA      PIC X(1).                     
               10  F730-NUM-HHTY-QUEUE-SERIAL     PIC X(5).                     
           05  F730-NUM-5YR-OCCUPN                PIC X(1).                     
           05  F730-CDE-JB-PARENT                 PIC X(1).                     
           05  F730-NUM-JB-REGN                   PIC X(8).                     
           05  F730-CDE-EXIST-FLAT-TYPE           PIC X(2).                     
           05  F730-CDE-NT1                       PIC X(3).                     
           05  F730-CDE-NT2                       PIC X(3).                     
           05  F730-CDE-NT3                       PIC X(3).                     
           05  F730-CDE-NT4                       PIC X(3).                     
           05  F730-CDE-NT5                       PIC X(3).                     
           05  FILLER                             PIC X(9).                     
           05  F730-CDE-FLAT-TYPE                 PIC X(2).                     
           05  F730-CDE-ALLOC-SCH                 PIC X(3).                     
           05  F730-CDE-JB                        PIC X(1).                     
           05  F730-NUM-MTH-RES-JB-IND            PIC 9(3).                     
           05  F730-NUM-MTH-SUBLET                PIC 9(3).                     
           05  F730-NUM-JB-CNT                    PIC 9(2).                     
           05  F730-CDE-CATEGORY                  PIC X(1).                     
           05  F730-CDE-SUCC-NT-ZONE              PIC X(3).                     
           05  F730-CDE-UNSUCC                    PIC X(1).                     
           05  F730-NUM-BALLOT-POSITION           PIC 9(5).                     
           05  F730-NUM-NT-FT-QUEUE               PIC X(5).                     
           05  F730-DTE-REGN.                                                   
               10  F730-DTE-ISSUE-REGN                PIC X(8).                 
           05  F730-CDE-ELIG-SCH                  PIC X(3).                     
           05  F730-NUM-UNSUCC-REQUEST            PIC X(2).                     
           05  F730-CDE-RESIDENT-HA1              PIC X(1).                     
           05  F730-CDE-RESIDENT-HA2              PIC X(1).                     
           05  F730-CDE-PARENT-CHILD              PIC X(1).                     
           05  F730-CDE-BO                        PIC X(2).                     
           05  F730-CDE-ELIG-TAG                  PIC X(2).                     
           05  F730-CDE-REQUEST-STATUS            PIC X.                        
           05  F730-DTE-REQUEST                   PIC X(8).                     
           05  FILLER                             PIC X(8).                     
           05  F730-NUM-2ND-ALLOC-SCH             PIC X(3).                     
           05  F730-NUM-JB-QUEUE-SEQUENCE         PIC X(8).                     
           05  F730-NUM-REQUEST-REASON            PIC X(2).                     
           05  FILLER                             PIC X(2).                     
           05  F730-NUM-USER-ID                   PIC X(8).                     
           05  F730-CDE-PRINT-STATUS              PIC X(1).                     
           05  F730-NUM-SHORTLIST-QUEUE.                                        
               10  F730-NUM-SLIST-PA-ZNFT         PIC X(05).                    
               10  F730-NUM-SLIST-PA-ZNFT-CAT     PIC X(05).                    
               10  F730-NUM-SLIST-PRIO-ZNFT       PIC X(05).                    
               10  F730-NUM-SLIST-PRIO-ZNFT-CAT   PIC X(05).                    
           05  F730-NUM-CA-TAG                    PIC X(1).                     
           05  F730-NUM-SPR-TAG                   PIC X(1).                     
           05  F730-NUM-SUBSIDISED-LOAN           PIC X(1).                     
           05  F730-NUM-CAP                       PIC X(1).                     
           05  F730-NUM-RES-NRIC1                 PIC X(9).                     
           05  F730-DTE-BALLOT                    PIC X(6).                     
           05  F730-NUM-JB-WITHIN-PA-RANGE        PIC X(1).                     
           05  F730-NUM-JB-BETTER-QUEUE           PIC X(1).                     
           05  F730-CDE-IDP-SCH                   PIC X(03).                    
           05  F730-TME-QUEUE-PREVQTR             PIC X(02).                    
           05  F730-NUM-PPO                       PIC X(01).                    
           05  F730-CDE-NT-ASSIGN                 PIC X(03).                    
           05  F730-CDE-REJECT                    PIC X(01).                    
           05  F730-CDE-UPD-AFTER-BALLOT          PIC X(01).                    
           05  F730-NUM-HA-OPT                    PIC X(01).                    
           05  F730-NUM-JB-REF                    PIC X(06).                    
           05  F730-NUM-DEBAR                     PIC X(2).                     
           05  F730-DTE-1ST-LETTER                PIC X(8).                     
           05  F730-NUM-OWN-PTE-PRTY              PIC X(1).                     
           05  F730-NUM-DISPOSED-PTE-PRTY         PIC X(1).                     
           05  F730-NUM-APPLN-TYPE                PIC X(1).                     
           05  F730-NUM-WARD                      PIC X(3).                     
           05  F730-NUM-ALLO-CAT                  PIC X(3).                     
           05  F730-NUM-RES-NRIC2                 PIC X(9).                     
           05  F730-NUM-LOAN-TAG                  PIC X(1).                     
           05  F730-NUM-PREM-CDE                  PIC X(3).                     
           05  F730-NUM-MORT-LOAN-POLICY          PIC X(2).                     
           05  F730-NUM-ELIG-LOAN-POLICY          PIC X(2).                     
           05  F730-NUM-ORIG-LOAN-TAG             PIC X(1).                     
           05  F730-NUM-SMS-STATUS                PIC X(1).                     
           05  F730-NUM-SMS-FT                    PIC X(2).                     
           05  F730-NUM-RSL-STATUS                PIC X(1).                     
           05  F730-NUM-RSL-FT                    PIC X(2).                     
           05  F730-NUM-RDB-STATUS                PIC X(1).                     
           05  F730-NUM-RDB-FT                    PIC X(2).                     
           05  F730-NUM-APPLN-SERIAL              PIC X(5).                     
           05  F730-NUM-JB-NRIC                   PIC X(9).                     
           05  F730-CDE-REQ-ALLOC-SCH             PIC X(3).                     
           05  F730-NUM-BUYBACK                   PIC X(1).                     
           05  F730-CDE-DIFF-ETHNIC               PIC X(1).                     
           05  F730-DTE-PRPSD-SALE                PIC X(8).                     
           05  F730-NUM-PORT-ELIG-TAG             PIC X(1).                     
           05  F730-NUM-MCPS-POSTAL               PIC X(6).                     
           05  FILLER                             PIC X(36).                    
           05  F730-KEY-FLD-FOR-K595.                                           
               10  F730-NUM-REGN-REF              PIC X(09).                    
               10  F730-DTE-UPDATE                PIC X(08).                    
               10  F730-TME-SYSTEM                PIC X(08).                    
                                                                                
       FD   P13F730A                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
       01   P13F730A-REC   PIC X(500).                                          
                                                                                
      *--------------------------------------------------------*                
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------*                
       01  WORK-AREAS.                                                          
           05 WS-READ-F730              PIC 9(8)    VALUE 0.                    
           05 WS-READ-F558              PIC 9(8)    VALUE 0.                    
           05 WS-MATCH                  PIC 9(8)    VALUE 0.                    
           05 WS-F558-FND               PIC 9(8)    VALUE 0.                    
           05 WS-F730-FND               PIC 9(8)    VALUE 0.                    
           05 WS-WRITE-F730A            PIC 9(8)    VALUE 0.                    
           05 WS-FLAG.                                                          
      **                                                                        
      ** BP133320  -  FOR BP13S16A, COL 5, 6, 7, 8                              
              10 WS-ZW                  PIC X(1)    VALUE SPACES.               
              10 WS-ZP                  PIC X(1)    VALUE SPACES.               
              10 WS-LS                  PIC X(1)    VALUE SPACES.               
              10 WS-XS                  PIC X(1)    VALUE SPACES.               
      **                                                                        
              10 WS-SLS                 PIC X(1)    VALUE SPACES.               
              10 WS-MAMS                PIC X(1)    VALUE SPACES.               
              10 FILLER REDEFINES WS-MAMS.                                      
                 15 WS-FL               PIC X(1).                               
              10 WS-TRF                 PIC X(1)    VALUE SPACES.               
              10 WS-PC                  PIC X(1)    VALUE SPACES.               
              10 WS-SB                  PIC X(1)    VALUE SPACES.               
              10 WS-S7                  PIC X(1)    VALUE SPACES.               
              10 WS-AS                  PIC X(1)    VALUE SPACES.               
              10 WS-HZ                  PIC X(1)    VALUE SPACES.               
                                                                                
       01  WS-SYSTEM-DATE.                                                      
           05  WS-SYS-CCYY            PIC 9(4).                                 
           05  WS-SYS-MM              PIC 9(2).                                 
           05  WS-SYS-DD              PIC 9(2).                                 
                                                                                
       01  WS-EDITED-DATE             PIC X(10).                                
       01  WS-TEMP-REC                PIC X(300) VALUE SPACES.                  
                                                                                
      *--------------------------------------------------------*                
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------*                
                                                                                
      *--------------------------------------------------------*                
       0000-MAIN.                                                               
      *--------------------------------------------------------*                
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-F730     THRU 2000-EXIT.                           
           PERFORM 3000-READ-F558     THRU 3000-EXIT.                           
                                                                                
           PERFORM 4000-COMPARE       THRU 4000-EXIT                            
                   UNTIL F730-NUM-REGN = HIGH-VALUES                            
                     AND F558-NUM-REGN  = HIGH-VALUES.                          
                                                                                
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       1000-OPEN-FILES.                                                         
      *--------------------------------------------------------*                
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13F558                                                 
           OUTPUT      P13F730A.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE   TO WS-SYSTEM-DATE.                      
                                                                                
           STRING WS-SYS-DD '/' WS-SYS-MM '/' WS-SYS-CCYY                       
                  DELIMITED BY SIZE                                             
                  INTO WS-EDITED-DATE                                           
           END-STRING.                                                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       2000-READ-F730.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BP13F730                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F730-NUM-REGN                            
                NOT AT END                                                      
                   ADD 1 TO WS-READ-F730                                        
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       3000-READ-F558.                                                          
      *--------------------------------------------------------*                
                                                                                
           READ BP13F558                                                        
                AT END                                                          
                   MOVE HIGH-VALUES TO F558-NUM-REGN                            
                NOT AT END                                                      
                   ADD 1 TO WS-READ-F558                                        
           END-READ.                                                            
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4000-COMPARE.                                                            
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES  TO WS-FLAG.                                             
           IF F730-NUM-REGN = F558-NUM-REGN                                     
              ADD 1 TO WS-MATCH                                                 
              PERFORM 4500-READ-NEXT   THRU 4500-EXIT                           
                UNTIL F558-NUM-REGN NOT = F730-NUM-REGN                         
                   OR F558-NUM-REGN = HIGH-VALUES                               
              PERFORM 4800-WRITE-F730A THRU 4800-EXIT                           
              PERFORM 2000-READ-F730   THRU 2000-EXIT                           
           ELSE                                                                 
              IF F730-NUM-REGN > F558-NUM-REGN                                  
                 PERFORM 3000-READ-F558   THRU 3000-EXIT                        
              ELSE                                                              
                 PERFORM 2000-READ-F730 THRU 2000-EXIT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4500-READ-NEXT.                                                          
      *--------------------------------------------------------*                
                                                                                
           IF BP13F558-REC(314:2) = 'DB'                                        
              IF F558-NUM-HDB-REF-SLS NOT = SPACES AND LOW-VALUES               
                  MOVE 'Y'  TO WS-SLS                                           
              END-IF                                                            
              IF F558-NUM-HDB-REF-MAM NOT = SPACES AND LOW-VALUES               
                  MOVE 'Y'  TO WS-MAMS                                          
              END-IF                                                            
              IF F558-NUM-HDB-REF-TR NOT = SPACES AND LOW-VALUES                
                 MOVE 'Y'  TO WS-TRF                                            
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF BP13F558-REC(314:2) = 'PC'                                        
                  MOVE 'Y'  TO WS-PC                                            
           END-IF.                                                              
                                                                                
           IF BP13F558-REC(314:2) = 'SB'                                        
                  MOVE 'Y'  TO WS-SB                                            
           END-IF.                                                              
                                                                                
           IF BP13F558-REC(314:2) = 'S7'                                        
                  MOVE 'Y'  TO WS-S7                                            
           END-IF.                                                              
                                                                                
           IF BP13F558-REC(314:2) = 'AS'                                        
                  MOVE 'Y'  TO WS-AS                                            
           END-IF.                                                              
                                                                                
           IF BP13F558-REC(314:2) = 'HZ'                                        
                  MOVE 'Y'  TO WS-HZ                                            
           END-IF.                                                              
                                                                                
           IF BP13F558-REC(314:2) = 'ZW'                                        
                  MOVE 'Y'  TO WS-ZW                                            
           END-IF.                                                              
                                                                                
           IF BP13F558-REC(314:2) = 'ZP'                                        
                  MOVE 'Y'  TO WS-ZP                                            
           END-IF.                                                              
                                                                                
           IF BP13F558-REC(314:2) = 'LS'                                        
                  MOVE 'Y'  TO WS-LS                                            
           END-IF.                                                              
                                                                                
           IF BP13F558-REC(314:2) = 'XS'                                        
                  MOVE 'Y'  TO WS-XS                                            
           END-IF.                                                              
                                                                                
           IF BP13F558-REC(314:2) = 'FL'                                        
              IF F558-NUM-HDB-REF-RSL(1:9) NOT =                                
                  F558-NUM-HDB-REF-SMS(1:9)                                     
                  MOVE 'Y'  TO WS-FL                                            
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-F558   THRU 3000-EXIT.                             
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       4800-WRITE-F730A.                                                        
      *--------------------------------------------------------*                
                                                                                
           MOVE SPACES         TO P13F730A-REC.                                 
           INITIALIZE             P13F730A-REC.                                 
                                                                                
      **                                                                        
      ** BP133320                                                               
           MOVE  WS-ZW            TO F730-ZW                                    
           MOVE  WS-ZP            TO F730-ZP                                    
           MOVE  WS-LS            TO F730-LS                                    
           MOVE  WS-XS            TO F730-XS                                    
      ** BP133320                                                               
                                                                                
           MOVE  WS-SLS           TO F730-SLS.                                  
           MOVE  WS-MAMS          TO F730-MAMS.                                 
           MOVE  WS-TRF           TO F730-TRF.                                  
           MOVE  WS-PC            TO F730-PC.                                   
           MOVE  WS-SB            TO F730-SB.                                   
           MOVE  WS-S7            TO F730-S7.                                   
           MOVE  WS-AS            TO F730-AS.                                   
           MOVE  WS-HZ            TO F730-HZ.                                   
                                                                                
           WRITE P13F730A-REC FROM BP13F730-REC.                                
           ADD 1 TO WS-WRITE-F730A.                                             
                                                                                
       4800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------------------------------*                
       9999-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------*                
                                                                                
           DISPLAY '*********** BP13C169 ************'.                         
           DISPLAY SPACES.                                                      
           DISPLAY 'DATE : ' WS-EDITED-DATE.                                    
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECORDS READ (BP13F730) :' WS-READ-F730.             
           DISPLAY 'NO. OF RECORDS READ (BP13F558) :' WS-READ-F558.             
           DISPLAY 'NO. OF RECORDS WRITE(P13F730A) :' WS-WRITE-F730A.           
           DISPLAY SPACES.                                                      
           DISPLAY 'NO. OF RECORDS MATCH           :' WS-MATCH.                 
           DISPLAY '***********************************'.                       
                                                                                
           CLOSE       BP13F558                                                 
                       BP13F730                                                 
                       P13F730A.                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
