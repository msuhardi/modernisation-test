      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13TB93.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  APRIL 22, 2010.                                           
      *===============================================================*         
      *   OBJECTIVE:  BROWSE BP13K595 USING F595-NRIC1 TO CHECK IF    *         
      *               NRIC IS BE/SBL BOOK CASE, IF, DO NOT OUTPUT.    *         
      *===============================================================*         
      *                                                               *         
      *     INPUT FILE :                                              *         
      *                  1. BP13F595                                  *         
      *                  2. BP13K595                                  *         
      *                                                               *         
      *     OUTPUT FILE: 1. P13F595A                                  *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP133835 22/04/2010 PCL3 NEW PROGRAM.                         *         
      * BP134277 12/07/2011 LSB1 BP13AY0H SET CUTOFF DATE 200901      *         
      * BP134388 13/01/2012 ESA1 CATER FOR BOOK-STATUS 'LS' & 'WS'    *         
      * BP134766 28/12/2012 LSB1 CUTOFF SET T 2012                    *         
      * BP134766 28/12/2012 CCC5 CUTOFF SET TO 201201                 *         
      * BP134780 08/01/2013 IMC1 REMOVE CUTOFF DATE HARDCODING        *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F595 ASSIGN TO BP13F595.                                  
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
                                                                                
           SELECT BP13K595 ASSIGN TO BP13K595                                   
                     ORGANIZATION IS INDEXED                                    
                      ACCESS MODE IS DYNAMIC                                    
                       RECORD KEY IS K595-KEY-FLD                               
             ALTERNATE RECORD KEY IS K595-NUM-NRIC1                             
             ALTERNATE RECORD KEY IS K595-NUM-NRIC2                             
             ALTERNATE RECORD KEY IS K595-NUM-NRIC3                             
             ALTERNATE RECORD KEY IS K595-NUM-NRIC4                             
                      FILE STATUS IS K595-STATUS.                               
           SELECT P13F595A ASSIGN TO P13F595A.                                  
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD  BP13K595                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BP13K595.                                                           
                                                                                
       FD  BP13F205                                                             
           RECORD CONTAINS 80  CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F205.                                                           
                                                                                
       FD  BP13F595                                                             
           RECORD CONTAINS 500 CHARACTERS                                       
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F595.                                                           
                                                                                
       FD   P13F595A                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE IS F.                                                
       01   P13F595A-REC.                                                       
            05  FILLER                    PIC X(461).                           
            05  FILLER                    PIC X(02).                            
            05  F595A-NUM-CUTOFF-SBF      PIC X(01).                            
            05  F595A-NUM-BE-CNT          PIC 9(02).                            
            05  F595A-NUM-BTO-CNT         PIC 9(02).                            
            05  F595A-NUM-BK-TAG          PIC X(01).                            
            05  F595A-NUM-BK-CNT          PIC 9(02).                            
            05  F595A-NUM-REST-CNT        PIC 9(02).                            
            05  F595A-NUM-BTO-ADD         PIC X(02).                            
            05  F595A-NUM-SCH-ACC         PIC X(09).                            
            05  F595A-NUM-FN-CNT          PIC X(02).                            
            05  F595A-NUM-STAT-S          PIC X(02).                            
            05  FILLER                    PIC X(01).                            
            05  F595A-NUM-AF-QUEUE        PIC X(02).                            
            05  F595A-NUM-BUY-STATUS      PIC X(01).                            
            05  F595A-NUM-ALLOC-TAG       PIC X(02).                            
            05  F595A-NUM-NE-CNT          PIC 9(02).                            
            05  F595A-NUM-BK-ADD          PIC 9(02).                            
            05  F595A-NUM-HARDLUCK        PIC 9(02).                            
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F595             PIC X(01) VALUE 'N'.                     
           05  WS-EOF-K595             PIC X(01) VALUE 'N'.                     
           05  K595-STATUS             PIC 99    VALUE 00.                      
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F595-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-K595-NRIC1           PIC 9(7)  VALUE ZEROES.                  
           05  WS-K595-NRIC2           PIC 9(7)  VALUE ZEROES.                  
           05  WS-K595-NRIC3           PIC 9(7)  VALUE ZEROES.                  
           05  WS-K595-NRIC4           PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE-F595A      PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-BYPASS           PIC 9(7)  VALUE ZEROES.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-PREV-NRIC             PIC X(9)  VALUE SPACES.                  
           05 WS-WRITE-OKAY            PIC X(1)  VALUE 'N'.                     
           05 WS-CUTOFF                PIC X(2)  VALUE SPACES.                  
           05 WS-BP13F595-REC          PIC X(500)  VALUE SPACES.                
           05 WS-DTE-REQUEST           PIC X(8)    VALUE SPACES.                
                                                                                
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-READ-BP13F205      THRU 1100-EXIT.                      
           PERFORM 2000-READ-BP13F595      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F595 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F595                                                 
                       BP13F205                                                 
                       BP13K595                                                 
               OUTPUT  P13F595A.                                                
                                                                                
           IF K595-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K595 - OPEN ERROR, STATUS: ' K595-STATUS             
              MOVE K595-STATUS                       TO RETURN-CODE             
              PERFORM 9999-CLOSE-ROUTINE           THRU 9999-EXIT               
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1100-READ-BP13F205.                                                      
      ******************************************************************        
                                                                                
           READ BP13F205           AT END                                       
                GO                 TO 9999-CLOSE-ROUTINE.                       
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F595.                                                      
      ******************************************************************        
                                                                                
           READ BP13F595           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F595                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F595-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE 'Y'                                TO WS-WRITE-OKAY.            
           MOVE SPACES                             TO WS-CUTOFF.                
           MOVE SPACES                             TO WS-BP13F595-REC.          
           MOVE SPACES                             TO WS-DTE-REQUEST.           
           MOVE F595-NUM-NRIC1                     TO WS-PREV-NRIC.             
                                                                                
           PERFORM 3100-PROCESS-NRIC             THRU 3100-EXIT.                
                                                                                
           IF WS-WRITE-OKAY = 'N'                                               
              PERFORM  7000-WRITE-P13F595A       THRU 7000-EXIT                 
           ELSE                                                                 
              ADD 1                              TO WS-CNT-BYPASS               
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F595            THRU 2000-EXIT.                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3050-CHECK-SBF-BE.                                                       
      ******************************************************************        
                                                                                
           IF WS-WRITE-OKAY = 'N'                                               
              PERFORM  7000-WRITE-P13F595A       THRU 7000-EXIT                 
           ELSE                                                                 
              ADD 1                                TO WS-CNT-BYPASS             
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F595            THRU 2000-EXIT.                
                                                                                
       3050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3100-PROCESS-NRIC.                                                       
      ******************************************************************        
                                                                                
           PERFORM 3500-START-BP13K595-NRIC1        THRU 3500-EXIT.             
                                                                                
           IF WS-WRITE-OKAY = 'Y'                                               
              PERFORM 4000-START-BP13K595-NRIC2     THRU 4000-EXIT              
           END-IF.                                                              
                                                                                
           IF WS-WRITE-OKAY = 'Y'                                               
              PERFORM 5000-START-BP13K595-NRIC3     THRU 5000-EXIT              
           END-IF.                                                              
                                                                                
           IF WS-WRITE-OKAY = 'Y'                                               
              PERFORM 6000-START-BP13K595-NRIC4     THRU 6000-EXIT              
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3500-START-BP13K595-NRIC1.                                               
      ******************************************************************        
                                                                                
           MOVE 'N'                        TO WS-EOF-K595.                      
                                                                                
           MOVE SPACES                     TO BP13K595-REC.                     
           INITIALIZE                         BP13K595-REC.                     
                                                                                
           MOVE F595-NUM-NRIC1             TO K595-NUM-NRIC1.                   
                                                                                
           START BP13K595 KEY >= K595-NUM-NRIC1.                                
                                                                                
           EVALUATE K595-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 4100-READNEXT-BP13K595     THRU 4100-EXIT           
                    PERFORM 3600-PROCESS-BP13K595-IC1  THRU 3600-EXIT           
                            UNTIL WS-EOF-K595 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                  DISPLAY 'START ERROR BP13K595 : ' K595-STATUS                 
                          ' NRIC : ' K595-NUM-NRIC1                             
                  PERFORM 9999-CLOSE-ROUTINE       THRU 9999-EXIT               
           END-EVALUATE.                                                        
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3600-PROCESS-BP13K595-IC1.                                               
      ******************************************************************        
                                                                                
           IF K595-NUM-NRIC1 = F595-NUM-NRIC1                                   
              ADD 1                               TO WS-K595-NRIC1              
              PERFORM 4300-CHECK-BP13K595-FLDS  THRU 4300-EXIT                  
              PERFORM 4100-READNEXT-BP13K595    THRU 4100-EXIT                  
           ELSE                                                                 
              MOVE 'Y'                            TO WS-EOF-K595                
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4000-START-BP13K595-NRIC2.                                               
      ******************************************************************        
                                                                                
           MOVE 'N'                        TO WS-EOF-K595.                      
                                                                                
           MOVE SPACES                     TO BP13K595-REC.                     
           INITIALIZE                         BP13K595-REC.                     
                                                                                
           MOVE F595-NUM-NRIC1             TO K595-NUM-NRIC2.                   
                                                                                
           START BP13K595 KEY >= K595-NUM-NRIC2.                                
                                                                                
           EVALUATE K595-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 4100-READNEXT-BP13K595     THRU 4100-EXIT           
                    PERFORM 4200-PROCESS-BP13K595-IC2  THRU 4200-EXIT           
                            UNTIL WS-EOF-K595 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                  DISPLAY 'START ERROR BP13K595 : ' K595-STATUS                 
                          ' NRIC : ' K595-NUM-NRIC2                             
                  PERFORM 9999-CLOSE-ROUTINE       THRU 9999-EXIT               
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4100-READNEXT-BP13K595.                                                  
      ******************************************************************        
                                                                                
           READ BP13K595 NEXT RECORD                                            
                         AT END MOVE 'Y' TO WS-EOF-K595.                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4200-PROCESS-BP13K595-IC2.                                               
      ******************************************************************        
                                                                                
           IF K595-NUM-NRIC2 = F595-NUM-NRIC1                                   
              ADD 1                               TO WS-K595-NRIC2              
              PERFORM 4300-CHECK-BP13K595-FLDS  THRU 4300-EXIT                  
              PERFORM 4100-READNEXT-BP13K595    THRU 4100-EXIT                  
           ELSE                                                                 
              MOVE 'Y'                            TO WS-EOF-K595                
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       4300-CHECK-BP13K595-FLDS.                                                
      ******************************************************************        
                                                                                
           IF F205-NUM-OPTION = 'YH' OR 'YJ'                                    
             IF (K595-NUM-BOOK-STATUS = 'BF' OR 'LF' OR 'WF' OR                 
                                        'LS' OR 'WS') AND                       
                (K595-NUM-ALLO-CAT = 'SBF' OR 'BE ')                            
                MOVE 'N' TO WS-WRITE-OKAY                                       
                IF K595-DTE-REQUEST > WS-DTE-REQUEST                            
                   IF K595-DTE-BALLOT < F205-DTE-REQUEST(1:6)                   
                      MOVE 'BF' TO WS-CUTOFF                                    
                   ELSE                                                         
                      MOVE 'AF' TO WS-CUTOFF                                    
                   END-IF                                                       
                   MOVE BP13K595-REC TO WS-BP13F595-REC                         
                   MOVE SPACES TO WS-BP13F595-REC(462:3)                        
                   MOVE K595-DTE-REQUEST TO WS-DTE-REQUEST                      
                END-IF                                                          
             END-IF                                                             
           END-IF.                                                              
                                                                                
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-START-BP13K595-NRIC3.                                               
      ******************************************************************        
                                                                                
           MOVE 'N'                        TO WS-EOF-K595.                      
                                                                                
           MOVE SPACES                     TO BP13K595-REC.                     
           INITIALIZE                         BP13K595-REC.                     
                                                                                
           MOVE F595-NUM-NRIC1             TO K595-NUM-NRIC3.                   
                                                                                
           START BP13K595 KEY >= K595-NUM-NRIC3.                                
                                                                                
           EVALUATE K595-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 4100-READNEXT-BP13K595     THRU 4100-EXIT           
                    PERFORM 5100-PROCESS-BP13K595-IC3  THRU 5100-EXIT           
                            UNTIL WS-EOF-K595 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                  DISPLAY 'START ERROR BP13K595 : ' K595-STATUS                 
                          ' NRIC : ' K595-NUM-NRIC3                             
                  PERFORM 9999-CLOSE-ROUTINE       THRU 9999-EXIT               
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5100-PROCESS-BP13K595-IC3.                                               
      ******************************************************************        
                                                                                
           IF K595-NUM-NRIC3 = F595-NUM-NRIC1                                   
              ADD 1                               TO WS-K595-NRIC3              
              PERFORM 4300-CHECK-BP13K595-FLDS  THRU 4300-EXIT                  
              PERFORM 4100-READNEXT-BP13K595    THRU 4100-EXIT                  
           ELSE                                                                 
              MOVE 'Y'                            TO WS-EOF-K595                
           END-IF.                                                              
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6000-START-BP13K595-NRIC4.                                               
      ******************************************************************        
                                                                                
           MOVE 'N'                        TO WS-EOF-K595.                      
                                                                                
           MOVE SPACES                     TO BP13K595-REC.                     
           INITIALIZE                         BP13K595-REC.                     
                                                                                
           MOVE F595-NUM-NRIC1             TO K595-NUM-NRIC4.                   
                                                                                
           START BP13K595 KEY >= K595-NUM-NRIC4.                                
                                                                                
           EVALUATE K595-STATUS                                                 
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 4100-READNEXT-BP13K595     THRU 4100-EXIT           
                    PERFORM 6100-PROCESS-BP13K595-IC4  THRU 6100-EXIT           
                            UNTIL WS-EOF-K595 = 'Y'                             
               WHEN 20                                                          
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                  DISPLAY 'START ERROR BP13K595 : ' K595-STATUS                 
                          ' NRIC : ' K595-NUM-NRIC4                             
                  PERFORM 9999-CLOSE-ROUTINE       THRU 9999-EXIT               
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       6100-PROCESS-BP13K595-IC4.                                               
      ******************************************************************        
                                                                                
           IF K595-NUM-NRIC4 = F595-NUM-NRIC1                                   
              ADD 1                               TO WS-K595-NRIC4              
              PERFORM 4300-CHECK-BP13K595-FLDS  THRU 4300-EXIT                  
              PERFORM 4100-READNEXT-BP13K595    THRU 4100-EXIT                  
           ELSE                                                                 
              MOVE 'Y'                            TO WS-EOF-K595                
           END-IF.                                                              
                                                                                
       6100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7000-WRITE-P13F595A.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                               TO P13F595A-REC.           
           INITIALIZE                                   P13F595A-REC.           
           MOVE WS-BP13F595-REC                     TO P13F595A-REC.            
           IF WS-CUTOFF = 'BF'                                                  
              MOVE 'B' TO F595A-NUM-CUTOFF-SBF                                  
           ELSE                                                                 
              MOVE 'A' TO F595A-NUM-CUTOFF-SBF                                  
           END-IF.                                                              
           MOVE F595-NUM-NRIC1 TO P13F595A-REC(41:9).                           
                                                                                
           WRITE P13F595A-REC                                                   
           ADD 1                                 TO WS-CNT-WRITE-F595A.         
                                                                                
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CB93'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F595 RECORDS READ............. : '            
                    WS-F595-READ.                                               
           DISPLAY '(2) NO OF BP13K595 RECORDS FOUND (NRIC1).... : '            
                    WS-K595-NRIC1.                                              
           DISPLAY '(3) NO OF BP13K595 RECORDS FOUND (NRIC2).... : '            
                    WS-K595-NRIC2.                                              
           DISPLAY '(4) NO OF BP13K595 RECORDS FOUND (NRIC3).... : '            
                    WS-K595-NRIC3.                                              
           DISPLAY '(5) NO OF BP13K595 RECORDS FOUND (NRIC4).... : '            
                    WS-K595-NRIC4.                                              
           DISPLAY '(6) NO OF RECORDS WRITTEN (P13F595A)........ : '            
                    WS-CNT-WRITE-F595A.                                         
           DISPLAY '(7) NO OF RECORDS BYPASS (SBL/BE BOOKED).... : '            
                    WS-CNT-BYPASS.                                              
                                                                                
           CLOSE BP13F595                                                       
                 BP13F205                                                       
                 P13F595A                                                       
                 BP13K595.                                                      
                                                                                
           IF K595-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13K595 - CLOSE ERROR, STATUS: ' K595-STATUS            
              MOVE K595-STATUS                       TO RETURN-CODE             
           END-IF.                                                              
                                                                                
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
