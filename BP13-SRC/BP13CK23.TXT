      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13CK23.                                                 
       AUTHOR.        IRVIN CHING.                                              
       DATE-WRITTEN.  SEPT 2, 2013.                                             
      *===============================================================*         
      *   OBJECTIVE:                                                  *         
      *===============================================================*         
      *                                                               *         
      *      INPUT FILE :                                             *         
      *                  1. BC164488                                  *         
      *                  2. BP13K595                                  *         
      *                                                               *         
      *     OUTPUT FILE: 1. BP13OUT                                   *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      * -------- ---------- ---- -----------------------              *         
      * BP135089 04/09/2013 IMC1 NEW PROGRAM.                         *         
      * BP137358 30/05/2018 EXPAND BP13F55A FROM 800 TO 1200          *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F55A ASSIGN TO BP13F55A.                                  
           SELECT BC164488 ASSIGN TO BC164488.                                  
                                                                                
           SELECT BP13OUT  ASSIGN TO BP13OUT.                                   
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F55A                                                            
            RECORD CONTAINS 1200 CHARACTERS                                     
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F55A.                                                   00015505
                                                                                
       FD  BC164488                                                             
           RECORD CONTAINS 37 CHARACTERS                                        
           RECORDING MODE IS F                                                  
           LABEL RECORDS ARE STANDARD.                                          
       01  BC164488-REC.                                                        
           05 BC16-SCH-ACC                  PIC X(9).                   00015605
           05 BC16-TOTAL-AREA               PIC 9(3)V99.                00015605
           05 BC16-DTE-HANDOVER             PIC X(10).                  00015605
                                                                                
       FD  BP13OUT                                                              
           RECORD CONTAINS 1200 CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       01  BP13OUT-REC       PIC  X(1200).                              00015505
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-BC16             PIC X(01) VALUE 'N'.                     
           05  WS-F55A-EOF             PIC X(01) VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-BC16-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-F55A-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE-FOUT       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-BYPASS           PIC 9(7)  VALUE ZEROES.                  
           05  WS-MATCH                PIC 9(7)  VALUE ZEROES.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-PREV-NRIC             PIC X(9)  VALUE SPACES.                  
           05 WS-WRITE-OKAY            PIC X(1)  VALUE 'N'.                     
           05 WS-FLOOR-AREA            PIC 9(7)V99 VALUE ZEROES.                
           05 WS-NUM-NRIC1             PIC X(9)  VALUE SPACES.                  
           05 WS-NUM-NRIC2             PIC X(9)  VALUE SPACES.                  
                                                                                
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F55A      THRU 2000-EXIT.                      
           PERFORM 2100-READ-BC164488      THRU 2100-EXIT.                      
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-F55A-EOF = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU  9999-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT BP13F55A                                                  
                      BC164488                                                  
               OUTPUT BP13OUT.                                                  
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BP13F55A.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F55A AT END                                                 
             MOVE 'Y'                TO WS-F55A-EOF                             
             GO TO 2000-EXIT.                                                   
                                                                                
           ADD 1                     TO WS-F55A-READ.                           
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2100-READ-BC164488.                                                      
      ******************************************************************        
                                                                                
           READ BC164488           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-BC16                             
                MOVE '999999999'   TO   BC16-SCH-ACC                            
                GO                 TO   2100-EXIT.                              
                                                                                
           ADD  1                  TO WS-BC16-READ.                             
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           IF F55A-NUM-RDB-HDB-REF(1:9)  =  BC16-SCH-ACC                        
                                                                                
              ADD 1 TO WS-MATCH                                                 
              IF BC16-TOTAL-AREA NOT NUMERIC                                    
                 MOVE ZEROES      TO BC16-TOTAL-AREA                            
              END-IF                                                            
                                                                                
              MOVE BC16-TOTAL-AREA          TO F55A-NUM-FLOOR-AREA              
              MOVE BP13F55A-REC             TO BP13OUT-REC                      
                                                                                
              PERFORM 7000-WRITE-BP13OUT    THRU 7000-EXIT                      
              PERFORM 2000-READ-BP13F55A    THRU 2000-EXIT                      
           ELSE                                                                 
              IF F55A-NUM-RDB-HDB-REF(1:9)  < BC16-SCH-ACC                      
                 MOVE BP13F55A-REC             TO BP13OUT-REC                   
                 PERFORM 7000-WRITE-BP13OUT    THRU 7000-EXIT                   
                 PERFORM 2000-READ-BP13F55A    THRU 2000-EXIT                   
              ELSE                                                              
                 PERFORM 2100-READ-BC164488    THRU 2100-EXIT                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7000-WRITE-BP13OUT.                                                      
      ******************************************************************        
                                                                                
           WRITE BP13OUT-REC.                                                   
           ADD 1                                 TO WS-CNT-WRITE-FOUT.          
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13CK23'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BC164488 RECORDS READ............. : '            
                    WS-BC16-READ.                                               
           DISPLAY '(2) NO OF BP13F55A RECORDS READ............. : '            
                    WS-F55A-READ.                                               
           DISPLAY '(3) NO OF RECORDS WRITTEN (BP13OUT)........ : '             
                    WS-CNT-WRITE-FOUT.                                          
           DISPLAY '(2) NO OF BP13F55A RECORDS MATCHED.......... : '            
                    WS-MATCH.                                                   
                                                                                
           CLOSE BC164488                                                       
                 BP13F55A                                                       
                 BP13OUT.                                                       
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
