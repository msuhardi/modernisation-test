       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C09B.                                                    
       AUTHOR.     LIZ PATIAG.                                                  
       INSTALLATION. HDB.                                                       
       DATE-WRITTEN. 25 OCT 2016.                                               
       DATE-COMPILED.                                                           
      *-----------------------------------------------------------*             
      *        SYSTEM OF COMMITMENT - BP13                        *             
      *-----------------------------------------------------------*             
      * OBJECTIVES :                                              *             
      *                                                           *             
      *  IF NRIC MATCHED WITH DEATH RECORD,                       *             
      *  DELETE ALL RECORDS FOUND WITH SAME REGN-NO.                            
      *                                                           *             
      * INPUT  FILE   : BP13F820 (FROM BP13C092)                  *             
      * I-O    FILE   : BP13KH00 (OCCUP FILE)                     *             
      *                                                           *             
      * REQ NO.   DATE    BY  DESCRIPTION                         *             
      * -------   ----    --- -----------                         *             
      * BP136458 25102016 FNP NEW PROGRAM                         *             
      *                                                           *             
      *-----------------------------------------------------------*             
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F820 ASSIGN TO BP13F820.                                  
                                                                                
           SELECT BP13KH00 ASSIGN TO BP13KH00                                   
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KH00-KEY-FLD                         
                           FILE STATUS  IS KH00-STATUS.                         
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD BP13KH00.                                                             
       COPY BP13KH00.                                                           
                                                                                
       FD BP13F820                                                              
           RECORDING MODE F.                                                    
       01  BP13F820-REC.                                                        
           05 F820-NUM-REGN                   PIC X(8).                         
           05 F820-NUM-NRIC                   PIC X(9).                         
           05 FILLER                          PIC X(79).                        
           05 F820-DTE-BIRTH                  PIC X(8).                         
           05 FILLER                          PIC X(138).                       
           05 F820-DTE-DEATH                  PIC X(8).                         
           05 FILLER                          PIC X(150).                       
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-CONTROL-COUNT.                                                    
           05  WS-F820-READ            PIC 9(8)    VALUE ZEROES.                
           05  WS-F820-AP              PIC 9(8)    VALUE ZEROES.                
           05  WS-F820-CO-M            PIC 9(8)    VALUE ZEROES.                
           05  WS-F820-CO-S            PIC 9(8)    VALUE ZEROES.                
           05  WS-F820-OC-SP           PIC 9(8)    VALUE ZEROES.                
           05  WS-F820-OC              PIC 9(8)    VALUE ZEROES.                
           05  WS-KH00-READ            PIC 9(8)    VALUE ZEROES.                
           05  WS-KH00-FND             PIC 9(8)    VALUE ZEROES.                
           05  WS-KH00-DELETED         PIC 9(8)    VALUE ZEROES.                
                                                                                
       01  KH00-STATUS                 PIC 9(02)   VALUE ZEROES.                
       01  WS-F820-EOF                 PIC X       VALUE SPACES.                
       01  WS-KH00-EOF                 PIC X       VALUE SPACES.                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------*                 
       0000-MAIN.                                                               
      *-------------------------------------------------------*                 
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           PERFORM 2000-READ-BP13F820    THRU 2000-EXIT.                        
           PERFORM 3000-PROCESS          THRU 3000-EXIT                         
                     UNTIL WS-F820-EOF = 'Y'.                                   
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------*                 
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------*                 
           OPEN INPUT  BP13F820                                                 
                I-O    BP13KH00.                                                
                                                                                
           IF KH00-STATUS NOT = 00 AND 97                                       
              MOVE KH00-STATUS          TO RETURN-CODE                          
              DISPLAY 'ERROR OPENING BP13KH00 FILE ' KH00-STATUS                
              GO TO 9000-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------*                 
       2000-READ-BP13F820.                                                      
      *-------------------------------------------------------*                 
           READ BP13F820 AT END                                                 
                MOVE 'Y' TO WS-F820-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1                            TO WS-F820-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------*                 
       3000-PROCESS.                                                            
      *-------------------------------------------------------*                 
           MOVE 'N'             TO WS-KH00-EOF.                                 
           MOVE SPACES          TO KH00-KEY-FLD.                                
           MOVE F820-NUM-REGN   TO KH00-NUM-REGN.                               
                                                                                
           PERFORM 4100-START-BP13KH00   THRU 4100-EXIT.                        
                                                                                
           PERFORM 2000-READ-BP13F820  THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------*                 
       4100-START-BP13KH00.                                                     
      *-------------------------------------------------------*                 
                                                                                
           START BP13KH00 KEY >= KH00-KEY-FLD.                                  
           EVALUATE KH00-STATUS                                                 
              WHEN 00                                                           
                 PERFORM 4200-READNEXT-BP13KH00  THRU 4200-EXIT                 
                    UNTIL KH00-NUM-REGN NOT = F820-NUM-REGN                     
                    OR WS-KH00-EOF = 'Y'                                        
              WHEN 10                                                           
              WHEN 23                                                           
                 CONTINUE                                                       
              WHEN OTHER                                                        
                 DISPLAY 'ERROR START BP13KH00, STATUS '                        
                    KH00-STATUS ' ,REGN ' KH00-NUM-REGN                         
                 MOVE KH00-STATUS  TO RETURN-CODE                               
                 PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                      
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------*                 
       4200-READNEXT-BP13KH00.                                                  
      *-------------------------------------------------------*                 
           READ BP13KH00 NEXT RECORD.                                           
                                                                                
           EVALUATE KH00-STATUS                                                 
              WHEN 00                                                           
              WHEN 02                                                           
                 IF KH00-NUM-REGN = F820-NUM-REGN                               
                    ADD 1 TO WS-KH00-FND                                        
                    PERFORM 5000-DELETE-BP13KH00  THRU 5000-EXIT                
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
                 MOVE 'Y' TO WS-KH00-EOF                                        
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING BP13KH00, STATUS IS '                   
                    KH00-STATUS ' ,REGN IS ' KH00-NUM-REGN                      
                 MOVE KH00-STATUS   TO RETURN-CODE                              
                 PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------*                 
       5000-DELETE-BP13KH00.                                                    
      *-------------------------------------------------------*                 
           DELETE BP13KH00.                                                     
                                                                                
           EVALUATE KH00-STATUS                                                 
              WHEN 00                                                           
                 ADD 1 TO WS-KH00-DELETED                                       
                 DISPLAY 'DEL: ' KH00-KEY-FLD                                   
                         ' REL:' KH00-NUM-RELATIONSHIP                          
                         ' ROLE:' KH00-NUM-ROLE                                 
                         ' D_REG:' KH00-DTE-ISSUE-REGN                          
                         ' MS:' KH00-NUM-MARITAL-STATUS                         
                         ' KEY ISS:' F820-DTE-BIRTH                             
                         ' D_DEATH:' F820-DTE-DEATH                             
              WHEN OTHER                                                        
                 DISPLAY 'ERROR DELETING BP13KH00, STATUS IS '                  
                     KH00-STATUS ' ,KEY  IS ' KH00-KEY-FLD                      
                 MOVE KH00-STATUS   TO RETURN-CODE                              
                 PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                        
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------*                 
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------*                 
                                                                                
            DISPLAY '   '.                                                      
            DISPLAY '~~~~~~~~~~~~~~~~ BP13C09B ~~~~~~~~~~~~~~~~~'.              
            DISPLAY '   '.                                                      
            DISPLAY 'NO OF REC READ FROM BP13F820 : ' WS-F820-READ.             
            DISPLAY 'NO OF REC FOUND IN  BP13KH00 : ' WS-KH00-FND.              
            DISPLAY 'NO OF REC APPL               : ' WS-F820-AP.               
            DISPLAY 'NO OF REC CO-APPL-MARRIED    : ' WS-F820-CO-M.             
            DISPLAY 'NO OF REC CO-APPL-SINGLE     : ' WS-F820-CO-S.             
            DISPLAY 'NO OF REC OCCUPIER-SPOUSE    : ' WS-F820-OC-SP.            
            DISPLAY 'NO OF REC OCCUPIER           : ' WS-F820-OC.               
            DISPLAY 'NO OF REC DELETE FROM P13KH00: ' WS-KH00-DELETED.          
                                                                                
            CLOSE BP13F820                                                      
                  BP13KH00.                                                     
                                                                                
           IF KH00-STATUS NOT = 00 AND 97                                       
              MOVE KH00-STATUS          TO RETURN-CODE                          
              DISPLAY 'ERROR CLOSING BP13KH00 FILE ' KH00-STATUS                
           END-IF.                                                              
            STOP RUN.                                                           
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
