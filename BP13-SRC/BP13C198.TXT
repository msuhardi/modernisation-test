      *-------------------------*                                               
       IDENTIFICATION DIVISION.                                                 
      *-------------------------*                                               
       PROGRAM-ID.    BP13C198.                                                 
      *AUTHOR.        JIJO FRANCIS.                                             
      *DATE-WRITTEN.  27/05/2003.                                               
      * ============================================================= *         
      * SYSTEM OF COMMITMENT  (BP13)                                  *         
      * ============================================================= *         
      *                                                               *         
      *  OBJECTIVE :                                                  *         
      *      1. PRINT MONTHLY REPORT ON CASES WITH CONTRA FACILITY    *         
      *         AFTER KEYS COLLECTION.                                *         
      *                                                               *         
      * ------------------------------------------------------------- *         
      * CHG REF  BY    DATE      DESCRIPTION                          *         
      * -------- ---- ---------- -----------                          *         
      * BP132355 JF4  27/05/2003 PRINT MONTHLY REPORT ON CASES WITH   *         
      *                          CONTRA FACILITY AFTER KEYS COLLECTION*         
      * BP132627 GAN1 01/12/2004 START BROWSE BP13K893 BY REGN TO     *         
      *                          REPLACE READ OF BP13F800             *         
      * BP132823 GAN1 10/03/2006 TO CORRECT BROWSING OF BP13K893      *         
      * ============================================================= *         
                                                                                
      *---------------------*                                                   
       ENVIRONMENT DIVISION.                                                    
      *---------------------*                                                   
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F410  ASSIGN       TO BP13F410.                           
                                                                                
           SELECT SY02F001  ASSIGN       TO SY02F001.                           
                                                                                
           SELECT BP13K480  ASSIGN       TO BP13K480                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K480-KEY-FLD                        
                            FILE STATUS  IS WS-K480-STATUS.                     
                                                                                
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS WS-K800-STATUS.                     
                                                                                
           SELECT BP13K893  ASSIGN       TO BP13K893                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K893-KEY-FLD                        
                            FILE STATUS  IS WS-K893-STATUS.                     
                                                                                
           SELECT BP13L198  ASSIGN       TO BP13L198.                           
                                                                                
      *---------------*                                                         
       DATA DIVISION.                                                           
      *---------------*                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13K480                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K480.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD  BP13F410                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13F410.                                                           
                                                                                
       FD  BP13L198                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 132 CHARACTERS.                                      
       01 BP13L198-REC  PIC X(132).                                             
                                                                                
       COPY SY02F001.                                                           
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
                                                                                
      *------------------------------*                                          
      *   WORKING STORAGE VARIABLES  *                                          
      *------------------------------*                                          
       01 WS-F410-EOF                PIC X(01) VALUE 'N'.                       
          88 F410-END-OF-FILE        VALUE 'Y'.                                 
       01 WS-K893-EOF                PIC X(01) VALUE 'N'.                       
          88 K893-END-OF-FILE        VALUE 'Y'.                                 
       01 WS-K480-FOUND              PIC X(01) VALUE 'N'.                       
          88 K480-REGNO-FOUND        VALUE 'Y'.                                 
       01 WS-K800-FOUND              PIC X(01) VALUE 'N'.                       
          88 K800-REGNO-FOUND        VALUE 'Y'.                                 
       01 WS-K893-FOUND              PIC X(01) VALUE 'N'.                       
          88 K893-REGNO-FOUND        VALUE 'Y'.                                 
       01 WS-K480-STATUS             PIC 9(02) VALUE ZEROES.                    
       01 WS-K800-STATUS             PIC 9(02) VALUE ZEROES.                    
       01 WS-K893-STATUS             PIC 9(02) VALUE ZEROES.                    
       01 WS-F410-READ-CNT           PIC 9(08) VALUE ZEROES.                    
       01 WS-K480-READ-CNT           PIC 9(08) VALUE ZEROES.                    
       01 WS-K800-READ-CNT           PIC 9(08) VALUE ZEROES.                    
       01 WS-K893-READ-CNT           PIC 9(08) VALUE ZEROES.                    
       01 WS-L198-WRITE-CNT          PIC 9(08) VALUE ZEROES.                    
       01 WS-CUR-MNTH-CASES          PIC 9(08) VALUE ZEROES.                    
       01 WS-NUM-FLAT-TYPE           PIC X(02) VALUE SPACES.                    
       01 WS-AMT-HSE-INCOME          PIC 9(06) VALUE ZEROES.                    
                                                                                
       01 WS-EDT-DATE.                                                          
          05 WS-EDT-DD               PIC X(02).                                 
          05 FILLER                  PIC X(01) VALUE '/'.                       
          05 WS-EDT-MM               PIC X(02).                                 
          05 FILLER                  PIC X(01) VALUE '/'.                       
          05 WS-EDT-CCYY             PIC X(04).                                 
                                                                                
       01 WS-CUR-DATE.                                                          
          05 WS-CUR-CCYY             PIC 9(04).                                 
          05 WS-CUR-MM               PIC 9(02).                                 
          05 WS-CUR-DD               PIC 9(02).                                 
                                                                                
       01 WS-F410-SO-DATE.                                                      
          05 WS-F410-SO-CC           PIC 9(02).                                 
          05 WS-F410-SO-YY           PIC 9(02).                                 
          05 WS-F410-SO-MM           PIC 9(02).                                 
          05 WS-F410-SO-DD           PIC 9(02).                                 
                                                                                
       01 WS-EDIT1                   PIC Z(07)9.                                
                                                                                
       01 WS-LINE-CNT                PIC 9(02) VALUE ZEROES.                    
       01 WS-PAGE-CNT                PIC 9(06) VALUE ZEROES.                    
       01 WS-MAX-LINES               PIC 9(02) VALUE ZEROES.                    
       01 WS-SLNO                    PIC 9(06) VALUE ZEROES.                    
                                                                                
       01 WS-REPORT-VARS.                                                       
          05 L198-HEADER1.                                                      
             10 FILLER               PIC X(08) VALUE 'BP13L198'.                
             10 FILLER               PIC X(02) VALUE SPACES.                    
             10 FILLER               PIC X(08) VALUE 'HDBCAT 3'.                
             10 FILLER               PIC X(25) VALUE SPACES.                    
             10 FILLER               PIC X(20) VALUE                            
                'SYSTEM OF COMMITMENT'.                                         
             10 FILLER               PIC X(30) VALUE SPACES.                    
             10 FILLER               PIC X(07) VALUE 'DATE : '.                 
             10 L198-DATE            PIC X(12) VALUE SPACES.                    
             10 FILLER               PIC X(09) VALUE '  PAGE : '.               
             10 L198-PAGE            PIC Z(05)9.                                
                                                                                
          05 L198-HEADER2.                                                      
             10 FILLER               PIC X(27) VALUE SPACES.                    
             10 FILLER               PIC X(19) VALUE                            
                'MONTHLY REPORT FOR '.                                          
             10 L198-MNTH-YEAR       PIC X(07) VALUE SPACES.                    
             10 FILLER               PIC X(30) VALUE                            
                ' ON CASES WITH CONTRA FACILITY'.                               
             10 FILLER               PIC X(10) VALUE SPACES.                    
             10 FILLER               PIC X(30) VALUE                            
                'SALES SECTION - AGREEMENT TEAM'.                               
                                                                                
          05 L198-HEADER3.                                                      
             10 FILLER               PIC X(33) VALUE                            
                '  S/NO     DATE OF SO     REGN NO'.                            
             10 FILLER               PIC X(34) VALUE                            
                '     HH TYPE    RM TYPE    CF TYPE'.                           
             10 FILLER               PIC X(33) VALUE                            
                '    HH INCOME      RESALE SCH-ACC'.                            
             10 FILLER               PIC X(20) VALUE                            
                '     RESALE RM TYPE'.                                          
                                                                                
          05 L198-HEADER4.                                                      
             10 FILLER               PIC X(132) VALUE ALL '-'.                  
                                                                                
          05 L198-DETAILS.                                                      
             10 L198-SLNO            PIC Z(5)9.                                 
             10 FILLER               PIC X(05) VALUE SPACES.                    
             10 L198-SO-DATE         PIC X(10) VALUE SPACES.                    
             10 FILLER               PIC X(05) VALUE SPACES.                    
             10 L198-REGNNO          PIC X(08) VALUE SPACES.                    
             10 FILLER               PIC X(07) VALUE SPACES.                    
             10 L198-HH-TYPE         PIC X(01) VALUE SPACES.                    
             10 FILLER               PIC X(09) VALUE SPACES.                    
             10 L198-RM-TYPE         PIC X(02) VALUE SPACES.                    
             10 FILLER               PIC X(09) VALUE SPACES.                    
             10 L198-CF-TYPE         PIC X(02) VALUE SPACES.                    
             10 FILLER               PIC X(07) VALUE SPACES.                    
             10 L198-HH-INCOME       PIC $,$$$,$$9.                             
             10 FILLER               PIC X(06) VALUE SPACES.                    
             10 L198-RESL-SCH-ACC    PIC X(13) VALUE SPACES.                    
             10 FILLER               PIC X(10) VALUE SPACES.                    
             10 L198-RESL-RM-TYPE    PIC X(02) VALUE SPACES.                    
                                                                                
          05 L198-TRAILER1.                                                     
             10 FILLER               PIC X(70) VALUE SPACES.                    
             10 FILLER               PIC X(45) VALUE                            
                'TOTAL NO. OF CONTRA CASES PRINTED          : '.                
             10 L198-CONTRA-CASES    PIC Z(05)9.                                
                                                                                
          05 L198-TRAILER2.                                                     
             10 FILLER               PIC X(70) VALUE SPACES.                    
             10 FILLER               PIC X(45) VALUE                            
                'NO. OF CASES IN SALE ORDER FILE (BP13K410) : '.                
             10 L198-F410-CASES      PIC Z(05)9.                                
                                                                                
          05 L198-TRAILER3.                                                     
             10 FILLER               PIC X(70) VALUE SPACES.                    
             10 FILLER               PIC X(45) VALUE                            
                'NO. OF CASES IN CONTRA FILE     (BP13K480) : '.                
             10 L198-K480-CASES      PIC Z(05)9.                                
                                                                                
          05 L198-TRAILER4.                                                     
             10 FILLER               PIC X(70) VALUE SPACES.                    
             10 FILLER               PIC X(45) VALUE                            
                'NO. OF CASES IN SOC MASTER      (BP13K800) : '.                
             10 L198-K800-CASES      PIC Z(05)9.                                
                                                                                
          05 L198-TRAILER5.                                                     
             10 FILLER               PIC X(70) VALUE SPACES.                    
             10 FILLER               PIC X(45) VALUE                            
                'NO. OF CASES FOUND IN BP13K893             : '.                
             10 L198-K893-CASES      PIC Z(05)9.                                
                                                                                
      *-----------------------*                                                 
      *  LINKAGE FOR BP13C913 *                                                 
      *-----------------------*                                                 
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH-ACC.                                              
              10  WS-LINK-NUM-SCH         PIC  X(04).                           
              10  WS-LINK-NUM-ACC         PIC  X(05).                           
       COPY P13COMM8.                                                           
                                                                                
      *-------------------*                                                     
       PROCEDURE DIVISION.                                                      
      *-------------------*                                                     
                                                                                
      *---------------------------------------------------------------*         
       0000-MAIN-ROUTINE.                                                       
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 2000-READ-BP13F410      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-DATA       THRU 3000-EXIT                       
                            UNTIL F410-END-OF-FILE.                             
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN  INPUT  BP13F410                                                
                        BP13K480                                                
                        BP13K800                                                
                        BP13K893                                                
                        SY02F001                                                
                 OUTPUT BP13L198.                                               
                                                                                
           IF WS-K480-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K480, OPEN ERROR, STATUS = ' WS-K480-STATUS          
              MOVE WS-K480-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800, OPEN ERROR, STATUS = ' WS-K800-STATUS          
              MOVE WS-K800-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K893, OPEN ERROR, STATUS = ' WS-K893-STATUS          
              MOVE WS-K893-STATUS           TO RETURN-CODE                      
              PERFORM 9000-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-CUR-DATE.                          
                                                                                
           MOVE WS-CUR-DD     TO WS-EDT-DD.                                     
           MOVE WS-CUR-MM     TO WS-EDT-MM.                                     
           MOVE WS-CUR-CCYY   TO WS-EDT-CCYY.                                   
                                                                                
           MOVE  '55'         TO WS-MAX-LINES.                                  
           MOVE  '60'         TO WS-LINE-CNT.                                   
           MOVE WS-EDT-DATE   TO L198-DATE.                                     
                                                                                
           PERFORM 2600-READ-F001    THRU 2600-EXIT.                            
                                                                                
           STRING  F001-CURR-MM                                                 
                   '/'                                                          
                   F001-CURR-CC                                                 
                   F001-CURR-YY                                                 
                   DELIMITED BY SIZE INTO L198-MNTH-YEAR.                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13F410.                                                      
      *---------------------------------------------------------------*         
           READ BP13F410 AT END MOVE 'Y' TO WS-F410-EOF                         
                                    GO TO 2000-EXIT.                            
           ADD 1    TO WS-F410-READ-CNT.                                        
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2600-READ-F001.                                                          
      ****************************************************************          
           READ SY02F001                                                        
           AT END                                                               
              DISPLAY 'EMPTY FILE - SY02F001 '                                  
              PERFORM  9000-CLOSE-ROUTINE.                                      
       2600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-DATA.                                                       
      *---------------------------------------------------------------*         
      *** CHECK IF MONTH AND YEAR OF SALE ORDER DATE EQUALS                     
      *** CURRENT MONTH AND YEAR.                                               
           MOVE F410-DTE-SO-EXECUTION       TO WS-F410-SO-DATE.                 
           IF (WS-F410-SO-MM = F001-CURR-MM)  AND                               
              (WS-F410-SO-CC = F001-CURR-CC) AND                                
              (WS-F410-SO-YY = F001-CURR-YY)                                    
              ADD 1    TO WS-CUR-MNTH-CASES                                     
              PERFORM 3100-READ-BP13K480        THRU 3100-EXIT                  
              IF  K480-REGNO-FOUND                                              
                 PERFORM 3200-READ-BP13K800     THRU 3200-EXIT                  
                 IF  NOT K800-REGNO-FOUND                                       
                    PERFORM 3300-PROCESS-BP13K893  THRU 3300-EXIT               
                    IF K893-REGNO-FOUND                                         
                      ADD  1    TO WS-K893-READ-CNT                             
                      PERFORM 4000-PRINT-REPORT THRU 4000-EXIT                  
                    END-IF                                                      
                 ELSE                                                           
                    PERFORM 4000-PRINT-REPORT   THRU 4000-EXIT                  
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F410           THRU 2000-EXIT.                 
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3100-READ-BP13K480.                                                      
      *---------------------------------------------------------------*         
           MOVE SPACES          TO BP13K480-REC.                                
           INITIALIZE    BP13K480-REC.                                          
           MOVE F410-NUM-REGN   TO K480-NUM-REGN.                               
           READ BP13K480.                                                       
           EVALUATE WS-K480-STATUS                                              
            WHEN 00                                                             
              MOVE   'Y'        TO WS-K480-FOUND                                
              ADD    1          TO WS-K480-READ-CNT                             
            WHEN 23                                                             
              MOVE   SPACE      TO WS-K480-FOUND                                
              DISPLAY 'REGN. NOT FOUND IN BP13K480 : ' F410-NUM-REGN            
            WHEN OTHER                                                          
              DISPLAY 'BP13K480 - READ ERROR, STATUS = ' WS-K480-STATUS         
              MOVE WS-K480-STATUS   TO RETURN-CODE                              
              PERFORM  9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3200-READ-BP13K800.                                                      
      *---------------------------------------------------------------*         
           MOVE SPACES          TO BP13K800-MASTER.                             
           INITIALIZE    BP13K800-MASTER.                                       
           MOVE F410-NUM-REGN   TO K800-NUM-REGN.                               
           READ BP13K800.                                                       
           EVALUATE WS-K800-STATUS                                              
            WHEN 00                                                             
              MOVE   'Y'                  TO WS-K800-FOUND                      
              ADD    1                    TO WS-K800-READ-CNT                   
              MOVE K800-NUM-FLAT-TYPE     TO WS-NUM-FLAT-TYPE                   
              MOVE K800-AMT-HSE-INCOME    TO WS-AMT-HSE-INCOME                  
            WHEN 23                                                             
              MOVE   SPACE      TO WS-K800-FOUND                                
              DISPLAY 'REGN. NOT FOUND IN BP13K800 : ' F410-NUM-REGN            
            WHEN OTHER                                                          
              DISPLAY 'BP13K800 - READ ERROR, STATUS = ' WS-K800-STATUS         
              MOVE WS-K800-STATUS   TO RETURN-CODE                              
              PERFORM  9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3300-PROCESS-BP13K893.                                                   
      *---------------------------------------------------------------*         
           MOVE 'N'           TO WS-K893-FOUND                                  
                                 WS-K893-EOF.                                   
           MOVE ZERO          TO WS-AMT-HSE-INCOME.                             
           MOVE SPACES        TO WS-NUM-FLAT-TYPE                               
                                 K893-KEY-FLD.                                  
           MOVE F410-NUM-REGN TO K893-NUM-REGN-HIST.                            
           START BP13K893 KEY >= K893-KEY-FLD                                   
           IF WS-K893-STATUS = 00                                               
              PERFORM 3310-READ-BP13K893 THRU 3310-EXIT                         
                UNTIL K893-END-OF-FILE OR                                       
                     (K893-NUM-REGN-HIST NOT = F410-NUM-REGN)                   
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3310-READ-BP13K893.                                                      
      *---------------------------------------------------------------*         
           READ BP13K893 NEXT RECORD AT END                                     
                MOVE 'Y' TO WS-K893-EOF                                         
                GO TO 3310-EXIT.                                                
                                                                                
           IF WS-K893-STATUS = 00                                               
             IF (K893-NUM-REGN-HIST NOT = F410-NUM-REGN)                        
               MOVE 'Y' TO WS-K893-EOF                                          
             ELSE                                                               
               IF K893-NUM-STATUS  = 'C'                                        
                 CONTINUE                                                       
               ELSE                                                             
                 MOVE K893-NUM-FLAT-TYPE  TO WS-NUM-FLAT-TYPE                   
                 MOVE K893-AMT-HSE-INCOME TO WS-AMT-HSE-INCOME                  
                 MOVE   'Y'               TO WS-K893-FOUND                      
               END-IF                                                           
             END-IF                                                             
           ELSE                                                                 
              IF WS-K893-STATUS = 23                                            
                 MOVE 'Y' TO WS-K893-EOF                                        
              ELSE                                                              
                 DISPLAY 'READ BP18K893 FAIL ERROR : ' WS-K893-STATUS           
                 MOVE WS-K893-STATUS      TO RETURN-CODE                        
                 PERFORM  9000-CLOSE-ROUTINE                                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3310-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-PRINT-REPORT.                                                       
      *---------------------------------------------------------------*         
           IF WS-LINE-CNT > WS-MAX-LINES                                        
              MOVE SPACES            TO BP13L198-REC                            
              ADD   1                TO WS-PAGE-CNT                             
              MOVE  0                TO WS-LINE-CNT                             
              MOVE WS-PAGE-CNT       TO L198-PAGE                               
              WRITE BP13L198-REC   FROM L198-HEADER1 AFTER PAGE                 
              WRITE BP13L198-REC   FROM L198-HEADER2                            
              WRITE BP13L198-REC   FROM L198-HEADER3 AFTER 2 LINES              
              WRITE BP13L198-REC   FROM L198-HEADER4                            
              MOVE SPACES            TO BP13L198-REC                            
              WRITE BP13L198-REC                                                
              ADD   6                TO WS-LINE-CNT                             
           END-IF.                                                              
                                                                                
           MOVE   SPACES               TO L198-DETAILS.                         
           INITIALIZE   L198-DETAILS.                                           
           ADD    1                    TO WS-SLNO.                              
           MOVE WS-SLNO                TO L198-SLNO.                            
           MOVE F410-NUM-REGN          TO L198-REGNNO.                          
           MOVE F410-NUM-HOUSEHOLD     TO L198-HH-TYPE.                         
           MOVE WS-NUM-FLAT-TYPE       TO L198-RM-TYPE.                         
           MOVE F410-NUM-CONTRA-TYPE   TO L198-CF-TYPE.                         
           MOVE WS-AMT-HSE-INCOME      TO L198-HH-INCOME.                       
           STRING F410-DTE-SO-EXECUTION(7:2) '/'                                
                  F410-DTE-SO-EXECUTION(5:2) '/'                                
                  F410-DTE-SO-EXECUTION(1:4)                                    
                  DELIMITED BY SIZE INTO  L198-SO-DATE.                         
           STRING K480-RESL-SCH-ACC(1:4) '-'                                    
                  K480-RESL-SCH-ACC(5:5) '-'                                    
                  K480-RESL-SCH-ACC(10:2)                                       
                  DELIMITED BY SIZE  INTO  L198-RESL-SCH-ACC.                   
           PERFORM 4100-RETRIEVE-ROOM-TYPE  THRU 4100-EXIT.                     
           MOVE SPACES                 TO BP13L198-REC.                         
           WRITE BP13L198-REC        FROM L198-DETAILS.                         
           ADD    1                    TO WS-LINE-CNT.                          
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4100-RETRIEVE-ROOM-TYPE.                                                 
      *---------------------------------------------------------------*         
           INITIALIZE  WS-LINK-REC                                              
                       BP13COMM8-REC.                                           
           MOVE K480-RESL-SCH-ACC(1:9)  TO  WS-LINK-REC.                        
           CALL 'BP13C913'  USING     WS-LINK-REC,                              
                                      BP13COMM8-REC.                            
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
              WHEN 00                                                           
                 IF COMM8-CDE-TYP-ACTLUSE = '06'                                
                   MOVE   'E '                 TO L198-RESL-RM-TYPE             
                 ELSE                                                           
                   MOVE COMM8-CDE-TYP-ACTLUSE  TO L198-RESL-RM-TYPE             
                 END-IF                                                         
              WHEN 100                                                          
                 MOVE    SPACES                TO L198-RESL-RM-TYPE             
              WHEN OTHER                                                        
                 DISPLAY 'ERROR READING PIDB TABLE ' COMM8-CDE-SYSERR           
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-ROUTINE.                                                      
      *---------------------------------------------------------------*         
      ***IF NOT ENOUGH LINES LEFT IN CURRENT PAGE, PRINT TRAILER LINES          
      ***ON NEXT PAGE.                                                          
           IF WS-LINE-CNT > 48                                                  
              MOVE SPACES            TO BP13L198-REC                            
              ADD   1                TO WS-PAGE-CNT                             
              MOVE WS-PAGE-CNT       TO L198-PAGE                               
              WRITE BP13L198-REC   FROM L198-HEADER1 AFTER PAGE                 
              WRITE BP13L198-REC   FROM L198-HEADER2                            
              WRITE BP13L198-REC   FROM L198-HEADER4 AFTER 2 LINES              
           END-IF.                                                              
           MOVE WS-SLNO              TO L198-CONTRA-CASES.                      
           MOVE SPACES               TO BP13L198-REC.                           
           WRITE BP13L198-REC      FROM L198-TRAILER1 AFTER 3 LINES.            
           MOVE WS-CUR-MNTH-CASES    TO L198-F410-CASES.                        
           MOVE SPACES               TO BP13L198-REC.                           
           WRITE BP13L198-REC      FROM L198-TRAILER2.                          
           MOVE WS-K480-READ-CNT     TO L198-K480-CASES.                        
           MOVE SPACES               TO BP13L198-REC.                           
           WRITE BP13L198-REC      FROM L198-TRAILER3.                          
           MOVE WS-K800-READ-CNT     TO L198-K800-CASES.                        
           MOVE SPACES               TO BP13L198-REC.                           
           WRITE BP13L198-REC      FROM L198-TRAILER4.                          
           MOVE WS-K893-READ-CNT     TO L198-K893-CASES.                        
           MOVE SPACES               TO BP13L198-REC.                           
           WRITE BP13L198-REC      FROM L198-TRAILER5.                          
                                                                                
           CLOSE   BP13F410                                                     
                   SY02F001                                                     
                   BP13K480                                                     
                   BP13K800                                                     
                   BP13K893                                                     
                   BP13L198.                                                    
                                                                                
           IF WS-K480-STATUS NOT = 00 AND 97                                    
             DISPLAY 'BP13K480 - CLOSE ERROR, STATUS = ' WS-K480-STATUS         
             MOVE WS-K480-STATUS     TO RETURN-CODE                             
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
             DISPLAY 'BP13K800 - CLOSE ERROR, STATUS = ' WS-K800-STATUS         
             MOVE WS-K800-STATUS     TO RETURN-CODE                             
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
             DISPLAY 'BP13K893 - CLOSE ERROR, STATUS = ' WS-K893-STATUS         
             MOVE WS-K893-STATUS     TO RETURN-CODE                             
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
