       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID. BP13C94K.                                                    
      *DATE-WRITTEN. 18 OCT 2018.                                               
      *AUTHOR.       DEEPIKA NALLAM.                                            
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE: 1) TO UPDATE F007-NUM-HDB-REF-NEW INTO BB24F007*            
      *                IF BLANK BY READING BP13K48K                *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE     BY    DESCRIPTION                        *            
      * -------- -------- ----- -----------                        *            
      * BP137465 20181019 DN8   NEW PROGRAM                        *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
      *-------------------------------------------------------------            
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *                                                                         
           SELECT BB24F007  ASSIGN       TO BB24F007                            
                            ORGANIZATION IS SEQUENTIAL                          
                            FILE STATUS  IS WS-F007-STATUS.                     
      *                                                                         
           SELECT BP13K48K  ASSIGN          TO BP13K48K                         
                            ORGANIZATION    IS INDEXED                          
                            ACCESS MODE     IS DYNAMIC                          
                            RECORD KEY      IS K48K-NUM-REGN                    
                            ALTERNATE KEY   IS K48K-NUM-CASE                    
                            FILE STATUS     IS WS-K48K-STATUS.                  
      *                                                                         
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BB24F007                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BB24F007.                                                           
      *                                                                         
       FD BP13K48K.                                                             
       COPY BP13K48K.                                                           
      *                                                                         
       WORKING-STORAGE SECTION.                                                 
      *-----------------------------------------------------------------        
       01 WS-F007-STATUS       PIC 9(02).                                       
       01 WS-K48K-STATUS       PIC 9(02).                                       
       01 WS-RECORD-FOUND      PIC X VALUE 'N'.                                 
       01 WS-EOF               PIC X VALUE 'N'.                                 
       01 WS-MESSAGE           PIC X(80) VALUE SPACES.                          
       01 WS-RETURN-CODE       PIC X(02) VALUE SPACES.                          
       01 WS-KEY               PIC X(09) VALUE SPACES.                          
       01 WS-READ-CNT          PIC 9(08) VALUE ZEROES.                          
       01 WS-SPACE-CNT         PIC 9(08) VALUE ZEROES.                          
       01 WS-ALREADY-UPDATED   PIC 9(08) VALUE ZEROES.                          
       01 WS-REC-UPDT          PIC 9(08) VALUE ZEROES.                          
       01 WS-REC-NTFND         PIC 9(08) VALUE ZEROES.                          
      *                                                                         
       PROCEDURE DIVISION.                                                      
      *----------------------------------------------------------               
       0000-MAIN-PARA.                                                          
      *----------------------------------------------------------               
           PERFORM 1000-OPEN-PARA.                                              
           PERFORM 2000-PROCESS-PARA UNTIL WS-EOF = 'Y'                         
           PERFORM 4000-CLOSE-PARA.                                             
      *                                                                         
           STOP RUN.                                                            
      *                                                                         
       0000-EXIT.                                                               
           EXIT.                                                                
      *----------------------------------------------------------               
       1000-OPEN-PARA.                                                          
      *----------------------------------------------------------               
           OPEN INPUT BP13K48K                                                  
                I-O   BB24F007                                                  
      *                                                                         
           DISPLAY WS-K48K-STATUS                                       STATUS  
      *                                                                         
                EVALUATE TRUE                                                   
      *                                                                         
                  WHEN  WS-K48K-STATUS NOT EQUAL TO 00                          
                   MOVE 'ERROR OPENING THE BP13K48K   ' TO WS-MESSAGE           
                   MOVE WS-K48K-STATUS TO WS-RETURN-CODE                        
                   PERFORM 5000-ERROR-PARA THRU 5000-EXIT                       
      *                                                                         
                  WHEN  OTHER                                                   
                   DISPLAY 'SUCCESSFUL IN OPENING THE FILES'                    
      *                                                                         
                END-EVALUATE.                                                   
      *                                                                         
       1000-EXIT.                                                               
           EXIT.                                                                
      *----------------------------------------------------------               
       2000-PROCESS-PARA.                                                       
      *----------------------------------------------------------               
           READ BB24F007                                                        
              AT END MOVE 'Y'                 TO WS-EOF                         
              NOT AT END                                                        
               ADD   1                        TO WS-READ-CNT                    
               IF F007-NUM-HDB-REF-NEW = SPACES                                 
                 ADD   1                      TO WS-SPACE-CNT                   
                 MOVE SPACES                  TO BP13K48K-REC                   
                 MOVE F007-NUM-CASE           TO K48K-NUM-CASE                  
                 PERFORM 2200-READ-BP13K48K THRU 2200-EXIT                      
               ELSE                                                             
                 ADD   1                      TO WS-ALREADY-UPDATED             
                 CONTINUE                                                       
               END-IF                                                           
           END-READ.                                                            
      *                                                                         
       2000-EXIT.                                                               
           EXIT.                                                                
      *----------------------------------------------------------               
       2200-READ-BP13K48K.                                                      
      *----------------------------------------------------------               
              INITIALIZE WS-RECORD-FOUND                                        
                                                                                
              READ BP13K48K                                                     
               KEY IS K48K-NUM-CASE                                             
               MOVE  K48K-NUM-CASE TO WS-KEY                                    
      *                                                                         
              EVALUATE TRUE                                                     
               WHEN WS-K48K-STATUS = 00                                         
                MOVE 'Y' TO WS-RECORD-FOUND                                     
                MOVE K48K-NUM-HDB-REF  TO F007-NUM-HDB-REF-NEW                  
                REWRITE BB24F007-REC                                            
                ADD                1   TO WS-REC-UPDT                           
      *                                                                         
               WHEN WS-K48K-STATUS = 23                                         
                MOVE 'N' TO WS-RECORD-FOUND                                     
                DISPLAY 'RECORD NOT FOUND FOR IN K48K: ' K48K-NUM-CASE          
                ADD                1   TO WS-REC-NTFND                          
      *                                                                         
               WHEN OTHER                                                       
                MOVE 'ERROR READING THE BP13K48K' TO WS-MESSAGE                 
                MOVE WS-K48K-STATUS TO WS-RETURN-CODE                           
                PERFORM 5000-ERROR-PARA THRU 5000-EXIT                          
      *                                                                         
              END-EVALUATE.                                                     
      *                                                                         
       2200-EXIT.                                                               
           EXIT.                                                                
      *----------------------------------------------------------               
       4000-CLOSE-PARA.                                                         
      *----------------------------------------------------------               
           DISPLAY 'NUMBER OF RECORDS READ     - '  WS-READ-CNT                 
           DISPLAY 'NUMBER OF RECS WITH NO REF - '  WS-SPACE-CNT                
           DISPLAY 'NUMBER OF RECS WITH REF    - '  WS-ALREADY-UPDATED          
           DISPLAY 'NUMBER OF RECS UPDATED REF - '  WS-REC-UPDT                 
           DISPLAY 'NUMBER OF RECS NOT FND K48K- '  WS-REC-NTFND                
      *                                                                         
           CLOSE BB24F007                                                       
                 BP13K48K                                                       
      *                                                                         
                EVALUATE TRUE                                                   
                  WHEN  WS-K48K-STATUS NOT EQUAL TO 00                          
                   MOVE 'ERROR CLOSING THE BP13K48K ' TO WS-MESSAGE             
                   MOVE WS-K48K-STATUS TO WS-RETURN-CODE                        
                   PERFORM 5000-ERROR-PARA THRU 5000-EXIT                       
      *                                                                         
                  WHEN  OTHER                                                   
                   DISPLAY 'SUCCESSFUL IN CLOSING THE FILES'                    
      *                                                                         
                END-EVALUATE.                                                   
      *                                                                         
       4000-EXIT.                                                               
           EXIT.                                                                
      *----------------------------------------------------------               
       5000-ERROR-PARA.                                                         
      *----------------------------------------------------------               
            DISPLAY WS-MESSAGE ';' WS-RETURN-CODE.                              
      *                                                                         
       5000-EXIT.                                                               
           EXIT.                                                                
      *                                                                         
