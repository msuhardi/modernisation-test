       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C827.                                                 
       AUTHOR.        GD.                                                       
      *DATE-WRITTEN.  14/01/95.                                                 
      *REMARKS.                                                                 
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1.  TO PRINT BE01F500 RECORDS WITH UNMATCHED        *                
      *        BP13F725 RECORD                                 *                
      *                                                        *                
      *    INPUT FILES : BE01F500                              *                
      *                  BP13F725                              *                
      *                                                        *                
      *    LISTINGS    : BP13L827                              *                
      *--------------------------------------------------------*                
      * CHG-NO   BY   DATE    DETAILS                          *                
      * ======   ==  ======= ================================= *                
      * C9506G2  OCT4 030895 CHANGE FORMAT OF BP13F725         *                
      * BP132376 MJ16 290703 USE REGNO INSTEAD OF REFNO OF FILE*                
      *                      BP13F725                          *                
      * BP138285 AM25 130520 FIX PAGINATION ISSUE              *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-9121.                                               
       OBJECT-COMPUTER. IBM-9121.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BE01F500 ASSIGN TO BE01F500.                                  
                                                                                
           SELECT BP13F725 ASSIGN TO BP13F725.                                  
                                                                                
           SELECT BP13L827 ASSIGN TO BP13L827.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BE01F500                                                            
            RECORDING MODE  F                                                   
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 50  CHARACTERS.                                     
       COPY BE01F500.                                                           
                                                                                
       FD   BP13F725                                                            
            RECORDING MODE  F                                                   
            BLOCK CONTAINS  0   RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 211 CHARACTERS.                                     
       COPY BP13F725.                                                           
                                                                                
       FD   BP13L827                                                            
            RECORDING MODE  F                                                   
            LABEL RECORDS   ARE OMITTED                                         
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   BP13L827-REC    PIC X(132).                                         
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F500-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-F725-EOF-SW                 PIC X(1)  VALUE 'N'.              
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-F500-READ-CTR               PIC 9(5)  VALUE ZEROS.            
           05  WS-F725-READ-CTR               PIC 9(5)  VALUE ZEROS.            
           05  WS-F500-UNMATCHED-CTR          PIC 9(5)  VALUE ZEROS.            
           05  WS-F500-MATCHED-CTR            PIC 9(5)  VALUE ZEROS.            
                                                                                
       01  WS-L827-CONTROLS.                                                    
           05  WS-L827-LINECTR                PIC 9(2)  VALUE 26.               
           05  WS-L827-PAGECTR                PIC 9(5)  VALUE ZEROS.            
           05  WS-L827-S-NO                   PIC 9(5)  VALUE ZEROS.            
                                                                                
       01  WS-MATCH-FIELDS.                                                     
           05  WS-F500-MATCH-FIELD.                                             
               10  WS-F500-REF                PIC X(8).                         
               10  WS-F500-NRIC               PIC X(9).                         
           05  WS-F725-MATCH-FIELD.                                             
               10  WS-F725-REF                PIC X(8).                         
               10  WS-F725-NRIC               PIC X(9).                         
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-YY                  PIC 9(2).                         
               10  WS-SYS-MM                  PIC 9(2).                         
               10  WS-SYS-DD                  PIC 9(2).                         
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-MM               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-YY               PIC 9(2).                         
                                                                                
      **********************************************************                
      *   BP13L827  -  R E P O R T    L A Y O U T              *                
      **********************************************************                
                                                                                
       01  L827-HEADING-1.                                                      
           05 FILLER                     PIC X(8)  VALUE 'BP13L827'.            
           05 FILLER                     PIC X(33) VALUE SPACES.                
           05 FILLER                     PIC X(41) VALUE                        
              'S Y S T E M   O F   C O M M I T T M E N T'.                      
           05 FILLER                     PIC X(16) VALUE SPACES.                
           05 FILLER                     PIC X(7)  VALUE 'DATE : '.             
           05 L827-DATE                  PIC X(8).                              
           05 FILLER                     PIC X(5)  VALUE SPACES.                
           05 FILLER                     PIC X(7)  VALUE 'PAGE : '.             
           05 L827-PAGE                  PIC ZZZ9.                              
                                                                                
       01  L827-HEADING-2.                                                      
           05 FILLER                     PIC X(41) VALUE SPACES.                
           05 FILLER                     PIC X(49) VALUE                        
              'LIST OF F500 RECORDS WITH NO MATCHING F725 RECORD'.              
                                                                                
       01  L827-HEADING-3.                                                      
           05 FILLER                     PIC X(05) VALUE SPACES.                
           05 FILLER                     PIC X(05) VALUE ' S/N'.                
           05 FILLER                     PIC X(05) VALUE SPACES.                
           05 FILLER                     PIC X(06) VALUE 'REF NO'.              
           05 FILLER                     PIC X(05) VALUE SPACES.                
           05 FILLER                     PIC X(09) VALUE '  NRIC'.              
                                                                                
       01  L827-HEADING-4.                                                      
           05 FILLER                     PIC X(05) VALUE SPACES.                
           05 FILLER                     PIC X(05) VALUE ALL '_'.               
           05 FILLER                     PIC X(05) VALUE SPACES.                
           05 FILLER                     PIC X(06) VALUE ALL '_'.               
           05 FILLER                     PIC X(05) VALUE SPACES.                
           05 FILLER                     PIC X(09) VALUE ALL '_'.               
                                                                                
       01  L827-DETAIL-1.                                                       
           05 FILLER                     PIC X(05) VALUE SPACES.                
           05 L827-S-NO                  PIC ZZZZ9.                             
           05 FILLER                     PIC X(05) VALUE SPACES.                
           05 L827-REF-NO                PIC X(08).                             
           05 FILLER                     PIC X(05) VALUE SPACES.                
           05 L827-NRIC                  PIC X(09).                             
                                                                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-INITIALIZATION THRU 1000-EXIT.                          
                                                                                
           PERFORM 2000-PROCESS-F500   THRU 2000-EXIT                           
              UNTIL WS-F500-EOF-SW = 'Y'.                                       
                                                                                
           PERFORM 9999-CLOSE-FILES.                                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-INITIALIZATION.                                                     
      ****************************************************************          
                                                                                
           OPEN INPUT  BE01F500                                                 
                       BP13F725                                                 
                OUTPUT BP13L827.                                                
                                                                                
           ACCEPT WS-SYSTEM-DATE      FROM DATE.                                
                                                                                
           MOVE   WS-SYS-DD           TO WS-EDITED-DD.                          
           MOVE   WS-SYS-MM           TO WS-EDITED-MM.                          
           MOVE   WS-SYS-YY           TO WS-EDITED-YY.                          
                                                                                
           MOVE   WS-EDITED-DATE      TO L827-DATE.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       2000-PROCESS-F500.                                                       
      ****************************************************************          
                                                                                
           READ BE01F500                                                        
              AT END MOVE 'Y'        TO   WS-F500-EOF-SW                        
              GO TO  2000-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD  +1                         TO  WS-F500-READ-CTR.                
           MOVE BE01F500-REF-NO(1:8)       TO  WS-F500-REF.                     
           MOVE BE01F500-IDNO              TO  WS-F500-NRIC.                    
                                                                                
           PERFORM 3000-MATCH-F725   THRU 3000-EXIT                             
              UNTIL WS-F725-MATCH-FIELD >= WS-F500-MATCH-FIELD.                 
                                                                                
           IF WS-F725-MATCH-FIELD NOT = WS-F500-MATCH-FIELD                     
              PERFORM 3100-PRINT-BP13L827  THRU 3100-EXIT                       
              ADD     +1                   TO   WS-F500-UNMATCHED-CTR           
           ELSE                                                                 
              ADD     +1                   TO   WS-F500-MATCHED-CTR             
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       3000-MATCH-F725.                                                         
      ****************************************************************          
                                                                                
           READ BP13F725                                                        
              AT END  MOVE 'Y'             TO   WS-F725-EOF-SW                  
                      MOVE HIGH-VALUES     TO   WS-F725-MATCH-FIELD             
                      GO TO 3000-EXIT                                           
           END-READ.                                                            
                                                                                
           ADD  +1                         TO   WS-F725-READ-CTR.               
                                                                                
           MOVE F725-NUM-REGN              TO   WS-F725-REF.                    
           MOVE F725-NUM-NRIC              TO   WS-F725-NRIC.                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       3100-PRINT-BP13L827.                                                     
      ****************************************************************          
                                                                                
           IF WS-L827-LINECTR > 25                                              
              PERFORM 3110-PRINT-HEADING   THRU 3110-EXIT                       
           END-IF.                                                              
                                                                                
           ADD   +1                        TO   WS-L827-LINECTR                 
                                                WS-L827-S-NO.                   
                                                                                
           MOVE WS-L827-S-NO               TO   L827-S-NO.                      
           MOVE BE01F500-IDNO              TO   L827-NRIC.                      
           MOVE BE01F500-REF-NO(1:8)       TO   L827-REF-NO.                    
                                                                                
           WRITE BP13L827-REC              FROM L827-DETAIL-1                   
                                          AFTER ADVANCING 2 LINES.              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       3110-PRINT-HEADING.                                                      
      ****************************************************************          
                                                                                
           ADD  +1                         TO   WS-L827-PAGECTR.                
           MOVE ZEROS                      TO   WS-L827-LINECTR.                
                                                                                
           MOVE WS-L827-PAGECTR            TO   L827-PAGE.                      
                                                                                
           WRITE BP13L827-REC              FROM L827-HEADING-1                  
                                          AFTER ADVANCING PAGE.                 
                                                                                
           WRITE BP13L827-REC              FROM L827-HEADING-2                  
                                          AFTER ADVANCING 1 LINE.               
                                                                                
           WRITE BP13L827-REC              FROM L827-HEADING-3                  
                                          AFTER ADVANCING 2 LINES.              
                                                                                
           WRITE BP13L827-REC              FROM L827-HEADING-4                  
                                          AFTER ADVANCING 1 LINE.               
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-FILES.                                                        
      ****************************************************************          
                                                                                
           CLOSE BE01F500                                                       
                 BP13F725                                                       
                 BP13L827.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : ' WS-EDITED-DATE.                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13C827 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BE01F500 RECORDS READ     = ' WS-F500-READ-CTR.             
           DISPLAY 'BP13F725 RECORDS READ     = ' WS-F725-READ-CTR.             
           DISPLAY 'BE01F500 RECORDS UNMATCHED= ' WS-F500-UNMATCHED-CTR.        
           DISPLAY 'BP13F500 RECORDS MATCHED  = ' WS-F500-MATCHED-CTR.          
           DISPLAY SPACES.                                                      
           DISPLAY '*======================================*'.                  
                                                                                
           STOP RUN.                                                            
                                                                                
