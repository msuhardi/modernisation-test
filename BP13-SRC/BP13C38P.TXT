      *===============================================================*         
       IDENTIFICATION DIVISION.                                                 
      *===============================================================*         
       PROGRAM-ID.    BP13C38P.                                                 
       AUTHOR.        PAULO CAMIA LEGASPI.                                      
       DATE-WRITTEN.  SEPTEMBER 23, 2015                                        
      *===============================================================*         
      * OBJECTIVE  :  PRINT SERS CASES                                *         
      *===============================================================*         
      *                                                               *         
      * INPUT FILE :                                                  *         
      *               1. BP13F410                                     *         
      *               2. BP13K800                                     *         
      *               3. BP13K110                                     *         
      *                                                               *         
      * OUTPUT FILE:  1. BP13L38P                                     *         
      *                                                               *         
      *===============================================================*         
      * REF NO   DATE       BY   AMENDMENTS/ENHANCEMENTS              *         
      *---------------------------------------------------------------*         
      * BP136390 23/09/2015 PCL4 NEW PROGRAM.                         *         
      * BP136548 03/01/2017 PCL4 TO CHECK FOR PREV MONTH              *         
      * BP137213 21/02/2018 PCL4 TO READ F410.MTHSO                   *         
      * BP139030 07/01/2022 ZAR7 REPLACE CHUA YEE MIEN TO LIM KOK CHUN          
      * BP139276 05/04/2024 KAC1 REPLACE LIM KOK CHUN BY              *         
      *                                  KATHIJAH_BEE_ALI_MOHAMED     *         
      *===============================================================*         
                                                                                
                                                                                
      **********************                                                    
       ENVIRONMENT DIVISION.                                                    
      **********************                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  BP13F410  ASSIGN        TO BP13F410.                         
           SELECT  BP13K893  ASSIGN        TO BP13K893                          
                             ACCESS MODE   IS DYNAMIC                           
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K893-KEY-FLD                      
                             FILE STATUS   IS WS-K893-STATUS.                   
           SELECT  BP13K800  ASSIGN        TO BP13K800                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K800-NUM-REGN                     
                             FILE STATUS   IS WS-K800-STATUS.                   
           SELECT  BM06K110  ASSIGN        TO BM06K110                          
                             ACCESS MODE   IS RANDOM                            
                             ORGANIZATION  IS INDEXED                           
                             RECORD KEY    IS K110-KEY-FLD                      
                             FILE STATUS   IS WS-K110-STATUS.                   
           SELECT  BP13L38P  ASSIGN        TO BP13L38P.                         
           SELECT  MAILC38P  ASSIGN        TO MAILC38P.                         
                                                                                
      ***************                                                           
       DATA DIVISION.                                                           
      ***************                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F410                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13K410.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BM06K110.                                                           
                                                                                
       FD   BP13L38P                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 300 CHARACTERS                                      
            LABEL RECORD IS OMITTED                                             
            RECORDING MODE IS F.                                                
       01   BP13L38P-REC               PIC X(300).                              
                                                                                
       FD   MAILC38P                                                            
            RECORDING MODE IS F                                                 
            RECORD CONTAINS  300 CHARACTERS.                                    
       01   MAILC38P-REC               PIC X(300).                              
                                                                                
       WORKING-STORAGE SECTION.                                                 
      ******************************************************************        
      *                   WORKING-STORAGE AREA                         *        
      ******************************************************************        
       01  FILE-VARIABLES.                                                      
           05  WS-EOF-F410             PIC X(1)  VALUE 'N'.                     
           05  WS-EOF-K110             PIC X(1)  VALUE 'N'.                     
           05  WS-K893-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K110-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K800-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-I                    PIC 9(1)  VALUE ZEROES.                  
           05  WS-HA                   PIC 9(1)  VALUE ZEROES.                  
           05  WS-F410-READ            PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO                  PIC 9(7)  VALUE ZEROES.                  
           05  WS-SNO1                 PIC 9(7)  VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(2)  VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(4)  VALUE ZEROES.                  
           05  WS-CNT-L38P-WRITE       PIC 9(7)  VALUE ZEROES.                  
           05  WS-CNT-WRITE            PIC 9(1)  VALUE 2.                       
           05  WS-MAX-LINE             PIC 9(2)  VALUE 55.                      
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-AMT-POD               PIC 9(6)     VALUE ZEROES.               
           05 WS-DEFER-PREMIUM         PIC S9(7)V99 VALUE ZEROES.               
           05 WS-DEFER-RATE            PIC 9(2)V99 VALUE ZEROES.                
           05 WS-PRIOR-DTE             PIC 9(08) VALUE ZEROES.                  
           05 WS-NUM-DTE-APPT          PIC 9(07) VALUE ZEROES.                  
           05 WS-SYSTEM-DATE           PIC 9(8)  VALUE ZEROES.                  
           05 WS-F410-NUM-REGN         PIC X(8)  VALUE SPACES.                  
           05 WS-NUM-NRIC              PIC X(9)  VALUE SPACES.                  
           05 WS-SNO-ZZZZ9             PIC ZZZZ9 VALUE ZEROES.                  
           05 WS-AMT-FORMAT            PIC S9(7)V99 VALUE ZEROES.               
           05 WS-FIRST-WRITE           PIC X(1)  VALUE 'Y'.                     
           05 WS-CNT-HA                PIC 9(2)  VALUE ZEROES.                  
                                                                                
       01  WS-DTE-REGN.                                                         
           05 WS-DTE-CCYY                   PIC 9(4).                           
           05 WS-DTE-MM                     PIC 9(2).                           
           05 WS-DTE-DD                     PIC 9(2).                           
                                                                                
       01  WS-FIX-VARS.                                                         
           05  WS-DOB-DATE.                                                     
               10  WS-DOB-YR                 PIC  9(04).                        
               10  WS-DOB-MMDD               PIC  9(04).                        
           05  WS-SYS-DATE2.                                                    
               10  WS-SYS-YR                 PIC  9(04).                        
               10  WS-SYS-MMDD               PIC  9(04).                        
           05  WS-AGE1                       PIC  9(04)  VALUE ZEROES.          
           05  WS-HA-DOB                     PIC  X(08).                        
           05  WS-HA-AGE                     PIC  9(04).                        
                                                                                
       01 WS-PRIO-DTE.                                                          
          05  WS-PRIO-CCYY                 PIC 9(04).                           
          05  WS-PRIO-MM                   PIC 9(02).                           
          05  WS-PRIO-DD                   PIC 9(02).                           
                                                                                
       01 WS-CURR-DATE.                                                         
          05  WS-CURR-CCYY                 PIC 9(04).                           
          05  WS-CURR-MM                   PIC 9(02).                           
          05  WS-CURR-DD                   PIC 9(02).                           
                                                                                
       01  WS-DEBAR-CODE               PIC X(02).                               
           88 VALID-YR-CODE          VALUE 'YR'.                                
           88 VALID-YS-CODE          VALUE 'YS'.                                
                                                                                
       01  L38P-HEADER01.                                                       
           05  FILLER              PIC X(8)      VALUE 'BP13L38P'.              
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                  'S Y S T E M   O F   '.                       
           05  FILLER              PIC X(19) VALUE                              
                                  'C O M M I T M E N T'.                        
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE : '.               
           05  L38P-DATE-HDR       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L38P-PAGE-HDR       PIC ZZZ9      VALUE ZEROES.                  
                                                                                
       01  L38P-HEADER02.                                                       
           05  FILLER              PIC X(15)     VALUE SPACES.                  
           05  FILLER              PIC X(28)     VALUE                          
               'SERS CASES WITH SERS RS TAG '.                                  
           05  FILLER              PIC X(42)     VALUE                          
               'AND TP DATE '.                                                  
           05  L38P-DTE-HDR        PIC X(10)     VALUE SPACES.                  
                                                                                
       01  L38P-HEADER03.                                                       
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(05)    VALUE '  S/N'.                  
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(08)    VALUE 'REGN NO '.               
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(11)    VALUE 'SERS RS TAG'.            
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(10)    VALUE 'TP DATE'.                
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(09)    VALUE 'FLAT TYPE'.              
           05  FILLER              PIC X(01)    VALUE SPACES.                   
           05  FILLER              PIC X(10)    VALUE 'LSE TENURE'.             
           05  FILLER              PIC X(03)    VALUE SPACES.                   
           05  FILLER              PIC X(10)    VALUE '  OFFER SP'.             
           05  FILLER              PIC X(02)    VALUE SPACES.                   
           05  FILLER     PIC X(26) VALUE 'PREM/LEVY AS PART OF PRICE'.         
           05  FILLER     PIC X(02) VALUE SPACES.                               
           05  FILLER     PIC X(18) VALUE 'PREMIUM DEFER FULL'.                 
           05  FILLER     PIC X(02) VALUE SPACES.                               
           05  FILLER     PIC X(21) VALUE 'PREMIUM VIA CASH/COMP'.              
           05  FILLER     PIC X(02) VALUE SPACES.                               
           05  FILLER     PIC X(15) VALUE 'LEVY DEFER FULL'.                    
           05  FILLER     PIC X(02) VALUE SPACES.                               
           05  FILLER     PIC X(10) VALUE '    SC/SPR'.                         
           05  FILLER     PIC X(02) VALUE SPACES.                               
           05  FILLER     PIC X(10) VALUE '  SGL PREM'.                         
           05  FILLER     PIC X(02) VALUE SPACES.                               
           05  FILLER     PIC X(10) VALUE '  ELD PREM'.                         
           05  FILLER     PIC X(02) VALUE SPACES.                               
           05  FILLER     PIC X(10) VALUE ' SERS DISC'.                         
           05  FILLER     PIC X(02) VALUE SPACES.                               
           05  FILLER     PIC X(10) VALUE '     PODFW'.                         
           05  FILLER     PIC X(02) VALUE SPACES.                               
           05  FILLER     PIC X(10) VALUE '  DISC OCS'.                         
           05  FILLER     PIC X(02) VALUE SPACES.                               
           05  FILLER     PIC X(10) VALUE '   AMT POG'.                         
           05  FILLER     PIC X(02) VALUE SPACES.                               
           05  FILLER     PIC X(10) VALUE '   AMT POE'.                         
           05  FILLER     PIC X(02) VALUE SPACES.                               
           05  FILLER     PIC X(10) VALUE '   AMT FLX'.                         
           05  FILLER     PIC X(02) VALUE SPACES.                               
                                                                                
                                                                                
       01  L38P-DETAIL01.                                                       
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L38P-SN            PIC X(05)        VALUE SPACES.                
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L38P-REGN          PIC X(08)        VALUE SPACES.                
           05  FILLER             PIC X(06)        VALUE SPACES.                
           05  L38P-RSTAG         PIC X(03)        VALUE SPACES.                
           05  FILLER             PIC X(04)        VALUE SPACES.                
           05  L38P-TP-DATE       PIC X(10)        VALUE SPACES.                
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L38P-FT            PIC X(09)        VALUE SPACES.                
           05  FILLER             PIC X(01)        VALUE SPACES.                
           05  L38P-TENURE        PIC X(10)        VALUE SPACES.                
           05  FILLER             PIC X(03)        VALUE SPACES.                
           05  L38P-OFFER-SP      PIC $$$$$$9.99.                               
           05  FILLER             PIC X(18)        VALUE SPACES.                
           05  L38P-PREM-LEVY     PIC $$$$$$9.99.                               
           05  FILLER             PIC X(10)        VALUE SPACES.                
           05  L38P-PREM-DEFER    PIC $$$$$$9.99.                               
           05  FILLER             PIC X(13)        VALUE SPACES.                
           05  L38P-PREM-CASH     PIC $$$$$$9.99.                               
           05  FILLER             PIC X(07)        VALUE SPACES.                
           05  L38P-LEVY-DEFER    PIC $$$$$$9.99.                               
           05  FILLER             PIC X(02)        VALUE SPACES.                
           05  L38P-SC-SPR        PIC $$$$$$9.99.                               
           05  FILLER             PIC X(02)        VALUE SPACES.                
           05  L38P-SGL-PREM      PIC $$$$$$9.99.                               
           05  FILLER             PIC X(02)        VALUE SPACES.                
           05  L38P-ELD-PREM      PIC $$$$$$9.99.                               
           05  FILLER             PIC X(02)        VALUE SPACES.                
           05  L38P-SERS-DISC     PIC $$$$$$9.99.                               
           05  FILLER             PIC X(02)        VALUE SPACES.                
           05  L38P-PODFW         PIC $$$$$$9.99.                               
           05  FILLER             PIC X(02)        VALUE SPACES.                
           05  L38P-OCS           PIC $$$$$$9.99.                               
           05  FILLER             PIC X(02)        VALUE SPACES.                
           05  L38P-POG           PIC $$$$$$9.99.                               
           05  FILLER             PIC X(02)        VALUE SPACES.                
           05  L38P-POE           PIC $$$$$$9.99.                               
           05  FILLER             PIC X(02)        VALUE SPACES.                
           05  L38P-FLX           PIC $$$$$$9.99.                               
           05  FILLER             PIC X(02)        VALUE SPACES.                
                                                                                
                                                                                
       01  WS-REPORT-DTL.                                                       
           05  WS-EMPTY-RPT.                                                    
               10 FILLER                  PIC X(30) VALUE SPACES.               
               10 FILLER                  PIC X(21)                             
                                          VALUE '<   EMPTY  REPORT   >'.        
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT1             PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT2             PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT3             PIC X(50)                               
               VALUE 'RCPT TO:<TAN_SIEW_LAY@HDB.GOV.SG>'.                       
           05  WS-MAIL-RPT3A            PIC X(50)                               
               VALUE 'RCPT TO:<SHAMSURIANOR_SIBENGAT@HDB.GOV.SG>'.              
           05  WS-MAIL-RPT3B            PIC X(50)                               
               VALUE 'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.           
           05  WS-MAIL-RPT4             PIC X(4)                                
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT5             PIC X(29)                               
               VALUE 'FROM:Soc System - Email Alert'.                           
           05  WS-MAIL-RPT6             PIC X(50)                               
               VALUE 'TO:<TAN_SIEW_LAY@HDB.GOV.SG>'.                            
           05  WS-MAIL-RPT6A            PIC X(50)                               
               VALUE 'TO:<SHAMSURIANOR_SIBENGAT@HDB.GOV.SG>'.                   
           05  WS-MAIL-RPT6B            PIC X(50)                               
               VALUE 'CC:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                
           05  WS-MAIL-RPT7             PIC X(80)                               
               VALUE 'SUBJECT: SERS CASES WITH SERS RS AND TP '.                
                                                                                
      ******************************************************************        
      *                   P R O G R A M     B O D Y                    *        
      ******************************************************************        
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      ******************************************************************        
       0000-MAIN.                                                               
      ******************************************************************        
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
                                                                                
           PERFORM 2000-READ-BP13F410      THRU 2000-EXIT.                      
                                                                                
           PERFORM 3000-PROCESS-RECORDS    THRU 3000-EXIT                       
             UNTIL WS-EOF-F410 = 'Y'.                                           
                                                                                
           PERFORM 9999-CLOSE-ROUTINE      THRU 9999-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       1000-OPEN-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F410                                                 
                       BP13K800                                                 
                       BP13K893                                                 
                       BM06K110                                                 
              OUTPUT   BP13L38P                                                 
                       MAILC38P.                                                
                                                                                
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR OPENING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR OPENING : ' WS-K110-STATUS              
              MOVE WS-K110-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K893 - ERROR OPENING : ' WS-K893-STATUS              
              MOVE WS-K893-STATUS                   TO RETURN-CODE              
              PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 7000-PRINT-HEADER       THRU 7000-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2000-READ-BP13F410.                                                      
      ******************************************************************        
                                                                                
           READ BP13F410           AT   END                                     
                MOVE 'Y'           TO   WS-EOF-F410                             
                GO                 TO   2000-EXIT.                              
                                                                                
           ADD  1                  TO WS-F410-READ.                             
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *************************************************************             
       2100-READ-BP13K800.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO BP13K800-MASTER.                    
           INITIALIZE                       BP13K800-MASTER.                    
                                                                                
           MOVE K410-NUM-REGN            TO K800-NUM-REGN.                      
                                                                                
           READ BP13K800.                                                       
                                                                                
           EVALUATE WS-K800-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    PERFORM 2200-START-BP13K893  THRU 2200-EXIT                 
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR ' WS-K800-STATUS               
                    DISPLAY 'K800 KEY: ' K800-NUM-REGN                          
                    MOVE WS-K800-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       2200-START-BP13K893.                                                     
      ******************************************************************        
                                                                                
           MOVE SPACES                           TO BP13K893-MASTER             
                                                    BP13K800-MASTER.            
           INITIALIZE                               BP13K893-MASTER             
                                                    BP13K800-MASTER.            
                                                                                
           MOVE SPACES                           TO K893-KEY-FLD.               
           MOVE K410-NUM-REGN                    TO K893-NUM-REGN-HIST.         
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K893-STATUS                                              
               WHEN 00                                                          
               WHEN 02                                                          
                    PERFORM 2300-READ-BP13K893  THRU 2300-EXIT                  
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'ERROR STARTING BP13K893: ' WS-K893-STATUS          
                            ' REGN = ' K410-NUM-REGN                            
                    PERFORM  9999-CLOSE-ROUTINE THRU 9999-EXIT                  
           END-EVALUATE.                                                        
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2300-READ-BP13K893.                                                      
      ******************************************************************        
                                                                                
            READ  BP13K893 NEXT AT END                                          
                  MOVE SPACES               TO K893-KEY-FLD.                    
                                                                                
            EVALUATE WS-K893-STATUS                                             
                WHEN 00                                                         
                WHEN 02                                                         
                     IF K893-NUM-REGN-HIST = K410-NUM-REGN                      
                        MOVE BP13K893-MASTER(1:2000) TO BP13K800-MASTER         
                     END-IF                                                     
                WHEN 23                                                         
                     CONTINUE                                                   
                WHEN OTHER                                                      
                     DISPLAY 'ERROR READING BP13K893 : ' WS-K893-STATUS         
                             ' REGN = ' K893-NUM-REGN-HIST                      
                     PERFORM  9999-CLOSE-ROUTINE  THRU 9999-EXIT                
            END-EVALUATE.                                                       
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       2400-READ-BM06K110.                                                      
      *************************************************************             
                                                                                
           MOVE SPACES                   TO K110-REC.                           
           INITIALIZE                       K110-REC.                           
                                                                                
           MOVE K410-NUM-SCH-ACC         TO K110-KEY-FLD.                       
                                                                                
           READ BM06K110.                                                       
                                                                                
           EVALUATE WS-K110-STATUS                                              
               WHEN 00                                                          
                    CONTINUE                                                    
                                                                                
               WHEN 10                                                          
               WHEN 23                                                          
                    MOVE SPACES          TO K110-REC                            
                    INITIALIZE              K110-REC                            
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'BM06K110 READ ERROR ' WS-K110-STATUS               
                    DISPLAY 'K110 KEY: ' K110-KEY-FLD                           
                    MOVE WS-K110-STATUS  TO RETURN-CODE                         
                                                                                
                    PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                   
           END-EVALUATE.                                                        
                                                                                
       2400-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       3000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           PERFORM 2100-READ-BP13K800         THRU 2100-EXIT.                   
           PERFORM 2400-READ-BM06K110         THRU 2400-EXIT.                   
                                                                                
      *    IF K410-DTE-KEY-ISSUE(1:6) = WS-CURR-DATE(1:6)                       
                                                                                
              IF ((K800-NUM-ALLO-CAT      = 'SER') OR                           
                  (K800-NUM-PORT-ELIG-TAG = 'Y'))   AND                         
                 (K800-NUM-SERS-RS    NOT = SPACES  AND LOW-VALUES)             
                 PERFORM 4000-PROCESS-RECORDS    THRU 4000-EXIT                 
      *       END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F410            THRU 2000-EXIT.                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       3001-INITIALIZE-FIELDS.                                                  
      ******************************************************************        
                                                                                
                                                                                
           IF K110-AMT-ELDERLY-PREMIUM NOT NUMERIC                              
              MOVE ZEROES TO K110-AMT-ELDERLY-PREMIUM                           
           END-IF.                                                              
                                                                                
           IF K110-AMT-SINGLES-PREMIUM NOT NUMERIC                              
              MOVE ZEROES TO K110-AMT-SINGLES-PREMIUM                           
           END-IF.                                                              
                                                                                
           IF K800-AMT-PREMIUM NOT NUMERIC                                      
              MOVE ZEROES TO K800-AMT-PREMIUM                                   
           END-IF.                                                              
                                                                                
           IF K800-NUM-PREMIUM-RATE NOT NUMERIC                                 
              MOVE ZEROES TO K800-NUM-PREMIUM-RATE                              
           END-IF.                                                              
                                                                                
           IF K800-AMT-PREM-PAID-CASH-COMP NOT NUMERIC                          
              MOVE ZEROES TO K800-AMT-PREM-PAID-CASH-COMP                       
           END-IF.                                                              
                                                                                
           IF K800-AMT-DEFER-LEVY NOT NUMERIC                                   
              MOVE ZEROES TO K800-AMT-DEFER-LEVY                                
           END-IF.                                                              
                                                                                
           IF K800-AMT-DEFER-PREM NOT NUMERIC                                   
              MOVE ZEROES TO K800-AMT-DEFER-PREM                                
           END-IF.                                                              
                                                                                
           IF K110-AMT-POD NOT NUMERIC                                          
              MOVE ZEROES TO K110-AMT-POD                                       
           END-IF.                                                              
                                                                                
           IF K110-AMT-POF NOT NUMERIC                                          
              MOVE ZEROES TO K110-AMT-POF                                       
           END-IF.                                                              
                                                                                
           IF K110-AMT-POW NOT NUMERIC                                          
              MOVE ZEROES TO K110-AMT-POW                                       
           END-IF.                                                              
                                                                                
           IF K110-AMT-POE NOT NUMERIC                                          
              MOVE ZEROES TO K110-AMT-POE                                       
           END-IF.                                                              
                                                                                
           IF K110-AMT-FLEXI NOT NUMERIC                                        
              MOVE ZEROES TO K110-AMT-FLEXI                                     
           END-IF.                                                              
                                                                                
           IF K110-AMT-POG NOT NUMERIC                                          
              MOVE ZEROES TO K110-AMT-POG                                       
           END-IF.                                                              
                                                                                
           IF K110-AMT-OCS-DISCOUNT NOT NUMERIC                                 
              MOVE ZEROES TO K110-AMT-OCS-DISCOUNT                              
           END-IF.                                                              
                                                                                
           IF K110-AMT-CASH-GRANT NOT NUMERIC                                   
              MOVE ZEROES TO K110-AMT-CASH-GRANT                                
           END-IF.                                                              
                                                                                
           IF K110-AMT-FLAT-DISCOUNT NOT NUMERIC                                
              MOVE ZEROES TO K110-AMT-FLAT-DISCOUNT                             
           END-IF.                                                              
                                                                                
           IF K110-1ST-OFFER-SELLING-PRICE NOT NUMERIC                          
              MOVE ZEROES TO K110-1ST-OFFER-SELLING-PRICE                       
           END-IF.                                                              
                                                                                
           IF K110-AMT-SC-SPR-PREMIUM NOT NUMERIC                               
              MOVE ZEROES TO K110-AMT-SC-SPR-PREMIUM                            
           END-IF.                                                              
                                                                                
           IF K800-AMT-LEASE-FEE NOT NUMERIC                                    
              MOVE ZEROES                 TO K800-AMT-LEASE-FEE                 
           END-IF.                                                              
                                                                                
           IF K800-AMT-CONVEY-FEE NOT NUMERIC                                   
              MOVE ZEROES                 TO K800-AMT-CONVEY-FEE                
           END-IF                                                               
                                                                                
           MOVE ZEROES                    TO WS-CNT-HA.                         
                                                                                
           IF K800-NUM-NRIC1 NOT = SPACES AND LOW-VALUES                        
              ADD 1                       TO WS-CNT-HA                          
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                        
              ADD 1                       TO WS-CNT-HA                          
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                        
              ADD 1                       TO WS-CNT-HA                          
           END-IF.                                                              
                                                                                
           IF K800-NUM-NRIC4 NOT = SPACES AND LOW-VALUES                        
              ADD 1                       TO WS-CNT-HA                          
           END-IF.                                                              
                                                                                
                                                                                
       3001-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       4000-PROCESS-RECORDS.                                                    
      ******************************************************************        
                                                                                
           MOVE SPACES                 TO L38P-DETAIL01.                        
           INITIALIZE                     L38P-DETAIL01.                        
                                                                                
           PERFORM 3001-INITIALIZE-FIELDS THRU 3001-EXIT.                       
                                                                                
           ADD 1                          TO WS-SNO1.                           
           MOVE WS-SNO1                   TO WS-SNO-ZZZZ9.                      
           MOVE WS-SNO-ZZZZ9              TO L38P-SN.                           
           MOVE K410-NUM-REGN             TO L38P-REGN.                         
           MOVE K800-NUM-SERS-RS          TO L38P-RSTAG.                        
           MOVE K800-NUM-LEASE-TENURE     TO L38P-TENURE.                       
           MOVE K800-NUM-FLAT-TYPE        TO L38P-FT.                           
                                                                                
           IF K410-DTE-KEY-ISSUE NOT = SPACES AND LOW-VALUES                    
                                              AND ZEROES                        
              STRING K410-DTE-KEY-ISSUE(7:2) '/'                                
                     K410-DTE-KEY-ISSUE(5:2) '/'                                
                     K410-DTE-KEY-ISSUE(1:4)                                    
              DELIMITED BY SIZE INTO L38P-TP-DATE                               
           END-IF.                                                              
                                                                                
           MOVE K800-AMT-PREMIUM                TO WS-AMT-FORMAT.               
           MOVE WS-AMT-FORMAT                   TO L38P-PREM-LEVY.              
                                                                                
           MOVE K800-AMT-DEFER-PREM             TO WS-AMT-FORMAT.               
           MOVE WS-AMT-FORMAT                   TO L38P-PREM-DEFER.             
                                                                                
           MOVE K800-AMT-PREM-PAID-CASH-COMP    TO WS-AMT-FORMAT.               
           MOVE WS-AMT-FORMAT                   TO L38P-PREM-CASH.              
                                                                                
           MOVE K800-AMT-DEFER-LEVY             TO WS-AMT-FORMAT.               
           MOVE WS-AMT-FORMAT                   TO L38P-LEVY-DEFER.             
                                                                                
                                                                                
           MOVE K110-1ST-OFFER-SELLING-PRICE    TO WS-AMT-FORMAT.               
           MOVE WS-AMT-FORMAT                   TO L38P-OFFER-SP.               
                                                                                
           MOVE K110-AMT-FLAT-DISCOUNT          TO WS-AMT-FORMAT.               
           MOVE WS-AMT-FORMAT                   TO L38P-SERS-DISC.              
                                                                                
           MOVE K110-AMT-SC-SPR-PREMIUM         TO WS-AMT-FORMAT.               
           MOVE WS-AMT-FORMAT                   TO L38P-SC-SPR.                 
                                                                                
           MOVE K110-AMT-SINGLES-PREMIUM        TO WS-AMT-FORMAT.               
           MOVE WS-AMT-FORMAT                   TO L38P-SGL-PREM.               
                                                                                
           MOVE K110-AMT-ELDERLY-PREMIUM        TO WS-AMT-FORMAT.               
           MOVE WS-AMT-FORMAT                   TO L38P-ELD-PREM.               
                                                                                
           MOVE K110-AMT-OCS-DISCOUNT           TO WS-AMT-FORMAT.               
           MOVE WS-AMT-FORMAT                   TO L38P-OCS.                    
                                                                                
           MOVE K110-AMT-FLEXI                  TO WS-AMT-FORMAT.               
           MOVE WS-AMT-FORMAT                   TO L38P-FLX.                    
                                                                                
           MOVE K110-AMT-POE                    TO WS-AMT-FORMAT.               
           MOVE WS-AMT-FORMAT                   TO L38P-POE.                    
                                                                                
           MOVE K110-AMT-POG                    TO WS-AMT-FORMAT.               
           MOVE WS-AMT-FORMAT                   TO L38P-POG.                    
                                                                                
           COMPUTE WS-AMT-POD =   K110-AMT-POD +                                
                                  K110-AMT-POF +                                
                                  K110-AMT-POW.                                 
                                                                                
           MOVE WS-AMT-POD                      TO WS-AMT-FORMAT.               
           MOVE WS-AMT-FORMAT                   TO L38P-PODFW.                  
                                                                                
           PERFORM 6000-PRINT-REPORT            THRU 6000-EXIT.                 
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       5000-HEADER-EMAIL.                                                       
      ******************************************************************        
                                                                                
            WRITE MAILC38P-REC FROM WS-MAIL-RPT1.                               
            WRITE MAILC38P-REC FROM WS-MAIL-RPT2.                               
            WRITE MAILC38P-REC FROM WS-MAIL-RPT3.                               
            WRITE MAILC38P-REC FROM WS-MAIL-RPT3A.                              
            WRITE MAILC38P-REC FROM WS-MAIL-RPT3B.                              
            WRITE MAILC38P-REC FROM WS-MAIL-RPT4.                               
            WRITE MAILC38P-REC FROM WS-MAIL-RPT5.                               
            WRITE MAILC38P-REC FROM WS-MAIL-RPT6.                               
            WRITE MAILC38P-REC FROM WS-MAIL-RPT6A.                              
            WRITE MAILC38P-REC FROM WS-MAIL-RPT6B.                              
                                                                                
            MOVE SPACES          TO  WS-MAIL-RPT7.                              
                                                                                
            STRING '(' WS-CURR-CCYY '/' WS-CURR-MM ')'                          
                  DELIMITED BY SIZE INTO L38P-DTE-HDR.                          
                                                                                
            STRING 'SUBJECT: SERS CASES WITH SERS RS TAG '                      
                   'AND TP DATE (' WS-CURR-CCYY '/' WS-CURR-MM ')'              
                  DELIMITED BY SIZE INTO WS-MAIL-RPT7.                          
                                                                                
            WRITE MAILC38P-REC FROM WS-MAIL-RPT7.                               
                                                                                
            STRING WS-CURR-DD '/' WS-CURR-MM '/' WS-CURR-CCYY                   
                    DELIMITED BY SIZE INTO WS-DATE-RPT.                         
                                                                                
            WRITE MAILC38P-REC FROM WS-DATE-RPT-DTL.                            
                                                                                
            MOVE SPACES TO MAILC38P-REC.                                        
            WRITE MAILC38P-REC.                                                 
                                                                                
            WRITE MAILC38P-REC FROM L38P-HEADER01.                              
            WRITE MAILC38P-REC FROM L38P-HEADER02.                              
                                                                                
            MOVE SPACES TO MAILC38P-REC.                                        
            WRITE MAILC38P-REC.                                                 
                                                                                
            WRITE MAILC38P-REC FROM L38P-HEADER03.                              
                                                                                
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       6000-PRINT-REPORT.                                                       
      ******************************************************************        
                                                                                
           WRITE BP13L38P-REC               FROM L38P-DETAIL01.                 
           WRITE MAILC38P-REC               FROM L38P-DETAIL01.                 
                                                                                
           MOVE SPACES                        TO L38P-DETAIL01.                 
           WRITE MAILC38P-REC               FROM L38P-DETAIL01.                 
           WRITE BP13L38P-REC               FROM L38P-DETAIL01.                 
                                                                                
                                                                                
           ADD 1                              TO WS-CNT-L38P-WRITE.             
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       7000-PRINT-HEADER.                                                       
      ******************************************************************        
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)      TO WS-SYSTEM-DATE               
                                                   WS-CURR-DATE.                
                                                                                
           IF WS-CURR-MM = 01                                                   
              MOVE 12                         TO WS-CURR-MM                     
              SUBTRACT 1                    FROM WS-CURR-CCYY                   
           ELSE                                                                 
              SUBTRACT 1                    FROM WS-CURR-MM                     
           END-IF.                                                              
                                                                                
           STRING WS-SYSTEM-DATE(7:2) '/'                                       
                  WS-SYSTEM-DATE(5:2) '/'                                       
                  WS-SYSTEM-DATE(1:4)                                           
              DELIMITED BY SIZE INTO L38P-DATE-HDR.                             
                                                                                
                                                                                
           ADD 1                               TO WS-PAGE-CTR.                  
           MOVE ZEROES                         TO L38P-PAGE-HDR.                
           MOVE WS-PAGE-CTR                    TO L38P-PAGE-HDR.                
                                                                                
           WRITE BP13L38P-REC    FROM L38P-HEADER01 AFTER PAGE.                 
           WRITE BP13L38P-REC    FROM L38P-HEADER02.                            
           WRITE BP13L38P-REC    FROM L38P-HEADER03 AFTER 2.                    
                                                                                
           INITIALIZE WS-LINE-CTR.                                              
           MOVE 5  TO WS-LINE-CTR.                                              
                                                                                
           PERFORM 5000-HEADER-EMAIL           THRU 5000-EXIT.                  
                                                                                
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9999-CLOSE-ROUTINE.                                                      
      ******************************************************************        
                                                                                
           DISPLAY '*************** CONTROL    TOTALS ***************'.         
           DISPLAY 'PROGRAM-ID : BP13C38P'.                                     
           DISPLAY '-------------------------------------------------'.         
           DISPLAY '(1) NO OF BP13F410 RECORDS READ............. : '            
                    WS-F410-READ.                                               
           DISPLAY '(2) NO OF BP13L38P RECORDS WRITTEN.......... : '            
                    WS-CNT-L38P-WRITE.                                          
                                                                                
           CLOSE    BP13F410                                                    
                    BP13K800                                                    
                    BP13K893                                                    
                    BM06K110                                                    
                    BP13L38P                                                    
                    MAILC38P.                                                   
                                                                                
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BM06K110 - ERROR CLOSING : ' WS-K110-STATUS              
              MOVE WS-K110-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K800 - ERROR CLOSING : ' WS-K800-STATUS              
              MOVE WS-K800-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           IF WS-K893-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K893 - ERROR CLOSING : ' WS-K893-STATUS              
              MOVE WS-K893-STATUS                   TO RETURN-CODE              
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
