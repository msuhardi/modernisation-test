       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.   BP13CFA2.                                                  
       AUTHOR.       RJB1.                                                      
       DATE-WRITTEN.  27/04/17.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC)                                 *            
      * ========================================================== *            
      *  OBJECTIVE: TO UPDATE BP13K482 AND BP13K486 BY DELETING    *            
      *             THE RECORDS OF THOSE WITH COMPLETED PAYMENT    *            
      *             AND WRITING THEM INTO SEQUENTIAL FILES AS      *            
      *             BACKUP                                         *            
      *                                                            *            
      *  I    :  P13F482A - ACCOUNTS WITH COMPLETED SMS-TAG. THIS  *            
      *                     FILE COMES FROM BP13CFA1 OUTPUT        *            
      *  I/O  :  BP13K486                                          *            
      *       :  BP13K482                                          *            
      *  O    :  BP13F482                                          *            
      *       :  BP13F486                                          *            
      * -----------------------------------------------------------*            
      * REF NO    DATE  BY   DESCRIPTIONS                          *            
      * -------- ------ ---- ------------                          *            
      * BP135315 050617 RJB1 NEW PGM                               *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT P13F482A ASSIGN TO P13F482A.                                  
                                                                                
           SELECT BP13K486 ASSIGN TO BP13K486                                   
                  ACCESS          IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K486-NUM-HDBREF-EXIST                      
                  FILE STATUS     IS WS-K486-STATUS.                            
                                                                                
           SELECT BP13K482 ASSIGN TO BP13K482                                   
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K482-KEY-FLD                               
                  ACCESS          IS RANDOM                                     
                  FILE STATUS     IS WS-K482-STATUS.                            
                                                                                
           SELECT BP13F482 ASSIGN TO BP13F482.                                  
           SELECT BP13F486 ASSIGN TO BP13F486.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  P13F482A   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 100 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD                                
                      RECORDING MODE IS F.                                      
       01  F482-RECORD.                                                         
           05 F482A-KEY-FLD.                                                    
              10 F482A-NUM-HDBREF-NEW        PIC X(11).                         
              10 F482A-NUM-HDBREF-DISPOSAL   PIC X(11).                         
           05 F482A-DTE-COMPLETE             PIC X(08).                         
           05 F482A-CDE-SMS-TAG              PIC X(01).                         
           05 FILLER                         PIC X(69).                         
                                                                                
       FD  BP13K486                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K486.                                                           
                                                                                
       FD  BP13K482                                                             
           RECORD CONTAINS 1000 CHARACTERS.                                     
       COPY BP13K482.                                                           
                                                                                
       FD  BP13F482   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 1000 CHARACTERS                           
                      LABEL RECORDS ARE STANDARD                                
                      RECORDING MODE IS F.                                      
       COPY BP13F482.                                                           
                                                                                
       FD  BP13F486   BLOCK CONTAINS 0 RECORDS                                  
                      RECORD CONTAINS 400 CHARACTERS                            
                      LABEL RECORDS ARE STANDARD                                
                      RECORDING MODE IS F.                                      
       01  BP13F486-REC                      PIC X(400).                        
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-SWITCHES.                                                         
           05  WS-K482-STATUS            PIC 99      VALUE ZEROS.               
           05  WS-K486-STATUS            PIC 99      VALUE ZEROS.               
           05  WS-F482-STATUS            PIC 99      VALUE ZEROS.               
           05  WS-F486-STATUS            PIC 99      VALUE ZEROS.               
           05  WS-F482A-EOF              PIC X       VALUE 'N'.                 
           05  WS-K486-EOF               PIC X       VALUE 'N'.                 
               88 K486-EOF                           VALUE 'Y'.                 
           05  WS-F482A-READ             PIC 9(6)    VALUE ZEROS.               
           05  WS-K482-READ              PIC 9(6)    VALUE ZEROS.               
           05  WS-K482-DELETE            PIC 9(6)    VALUE ZEROS.               
           05  WS-K486-READ              PIC 9(6)    VALUE ZEROS.               
           05  WS-K486-DELETE            PIC 9(6)    VALUE ZEROS.               
           05  WS-F482-WRITE             PIC 9(6)    VALUE ZEROS.               
           05  WS-F486-WRITE             PIC 9(6)    VALUE ZEROS.               
                                                                                
       01  WS-VARIABLE.                                                         
           05  WS-K486-KEY               PIC X(31)   VALUE SPACES.              
           05  WS-K482-KEY               PIC X(22)   VALUE SPACES.              
                                                                                
       01  WS-DATE-TIME.                                                        
           05  WS-SYS-DATE               PIC X(08)   VALUE SPACES.              
           05  WS-SYS-TIME               PIC X(09)   VALUE SPACES.              
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
            PERFORM 100-OPEN-FILES      THRU 100-EXIT.                          
            PERFORM 200-READ-P13F482A   THRU 200-EXIT.                          
            PERFORM 500-PROCESS         THRU 500-EXIT                           
                UNTIL WS-F482A-EOF = 'Y'                                        
            PERFORM 999-CLOSE-FILES     THRU 999-EXIT.                          
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  P13F482A                                                 
                I-O    BP13K486                                                 
                       BP13K482                                                 
                OUTPUT BP13F482                                                 
                       BP13F486.                                                
                                                                                
           IF WS-K482-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, K482-STATUS ' WS-K482-STATUS              
              MOVE WS-K482-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           IF WS-K486-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING ERROR, K486-STATUS ' WS-K486-STATUS              
              MOVE WS-K486-STATUS       TO RETURN-CODE                          
              PERFORM 999-CLOSE-FILES THRU 999-EXIT.                            
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-SYS-DATE.                           
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-P13F482A.                                                       
      *-------------------------------------------------------------            
           READ P13F482A AT END                                                 
           MOVE 'Y'                      TO WS-F482A-EOF                        
           GO TO 200-EXIT.                                                      
                                                                                
           ADD 1                         TO WS-F482A-READ.                      
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       500-PROCESS.                                                             
      *-------------------------------------------------------------            
           PERFORM 600-PROCESS-BP13K482                                         
              THRU 600-EXIT.                                                    
                                                                                
           PERFORM 700-PROCESS-BP13K486                                         
              THRU 700-EXIT.                                                    
                                                                                
           PERFORM 200-READ-P13F482A THRU 200-EXIT.                             
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       600-PROCESS-BP13K482.                                                    
      *-------------------------------------------------------------            
           MOVE SPACES                   TO K482-KEY-FLD.                       
           MOVE F482A-KEY-FLD            TO K482-KEY-FLD.                       
                                                                                
           PERFORM 610-READ-BP13K482                                            
              THRU 610-EXIT.                                                    
                                                                                
           IF WS-K482-STATUS = 00                                               
              PERFORM 620-WRITE-F482                                            
                 THRU 620-EXIT                                                  
              PERFORM 630-DELETE-K482                                           
                 THRU 630-EXIT                                                  
           END-IF.                                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       610-READ-BP13K482.                                                       
      *-------------------------------------------------------------            
           READ BP13K482.                                                       
                                                                                
           IF WS-K482-STATUS = 00                                               
              ADD 1 TO WS-K482-READ                                             
           ELSE                                                                 
              IF WS-K482-STATUS = 10 OR 23                                      
                 DISPLAY 'K482 RECORD NOT FOUND : ' K482-KEY-FLD                
              ELSE                                                              
                 DISPLAY 'BP13K482 - READ ERROR (' WS-K482-STATUS               
                          '), KEY IS ('  K482-KEY-FLD ')'                       
                 PERFORM 999-CLOSE-FILES                                        
                    THRU 999-EXIT                                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       610-EXIT.                                                                
           EXIT.                                                                
                                                                                
       620-WRITE-F482.                                                          
      *-------------------------------------------------------------            
           MOVE SPACES                     TO BP13F482-REC.                     
           INITIALIZE                         BP13F482-REC.                     
                                                                                
           MOVE  BP13K482-REC              TO BP13F482-REC.                     
                                                                                
           WRITE BP13F482-REC.                                                  
                                                                                
           IF WS-F482-STATUS = ZEROES                                           
              ADD 1                        TO WS-F482-WRITE                     
           ELSE                                                                 
              DISPLAY 'WRITE ERROR F482 - STATUS ' WS-F482-STATUS               
                      ', KEY: ' K482-KEY-FLD                                    
              MOVE WS-F482-STATUS          TO RETURN-CODE                       
              PERFORM 999-CLOSE-FILES    THRU 999-EXIT                          
           END-IF.                                                              
                                                                                
       620-EXIT.                                                                
           EXIT.                                                                
                                                                                
       630-DELETE-K482.                                                         
      *-------------------------------------------------------------            
           MOVE K482-KEY-FLD           TO WS-K482-KEY.                          
                                                                                
           DELETE BP13K482.                                                     
                                                                                
           IF WS-K482-STATUS = 00                                               
              ADD 1                   TO WS-K482-DELETE                         
              DISPLAY 'BP13K482 DELETE SUCCESSFUL - ' WS-K482-KEY               
           ELSE                                                                 
              DISPLAY 'ERROR DELETING  BP13K482 STATUS '                        
                                       WS-K482-STATUS                           
                      ', KEY: ' K482-KEY-FLD                                    
              MOVE WS-K482-STATUS      TO RETURN-CODE                           
              PERFORM 999-CLOSE-FILES  THRU 999-EXIT                            
           END-IF.                                                              
                                                                                
       630-EXIT.                                                                
           EXIT.                                                                
                                                                                
       700-PROCESS-BP13K486.                                                    
      *-------------------------------------------------------------            
           MOVE SPACES                    TO K486-KEY-FLD.                      
           MOVE F482A-NUM-HDBREF-DISPOSAL TO K486-NUM-HDBREF-EXIST.             
                                                                                
           PERFORM 710-START-K486                                               
              THRU 710-EXIT.                                                    
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       710-START-K486.                                                          
      *-------------------------------------------------------------            
           MOVE 'N'                   TO WS-K486-EOF.                           
                                                                                
           START BP13K486 KEY >= K486-NUM-HDBREF-EXIST.                         
                                                                                
           IF WS-K486-STATUS = 00 OR 02                                         
              PERFORM 720-READNXT-K486                                          
                 THRU 720-EXIT                                                  
                UNTIL K486-EOF OR                                               
                K486-NUM-HDBREF-EXIST NOT = F482A-NUM-HDBREF-DISPOSAL           
           ELSE                                                                 
              IF WS-K486-STATUS = 10 OR 23                                      
                 MOVE 'Y'             TO WS-K486-EOF                            
              ELSE                                                              
                 DISPLAY 'BP13K486 - START ERROR (' WS-K486-STATUS              
                          '), KEY IS ('  K486-KEY-FLD ')'                       
                 PERFORM 999-CLOSE-FILES                                        
                    THRU 999-EXIT                                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       710-EXIT.                                                                
           EXIT.                                                                
                                                                                
       720-READNXT-K486.                                                        
      *-------------------------------------------------------------            
           READ BP13K486 NEXT RECORD.                                           
                                                                                
           IF WS-K486-STATUS = 00 OR 02                                         
              IF K486-NUM-HDBREF-EXIST = F482A-NUM-HDBREF-DISPOSAL              
                 ADD 1                   TO WS-K486-READ                        
                 PERFORM 730-WRITE-F486                                         
                    THRU 730-EXIT                                               
                 PERFORM 740-DELETE-K486                                        
                    THRU 740-EXIT                                               
              ELSE                                                              
                 MOVE 'Y'                TO WS-K486-EOF                         
              END-IF                                                            
           ELSE                                                                 
              IF WS-K486-STATUS = 10 OR 23                                      
                 MOVE 'Y'                TO WS-K486-EOF                         
              ELSE                                                              
                 MOVE 'Y'                TO WS-K486-EOF                         
                 DISPLAY 'BP13K486 - READ NEXT ERROR (' WS-K486-STATUS          
                         ') ,KEY IS ('  K486-KEY-FLD ')'                        
                 PERFORM 999-CLOSE-FILES                                        
                    THRU 999-EXIT                                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       720-EXIT.                                                                
           EXIT.                                                                
                                                                                
       730-WRITE-F486.                                                          
      *-------------------------------------------------------------            
           MOVE SPACES                     TO BP13F486-REC.                     
           INITIALIZE                         BP13F486-REC.                     
                                                                                
           MOVE  BP13K486-REC              TO BP13F486-REC.                     
                                                                                
           WRITE BP13F486-REC.                                                  
                                                                                
           IF WS-F486-STATUS = ZEROES                                           
              ADD 1                        TO WS-F486-WRITE                     
           ELSE                                                                 
              DISPLAY 'WRITE ERROR F486 - STATUS ' WS-F486-STATUS               
                      ', KEY: ' K486-KEY-FLD                                    
              MOVE WS-F486-STATUS          TO RETURN-CODE                       
              PERFORM 999-CLOSE-FILES    THRU 999-EXIT                          
           END-IF.                                                              
                                                                                
       730-EXIT.                                                                
           EXIT.                                                                
                                                                                
       740-DELETE-K486.                                                         
      *-------------------------------------------------------------            
                                                                                
           MOVE K486-KEY-FLD           TO WS-K486-KEY.                          
                                                                                
           DELETE BP13K486.                                                     
                                                                                
           IF WS-K486-STATUS = 00                                               
              ADD 1                   TO WS-K486-DELETE                         
              DISPLAY 'BP13K486 DELETE SUCCESSFUL - ' WS-K486-KEY               
           ELSE                                                                 
              DISPLAY 'ERROR DELETING  BP13K486 STATUS '                        
                                       WS-K486-STATUS                           
                      ', KEY: ' K486-KEY-FLD                                    
              MOVE WS-K486-STATUS      TO RETURN-CODE                           
              PERFORM 999-CLOSE-FILES  THRU 999-EXIT                            
           END-IF.                                                              
                                                                                
       740-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       999-CLOSE-FILES.                                                         
      *-------------------------------------------------------------            
           DISPLAY '* ------------ BP13CFA2 ------------- * '.                  
           DISPLAY 'NO OF REC READ    BP13F482 = ' WS-F482A-READ.               
           DISPLAY 'NO OF REC READ    BP13K486 = ' WS-K486-READ.                
           DISPLAY 'NO OF REC DELETED BP13K486 = ' WS-K486-DELETE.              
           DISPLAY 'NO OF REC READ    BP13K482 = ' WS-K482-READ.                
           DISPLAY 'NO OF REC DELETED BP13K482 = ' WS-K482-DELETE.              
           DISPLAY 'NO OF REC WRITTEN BP13F482 = ' WS-F482-WRITE.               
           DISPLAY 'NO OF REC WRITTEN BP13F486 = ' WS-F486-WRITE.               
                                                                                
           CLOSE P13F482A                                                       
                 BP13K486                                                       
                 BP13K482                                                       
                 BP13F482                                                       
                 BP13F486.                                                      
                                                                                
           IF WS-K486-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K486, STATUS ' WS-K486-STATUS          
              MOVE WS-K486-STATUS        TO RETURN-CODE.                        
                                                                                
           IF WS-K482-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K482, STATUS ' WS-K482-STATUS          
              MOVE WS-K482-STATUS        TO RETURN-CODE.                        
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
