       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C211.                                                 
       AUTHOR.        JAM.                                                      
       DATE-WRITTEN.  24/05/2001.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT         (BP13)                        *            
      * ========================================================== *            
      *                                                            *            
      *  DESCRIPTION :  TO DELETE RECORDS BASED ON THE FOLLOWING   *            
      *                 TRANSACTION FILES:                         *            
      *                                                            *            
      *  INPUT TRANS  :  BP13F230                                  *            
      *  I-O FILES    :  BP13K230                                  *            
      * ---------------------------------------------------------- *            
      * CHG-NO.  BY  ON       DESCRIPTION                          *            
      * -------- --- -------- -----------                          *            
      * BP131017 JAM 20010524 NEW PROGRAM.                         *            
      *                       TO DELETE RECORDS FROM BP13K230      *            
      * BP132278 JF4 20030328 CHGE BP13F230 LRECL TO 450.          *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F230   ASSIGN TO BP13F230.                                
                                                                                
           SELECT BP13K230  ASSIGN       TO BP13K230                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K230-KEY-FLD                        
                            FILE STATUS  IS BP13K230-STATUS.                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BP13F230                                                            
            RECORDING MODE  F                                                   
            LABEL RECORDS   ARE STANDARD                                        
            BLOCK CONTAINS  0   RECORDS                                         
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13F230.                                                           
                                                                                
       FD   BP13K230                                                            
            RECORD CONTAINS 450 CHARACTERS.                                     
       COPY BP13K230.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  BP13K230-STATUS           PIC 9(2)   VALUE ZEROES.               
           05  BP13F230-STATUS           PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-F230-READ-CTR          PIC 9(8)   VALUE ZEROES.               
           05  WS-K230-DELETE-CTR        PIC 9(8)   VALUE ZEROES.               
                                                                                
           05  WS-F230-FLAG              PIC X(1)   VALUE 'N'.                  
               88 WS-F230-EOF            VALUE 'Y'.                             
       01  WS-EDT-DATE.                                                         
           05  WS-EDT-DD           PIC 99.                                      
           05  FILLER              PIC X      VALUE '/'.                        
           05  WS-EDT-MM           PIC 99.                                      
           05  FILLER              PIC X      VALUE '/'.                        
           05  WS-EDT-CCYY         PIC 9999.                                    
                                                                                
       01  WS-DTE-TMP              PIC X(10).                                   
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY         PIC 9999.                                    
           05  WS-CUR-MM           PIC 99.                                      
           05  WS-CUR-DD           PIC 99.                                      
                                                                                
       01  WS-LINER           PIC X(96) VALUE ALL '~'.                          
                                                                                
       01  WS-LINER1.                                                           
           10  FILLER         PIC X(90) VALUE ALL "-".                          
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE SECTION.                                               
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 3000-READ-BP13F230      THRU 3000-EXIT                       
           PERFORM 2000-PROCESS-BP13F230   THRU 2000-EXIT                       
                   UNTIL WS-F230-EOF.                                           
           PERFORM 9000-CLOSE-RTN          THRU 9000-EXIT.                      
                                                                                
       0000-MAIN-EXIT.                                                          
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F230                                                 
                I-O    BP13K230.                                                
                                                                                
           IF BP13K230-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING BP13K230 : ' BP13K230-STATUS               
              MOVE BP13K230-STATUS             TO RETURN-CODE                   
              PERFORM 9000-CLOSE-RTN           THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           IF BP13F230-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING BP13F230 : ' BP13F230-STATUS               
              MOVE BP13F230-STATUS             TO RETURN-CODE                   
              PERFORM 9000-CLOSE-RTN           THRU 9000-EXIT                   
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
           MOVE WS-CUR-CCYY           TO WS-EDT-CCYY.                           
           MOVE WS-CUR-MM             TO WS-EDT-MM.                             
           MOVE WS-CUR-DD             TO WS-EDT-DD.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       2000-PROCESS-BP13F230.                                                   
      *-------------------------------------------------------------            
           MOVE F230-HEADER-REC TO K230-DETAIL-REC.                             
           PERFORM 2100-DELETE-BP13K230 THRU 2100-EXIT.                         
           PERFORM 3000-READ-BP13F230   THRU 3000-EXIT.                         
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2100-DELETE-BP13K230.                                                    
           READ BP13K230 KEY IS K230-KEY-FLD.                                   
           EVALUATE BP13K230-STATUS                                             
           WHEN 00                                                              
                DELETE BP13K230                                                 
                IF BP13K230-STATUS = 00                                         
                   ADD 1 TO WS-K230-DELETE-CTR                                  
                ELSE                                                            
                   DISPLAY 'INVALID DELETE BP13K230 KEY: '                      
                           K230-KEY-FLD                                         
                END-IF                                                          
           WHEN OTHER                                                           
                DISPLAY 'RECORD NOT FOUND, BP13K230, KEY: '                     
                                           K230-KEY-FLD                         
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
       3000-READ-BP13F230.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BP13F230 AT END                                                 
                MOVE 'Y' TO WS-F230-FLAG                                        
                GO TO 3000-EXIT.                                                
           ADD 1 TO WS-F230-READ-CTR.                                           
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-RTN.                                                          
      *-------------------------------------------------------------            
           DISPLAY WS-LINER.                                                    
           DISPLAY ' PROGRAM : BP13C211        '.                               
           DISPLAY ' PURPOSE : DELETE RECORDS: '.                               
           DISPLAY ' RUN DATE: ' WS-EDT-DATE.                                   
           DISPLAY WS-LINER.                                                    
           DISPLAY ' '.                                                         
           DISPLAY WS-LINER1.                                                   
           DISPLAY 'BP13F230 RECORDS READ    => ' WS-F230-READ-CTR.             
           DISPLAY 'BP13K230 RECORDS DETELED => ' WS-K230-DELETE-CTR.           
           DISPLAY WS-LINER1.                                                   
                                                                                
           CLOSE BP13F230, BP13K230.                                            
                                                                                
           IF BP13K230-STATUS NOT = 0 AND 97                                    
              MOVE BP13K230-STATUS       TO RETURN-CODE                         
              DISPLAY 'BP13K230 CLOSE ERROR : ' BP13K230-STATUS                 
           END-IF.                                                              
                                                                                
           IF BP13F230-STATUS NOT = 0 AND 97                                    
              MOVE BP13F230-STATUS       TO RETURN-CODE                         
              DISPLAY 'BP13F230 CLOSE ERROR : ' BP13F230-STATUS                 
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
