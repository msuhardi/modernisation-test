       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C389.                                                 
       AUTHOR.        MTDULLONA.                                                
      *DATE-WRITTEN.  FEB2004.                                                  
      ***************************************************************           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      ***************************************************************           
      * OBJECTIVE : TO PRINT WEEKLY REPORT ON KEYS BORROWED FOR     *           
      *             MORE THAN 6 WEEKS FROM ACCEPTANCE DATE          *           
      *                                                             *           
      *-------------------------------------------------------------*           
      * INPUT FILES: BP13F435 (SORTED BY KEY LOCATION)              *           
      *              BP13K431                                       *           
      *              BM06K110                                       *           
      *                                                             *           
      * OUTPUT FILE: BP13L389 (REPORT)                              *           
      *-------------------------------------------------------------*           
      * CHG REF  OIC DATE     DESCRIPTION                           *           
      *--------- --- -------- --------------------------------------*           
      * BP132486 MTD 26/02/04 PRINT WEEKLY REPORT ON KEYS BORROWED  *           
      * BP132486 MTD 12/03/04 CHANGE BP13K437 TO BP13K431           *           
      ***************************************************************           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K431  ASSIGN       TO BP13K431                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K431-KEY-FLD                        
                            FILE STATUS  IS WS-K431-STATUS.                     
                                                                                
           SELECT BM06K110  ASSIGN       TO BM06K110                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS DYNAMIC                             
                            RECORD KEY   IS K110-KEY-FLD                        
                            FILE STATUS  IS WS-K110-STATUS.                     
                                                                                
           SELECT BP13F435  ASSIGN TO BP13F435.                                 
                                                                                
           SELECT BP13L389  ASSIGN TO BP13L389.                                 
                                                                                
       DATA DIVISION.                                                           
      *--------------------------------------------------------------           
       FILE SECTION.                                                            
      *--------------------------------------------------------------           
                                                                                
       FD  BP13K431                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13K431.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BP13F435                                                             
           RECORD CONTAINS 250 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       COPY BP13F435.                                                           
                                                                                
       FD  BP13L389                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS OMITTED                                                
           RECORDING MODE IS F.                                                 
       01  BP13L389-REC                       PIC X(132).                       
                                                                                
      *--------------------------------------------------------------           
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------           
       01  WS-WORK-VAR.                                                         
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-DATE         PIC X(08) VALUE SPACES.                  
               10  FILLER              PIC X(13) VALUE SPACES.                  
           05  WS-DISP-DATE.                                                    
               10  WS-DISP-DD          PIC X(02) VALUE SPACES.                  
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-DISP-MM          PIC X(02) VALUE SPACES.                  
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-DISP-CCYY        PIC X(04) VALUE SPACES.                  
           05  WS-RETCODE              PIC -ZZZZ.                               
           05  WS-TEL-BORROWER         PIC X(08).                               
           05  WS-DTE-ACCEPTANCE       PIC 9(08).                               
           05  WS-DTE-KEY-AVAIL        PIC 9(08).                               
           05  WS-CURRENT-DATE         PIC 9(08).                               
           05  WS-BRWD-PERIOD          PIC 9(04).                               
           05  WS-BRWD-PERIOD1         PIC 9(04).                               
           05  WS-BRWD-PERIOD2         PIC 9(04).                               
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K431-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K110-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-END-OF-F435-SW       PIC X(01) VALUE SPACES.                  
               88  C-END-OF-F435       VALUE 'Y'.                               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F435-READ-CTR       PIC 9(06) VALUE ZEROES.                   
           05  WS-PRT-CTR             PIC 9(06) VALUE ZEROES.                   
           05  WS-LINE-CTR            PIC 9(02) VALUE ZEROES.                   
           05  WS-PAGE-CTR            PIC 9(04) VALUE ZEROES.                   
           05  WS-SNO-CTR             PIC 9(04) VALUE ZEROES.                   
                                                                                
      *-------------------------------------------------------------            
      * REPORT LAYOUT                                                           
      *-------------------------------------------------------------            
       01  L389-HEAD-01.                                                        
           05 FILLER                   PIC X(55)  VALUE 'BP13L389'.             
           05 FILLER                   PIC X(20)  VALUE                         
              'SYSTEM OF COMMITMENT'.                                           
           05 FILLER                   PIC X(39)  VALUE SPACES.                 
           05 FILLER                   PIC X(7)   VALUE 'DATE : '.              
           05 L389-DATE                PIC X(10)  VALUE SPACES.                 
                                                                                
       01  L389-HEAD-02.                                                        
           05 FILLER                   PIC X(37)  VALUE SPACES.                 
           05 FILLER                   PIC X(56)  VALUE                         
              'WEEKLY REPORT ON KEYS BORROWED FOR MORE THAN 6 WEEKS'.           
           05 FILLER                   PIC X(21)  VALUE SPACES.                 
           05 FILLER                   PIC X(7)   VALUE 'PAGE : '.              
           05 L389-PAGE                PIC ZZZ9   VALUE ZEROS.                  
                                                                                
       01  L389-HEAD-02-A.                                                      
           05 FILLER                   PIC X(55)  VALUE SPACES.                 
           05 FILLER                   PIC X(20)  VALUE                         
              'FROM ACCEPTANCE DATE'.                                           
                                                                                
       01  L389-HEAD-03.                                                        
           05 FILLER                   PIC X(55)  VALUE                         
              'S/NO KEY  FLAT ADDRESS                   BORROWER NAME '.        
           05 FILLER                   PIC X(55)  VALUE                         
              '                           TEL NO.  REGN NO  DTE-ACCEPT'.        
           05 FILLER                   PIC X(22)  VALUE                         
              '/ DTE-BRWD   PERIOD   '.                                         
                                                                                
       01  L389-HEAD-04.                                                        
           05 FILLER                   PIC X(55)  VALUE                         
              '                                                       '.        
           05 FILLER                   PIC X(55)  VALUE                         
              '                                             DTE-KEY   '.        
           05 FILLER                   PIC X(22)  VALUE                         
              '                      '.                                         
                                                                                
       01  L389-DTL-LINE.                                                       
           05  L389-SNO                 PIC ZZZ9.                               
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  L389-NUM-KEY-LOCN        PIC X(04).                              
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  L389-ADDRESS             PIC X(30).                              
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  L389-NME-BORROWER        PIC X(40).                              
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  L389-TEL-BORROWER        PIC X(08).                              
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  L389-NUM-REGN            PIC X(08).                              
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  L389-DTE-ACCEPTANCE      PIC X(10).                              
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  L389-DTE-BORROWED        PIC X(10).                              
           05  FILLER                   PIC X(01) VALUE SPACES.                 
           05  L389-BRWD-PERIOD         PIC ZZZ9.                               
                                                                                
       01  L389-TOTAL-LINE.                                                     
           05  FILLER                   PIC X(18) VALUE SPACES.                 
           05  FILLER                   PIC X(30) VALUE                         
               'TOTAL NUMBER OF CASES: '.                                       
            05  L389-TOTAL-CASE         PIC Z(08).                              
                                                                                
       01  L389-LINE                    PIC X(132) VALUE ALL '-'.               
                                                                                
      *--------------------------------------------------------------           
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------------           
                                                                                
      *--------------------------------*                                        
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
           PERFORM 3000-PROCESS-RECORDS   THRU 3000-EXIT                        
             UNTIL C-END-OF-F435.                                               
           PERFORM 5000-PRINT-TOTALS      THRU 5000-EXIT.                       
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       1000-INITIALIZATION.                                                     
      *--------------------------------*                                        
                                                                                
           OPEN INPUT    BP13F435                                               
                         BP13K431                                               
                         BM06K110                                               
                OUTPUT   BP13L389.                                              
                                                                                
           IF WS-K431-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BP13K431-STATUS ' WS-K431-STATUS              
              MOVE WS-K431-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BM06K110-STATUS ' WS-K110-STATUS              
              MOVE WS-K110-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
           MOVE WS-SYS-DATE(1:4)       TO WS-DISP-CCYY.                         
           MOVE WS-SYS-DATE(5:2)       TO WS-DISP-MM.                           
           MOVE WS-SYS-DATE(7:2)       TO WS-DISP-DD.                           
                                                                                
           MOVE WS-SYS-DATE            TO WS-CURRENT-DATE.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3000-PROCESS-RECORDS.                                                    
      *--------------------------------*                                        
                                                                                
           READ BP13F435 NEXT AT END                                            
                MOVE 'Y'     TO WS-END-OF-F435-SW                               
                GO TO 3000-EXIT.                                                
                                                                                
           PERFORM 3100-GET-BORROWED-PERIOD       THRU 3100-EXIT.               
                                                                                
           IF WS-BRWD-PERIOD >= 42                                              
              ADD  1      TO WS-PRT-CTR                                         
              PERFORM 3200-PRINT-RECORD        THRU 3200-EXIT                   
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3100-GET-BORROWED-PERIOD.                                                
      *--------------------------------*                                        
                                                                                
           MOVE ZEROES           TO WS-BRWD-PERIOD                              
                                    WS-BRWD-PERIOD1                             
                                    WS-BRWD-PERIOD2.                            
                                                                                
      **-- READ BM06K110 TO CHECK IF FLAT ACCEPTED                              
           MOVE SPACES                   TO K110-KEY-FLD.                       
           MOVE F435-NUM-HDB-REF         TO K110-SCH-ACC-NO.                    
           PERFORM 3110-READ-BM06K110       THRU 3110-EXIT.                     
           IF (K110-REGN-NO = SPACES OR LOW-VALUES) OR                          
              (K110-REGN-NO (1:1) = '#' OR SPACES OR LOW-VALUES)                
              GO TO 3100-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF K110-DTE-ACCEPTANCE NOT = ZEROES AND SPACES AND LOW-VALUES        
              MOVE K110-DTE-ACCEPTANCE      TO WS-DTE-ACCEPTANCE                
              COMPUTE WS-BRWD-PERIOD1 =                                         
                      FUNCTION INTEGER-OF-DATE(WS-CURRENT-DATE) -               
                      FUNCTION INTEGER-OF-DATE(WS-DTE-ACCEPTANCE)               
           END-IF.                                                              
                                                                                
           IF K110-DTE-KEY-AVAIL NOT = ZEROES AND SPACES AND LOW-VALUES         
              MOVE K110-DTE-KEY-AVAIL       TO WS-DTE-KEY-AVAIL                 
              COMPUTE WS-BRWD-PERIOD2 =                                         
                      FUNCTION INTEGER-OF-DATE(WS-CURRENT-DATE) -               
                      FUNCTION INTEGER-OF-DATE(WS-DTE-KEY-AVAIL)                
           END-IF.                                                              
                                                                                
           IF WS-BRWD-PERIOD2 = ZEROES                                          
              MOVE WS-BRWD-PERIOD1    TO WS-BRWD-PERIOD                         
           ELSE                                                                 
              IF WS-BRWD-PERIOD2 > WS-BRWD-PERIOD1                              
                 MOVE WS-BRWD-PERIOD1 TO WS-BRWD-PERIOD                         
              ELSE                                                              
                 MOVE WS-BRWD-PERIOD2 TO WS-BRWD-PERIOD                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3110-READ-BM06K110.                                                      
      *--------------------------------*                                        
                                                                                
           READ BM06K110.                                                       
           EVALUATE WS-K110-STATUS                                              
              WHEN 00                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BM06K110, KEY: '                
                            K110-KEY-FLD                                        
                   MOVE SPACES     TO K110-REC                                  
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BM06K110,STATUS ' WS-K110-STATUS         
                   MOVE WS-K110-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3200-PRINT-RECORD.                                                       
      *--------------------------------*                                        
                                                                                
      **-- READ BP13K431 TO GET BORROWER'S NAME                                 
           MOVE SPACES                   TO K431-KEY-FLD.                       
           MOVE F435-NUM-BORROWER        TO K431-NUM-BORROWER.                  
           PERFORM 3210-READ-BP13K431       THRU 3210-EXIT.                     
                                                                                
           PERFORM 3220-GET-TEL-NUMBER      THRU 3220-EXIT.                     
                                                                                
           PERFORM 4000-CHECK-LINE-CTR          THRU 4000-EXIT.                 
                                                                                
           MOVE SPACES                TO L389-DTL-LINE.                         
           WRITE BP13L389-REC  FROM L389-DTL-LINE.                              
           ADD 1          TO WS-LINE-CTR.                                       
                                                                                
           PERFORM 4000-CHECK-LINE-CTR          THRU 4000-EXIT.                 
                                                                                
           ADD 1                      TO WS-SNO-CTR.                            
           MOVE WS-SNO-CTR            TO L389-SNO.                              
           MOVE F435-NUM-KEY-LOCN     TO L389-NUM-KEY-LOCN.                     
           STRING 'BLK ' F435-NUM-BLK                                           
                  ' #'   F435-NUM-LEVEL                                         
                  '-'    F435-NUM-UNIT-MAIN                                     
                  DELIMITED BY SIZE INTO L389-ADDRESS                           
           END-STRING.                                                          
           MOVE K431-NME-BORROWER         TO L389-NME-BORROWER.                 
           MOVE WS-TEL-BORROWER           TO L389-TEL-BORROWER.                 
           MOVE K110-REGN-NO              TO L389-NUM-REGN.                     
           MOVE WS-BRWD-PERIOD            TO L389-BRWD-PERIOD.                  
           STRING K110-DTE-ACCEPTANCE (7:2) '/'                                 
                  K110-DTE-ACCEPTANCE (5:2) '/'                                 
                  K110-DTE-ACCEPTANCE (1:4)                                     
                  DELIMITED BY SIZE  INTO L389-DTE-ACCEPTANCE                   
           END-STRING.                                                          
           STRING F435-DTE-KEY-BORROW (7:2) '/'                                 
                  F435-DTE-KEY-BORROW (5:2) '/'                                 
                  F435-DTE-KEY-BORROW (1:4)                                     
                  DELIMITED BY SIZE  INTO L389-DTE-BORROWED                     
           END-STRING.                                                          
                                                                                
           WRITE BP13L389-REC  FROM L389-DTL-LINE.                              
           ADD 1          TO WS-LINE-CTR.                                       
                                                                                
      **-- PRINT STREET NAME (NEXT LINE)                                        
           MOVE SPACES                TO L389-DTL-LINE.                         
           PERFORM 4000-CHECK-LINE-CTR          THRU 4000-EXIT.                 
           MOVE F435-NME-STREET       TO L389-ADDRESS.                          
           STRING K110-DTE-KEY-AVAIL (7:2) '/'                                  
                  K110-DTE-KEY-AVAIL (5:2) '/'                                  
                  K110-DTE-KEY-AVAIL (1:4)                                      
                  DELIMITED BY SIZE  INTO L389-DTE-ACCEPTANCE                   
           END-STRING.                                                          
                                                                                
           WRITE BP13L389-REC  FROM L389-DTL-LINE.                              
           ADD 1          TO WS-LINE-CTR.                                       
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3210-READ-BP13K431.                                                      
      *--------------------------------*                                        
                                                                                
           READ BP13K431.                                                       
           EVALUATE WS-K431-STATUS                                              
              WHEN 00                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BP13K431, KEY: '                
                            K431-KEY-FLD                                        
                   MOVE SPACES     TO BP13K431-REC                              
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BP13K431,STATUS ' WS-K431-STATUS         
                   MOVE WS-K431-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3220-GET-TEL-NUMBER.                                                     
      *--------------------------------*                                        
                                                                                
           MOVE SPACES           TO WS-TEL-BORROWER.                            
           IF K431-NUM-TEL-OFFICE NOT = SPACES AND LOW-VALUES                   
              MOVE K431-NUM-TEL-OFFICE       TO WS-TEL-BORROWER                 
           ELSE                                                                 
              IF K431-NUM-TEL-HP NOT = SPACES AND LOW-VALUES                    
                 MOVE K431-NUM-TEL-HP        TO WS-TEL-BORROWER                 
              ELSE                                                              
                 IF K431-NUM-TEL-PAGER NOT = SPACES AND LOW-VALUES              
                    MOVE K431-NUM-TEL-PAGER  TO WS-TEL-BORROWER                 
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3220-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       4000-CHECK-LINE-CTR.                                                     
      *--------------------------------*                                        
                                                                                
           IF WS-LINE-CTR > 55  OR                                              
              WS-LINE-CTR = 0                                                   
              PERFORM 4100-PRINT-PAGE-BREAK   THRU 4100-EXIT                    
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       4100-PRINT-PAGE-BREAK.                                                   
      *--------------------------------*                                        
                                                                                
           ADD   1   TO WS-PAGE-CTR.                                            
           MOVE WS-PAGE-CTR    TO L389-PAGE.                                    
           MOVE WS-DISP-DATE   TO L389-DATE.                                    
           WRITE BP13L389-REC  FROM L389-HEAD-01 AFTER PAGE.                    
           WRITE BP13L389-REC  FROM L389-HEAD-02.                               
           WRITE BP13L389-REC  FROM L389-HEAD-02-A.                             
           WRITE BP13L389-REC  FROM L389-LINE    AFTER 2.                       
           WRITE BP13L389-REC  FROM L389-HEAD-03.                               
           WRITE BP13L389-REC  FROM L389-HEAD-04.                               
           WRITE BP13L389-REC  FROM L389-LINE.                                  
           MOVE 8         TO   WS-LINE-CTR.                                     
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       5000-PRINT-TOTALS.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 4000-CHECK-LINE-CTR          THRU 4000-EXIT.                 
           MOVE SPACES               TO L389-DTL-LINE.                          
           WRITE BP13L389-REC  FROM L389-DTL-LINE.                              
           ADD 1          TO WS-LINE-CTR.                                       
                                                                                
           PERFORM 4000-CHECK-LINE-CTR          THRU 4000-EXIT.                 
           MOVE WS-PRT-CTR           TO L389-TOTAL-CASE.                        
           WRITE BP13L389-REC  FROM L389-TOTAL-LINE.                            
           ADD 1          TO WS-LINE-CTR.                                       
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       9000-CLOSE-FILES.                                                        
      *--------------------------------*                                        
                                                                                
           DISPLAY 'PROGRAM ID   : BP13C389'.                                   
           DISPLAY 'CURRENT DATE : ' WS-DISP-DATE.                              
           DISPLAY SPACES.                                                      
           DISPLAY '*=============== COUNTERS ===============*'.                
           DISPLAY 'BP13F435 RECORDS READ       : ' WS-F435-READ-CTR.           
           DISPLAY 'NO OF RECORDS PRINTED       : ' WS-PRT-CTR.                 
                                                                                
           CLOSE BP13F435                                                       
                 BP13K431                                                       
                 BM06K110                                                       
                 BP13L389.                                                      
                                                                                
            IF WS-K431-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BP13K431-STATUS ' WS-K431-STATUS            
            END-IF.                                                             
                                                                                
            IF WS-K110-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BM06K110-STATUS ' WS-K110-STATUS            
            END-IF.                                                             
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
