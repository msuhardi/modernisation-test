      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C93Q.                                                 
      *AUTHOR.        LIZ PATIAG.                                               
      *DATE-WRITTEN.  21 JUN 2017.                                              
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : TO BACK-UP BP13FW3D BASED ON COMPLETED      *           
      *                 BOOKING TAG FROM BP13K813                   *           
      *   INPUT FILES :                                             *           
      *   1.  BP13FW3D                                              *           
      *   2.  BP13K813                                              *           
      *                                                             *           
      *   I-O FILE    :                                             *           
      *   1.  BP13KW3D                                              *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136856 21/06/2017 FNP1 NEW PROGRAM                        *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FW3D ASSIGN TO BP13FW3D.                                  
                                                                                
           SELECT BP13K813 ASSIGN       TO BP13K813                             
                           ACCESS MODE  IS DYNAMIC                              
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K813-KEY-FLD                         
                           FILE STATUS  IS BP13K813-STATUS.                     
                                                                                
           SELECT BP13KW3D ASSIGN       TO BP13KW3D                             
                           ACCESS MODE  IS RANDOM                               
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS KW3D-KEY-FLD                         
                           FILE STATUS  IS BP13KW3D-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13FW3D                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  300 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13FW3D.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   BP13KW3D                                                            
            RECORD CONTAINS  300 CHARACTERS.                                    
       COPY BP13KW3D.                                                           
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-CURR-DATE           PIC X(08) VALUE ZEROES.                       
       01  WS-FW3D-READ           PIC 9(08) VALUE ZEROES.                       
       01  WS-KW3D-UPDATED        PIC 9(08) VALUE ZEROES.                       
       01  WS-FW3D-EOF            PIC X     VALUE SPACES.                       
       01  BP13K813-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  BP13KW3D-STATUS        PIC 9(02) VALUE ZEROES.                       
       01  WS-NUM-FT              PIC X(02) VALUE SPACES.                       
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
           PERFORM 2000-READ-FW3D    THRU 2000-EXIT.                            
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-FW3D-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13FW3D                                                 
                       BP13K813                                                 
                I-O    BP13KW3D.                                                
                                                                                
           IF BP13K813-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPEN ERROR BP13K813. STATUS : ' BP13K813-STATUS          
              MOVE BP13K813-STATUS       TO   RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
           IF BP13KW3D-STATUS NOT = 00 AND 97                                   
              DISPLAY 'OPEN ERROR BP13KW3D. STATUS : ' BP13KW3D-STATUS          
              MOVE BP13KW3D-STATUS       TO   RETURN-CODE                       
              PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT                     
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-FW3D.                                                          
      *---------------*                                                         
                                                                                
           READ BP13FW3D AT END                                                 
                MOVE HIGH-VALUES TO BP13FW3D-REC                                
                MOVE 'Y' TO WS-FW3D-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-FW3D-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
           IF FW3D-NUM-ALLO-CAT = 'BTO' AND                                     
              FW3D-NUM-NT-ZONE  NOT = SPACES                                    
              PERFORM 4000-START-BP13K813   THRU 4000-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-FW3D          THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       4000-START-BP13K813.                                                     
      *-------------------*                                                     
                                                                                
           MOVE SPACES                        TO  BP13K813-REC.                 
           INITIALIZE                             BP13K813-REC.                 
                                                                                
           MOVE SPACES                        TO  K813-KEY-FLD.                 
           MOVE FW3D-NUM-NT-ZONE              TO  K813-NUM-ZONE.                
                                                                                
           START BP13K813 KEY >= K813-KEY-FLD                                   
                                                                                
           EVALUATE BP13K813-STATUS                                             
           WHEN 00                                                              
           WHEN 02                                                              
                PERFORM 4100-READ-BP13K813   THRU 4100-EXIT                     
           WHEN 23                                                              
                CONTINUE                                                        
           WHEN OTHER                                                           
                DISPLAY 'READ ERROR BP13K813, STATUS  '                         
                        BP13K813-STATUS                                         
                        ',  KEY    ' K813-KEY-FLD                               
                MOVE BP13K813-STATUS         TO RETURN-CODE                     
                PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       4100-READ-BP13K813.                                                      
      *-----------------*                                                       
                                                                                
            READ  BP13K813 NEXT AT END                                          
                  MOVE SPACES               TO K813-KEY-FLD.                    
                                                                                
            EVALUATE BP13K813-STATUS                                            
            WHEN 00                                                             
            WHEN 02                                                             
                IF K813-NUM-BOOKING-COMPLT = 'M' OR 'B'                         
                   PERFORM 5000-READ-BP13KW3D THRU 5000-EXIT                    
                END-IF                                                          
            WHEN 23                                                             
                CONTINUE                                                        
            WHEN OTHER                                                          
                DISPLAY 'ERROR READING BP13K813 : ' BP13K813-STATUS             
                             ' KEY = ' K813-KEY-FLD                             
                PERFORM  9000-CLOSE-FILES    THRU 9000-EXIT                     
            END-EVALUATE.                                                       
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       5000-READ-BP13KW3D.                                                      
      *-----------------*                                                       
           MOVE SPACES                 TO KW3D-KEY-FLD.                         
           MOVE FW3D-KEY-FLD           TO KW3D-KEY-FLD.                         
                                                                                
           READ BP13KW3D.                                                       
                                                                                
           EVALUATE BP13KW3D-STATUS                                             
           WHEN 00                                                              
           WHEN 02                                                              
              PERFORM 5500-UPDATE-BP13KW3D  THRU 5500-EXIT                      
           WHEN 23                                                              
              DISPLAY 'RECORD NOT FOUND IN BP13KW3D, KEY IS'                    
                      KW3D-KEY-FLD                                              
           WHEN OTHER                                                           
                DISPLAY 'READ ERROR BP13KW3D, STATUS  '                         
                        BP13KW3D-STATUS                                         
                        ',  KEY    ' KW3D-KEY-FLD                               
                MOVE BP13KW3D-STATUS         TO RETURN-CODE                     
                PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       5500-UPDATE-BP13KW3D.                                                    
      *-----------------*                                                       
           IF KW3D-CDE-COMPLETE-TAG = SPACES                                    
              MOVE K813-NUM-BOOKING-COMPLT TO KW3D-CDE-COMPLETE-TAG             
           ELSE                                                                 
              IF KW3D-CDE-COMPLETE-TAG = 'M'                                    
                 IF K813-NUM-BOOKING-COMPLT = 'B'                               
                    MOVE K813-NUM-BOOKING-COMPLT TO                             
                                              KW3D-CDE-COMPLETE-TAG             
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           REWRITE BP13KW3D-REC.                                                
                                                                                
           EVALUATE BP13KW3D-STATUS                                             
           WHEN 00                                                              
                ADD 1 TO WS-KW3D-UPDATED                                        
           WHEN OTHER                                                           
                DISPLAY 'ERROR REWRITING BP13KW3D, STATUS  '                    
                        BP13KW3D-STATUS                                         
                        ',  KEY    ' KW3D-KEY-FLD                               
                MOVE BP13KW3D-STATUS         TO RETURN-CODE                     
                PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13C93Q  *************'.                           
           DISPLAY 'RECS READ FROM  BP13FW3D    : ' WS-FW3D-READ.               
           DISPLAY '                              '.                            
           DISPLAY 'RECS UPDATED IN BP13KW3D    : ' WS-KW3D-UPDATED.            
                                                                                
           CLOSE BP13FW3D                                                       
                 BP13K813                                                       
                 BP13KW3D.                                                      
                                                                                
           IF BP13K813-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR BP13K813. STATUS : ' BP13K813-STATUS         
              MOVE BP13K813-STATUS       TO   RETURN-CODE                       
           END-IF.                                                              
                                                                                
           IF BP13KW3D-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR BP13KW3D. STATUS : ' BP13KW3D-STATUS         
              MOVE BP13KW3D-STATUS       TO   RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
