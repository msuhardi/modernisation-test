      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
       PROGRAM-ID.   BP13C291.                                                  
       AUTHOR.       MJ16.                                                      
      *DATE-WRITTEN. 09/01/2004.                                                
      *REMARKS.                                                                 
      ******************************************************************        
      *                                                                *        
      *   SYSTEM NAME  :   SYSTEM OF COMMITMENT   (SOC)                *        
      *                                                                *        
      *   SYSTEM ID    :   BP13                                        *        
      *                                                                *        
      *   OBJECTIVE    :   1. TO WRITE BP13F200 FROM BP13K200 AFTER    *        
      *                       COMPARING WITH BP13F205                  *        
      *                                                                *        
      *   INPUT FILES  :   1. BP13F205                                 *        
      *                    2. BP13K200                                 *        
      *                                                                *        
      *   OUTPUT FILE  :   1. BP13F200                                 *        
      *                                                                *        
      *   CHGE-NO  BY    DATE   DESCRIPTION                            *        
      *   -------  ---  ------  -------------------------------------  *        
      *   BP13XXX  MJ16 090104  NEW PROGRAM                            *        
      *   BP133056 LMS1 200707  TO CATER FOR RESERVED LIST.            *        
      ******************************************************************        
                                                                                
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205  ASSIGN       TO BP13F205.                           
                                                                                
           SELECT BP13K200  ASSIGN        TO BP13K200                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K200-KEY-FLD                       
                            FILE STATUS   IS BP13K200-STATUS.                   
                                                                                
           SELECT BP13F200  ASSIGN        TO BP13F200.                          
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
      *============*                                                            
       FILE SECTION.                                                            
      *============*                                                            
                                                                                
       FD   BP13F205                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 80 CHARACTERS.                                      
       COPY BP13F205.                                                           
                                                                                
       FD   BP13K200                                                            
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13K200.                                                           
                                                                                
       FD   BP13F200                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 300 CHARACTERS.                                     
       COPY BP13F200.                                                           
                                                                                
      *=======================*                                                 
       WORKING-STORAGE SECTION.                                                 
      *=======================*                                                 
                                                                                
       01  WS-INDICATORS.                                                       
           05  BP13K200-STATUS            PIC 99     VALUE 00.                  
           05  WS-EOF-F205                PIC X      VALUE 'N'.                 
           05  WS-EOF-K200                PIC X      VALUE 'N'.                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F205-CNT                PIC 9(6)   VALUE ZEROS.               
           05  WS-K200-CNT                PIC 9(6)   VALUE ZEROS.               
           05  WS-WRT-K200-CNT            PIC 9(6)   VALUE ZEROS.               
                                                                                
       01  WS-F205-KEY.                                                         
           05  WS-DTE-ALLOCN              PIC X(8)   VALUE SPACES.              
           05  WS-NUM-NT-ZONE             PIC X(3)   VALUE SPACES.              
           05  WS-NUM-FLAT-TYPE           PIC X(2)   VALUE SPACES.              
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
                                                                                
      ******************************************************************        
       000-MAIN-ROUTINE.                                                        
      ******************************************************************        
                                                                                
            PERFORM  100-OPEN-ROUTINE      THRU     100-EXIT.                   
            PERFORM  200-READ-BP13F205     THRU     200-EXIT.                   
            PERFORM  400-MAIN-ROUTINE      THRU     400-EXIT                    
                         UNTIL WS-EOF-F205 = 'Y'.                               
            PERFORM  900-CLOSE-ROUTINE     THRU     900-EXIT.                   
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       100-OPEN-ROUTINE.                                                        
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13F205                                                 
                       BP13K200                                                 
                OUTPUT BP13F200.                                                
                                                                                
           IF BP13K200-STATUS NOT = 00 AND 97                                   
              DISPLAY 'ERROR OPENING BP13K200, STATUS ' BP13K200-STATUS         
              MOVE     BP13K200-STATUS   TO   RETURN-CODE                       
              PERFORM  900-CLOSE-ROUTINE THRU 900-EXIT                          
           END-IF.                                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       200-READ-BP13F205.                                                       
      ******************************************************************        
           READ BP13F205                                                        
                AT END MOVE  'Y' TO  WS-EOF-F205                                
                       GO TO 200-EXIT.                                          
                                                                                
           ADD  1  TO  WS-F205-CNT.                                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       400-MAIN-ROUTINE.                                                        
      ******************************************************************        
           MOVE F205-DTE-ALLOCN    TO WS-DTE-ALLOCN.                            
           MOVE F205-NUM-NT-ZONE   TO WS-NUM-NT-ZONE.                           
           MOVE F205-NUM-FLAT-TYPE TO WS-NUM-FLAT-TYPE.                         
                                                                                
           IF WS-F205-KEY NOT = SPACES AND LOW-VALUES                           
              PERFORM 500-STARTBR-K200  THRU 500-EXIT                           
           ELSE                                                                 
              CONTINUE                                                          
           END-IF.                                                              
                                                                                
           PERFORM    200-READ-BP13F205 THRU  200-EXIT.                         
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       500-STARTBR-K200.                                                        
      ******************************************************************        
           MOVE SPACES      TO K200-KEY-FLD                                     
           MOVE WS-F205-KEY TO K200-KEY-FLD.                                    
                                                                                
           START BP13K200 KEY >= K200-KEY-FLD.                                  
                                                                                
           EVALUATE BP13K200-STATUS                                             
             WHEN 00                                                            
               PERFORM 510-READNXT-K200 THRU 510-EXIT UNTIL                     
                 WS-EOF-K200 = 'Y' OR                                           
                 WS-F205-KEY NOT = K200-KEY-FLD(1:13)                           
             WHEN 23                                                            
             WHEN 10                                                            
               DISPLAY 'RECORD NOT FND IN BP13K200:' WS-F205-KEY                
             WHEN OTHER                                                         
               DISPLAY 'ERROR IN STARTBR OF BP13K200:' BP13K200-STATUS          
               PERFORM  900-CLOSE-ROUTINE THRU 900-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       510-READNXT-K200.                                                        
      ******************************************************************        
           MOVE 'N' TO WS-EOF-K200.                                             
           READ BP13K200 NEXT AT END MOVE 'Y' TO WS-EOF-K200                    
                GO  TO 510-EXIT.                                                
                                                                                
           EVALUATE BP13K200-STATUS                                             
             WHEN 00                                                            
               IF WS-F205-KEY = K200-KEY-FLD(1:13)                              
                  IF K200-NUM-RESERVE = F205-NUM-RESERVE                        
                     MOVE SPACES       TO BP13F200-REC                          
                     INITIALIZE           BP13F200-REC                          
                     MOVE BP13K200-REC TO BP13F200-REC                          
                     WRITE BP13F200-REC                                         
                     ADD   1           TO WS-K200-CNT                           
                     ADD   1           TO WS-WRT-K200-CNT                       
                  END-IF                                                        
               END-IF                                                           
             WHEN 10                                                            
               MOVE 'Y' TO WS-EOF-K200                                          
             WHEN OTHER                                                         
               DISPLAY 'ERROR IN READNXT OF BP13K200:' BP13K200-STATUS          
               PERFORM  900-CLOSE-ROUTINE THRU 900-EXIT                         
           END-EVALUATE.                                                        
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       900-CLOSE-ROUTINE.                                                       
      ******************************************************************        
                                                                                
           DISPLAY '  '.                                                        
           DISPLAY '*----------- BP13C291 SUMMARY --------------*'.             
           DISPLAY '  '.                                                        
           DISPLAY 'NO OF BP13F205 REC READ    = ' WS-F205-CNT.                 
           DISPLAY 'NO OF BP13K200 REC READ    = ' WS-K200-CNT.                 
           DISPLAY 'NO OF BP13F200 REC WRITTEN = ' WS-WRT-K200-CNT.             
                                                                                
           CLOSE BP13F205                                                       
                 BP13K200                                                       
                 BP13F200.                                                      
                                                                                
           IF BP13K200-STATUS NOT = 00                                          
              DISPLAY 'ERROR CLOSING BP13K200, STATUS ' BP13K200-STATUS         
              MOVE     BP13K200-STATUS   TO    RETURN-CODE                      
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
