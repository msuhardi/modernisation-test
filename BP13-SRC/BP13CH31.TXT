       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CH31.                                                 
      *AUTHOR.        ZARITA DANE SARMIENTO.                                    
      *DATE-WRITTEN.  21JUL2014.                                                
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES : RE-ASSIGN OIC FOR DBSS/EC BASED ON     *                
      *                 EC/DBSS OIC-LIST BP13K025.             *                
      *                                                        *                
      *    1. UPDATE OF OIC FILE.                              *                
      *                                                        *                
      *    INPUT FILES  : BP13F026 - TRANSACTION FILE          *                
      *    I-O            BP13K022 - OIC FILE                  *                
      *                   BP13K025 - CONTROL FILE              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY  DATE      DETAILS                          *                
      *=======  === ========= =================================*                
      *BP135406 ZDD 20140724  NEW PGM                          *                
      *BP136185 SMR 20170109  CATER FOR NUM-PRJCT(DBS/EC/NPL)  *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F026 ASSIGN TO BP13F026                                   
                  FILE STATUS     IS WS-F026-STATUS.                            
                                                                                
           SELECT BP13F022 ASSIGN TO BP13F022                                   
                  FILE STATUS     IS WS-F022-STATUS.                            
                                                                                
           SELECT BP13FB25 ASSIGN TO BP13FB25                                   
                  FILE STATUS     IS WS-FB25-STATUS.                            
                                                                                
           SELECT BP13K022 ASSIGN TO BP13K022                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K022-KEY-FLD                               
                  ALTERNATE KEY   IS K022-NUM-REGN                              
                  FILE STATUS     IS WS-K022-STATUS.                            
                                                                                
           SELECT BP13K025 ASSIGN TO BP13K025                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K025-KEY-FLD                               
                  FILE STATUS     IS WS-K025-STATUS.                            
                                                                                
           SELECT BP13K028 ASSIGN TO BP13K028                                   
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K028-KEY-FLD                               
                  FILE STATUS     IS WS-K028-STATUS                             
                  ACCESS MODE     IS DYNAMIC.                                   
                                                                                
           SELECT BP13KB25 ASSIGN TO BP13KB25                                   
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KB25-KEY-FLD                               
                  ALTERNATE KEY   IS KB25-ALT-KEY                               
                  FILE STATUS     IS WS-KB25-STATUS                             
                  ACCESS MODE     IS DYNAMIC.                                   
                                                                                
           SELECT BP13RPT1 ASSIGN TO BP13RPT1                                   
                  FILE STATUS     IS WS-RPT1-STATUS.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F026                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13F026.                                                           
                                                                                
       FD   BP13F022                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13F022.                                                           
                                                                                
       FD   BP13FB25                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 3400 CHARACTERS.                                    
       COPY BP13FB25.                                                           
                                                                                
       FD   BP13K022                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K022.                                                           
                                                                                
       FD   BP13K025                                                            
            RECORD CONTAINS 100 CHARACTERS.                                     
       COPY BP13K025.                                                           
                                                                                
       FD   BP13K028 RECORD CONTAINS  100 CHARACTERS.                           
       COPY BP13K028.                                                           
                                                                                
       FD   BP13KB25 RECORD CONTAINS 3400 CHARACTERS.                           
       COPY BP13KB25.                                                           
                                                                                
       FD   BP13RPT1                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 100 CHARACTERS.                                     
       01 BP13RPT1-REC.                                                         
          05  WS-K022-OLD     PIC X(05).                                        
          05  FILLER1         PIC X(01).                                        
          05  WS-K022-REGN    PIC X(08).                                        
          05  FILLER2         PIC X(01).                                        
          05  WS-K022-OIC     PIC X(05).                                        
          05  FILLER3         PIC X(01).                                        
          05  WS-K025-PROJ    PIC X(03).                                        
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F026-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-K022-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-K025-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-KB25-EOF                    PIC X(1)  VALUE 'N'.              
           05  WS-EOF-K022                    PIC X(1)  VALUE 'N'.              
           05  WS-KB25-DELETED                PIC X(1)  VALUE 'N'.              
           05  WS-K022-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-F026-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-F022-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-RPT1-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-K025-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-K028-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-KB25-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-FB25-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-KB78-STATUS                 PIC 9(2)  VALUE 0.                
           05  WS-CNT-READ-F026               PIC 9(6)  VALUE ZEROS.            
           05  WS-SKIPPED                     PIC 9(6)  VALUE ZEROS.            
           05  WS-CANCELLED                   PIC 9(6)  VALUE ZEROS.            
           05  WS-NOT-ECDBS                   PIC 9(6)  VALUE ZEROS.            
           05  WS-REC-DELETED                 PIC 9(6)  VALUE ZEROS.            
           05  WS-DEL-KB25                    PIC 9(6)  VALUE ZEROS.            
           05  WS-K028-UPDATED                PIC 9(6)  VALUE ZEROS.            
           05  WS-KB25-WRITTEN                PIC 9(6)  VALUE ZEROS.            
           05  WS-RPT1-WRT                    PIC 9(6)  VALUE ZEROS.            
           05  WS-BACKUP-K022                 PIC 9(6)  VALUE ZEROS.            
           05  WS-BACKUP-KB25                 PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-READ-K022               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-READ-K025               PIC 9(6)  VALUE ZEROS.            
           05  WS-CNT-READ-KB25               PIC 9(6)  VALUE ZEROS.            
           05  WS-DEL-KB25-NFND               PIC 9(6)  VALUE ZEROS.            
                                                                                
                                                                                
       01  WS-MISCELLANEOUS-VARS.                                               
           05  WS-REGN-ALLOC-CTR           PIC 9(6)  VALUE ZEROS.               
           05  WS-PREV-OIC                 PIC X(5).                            
           05  WS-ALLO-CAT-PREV            PIC X(1).                            
           05  WS-CNT                      PIC 9(3)  VALUE 0.                   
           05  WS-SKIP                     PIC 9(1)  VALUE 0.                   
           05  WS-FLAG                     PIC X(1)  VALUE 'N'.                 
           05  WS-DONE                     PIC X(1)  VALUE 'N'.                 
           05  WS-READ-FLAG-BL             PIC X(1)  VALUE 'N'.                 
           05  WS-FOUND                    PIC X(1)  VALUE 'N'.                 
           05  WS-FOUND-KB25               PIC X(1)  VALUE 'N'.                 
           05  WS-PROJ-CODE                PIC X(3)  VALUE 'N'.                 
           05  WS-LAST-REC                 PIC X(1)  VALUE 'N'.                 
           05  WS-SYSTEM-DATE              PIC X(08) VALUE SPACES.              
           05  WS-KB25-REC                 PIC X(3400) VALUE SPACES.            
           05  WS-K022-REC                 PIC X(100) VALUE SPACES.             
           05  WS-KB25-KEY-FLD             PIC X(24) VALUE SPACES.              
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
                                                                                
           PERFORM 1100-READ-F026    THRU 1100-EXIT.                            
                                                                                
           PERFORM 1500-PROCESS-F026      THRU 1500-EXIT                        
                  UNTIL WS-F026-EOF-SW = 'Y'.                                   
                                                                                
           PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F026                                                 
                I-O    BP13K022                                                 
                       BP13K025                                                 
                       BP13K028                                                 
                       BP13KB25                                                 
                OUTPUT BP13RPT1                                                 
                       BP13F022                                                 
                       BP13FB25.                                                
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K022 OPEN ERROR, STATUS IS ' WS-K022-STATUS          
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K025-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K025 OPEN ERROR, STATUS IS ' WS-K025-STATUS          
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-K028-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K028 OPEN ERROR, STATUS IS ' WS-K028-STATUS          
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           IF WS-KB25-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13KB25 OPEN ERROR, STATUS IS ' WS-KB25-STATUS          
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYSTEM-DATE.                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1100-READ-F026.                                                          
      ****************************************************************          
                                                                                
           READ BP13F026                                                        
              AT END MOVE 'Y' TO WS-F026-EOF-SW                                 
              GO TO 1100-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD  +1                TO WS-CNT-READ-F026.                          
                                                                                
           MOVE 'N'               TO WS-FOUND.                                  
                                                                                
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1500-PROCESS-F026.                                                       
      ****************************************************************          
                                                                                
           MOVE SPACES               TO   WS-PROJ-CODE.                         
           MOVE SPACES               TO   BP13K022-REC.                         
           MOVE 'N'                  TO   WS-EOF-K022.                          
           MOVE 'N'                  TO   WS-DONE.                              
                                                                                
           MOVE F026-NUM-FROM-OIC    TO K022-NUM-OIC.                           
                                                                                
           PERFORM 1600-START-K022    THRU 1600-EXIT                            
                                                                                
           PERFORM 1100-READ-F026    THRU 1100-EXIT.                            
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1600-START-K022.                                                         
      ****************************************************************          
                                                                                
           START BP13K022 KEY >= K022-KEY-FLD.                                  
                                                                                
           EVALUATE WS-K022-STATUS                                              
            WHEN 00                                                             
            WHEN 02                                                             
               PERFORM 1700-NEXT-K022 THRU 1700-EXIT                            
               PERFORM 2000-PROCESS-ASSIGN THRU 2000-EXIT                       
                      UNTIL WS-EOF-K022 = 'Y'                                   
                      OR K022-NUM-OIC NOT = F026-NUM-FROM-OIC                   
            WHEN 23                                                             
              MOVE   'Y'    TO WS-EOF-K022                                      
              DISPLAY 'NO RECORD FOUND  BP13K022START  ' K022-KEY-FLD           
            WHEN OTHER                                                          
              DISPLAY 'ERROR READING OF BP13K022. STATUS IS '                   
                       WS-K022-STATUS                                           
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       1600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1700-NEXT-K022.                                                          
      ****************************************************************          
                                                                                
           READ BP13K022 NEXT RECORD                                            
              AT END MOVE 'Y' TO WS-K022-EOF-SW                                 
              GO TO 1700-EXIT                                                   
           END-READ.                                                            
                                                                                
           ADD  +1                TO WS-CNT-READ-K022.                          
                                                                                
                                                                                
       1700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2000-PROCESS-ASSIGN.                                                     
      ****************************************************************          
                                                                                
           IF K022-NUM-OIC = F026-NUM-FROM-OIC                                  
                                                                                
             IF  K022-NUM-REGN(1:1) = '5' OR '6'                                
                 PERFORM 2200-READ-KB25    THRU 2200-EXIT                       
                 IF WS-FOUND-KB25 = 'Y'                                         
                    PERFORM 3100-PROCESS-CASE      THRU 3100-EXIT               
                 ELSE                                                           
                    DISPLAY 'KB25 NOT FND/K022 DELETED -' K022-NUM-REGN         
                    PERFORM 6000-DELETE-BP13K022 THRU 6000-EXIT                 
                    ADD 1 TO WS-SKIPPED                                         
                    ADD 1 TO WS-DEL-KB25-NFND                                   
                 END-IF                                                         
             ELSE                                                               
                 ADD 1 TO WS-NOT-ECDBS                                          
             END-IF                                                             
           END-IF.                                                              
                                                                                
           PERFORM 1700-NEXT-K022 THRU 1700-EXIT.                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       2200-READ-KB25.                                                          
      ****************************************************************          
                                                                                
           MOVE LOW-VALUES           TO BP13KB25-REC.                           
           MOVE K022-NUM-OIC         TO KB25-NUM-OIC.                           
           MOVE K022-NUM-REGN        TO KB25-NUM-REGN.                          
                                                                                
           READ BP13KB25 KEY IS KB25-ALT-KEY.                                   
                                                                                
           IF WS-KB25-STATUS = 00                                               
              MOVE 'Y' TO WS-FOUND-KB25                                         
              EVALUATE KB25-NUM-PROJECT(1:1)                                    
              WHEN 'D'                                                          
                   MOVE 'DBS'         TO WS-PROJ-CODE                           
              WHEN 'E'                                                          
                   MOVE 'EC '         TO WS-PROJ-CODE                           
              END-EVALUATE                                                      
              MOVE KB25-KEY-FLD       TO WS-KB25-KEY-FLD                        
           ELSE                                                                 
           IF WS-KB25-STATUS = 23                                               
              MOVE 'N' TO WS-FOUND-KB25                                         
              DISPLAY 'REG NUM NOT FOUND ON KB25. STAT IS '                     
                       WS-KB25-STATUS '/' KB25-NUM-REGN                         
           ELSE                                                                 
              DISPLAY 'ERROR WITH READING  OF BP13KB25. STATUS IS '             
                       WS-KB25-STATUS                                           
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2300-READ-BP13KB25.                                                      
      ****************************************************************          
                                                                                
           MOVE SPACES               TO BP13KB25-REC.                           
           MOVE WS-KB25-KEY-FLD      TO KB25-KEY-FLD.                           
                                                                                
           READ BP13KB25.                                                       
                                                                                
           IF WS-KB25-STATUS = 00                                               
              MOVE 'Y' TO WS-FOUND-KB25                                         
              ADD 1    TO WS-CNT-READ-KB25                                      
           ELSE                                                                 
           IF WS-KB25-STATUS = 23                                               
              MOVE 'N' TO WS-FOUND-KB25                                         
              DISPLAY 'REG NUM NOT FOUND ON  BP13KB25. STATUS IS '              
                       WS-KB25-STATUS ' ' KB25-KEY-FLD                          
           ELSE                                                                 
              DISPLAY 'ERROR WITH READING  OF BP13KB25. STATUS IS '             
                       WS-KB25-STATUS                                           
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       2500-READ-K025.                                                          
      ****************************************************************          
                                                                                
           MOVE  LOW-VALUES                    TO K025-KEY-FLD.                 
           MOVE  WS-PROJ-CODE                  TO K025-NUM-PRJCT.               
           MOVE  'N'                           TO WS-FLAG.                      
           START BP13K025 KEY >  K025-KEY-FLD.                                  
                                                                                
           IF WS-K025-STATUS = 00                                               
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'ERROR WITH START READ OF BP13K025. STATUS IS '           
                       WS-K025-STATUS                                           
              PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                     
           END-IF.                                                              
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       2600-CHECK-LAST-REC-BL.                                                  
      ****************************************************************          
                                                                                
              PERFORM UNTIL WS-FLAG = 'Y'                                       
                 PERFORM 4500-READNEXT-K025  THRU 4500-EXIT                     
                 IF WS-K025-STATUS = 00 OR 02  AND                              
                    K025-NUM-PRJCT = WS-PROJ-CODE                               
                    IF K025-CDE-LAST-REC-BL = 'Y'                               
                       MOVE  'Y'            TO WS-LAST-REC                      
                                               WS-FLAG                          
                       PERFORM 4650-REWRITE-K025 THRU 4650-EXIT                 
                    END-IF                                                      
                 ELSE                                                           
                   IF WS-K025-STATUS = 10 OR 46                                 
                      MOVE 'Y'    TO WS-K025-EOF-SW                             
                   ELSE                                                         
                      DISPLAY 'ERROR READING CURRENT OIC IN BP13K025'           
                      MOVE WS-K025-STATUS          TO RETURN-CODE               
                      PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                 
                   END-IF                                                       
                 END-IF                                                         
              END-PERFORM.                                                      
                                                                                
       2600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       3100-PROCESS-CASE.                                                       
      ****************************************************************          
                                                                                
           MOVE 'N'                          TO   WS-LAST-REC                   
                                                  WS-K025-EOF-SW                
                                                  WS-DONE.                      
                                                                                
           DISPLAY ' PROJ -' WS-PROJ-CODE                                       
                   ' REGN -' K022-NUM-REGN                                      
                   ' OLD OIC -' K022-NUM-OIC                                    
           PERFORM 2500-READ-K025          THRU 2500-EXIT.                      
           PERFORM 2600-CHECK-LAST-REC-BL  THRU 2600-EXIT.                      
                                                                                
           MOVE 'N'                          TO WS-FLAG.                        
                                                                                
           IF WS-K025-EOF-SW = 'Y'                                              
              MOVE 'N'                  TO WS-K025-EOF-SW                       
              PERFORM 2500-READ-K025  THRU 2500-EXIT                            
           END-IF.                                                              
                                                                                
           PERFORM 4500-READNEXT-K025  THRU 4500-EXIT                           
           PERFORM 4600-PROCESS-K025   THRU 4600-EXIT                           
               UNTIL WS-K025-EOF-SW = 'Y' OR                                    
                     WS-FLAG = 'Y'.                                             
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4500-READNEXT-K025.                                                      
      ****************************************************************          
                                                                                
            READ BP13K025 NEXT RECORD                                           
               AT END MOVE 'Y' TO WS-K025-EOF-SW                                
               GO TO 4500-EXIT                                                  
            END-READ.                                                           
            ADD 1   TO WS-CNT-READ-K025.                                        
                                                                                
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4600-PROCESS-K025.                                                       
      ****************************************************************          
                                                                                
           IF WS-K025-EOF-SW NOT = 'Y'                                          
             IF K025-NUM-PRJCT = WS-PROJ-CODE                                   
                IF K025-CDE-BLOCK NOT = 'Y' AND                                 
                   K025-NUM-OIC NOT = F026-NUM-FROM-OIC                         
                   PERFORM 4650-REWRITE-K025  THRU 4650-EXIT                    
                   IF WS-DONE = 'Y'                                             
                      PERFORM 4700-UPD-KB25-K022 THRU 4700-EXIT                 
                   END-IF                                                       
                END-IF                                                          
             ELSE                                                               
                MOVE 'N'    TO  WS-K025-EOF-SW                                  
                PERFORM 2500-READ-K025  THRU 2500-EXIT                          
             END-IF                                                             
           ELSE                                                                 
              MOVE 'N'    TO WS-K025-EOF-SW                                     
              PERFORM 2500-READ-K025  THRU 2500-EXIT                            
           END-IF.                                                              
                                                                                
           PERFORM 4500-READNEXT-K025  THRU 4500-EXIT.                          
                                                                                
       4600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4650-REWRITE-K025.                                                       
      ****************************************************************          
                                                                                
            IF WS-LAST-REC = 'Y'                                                
               MOVE 'N'             TO K025-CDE-LAST-REC-BL                     
               MOVE 'N'             TO WS-LAST-REC                              
            ELSE                                                                
             IF K025-NUM-RATIO = K025-NUM-CUR-CNT                               
                SUBTRACT K025-NUM-RATIO FROM K025-NUM-MAX-CNT                   
                         GIVING WS-SKIP                                         
                IF WS-SKIP <= K025-NUM-SKIP                                     
                   MOVE ZEROES                TO K025-NUM-SKIP                  
                                                                                
                   MOVE 1                     TO K025-NUM-CUR-CNT               
                   MOVE 'Y'                   TO K025-CDE-LAST-REC-BL           
                   MOVE 'Y'                   TO WS-DONE                        
                ELSE                                                            
                   ADD  1                     TO K025-NUM-SKIP                  
                END-IF                                                          
             ELSE                                                               
                MOVE 1                 TO K025-NUM-CUR-CNT                      
                MOVE 'Y'               TO K025-CDE-LAST-REC-BL                  
                MOVE 'Y'               TO WS-DONE                               
              END-IF                                                            
            END-IF.                                                             
                                                                                
            REWRITE BP13K025-REC.                                               
                                                                                
            EVALUATE WS-K025-STATUS                                             
            WHEN 00                                                             
               CONTINUE                                                         
            WHEN OTHER                                                          
               DISPLAY 'ERROR WRITING BP13K025. STATUS IS '                     
                        WS-K025-STATUS                                          
               PERFORM 9999-CLOSE-ROUTINE     THRU 9999-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       4650-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       4700-UPD-KB25-K022.                                                      
      ****************************************************************          
                                                                                
           PERFORM 2300-READ-BP13KB25   THRU 2300-EXIT.                         
                                                                                
           IF WS-FOUND-KB25 = 'Y'                                               
             PERFORM 5050-PROCESS-KB25    THRU 5050-EXIT                        
           END-IF.                                                              
                                                                                
           PERFORM 5080-WRITE-K028    THRU 5080-EXIT.                           
           PERFORM 5000-UPDATE-K022   THRU 5000-EXIT.                           
           MOVE 'Y'                   TO WS-FLAG.                               
                                                                                
       4700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5000-UPDATE-K022.                                                        
      ****************************************************************          
                                                                                
           MOVE BP13K022-REC            TO   WS-K022-REC                        
           PERFORM 5090-DELETE-K022    THRU 5090-EXIT.                          
                                                                                
           WRITE BP13F022-REC   FROM BP13K022-REC                               
           ADD 1                  TO WS-BACKUP-K022                             
                                                                                
            MOVE SPACES                    TO BP13RPT1-REC.                     
            MOVE ';'                       TO FILLER1                           
                                              FILLER2                           
                                              FILLER3.                          
                                                                                
            MOVE WS-K022-REC               TO BP13K022-REC                      
            MOVE F026-NUM-FROM-OIC         TO WS-K022-OLD.                      
            MOVE K025-NUM-OIC              TO K022-NUM-OIC                      
                                              WS-K022-OIC.                      
            MOVE K022-NUM-REGN             TO WS-K022-REGN.                     
            MOVE K025-NUM-PRJCT            TO WS-K025-PROJ.                     
            DISPLAY ' NEW OIC -' K022-NUM-OIC.                                  
                                                                                
            WRITE BP13K022-REC.                                                 
                                                                                
            EVALUATE WS-K022-STATUS                                             
            WHEN 00                                                             
               ADD 1 TO WS-REGN-ALLOC-CTR                                       
               WRITE BP13RPT1-REC                                               
               ADD 1 TO WS-RPT1-WRT                                             
            WHEN OTHER                                                          
               DISPLAY 'ERROR WRITING BP13K022. STATUS IS '                     
                        WS-K022-STATUS                                          
               PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
            END-EVALUATE.                                                       
                                                                                
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5050-PROCESS-KB25.                                                       
      ****************************************************************          
                                                                                
           MOVE BP13KB25-REC           TO   WS-KB25-REC.                        
                                                                                
           WRITE BP13FB25-REC        FROM   BP13KB25-REC.                       
           ADD 1                       TO   WS-BACKUP-KB25.                     
                                                                                
           PERFORM 5070-DELETE-KB25  THRU   5070-EXIT.                          
                                                                                
           ADD 1                       TO   WS-DEL-KB25.                        
                                                                                
           PERFORM 5060-WRITE-KB25     THRU 5060-EXIT.                          
                                                                                
       5050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5070-DELETE-KB25.                                                        
      ****************************************************************          
                                                                                
           DELETE BP13KB25.                                                     
                                                                                
           IF WS-KB25-STATUS   NOT  = 00                                        
             DISPLAY 'ERROR DELETING THE RECORD..STAT'                          
              WS-KB25-STATUS                                                    
             MOVE WS-KB25-STATUS               TO RETURN-CODE                   
             PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                          
           END-IF.                                                              
                                                                                
       5070-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5060-WRITE-KB25.                                                         
      ****************************************************************          
                                                                                
            MOVE WS-KB25-REC               TO BP13KB25-REC                      
            MOVE K025-NUM-OIC              TO KB25-NUM-OIC.                     
                                                                                
            WRITE BP13KB25-REC.                                                 
                                                                                
            EVALUATE WS-KB25-STATUS                                             
            WHEN 00                                                             
            WHEN 02                                                             
            WHEN 22                                                             
               ADD 1 TO WS-KB25-WRITTEN                                         
            WHEN OTHER                                                          
               DISPLAY 'ERROR WRITING BP13KB25. STATUS IS '                     
                        WS-KB25-STATUS                                          
               MOVE WS-KB25-STATUS           TO RETURN-CODE                     
               PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                       
            END-EVALUATE.                                                       
       5060-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5080-WRITE-K028.                                                         
      ****************************************************************          
                                                                                
           MOVE SPACES               TO   BP13K028-REC.                         
           INITIALIZE                     BP13K028-REC.                         
                                                                                
           MOVE K022-NUM-REGN        TO   K028-NUM-REGN                         
           MOVE K022-NUM-OIC         TO   K028-NUM-OIC                          
           MOVE WS-SYSTEM-DATE       TO   K028-DTE-HISTORY                      
           MOVE K022-CDE-ALLO-CAT    TO   K028-CDE-ALLO-CAT                     
           MOVE K022-CDE-BANKLOAN    TO   K028-CDE-BANKLOAN                     
           MOVE K022-CDE-ALERT       TO   K028-CDE-ALERT                        
           MOVE K022-CDE-ALERT-TYPE  TO   K028-CDE-ALERT-TYPE                   
           MOVE K022-NUM-OIC-APPL    TO   K028-NUM-OIC-APPL                     
           MOVE K022-NUM-PRE-OIC-TAG TO   K028-NUM-PRE-OIC-TAG                  
           MOVE 'BP134757'           TO   K028-NUM-USERID                       
           MOVE K022-DTE-UPDATE      TO   K028-DTE-UPDATE                       
                                                                                
           WRITE BP13K028-REC.                                                  
                                                                                
           EVALUATE WS-K028-STATUS                                              
             WHEN 00                                                            
                 ADD 1 TO WS-K028-UPDATED                                       
             WHEN 02                                                            
             WHEN 22                                                            
                 DISPLAY 'DUP RECORD K028..STAT' WS-K028-STATUS                 
                          '/' K022-NUM-REGN                                     
             WHEN OTHER                                                         
                 DISPLAY 'ERROR WRITING THE RECORD K028..STAT'                  
                 WS-K028-STATUS                                                 
                 DISPLAY 'KEY FIELD K028 ' K028-KEY-FLD                         
                 MOVE WS-K028-STATUS           TO RETURN-CODE                   
                 PERFORM 9999-CLOSE-ROUTINE  THRU 9999-EXIT                     
           END-EVALUATE.                                                        
                                                                                
       5080-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5090-DELETE-K022.                                                        
      ****************************************************************          
                                                                                
           DELETE BP13K022.                                                     
                                                                                
           IF WS-K022-STATUS   NOT  = 00                                        
              DISPLAY 'ERROR DELETING THE RECORD..STAT'                         
              WS-K022-STATUS                                                    
              MOVE WS-K022-STATUS               TO RETURN-CODE                  
              PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                         
           END-IF.                                                              
                                                                                
           ADD 1 TO WS-REC-DELETED.                                             
                                                                                
       5090-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       6000-DELETE-BP13K022.                                                    
      ****************************************************************          
                                                                                
           PERFORM 5080-WRITE-K028    THRU 5080-EXIT                            
                                                                                
           WRITE BP13F022-REC   FROM BP13K022-REC                               
           ADD 1   TO WS-BACKUP-K022                                            
                                                                                
           DELETE BP13K022                                                      
                                                                                
           IF WS-K022-STATUS NOT  = 00                                          
             DISPLAY 'ERROR DELETING THE RECORD..STAT'                          
             WS-K022-STATUS                                                     
             MOVE WS-K022-STATUS             TO RETURN-CODE                     
             PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT                          
           END-IF                                                               
                                                                                
           ADD 1 TO WS-REC-DELETED.                                             
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           CLOSE BP13F026                                                       
                 BP13K025                                                       
                 BP13K022                                                       
                 BP13K028                                                       
                 BP13KB25                                                       
                 BP13RPT1                                                       
                 BP13F022                                                       
                 BP13FB25.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13CH31 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F026 RECORDS READ        = ' WS-CNT-READ-F026.          
           DISPLAY 'NO OF K025 REC READ          = ' WS-CNT-READ-K025.          
           DISPLAY 'NO OF K022 REC READ          = ' WS-CNT-READ-K025.          
           DISPLAY 'TOTAL REGNO DELETED -K022    = ' WS-REC-DELETED.            
           DISPLAY 'NO OF REGNO REASSIGNED -K022 = ' WS-REGN-ALLOC-CTR.         
           DISPLAY 'NO OF RECORD WRITTEN TO K028 = ' WS-K028-UPDATED.           
           DISPLAY 'CASES NOT FND IN        KB25 = ' WS-SKIPPED.                
           DISPLAY 'NO OF REG NTFND KB25 DEL K022= ' WS-DEL-KB25-NFND.          
           DISPLAY 'NO OF RECORD BACKUP FROM K022= ' WS-BACKUP-K022.            
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF KB25 REC READ          = ' WS-CNT-READ-KB25.          
           DISPLAY 'NO OF RECORD DELETED TO KB25 = ' WS-DEL-KB25.               
           DISPLAY 'NO OF REC UPD W/ NEW OIC KB25= ' WS-KB25-WRITTEN.           
           DISPLAY 'NO OF RECORD BACKUP FROM KB25= ' WS-BACKUP-KB25.            
           DISPLAY SPACES.                                                      
           DISPLAY 'NO OF REC WRITTEN ON REPORT  = ' WS-RPT1-WRT.               
           DISPLAY SPACES.                                                      
           DISPLAY 'NOT EC AND DBS CASE          = ' WS-NOT-ECDBS.              
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           STOP RUN.                                                            
       9999-EXIT.                                                               
           EXIT.                                                                
