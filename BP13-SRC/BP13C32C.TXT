      *-----------------------*                                                 
       IDENTIFICATION DIVISION.                                                 
      *-----------------------*                                                 
       PROGRAM-ID.    BP13C32C.                                                 
       AUTHOR.        JGO1.                                                     
      *DATE-WRITTEN.  14/01/14.                                                 
                                                                                
      * ======================================================== *              
      *                SYSTEM OF COMMITMENT  (BP13)              *              
      * ======================================================== *              
      *    OBJECTIVE :                                           *              
      *      1. THIS IS A PROGRAM TO PRINT A LIST OF PAYMENT FOR *              
      *         STAMPING FOR SUA/TOL (DRAFT ONLY)                *              
      *                                                          *              
      *    INPUT FILES :                                         *              
      *      1  BP13F315                                         *              
      *      2. BP13K800                                         *              
      *                                                          *              
      *    OUTPUT FILE :                                         *              
      *      1. BP13L32C                                         *              
      *                                                          *              
      *    LISTING :                                             *              
      *     1. CONTOL LISTING  (BP13L32C)                        *              
      *                                                          *              
      * ======================================================== *              
      * CHG-NO   BY   DATE   DESCRIPTION                         *              
      * -------- ---  ------ -----------                         *              
      * BP135017 JGO1 140114 NEW PROGRAM                         *              
      * BP135602 JGO1 260115 READ K893 IF NOT FOUND IN K800      *              
      * ======================================================== *              
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
                                                                                
           SELECT BP13F315  ASSIGN       TO BP13F315.                           
           SELECT BP13K800  ASSIGN       TO BP13K800                            
                            ACCESS MODE  IS RANDOM                              
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K800-NUM-REGN                       
                            FILE STATUS  IS BP13K800-STATUS.                    
                                                                                
           SELECT BP13K893  ASSIGN       TO BP13K893                            
                            ACCESS MODE  IS DYNAMIC                             
                            ORGANIZATION IS INDEXED                             
                            RECORD KEY   IS K893-KEY-FLD                        
                            FILE STATUS  IS BP13K893-STATUS.                    
                                                                                
           SELECT BP13L32C  ASSIGN       TO BP13L32C.                           
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F315                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       COPY BP13F315.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K893                                                            
            RECORD CONTAINS 2050 CHARACTERS.                                    
       COPY BP13K893.                                                           
                                                                                
       FD   BP13L32C                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            RECORDING MODE  IS  F.                                              
       01   PRINT-REC                   PIC X(132).                             
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *=======================*                                                 
       01  WS1-FILE-ACCESS-STATUS.                                              
           05  BP13K800-STATUS         PIC 9(2)      VALUE ZEROS.               
           05  BP13K893-STATUS         PIC 9(2)      VALUE ZEROS.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-F315-READ        PIC 9(3)      VALUE ZEROS.               
           05  WS-CNT-K800-READ        PIC 9(3)      VALUE ZEROS.               
           05  WS-CNT-K893-READ        PIC 9(3)      VALUE ZEROS.               
           05  WS-BP13K893-FND         PIC X(01)     VALUE SPACES.              
           05  WS-BP13K893-EOF         PIC X(01)     VALUE SPACES.              
           05  WS-CNT-L32C-WRITE       PIC 9(3)      VALUE ZEROS.               
           05  WS-CNT-L32C-DETLS       PIC 9(4)      VALUE ZEROS.               
                                                                                
       01  WS-ACCUM-VARIABLES.                                                  
           05  WS-NO-OF-PAGE           PIC 9(5)      VALUE ZEROS.               
           05  WS-TOTAL-ACTUAL-LEASE   PIC S9(6)V99  VALUE ZEROS.               
           05  WS-DUP-LEASE            PIC S9(3)V99  VALUE ZEROS.               
           05  WS-TOTAL-DUP-LEASE      PIC S9(3)V99  VALUE ZEROS.               
           05  WS-TOTAL-LEASE          PIC S9(7)V99  VALUE ZEROS.               
           05  WS-COMP-TOT-LEASE       PIC S9(7)V99  VALUE ZEROS.               
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-F315-EOF-IND         PIC X(1)      VALUE 'N'.                 
               88 WS-F315-EOF                        VALUE 'Y'.                 
           05  WS-NEW-VOUCHER-NO       PIC X(1)      VALUE 'Y'.                 
                                                                                
       01  WS-DATES-VARIABLES.                                                  
           05  WS-AO-DATE.                                                      
               10  WS-AO-YEAR          PIC 9(4)      VALUE 0.                   
               10  WS-AO-MM            PIC 99        VALUE 0.                   
               10  WS-AO-DD            PIC 99        VALUE 0.                   
           05  WS-DATE.                                                         
               10 WS-DD                PIC 99        VALUE 00.                  
               10 FILLER               PIC X         VALUE '/'.                 
               10 WS-MM                PIC 99        VALUE 00.                  
               10 FILLER               PIC X         VALUE '/'.                 
               10 WS-YEAR              PIC 9(4)      VALUE 00.                  
           05  WS-PR-CUR-DATE.                                                  
               10 WS-PC-DD             PIC 99        VALUE 00.                  
               10 FILLER               PIC X         VALUE '/'.                 
               10 WS-PC-MM             PIC 99        VALUE 00.                  
               10 FILLER               PIC X         VALUE '/'.                 
               10 WS-PC-CCYY           PIC 9999      VALUE ZERO.                
           05  WS-CURRENT-CCYYMMDD.                                             
                   15 WS-CUR-CCYY      PIC 9999      VALUE ZERO.                
                   15 WS-CUR-MM        PIC 99        VALUE 00.                  
                   15 WS-CUR-DD        PIC 99        VALUE 00.                  
                                                                                
       01  WS-DEFAULT-VALUES.                                                   
           05  WS-DEF-DUP-LEASE        PIC 9(2)V99   VALUE  0.                  
           05  WS-MAX-LINES            PIC 9(2)      VALUE  25.                 
                                                                                
       01  WS-SCH-ACC.                                                          
           05  WS-SCH-1-4              PIC X(4)      VALUE SPACES.              
           05  WS-SCH-5-8              PIC X(4)      VALUE SPACES.              
           05  WS-SCH-9-9              PIC X(1)      VALUE SPACE.               
           05  WS-SCH-10-11            PIC X(2)      VALUE SPACES.              
                                                                                
       01  WS-NUM-VOUCHER              PIC X(11)     VALUE SPACES.              
       01  WS-NUM-GL-ACCT              PIC X(13)     VALUE SPACES.              
                                                                                
                                                                                
      *----------------------------------------------------------*              
      *        WS FOR LIST OF PAYMENT  FOR STAMPING              *              
      *----------------------------------------------------------*              
       01  L32C-PR-HDG-01.                                                      
           05  FILLER              PIC X(8)    VALUE 'BP13L32C'.                
           05  FILLER              PIC X(10)   VALUE '      HDB3'.              
           05  FILLER              PIC X(28)   VALUE SPACES.                    
           05  FILLER              PIC X(39)   VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER              PIC X(12)   VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'DATE : '.                 
           05  L32C-CURRENT-DATE   PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(6)    VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'PAGE : '.                 
           05  L32C-PAGE           PIC ZZZZ9.                                   
                                                                                
       01  L32C-PR-HDG-02.                                                      
           05  FILLER              PIC X(30)   VALUE                            
               'HOUSING AND DEVELOPMENT BOARD'.                                 
           05  FILLER              PIC X(21)   VALUE SPACES.                    
           05  FILLER              PIC X(30)   VALUE                            
               'P A Y M E N T    V O U C H E R'.                                
                                                                                
       01  L32C-PR-HDG-03.                                                      
           05  FILLER              PIC X(46)   VALUE SPACES.                    
           05  FILLER              PIC X(85)   VALUE ALL '-'.                   
                                                                                
       01  L32C-PR-HDG-04.                                                      
           05  FILLER              PIC X(46)   VALUE                            
               'SALES UNIT (AGREEMENT TEAM)'.                                   
           05  FILLER              PIC X(1)    VALUE '¦'.                       
           05  FILLER              PIC X(27)   VALUE                            
               ' FOR FINANCE DEPARTMENT USE'.                                   
           05  FILLER              PIC X(21)   VALUE SPACES.                    
           05  FILLER              PIC X(36)   VALUE                            
               '¦  BATCH NO    :                   ¦'.                          
                                                                                
       01  L32C-PR-HDG-05.                                                      
           05  FILLER              PIC X(46)   VALUE SPACES.                    
           05  FILLER              PIC X(1)    VALUE '¦'.                       
           05  FILLER              PIC X(48)   VALUE SPACES.                    
           05  FILLER              PIC X(1)    VALUE '¦'.                       
           05  FILLER              PIC X(34)   VALUE SPACES.                    
           05  FILLER              PIC X(1)    VALUE '¦'.                       
                                                                                
       01  L32C-PR-HDG-06.                                                      
           05  FILLER              PIC X(46)   VALUE 'SYSTEM CODE : 04'.        
           05  FILLER              PIC X(14)   VALUE '¦ CHECKED BY :'.          
           05  FILLER              PIC X(35)   VALUE SPACES.                    
           05  FILLER              PIC X(17)   VALUE '¦               '.        
           05  FILLER              PIC X(11)   VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE SPACES.                    
           05  FILLER              PIC X(1)    VALUE '¦'.                       
                                                                                
       01  L32C-PR-HDG-07.                                                      
           05  FILLER              PIC X(46)   VALUE                            
              'GL CODE     : 0000000104250'.                                    
           05  FILLER              PIC X(85)   VALUE ALL '-'.                   
                                                                                
       01  L32C-PR-HDG-08.                                                      
           05  FILLER              PIC X(46)   VALUE                            
              'DESCRIPTION : STAMP FEES'.                                       
                                                                                
       01  L32C-PR-HDG-09.                                                      
           05  FILLER              PIC X(53)   VALUE                            
              'PAYABLE TO  : COMMISSIONER OF STAMP DUTIES         '.            
           05  FILLER              PIC X(13)   VALUE  SPACES.                   
           05  FILLER              PIC X(20)   VALUE                            
              'BANK   : DBS        '.                                           
           05  FILLER              PIC X(11)   VALUE  SPACES.                   
           05  FILLER              PIC X(18)   VALUE                            
              'BANK CODE   : 7171'.                                             
                                                                                
                                                                                
       01  L32C-PR-HDG-09A.                                                     
           05  FILLER              PIC X(24)   VALUE                            
              'A/C NO      : 0010468600'.                                       
           05  FILLER              PIC X(42)   VALUE  SPACES.                   
           05  FILLER              PIC X(20)   VALUE                            
              'BRANCH : SHENTON WAY'.                                           
           05  FILLER              PIC X(11)   VALUE  SPACES.                   
           05  FILLER              PIC X(17)   VALUE                            
              'BRANCH CODE : 001'.                                              
                                                                                
       01  L32C-PR-HDG-10.                                                      
           05  FILLER              PIC X(11)   VALUE                            
               'REMITTANCE '.                                                   
           05  FILLER              PIC X(55)   VALUE                            
              'ADVICE  DESPATCH TO :  C/O SC(STAMPING) OF SALES UNIT'.          
                                                                                
       01  L32C-PR-HDG-11.                                                      
           05  FILLER              PIC X(3)    VALUE 'NO.'.                     
           05  FILLER              PIC X(4)    VALUE SPACES.                    
           05  FILLER              PIC X(11)   VALUE                            
               'SCH-ACCT-NO'.                                                   
           05  FILLER              PIC X(5)    VALUE SPACES.                    
           05  FILLER              PIC X(3)    VALUE 'BLK'.                     
           05  FILLER              PIC X(3)    VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'REGN NO'.                 
           05  FILLER              PIC X(4)    VALUE SPACES.                    
           05  FILLER              PIC X(24)   VALUE                            
               'FLAT ADDRESS/STREET NAME'.                                      
           05  FILLER              PIC X(3)    VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'FLAT NO'.                 
           05  FILLER              PIC X(5)    VALUE SPACES.                    
           05  FILLER              PIC X(7)    VALUE 'DATE OF'.                 
           05  FILLER              PIC X(8)    VALUE SPACES.                    
           05  FILLER              PIC X(12)   VALUE '   STAMP FEE'.            
           05  FILLER              PIC X(7)    VALUE SPACES.                    
           05  FILLER              PIC X(3)    VALUE 'DOC'.                     
           05  FILLER              PIC X(10)   VALUE SPACES.                    
           05  FILLER              PIC X(10)   VALUE 'TOTAL'.                   
                                                                                
       01  L32C-PR-HDG-12.                                                      
           05  FILLER              PIC X(79)   VALUE SPACES.                    
           05  FILLER              PIC X(9)    VALUE ' SUA/TOL '.               
           05  FILLER              PIC X(8)    VALUE SPACES.                    
           05  FILLER              PIC X(9)    VALUE SPACES.                    
           05  FILLER              PIC X(8)    VALUE SPACES.                    
           05  FILLER              PIC X(5)    VALUE 'TYPE '.                   
                                                                                
       01  L32C-PR-DETAIL.                                                      
           05  L32C-NO             PIC ZZZ9.                                    
           05  FILLER              PIC X(2)    VALUE SPACES.                    
           05  L32C-SCH-ACC.                                                    
               10 L32C-SCH-1-4     PIC X(4)    VALUE SPACES.                    
               10 FILLER           PIC X(1)    VALUE '-'.                       
               10 L32C-SCH-5-8     PIC X(4)    VALUE SPACES.                    
               10 FILLER           PIC X(1)    VALUE '-'.                       
               10 L32C-SCH-9-9     PIC X(1)    VALUE SPACE.                     
               10 FILLER           PIC X(1)    VALUE '-'.                       
               10 L32C-SCH-10-11   PIC X(2)    VALUE SPACES.                    
           05  FILLER              PIC X(2)    VALUE SPACES.                    
           05  L32C-BLK-NO         PIC X(5)    VALUE SPACES.                    
           05  FILLER              PIC X(2)    VALUE SPACES.                    
           05  L32C-REGN-NO        PIC X(8)    VALUE SPACES.                    
           05  FILLER              PIC X(3)    VALUE SPACES.                    
           05  L32C-STREET         PIC X(23)   VALUE SPACES.                    
           05  FILLER              PIC X(4)    VALUE SPACES.                    
           05  L32C-FLAT-NO.                                                    
               10  FILLER          PIC X(1)    VALUE '#'.                       
               10  L32C-LEVEL-NO   PIC X(2)    VALUE SPACES.                    
               10  FILLER          PIC X(1)    VALUE '-'.                       
               10  L32C-UNIT-NO    PIC X(4)    VALUE SPACES.                    
           05  FILLER              PIC X(4)    VALUE SPACES.                    
           05  L32C-PROCESS-DATE   PIC X(12)   VALUE SPACES.                    
           05  FILLER              PIC X(4)    VALUE SPACES.                    
           05  L32C-LEASE          PIC -$$$,$$9.99.                             
           05  FILLER              PIC X(7)    VALUE SPACES.                    
           05  L32C-DOC-TYPE       PIC X(3).                                    
           05  FILLER              PIC X(3)    VALUE SPACES.                    
           05  L32C-DETL-TOTAL     PIC -$$$$,$$9.99.                            
                                                                                
       01  L32C-PR-TOTAL-LINE.                                                  
           05  FILLER              PIC X(39)   VALUE SPACES.                    
           05  FILLER              PIC X(13)   VALUE '(SERIAL NO : '.           
           05  L32C-VR-NO-LOW      PIC X(4)    VALUE '0001'.                    
           05  FILLER              PIC X(4)    VALUE ' TO '.                    
           05  L32C-VR-NO-HIGH     PIC X(4)    VALUE '0010'.                    
           05  FILLER              PIC X(1)    VALUE ')'.                       
           05  FILLER              PIC X(13)   VALUE SPACES.                    
           05  FILLER              PIC X(16)   VALUE 'TOTAL : '.                
           05  L32C-TOTAL-ACTUAL   PIC -$$$$,$$9.99.                            
           05  FILLER              PIC X(11)   VALUE SPACES.                    
           05  L32C-TOTAL-LEASE    PIC -$$,$$$,$$9.99.                          
                                                                                
       01  L32C-PR-DASH-LINE.                                                   
           05  FILLER              PIC X(94)   VALUE SPACES.                    
           05  FILLER              PIC X(38)   VALUE ALL '-'.                   
                                                                                
       01  L32C-PR-FTG-01.                                                      
           05  FILLER              PIC X(15)   VALUE 'NET PAYABLE  : '.         
           05  L32C-AMT-WORDS.                                                  
               10  L32C-AMT-DESC1  PIC X(60)   VALUE SPACES.                    
               10  L32C-AMT-DESC2  PIC X(60)   VALUE SPACES.                    
                                                                                
       01  L32C-PR-FTG-02.                                                      
           05  FILLER              PIC X(39)   VALUE SPACES.                    
           05  FILLER              PIC X(14)   VALUE 'CHECKED BY : '.           
           05  FILLER              PIC X(21)   VALUE ALL '_'.                   
           05  FILLER              PIC X(4)    VALUE SPACES.                    
           05  FILLER              PIC X(16)   VALUE 'CERTIFIED BY : '.         
           05  FILLER              PIC X(21)   VALUE ALL '_'.                   
                                                                                
      *----------------------------------------------------------*              
      *    WS VARIABLES FOR CONVERTING AMOUNT TO WORDS           *              
      *----------------------------------------------------------*              
       01  WS-SUBSCRIPTS.                                                       
           05  WA-SUB              PIC 999   VALUE 0.                           
           05  WF-SUB              PIC 999   VALUE 0.                           
           05  WL-SUB              PIC 999   VALUE 0.                           
           05  WS-SUB              PIC 999   VALUE 0.                           
           05  WS-FIRST-SUB        PIC 999   VALUE 0.                           
                                                                                
       01  WA-NAMES.                                                            
           05  FILLER              PIC X(10) VALUE '3ONE      '.                
           05  FILLER              PIC X(10) VALUE '3TWO      '.                
           05  FILLER              PIC X(10) VALUE '5THREE    '.                
           05  FILLER              PIC X(10) VALUE '4FOUR     '.                
           05  FILLER              PIC X(10) VALUE '4FIVE     '.                
           05  FILLER              PIC X(10) VALUE '3SIX      '.                
           05  FILLER              PIC X(10) VALUE '5SEVEN    '.                
           05  FILLER              PIC X(10) VALUE '5EIGHT    '.                
           05  FILLER              PIC X(10) VALUE '4NINE     '.                
           05  FILLER              PIC X(10) VALUE '3TEN      '.                
           05  FILLER              PIC X(10) VALUE '6ELEVEN   '.                
           05  FILLER              PIC X(10) VALUE '6TWELVE   '.                
           05  FILLER              PIC X(10) VALUE '8THIRTEEN '.                
           05  FILLER              PIC X(10) VALUE '8FOURTEEN '.                
           05  FILLER              PIC X(10) VALUE '7FIFTEEN  '.                
           05  FILLER              PIC X(10) VALUE '7SIXTEEN  '.                
           05  FILLER              PIC X(10) VALUE '9SEVENTEEN'.                
           05  FILLER              PIC X(10) VALUE '8EIGHTEEN '.                
           05  FILLER              PIC X(10) VALUE '8NINETEEN '.                
           05  FILLER              PIC X(10) VALUE '6TWENTY   '.                
           05  FILLER              PIC X(10) VALUE '6THIRTY   '.                
           05  FILLER              PIC X(10) VALUE '5FORTY    '.                
           05  FILLER              PIC X(10) VALUE '5FIFTY    '.                
           05  FILLER              PIC X(10) VALUE '5SIXTY    '.                
           05  FILLER              PIC X(10) VALUE '7SEVENTY  '.                
           05  FILLER              PIC X(10) VALUE '6EIGHTY   '.                
           05  FILLER              PIC X(10) VALUE '6NINETY   '.                
       01  WA-NAMER REDEFINES WA-NAMES.                                         
           05  WA-NAME             PIC X(10) OCCURS 27 TIMES.                   
                                                                                
       01  WS-LITERALS.                                                         
           05  L-CENT              PIC X(5)  VALUE '4CENT'.                     
           05  L-AND               PIC X(4)  VALUE '3AND'.                      
           05  L-X                 PIC X     VALUE '%'.                         
           05  L-MILLION           PIC X(8)  VALUE '7MILLION'.                  
           05  L-THOUSAND          PIC X(9)  VALUE '8THOUSAND'.                 
           05  L-DOLLARS           PIC X(8)  VALUE '7DOLLARS'.                  
           05  L-CENTS             PIC X(6)  VALUE '6CENTS'.                    
           05  L-ONLY              PIC X(6)  VALUE '5ONLY '.                    
           05  L-HUNDRED           PIC X(8)  VALUE '7HUNDRED'.                  
           05  L-DOLLAR            PIC X(7)  VALUE '6DOLLAR'.                   
                                                                                
       01  SWITCHES.                                                            
           05  WS-SWITCH           PIC 9     VALUE 0.                           
           05  WS-THO-SW           PIC 9     VALUE 0.                           
           05  WS-CNT-SW           PIC 9     VALUE 0.                           
           05  WS-LIT-SW           PIC 9     VALUE 0.                           
           05  WS-TEN-SW           PIC 9     VALUE 0.                           
           05  WS-AND-SW           PIC 9     VALUE 0.                           
                                                                                
       01  WS-PLACE-COUNTER        PIC 999   VALUE 0.                           
       01  WL-AREA-SAVE            PIC X(12) VALUE SPACES.                      
       01  WS-RESULT               PIC 999   VALUE 0.                           
       01  WL-AREA.                                                             
           05  WL-ITEM1            PIC 9     VALUE 0.                           
           05  FILLER              PIC X(11) VALUE SPACE.                       
       01  WL-ITM REDEFINES WL-AREA.                                            
           05  WL-ITEM             PIC X     OCCURS 12 TIMES.                   
                                                                                
       01  WS-CHEQUE-DETAILS.                                                   
           05  WS-AMT              PIC 9(7)V99 VALUE 0.                         
           05  WS-DIGIT REDEFINES  WS-AMT OCCURS 9 TIMES PIC 9.                 
           05  WF-DETAILS.                                                      
               10  WF-LINE-1       PIC X(60).                                   
               10  WF-LINE-2       PIC X(60).                                   
           05  WF-ITEM REDEFINES WF-DETAILS OCCURS 120 PIC X.                   
           05  WP-ITEM REDEFINES WF-DETAILS OCCURS 2 PIC X(60).                 
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       000-MAIN-ROUTINE.                                                        
      ******************************************************************        
                                                                                
           PERFORM  100-OPEN-FILES     THRU  100-EXIT.                          
           PERFORM  200-READ-BP13F315  THRU  200-EXIT.                          
           PERFORM  300-MAIN-ROUTINE   THRU  300-EXIT UNTIL WS-F315-EOF.        
           PERFORM  700-WRITE-TRAILER  THRU  700-EXIT.                          
           PERFORM  900-CLOSE-FILES    THRU  900-EXIT.                          
                                                                                
       000-MAIN-EXIT.                                                           
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       100-OPEN-FILES.                                                          
      ******************************************************************        
           OPEN INPUT  BP13K800                                                 
                       BP13K893                                                 
                I-O    BP13F315                                                 
                OUTPUT BP13L32C.                                                
                                                                                
           IF BP13K800-STATUS NOT = 00 AND NOT = 97                             
              DISPLAY 'OPENING ERROR, BP13K800-STATUS ' BP13K800-STATUS         
              MOVE     BP13K800-STATUS    TO   RETURN-CODE                      
              PERFORM  900-CLOSE-FILES    THRU 900-EXIT                         
           END-IF.                                                              
                                                                                
           IF BP13K893-STATUS NOT = 00 AND NOT = 97                             
              DISPLAY 'OPENING ERROR, BP13K893-STATUS ' BP13K893-STATUS         
              MOVE     BP13K893-STATUS    TO   RETURN-CODE                      
              PERFORM  900-CLOSE-FILES    THRU 900-EXIT                         
           END-IF.                                                              
                                                                                
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       200-READ-BP13F315.                                                       
      ******************************************************************        
           READ BP13F315                                                        
                AT END MOVE 'Y' TO WS-F315-EOF-IND                              
                GO TO  200-EXIT.                                                
                                                                                
           ADD 1 TO WS-CNT-F315-READ.                                           
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       300-MAIN-ROUTINE.                                                        
      ******************************************************************        
      *------------------------------------------------------------*            
      *    1. READ FILE BP13F315.                                  *            
      *    2. RETRIEVE FLAT'S DETAIL FROM BP13F315.                *            
      *    3. READ K800 SOC MASTER TO RETRIEVE K800-DTE-RAISE-AO.  *            
      *    4. PRINT LIST OF PAYMENT FOR STAMPING.                  *            
      *------------------------------------------------------------*            
           MOVE    F315-REGN-NO       TO    K800-NUM-REGN.                      
           PERFORM 400-READ-BP13K800  THRU  400-EXIT.                           
                                                                                
           IF F315-CDE-RELEASE-PAYMENT NOT = 'Y'                                
              MOVE    F315-SCH-ACC       TO    WS-SCH-ACC                       
              MOVE    F315-NUM-BLK       TO    L32C-BLK-NO                      
              MOVE    F315-NME-STREET    TO    L32C-STREET                      
              MOVE    F315-NUM-MAIN      TO    L32C-UNIT-NO                     
              MOVE    F315-NUM-LEVEL     TO    L32C-LEVEL-NO                    
              MOVE    F315-REGN-NO       TO    L32C-REGN-NO                     
                                                                                
              MOVE    WS-SCH-1-4         TO    L32C-SCH-1-4                     
              MOVE    WS-SCH-5-8         TO    L32C-SCH-5-8                     
              MOVE    WS-SCH-9-9         TO    L32C-SCH-9-9                     
              MOVE    WS-SCH-10-11       TO    L32C-SCH-10-11                   
              MOVE    F315-CDE-SUBTRANSFER-DOC                                  
                                         TO    L32C-DOC-TYPE                    
                                                                                
              IF WS-NEW-VOUCHER-NO  = 'Y'                                       
                 DISPLAY 'NEW VOUCHER '                                         
                 MOVE  'N' TO WS-NEW-VOUCHER-NO                                 
                 PERFORM  500-WRITE-HEADER     THRU  500-EXIT                   
              END-IF                                                            
                                                                                
              PERFORM  600-WRITE-TRANS-DETAIL  THRU  600-EXIT                   
              MOVE     'U' TO F315-CDE-RELEASE-PAYMENT                          
              REWRITE BP13F315-TRANS-REC                                        
              PERFORM  200-READ-BP13F315       THRU  200-EXIT                   
           ELSE                                                                 
              PERFORM  200-READ-BP13F315       THRU  200-EXIT                   
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       400-READ-BP13K800.                                                       
      ******************************************************************        
      *------------------------------------------------------------*            
      *    READ APPLICATION MASTER TO GET ACTUAL LEASE STAMP FEE   *            
      *------------------------------------------------------------*            
           READ BP13K800.                                                       
                                                                                
           IF BP13K800-STATUS = 00                                              
              ADD  1                         TO  WS-CNT-K800-READ               
              DISPLAY 'CNT-K800 = ' WS-CNT-K800-READ                            
                                                                                
              IF F315-CDE-RELEASE-PAYMENT NOT = 'Y'                             
                                                                                
                 MOVE F315-DTE-PROC          TO   WS-AO-DATE                    
                 MOVE WS-AO-DD               TO   WS-DD                         
                 MOVE WS-AO-MM               TO   WS-MM                         
                 MOVE WS-AO-YEAR             TO   WS-YEAR                       
                 MOVE WS-DATE                TO   L32C-PROCESS-DATE             
                                                                                
                 MOVE F315-AMT-STAMP-DUTIES  TO   L32C-LEASE                    
                                                   WS-COMP-TOT-LEASE            
                 ADD  WS-COMP-TOT-LEASE      TO   WS-TOTAL-ACTUAL-LEASE         
                 ADD  F315-AMT-LEASE-FEE-DUP TO   WS-TOTAL-DUP-LEASE            
                 MOVE 0                      TO   F315-AMT-STAMP-DUTIES         
                 ADD  WS-DUP-LEASE  WS-COMP-TOT-LEASE                           
                                              TO   F315-AMT-STAMP-DUTIES        
                                                   WS-TOTAL-LEASE               
                 MOVE F315-AMT-STAMP-DUTIES  TO   L32C-DETL-TOTAL               
                                                                                
              ELSE                                                              
                 DISPLAY 'CASE ALREADY RELEASED, REGN NO = '                    
                          K800-NUM-REGN                                         
                 GO TO 400-EXIT                                                 
              END-IF                                                            
           ELSE                                                                 
              IF BP13K800-STATUS = 23                                           
                 PERFORM 450-READ-BP13K893 THRU 450-EXIT                        
              ELSE                                                              
                 DISPLAY 'ERROR READING, BP13K800 ' BP13K800-STATUS             
                 MOVE     BP13K800-STATUS    TO     RETURN-CODE                 
                 PERFORM  900-CLOSE-FILES    THRU   900-EXIT.                   
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       450-READ-BP13K893.                                                       
      ******************************************************************        
      *------------------------------------------------------------*            
      *    READ APPLICATION HISTORY                                *            
      *------------------------------------------------------------*            
           MOVE SPACES                 TO BP13K893-MASTER.                      
           INITIALIZE                     BP13K893-MASTER.                      
           MOVE F315-REGN-NO           TO K893-NUM-REGN-HIST.                   
           MOVE 'N'                    TO WS-BP13K893-EOF                       
                                                                                
           START BP13K893 KEY >= K893-KEY-FLD.                                  
                                                                                
           EVALUATE BP13K893-STATUS                                             
           WHEN 00                                                              
             WHEN 00                                                            
                PERFORM 451-NEXT-BP13K893    THRU 451-EXIT                      
                  UNTIL K893-NUM-REGN-HIST NOT = F315-REGN-NO                   
                     OR WS-BP13K893-FND = 'Y'                                   
           WHEN 23                                                              
                 DISPLAY 'K893 RECORD NOT FOUND. REGN NO = '                    
                          F315-REGN-NO                                          
                 MOVE ZEROES    TO  L32C-LEASE                                  
                 MOVE     BP13K893-STATUS    TO     RETURN-CODE                 
                 PERFORM  900-CLOSE-FILES    THRU   900-EXIT                    
           WHEN OTHER                                                           
                 DISPLAY 'ERROR IN STARTBR K893' BP13K893-STATUS                
                 MOVE     BP13K893-STATUS    TO     RETURN-CODE                 
                 PERFORM 900-CLOSE-FILES  THRU 900-EXIT                         
           END-EVALUATE.                                                        
                                                                                
           IF WS-BP13K893-FND = 'Y'                                             
             MOVE BP13K893-MASTER(1:2000)   TO BP13K800-MASTER                  
              ADD  1                         TO  WS-CNT-K893-READ               
              DISPLAY 'CNT-K893 = ' WS-CNT-K893-READ                            
                                                                                
              IF F315-CDE-RELEASE-PAYMENT NOT = 'Y'                             
                                                                                
                 MOVE F315-DTE-PROC          TO   WS-AO-DATE                    
                 MOVE WS-AO-DD               TO   WS-DD                         
                 MOVE WS-AO-MM               TO   WS-MM                         
                 MOVE WS-AO-YEAR             TO   WS-YEAR                       
                 MOVE WS-DATE                TO   L32C-PROCESS-DATE             
                                                                                
                 MOVE F315-AMT-STAMP-DUTIES  TO   L32C-LEASE                    
                                                   WS-COMP-TOT-LEASE            
                 ADD  WS-COMP-TOT-LEASE      TO   WS-TOTAL-ACTUAL-LEASE         
                 ADD  F315-AMT-LEASE-FEE-DUP TO   WS-TOTAL-DUP-LEASE            
                 MOVE 0                      TO   F315-AMT-STAMP-DUTIES         
                 ADD  WS-DUP-LEASE  WS-COMP-TOT-LEASE                           
                                              TO   F315-AMT-STAMP-DUTIES        
                                                   WS-TOTAL-LEASE               
                 MOVE F315-AMT-STAMP-DUTIES  TO   L32C-DETL-TOTAL               
                                                                                
              ELSE                                                              
                 DISPLAY 'CASE ALREADY RELEASED, REGN NO = '                    
                          F315-REGN-NO                                          
                 GO TO 450-EXIT                                                 
              END-IF                                                            
           END-IF.                                                              
                                                                                
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       451-NEXT-BP13K893.                                                       
      ******************************************************************        
                                                                                
           READ BP13K893 NEXT RECORD                                            
             AT END                                                             
                MOVE 'Y'                  TO WS-BP13K893-EOF                    
           END-READ.                                                            
                                                                                
           EVALUATE BP13K893-STATUS                                             
              WHEN 00                                                           
              WHEN 02                                                           
                 IF K893-NUM-REGN-HIST = F315-REGN-NO                           
                    MOVE 'Y'            TO  WS-BP13K893-EOF                     
                    MOVE 'Y'            TO  WS-BP13K893-FND                     
                 END-IF                                                         
              WHEN 10                                                           
              WHEN 23                                                           
                 MOVE ZEROES    TO  L32C-LEASE                                  
                 MOVE     BP13K893-STATUS    TO     RETURN-CODE                 
                 PERFORM  900-CLOSE-FILES    THRU   900-EXIT                    
              WHEN OTHER                                                        
                 DISPLAY 'ERROR IN STARTBR K893' BP13K893-STATUS                
                 MOVE     BP13K893-STATUS    TO     RETURN-CODE                 
                 PERFORM 900-CLOSE-FILES  THRU 900-EXIT                         
           END-EVALUATE.                                                        
                                                                                
                                                                                
       451-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      ******************************************************************        
       500-WRITE-HEADER.                                                        
      ******************************************************************        
           PERFORM  520-PRINT-HEADING   THRU   520-EXIT.                        
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       520-PRINT-HEADING.                                                       
      ******************************************************************        
      *------------------------------------------------------------*            
      *    PRINT HEADING LINE                                      *            
      *------------------------------------------------------------*            
           MOVE   SPACES            TO    PRINT-REC.                            
           ADD    1                 TO    WS-NO-OF-PAGE.                        
           MOVE   WS-NO-OF-PAGE     TO    L32C-PAGE.                            
           MOVE   FUNCTION CURRENT-DATE(1:8)                                    
                                    TO    WS-CURRENT-CCYYMMDD.                  
           MOVE   WS-CUR-DD         TO    WS-PC-DD.                             
           MOVE   WS-CUR-MM         TO    WS-PC-MM.                             
           MOVE   WS-CUR-CCYY       TO    WS-PC-CCYY.                           
           MOVE   WS-PR-CUR-DATE    TO    L32C-CURRENT-DATE.                    
           WRITE  PRINT-REC FROM L32C-PR-HDG-01 AFTER PAGE.                     
           WRITE  PRINT-REC FROM L32C-PR-HDG-02 AFTER 2.                        
           WRITE  PRINT-REC FROM L32C-PR-HDG-03 AFTER 2.                        
           WRITE  PRINT-REC FROM L32C-PR-HDG-04 AFTER 1.                        
           WRITE  PRINT-REC FROM L32C-PR-HDG-05 AFTER 1.                        
           WRITE  PRINT-REC FROM L32C-PR-HDG-05 AFTER 1.                        
           WRITE  PRINT-REC FROM L32C-PR-HDG-06 AFTER 1.                        
           WRITE  PRINT-REC FROM L32C-PR-HDG-07 AFTER 1.                        
           WRITE  PRINT-REC FROM L32C-PR-HDG-08 AFTER 1.                        
           WRITE  PRINT-REC FROM L32C-PR-HDG-09 AFTER 1.                        
           WRITE  PRINT-REC FROM L32C-PR-HDG-09A AFTER 1.                       
           WRITE  PRINT-REC FROM L32C-PR-HDG-10 AFTER 1.                        
           WRITE  PRINT-REC FROM L32C-PR-HDG-11 AFTER 2.                        
           WRITE  PRINT-REC FROM L32C-PR-HDG-12 AFTER 1.                        
           MOVE   SPACES TO PRINT-REC.                                          
           WRITE  PRINT-REC AFTER 1.                                            
                                                                                
       520-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       600-WRITE-TRANS-DETAIL.                                                  
      ******************************************************************        
           ADD     1                  TO    WS-CNT-L32C-DETLS.                  
                                                                                
           PERFORM 620-PRINT-DETAIL   THRU  620-EXIT.                           
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       620-PRINT-DETAIL.                                                        
      ******************************************************************        
      *------------------------------------------------------------*            
      *    PRINT LIST OF PAYMENT FOR STAMPING                      *            
      *------------------------------------------------------------*            
           ADD   1                   TO    WS-CNT-L32C-WRITE.                   
           MOVE  WS-CNT-L32C-DETLS   TO    L32C-NO.                             
           WRITE PRINT-REC           FROM  L32C-PR-DETAIL AFTER 1.              
           MOVE  SPACES              TO    PRINT-REC.                           
                                                                                
           PERFORM     640-CHECK-LINECNT   THRU  640-EXIT.                      
                                                                                
       620-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       640-CHECK-LINECNT.                                                       
      ******************************************************************        
      *------------------------------------------------------------*            
      *    CHECK LINE COUNT TO DETERMINE PAGE THROW                *            
      *------------------------------------------------------------*            
           DISPLAY 'CHECK-LINECNT:'.                                            
           DISPLAY 'WS-CNT-L32C-DETLS: '  WS-CNT-L32C-DETLS.                    
                                                                                
           IF WS-CNT-L32C-DETLS = WS-MAX-LINES OR WS-F315-EOF                   
              PERFORM  700-WRITE-TRAILER  THRU 700-EXIT                         
              MOVE     0   TO WS-CNT-L32C-DETLS                                 
                              WS-TOTAL-ACTUAL-LEASE                             
                              WS-TOTAL-DUP-LEASE                                
                              WS-TOTAL-LEASE                                    
              MOVE    'Y'  TO WS-NEW-VOUCHER-NO                                 
           END-IF.                                                              
                                                                                
       640-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       700-WRITE-TRAILER.                                                       
      ******************************************************************        
           IF WS-CNT-L32C-DETLS = 0                                             
              GO TO 700-EXIT                                                    
           END-IF.                                                              
                                                                                
           PERFORM  720-PRINT-TOTAL-LINE     THRU   720-EXIT.                   
           PERFORM  800-PRINT-FOOTING        THRU   800-EXIT.                   
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       720-PRINT-TOTAL-LINE.                                                    
      ******************************************************************        
      *------------------------------------------------------------*            
      *    PRINT TOTAL LEASE LINE                                  *            
      *------------------------------------------------------------*            
                                                                                
           IF WS-CNT-F315-READ = 0                                              
              GO TO 720-EXIT                                                    
           END-IF.                                                              
                                                                                
           WRITE PRINT-REC             FROM  L32C-PR-DASH-LINE AFTER 3.         
           MOVE  SPACES                TO    PRINT-REC.                         
           MOVE  WS-CNT-L32C-DETLS     TO    L32C-VR-NO-HIGH.                   
           MOVE  WS-TOTAL-ACTUAL-LEASE TO    L32C-TOTAL-ACTUAL.                 
           MOVE  WS-TOTAL-LEASE        TO    L32C-TOTAL-LEASE.                  
           WRITE PRINT-REC             FROM  L32C-PR-TOTAL-LINE AFTER 1.        
           MOVE  SPACES                TO    PRINT-REC.                         
           WRITE PRINT-REC             FROM  L32C-PR-DASH-LINE AFTER 1.         
                                                                                
       720-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       800-PRINT-FOOTING.                                                       
      ******************************************************************        
           MOVE    WS-TOTAL-LEASE       TO    WS-AMT.                           
           PERFORM 9000-CONVT-AMT-WORD  THRU  9000-EXIT.                        
           MOVE    WP-ITEM (1)          TO    L32C-AMT-DESC1.                   
           MOVE    WP-ITEM (2)          TO    L32C-AMT-DESC2.                   
           WRITE   PRINT-REC            FROM  L32C-PR-FTG-01   AFTER 2.         
           WRITE   PRINT-REC            FROM  L32C-PR-FTG-02   AFTER 5.         
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       900-CLOSE-FILES.                                                         
      ******************************************************************        
      *------------------------------------------------------------*            
      *    CLOSE FILES AND END PROCESSING                          *            
      *------------------------------------------------------------*            
           CLOSE BP13F315                                                       
                 BP13K800                                                       
                 BP13K893                                                       
                 BP13L32C.                                                      
                                                                                
           IF BP13K800-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BP13K800-STATUS ' BP13K800-STATUS         
              MOVE     BP13K800-STATUS TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF BP13K893-STATUS NOT = 00                                          
              DISPLAY 'CLOSING ERROR, BP13K893-STATUS ' BP13K893-STATUS         
              MOVE     BP13K893-STATUS TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           DISPLAY '                                   '.                       
           DISPLAY '******   BP13C32C CONTROL TOTALS   ******'.                 
           DISPLAY '                                   '.                       
           DISPLAY 'NO OF RECORDS READ FROM DAILY TRANS (BP13F315)  = '         
                    WS-CNT-F315-READ.                                           
           DISPLAY 'NO OF RECORDS READ FROM RFF MASTER  (BP13K800)  = '         
                    WS-CNT-K800-READ.                                           
           DISPLAY 'NO OF RECORDS READ FROM RFF HISTORY (BP13K893)  = '         
                    WS-CNT-K893-READ.                                           
           DISPLAY 'NO OF PAYMENTS WRITTEN (BP13L32C)               = '         
                    WS-CNT-L32C-WRITE.                                          
                                                                                
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       9000-CONVT-AMT-WORD.                                                     
      ******************************************************************        
      *------------------------------------------------------------*            
      *     OBJECTIVE  :  CONVERSION FROM AMOUNT IN DIGITS         *            
      *                              INTO AMOUNT IN WORDS          *            
      *------------------------------------------------------------*            
            MOVE SPACES TO WF-DETAILS.                                          
            MOVE ZERO   TO WS-SWITCH                                            
                           WS-THO-SW                                            
                           WS-CNT-SW                                            
                           WS-LIT-SW                                            
                           WS-TEN-SW                                            
                           WS-AND-SW.                                           
            MOVE 60     TO WS-PLACE-COUNTER.                                    
            MOVE 1      TO WF-SUB.                                              
                                                                                
            PERFORM B000-ROUTINE THRU B990-EXIT                                 
                         VARYING WS-SUB FROM 1 BY 1 UNTIL WS-SUB = 10.          
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       B000-ROUTINE SECTION.                                                    
      ******************************************************************        
       B000-GO.                                                                 
            IF WS-DIGIT (WS-SUB) = ZERO                                         
                 IF WS-TEN-SW = ZERO                                            
                    GO TO B800-MOVE-TYPE                                        
                 ELSE                                                           
                    GO TO B010-PROCESS.                                         
                                                                                
            IF WS-FIRST-SUB = ZERO                                              
               MOVE WS-SUB TO WS-FIRST-SUB.                                     
                                                                                
            IF WS-SWITCH = ZERO                                                 
               MOVE 1 TO WS-SWITCH.                                             
                                                                                
       B010-PROCESS.                                                            
            IF WS-SUB = 8                                                       
               MOVE 9 TO WS-CNT-SW.                                             
                                                                                
            IF WS-SUB = 9                                                       
               IF WS-CNT-SW = ZERO                                              
                  MOVE WS-DIGIT (WS-SUB) TO WS-CNT-SW.                          
                                                                                
            IF WS-SUB = 2 OR 3 OR 4                                             
               MOVE 1 TO WS-THO-SW.                                             
                                                                                
            MOVE WS-DIGIT (WS-SUB) TO WA-SUB.                                   
            IF WS-SUB = 3 OR 6 OR 8                                             
               IF WS-DIGIT (WS-SUB) = 1                                         
                  MOVE 1 TO WS-TEN-SW                                           
                  GO TO B990-EXIT                                               
               ELSE                                                             
                  ADD 18 TO WA-SUB                                              
            ELSE                                                                
            IF WS-SUB = 1 OR 4 OR 7 OR 9                                        
               IF WS-TEN-SW = 1                                                 
                  MOVE 0 TO WS-TEN-SW                                           
                  ADD 10 TO WA-SUB.                                             
            MOVE WA-NAME (WA-SUB) TO WL-AREA.                                   
            PERFORM R000-LOAD-IN.                                               
                                                                                
       B300-TENS.                                                               
            IF WS-SUB = 3 OR 6 OR 8                                             
               GO TO B990-EXIT.                                                 
                                                                                
       B400-MOVE-HUNDRED.                                                       
            IF WS-SUB = 2 OR 5                                                  
               MOVE L-HUNDRED TO WL-AREA                                        
               MOVE 1 TO WS-LIT-SW                                              
               PERFORM R000-LOAD-IN                                             
               MOVE 1 TO WS-AND-SW                                              
               GO TO B990-EXIT.                                                 
                                                                                
       B800-MOVE-TYPE.                                                          
            IF WS-SWITCH = ZERO                                                 
               GO TO B990-EXIT.                                                 
                                                                                
            IF WS-SUB = 1                                                       
               MOVE  L-MILLION TO WL-AREA                                       
               MOVE  1         TO WS-LIT-SW                                     
               MOVE  1         TO WS-AND-SW                                     
               GO TO B880-LOAD-IN.                                              
                                                                                
            IF WS-SUB = 7                                                       
               MOVE 1 TO WS-LIT-SW WS-AND-SW                                    
               IF WS-FIRST-SUB = 7  AND                                         
                  WS-DIGIT (WS-SUB) = 1                                         
                  MOVE L-DOLLAR TO WL-AREA                                      
                  GO TO B880-LOAD-IN                                            
               ELSE                                                             
                  MOVE L-DOLLARS TO WL-AREA                                     
                  GO TO B880-LOAD-IN.                                           
                                                                                
            IF WS-SUB = 4 AND WS-THO-SW = 1                                     
               MOVE 1 TO WS-AND-SW WS-LIT-SW                                    
               MOVE L-THOUSAND TO WL-AREA                                       
               GO TO B880-LOAD-IN.                                              
                                                                                
            IF WS-SUB = 9                                                       
               MOVE 1 TO WS-LIT-SW                                              
               IF WS-CNT-SW = ZERO                                              
                  MOVE L-ONLY TO WL-AREA                                        
                  GO TO B880-LOAD-IN                                            
               ELSE                                                             
               IF WS-CNT-SW = 1                                                 
                  MOVE L-CENT TO WL-AREA                                        
                  PERFORM R000-LOAD-IN                                          
                  MOVE L-ONLY TO WL-AREA                                        
                  GO TO B880-LOAD-IN                                            
               ELSE                                                             
                  MOVE L-CENTS TO WL-AREA                                       
                  PERFORM R000-LOAD-IN                                          
                  MOVE L-ONLY TO WL-AREA                                        
                  GO TO B880-LOAD-IN.                                           
            GO TO B990-EXIT.                                                    
                                                                                
       B880-LOAD-IN.                                                            
            PERFORM R000-LOAD-IN.                                               
                                                                                
       B990-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       R000-LOAD-IN SECTION.                                                    
      ******************************************************************        
      *------------------------------------------------------------*            
      *     THIS SECTION LOADS THE WORDS INTO THE THREE LINES      *            
      *     AND JUST KEEPS INCREMENTING A SUBSCRIPT.               *            
      *------------------------------------------------------------*            
                                                                                
       R001-GO.                                                                 
            IF WS-LIT-SW = 1                                                    
               MOVE ZERO TO WS-LIT-SW                                           
               GO TO R050-CONT.                                                 
                                                                                
            IF WS-SUB = 2 OR 5                                                  
               GO TO R050-CONT.                                                 
                                                                                
       R025-ADD-TEST.                                                           
            IF WS-AND-SW NOT = 1                                                
               GO TO R050-CONT.                                                 
                                                                                
            MOVE ZERO TO WS-AND-SW.                                             
            MOVE WL-AREA TO WL-AREA-SAVE.                                       
            MOVE L-AND TO WL-AREA.                                              
            PERFORM R200-LOADUP.                                                
            MOVE WL-AREA-SAVE TO WL-AREA.                                       
                                                                                
       R050-CONT.                                                               
            PERFORM R200-LOADUP.                                                
                                                                                
       R099-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       R200-LOADUP SECTION.                                                     
      ******************************************************************        
       R201-GO.                                                                 
            ADD WL-ITEM1 WF-SUB GIVING WS-RESULT.                               
            IF  WS-RESULT > WS-PLACE-COUNTER                                    
                ADD 1 WS-PLACE-COUNTER GIVING WF-SUB                            
                ADD 60 TO WS-PLACE-COUNTER.                                     
                                                                                
            PERFORM R500-LOAD VARYING WL-SUB FROM 2 BY 1 UNTIL                  
                    WL-ITEM (WL-SUB) = L-X.                                     
                                                                                
       R490-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       R500-LOAD SECTION.                                                       
      ******************************************************************        
       R501-GO.                                                                 
                                                                                
            MOVE WL-ITEM (WL-SUB) TO WF-ITEM (WF-SUB).                          
            ADD  1                TO WF-SUB.                                    
            IF WL-ITEM (WL-SUB) = SPACE                                         
               ADD   1     TO     WL-SUB                                        
               MOVE  L-X   TO     WL-ITEM (WL-SUB)                              
               SUBTRACT 1  FROM   WL-SUB.                                       
                                                                                
       R990-EXIT.                                                               
            EXIT.                                                               
