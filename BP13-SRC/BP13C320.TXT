      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C320.                                                 
      *AUTHOR.        KPS.                                                      
      *DATE-WRITTEN.  16/09/93.                                                 
                                                                                
      * ========================================================= *             
      * SYSTEM OF COMMITMENT (BP13)                               *             
      * ========================================================= *             
      *    OBJECTIVE :                                            *             
      *        1. THIS IS THE PROGRAM TO PRINT OUT REPORT FOR     *             
      *           DUPLICATE BP13F820 RECORDS.                     *             
      *                                                           *             
      *    INPUT FILES :                                          *             
      *        1. BP13F820                                        *             
      *                                                           *             
      *    LISTING :                                              *             
      *        1. BP13L320                                        *             
      *                                                           *             
      * CHG NO   BY  DATE      DESCRIPTION                        *             
      * -------- --- --------  -----------                        *             
      * BP130020 SSS 19950930  REDUCE REC-CNT FROM 50 TO 34/PAGE  *             
      * BP130326 SSS 19970625  REMOVE REDUNANT DISPLAY STATEMENT  *             
      * BP130418 CLT 19980825  Y2K COMPLIANCE CHANGES.            *             
      * BP132702 JF4 20050812  INCLUDE OIC IN THE REPORT          *             
      * BP134204 IMC 20120607  ADD NEW DTE-ACCEPTANCE AND STAT FLD*             
      * BP138300 KR13 20200528 ADD PARM FOR RPRT ID-IOM8 MIGRATION*             
      * ========================================================= *             
                                                                                
      *------------------------*                                                
       ENVIRONMENT DIVISION.                                                    
      *------------------------*                                                
       CONFIGURATION SECTION.                                                   
      *------------------------*                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
      *------------------------*                                                
       INPUT-OUTPUT SECTION.                                                    
      *------------------------*                                                
       FILE-CONTROL.                                                            
      *------------------------*                                                
                                                                                
           SELECT BP13F820 ASSIGN TO BP13F820.                                  
                                                                                
           SELECT BP13K022  ASSIGN        TO BP13K022                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K022-KEY-FLD                       
                            ALTERNATE KEY IS K022-NUM-REGN                      
                            FILE STATUS   IS BP13K022-STATUS.                   
                                                                                
           SELECT BP13K800  ASSIGN        TO BP13K800                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS RANDOM                             
                            RECORD KEY    IS K800-NUM-REGN                      
                            FILE STATUS   IS BP13K800-STATUS.                   
                                                                                
           SELECT BP13L320 ASSIGN TO BP13L320.                                  
                                                                                
                                                                                
      *------------------------*                                                
       DATA DIVISION.                                                           
      *------------------------*                                                
       FILE SECTION.                                                            
      *------------------------*                                                
       FD  BP13F820          BLOCK CONTAINS 0 RECORDS                           
                             RECORD CONTAINS 400 CHARACTERS                     
                             LABEL RECORDS ARE STANDARD                         
                             RECORDING MODE IS F.                               
       COPY BP13F820.                                                           
                                                                                
       FD  BP13K022          RECORD CONTAINS  100 CHARACTERS.                   
       COPY BP13K022.                                                           
                                                                                
       FD  BP13K800          RECORD CONTAINS 2000 CHARACTERS.                   
       COPY BP13K800.                                                           
                                                                                
       FD  BP13L320          BLOCK CONTAINS 0 RECORDS                           
                             RECORD CONTAINS 132 CHARACTERS                     
                             LABEL RECORDS ARE OMITTED                          
                             RECORDING MODE IS F.                               
       01  L320-PRTREC       PIC X(132).                                        
                                                                                
                                                                                
      *------------------------*                                                
       WORKING-STORAGE SECTION.                                                 
      *------------------------*                                                
       01  WS-FILE-STATUS.                                                      
           05  F820-STATUS          PIC 99      VALUE ZEROS.                    
           05  BP13K022-STATUS      PIC 99      VALUE ZEROS.                    
           05  BP13K800-STATUS      PIC 99      VALUE ZEROS.                    
                                                                                
       01  WS-DATE.                                                             
           05 WS-DATE-CC            PIC X(2)    VALUE SPACES.                   
           05 WS-DATE-YY            PIC X(2)    VALUE SPACES.                   
           05 WS-DATE-MM            PIC X(2)    VALUE SPACES.                   
           05 WS-DATE-DD            PIC X(2)    VALUE SPACES.                   
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-PAGE              PIC 9(4)    VALUE ZEROS.                    
           05  WS-REC-CNT           PIC 99      VALUE 99.                       
           05  WS-SEQ-CNT           PIC 9(5)    VALUE ZEROS.                    
           05  WS-READ-F820-CNT     PIC 9(5)    VALUE ZEROS.                    
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-EOF-F820          PIC X(1)    VALUE 'N'.                      
                                                                                
       01  WS-CDE-99-CASE-ONLY      PIC X VALUE 'N'.                            
                                                                                
       01  WS-PREV-VALUES.                                                      
           05  WS-PREV-REGN         PIC X(8).                                   
           05  WS-PREV-NRIC         PIC X(9).                                   
           05  WS-PREV-OCCUP        PIC X(66).                                  
           05  WS-PREV-RELATIONSHIP PIC X(2).                                   
                                                                                
       01  L320-HDG1.                                                           
           05  FILLER               PIC X(8)   VALUE 'BP13L320'.                
           05  FILLER               PIC X(10)  VALUE '      HDB3'.              
           05  FILLER               PIC X(28)  VALUE SPACES.                    
           05  FILLER               PIC X(39)  VALUE                            
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER               PIC X(17)  VALUE SPACES.                    
           05  FILLER               PIC X(6)   VALUE 'DATE: '.                  
           05  PRT-DATE             PIC X(10).                                  
           05  FILLER               PIC X(2)   VALUE SPACES.                    
           05  FILLER               PIC X(6)   VALUE 'PAGE: '.                  
           05  PRT-PAGE             PIC ZZZZ9.                                  
                                                                                
       01  L320-HDG2.                                                           
           05  FILLER               PIC X(51)  VALUE SPACES.                    
           05  FILLER               PIC X(51)  VALUE                            
               'LIST OF OCCUPIERS PENDING UPDATE'.                              
           05  FILLER               PIC X(30)  VALUE 'AGREEMENT TEAM'.          
                                                                                
       01  L320-HDG3.                                                           
           05  FILLER               PIC X(51)  VALUE SPACES.                    
           05  FILLER               PIC X(81)  VALUE                            
               '(RELATIONSHIP CODE IS INCORRECT)'.                              
                                                                                
       01  L320-HDG4.                                                           
           05  FILLER               PIC X(2)   VALUE SPACES.                    
           05  FILLER               PIC X(2)   VALUE 'NO'.                      
           05  FILLER               PIC X(4)   VALUE SPACES.                    
           05  FILLER               PIC X(7)   VALUE 'REGN-NO'.                 
           05  FILLER               PIC X(5)   VALUE SPACES.                    
           05  FILLER               PIC X(4)   VALUE 'NRIC'.                    
           05  FILLER               PIC X(8)   VALUE SPACES.                    
           05  FILLER               PIC X(12)  VALUE 'RELATIONSHIP'.            
           05  FILLER               PIC X(4)   VALUE SPACES.                    
           05  FILLER               PIC X(4)   VALUE 'NAME'.                    
           05  FILLER               PIC X(50)  VALUE SPACES.                    
           05  FILLER               PIC X(3)   VALUE 'OIC'.                     
           05  FILLER               PIC X(2)   VALUE SPACES.                    
           05  FILLER               PIC X(4)   VALUE 'STAT'.                    
           05  FILLER               PIC X(2)   VALUE SPACES.                    
           05  FILLER               PIC X(10)  VALUE 'ACCEPTANCE'.              
                                                                                
       01  L320-DTL.                                                            
           05  FILLER               PIC X      VALUE SPACES.                    
           05  PRT-SEQ-CNT          PIC ZZZ9   VALUE ZEROS.                     
           05  FILLER               PIC X(3)   VALUE SPACES.                    
           05  PRT-REGN-NO          PIC X(8).                                   
           05  FILLER               PIC X(4)   VALUE SPACES.                    
           05  PRT-NUM-NRIC         PIC X(9).                                   
           05  FILLER               PIC X(7)   VALUE SPACES.                    
           05  PRT-RELATIONSHIP     PIC X(2).                                   
           05  FILLER               PIC X(10)  VALUE SPACES.                    
           05  PRT-NME-OCCUP        PIC X(50).                                  
           05  FILLER               PIC X(04)  VALUE SPACES.                    
           05  PRT-NUM-OIC          PIC X(05).                                  
           05  FILLER               PIC X(02)  VALUE SPACES.                    
           05  PRT-NUM-STATUS       PIC X(01).                                  
           05  FILLER               PIC X(03)  VALUE SPACES.                    
           05  PRT-DTE-ACCEPTANCE   PIC X(10).                                  
           05  FILLER               PIC X(02)  VALUE SPACES.                    
                                                                                
       LINKAGE SECTION.                                                         
      *==============================================================*          
       01  PARM-DATA.                                                           
           05  PARM-STRING-LENGTH        PIC 9(4) COMP.                         
           05  PARM-STRING               PIC X(2) VALUE SPACES.                 
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION USING PARM-DATA.                                      
      *-------------------------------------------------------------            
       000-MAIN.                                                                
           PERFORM 100-OPEN-ROUTINE   THRU 100-EXIT.                            
           PERFORM 200-READ-BP13F820  THRU 200-EXIT.                            
           PERFORM 300-PROCESS-REPORT THRU 300-EXIT                             
                   UNTIL WS-EOF-F820 = 'Y'.                                     
           PERFORM 999-CLOSE-ROUTINE  THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       100-OPEN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           OPEN  INPUT BP13F820                                                 
                       BP13K022                                                 
                       BP13K800                                                 
                OUTPUT BP13L320.                                                
                                                                                
           IF BP13K022-STATUS  NOT =  ZEROES AND 97                             
              DISPLAY 'BP13K022 OPEN ERROR, STATUS ' BP13K022-STATUS            
              MOVE  BP13K022-STATUS        TO   RETURN-CODE                     
              PERFORM 999-CLOSE-ROUTINE                                         
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS  NOT =  ZEROES AND 97                             
              DISPLAY 'BP13K800 OPEN ERROR, STATUS ' BP13K800-STATUS            
              MOVE  BP13K800-STATUS        TO   RETURN-CODE                     
              PERFORM 999-CLOSE-ROUTINE                                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
           STRING WS-DATE(5:2) '/' WS-DATE(3:2) '/' WS-DATE(1:4)                
                  DELIMITED BY SIZE INTO PRT-DATE.                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       200-READ-BP13F820.                                                       
      *-------------------------------------------------------------            
           READ BP13F820 AT END                                                 
                         MOVE 'Y' TO WS-EOF-F820                                
                         GO TO 200-EXIT.                                        
           ADD 1 TO WS-READ-F820-CNT.                                           
                                                                                
           MOVE F820-NME-OCCP          TO WS-PREV-OCCUP.                        
           MOVE F820-NUM-NRIC          TO WS-PREV-NRIC.                         
           MOVE F820-NUM-REGN          TO WS-PREV-REGN.                         
           MOVE F820-NUM-RELATIONSHIP  TO WS-PREV-RELATIONSHIP.                 
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       300-PROCESS-REPORT.                                                      
      *-------------------------------------------------------------            
           READ BP13F820 AT END                                                 
                         MOVE 'Y' TO WS-EOF-F820                                
                         GO TO 300-EXIT.                                        
           ADD 1 TO WS-READ-F820-CNT.                                           
                                                                                
           IF F820-NUM-REGN = WS-PREV-REGN                                      
              PERFORM 400-PRINT-ERROR-LIST THRU 400-EXIT                        
           ELSE                                                                 
           IF F820-NUM-RELATIONSHIP = '99'                                      
              MOVE 'Y' TO WS-CDE-99-CASE-ONLY                                   
              PERFORM 400-PRINT-ERROR-LIST THRU 400-EXIT                        
           ELSE                                                                 
              MOVE F820-NME-OCCP          TO WS-PREV-OCCUP                      
              MOVE F820-NUM-NRIC          TO WS-PREV-NRIC                       
              MOVE F820-NUM-REGN          TO WS-PREV-REGN                       
              MOVE F820-NUM-RELATIONSHIP  TO WS-PREV-RELATIONSHIP.              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       400-PRINT-ERROR-LIST.                                                    
      *-------------------------------------------------------------            
            IF WS-REC-CNT > 34                                                  
               MOVE 0 TO WS-REC-CNT                                             
               PERFORM 500-PRINT-HEADER THRU 500-EXIT.                          
                                                                                
            IF WS-CDE-99-CASE-ONLY  = 'Y'                                       
               NEXT SENTENCE                                                    
            ELSE                                                                
               MOVE SPACES                 TO L320-PRTREC                       
               ADD  1                      TO WS-SEQ-CNT                        
               MOVE WS-SEQ-CNT             TO PRT-SEQ-CNT                       
               MOVE WS-PREV-REGN           TO PRT-REGN-NO                       
               MOVE WS-PREV-NRIC           TO PRT-NUM-NRIC                      
               MOVE WS-PREV-RELATIONSHIP   TO PRT-RELATIONSHIP                  
               MOVE WS-PREV-OCCUP          TO PRT-NME-OCCUP                     
                                                                                
               MOVE SPACES                 TO K022-KEY-FLD                      
               MOVE WS-PREV-REGN           TO K022-NUM-REGN                     
               PERFORM 410-READ-BP13K022   THRU 410-EXIT                        
               MOVE K022-NUM-OIC           TO PRT-NUM-OIC                       
                                                                                
               WRITE L320-PRTREC FROM L320-DTL AFTER 2                          
               ADD 1 TO WS-REC-CNT                                              
            END-IF.                                                             
                                                                                
            MOVE SPACES                 TO L320-PRTREC.                         
            ADD  1                      TO WS-SEQ-CNT.                          
            MOVE WS-SEQ-CNT             TO PRT-SEQ-CNT.                         
                                                                                
            MOVE F820-NUM-REGN          TO PRT-REGN-NO.                         
            MOVE F820-NUM-NRIC          TO PRT-NUM-NRIC.                        
            MOVE F820-NUM-RELATIONSHIP  TO PRT-RELATIONSHIP.                    
            MOVE F820-NME-OCCP          TO PRT-NME-OCCUP.                       
                                                                                
            MOVE SPACES                 TO K022-KEY-FLD.                        
            MOVE F820-NUM-REGN          TO K022-NUM-REGN.                       
            PERFORM 410-READ-BP13K022   THRU 410-EXIT.                          
            MOVE K022-NUM-OIC           TO PRT-NUM-OIC.                         
                                                                                
            MOVE F820-NUM-REGN          TO K800-NUM-REGN.                       
            PERFORM 420-READ-BP13K800   THRU 420-EXIT.                          
            MOVE K800-NUM-STATUS        TO PRT-NUM-STATUS.                      
            IF K800-DTE-ACCEPTANCE NOT = SPACES AND LOW-VALUES                  
               STRING K800-DTE-ACCEPTANCE(7:2) '/'                              
                      K800-DTE-ACCEPTANCE(5:2) '/'                              
                      K800-DTE-ACCEPTANCE(1:4)                                  
               DELIMITED BY SIZE INTO PRT-DTE-ACCEPTANCE                        
            END-IF.                                                             
                                                                                
            WRITE L320-PRTREC FROM L320-DTL AFTER 1.                            
            ADD 1 TO WS-REC-CNT.                                                
                                                                                
            MOVE F820-NME-OCCP          TO WS-PREV-OCCUP.                       
            MOVE F820-NUM-NRIC          TO WS-PREV-NRIC.                        
            MOVE F820-NUM-REGN          TO WS-PREV-REGN.                        
            MOVE F820-NUM-RELATIONSHIP  TO WS-PREV-RELATIONSHIP.                
            MOVE 'N' TO   WS-CDE-99-CASE-ONLY.                                  
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       410-READ-BP13K022.                                                       
      *-------------------------------------------------------------            
           READ BP13K022 KEY IS K022-NUM-REGN.                                  
                                                                                
           EVALUATE BP13K022-STATUS                                             
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN 23                                                           
                 MOVE  SPACES            TO   K022-NUM-OIC                      
              WHEN OTHER                                                        
                 DISPLAY 'BP13K022 READ ERROR, STATUS ' BP13K022-STATUS         
                         ' , KEY : ' K022-NUM-REGN                              
                 MOVE BP13K022-STATUS    TO   RETURN-CODE                       
                 PERFORM 999-CLOSE-ROUTINE                                      
           END-EVALUATE.                                                        
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       420-READ-BP13K800.                                                       
      *-------------------------------------------------------------            
           READ BP13K800 KEY IS K800-NUM-REGN.                                  
                                                                                
           EVALUATE BP13K800-STATUS                                             
              WHEN 00                                                           
                 CONTINUE                                                       
              WHEN 23                                                           
                 MOVE  SPACES            TO   K800-DTE-ACCEPTANCE               
                 MOVE  SPACES            TO   K800-NUM-STATUS                   
              WHEN OTHER                                                        
                 DISPLAY 'BP13K800 READ ERROR, STATUS ' BP13K800-STATUS         
                         ' , KEY : ' K800-NUM-REGN                              
                 MOVE BP13K800-STATUS    TO   RETURN-CODE                       
                 PERFORM 999-CLOSE-ROUTINE                                      
           END-EVALUATE.                                                        
                                                                                
       420-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       500-PRINT-HEADER.                                                        
      *-------------------------------------------------------------            
           ADD 1                     TO WS-PAGE.                                
           MOVE WS-PAGE              TO PRT-PAGE.                               
                                                                                
           MOVE SPACES TO L320-PRTREC.                                          
           MOVE PARM-STRING  TO L320-HDG1(9:2).                                 
           WRITE L320-PRTREC FROM L320-HDG1 AFTER PAGE.                         
           WRITE L320-PRTREC FROM L320-HDG2 AFTER 1.                            
           WRITE L320-PRTREC FROM L320-HDG3 AFTER 1.                            
           WRITE L320-PRTREC FROM L320-HDG4 AFTER 1.                            
           MOVE SPACES TO L320-PRTREC.                                          
           WRITE L320-PRTREC.                                                   
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       999-CLOSE-ROUTINE.                                                       
      *-------------------------------------------------------------            
           CLOSE BP13F820                                                       
                 BP13K022                                                       
                 BP13K800                                                       
                 BP13L320.                                                      
                                                                                
           IF F820-STATUS NOT = 0 AND 97                                        
              DISPLAY 'CLOSING ERROR, BP13F820-STATUS ' F820-STATUS.            
                                                                                
           IF BP13K022-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K022 CLOSE ERROR, STATUS ' BP13K022-STATUS            
             MOVE BP13K022-STATUS     TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS  NOT =  ZEROES AND 97                             
             DISPLAY 'BP13K800 CLOSE ERROR, STATUS ' BP13K800-STATUS            
             MOVE BP13K800-STATUS     TO RETURN-CODE                            
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
