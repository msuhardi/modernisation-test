       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.  BP13CF40.                                                   
       AUTHOR.      ZARITA DANE SARMIENTO.                                      
       DATE-WRITTEN 18/10/2011.                                                 
                                                                                
      ****************************************************************          
      *            SYSTEM OF COMMITMENT (BP13)                       *          
      *--------------------------------------------------------------*          
      * OBJECTIVES  THIS PROGRAM IS TO CALL BP13C907 TO GET THE      *          
      *             DISTANCE BETWEEN TWO POSTAL CODES AND BLK-NO.    *          
      *             PROGRAM WILL BROWSE THRU ALL BLK-NO UNDER THE NT *          
      *             SO LONG AS ONE OF THE BLK-NO IS =< 2KM, GRANT    *          
      *             THE APPLN UNDER MCPS, MOVE 'T' TO RES-HA1.       *          
      *                                                              *          
      *             PRINT 3 REPORTS -                                *          
      *   P13LF40A - WITHIN 2 KM                                     *          
      *   P13LF40B - ALL BLK OUT OF 2 KM                             *          
      *   P13LF40C - PRIVATE ADDR, BLK NOTFD IN BB29 FILE, APPLN ADDR*          
      *              NOTFD IN BB29 FILE                              *          
      *--------------------------------------------------------------*          
      * MODIFICATIONS :                                              *          
      *                                                              *          
      * CHGE REQ  DATE   AUTHOR  REMARKS                             *          
      * --------- ----- ----     ----------------------------------- *          
      * BP134333  181011 ZDD1-NEW PGM  PATTERN TO BP13C014           *          
      * BP135254  150414 SMR -DELETE UNUSED FILE (BP13K816)          *          
      ****************************************************************          
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F730 ASSIGN  TO  BP13F730.                                
           SELECT BP13F725 ASSIGN  TO  BP13F725.                                
                                                                                
           SELECT BP13K730 ASSIGN  TO  BP13K730                                 
                     ORGANIZATION  IS  INDEXED                                  
                     ACCESS MODE   IS  RANDOM                                   
                     RECORD KEY    IS  K730-KEY-NEW                             
                     FILE STATUS   IS  WS-K730-STATUS.                          
                                                                                
           SELECT BP13KA20 ASSIGN  TO  BP13KA20                                 
                     ORGANIZATION  IS  INDEXED                                  
                     ACCESS MODE   IS  DYNAMIC                                  
                     RECORD KEY    IS  KA20-KEY-FLD                             
                     FILE STATUS   IS  WS-KA20-STATUS.                          
                                                                                
           SELECT BP13KB78 ASSIGN  TO  BP13KB78                                 
                     ORGANIZATION  IS  INDEXED                                  
                     ACCESS MODE   IS  RANDOM                                   
                     RECORD KEY    IS  KB78-KEY-FLD                             
                     FILE STATUS   IS  WS-KB78-STATUS.                          
                                                                                
           SELECT P13LF40A ASSIGN  TO  P13LF40A.                                
           SELECT P13LF40C ASSIGN  TO  P13LF40C.                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            RECORD CONTAINS 500 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F730.                                                           
                                                                                
       FD   BP13F725                                                            
            RECORD CONTAINS 211 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       COPY BP13F725.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
                                                                                
       COPY BP13K730.                                                           
                                                                                
       FD   BP13KA20                                                            
            RECORD CONTAINS 200 CHARACTERS.                                     
                                                                                
       COPY BP13KA20.                                                           
                                                                                
       FD   BP13KB78                                                            
            RECORD CONTAINS 3300 CHARACTERS.                                    
                                                                                
       COPY BP13KB78.                                                           
                                                                                
       FD   P13LF40A                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
                                                                                
       01 P13LF40-REC-A                      PIC  X(132).                       
                                                                                
       FD   P13LF40C                                                            
            RECORD CONTAINS 132 CHARACTERS                                      
            BLOCK CONTAINS 0 RECORDS                                            
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
                                                                                
       01 P13LF40-REC-C                      PIC  X(132).                       
                                                                                
                                                                                
      *----------------------------*                                            
       WORKING-STORAGE SECTION.                                                 
      *----------------------------*                                            
       01  WS-VARIABLES.                                                        
           05  WS-DIST                       PIC  9(03)V99.                     
           05  WS-TEMP-REFNO                 PIC  X(08).                        
           05  WS-NON-HDB                    PIC  X     VALUE 'N'.              
           05  WS-F730-NOTFND                PIC  X     VALUE 'N'.              
           05  WS-F725-EOF                   PIC  X     VALUE 'N'.              
           05  WS-F730-EOF                   PIC  X     VALUE 'N'.              
           05  WS-FND-KB78                   PIC  X     VALUE 'N'.              
           05  WS-K730-STATUS                PIC  9(02) VALUE ZEROES.           
           05  WS-KA20-STATUS                PIC  9(02) VALUE ZEROES.           
           05  WS-KB78-STATUS                PIC  9(02) VALUE ZEROES.           
           05  WS-RETURN-CODE                PIC  9(02) VALUE ZEROES.           
           05  WS-PREV-LISTING               PIC  X(08) VALUE SPACES.           
           05  WS-REFNO-A                    PIC  X(08) VALUE SPACES.           
           05  WS-REFNO-B                    PIC  X(08) VALUE SPACES.           
           05  WS-REFNO-C                    PIC  X(08) VALUE SPACES.           
           05  WS-MCPS-TAG                   PIC  X     VALUE 'N'.              
           05  WS-INELIG-TAG                 PIC  X     VALUE SPACES.           
                                                                                
       01  WS-DATE.                                                             
           05  WS-CCYY                PIC 9(04).                                
           05  WS-MM                  PIC 9(02).                                
           05  WS-DD                  PIC 9(02).                                
                                                                                
       01  WS-DATE-FORMATTED.                                                   
           05  WS-DD-F                PIC X(02).                                
           05  FILLER                 PIC X VALUE '/'.                          
           05  WS-MM-F                PIC X(02).                                
           05  FILLER                 PIC X VALUE '/'.                          
           05  WS-CCYY-F              PIC X(04).                                
                                                                                
       01  WS-POSTAL-TABLE.                                                     
           05  WS-POSTAL-ARRAY   OCCURS 2000 TIMES.                             
               10  WS-PC1-REFNO              PIC  X(08).                        
               10  WS-PC1-NRIC               PIC  X(09).                        
               10  WS-PC1-CODE               PIC  X(06).                        
               10  WS-PC1-BLK                PIC  X(05).                        
               10  WS-PC2-NT                 PIC  X(03).                        
               10  WS-PC2-CODE               PIC  X(06).                        
               10  WS-PC2-BLK                PIC  X(05).                        
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-SUB                        PIC 9(6) VALUE ZEROES.             
           05  WS-INDX                       PIC 9(6) VALUE ZEROES.             
           05  WS-F725-READ                  PIC 9(6) VALUE ZEROES.             
           05  WS-F730-READ                  PIC 9(6) VALUE ZEROES.             
           05  WS-KA20-READ                  PIC 9(6) VALUE ZEROES.             
           05  WS-K730-READ                  PIC 9(6) VALUE ZEROES.             
           05  WS-F730-NOTMATCH              PIC 9(6) VALUE ZEROES.             
           05  WS-F725-NOTMATCH              PIC 9(6) VALUE ZEROES.             
           05  WS-KA20-NTFND                 PIC 9(6) VALUE ZEROES.             
           05  WS-K730-NTFND                 PIC 9(6) VALUE ZEROES.             
           05  WS-K730-REWRITE               PIC 9(6) VALUE ZEROES.             
           05  WS-CNT-PAGE-A                 PIC 9(6) VALUE ZEROES.             
           05  WS-CNT-PAGE-B                 PIC 9(6) VALUE ZEROES.             
           05  WS-CNT-PAGE-C                 PIC 9(6) VALUE ZEROES.             
           05  WS-CNT-LINE-A                 PIC 9(6) VALUE ZEROES.             
           05  WS-CNT-LINE-B                 PIC 9(6) VALUE ZEROES.             
           05  WS-CNT-LINE-C                 PIC 9(6) VALUE ZEROES.             
           05  WS-CNT-SNO-A                  PIC 9(6) VALUE ZEROES.             
           05  WS-CNT-SNO-B                  PIC 9(6) VALUE ZEROES.             
           05  WS-CNT-SNO-C                  PIC 9(6) VALUE ZEROES.             
                                                                                
       01  RPT-HEADER1.                                                         
           05  RPT-LISTING                PIC X(08) VALUE 'P13LF40A'.           
           05  FILLER                     PIC X(10) VALUE '      HDB3'.         
           05  FILLER                     PIC X(27) VALUE SPACES.               
           05  FILLER                     PIC X(39) VALUE                       
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER                     PIC X(14) VALUE SPACES.               
           05  FILLER                     PIC X(07) VALUE 'DATE : '.            
           05  RPT-DTE-CURRENT            PIC X(10) VALUE SPACES.               
           05  FILLER                     PIC X(04) VALUE SPACES.               
           05  FILLER                     PIC X(07) VALUE 'PAGE : '.            
           05  RPT-PAGE                   PIC ZZZZZ9.                           
                                                                                
       01  RPT-HEADER2.                                                         
           05  FILLER                     PIC X(38) VALUE SPACES.               
           05  FILLER                     PIC X(55) VALUE                       
               'REPORT ON DISTANCE COMPUTATION BASED ON POSTAL CODES'.          
           05  FILLER                     PIC X(39) VALUE SPACES.               
                                                                                
       01  RPT-HEADER3.                                                         
           05  FILLER                     PIC X(42) VALUE SPACES.               
           05  RPT-LISTING-TYPE           PIC X(45) VALUE SPACES.               
           05  FILLER                     PIC X(39) VALUE SPACES.               
                                                                                
       01  RPT-CAPTION.                                                         
           05  FILLER                     PIC X(03) VALUE SPACES.               
           05  FILLER                     PIC X(04) VALUE 'S/NO'.               
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  FILLER                     PIC X(05) VALUE 'REFNO'.              
           05  FILLER                     PIC X(03) VALUE SPACES.               
           05  FILLER                     PIC X(09) VALUE 'MCPS-NRIC'.          
           05  FILLER                     PIC X(19) VALUE SPACES.               
           05  FILLER                     PIC X(07) VALUE 'ADDRESS'.            
           05  FILLER                     PIC X(19) VALUE SPACES.               
           05  FILLER                     PIC X(07) VALUE 'POSTAL1'.            
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  FILLER                     PIC X(02) VALUE 'NT'.                 
           05  FILLER                     PIC X(03) VALUE SPACES.               
           05  FILLER                     PIC X(07) VALUE 'POSTAL2'.            
           05  FILLER                     PIC X(03) VALUE SPACES.               
           05  FILLER                     PIC X(03) VALUE 'BLK'.                
           05  FILLER                     PIC X(03) VALUE SPACES.               
           05  FILLER                     PIC X(08) VALUE 'DISTANCE'.           
           05  FILLER                     PIC X(02) VALUE SPACES.               
                                                                                
       01  RPT-DETAILS.                                                         
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  RPT-CNT-SNO                PIC ZZZZ9 VALUE ZEROES.               
           05  FILLER                     PIC X(01) VALUE SPACES.               
           05  RPT-REFNO                  PIC X(08) VALUE SPACES.               
           05  FILLER                     PIC X(01) VALUE SPACES.               
           05  RPT-MCPS-NRIC              PIC X(09) VALUE SPACES.               
           05  FILLER                     PIC X(03) VALUE SPACES.               
           05  RPT-ADDRESS                PIC X(40) VALUE SPACES.               
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  RPT-POSTAL1                PIC X(06) VALUE SPACES.               
           05  FILLER                     PIC X(03) VALUE SPACES.               
           05  RPT-NT                     PIC X(03) VALUE SPACES.               
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  RPT-POSTAL2                PIC X(06) VALUE SPACES.               
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  RPT-BLK                    PIC X(05) VALUE SPACES.               
           05  FILLER                     PIC X(04) VALUE SPACES.               
           05  RPT-DISTANCE               PIC ZZ9.99 VALUE ZEROES.              
           05  FILLER                     PIC X(25) VALUE SPACES.               
                                                                                
       01  RPT-DETAIL2.                                                         
           05  FILLER                     PIC X(06) VALUE SPACES.               
           05  FILLER                     PIC X(37) VALUE ALL '*'.              
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  FILLER                     PIC X(23) VALUE                       
               'MCPS ELIGIBILITY TAG = '.                                       
           05  RPT-MCPS-TAG               PIC X(01) VALUE SPACES.               
           05  FILLER                     PIC X(02) VALUE SPACES.               
           05  FILLER                     PIC X(37) VALUE ALL '*'.              
                                                                                
      *----------------------------*                                            
      *    LINKAGE FOR BP13C907    *                                            
      *----------------------------*                                            
       01  LINK-REC.                                                            
           05  LINK-OPEN-CLOSE               PIC X.                             
           05  LINK-NON-HDB                  PIC X.                             
           05  LINK-RETURN-CODE              PIC 9(02).                         
           05  LINK-POSTAL-CODE1             PIC X(06).                         
           05  LINK-POSTAL-BLK1              PIC X(05).                         
           05  LINK-POSTAL-CODE2             PIC X(06).                         
           05  LINK-POSTAL-BLK2              PIC X(05).                         
           05  LINK-DISTANCE                 PIC 9(03)V99.                      
                                                                                
                                                                                
                                                                                
      *----------------------------*                                            
       PROCEDURE DIVISION.                                                      
      *----------------------------*                                            
      ******************************************************                    
       0000-MAIN-PROCESS.                                                       
      ******************************************************                    
           PERFORM 0100-INITIALIZATION  THRU 0100-EXIT.                         
           PERFORM 0300-READ-BP13F730   THRU 0300-EXIT.                         
           PERFORM 0200-READ-BP13F725   THRU 0200-EXIT.                         
           PERFORM 0600-PROCESS-RECORDS THRU 0600-EXIT                          
                   UNTIL F730-NUM-RES-NRIC1 = HIGH-VALUES                       
                     AND F725-NUM-NRIC      = HIGH-VALUES.                      
           PERFORM 9999-CLOSE-FILES     THRU 9999-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0100-INITIALIZATION.                                                     
      ******************************************************                    
           OPEN INPUT BP13F730                                                  
                      BP13F725                                                  
                      BP13KA20                                                  
                      BP13KB78                                                  
                  I-O BP13K730                                                  
               OUTPUT P13LF40A                                                  
                      P13LF40C.                                                 
                                                                                
                                                                                
           IF WS-K730-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K730: ' WS-K730-STATUS                 
              MOVE WS-K730-STATUS      TO RETURN-CODE                           
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-KA20-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13KA20: ' WS-KA20-STATUS                 
              MOVE WS-KA20-STATUS      TO RETURN-CODE                           
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
           END-IF.                                                              
                                                                                
           IF WS-KB78-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR OPENING BP13KB78: ' WS-KB78-STATUS                 
              MOVE WS-KB78-STATUS      TO RETURN-CODE                           
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
           END-IF.                                                              
                                                                                
           INITIALIZE LINK-REC.                                                 
           MOVE 'O'        TO LINK-OPEN-CLOSE.                                  
           CALL 'BP13C907' USING LINK-REC.                                      
           IF WS-RETURN-CODE NOT = 00 AND 97                                    
              MOVE WS-RETURN-CODE     TO RETURN-CODE                            
              PERFORM 9999-CLOSE-FILES THRU 9999-EXIT                           
           END-IF.                                                              
           MOVE SPACE      TO LINK-OPEN-CLOSE.                                  
                                                                                
           MOVE  FUNCTION CURRENT-DATE  TO WS-DATE.                             
           MOVE  WS-CCYY                TO WS-CCYY-F.                           
           MOVE  WS-MM                  TO WS-MM-F.                             
           MOVE  WS-DD                  TO WS-DD-F.                             
           MOVE  WS-DATE-FORMATTED      TO RPT-DTE-CURRENT.                     
                                                                                
       0100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0200-READ-BP13F725.                                                      
      ******************************************************                    
           READ BP13F725 AT END                                                 
                MOVE HIGH-VALUES         TO F725-NUM-NRIC                       
                GO TO 0200-EXIT.                                                
                                                                                
           ADD  1                        TO WS-F725-READ.                       
                                                                                
       0200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0300-READ-BP13F730.                                                      
      ******************************************************                    
           READ BP13F730 AT END                                                 
                MOVE HIGH-VALUES         TO F730-NUM-RES-NRIC1                  
               GO TO 0300-EXIT.                                                 
                                                                                
           ADD  1                        TO WS-F730-READ.                       
                                                                                
       0300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0400-READ-BP13KA20.                                                      
      ******************************************************                    
                                                                                
           PERFORM 0560-READ-BP13KB78 THRU 0560-EXIT.                           
                                                                                
           MOVE SPACES                   TO BP13KA20-REC.                       
           MOVE KB78-NUM-PROJECT         TO KA20-NUM-PRJCT.                     
                                                                                
           START BP13KA20 KEY IS >= KA20-KEY-FLD.                               
                                                                                
           IF WS-KA20-STATUS = 00                                               
              PERFORM 0500-READNXT-BP13KA20 THRU 0500-EXIT                      
                   UNTIL KA20-NUM-PRJCT    NOT = KB78-NUM-PROJECT               
                   OR    KA20-KEY-FLD         = HIGH-VALUES                     
           ELSE                                                                 
              IF WS-KA20-STATUS = 23                                            
                 ADD 1                   TO WS-KA20-NTFND                       
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13KA20,STATUS ' WS-KA20-STATUS           
                 MOVE WS-KA20-STATUS     TO RETURN-CODE                         
                 GO TO 9999-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       0400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0500-READNXT-BP13KA20.                                                   
      ******************************************************                    
           READ BP13KA20 NEXT                                                   
                AT END MOVE HIGH-VALUES TO KA20-KEY-FLD                         
                GO TO 0500-EXIT.                                                
                                                                                
           ADD 1                        TO WS-KA20-READ.                        
                                                                                
           IF KA20-NUM-PRJCT   = KB78-NUM-PROJECT                               
             IF  KB78-NUM-NEW-TOWN = F730-CDE-NT1                               
               AND KB78-NUM-FLAT-TYPE = F730-CDE-FLAT-TYPE                      
               PERFORM 0540-MOVE-POST-BLK-NT THRU 0540-EXIT                     
             END-IF                                                             
           END-IF.                                                              
                                                                                
       0500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ********************************************                              
       0540-MOVE-POST-BLK-NT.                                                   
      ********************************************                              
           ADD  1                    TO WS-SUB.                                 
           MOVE KA20-NUM-POSTAL      TO WS-PC2-CODE(WS-SUB).                    
           MOVE KA20-NUM-BLK         TO WS-PC2-BLK (WS-SUB).                    
           MOVE KB78-NUM-NEW-TOWN    TO WS-PC2-NT  (WS-SUB).                    
                                                                                
       0540-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0560-READ-BP13KB78.                                                      
      ******************************************************                    
           MOVE SPACES                TO BP13KB78-REC.                          
           MOVE F730-NUM-REGN         TO KB78-NUM-REGN.                         
                                                                                
           READ BP13KB78.                                                       
                                                                                
           IF WS-KB78-STATUS = 00                                               
              MOVE 'Y'                      TO WS-FND-KB78                      
           ELSE                                                                 
              IF WS-KB78-STATUS = 23                                            
                 MOVE 'N'                      TO WS-FND-KB78                   
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13KB78,STATUS ' WS-KB78-STATUS           
                 MOVE WS-KB78-STATUS     TO RETURN-CODE                         
                 GO TO 9999-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       0560-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0600-PROCESS-RECORDS.                                                    
      ******************************************************                    
           IF F725-NUM-NRIC   = F730-NUM-RES-NRIC1                              
              MOVE 'N'                      TO WS-F730-NOTFND                   
              INITIALIZE                       WS-POSTAL-TABLE                  
              MOVE F725-CDE-POSTAL          TO WS-PC1-CODE(1)                   
              MOVE F725-NUM-BLK-HSE         TO WS-PC1-BLK(1)                    
              PERFORM 0400-READ-BP13KA20    THRU 0400-EXIT                      
              IF WS-KA20-STATUS = 00 OR 10                                      
                 IF F725-NUM-ADDR-TYPE NOT = 'A'                                
                    PERFORM 0900-PRINT-REPORT  THRU 0900-EXIT                   
                 ELSE                                                           
                    MOVE    1                     TO WS-INDX                    
                    MOVE   'N'                    TO WS-NON-HDB                 
                    MOVE SPACES                   TO WS-MCPS-TAG                
                    PERFORM 0700-GET-DISTANCE     THRU 0700-EXIT                
                       VARYING WS-INDX FROM 1 BY 1                              
                       UNTIL WS-INDX > WS-SUB                                   
                          OR WS-NON-HDB = 'Y'                                   
                    MOVE WS-MCPS-TAG     TO RPT-MCPS-TAG                        
                    WRITE P13LF40-REC-A  FROM RPT-DETAIL2 AFTER 1               
                 END-IF                                                         
              END-IF                                                            
              PERFORM 0300-READ-BP13F730    THRU 0300-EXIT                      
              PERFORM 0200-READ-BP13F725    THRU 0200-EXIT                      
           ELSE                                                                 
              IF F725-NUM-NRIC  <  F730-NUM-RES-NRIC1                           
                 ADD 1 TO WS-F725-NOTMATCH                                      
                 PERFORM 0200-READ-BP13F725 THRU 0200-EXIT                      
              ELSE                                                              
                 MOVE 'Y'                   TO WS-F730-NOTFND                   
                 PERFORM 0900-PRINT-REPORT  THRU 0900-EXIT                      
                 ADD 1 TO WS-F730-NOTMATCH                                      
                 PERFORM 0300-READ-BP13F730 THRU 0300-EXIT                      
              END-IF                                                            
           END-IF.                                                              
                                                                                
       0600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0700-GET-DISTANCE.                                                       
      ******************************************************                    
           MOVE ZEROES                TO WS-DIST.                               
           MOVE WS-PC1-CODE(1)        TO LINK-POSTAL-CODE1.                     
           MOVE WS-PC1-BLK (1)        TO LINK-POSTAL-BLK1.                      
           MOVE WS-PC2-CODE(WS-INDX)  TO LINK-POSTAL-CODE2.                     
           MOVE WS-PC2-BLK (WS-INDX)  TO LINK-POSTAL-BLK2.                      
                                                                                
           CALL 'BP13C907' USING LINK-REC.                                      
                                                                                
           MOVE LINK-DISTANCE         TO WS-DIST.                               
           MOVE LINK-NON-HDB          TO WS-NON-HDB.                            
                                                                                
           PERFORM 0900-PRINT-REPORT  THRU 0900-EXIT.                           
                                                                                
                                                                                
       0700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0800-PROCESS-BP13K730.                                                   
      ******************************************************                    
           MOVE F730-NUM-REGN         TO K730-NUM-REGN.                         
                                                                                
           READ BP13K730.                                                       
                                                                                
           IF WS-K730-STATUS = 00                                               
              ADD   1                       TO WS-K730-READ                     
      *       MOVE 'T'                      TO K730-CDE-RESIDENT-HA1            
              MOVE WS-INELIG-TAG            TO K730-CDE-RESIDENT-HA1            
              PERFORM 0850-REWRITE-BP13K730 THRU 0850-EXIT                      
           ELSE                                                                 
              IF WS-K730-STATUS = 23                                            
                 ADD  1                  TO WS-K730-NTFND                       
              ELSE                                                              
                 DISPLAY 'ERROR READ BP13K730,STATUS ' WS-K730-STATUS           
                 MOVE WS-K730-STATUS     TO RETURN-CODE                         
                 GO TO 9999-CLOSE-FILES                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       0800-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0850-REWRITE-BP13K730.                                                   
      ******************************************************                    
           REWRITE BP13K730-REC.                                                
                                                                                
           IF WS-K730-STATUS = 00                                               
              ADD  1                     TO WS-K730-REWRITE                     
           ELSE                                                                 
              DISPLAY 'ERROR REWRITE BP13K730,STATUS ' WS-K730-STATUS           
              MOVE WS-K730-STATUS     TO RETURN-CODE                            
              GO TO 9999-CLOSE-FILES                                            
           END-IF.                                                              
                                                                                
       0850-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0900-PRINT-REPORT.                                                       
      ******************************************************                    
           MOVE SPACES                TO  RPT-DETAILS.                          
           INITIALIZE                     RPT-DETAILS.                          
                                                                                
           IF WS-F730-NOTFND = 'Y'                                              
              MOVE F730-NUM-REF            TO RPT-REFNO                         
              MOVE F730-NUM-RES-NRIC1      TO RPT-MCPS-NRIC                     
              MOVE 'RECORD NOT FOUND IN BP13F725 FILE'                          
                                           TO RPT-ADDRESS                       
              MOVE SPACES                  TO RPT-DETAILS(102:30)               
           ELSE                                                                 
              MOVE F730-NUM-REF            TO RPT-REFNO                         
              MOVE F730-NUM-RES-NRIC1      TO RPT-MCPS-NRIC                     
              STRING 'BLK '                                                     
                      F725-NUM-BLK-HSE                                          
                     ' #'                                                       
                      F725-NUM-STOREY                                           
                     '-'                                                        
                      F725-NUM-UNIT ' '                                         
                      F725-NME-STREET ' '                                       
                      DELIMITED BY SIZE INTO  RPT-ADDRESS                       
              MOVE WS-PC1-CODE (1)         TO RPT-POSTAL1                       
              MOVE WS-PC2-NT   (WS-INDX)   TO RPT-NT                            
              MOVE WS-PC2-CODE (WS-INDX)   TO RPT-POSTAL2                       
              MOVE WS-PC2-BLK  (WS-INDX)   TO RPT-BLK                           
              MOVE WS-DIST                 TO RPT-DISTANCE                      
              IF WS-NON-HDB = 'Y'                                               
                 MOVE SPACES               TO RPT-DETAILS(102:30)               
                 MOVE 'POSTAL & BLK NOT FOUND IN BB29'                          
                                           TO RPT-DETAILS(102:30)               
              END-IF                                                            
              IF F725-NUM-ADDR-TYPE NOT = 'A'                                   
                 MOVE SPACES               TO RPT-DETAILS(102:30)               
                 MOVE 'NON-HDB RESIDENT'   TO RPT-DETAILS(102:30)               
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-F730-NOTFND = 'Y'                                              
              INITIALIZE  WS-DIST                                               
              MOVE SPACES                    TO WS-MCPS-TAG                     
              MOVE SPACES                    TO WS-INELIG-TAG                   
              PERFORM 0800-PROCESS-BP13K730  THRU 0800-EXIT                     
              PERFORM 1050-PRINT-REPORT-C THRU 1050-EXIT                        
           ELSE                                                                 
              IF F725-NUM-ADDR-TYPE NOT = 'A'                                   
                 INITIALIZE  WS-DIST                                            
                 MOVE 'P'                       TO WS-MCPS-TAG                  
                 MOVE 'P'                       TO WS-INELIG-TAG                
                 PERFORM 0800-PROCESS-BP13K730  THRU 0800-EXIT                  
                 PERFORM 1050-PRINT-REPORT-C THRU 1050-EXIT                     
              ELSE                                                              
                 IF WS-NON-HDB = 'Y'                                            
                    INITIALIZE  WS-DIST                                         
                    MOVE 'P'                       TO WS-MCPS-TAG               
                    MOVE 'P'                       TO WS-INELIG-TAG             
                    PERFORM 0800-PROCESS-BP13K730  THRU 0800-EXIT               
                    PERFORM 1050-PRINT-REPORT-C THRU 1050-EXIT                  
                 ELSE                                                           
                    IF WS-DIST > 2.04                                           
                       IF WS-MCPS-TAG NOT = 'Y'                                 
                          MOVE 'H'                  TO WS-MCPS-TAG              
                          MOVE 'H'                  TO WS-INELIG-TAG            
                          PERFORM 0800-PROCESS-BP13K730  THRU 0800-EXIT         
                          PERFORM 0950-PRINT-REPORT-A    THRU 0950-EXIT         
                       ELSE                                                     
                          MOVE 'T'                  TO WS-INELIG-TAG            
                          PERFORM 0800-PROCESS-BP13K730  THRU 0800-EXIT         
                          MOVE 'Y'                  TO WS-MCPS-TAG              
                          PERFORM 0950-PRINT-REPORT-A    THRU 0950-EXIT         
                       END-IF                                                   
                    ELSE                                                        
                       MOVE 'T'                       TO WS-INELIG-TAG          
                       PERFORM 0800-PROCESS-BP13K730  THRU 0800-EXIT            
                       MOVE 'Y'                       TO WS-MCPS-TAG            
                       PERFORM 0950-PRINT-REPORT-A    THRU 0950-EXIT            
                    END-IF                                                      
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       0900-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       0950-PRINT-REPORT-A.                                                     
      ******************************************************                    
           MOVE 'P13LF40A'                 TO RPT-LISTING.                      
           MOVE 'SHORTEST DISTANCE COMPUTED FOR MCPS ELIBILITY'                 
                                           TO RPT-LISTING-TYPE.                 
           IF WS-CNT-LINE-A = ZEROES OR                                         
              WS-CNT-LINE-A > 56                                                
              ADD   1                      TO WS-CNT-PAGE-A                     
              MOVE  WS-CNT-PAGE-A          TO RPT-PAGE                          
              WRITE P13LF40-REC-A FROM RPT-HEADER1  AFTER PAGE                  
              WRITE P13LF40-REC-A FROM RPT-HEADER2  AFTER 1                     
              WRITE P13LF40-REC-A FROM RPT-HEADER3  AFTER 1                     
              MOVE  SPACES TO P13LF40-REC-A                                     
              WRITE P13LF40-REC-A                                               
              WRITE P13LF40-REC-A FROM RPT-CAPTION                              
              MOVE  SPACES TO P13LF40-REC-A                                     
              WRITE P13LF40-REC-A                                               
              MOVE  5 TO WS-CNT-LINE-A                                          
           END-IF.                                                              
                                                                                
           IF F730-NUM-REF NOT = WS-REFNO-A                                     
              ADD   1                      TO WS-CNT-SNO-A                      
              MOVE  WS-CNT-SNO-A           TO RPT-CNT-SNO                       
           ELSE                                                                 
              MOVE SPACES            TO RPT-DETAILS(1:80)                       
           END-IF.                                                              
                                                                                
           WRITE P13LF40-REC-A          FROM RPT-DETAILS AFTER 1.               
           ADD   1                      TO WS-CNT-LINE-A.                       
           MOVE 'P13LF40A'              TO WS-PREV-LISTING.                     
                                                                                
           IF RPT-REFNO NOT = SPACES AND LOW-VALUES                             
              MOVE RPT-REFNO               TO WS-REFNO-A                        
           END-IF.                                                              
       0950-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************                    
       1050-PRINT-REPORT-C.                                                     
      ******************************************************                    
           MOVE 'P13LF40C'                 TO RPT-LISTING.                      
           MOVE 'DISTANCE NOT COMPUTED FOR NON-HDB ADDRESS'                     
                                           TO RPT-LISTING-TYPE.                 
                                                                                
           IF WS-CNT-LINE-C = ZEROES OR                                         
              WS-CNT-LINE-C > 56                                                
              ADD   1                      TO WS-CNT-PAGE-C                     
              MOVE  WS-CNT-PAGE-C          TO RPT-PAGE                          
              WRITE P13LF40-REC-C FROM RPT-HEADER1  AFTER PAGE                  
              WRITE P13LF40-REC-C FROM RPT-HEADER2  AFTER 1                     
              WRITE P13LF40-REC-C FROM RPT-HEADER3  AFTER 1                     
              MOVE  SPACES TO P13LF40-REC-C                                     
              WRITE P13LF40-REC-C                                               
              WRITE P13LF40-REC-C FROM RPT-CAPTION(1:101)                       
              MOVE  SPACES TO P13LF40-REC-C                                     
              WRITE P13LF40-REC-C                                               
              MOVE  5 TO WS-CNT-LINE-C                                          
           END-IF.                                                              
                                                                                
           IF F730-NUM-REF NOT = WS-REFNO-C                                     
              ADD   1                      TO WS-CNT-SNO-C                      
              MOVE  WS-CNT-SNO-C           TO RPT-CNT-SNO                       
           ELSE                                                                 
              MOVE SPACES            TO RPT-DETAILS(1:80)                       
           END-IF.                                                              
                                                                                
           WRITE P13LF40-REC-C     FROM RPT-DETAILS  AFTER 1.                   
           ADD   1                      TO WS-CNT-LINE-C.                       
           MOVE 'P13LF40C'              TO WS-PREV-LISTING.                     
                                                                                
           IF RPT-REFNO NOT = SPACES AND LOW-VALUES                             
              MOVE RPT-REFNO               TO WS-REFNO-C                        
           END-IF.                                                              
                                                                                
       1050-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************                                                 
       9999-CLOSE-FILES.                                                        
      *************************                                                 
           DISPLAY '* ------------ BP13CF40 ------------- * '.                  
           DISPLAY 'NO OF REC READ    BP13F730 = ' WS-F730-READ.                
           DISPLAY 'NO OF REC READ    BP13F725 = ' WS-F725-READ.                
           DISPLAY 'NO OF REC READ    BP13KA20 = ' WS-KA20-READ.                
           DISPLAY 'NO OF REC READ    BP13K730 = ' WS-K730-READ.                
           DISPLAY 'NO OF REC REWRITE BP13K730 = ' WS-K730-REWRITE.             
           DISPLAY 'NO OF REC NOTFND  BP13F730 = ' WS-F730-NOTMATCH.            
           DISPLAY 'NO OF REC NOTFND  BP13F725 = ' WS-F725-NOTMATCH.            
           DISPLAY 'NO OF REC NOTFND  BP13KA20 = ' WS-KA20-NTFND.               
           DISPLAY 'NO OF REC NOTFND  BP13K730 = ' WS-K730-NTFND.               
                                                                                
           CLOSE BP13F725                                                       
                 BP13K730                                                       
                 BP13KA20                                                       
                 BP13KB78                                                       
                 P13LF40A                                                       
                 P13LF40C.                                                      
                                                                                
           IF WS-K730-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K730, STATUS ' WS-K730-STATUS          
              MOVE WS-K730-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KA20-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13KA20, STATUS ' WS-KA20-STATUS          
              MOVE WS-KA20-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           IF WS-KB78-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13KB78, STATUS ' WS-KB78-STATUS          
              MOVE WS-KB78-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           INITIALIZE LINK-REC.                                                 
           MOVE 'C'        TO LINK-OPEN-CLOSE.                                  
           CALL 'BP13C907' USING LINK-REC.                                      
           IF LINK-RETURN-CODE NOT = 00 AND 97                                  
              MOVE LINK-RETURN-CODE        TO RETURN-CODE                       
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
