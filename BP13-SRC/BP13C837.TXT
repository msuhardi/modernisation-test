       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C837.                                                 
      *AUTHOR.        ED.                                                       
      *DATE-WRITTEN.  27SEP2001.                                                
      * ====================================================== *                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT  (SOC)           *                
      *                                                        *                
      *    SYSTEM ID   : BP13                                  *                
      *                                                        *                
      *    OBJECTIVES :                                        *                
      *                                                        *                
      *    1. INSPECT OCCUPIER DATA FOR EXISTENCE OF MTF CASES *                
      *    2. TAG CORRESPONDING BP13K730-CDE-RESIDENT-HA1      *                
      *                                                        *                
      *    INPUT  FILE  : BP13F730 - REQUEST DATA ENTRY FILE   *                
      *                   BP13F825 - REQUEST OCCUPIER FILE     *                
      *                   BP13K060 - CODE TRANSCRIPTION FILE   *                
      *    I/O    FILE  : BP13K730 - REQUEST DATA ENTRY FILE   *                
      *                                                        *                
      *    OUTPUT FILES : NIL                                  *                
      *                                                        *                
      *    LISTINGS     : BP13L837 - LIST OF TAGGED MCPS CASES *                
      *                   P13L837B - LIST OF NON-TAGGED MCPS   *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY    DATE    DETAILS                          *                
      * ======  ==   ====== ================================== *                
      *BP131064 ED   270901 NEW PGM                            *                
      *BP131064 ED   061201 TO INDICATE IF APPL OR OCCP        *                
      *                     TO CATER FOR SINGLE PARENTS        *                
      *BP132438 CKK  121103 TO MOVE 'M' TO ELIG-TAG FOR INELIG *                
      *                     CASES                              *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F730 ASSIGN TO BP13F730.                                  
                                                                                
           SELECT BP13F825 ASSIGN TO BP13F825.                                  
                                                                                
           SELECT BP13K060 ASSIGN TO BP13K060                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K060-KEY-FLD                                  
                  FILE STATUS  IS WS-K060-STATUS.                               
                                                                                
           SELECT BP13K730 ASSIGN TO BP13K730                                   
                  ORGANIZATION IS INDEXED                                       
                  ACCESS MODE  IS RANDOM                                        
                  RECORD KEY   IS K730-KEY-NEW                                  
                  FILE STATUS  IS WS-K730-STATUS.                               
                                                                                
           SELECT BP13L837 ASSIGN TO BP13L837.                                  
                                                                                
           SELECT P13L837B ASSIGN TO P13L837B.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F730                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13F730.                                                           
                                                                                
       FD   BP13F825                                                            
            RECORDING MODE    F                                                 
            BLOCK CONTAINS    0 RECORDS                                         
            LABEL RECORDS   ARE STANDARD                                        
            RECORD CONTAINS 200 CHARACTERS.                                     
       COPY BP13F825.                                                           
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS  25 CHARACTERS.                                     
       COPY BP13K060.                                                           
                                                                                
       FD   BP13K730                                                            
            RECORD CONTAINS 500 CHARACTERS.                                     
       COPY BP13K730.                                                           
                                                                                
       FD   BP13L837                                                            
            RECORDING MODE    F                                                 
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   BP13L837-REC    PIC X(132).                                         
                                                                                
       FD   P13L837B                                                            
            RECORDING MODE    F                                                 
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   P13L837B-REC    PIC X(132).                                         
                                                                                
      ****************************************************************          
       WORKING-STORAGE SECTION.                                                 
      ****************************************************************          
                                                                                
       01  WS-FILE-CONTROLS.                                                    
           05  WS-F730-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-F825-EOF-SW                 PIC X(1)  VALUE 'N'.              
           05  WS-K060-STATUS                 PIC 9(2)  VALUE ZEROS.            
           05  WS-K730-STATUS                 PIC 9(2)  VALUE ZEROS.            
                                                                                
       01  WS-RECORD-COUNTERS.                                                  
           05  WS-CNT-READ-F730               PIC 9(5)  VALUE ZEROS.            
           05  WS-CNT-UPD-K730                PIC 9(5)  VALUE ZEROS.            
                                                                                
       01  WS-L837-CONTROLS.                                                    
           05  WS-L837-LINECTR                PIC 9(2)  VALUE 50.               
           05  WS-L837-PAGECTR                PIC 9(5)  VALUE ZEROS.            
           05  WS-L837-SNO                    PIC 9(5)  VALUE 0.                
           05  WS-L837-SNO-X                  PIC 99999.                        
                                                                                
       01  WS-L837B-CONTROLS.                                                   
           05  WS-L837B-LINECTR               PIC 9(2)  VALUE 50.               
           05  WS-L837B-PAGECTR               PIC 9(5)  VALUE ZEROS.            
           05  WS-L837B-SNO                   PIC 9(5)  VALUE 0.                
                                                                                
       01  WS-MISCELLANEOUS-VARS.                                               
           05  WS-BLANKS                      PIC X(132) VALUE SPACES.          
           05  WS-UPDATE-TAG                  PIC X(1)   VALUE SPACES.          
           05  WS-BIRTH-YY                    PIC 9(4).                         
           05  WS-OCCP-CTR                    PIC 9(2).                         
           05  WS-CTR                         PIC 9(2).                         
           05  WS-O-CTR                       PIC 9(2).                         
           05  WS-T-CTR                       PIC 9.                            
           05  WS-RELATIONSHIP-SW             PIC X(02).                        
               88 APPLICANT         VALUE '00'.                                 
               88 SPOUSE            VALUE '10' '20'.                            
               88 FIANCEE           VALUE '09'.                                 
               88 CHILD             VALUE '41'.                                 
               88 CHILD-SPOUSE      VALUE '42'.                                 
               88 FATHER            VALUE '02'.                                 
               88 MOTHER            VALUE '12'.                                 
               88 FATHER-IN-LAW     VALUE '22'.                                 
               88 MOTHER-IN-LAW     VALUE '32'.                                 
               88 GRANDPARENT       VALUE '01' '11' '21' '31'.                  
               88 GRANDCHILD        VALUE '43'.                                 
               88 GCHILD-SPOUSE     VALUE '44'.                                 
                                                                                
           05  WS-MARITAL-STAT-SW             PIC X(01).                        
               88 SINGLE            VALUE '1' 'S'.                              
               88 MARRIED           VALUE '2' 'M'.                              
               88 DIV-WID-SEP       VALUE '1' '3' '4' '5' 'W' 'D' 'P'.          
                                                                                
           05  WS-CITIZEN-SW                  PIC X(02).                        
               88 SC                VALUE '10'.                                 
               88 SP                VALUE '20'.                                 
               88 SC-SP             VALUE '10' '20'.                            
                                                                                
           05  WS-CIT-CTRS.                                                     
               10  WS-SC-CTR                  PIC 9(2).                         
               10  WS-SP-CTR                  PIC 9(2).                         
               10  WS-SC-SP-CTR               PIC 9(2).                         
               10  WS-OC-CTR                  PIC 9(2).                         
                                                                                
       01  WS-TIER-ARR.                                                         
         03  WS-TIERS            OCCURS 8 TIMES.                                
           05 WS-TIER               OCCURS 15 TIMES.                            
               10 WS-TIER-CIT                 PIC X(02).                        
                                                                                
       01  WS-TIER-CTRS.                                                        
           05  WS-T01-CTR                     PIC 99.                           
           05  WS-T02-CTR                     PIC 99.                           
           05  WS-T03-CTR                     PIC 99.                           
           05  WS-T04-CTR                     PIC 99.                           
           05  WS-T05-CTR                     PIC 99.                           
           05  WS-T06-CTR                     PIC 99.                           
           05  WS-T07-CTR                     PIC 99.                           
           05  WS-T08-CTR                     PIC 99.                           
           05  WS-TIER-CTR                    PIC 99.                           
                                                                                
       01  WS-NUCLEUS-FLAGS.                                                    
           05  WS-WITH-FATHER                 PIC 9.                            
           05  WS-WITH-MOTHER                 PIC 9.                            
           05  WS-WITH-FATHER-IN-LAW          PIC 9.                            
           05  WS-WITH-MOTHER-IN-LAW          PIC 9.                            
           05  WS-WITH-SPOUSE                 PIC 9.                            
           05  WS-WITH-FIANCEE                PIC 9.                            
           05  WS-WITH-CHILD                  PIC 9.                            
           05  WS-WITH-CHILD-SPOUSE           PIC 9.                            
           05  WS-WITH-GRANDPARENT            PIC 9.                            
           05  WS-WITH-GRANDCHILD             PIC 9.                            
                                                                                
           05  WS-SINGLE-FATHER               PIC 9.                            
           05  WS-SINGLE-MOTHER               PIC 9.                            
           05  WS-SINGLE-FATHER-IN-LAW        PIC 9.                            
           05  WS-SINGLE-MOTHER-IN-LAW        PIC 9.                            
           05  WS-SINGLE-GRANDPARENT          PIC 9.                            
           05  WS-SINGLE-CHILD                PIC 9.                            
                                                                                
           05  WS-HA1-MARRIED                 PIC 9.                            
           05  WS-HA1-SINGLE                  PIC 9.                            
           05  WS-HA1-DIV-WID-SEP             PIC 9.                            
           05  WS-CHILD-DIV-WID-SEP           PIC 9.                            
                                                                                
           05  WS-WITH-SPOUSE-SC-SP           PIC 9.                            
           05  WS-FIANCEE-SC-SP               PIC 9.                            
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC              PIC 9(2).                         
                   15  WS-SYS-YY              PIC 9(2).                         
               10  WS-SYS-MM                  PIC 9(2).                         
               10  WS-SYS-DD                  PIC 9(2).                         
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-MM               PIC 9(2).                         
               10  FILLER                     PIC X(1)  VALUE '/'.              
               10  WS-EDITED-YY               PIC 9(4).                         
                                                                                
      *============================================================*            
      *         BP13L837  -  R E P O R T    L A Y O U T            *            
      *============================================================*            
                                                                                
       01  L837-HEADER-1A.                                                      
           05  FILLER              PIC X(08)     VALUE 'BP13L837'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(41)     VALUE                          
               'S Y S T E M    O F    C O M M I T M E N T'.                     
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  L837-RUN-DATE       PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L837-PAGE-NO        PIC ZZZZ9 .                                  
                                                                                
       01  L837-HEADER-1B.                                                      
           05  FILLER              PIC X(08)     VALUE 'P13L837B'.              
           05  FILLER              PIC X(04)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(25)     VALUE SPACES.                  
           05  FILLER              PIC X(41)     VALUE                          
               'S Y S T E M    O F    C O M M I T M E N T'.                     
           05  FILLER              PIC X(12)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'DATE :'.                
           05  L837-RUN-DATE2      PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(07)     VALUE 'PAGE : '.               
           05  L837-PAGE-NO2       PIC ZZZZ9 .                                  
                                                                                
       01  L837-HEADER-02.                                                      
           05  FILLER              PIC X(40)     VALUE SPACES.                  
           05  FILLER              PIC X(55)     VALUE                          
           'REPORT ON TAGGING OF MCP SCHEME FOR MULTI-TIER FAMILY'.             
                                                                                
       01  L837-HEADER-02B.                                                     
           05  FILLER              PIC X(40)     VALUE SPACES.                  
           05  FILLER              PIC X(55)     VALUE                          
           'REPORT ON NON-MCP CASES FOR MULTI-TIER FAMILY'.                     
                                                                                
       01  L837-HEADER-03.                                                      
           05  FILLER              PIC X(117)    VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'ELIG'.                  
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE 'MC'.                    
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'EST'.                   
                                                                                
       01  L837-HEADER-03B.                                                     
           05  FILLER              PIC X(05)     VALUE ' S/N '.                 
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(08)     VALUE 'REF NO'.                
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(09)     VALUE 'NRIC'.                  
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(45)     VALUE 'NAME'.                  
           05  FILLER              PIC X(04)     VALUE 'RELN'.                  
           05  FILLER              PIC X(03)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE 'RELN DESC'.             
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'CIT'.                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'MAR STAT'.              
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(04)     VALUE 'SCH '.                  
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(02)     VALUE 'PS'.                    
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  FILLER              PIC X(03)     VALUE 'FT '.                   
                                                                                
       01  L837-HEADER-04.                                                      
           05  FILLER              PIC X(130)    VALUE ALL '-'.                 
                                                                                
       01  L837-DETAIL.                                                         
           05  L837-SNO            PIC X(05).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L837-REFNO          PIC X(08).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L837-NRIC           PIC X(09).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L837-NAME           PIC X(45).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L837-RELN           PIC X(03).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L837-RELN-DESC      PIC X(20).                                   
           05  FILLER              PIC X(02)     VALUE SPACES.                  
           05  L837-CITIZEN        PIC X(03).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L837-MAR-STAT       PIC X(10).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L837-ELIG-SCH       PIC X(04).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L837-HA1-TAG        PIC X(02).                                   
           05  FILLER              PIC X(01)     VALUE SPACES.                  
           05  L837-EST-FT         PIC X(03).                                   
                                                                                
       01  WS-RPT-DTLS.                                                         
         03 WS-RPT-ARRAY           OCCURS 15 TIMES.                             
           05  WS-REFNO            PIC X(08).                                   
           05  WS-NRIC             PIC X(09).                                   
           05  WS-NAME             PIC X(48).                                   
           05  WS-RELN             PIC X(03).                                   
           05  WS-CITZ             PIC X(02).                                   
           05  WS-MAR-STAT         PIC X(10).                                   
           05  WS-HA-TAG           PIC X(01).                                   
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE      THRU 1000-EXIT.                       
           PERFORM 2000-READ-F730         THRU 2000-EXIT.                       
           PERFORM 2500-READ-F825         THRU 2500-EXIT.                       
                                                                                
           PERFORM 3000-PROCESS-F730                                            
              THRU 3000-EXIT                                                    
             UNTIL (WS-F730-EOF-SW = 'Y') OR                                    
                   (WS-F825-EOF-SW = 'Y').                                      
                                                                                
           PERFORM 9999-CLOSE-ROUTINE.                                          
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F730                                                 
                       BP13F825                                                 
                       BP13K060                                                 
                I-O    BP13K730                                                 
                OUTPUT BP13L837                                                 
                       P13L837B.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYSTEM-DATE.                 
                                                                                
           MOVE   WS-SYS-DD           TO WS-EDITED-DD.                          
           MOVE   WS-SYS-MM           TO WS-EDITED-MM.                          
           MOVE   WS-SYS-CCYY         TO WS-EDITED-YY.                          
                                                                                
           IF WS-K730-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K730 OPEN ERROR, STATUS IS ' WS-K730-STATUS          
              GO TO   9999-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K060 OPEN ERROR, STATUS IS ' WS-K060-STATUS          
              GO TO   9999-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           MOVE   WS-EDITED-DATE      TO L837-RUN-DATE.                         
           MOVE   WS-EDITED-DATE      TO L837-RUN-DATE2.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       2000-READ-F730.                                                          
      ****************************************************************          
                                                                                
           READ BP13F730                                                        
              AT END MOVE 'Y'   TO WS-F730-EOF-SW                               
                     GO TO 2000-EXIT.                                           
                                                                                
           ADD  +1                 TO WS-CNT-READ-F730.                         
           MOVE ZEROES             TO WS-NUCLEUS-FLAGS                          
                                      WS-TIER-CTRS                              
                                      WS-OCCP-CTR                               
                                      WS-CIT-CTRS.                              
           INITIALIZE                 WS-TIER-ARR.                              
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       2500-READ-F825.                                                          
      ****************************************************************          
                                                                                
           READ BP13F825                                                        
              AT END     MOVE 'Y'  TO WS-F825-EOF-SW                            
                         GO TO 2500-EXIT.                                       
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       3000-PROCESS-F730.                                                       
      ****************************************************************          
                                                                                
           IF F730-NUM-REGN = F825-NUM-REGN                                     
                                                                                
              PERFORM 4000-PROCESS-NUCLEUS  THRU 4000-EXIT                      
                UNTIL (F825-NUM-REGN  > F730-NUM-REGN) OR                       
                      (WS-F825-EOF-SW = 'Y'          ) OR                       
                      (WS-OCCP-CTR > 14)                                        
                                                                                
              PERFORM 5000-CHECK-OCCUPANTS  THRU 5000-EXIT                      
                                                                                
              IF WS-TIER-CTR >= 2                                               
                 MOVE 'Y'            TO WS-UPDATE-TAG                           
                 PERFORM 6000-UPDATE-BP13K730 THRU 6000-EXIT                    
              ELSE                                                              
                 MOVE 'N'            TO WS-UPDATE-TAG                           
                 PERFORM 6000-UPDATE-BP13K730 THRU 6000-EXIT                    
              END-IF                                                            
                                                                                
              PERFORM 7000-PRINT-RPT        THRU 7000-EXIT                      
                      VARYING WS-CTR FROM 1 BY 1                                
                      UNTIL WS-CTR > WS-OCCP-CTR                                
                                                                                
              PERFORM 2000-READ-F730        THRU 2000-EXIT                      
                                                                                
           ELSE                                                                 
           IF F730-NUM-REGN > F825-NUM-REGN                                     
              PERFORM 2500-READ-F825                                            
                 THRU 2500-EXIT                                                 
                UNTIL (F825-NUM-REGN  >= F730-NUM-REGN) OR                      
                      (WS-F825-EOF-SW  = 'Y'          )                         
           ELSE                                                                 
              PERFORM 2000-READ-F730      THRU 2000-EXIT                        
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ****************************************************************          
       4000-PROCESS-NUCLEUS.                                                    
      ****************************************************************          
                                                                                
           ADD                         1 TO WS-OCCP-CTR.                        
           MOVE F825-NUM-RELATIONSHIP    TO WS-RELATIONSHIP-SW.                 
           MOVE F825-NUM-MARITAL-STATUS  TO WS-MARITAL-STAT-SW.                 
           MOVE F825-NUM-CITIZENSHIP     TO WS-CITIZEN-SW.                      
                                                                                
           EVALUATE TRUE                                                        
           WHEN APPLICANT                                                       
              ADD  1            TO WS-T01-CTR  WS-T02-CTR                       
                                   WS-T04-CTR  WS-T05-CTR  WS-T06-CTR           
                                                                                
              MOVE F825-NUM-CITIZENSHIP    TO WS-TIER-CIT(1, WS-T01-CTR)        
              MOVE F825-NUM-CITIZENSHIP    TO WS-TIER-CIT(2, WS-T02-CTR)        
              MOVE F825-NUM-CITIZENSHIP    TO WS-TIER-CIT(4, WS-T04-CTR)        
              MOVE F825-NUM-CITIZENSHIP    TO WS-TIER-CIT(5, WS-T05-CTR)        
              MOVE F825-NUM-CITIZENSHIP    TO WS-TIER-CIT(6, WS-T06-CTR)        
                                                                                
           WHEN FATHER                                                          
              MOVE 1                       TO WS-WITH-FATHER                    
              ADD  1                       TO WS-T04-CTR                        
              MOVE F825-NUM-CITIZENSHIP    TO WS-TIER-CIT(4, WS-T04-CTR)        
                                                                                
           WHEN MOTHER                                                          
              MOVE 1                       TO WS-WITH-MOTHER                    
              ADD  1                       TO WS-T04-CTR                        
              MOVE F825-NUM-CITIZENSHIP    TO WS-TIER-CIT(4, WS-T04-CTR)        
                                                                                
           WHEN FATHER-IN-LAW                                                   
              MOVE 1                       TO WS-WITH-FATHER-IN-LAW             
              ADD  1                       TO WS-T05-CTR                        
              MOVE F825-NUM-CITIZENSHIP    TO WS-TIER-CIT(5, WS-T05-CTR)        
                                                                                
           WHEN MOTHER-IN-LAW                                                   
              MOVE 1                       TO WS-WITH-MOTHER-IN-LAW             
              ADD  1                       TO WS-T05-CTR                        
              MOVE F825-NUM-CITIZENSHIP    TO WS-TIER-CIT(5, WS-T05-CTR)        
                                                                                
           WHEN SPOUSE                                                          
              MOVE 1                       TO WS-WITH-SPOUSE                    
              ADD  1                       TO WS-T01-CTR                        
              MOVE F825-NUM-CITIZENSHIP    TO WS-TIER-CIT(1, WS-T01-CTR)        
                                                                                
           WHEN CHILD                                                           
              MOVE 1                       TO WS-WITH-CHILD                     
              ADD  1               TO WS-T01-CTR WS-T02-CTR WS-T03-CTR          
                                      WS-T07-CTR WS-T08-CTR                     
                                                                                
              MOVE F825-NUM-CITIZENSHIP    TO WS-TIER-CIT(1, WS-T01-CTR)        
              MOVE F825-NUM-CITIZENSHIP    TO WS-TIER-CIT(2, WS-T02-CTR)        
              MOVE F825-NUM-CITIZENSHIP    TO WS-TIER-CIT(3, WS-T03-CTR)        
              MOVE F825-NUM-CITIZENSHIP    TO WS-TIER-CIT(7, WS-T07-CTR)        
              MOVE F825-NUM-CITIZENSHIP    TO WS-TIER-CIT(8, WS-T08-CTR)        
                                                                                
           WHEN CHILD-SPOUSE                                                    
              MOVE 1                       TO WS-WITH-CHILD-SPOUSE              
              ADD  1                       TO WS-T07-CTR WS-T08-CTR             
                                                                                
              MOVE F825-NUM-CITIZENSHIP    TO WS-TIER-CIT(7, WS-T07-CTR)        
              MOVE F825-NUM-CITIZENSHIP    TO WS-TIER-CIT(8, WS-T08-CTR)        
                                                                                
           WHEN GRANDPARENT                                                     
              MOVE 1                       TO WS-WITH-GRANDPARENT               
              ADD  1                       TO WS-T06-CTR                        
              MOVE F825-NUM-CITIZENSHIP    TO WS-TIER-CIT(6, WS-T06-CTR)        
                                                                                
           WHEN GRANDCHILD                                                      
              MOVE 1                       TO WS-WITH-GRANDCHILD                
              ADD  1                       TO WS-T03-CTR WS-T08-CTR             
                                                                                
              MOVE F825-NUM-CITIZENSHIP    TO WS-TIER-CIT(3, WS-T03-CTR)        
              MOVE F825-NUM-CITIZENSHIP    TO WS-TIER-CIT(8, WS-T08-CTR)        
                                                                                
           END-EVALUATE.                                                        
                                                                                
           IF APPLICANT                                                         
              EVALUATE TRUE                                                     
              WHEN SINGLE                                                       
                   MOVE            1 TO WS-HA1-SINGLE                           
              WHEN MARRIED                                                      
                   MOVE            1 TO WS-HA1-MARRIED                          
              WHEN DIV-WID-SEP                                                  
                   MOVE            1 TO WS-HA1-DIV-WID-SEP                      
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
           IF FIANCEE AND (SC OR SP)                                            
              MOVE 1  TO WS-FIANCEE-SC-SP                                       
           END-IF.                                                              
                                                                                
           IF SINGLE                                                            
              EVALUATE TRUE                                                     
                 WHEN FATHER                                                    
                      MOVE 1 TO WS-SINGLE-FATHER                                
                 WHEN MOTHER                                                    
                      MOVE 1 TO WS-SINGLE-MOTHER                                
                 WHEN FATHER-IN-LAW                                             
                      MOVE 1 TO WS-SINGLE-FATHER-IN-LAW                         
                 WHEN MOTHER-IN-LAW                                             
                      MOVE 1 TO WS-SINGLE-MOTHER-IN-LAW                         
                 WHEN GRANDPARENT                                               
                      MOVE 1 TO WS-SINGLE-GRANDPARENT                           
                 WHEN CHILD                                                     
                      MOVE 1 TO WS-SINGLE-CHILD                                 
              END-EVALUATE                                                      
           END-IF.                                                              
                                                                                
           IF SPOUSE AND (SC OR SP)                                             
              MOVE 1  TO WS-WITH-SPOUSE-SC-SP                                   
           END-IF.                                                              
                                                                                
           IF CHILD AND DIV-WID-SEP                                             
              MOVE 1  TO WS-CHILD-DIV-WID-SEP                                   
           END-IF.                                                              
                                                                                
           IF SC                                                                
              ADD 1   TO WS-SC-CTR                                              
           ELSE                                                                 
           IF SP                                                                
              ADD 1   TO WS-SP-CTR                                              
           ELSE                                                                 
              ADD 1   TO WS-OC-CTR                                              
           END-IF.                                                              
                                                                                
           MOVE F825-NUM-REF             TO WS-REFNO   (WS-OCCP-CTR)            
           MOVE F825-NUM-NRIC            TO WS-NRIC    (WS-OCCP-CTR)            
           MOVE F825-NME-OCCP            TO WS-NAME    (WS-OCCP-CTR)            
           MOVE F825-NUM-RELATIONSHIP    TO WS-RELN    (WS-OCCP-CTR)            
           MOVE F825-NUM-CITIZENSHIP     TO WS-CITZ    (WS-OCCP-CTR)            
           MOVE F825-NUM-MARITAL-STATUS  TO WS-MAR-STAT(WS-OCCP-CTR)            
                                                                                
           IF F825-NUM-NRIC = F730-NUM-NRIC1 OR F730-NUM-NRIC2 OR               
                              F730-NUM-NRIC3 OR F730-NUM-NRIC4                  
              MOVE 'H'                   TO WS-HA-TAG  (WS-OCCP-CTR)            
           ELSE                                                                 
              MOVE 'O'                   TO WS-HA-TAG  (WS-OCCP-CTR)            
           END-IF.                                                              
                                                                                
           PERFORM 2500-READ-F825         THRU 2500-EXIT.                       
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       5000-CHECK-OCCUPANTS.                                                    
      ****************************************************************          
                                                                                
           IF WS-SC-CTR <= 1                                                    
              GO TO 5000-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF (WS-WITH-SPOUSE = 1)             AND                              
              (WS-WITH-CHILD = 1)                                               
              MOVE         1                   TO WS-T-CTR                      
              MOVE         WS-T01-CTR          TO WS-O-CTR                      
              PERFORM 5500-CHECK-TIER          THRU 5500-EXIT                   
           END-IF.                                                              
                                                                                
           IF (WS-HA1-DIV-WID-SEP = 1)         AND                              
              (WS-WITH-SPOUSE = 0)             AND                              
              (WS-WITH-CHILD = 1)                                               
              MOVE         2                   TO WS-T-CTR                      
              MOVE         WS-T02-CTR          TO WS-O-CTR                      
              PERFORM 5500-CHECK-TIER          THRU 5500-EXIT                   
           END-IF.                                                              
                                                                                
      * TO CATER FOR APPLN WITH MORE THAN 1 CHILD AND 1 CHILD IS SINGLE         
      * WHILE THE OTHER IS NOT.                                                 
           IF (WS-CHILD-DIV-WID-SEP = 1)       AND                              
              (WS-WITH-CHILD-SPOUSE = 0)       AND                              
              (WS-WITH-GRANDCHILD = 1)         AND                              
              (WS-SINGLE-CHILD = 0)                                             
              MOVE         3                   TO WS-T-CTR                      
              MOVE         WS-T03-CTR          TO WS-O-CTR                      
              PERFORM 5500-CHECK-TIER          THRU 5500-EXIT                   
           END-IF.                                                              
                                                                                
           IF ((WS-WITH-FATHER = 1) AND (WS-SINGLE-FATHER = 0)) OR              
              ((WS-WITH-MOTHER = 1) AND (WS-SINGLE-MOTHER = 0))                 
              MOVE         4                   TO WS-T-CTR                      
              MOVE         WS-T04-CTR          TO WS-O-CTR                      
              PERFORM 5500-CHECK-TIER          THRU 5500-EXIT                   
           END-IF.                                                              
                                                                                
           IF ((WS-WITH-FATHER-IN-LAW = 1) AND                                  
               (WS-SINGLE-FATHER-IN-LAW = 0))  OR                               
              ((WS-WITH-MOTHER-IN-LAW = 1) AND                                  
               (WS-SINGLE-MOTHER-IN-LAW = 0))                                   
              MOVE         5                   TO WS-T-CTR                      
              MOVE         WS-T05-CTR          TO WS-O-CTR                      
              PERFORM 5500-CHECK-TIER          THRU 5500-EXIT                   
           END-IF.                                                              
                                                                                
           IF (WS-WITH-GRANDPARENT = 1) AND (WS-SINGLE-GRANDPARENT = 0)         
              MOVE         6                   TO WS-T-CTR                      
              MOVE         WS-T06-CTR          TO WS-O-CTR                      
              PERFORM 5500-CHECK-TIER          THRU 5500-EXIT                   
           END-IF.                                                              
                                                                                
           IF (WS-WITH-CHILD = 1)              AND                              
              (WS-WITH-CHILD-SPOUSE = 1)       AND                              
              (WS-WITH-GRANDCHILD = 0)                                          
              MOVE         7                   TO WS-T-CTR                      
              MOVE         WS-T07-CTR          TO WS-O-CTR                      
              PERFORM 5500-CHECK-TIER          THRU 5500-EXIT                   
           END-IF.                                                              
                                                                                
           IF (WS-WITH-CHILD = 1)              AND                              
              (WS-WITH-CHILD-SPOUSE = 1)       AND                              
              (WS-WITH-GRANDCHILD = 1)         AND                              
              (WS-SINGLE-CHILD = 0)                                             
              MOVE         8                   TO WS-T-CTR                      
              MOVE         WS-T08-CTR          TO WS-O-CTR                      
              PERFORM 5500-CHECK-TIER          THRU 5500-EXIT                   
           END-IF.                                                              
                                                                                
           IF (WS-WITH-SPOUSE-SC-SP = 1)       AND                              
              (WS-WITH-CHILD = 0)                                               
              ADD 1                     TO WS-TIER-CTR                          
           END-IF.                                                              
                                                                                
           IF (WS-FIANCEE-SC-SP = 1)                                            
              ADD 1                     TO WS-TIER-CTR                          
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       5500-CHECK-TIER.                                                         
      *************************************************************             
                                                                                
           MOVE ZEROES        TO WS-CIT-CTRS.                                   
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1                                   
              UNTIL WS-CTR > WS-O-CTR                                           
                 MOVE WS-TIER-CIT(WS-T-CTR, WS-CTR)  TO WS-CITIZEN-SW           
                 EVALUATE TRUE                                                  
                 WHEN SC                                                        
                      ADD   1 TO WS-SC-CTR                                      
                 WHEN SP                                                        
                      ADD   1 TO WS-SP-CTR                                      
                 WHEN OTHER                                                     
                      ADD   1 TO WS-OC-CTR                                      
                 END-EVALUATE                                                   
           END-PERFORM.                                                         
                                                                                
           IF ((WS-SC-CTR >= 2) OR                                              
              ((WS-SC-CTR  = 1) AND (WS-SP-CTR >= 1)))                          
                ADD  1          TO WS-TIER-CTR                                  
           END-IF.                                                              
                                                                                
       5500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       6000-UPDATE-BP13K730.                                                    
      *************************************************************             
                                                                                
           MOVE F730-NUM-REGN          TO K730-NUM-REGN.                        
                                                                                
           READ BP13K730.                                                       
           EVALUATE WS-K730-STATUS                                              
           WHEN 00                                                              
                IF WS-UPDATE-TAG = 'Y'                                          
                   MOVE '1'               TO K730-CDE-RESIDENT-HA1              
                   PERFORM 6500-REWRITE-K730 THRU 6500-EXIT                     
                ELSE                                                            
                   IF WS-UPDATE-TAG = 'N'                                       
                      MOVE 'M'              TO K730-CDE-RESIDENT-HA1            
                      PERFORM 6500-REWRITE-K730 THRU 6500-EXIT                  
                   END-IF                                                       
                END-IF                                                          
           WHEN 23                                                              
                DISPLAY 'RECORD NOT FOUND IN BP13K730. KEY IS '                 
                         F730-NUM-REGN                                          
           WHEN OTHER                                                           
                DISPLAY 'ERROR READING BP13K730 STATUS IS '                     
                         WS-K730-STATUS                                         
           END-EVALUATE.                                                        
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       6500-REWRITE-K730.                                                       
      *************************************************************             
                                                                                
           REWRITE BP13K730-REC.                                                
                                                                                
           IF WS-K730-STATUS = ZEROS                                            
              ADD +1  TO WS-CNT-UPD-K730                                        
           ELSE                                                                 
              DISPLAY 'BP13K730 UPDATE ERROR, STATUS IS ' WS-K730-STATUS        
              DISPLAY 'BP13K730 RECORD KEY IS ' F730-NUM-REGN                   
           END-IF.                                                              
                                                                                
       6500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       7000-PRINT-RPT.                                                          
      *************************************************************             
           PERFORM 7500-MOVE-VALUES   THRU 7500-EXIT.                           
           IF WS-UPDATE-TAG = 'Y'                                               
              IF WS-L837-LINECTR >= 50                                          
                 ADD                    1 TO WS-L837-PAGECTR                    
                 MOVE WS-L837-PAGECTR     TO L837-PAGE-NO                       
                 WRITE BP13L837-REC     FROM L837-HEADER-1A AFTER PAGE          
                 WRITE BP13L837-REC     FROM L837-HEADER-02                     
                 WRITE BP13L837-REC     FROM L837-HEADER-03 AFTER 2             
                 WRITE BP13L837-REC     FROM L837-HEADER-03B                    
                 WRITE BP13L837-REC     FROM L837-HEADER-04                     
                 WRITE BP13L837-REC     FROM L837-DETAIL                        
                 MOVE                   7 TO WS-L837-LINECTR                    
              ELSE                                                              
                 ADD                    1 TO WS-L837-LINECTR                    
                 WRITE BP13L837-REC     FROM L837-DETAIL                        
              END-IF                                                            
           ELSE                                                                 
              IF WS-L837B-LINECTR >= 50                                         
                 ADD                    1 TO WS-L837B-PAGECTR                   
                 MOVE WS-L837B-PAGECTR    TO L837-PAGE-NO2                      
                 WRITE P13L837B-REC     FROM L837-HEADER-1B AFTER PAGE          
                 WRITE P13L837B-REC     FROM L837-HEADER-02B                    
                 WRITE P13L837B-REC     FROM L837-HEADER-03 AFTER 2             
                 WRITE P13L837B-REC     FROM L837-HEADER-03B                    
                 WRITE P13L837B-REC     FROM L837-HEADER-04                     
                 WRITE P13L837B-REC     FROM L837-DETAIL                        
                 MOVE                   7 TO WS-L837B-LINECTR                   
              ELSE                                                              
                 ADD                    1 TO WS-L837B-LINECTR                   
                 WRITE P13L837B-REC     FROM L837-DETAIL                        
              END-IF                                                            
           END-IF.                                                              
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *************************************************************             
       7500-MOVE-VALUES.                                                        
      *************************************************************             
                                                                                
           IF WS-UPDATE-TAG = 'Y'                                               
              IF WS-CTR = 1                                                     
                 ADD                       1 TO WS-L837-SNO                     
                 MOVE WS-L837-SNO            TO WS-L837-SNO-X                   
                 MOVE WS-L837-SNO-X          TO L837-SNO                        
                 MOVE F730-CDE-ELIG-SCH      TO L837-ELIG-SCH                   
                 MOVE K730-CDE-RESIDENT-HA1  TO L837-HA1-TAG                    
                 WRITE BP13L837-REC          FROM WS-BLANKS                     
                 ADD                       1 TO WS-L837-LINECTR                 
              ELSE                                                              
                 MOVE SPACES                 TO L837-ELIG-SCH                   
                                                L837-SNO                        
                 MOVE WS-HA-TAG(WS-CTR)      TO L837-HA1-TAG                    
              END-IF                                                            
           ELSE                                                                 
              IF WS-CTR = 1                                                     
                 ADD                       1 TO WS-L837B-SNO                    
                 MOVE WS-L837B-SNO           TO WS-L837-SNO-X                   
                 MOVE WS-L837-SNO-X          TO L837-SNO                        
                 MOVE F730-CDE-ELIG-SCH      TO L837-ELIG-SCH                   
                 MOVE ' '                   TO L837-HA1-TAG                     
                 WRITE P13L837B-REC          FROM WS-BLANKS                     
                 ADD                       1 TO WS-L837B-LINECTR                
              ELSE                                                              
                 MOVE SPACES                 TO L837-ELIG-SCH                   
                                                L837-SNO                        
                 MOVE WS-HA-TAG(WS-CTR)      TO L837-HA1-TAG                    
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-UPDATE-TAG = '1'                                               
              IF WS-CTR = 1                                                     
                 MOVE F730-CDE-NT1           TO L837-EST-FT                     
              ELSE                                                              
                 IF WS-CTR = 2                                                  
                    MOVE F730-CDE-FLAT-TYPE  TO L837-EST-FT                     
                 ELSE                                                           
                    MOVE SPACES              TO L837-EST-FT                     
                 END-IF                                                         
              END-IF                                                            
           ELSE                                                                 
              IF WS-CTR = 1                                                     
                 MOVE F730-CDE-NT1           TO L837-EST-FT                     
              ELSE                                                              
                 IF WS-CTR = 2                                                  
                    MOVE F730-CDE-FLAT-TYPE  TO L837-EST-FT                     
                 ELSE                                                           
                    MOVE SPACES              TO L837-EST-FT                     
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE WS-REFNO(WS-CTR)          TO L837-REFNO.                        
           MOVE WS-NRIC(WS-CTR)           TO L837-NRIC.                         
           MOVE WS-NAME(WS-CTR)           TO L837-NAME.                         
           MOVE WS-RELN(WS-CTR)           TO L837-RELN.                         
                                                                                
           MOVE SPACES                    TO BP13K060-REC.                      
           MOVE '35'                      TO K060-SERIAL-NO.                    
           MOVE WS-RELN(WS-CTR)           TO K060-CODE.                         
           PERFORM 8000-READ-K060         THRU 8000-EXIT.                       
           MOVE K060-DESC                 TO L837-RELN-DESC.                    
                                                                                
           MOVE SPACES                    TO BP13K060-REC.                      
           MOVE '41'                      TO K060-SERIAL-NO.                    
           MOVE WS-CITZ(WS-CTR)           TO K060-CODE.                         
           PERFORM 8000-READ-K060         THRU 8000-EXIT.                       
           MOVE K060-DESC                 TO L837-CITIZEN.                      
                                                                                
           MOVE SPACES                    TO BP13K060-REC.                      
           MOVE '21'                      TO K060-SERIAL-NO.                    
           MOVE WS-MAR-STAT(WS-CTR)       TO K060-CODE.                         
           PERFORM 8000-READ-K060         THRU 8000-EXIT.                       
           MOVE K060-DESC                 TO L837-MAR-STAT.                     
                                                                                
       7500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       8000-READ-K060.                                                          
      ****************************************************************          
           READ BP13K060 KEY IS K060-KEY-FLD.                                   
                                                                                
           IF WS-K060-STATUS = 0                                                
              CONTINUE                                                          
           ELSE                                                                 
           IF WS-K060-STATUS = 23                                               
              DISPLAY 'BP13K060 RECORD NOT FOUND, KEY IS ' K060-KEY-FLD         
              DISPLAY 'F730-KEY-FLD-' F730-KEY-FLD                              
           ELSE                                                                 
              DISPLAY 'BP13K060 READ ERROR, STATUS IS ' WS-K060-STATUS          
              DISPLAY 'K060-KEY-FLD-' K060-KEY-FLD                              
                    '& F730-KEY-FLD-' F730-KEY-FLD                              
           END-IF.                                                              
       8000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       9999-CLOSE-ROUTINE.                                                      
      ****************************************************************          
                                                                                
           CLOSE BP13F730                                                       
                 BP13F825                                                       
                 BP13K060                                                       
                 BP13K730                                                       
                 BP13L837                                                       
                 P13L837B.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : ' WS-EDITED-DATE.                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13C837 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13F730 RECORDS READ        = ' WS-CNT-READ-F730.          
           DISPLAY 'BP13L837 RECORDS WRITTEN     = ' WS-L837-SNO.               
           DISPLAY 'P13L837B RECORDS WRITTEN     = ' WS-L837B-SNO.              
           DISPLAY 'BP13K730 RECORDS UPDATED     = ' WS-CNT-UPD-K730.           
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
                                                                                
           STOP RUN.                                                            
                                                                                
