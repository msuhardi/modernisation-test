       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C42Q.                                                 
      *AUTHOR.        KVL1                                                      
      *DATE-WRITTEN.  25/09/2017.                                               
      * ========================================================== *            
      *                SYSTEM OF COMMITMENT (BP13)                 *            
      * ========================================================== *            
      *  OBJECTIVE : PROCESS AG07 VOUCHER RETURN FILE (AG07F640)   *            
      *              FOR EXCESS REFUND STATUS                      *            
      *                                                            *            
      *  INPUT   :  AG07F640 - AG07 INTERFACE FILE                 *            
      *             AG07K970 - AG07 REJECT CODES                   *            
      *                                                            *            
      *  I-O     :  BP13K48K - CONTRA/TEMP LOAN REQUEST FILE       *            
      *                                                            *            
      *  LISTING :  P13L42QA - REJECTED VOUCHERS LIST              *            
      *          :  P13L42QB - ERROR LIST                          *            
      *                                                            *            
      * ========================================================== *            
      * CHG-NO    BY    DATE    DESCRIPTION                        *            
      * BP136986  KVL1  250917  NEW PROGRAM                        *            
      * BP138831  KV8   170821  TO REPLACE LEE_LI_PENG TO          *            
      *                         BERNICE_JH_GAN                     *            
      * BP139175  KAC1  290424  REPL Bernice_JH_GAN@hdb.gov.sg WITH*            
      *                              Iris_LL_KOR@hdb.gov.sg        *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT AG07F640 ASSIGN        TO AG07F640.                           
                                                                                
           SELECT BP13K48K ASSIGN        TO BP13K48K                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K48K-KEY-FLD                        
                           FILE STATUS   IS WS-K48K-STATUS.                     
                                                                                
           SELECT AG07K970 ASSIGN        TO AG07K970                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K970-KEY-FLD                        
                           FILE STATUS   IS WS-K970-STATUS.                     
                                                                                
           SELECT P13L42QA ASSIGN        TO P13L42QA.                           
           SELECT P13L42QB ASSIGN        TO P13L42QB.                           
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  AG07F640                                                             
           RECORD CONTAINS 100 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
           COPY AG07F640.                                                       
                                                                                
       FD  BP13K48K                                                             
           RECORD CONTAINS 1200 CHARACTERS.                                     
           COPY BP13K48K.                                                       
                                                                                
       FD  AG07K970                                                             
           RECORD CONTAINS 40  CHARACTERS.                                      
           COPY AG07K970.                                                       
                                                                                
       FD  P13L42QA                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  P13L42QA-REC                  PIC X(132).                            
                                                                                
       FD  P13L42QB                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       01  P13L42QB-REC                  PIC X(132).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K48K-STATUS            PIC X(2)    VALUE SPACES.              
           05  WS-K970-STATUS            PIC X(2)    VALUE SPACES.              
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-S                      PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F640-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F640-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F640-ACC           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-F640-REJ           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K48K-UPD           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K48K-WRT           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-K48K-DEL           PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-SNO-A              PIC 9(4)   VALUE ZEROES.               
           05  WS-CNT-SNO-B              PIC 9(4)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-F640-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-DONE                   PIC X(1)   VALUE 'N'.                  
           05  WS-K48K-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-K970-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-REJECT-FL              PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY           PIC 9(4)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY           PIC 9(4)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE SPACES.               
                                                                                
       01  WS-OTHERS.                                                           
           05  WS-NUM-DDD-FILE-REF.                                             
               10  WS-NUM-REGN           PIC X(8)   VALUE SPACES.               
               10  FILLER                PIC X(1)   VALUE SPACES.               
               10  WS-NUM-TRANS          PIC X(3)   VALUE SPACES.               
               10  FILLER                PIC X(3)   VALUE SPACES.               
           05  WS-DTE-SUB                PIC X(8)   VALUE SPACES.               
                                                                                
      *-------------------------------------------------------------            
      * LAYOUT MAILFILE  -  DATASET FOR EMAIL                                   
      *-------------------------------------------------------------            
       01  MAIL-HDR1.                                                           
           05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB1'.                 
           05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
       01  MAIL-HDR2.                                                           
           05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
           05  MAIL-SENDID     PIC X(04)  VALUE 'OPCP'.                         
           05  FILLER          PIC X(09)  VALUE '@SGPHDB1>'.                    
           05  FILLER          PIC X(56)  VALUE SPACES.                         
                                                                                
      *  for testing purpose -------                                            
      *01  MAIL-HDR1.                                                           
      *    05  FILLER          PIC X(12)  VALUE 'HELO SGPHDB3'.                 
      *    05  FILLER          PIC X(68)  VALUE SPACES.                         
                                                                                
      *01  MAIL-HDR2.                                                           
      *    05  FILLER          PIC X(11)  VALUE 'MAIL FROM:<'.                  
      *    05  MAIL-SENDID     PIC X(07)  VALUE 'CS93627'.                      
      *    05  FILLER          PIC X(09)  VALUE '@SGPHDB3>'.                    
      *    05  FILLER          PIC X(53)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL1.                                                           
           05  FILLER          PIC X(09)  VALUE 'RCPT TO:<'.                    
      *    05  RCPT-ID         PIC X(11)  VALUE 'LEE_LI_PENG'.                  
           05  RCPT-ID         PIC X(14)  VALUE 'Iris_LL_KOR'.                  
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
                                                                                
       01  MAIL-DTL2.                                                           
           05  FILLER          PIC X(04)  VALUE 'DATA'.                         
           05  FILLER          PIC X(76)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL3.                                                           
           05  FILLER          PIC X(05)  VALUE 'FROM:'.                        
           05  MAIL-SENDMAILID PIC X(24)  VALUE                                 
               'SOC SYSTEM - EMAIL ALERT'.                                      
           05  FILLER          PIC X(49)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL4.                                                           
           05  FILLER          PIC X(04)  VALUE 'TO:<'.                         
      *    05  TO-MAIL-ID      PIC X(11)  VALUE 'LEE_LI_PENG'.                  
           05  TO-MAIL-ID      PIC X(14)  VALUE 'Iris_LL_KOR'.                  
           05  FILLER          PIC X(12)  VALUE '@HDB.GOV.SG>'.                 
           05  FILLER          PIC X(57)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL5.                                                           
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  TITLE1          PIC X(10)  VALUE 'SUMMARY - '.                   
           05  MAIL-SUBJECT    PIC X(52)  VALUE                                 
               'TEMP LOAN EXCESS REFUND VOUCHERS REJECTED BY ETLSVS'.           
                                                                                
       01  MAIL-DTL5A.                                                          
           05  FILLER          PIC X(10)  VALUE 'SUBJECT : '.                   
           05  TITLE1A         PIC X(10)  VALUE 'SUMMARY - '.                   
           05  MAIL-SUBJECTA   PIC X(52)  VALUE                                 
               'EXCEPTION REPORT ON TEMP LOAN EXCESS REFUND VOUCHERS'.          
                                                                                
       01  MAIL-DTL6.                                                           
           05  FILLER          PIC X(06)  VALUE 'DATE: '.                       
           05  MAIL-DATE-DTL6  PIC X(10)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL7.                                                           
           05  FILLER          PIC X(10)  VALUE 'BP13L42QA '.                   
           05  FILLER          PIC X(10)  VALUE 'HDBCAT 3  '.                   
           05  FILLER          PIC X(41)  VALUE                                 
               'S Y S T E M   O F   C O M M I T M E N T  '.                     
           05  FILLER          PIC X(06)  VALUE 'DATE: '.                       
           05  MAIL-DATE-DTL7  PIC X(10)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL7A.                                                          
           05  FILLER          PIC X(10)  VALUE 'BP13L42QB '.                   
           05  FILLER          PIC X(10)  VALUE 'HDBCAT 3  '.                   
           05  FILLER          PIC X(41)  VALUE                                 
               'S Y S T E M   O F   C O M M I T M E N T  '.                     
           05  FILLER          PIC X(06)  VALUE 'DATE: '.                       
           05  MAIL-DATE-DTL7A PIC X(10)  VALUE SPACES.                         
                                                                                
       01  MAIL-DTL8.                                                           
           05  FILLER          PIC X(10)  VALUE SPACES.                         
           05  FILLER          PIC X(52)  VALUE                                 
               'TEMP LOAN EXCESS REFUND VOUCHERS REJECTED BY ETLSVS'.           
                                                                                
       01  MAIL-DTL8A.                                                          
           05  FILLER          PIC X(10)  VALUE SPACES.                         
           05  FILLER          PIC X(52)  VALUE                                 
               'EXCEPTION REPORT ON TEMP LOAN EXCESS REFUND VOUCHERS'.          
                                                                                
       01  MAIL-BODY02.                                                         
           05 FILLER           PIC X(02)  VALUE SPACES.                         
           05 FILLER           PIC X(04)  VALUE 'S/N '.                         
           05 FILLER           PIC X(05)  VALUE SPACES.                         
           05 FILLER           PIC X(08)  VALUE ' Regn No'.                     
           05 FILLER           PIC X(05)  VALUE SPACES.                         
           05 FILLER           PIC X(10)  VALUE 'Voucher No'.                   
           05 FILLER           PIC X(07)  VALUE SPACES.                         
           05 FILLER           PIC X(11)  VALUE 'Submit Date'.                  
           05 FILLER           PIC X(05)  VALUE SPACES.                         
           05 FILLER           PIC X(13)  VALUE 'Reject Reason'.                
                                                                                
       01  MAIL-BODY02A.                                                        
           05 FILLER           PIC X(02)  VALUE SPACES.                         
           05 FILLER           PIC X(04)  VALUE 'S/N '.                         
           05 FILLER           PIC X(05)  VALUE SPACES.                         
           05 FILLER           PIC X(08)  VALUE ' Regn No'.                     
           05 FILLER           PIC X(05)  VALUE SPACES.                         
           05 FILLER           PIC X(10)  VALUE 'Voucher No'.                   
           05 FILLER           PIC X(07)  VALUE SPACES.                         
           05 FILLER           PIC X(17)  VALUE 'Error Description'.            
                                                                                
       01  MAIL-BODY03.                                                         
           05 FILLER           PIC X(02)  VALUE SPACES.                         
           05  L42QA-SNO       PIC ZZZ9.                                        
           05  FILLER          PIC X(06)  VALUE SPACES.                         
           05  L42QA-NUM-REGN  PIC X(08)  VALUE SPACES.                         
           05  FILLER          PIC X(04)  VALUE SPACES.                         
           05  L42QA-NUM-VR    PIC X(13)  VALUE SPACES.                         
           05  FILLER          PIC X(05)  VALUE SPACES.                         
           05  L42QA-DTE-SUB   PIC X(10)  VALUE SPACES.                         
           05  FILLER          PIC X(05)  VALUE SPACES.                         
           05  L42QA-NUM-DESC  PIC X(41)  VALUE SPACES.                         
                                                                                
                                                                                
       01  MAIL-BODY03A.                                                        
           05 FILLER           PIC X(02)  VALUE SPACES.                         
           05 L42QB-SNO        PIC ZZZ9.                                        
           05 FILLER           PIC X(06)  VALUE SPACES.                         
           05 L42QB-NUM-REGN   PIC X(08)  VALUE SPACES.                         
           05 FILLER           PIC X(04)  VALUE SPACES.                         
           05 L42QB-NUM-VR     PIC X(13)  VALUE SPACES.                         
           05 FILLER           PIC X(04)  VALUE SPACES.                         
           05 L42QB-NUM-DESC   PIC X(50)  VALUE SPACES.                         
                                                                                
       01  MAIL-SPACES.                                                         
           05 FILLER           PIC X(80)  VALUE SPACES.                         
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 2000-AG07F640-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-F640-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  AG07F640                                                 
                       AG07K970                                                 
                I-O    BP13K48K                                                 
                OUTPUT P13L42QA                                                 
                       P13L42QB.                                                
                                                                                
           IF WS-K48K-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K48K ERROR ' WS-K48K-STATUS                  
              MOVE     WS-K48K-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           IF WS-K970-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13K970 ERROR ' WS-K970-STATUS                  
              MOVE     WS-K970-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
                                                                                
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
           MOVE WS-DTE-CURR-10             TO   MAIL-DATE-DTL6                  
                                                MAIL-DATE-DTL7                  
                                                MAIL-DATE-DTL7A.                
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-AG07F640-READ.                                                      
      *=============================================================            
                                                                                
           READ AG07F640 AT END                                                 
                MOVE 'Y'                   TO   WS-F640-EOF                     
                GO                         TO   2000-EXIT.                      
                                                                                
           ADD 1                           TO   WS-CNT-F640-READ.               
           MOVE F640-NUM-DDD-FILE-REF      TO   WS-NUM-DDD-FILE-REF.            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   L42QB-NUM-DESC.                 
                                                                                
           MOVE 'N'                        TO   WS-K48K-FND                     
                                                WS-REJECT-FL.                   
                                                                                
           MOVE SPACES                     TO   BP13K48K-REC.                   
           MOVE WS-NUM-REGN                TO   K48K-NUM-REGN.                  
           PERFORM 3100-READ-BP13K48K      THRU 3100-EXIT.                      
                                                                                
           IF WS-K48K-FND = 'Y'                                                 
              EVALUATE F640-NUM-VR                                              
                 WHEN K48K-EXC-REF-REFUND-VCHR1                                 
                    MOVE 1 TO WS-S                                              
                    IF K48K-EXC-REF-STAT-VCHR1 NOT = SPACES AND                 
                                                     LOW-VALUES                 
                       MOVE 'Y' TO WS-DONE                                      
                    ELSE                                                        
                       MOVE 'N' TO WS-DONE                                      
                    END-IF                                                      
                 WHEN K48K-EXC-REF-REFUND-VCHR2                                 
                    MOVE 2 TO WS-S                                              
                    IF K48K-EXC-REF-STAT-VCHR2 NOT = SPACES AND                 
                                                     LOW-VALUES                 
                       MOVE 'Y' TO WS-DONE                                      
                    ELSE                                                        
                       MOVE 'N' TO WS-DONE                                      
                    END-IF                                                      
                 WHEN K48K-EXC-REF-REFUND-VCHR3                                 
                    MOVE 3 TO WS-S                                              
                    IF K48K-EXC-REF-STAT-VCHR3 NOT = SPACES AND                 
                                                     LOW-VALUES                 
                       MOVE 'Y' TO WS-DONE                                      
                    ELSE                                                        
                       MOVE 'N' TO WS-DONE                                      
                    END-IF                                                      
                 WHEN OTHER                                                     
                    ADD 1  TO WS-CNT-F640-SKIP                                  
                    MOVE 'VOUCHER NUMBER NOT FOUND'                             
                           TO L42QB-NUM-DESC                                    
                    PERFORM 8900-PRINT-ERROR                                    
                           THRU 8900-EXIT                                       
                    GO TO 3000-PROC-NEXT-REC                                    
              END-EVALUATE                                                      
              IF WS-DONE = 'Y'                                                  
                 ADD 1                  TO   WS-CNT-F640-SKIP                   
                 MOVE 'VOUCHER ALREADY PROCESSED'                               
                                        TO   L42QB-NUM-DESC                     
                 PERFORM 8900-PRINT-ERROR                                       
                                        THRU 8900-EXIT                          
                 GO TO 3000-PROC-NEXT-REC                                       
              END-IF                                                            
                                                                                
              PERFORM 4000-UPD-STATUS   THRU 4000-EXIT                          
              IF WS-REJECT-FL = 'Y'                                             
                 PERFORM 5000-GET-REJ-REASON                                    
                                        THRU 5000-EXIT                          
                 PERFORM 5100-PRINT-REJ                                         
                                        THRU 5100-EXIT                          
              END-IF                                                            
                                                                                
           ELSE                                                                 
              ADD 1                        TO   WS-CNT-F640-SKIP                
              MOVE 'REGN NUMBER NOT FOUND IN BP13K48K'                          
                                           TO   L42QB-NUM-DESC                  
              PERFORM 8900-PRINT-ERROR                                          
                                         THRU   8900-EXIT                       
           END-IF.                                                              
                                                                                
       3000-PROC-NEXT-REC.                                                      
                                                                                
           PERFORM 2000-AG07F640-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-READ-BP13K48K.                                                      
      *=============================================================            
                                                                                
            READ BP13K48K.                                                      
                                                                                
            EVALUATE WS-K48K-STATUS                                             
                WHEN 00                                                         
                     MOVE 'Y'                    TO WS-K48K-FND                 
                WHEN 23                                                         
                     MOVE 'N'                    TO WS-K48K-FND                 
                WHEN OTHER                                                      
                     DISPLAY 'ERROR IN BP13K48K : ' WS-K48K-STATUS              
                             '  KEY IS : ' WS-NUM-REGN                          
                     MOVE     WS-K48K-STATUS     TO RETURN-CODE                 
                     PERFORM 9000-CLOSE-ROUTINE  THRU 9000-EXIT                 
            END-EVALUATE.                                                       
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4000-UPD-STATUS.                                                         
      *=============================================================            
                                                                                
           EVALUATE F640-NUM-REJECT                                             
              WHEN ZEROES                                                       
              WHEN SPACES                                                       
              WHEN LOW-VALUES                                                   
                 ADD 1                  TO   WS-CNT-F640-ACC                    
                 EVALUATE WS-S                                                  
                    WHEN 1                                                      
                       MOVE 'S'         TO   K48K-EXC-REF-STAT-VCHR1            
                       MOVE F640-NUM-BATCH                                      
                                        TO   K48K-EXC-REF-REFUND-BTCH1          
                       MOVE WS-DTE-CURR-8                                       
                                        TO   K48K-EXC-REF-DTE-BTCH1             
                    WHEN 2                                                      
                       MOVE 'S'         TO   K48K-EXC-REF-STAT-VCHR2            
                       MOVE F640-NUM-BATCH                                      
                                        TO   K48K-EXC-REF-REFUND-BTCH2          
                       MOVE WS-DTE-CURR-8                                       
                                        TO   K48K-EXC-REF-DTE-BTCH2             
                    WHEN 3                                                      
                       MOVE 'S'         TO   K48K-EXC-REF-STAT-VCHR3            
                       MOVE F640-NUM-BATCH                                      
                                        TO   K48K-EXC-REF-REFUND-BTCH3          
                       MOVE WS-DTE-CURR-8                                       
                                        TO   K48K-EXC-REF-DTE-BTCH3             
                 END-EVALUATE                                                   
              WHEN OTHER                                                        
                 ADD   1                TO   WS-CNT-F640-REJ                    
                 MOVE 'Y'               TO   WS-REJECT-FL                       
                 EVALUATE WS-S                                                  
                    WHEN 1                                                      
                       MOVE SPACES      TO   K48K-EXC-REF-DTE-VCHR1             
                                             K48K-EXC-REF-REFUND-VCHR1          
                       MOVE 0           TO   K48K-EXC-REF-AMT-VCHR1             
                    WHEN 2                                                      
                       MOVE SPACES      TO   K48K-EXC-REF-DTE-VCHR2             
                                             K48K-EXC-REF-REFUND-VCHR2          
                       MOVE 0           TO   K48K-EXC-REF-AMT-VCHR2             
                    WHEN 3                                                      
                       MOVE SPACES      TO   K48K-EXC-REF-DTE-VCHR3             
                                             K48K-EXC-REF-REFUND-VCHR3          
                       MOVE 0           TO   K48K-EXC-REF-AMT-VCHR3             
                 END-EVALUATE                                                   
                 SUBTRACT 1             FROM WS-S                               
                 MOVE WS-S              TO   K48K-EXC-REF-CSH-VCH-NO            
           END-EVALUATE                                                         
                                                                                
           PERFORM 4010-REWRITE-BP13K48K   THRU 4010-EXIT.                      
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       4010-REWRITE-BP13K48K.                                                   
      *=============================================================            
                                                                                
           REWRITE BP13K48K-REC.                                                
                                                                                
           EVALUATE WS-K48K-STATUS                                              
           WHEN 00                                                              
              ADD 1                        TO   WS-CNT-K48K-UPD                 
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'REWRITE BP13K48K ERROR ' WS-K48K-STATUS                  
              MOVE     WS-K48K-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       4010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5000-GET-REJ-REASON.                                                     
      *=============================================================            
                                                                                
           MOVE F640-NUM-REJECT            TO   K970-NUM-CODE.                  
           PERFORM 5010-AG07K970-READ      THRU 5010-EXIT.                      
                                                                                
           IF WS-K970-FND = 'Y'                                                 
              STRING K970-NUM-CODE '-' K970-TXT-EMSG                            
                     DELIMITED BY SIZE     INTO L42QA-NUM-DESC                  
           ELSE                                                                 
              MOVE F640-NUM-REJECT         TO   L42QA-NUM-DESC                  
           END-IF.                                                              
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5010-AG07K970-READ.                                                      
      *=============================================================            
                                                                                
           READ AG07K970.                                                       
                                                                                
           EVALUATE WS-K970-STATUS                                              
           WHEN 00                                                              
              MOVE 'Y'                     TO   WS-K970-FND                     
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'N'                     TO   WS-K970-FND                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READ AG07K970 ERROR '    WS-K970-STATUS                  
              MOVE     WS-K970-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       5010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5100-PRINT-REJ.                                                          
      *=============================================================            
                                                                                
           IF WS-CNT-SNO-A  = 0                                                 
              PERFORM 5110-PRINT-REJ-HDR   THRU 5110-EXIT                       
           END-IF.                                                              
                                                                                
           ADD  1                          TO   WS-CNT-SNO-A.                   
           MOVE WS-CNT-SNO-A               TO   L42QA-SNO.                      
           MOVE WS-NUM-REGN                TO   L42QA-NUM-REGN.                 
           MOVE F640-NUM-VR                TO   L42QA-NUM-VR.                   
           EVALUATE WS-S                                                        
              WHEN 1                                                            
                 MOVE K48K-EXC-REF-DTE-VCHR1                                    
                                           TO   L42QA-DTE-SUB                   
              WHEN 2                                                            
                 MOVE K48K-EXC-REF-DTE-VCHR2                                    
                                           TO   L42QA-DTE-SUB                   
              WHEN OTHER                                                        
                 MOVE K48K-EXC-REF-DTE-VCHR3                                    
                                           TO   L42QA-DTE-SUB                   
           END-EVALUATE.                                                        
                                                                                
           WRITE P13L42QA-REC              FROM MAIL-BODY03.                    
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       5110-PRINT-REJ-HDR.                                                      
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   P13L42QA-REC.                   
           WRITE P13L42QA-REC              FROM MAIL-HDR1.                      
           WRITE P13L42QA-REC              FROM MAIL-HDR2.                      
           WRITE P13L42QA-REC              FROM MAIL-DTL1.                      
           WRITE P13L42QA-REC              FROM MAIL-DTL2.                      
           WRITE P13L42QA-REC              FROM MAIL-DTL3.                      
           WRITE P13L42QA-REC              FROM MAIL-DTL4.                      
           WRITE P13L42QA-REC              FROM MAIL-DTL5.                      
           WRITE P13L42QA-REC              FROM MAIL-DTL6.                      
           WRITE P13L42QA-REC              FROM MAIL-DTL7.                      
           WRITE P13L42QA-REC              FROM MAIL-DTL8.                      
           WRITE P13L42QA-REC              FROM MAIL-SPACES.                    
           WRITE P13L42QA-REC              FROM MAIL-BODY02.                    
           WRITE P13L42QA-REC              FROM MAIL-SPACES.                    
                                                                                
       5110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8900-PRINT-ERROR.                                                        
      *=============================================================            
                                                                                
           IF WS-CNT-SNO-B = 0                                                  
              PERFORM 8910-PRINT-ERROR-HDR THRU 8910-EXIT                       
           END-IF.                                                              
                                                                                
           ADD  1                          TO   WS-CNT-SNO-B.                   
           MOVE WS-CNT-SNO-B               TO   L42QB-SNO.                      
           MOVE WS-NUM-REGN                TO   L42QB-NUM-REGN.                 
           MOVE F640-NUM-VR                TO   L42QB-NUM-VR.                   
                                                                                
           WRITE P13L42QB-REC             FROM MAIL-BODY03A.                    
                                                                                
       8900-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       8910-PRINT-ERROR-HDR.                                                    
      *=============================================================            
                                                                                
           MOVE SPACES                     TO   P13L42QB-REC.                   
           WRITE P13L42QB-REC              FROM MAIL-HDR1.                      
           WRITE P13L42QB-REC              FROM MAIL-HDR2.                      
           WRITE P13L42QB-REC              FROM MAIL-DTL1.                      
           WRITE P13L42QB-REC              FROM MAIL-DTL2.                      
           WRITE P13L42QB-REC              FROM MAIL-DTL3.                      
           WRITE P13L42QB-REC              FROM MAIL-DTL4.                      
           WRITE P13L42QB-REC              FROM MAIL-DTL5A.                     
           WRITE P13L42QB-REC              FROM MAIL-DTL6.                      
           WRITE P13L42QB-REC              FROM MAIL-DTL7A.                     
           WRITE P13L42QB-REC              FROM MAIL-DTL8A.                     
           WRITE P13L42QB-REC              FROM MAIL-SPACES.                    
           WRITE P13L42QB-REC              FROM MAIL-BODY02A.                   
           WRITE P13L42QB-REC              FROM MAIL-SPACES.                    
                                                                                
       8910-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           DISPLAY ' '.                                                         
           DISPLAY '*------- BP13C42Q CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF AG07F640 READ      : ' WS-CNT-F640-READ.          
           DISPLAY ' 2. NO OF AG07F640 ACCEPTED  : ' WS-CNT-F640-ACC.           
           DISPLAY ' 3. NO OF AG07F640 REJECTED  : ' WS-CNT-F640-REJ.           
           DISPLAY ' 4. NO OF AG07F640 SKIPPED   : ' WS-CNT-F640-SKIP.          
           DISPLAY ' 5. NO OF BP13K48K UPDATED   : ' WS-CNT-K48K-UPD.           
           DISPLAY ' '.                                                         
                                                                                
           CLOSE AG07F640                                                       
                 AG07K970                                                       
                 BP13K48K                                                       
                 P13L42QA                                                       
                 P13L42QB.                                                      
                                                                                
           IF WS-K48K-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K48K ERROR ' WS-K48K-STATUS                  
              MOVE     WS-K48K-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           IF WS-K970-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13K970 ERROR ' WS-K970-STATUS                  
              MOVE     WS-K970-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
