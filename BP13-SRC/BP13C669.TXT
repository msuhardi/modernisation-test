       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C669.                                                 
      *AUTHOR.        MANU.                                                     
      *DATE-WRITTEN.  11/06/1997.                                               
      * =========================================================== *           
      *  SYSTEM OF COMMITMENT (SOC)                                 *           
      * =========================================================== *           
      *  OBJECTIVE :                                                *           
      *    1. PROGRAM TO READ SUBLET FILE, DEDUCT THE SUBLET PERIOD *           
      *       FROM OCCUPTION PERIOD,AFTER MATCHING  BP13F558  WITH  *           
      *       AB03K300                                              *           
      *                                                             *           
      *  FILES :                                                    *           
      *  INPUT  - 1. BP13F558  (BP13.F558.TRANS)                    *           
      *                                                             *           
      *  OUTPUT - 1. P13F558   (BP13.F558.TRANS)                    *           
      *                                                             *           
      *  LISTING : - 1. P13L669                                     *           
      *                                                             *           
      * ----------------------------------------------------------- *           
      * CHG-NO   DATE     BY   DESCRIPTION                          *           
      * -------- -------- ---  -----------                          *           
      * BP130323 11061997 MMS  NEW PROGRAM                          *           
      * BP130377 17091997 LSB  SUBLET-END-DATE = 0000, TAKE SYS-DATE*           
      * BP130445 05021998 SSS  EXPAND F558 RECL FROM 150 TO 400     *           
      * BP130445 12021998 LSB  INCLUDE SUBLET DUE TO TRANSFER HIST  *           
      * BP130554 27101998 LSB  SUBLET EXEMPTION                     *           
      * BP130623 11021999 LSB  CHECK  EXEMPTION-CODE FOR LOW-VALUES *           
      * BP130645 11021999 SRA  UPDATE SUBLET-EXM-SMS,EXM-RSL & EXM- *           
      *                        TRF BY MOVING K300-CDE-SUBLET-EXEMPT *           
      * BP130623 05051999 LSB  HDBREF SMS=RSL,COMPUTE SUBLET FOR RSL*           
      * BP130819 22052000 SRA  USE HDB-REF(1:09), MATCH NRIC'S.     *           
      * BP130857 07072000 SSS  REMOVE 'TEAM'                        *           
      * BP130897 03082000 AKT  EXPAND F558 RECL FROM 400 TO 600     *           
      * BP132589 13092004 SK   INCLUDE BP13C998,BR12C935            *           
      * BP132590 27102004 JF4  WRITE EXEMPT CODE IF TOTAL SUBLET    *           
      *                        PERIOD > ZERO                        *           
      * BP132590 13012005 JF4  CHK FOR NON-NUMERIC VALUE IN         *           
      *                        B935-QTY-TOT-SUBLET-MTH              *           
      * BP134111 27/06/11 ESD1 CATER FOR OPENING AND CLOSING FILE   *           
      *                        FOR BR12C935.                        *           
      * =========================================================== *           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
                                                                                
           SELECT P13F558 ASSIGN TO P13F558.                                    
                                                                                
           SELECT P13L669 ASSIGN TO P13L669.                                    
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F558                                                             
                              BLOCK CONTAINS 0 RECORDS                          
                              RECORD CONTAINS 600 CHARACTERS                    
                              RECORDING MODE IS F.                              
       COPY BP13F558.                                                           
                                                                                
       FD   P13F558           BLOCK CONTAINS 0 RECORDS                          
                              RECORD CONTAINS 600 CHARACTERS                    
                              RECORDING MODE IS F.                              
       01   P13F558-REC       PIC X(600).                                       
                                                                                
       FD   P13L669           BLOCK CONTAINS 0 RECORDS                          
                              RECORD CONTAINS 132 CHARACTERS                    
                              RECORDING MODE IS F.                              
       01   P13L669-REC       PIC X(132).                                       
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  L669-TOP-TITLE1.                                                     
           05  FILLER                  PIC X(07) VALUE 'P13L669'.               
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE 'HDBCAT3'.               
           05  FILLER                  PIC X(18) VALUE SPACES.                  
           05  FILLER                  PIC X(43) VALUE                          
                 'S Y T E M    O F   C O M M I T M E N T'.                      
           05  FILLER                  PIC X(12) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE 'DATE : '.               
           05  L669-DATE               PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE 'PAGE : '.               
           05  WS-EDITED-PAGENO        PIC Z(04).                               
                                                                                
       01  L669-TOP-TITLE2.                                                     
           05  FILLER                  PIC X(20) VALUE SPACES.                  
           05  FILLER                  PIC X(52) VALUE                          
             'REPORT ON SOC FOUND IN SMS/ RESALE/ TRANSFER  WITH '.             
           05  FILLER                  PIC X(20) VALUE                          
              'SUBLET INFORMATION  '.                                           
                                                                                
       01  L669-HEADER-LINE.                                                    
           05  FILLER                  PIC X(04) VALUE ' S/N'.                  
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'REGN NO'.               
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'NRIC NO'.               
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  FILLER                  PIC X(13) VALUE 'HDB-REF-SMS'.           
           05  FILLER                  PIC X(13) VALUE 'HDB-REF-RSL'.           
           05  FILLER                  PIC X(13) VALUE 'HDB-REF-TRF'.           
           05  FILLER                  PIC X(7)  VALUE 'EFF-DTE'.               
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'END-DTE'.               
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE 'SUBLET-SMS'.            
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE 'SUBLET-RSL'.            
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE 'SUBLET-TRF'.            
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'TOTAL'.                 
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(12) VALUE 'EXEMPTION'.             
           05  FILLER                  PIC X(02) VALUE SPACES.                  
                                                                                
       01  L669-DET-LINE.                                                       
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  WS-EDITED-SERNO         PIC Z(3) BLANK WHEN ZEROS.               
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  REGN-NO                 PIC X(8).                                
           05  FILLER                  PIC X(01) VALUE SPACES.                  
           05  NRIC-NO                 PIC X(9).                                
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  HDB-SMS                 PIC X(11).                               
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  HDB-RSL                 PIC X(11).                               
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  HDB-TRF                 PIC X(11).                               
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  DTE-EFF                 PIC X(6).                                
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  DTE-END                 PIC X(6).                                
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  SUB-SMS                 PIC 9(3).                                
           05  FILLER                  PIC X(08) VALUE SPACES.                  
           05  SUB-RSL                 PIC 9(3).                                
           05  FILLER                  PIC X(08) VALUE SPACES.                  
           05  SUB-TRF                 PIC 9(3).                                
           05  FILLER                  PIC X(08) VALUE SPACES.                  
           05  SUB-ACCUM               PIC 9(3).                                
           05  FILLER                  PIC X(05) VALUE SPACES.                  
           05  SUB-EXEMP               PIC X(2).                                
           05  FILLER                  PIC X(10) VALUE SPACES.                  
                                                                                
       01  L669-BLANK-LINE             PIC X(130) VALUE SPACES.                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-VALID-STAT-CNTR      PIC 99    VALUE ZEROS.                   
           05  WS-EOF-F558             PIC X     VALUE 'N'.                     
           05  WS-EOF-K300             PIC X     VALUE 'N'.                     
           05  WS-CHGE-FLAG            PIC X     VALUE 'N'.                     
           05  WS-READ-F558            PIC 9(6)  VALUE ZEROES.                  
           05  WS-WRITE-F558           PIC 9(6)  VALUE ZEROES.                  
           05  WS-WRITE-REPORT         PIC 9(6)  VALUE ZEROES.                  
           05  WS-READ-K300            PIC 9(6)  VALUE ZEROES.                  
           05  WS-K300-NOTFND          PIC 9(6)  VALUE ZEROES.                  
           05  WS-CNT-SMS              PIC 9(6)  VALUE ZEROES.                  
           05  WS-CNT-RSL              PIC 9(6)  VALUE ZEROES.                  
           05  WS-CNT-TRF              PIC 9(6)  VALUE ZEROES.                  
           05  WS-PAGENO               PIC 9(4)  VALUE ZEROES.                  
           05  WS-LINE-CNT             PIC 9(2)  VALUE 90.                      
           05  WS-SERNO                PIC 9(3)  VALUE ZEROES.                  
           05  WS-CTR                  PIC 9(2)  VALUE ZEROES.                  
           05  WS-CTR1                 PIC 9(2)  VALUE ZEROES.                  
                                                                                
       01  WS-NUM-HDB-REF              PIC X(09) VALUE SPACES.                  
       01  WS-PRINT-HDB-REF            PIC X(11) VALUE SPACES.                  
       01  WS-HDB-REF                  PIC X(11) VALUE SPACES.                  
                                                                                
       01  WS-END-DATE.                                                         
           05 WS-END-CC                PIC XX VALUE SPACES.                     
           05 WS-END-YY                PIC XX VALUE SPACES.                     
           05 WS-END-MM                PIC XX VALUE SPACES.                     
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05 WS-DTE-YY                PIC XX VALUE SPACES.                     
           05 WS-DTE-MM                PIC XX VALUE SPACES.                     
           05 WS-DTE-DD                PIC XX   VALUE SPACES.                   
                                                                                
       01  WS-DTE-CC                   PIC XX   VALUE SPACES.                   
       01  WS-CURR-DATE                PIC X(8) VALUE SPACES.                   
                                                                                
       01 WS-FOR-DATE-CALCULATION.                                              
          05 WS-SUBLET-SMS             PIC 9(3) VALUE ZEROES.                   
          05 WS-SUBLET-RSL             PIC 9(3) VALUE ZEROES.                   
          05 WS-SUBLET-TRF             PIC 9(3) VALUE ZEROES.                   
          05 WS-EXEMP                  PIC 9(3) VALUE ZEROES.                   
          05 WS-MONTH-TOT              PIC 9(3) VALUE ZEROES.                   
          05 WS-ACCUM                  PIC 9(3) VALUE ZEROES.                   
          05 WS-MONTH                  PIC 9(3) VALUE ZEROES.                   
          05 WS-MONTH-ACCUM            PIC 9(3) VALUE ZEROES.                   
          05 WS-PRINT-TAG              PIC X(1) VALUE SPACES.                   
          05 WS-REGN-NO                PIC X(8) VALUE SPACES.                   
          05 WS-CHK-SUBLET-SMS         PIC 9(3) VALUE ZEROES.                   
          05 WS-CHK-SUBLET-RSL         PIC 9(3) VALUE ZEROES.                   
          05 WS-CHK-SUBLET-TRF         PIC 9(3) VALUE ZEROES.                   
          05 WS-DTE-END.                                                        
             10 WS-CCYY-END            PIC 9(4).                                
             10 WS-MM-END              PIC 9(2).                                
          05 WS-DTE-EFF.                                                        
             10 WS-CCYY-EFF            PIC 9(4).                                
             10 WS-MM-EFF              PIC 9(2).                                
                                                                                
           05  WS-SUBLET-EXEMPT        PIC X(2)  VALUE SPACES.                  
                                                                                
      *------------------------*                                                
      *  LINKAGE FOR BP13C998                                                   
      *------------------------*                                                
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-NRIC           PIC X(09).                             
                                                                                
       COPY P13COM91.                                                           
       COPY BR12935C.                                                           
                                                                                
      *================================================================*        
       PROCEDURE DIVISION.                                                      
      *================================================================*        
                                                                                
       0000-SUMMARY.                                                            
            PERFORM 1000-OPEN-FILES     THRU 1000-EXIT.                         
            PERFORM 2000-READ-BP13F558  THRU 2000-EXIT.                         
            PERFORM 3000-PROCESS-DETAIL THRU 3000-EXIT                          
                                        UNTIL WS-EOF-F558 = 'Y'.                
            PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT.                         
            STOP RUN.                                                           
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       1000-OPEN-FILES.                                                         
      *================================================================*        
           OPEN  INPUT  BP13F558                                                
                 OUTPUT P13F558                                                 
                        P13L669.                                                
                                                                                
           ACCEPT WS-CURRENT-DATE    FROM DATE.                                 
                                                                                
           IF WS-CURRENT-DATE(1:2)  > 90                                        
              MOVE '19' TO WS-DTE-CC                                            
           ELSE                                                                 
              MOVE '20' TO WS-DTE-CC                                            
           END-IF.                                                              
                                                                                
            STRING WS-DTE-CC                                                    
                   WS-DTE-YY                                                    
                   WS-DTE-MM                                                    
                   WS-DTE-DD                                                    
                   DELIMITED BY SIZE                                            
                   INTO WS-CURR-DATE                                            
            END-STRING.                                                         
                                                                                
            STRING WS-DTE-DD '/'                                                
                   WS-DTE-MM '/'                                                
                   WS-DTE-CC                                                    
                   WS-DTE-YY DELIMITED BY SIZE INTO L669-DATE.                  
            MOVE WS-DTE-CC TO WS-END-CC.                                        
            MOVE WS-DTE-YY TO WS-END-YY.                                        
            MOVE WS-DTE-MM TO WS-END-MM.                                        
                                                                                
            PERFORM 4600-OPEN-BR12C935  THRU  4600-EXIT.                        
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
      *================================================================*        
       2000-READ-BP13F558.                                                      
      *================================================================*        
           MOVE SPACES   TO BP13F558-REC.                                       
           MOVE SPACES   TO L669-DET-LINE.                                      
                                                                                
           READ BP13F558 AT END                                                 
                MOVE 'Y' TO WS-EOF-F558                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD    1   TO WS-READ-F558.                                          
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3000-PROCESS-DETAIL.                                                     
      *================================================================*        
           MOVE 'Y'                     TO WS-CHGE-FLAG.                        
           MOVE ZEROES                  TO WS-SUBLET-SMS                        
                                           WS-SUBLET-RSL                        
                                           WS-SUBLET-TRF                        
                                           WS-MONTH-TOT WS-ACCUM.               
           MOVE SPACES                  TO WS-PRINT-TAG                         
                                           L669-DET-LINE                        
                                           WS-PRINT-HDB-REF.                    
                                                                                
           IF (F558-NUM-NRIC NOT = SPACES AND LOW-VALUES) AND                   
              ((F558-NUM-HDB-REF-SMS NOT = LOW-VALUES AND SPACES) OR            
               (F558-NUM-HDB-REF-RSL NOT = LOW-VALUES AND SPACES) OR            
               (F558-NUM-HDB-REF-TR  NOT = LOW-VALUES AND SPACES))              
              MOVE F558-NUM-NRIC  TO WS-LINK-NUM-NRIC                           
              CALL 'BP13C998' USING WS-LINK-REC, BP13COM91-REC                  
           END-IF.                                                              
                                                                                
           IF F558-CDE-FILE-STATUS-SMS NOT = 'OM'                               
            IF F558-NUM-HDB-REF-SMS NOT = LOW-VALUES AND SPACES                 
               MOVE 'S'                  TO WS-PRINT-TAG                        
               MOVE SPACES               TO WS-B935-AREA                        
               MOVE F558-NUM-HDB-REF-SMS TO B935-NUM-HDB-REF                    
                                            WS-PRINT-HDB-REF                    
                                            WS-HDB-REF                          
               PERFORM 3500-GET-MTH-SUBLET-EXEM-CDE                             
                                         THRU 3500-EXIT                         
               IF COM91-ERR-CODE = '0000'                                       
                  PERFORM 3600-GET-EX-ROLE-DETAIL THRU 3600-EXIT                
               END-IF                                                           
               MOVE WS-MONTH-TOT       TO WS-SUBLET-SMS                         
               MOVE WS-SUBLET-EXEMPT   TO F558-NUM-SUBLET-EXM-SMS               
               PERFORM 3700-WRITE-OUT  THRU 3700-EXIT                           
            END-IF                                                              
           END-IF.                                                              
                                                                                
           IF F558-CDE-FILE-STATUS-RSL NOT = 'OM'                               
            IF F558-NUM-HDB-REF-RSL NOT = SPACES AND LOW-VALUES                 
               MOVE 'R'                  TO WS-PRINT-TAG                        
               MOVE SPACES               TO WS-B935-AREA                        
                                            HDB-SMS HDB-TRF                     
               MOVE ZEROS                TO WS-ACCUM WS-MONTH-TOT               
                                            SUB-SMS SUB-TRF                     
               MOVE F558-NUM-HDB-REF-RSL TO B935-NUM-HDB-REF                    
                                            WS-PRINT-HDB-REF                    
                                            WS-HDB-REF                          
               PERFORM 3500-GET-MTH-SUBLET-EXEM-CDE                             
                                         THRU 3500-EXIT                         
               IF COM91-ERR-CODE = '0000'                                       
                  PERFORM 3600-GET-EX-ROLE-DETAIL  THRU 3600-EXIT               
               END-IF                                                           
               MOVE WS-MONTH-TOT       TO WS-SUBLET-RSL                         
               MOVE WS-SUBLET-EXEMPT   TO F558-NUM-SUBLET-EXM-RSL               
               PERFORM 3700-WRITE-OUT  THRU 3700-EXIT                           
            END-IF                                                              
           END-IF.                                                              
                                                                                
           IF F558-NUM-HDB-REF-TR NOT = SPACES AND LOW-VALUES                   
              MOVE 'T'                   TO WS-PRINT-TAG                        
               MOVE SPACES               TO WS-B935-AREA                        
                                            HDB-SMS HDB-RSL                     
               MOVE ZEROS                TO WS-ACCUM WS-MONTH-TOT               
                                            SUB-SMS SUB-RSL                     
               MOVE F558-NUM-HDB-REF-TR  TO B935-NUM-HDB-REF                    
                                            WS-PRINT-HDB-REF                    
                                            WS-HDB-REF                          
               PERFORM 3500-GET-MTH-SUBLET-EXEM-CDE                             
                                         THRU 3500-EXIT                         
               IF COM91-ERR-CODE = '0000'                                       
                  PERFORM 3600-GET-EX-ROLE-DETAIL  THRU 3600-EXIT               
               END-IF                                                           
               MOVE WS-MONTH-TOT       TO WS-SUBLET-TRF                         
               MOVE WS-SUBLET-EXEMPT   TO F558-NUM-SUBLET-EXM-TRF               
               PERFORM 3700-WRITE-OUT  THRU 3700-EXIT                           
           END-IF.                                                              
                                                                                
           PERFORM 5000-WRITE-BP13F558 THRU 5000-EXIT.                          
           PERFORM 2000-READ-BP13F558  THRU 2000-EXIT.                          
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3500-GET-MTH-SUBLET-EXEM-CDE.                                            
      *================================================================*        
      *IF THE SCHEME ACC-NO IS 11 BYTE THEN ONLY CALLING BR12C935               
                                                                                
           IF B935-NUM-HDB-REF(10:2) = SPACES OR LOW-VALUES                     
              GO TO 3500-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF F558-DTE-ISSUE-REGN NOT = SPACES AND LOW-VALUES                   
              MOVE F558-DTE-ISSUE-REGN      TO B935-DTE-INPUT                   
           ELSE                                                                 
              MOVE WS-CURR-DATE             TO B935-DTE-INPUT                   
           END-IF.                                                              
                                                                                
           PERFORM 4500-CALL-BR12C935 THRU 4500-EXIT                            
                                                                                
           IF B935-QTY-EFF-NON-OCPTN-MTH NOT NUMERIC                            
              MOVE ZEROS TO B935-QTY-EFF-NON-OCPTN-MTH                          
           END-IF.                                                              
                                                                                
           ADD  B935-QTY-EFF-NON-OCPTN-MTH TO WS-MONTH-TOT                      
           ADD  B935-QTY-EFF-NON-OCPTN-MTH TO WS-ACCUM                          
                                                                                
           MOVE SPACES   TO WS-SUBLET-EXEMPT.                                   
           IF B935-QTY-TOT-SUBLET-MTH NOT NUMERIC                               
              MOVE ZEROES    TO B935-QTY-TOT-SUBLET-MTH                         
           END-IF.                                                              
                                                                                
           IF B935-QTY-TOT-SUBLET-MTH > 0                                       
              PERFORM VARYING WS-CTR FROM 1 BY 1                                
                UNTIL WS-CTR > 20                                               
                IF (B935-NUM-SUBLET-EXEMPT(WS-CTR) NOT = SPACES     AND         
                                                         LOW-VALUES AND         
                                                         'ZZ')      AND         
                   (WS-SUBLET-EXEMPT = SPACES)                                  
                   MOVE B935-NUM-SUBLET-EXEMPT(WS-CTR)                          
                                              TO WS-SUBLET-EXEMPT               
                END-IF                                                          
                IF (B935-NUM-CASE-NO(WS-CTR) NOT = SPACES                       
                                                   AND LOW-VALUES)              
                   MOVE B935-DTE-SUBLET-EFF(WS-CTR)  TO WS-DTE-EFF              
                END-IF                                                          
              END-PERFORM                                                       
           END-IF.                                                              
                                                                                
           MOVE B935-DTE-INPUT             TO WS-DTE-END.                       
                                                                                
       3500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3600-GET-EX-ROLE-DETAIL.                                                 
      *================================================================*        
                                                                                
           PERFORM VARYING WS-CTR1 FROM 1 BY 1 UNTIL WS-CTR1 > 6                
             IF (COM91-SCH-ACCT(WS-CTR1)                                        
                             NOT = SPACES AND LOW-VALUES) AND                   
                (COM91-NUM-ROLE(WS-CTR1) = '023' OR '024')                      
                IF (WS-HDB-REF(1:9) = COM91-SCH-ACCT(WS-CTR1)(1:9)) AND         
                   (WS-HDB-REF NOT  = COM91-SCH-ACCT(WS-CTR1))                  
                   MOVE SPACES TO WS-B935-AREA                                  
                   MOVE COM91-SCH-ACCT(WS-CTR1) TO B935-NUM-HDB-REF             
                                                   WS-PRINT-HDB-REF             
                   PERFORM 3500-GET-MTH-SUBLET-EXEM-CDE  THRU 3500-EXIT         
                END-IF                                                          
             END-IF                                                             
           END-PERFORM.                                                         
                                                                                
       3600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       3700-WRITE-OUT.                                                          
      *================================================================*        
                                                                                
           IF B935-QTY-TOT-SUBLET-MTH NOT NUMERIC                               
              MOVE ZEROES    TO B935-QTY-TOT-SUBLET-MTH                         
           END-IF.                                                              
                                                                                
           IF B935-QTY-TOT-SUBLET-MTH > 0                                       
              ADD 1 TO WS-WRITE-REPORT                                          
              PERFORM 4110-PRINT-REPORT   THRU 4110-EXIT                        
           END-IF.                                                              
                                                                                
       3700-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4110-PRINT-REPORT.                                                       
      *================================================================*        
            IF WS-LINE-CNT > 55                                                 
               PERFORM 4120-HEADING  THRU 4120-EXIT                             
            END-IF.                                                             
                                                                                
            IF WS-CHGE-FLAG = 'Y'                                               
               MOVE  F558-NUM-REGN   TO REGN-NO                                 
               MOVE  F558-NUM-NRIC   TO NRIC-NO                                 
               MOVE 'N'              TO WS-CHGE-FLAG                            
               ADD   01              TO WS-SERNO                                
               MOVE WS-SERNO         TO WS-EDITED-SERNO                         
            ELSE                                                                
               MOVE SPACES           TO REGN-NO NRIC-NO                         
               MOVE ZEROS            TO WS-EDITED-SERNO                         
            END-IF.                                                             
                                                                                
            IF WS-PRINT-TAG = 'S'                                               
                MOVE WS-PRINT-HDB-REF TO HDB-SMS                                
                MOVE WS-MONTH-TOT     TO SUB-SMS                                
            ELSE                                                                
            IF WS-PRINT-TAG = 'R'                                               
                MOVE WS-PRINT-HDB-REF TO HDB-RSL                                
                MOVE WS-MONTH-TOT     TO SUB-RSL                                
            ELSE                                                                
            IF WS-PRINT-TAG = 'T'                                               
                MOVE WS-PRINT-HDB-REF TO HDB-TRF                                
                MOVE WS-MONTH-TOT     TO SUB-TRF                                
            END-IF.                                                             
                                                                                
            MOVE WS-DTE-EFF             TO DTE-EFF.                             
            MOVE WS-DTE-END             TO DTE-END.                             
            MOVE WS-ACCUM               TO SUB-ACCUM.                           
            MOVE WS-SUBLET-EXEMPT       TO SUB-EXEMP.                           
            WRITE P13L669-REC FROM L669-DET-LINE.                               
            ADD  01                     TO WS-LINE-CNT.                         
                                                                                
       4110-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       4120-HEADING.                                                            
      *================================================================*        
            ADD      01        TO WS-PAGENO.                                    
            MOVE WS-PAGENO     TO WS-EDITED-PAGENO.                             
            MOVE     01        TO WS-LINE-CNT.                                  
            WRITE  P13L669-REC FROM L669-TOP-TITLE1 AFTER PAGE.                 
            WRITE  P13L669-REC FROM L669-TOP-TITLE2 AFTER 2.                    
            WRITE  P13L669-REC FROM L669-HEADER-LINE AFTER 2.                   
            WRITE  P13L669-REC FROM L669-BLANK-LINE.                            
            ADD      06        TO WS-LINE-CNT.                                  
                                                                                
       4120-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4500-CALL-BR12C935.                                                      
      *================================================================*        
                                                                                
           CALL 'BR12C935' USING  WS-B935-AREA.                                 
           IF B935-NUM-ERROR NOT = '00000'                                      
              DISPLAY 'ERROR CALLING BR12C935. REF - ' B935-NUM-HDB-REF         
           END-IF.                                                              
                                                                                
       4500-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4600-OPEN-BR12C935.                                                      
      *================================================================*        
                                                                                
           MOVE 'O' TO B935-CDE-ACTION                                          
                                                                                
           CALL 'BR12C935' USING  WS-B935-AREA.                                 
           IF B935-NUM-ERROR NOT = '00000'                                      
              DISPLAY 'OPEN ERROR BR12C935 ERROR CDE - '                        
                       B935-NUM-SYSERR ' REF.- '    B935-NUM-HDB-REF            
           END-IF.                                                              
                                                                                
       4600-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       4700-CLOSE-BR12C935.                                                     
      *================================================================*        
                                                                                
           MOVE 'C' TO B935-CDE-ACTION                                          
                                                                                
           CALL 'BR12C935' USING  WS-B935-AREA.                                 
           IF B935-NUM-ERROR NOT = '00000'                                      
              DISPLAY 'CLOSE ERROR BR12C935 ERROR CDE - '                       
                       B935-NUM-SYSERR ' REF.- '    B935-NUM-HDB-REF            
           END-IF.                                                              
                                                                                
       4700-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *================================================================*        
       5000-WRITE-BP13F558.                                                     
      *================================================================*        
            MOVE ZEROES        TO F558-NUM-SUBLET-SMS.                          
            MOVE ZEROES        TO F558-NUM-SUBLET-RSL.                          
                                                                                
            IF WS-REGN-NO = F558-NUM-REGN                                       
               IF WS-SUBLET-SMS NOT = ZEROES                                    
                  IF WS-SUBLET-SMS > WS-CHK-SUBLET-SMS                          
                     MOVE WS-SUBLET-SMS     TO F558-NUM-SUBLET-SMS              
                     ADD 1 TO WS-CNT-SMS                                        
                  ELSE                                                          
                     MOVE WS-CHK-SUBLET-SMS TO F558-NUM-SUBLET-SMS              
                     ADD 1 TO WS-CNT-SMS                                        
                  END-IF                                                        
               END-IF                                                           
               IF WS-SUBLET-RSL NOT = ZEROES                                    
                  IF WS-SUBLET-RSL > WS-CHK-SUBLET-RSL                          
                     MOVE WS-SUBLET-RSL     TO F558-NUM-SUBLET-RSL              
                     ADD 1 TO WS-CNT-RSL                                        
                  ELSE                                                          
                     MOVE WS-CHK-SUBLET-RSL TO F558-NUM-SUBLET-RSL              
                     ADD 1 TO WS-CNT-RSL                                        
                  END-IF                                                        
               END-IF                                                           
               IF WS-SUBLET-TRF NOT = ZEROES                                    
                  IF WS-SUBLET-TRF > WS-CHK-SUBLET-TRF                          
                     MOVE WS-SUBLET-TRF     TO F558-NUM-SUBLET-TR               
                     ADD 1 TO WS-CNT-TRF                                        
                  ELSE                                                          
                     MOVE WS-CHK-SUBLET-TRF TO F558-NUM-SUBLET-TR               
                     ADD 1 TO WS-CNT-TRF                                        
                  END-IF                                                        
               END-IF                                                           
            ELSE                                                                
               IF WS-SUBLET-SMS NOT = ZEROES                                    
                  MOVE WS-SUBLET-SMS TO F558-NUM-SUBLET-SMS                     
                  ADD 1 TO WS-CNT-SMS                                           
               END-IF                                                           
               IF WS-SUBLET-RSL NOT = ZEROES                                    
                  MOVE WS-SUBLET-RSL TO F558-NUM-SUBLET-RSL                     
                  ADD 1 TO WS-CNT-RSL                                           
               END-IF                                                           
               IF WS-SUBLET-TRF NOT = ZEROES                                    
                  MOVE WS-SUBLET-TRF TO F558-NUM-SUBLET-TR                      
                  ADD 1 TO WS-CNT-TRF                                           
               END-IF                                                           
            END-IF.                                                             
                                                                                
            WRITE  P13F558-REC FROM BP13F558-REC.                               
            ADD 1 TO WS-WRITE-F558.                                             
            MOVE F558-NUM-REGN TO WS-REGN-NO.                                   
            MOVE F558-NUM-SUBLET-SMS TO WS-CHK-SUBLET-SMS.                      
            MOVE F558-NUM-SUBLET-RSL TO WS-CHK-SUBLET-RSL.                      
            MOVE F558-NUM-SUBLET-TR  TO WS-CHK-SUBLET-TRF.                      
                                                                                
       5000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *================================================================*        
       9000-CLOSE-FILES.                                                        
      *================================================================*        
           PERFORM 4700-CLOSE-BR12C935  THRU  4700-EXIT                         
                                                                                
           CLOSE BP13F558                                                       
                 P13F558                                                        
                 P13L669.                                                       
                                                                                
           DISPLAY '-- PROGRAM : BP13C669 ---'.                                 
           DISPLAY ' '.                                                         
           DISPLAY 'NO OF RECS READ FROM BP13F558 --> ' WS-READ-F558.           
           DISPLAY 'NO OF RECS WRITE  TO P13F558  --> ' WS-WRITE-F558.          
           DISPLAY 'NO OF RECS WRITE  TO REPORT   --> ' WS-WRITE-REPORT.        
           DISPLAY 'NO OF SUBLET DUE TO SMS REC   --> ' WS-CNT-SMS.             
           DISPLAY 'NO OF SUBLET DUE TO RSL REC   --> ' WS-CNT-RSL.             
           DISPLAY 'NO OF SUBLET DUE TO TRF REC   --> ' WS-CNT-TRF.             
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
