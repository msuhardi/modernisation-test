       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C27B.                                                 
      *AUTHOR.        LMS.                                                      
      *DATE-WRITTEN.  07/12/05                                                  
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE   :  PROGRAM TO OUTPUT                           *            
      *                                                            *            
      *                1)  SELECT BM06F800 THAT MATCH THE ZONE/NT, *            
      *                    ALLOCATION DATE AND FLAT-TYPE OF THE    *            
      *                    CONTROL RECORD.                         *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHGE NO  OIC   DATE     DESCRIPTION                        *            
      * -------- ----  ------   -----------                        *            
      * BP132785 LMS 20051205   NEW PROGRAM.                       *            
      * BP133349 LMS 20080618   CATER FOR SER 2-ROOM NEW FLATS.    *            
      * BP134045 ESA120101025   CATER FOR NE NPL                   *            
      * BP134731 PCL420121112   TO CATER FOR MODL-DESIGN 89        *            
      * BP135838 ESA120150701   TO CATER FOR MODL-DESIGN 92        *            
      * BP136002 ESA120151109   TO CATER FOR 2-ROOM FLEXI          *            
      * BP136991 ESA120170919   TO CATER FOR MODL-DESIGN = '98'    *            
      * ========================================================== *            
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F205  ASSIGN        TO  BP13F205.                         
                                                                                
           SELECT BM06F800  ASSIGN        TO  BM06F800.                         
                                                                                
           SELECT M06F800A  ASSIGN        TO  M06F800A.                         
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD   BP13F205                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 80  CHARACTERS.                                     
       COPY BP13F205.                                                           
                                                                                
       FD   BM06F800                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 800 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BM06F800.                                                           
                                                                                
       FD   M06F800A                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 800 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   BM06F800A-REC         PIC X(800).                                   
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-EOF-F205                PIC X       VALUE 'N'.                
           05  WS-EOF-F800                PIC X       VALUE 'N'.                
                                                                                
       01  WS-COUNTERS-POINTERS.                                                
           05  WS-READ-CNT-F800           PIC 9(5)    VALUE ZEROS.              
           05  WS-READ-TOTAL-CNT          PIC 9(5)    VALUE ZEROS.              
           05  WS-WRITE-CNT-F800          PIC 9(5)    VALUE ZEROS.              
                                                                                
       01  WS-F800-NUM-FLAT-TYPE          PIC X(2)    VALUE SPACES.             
                                                                                
       01  WS-DATE                        PIC X(8)    VALUE SPACES.             
                                                                                
       01  WS-TIME                        PIC X(8)    VALUE SPACES.             
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-ROUTINE.                                                        
      *-------------------------------------------------------------            
           PERFORM  100-OPEN-FILES        THRU   100-EXIT.                      
           PERFORM  200-READ-BP13F205     THRU   200-EXIT                       
                        UNTIL WS-EOF-F205 = 'Y'.                                
           PERFORM  9999-CLOSE-ROUTINE     THRU   9999-EXIT.                    
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-OPEN-FILES.                                                          
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F205                                                 
                OUTPUT M06F800A.                                                
                                                                                
           MOVE ZEROS                       TO WS-COUNTERS-POINTERS.            
           MOVE FUNCTION CURRENT-DATE (1:8) TO WS-DATE.                         
           MOVE FUNCTION CURRENT-DATE (9:8) TO WS-TIME.                         
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-BP13F205.                                                       
      *-------------------------------------------------------------            
           READ BP13F205                                                        
                AT END MOVE 'Y'         TO     WS-EOF-F205                      
                       GO TO 200-EXIT.                                          
                                                                                
           ADD     1                    TO     WS-READ-TOTAL-CNT.               
                                                                                
              PERFORM 300-PROCESS-RECORD THRU  300-EXIT.                        
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-PROCESS-RECORD.                                                      
      *-------------------------------------------------------------            
           OPEN    INPUT BM06F800.                                              
           PERFORM 320-READ-BM06F800      THRU   320-EXIT.                      
           MOVE    'N'                    TO  WS-EOF-F800.                      
           PERFORM 400-CHECK-NT-ZONE-ALLOCDTE THRU 400-EXIT                     
                       UNTIL WS-EOF-F800  = 'Y'.                                
                                                                                
           CLOSE   BM06F800.                                                    
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       320-READ-BM06F800.                                                       
      *-------------------------------------------------------------            
           READ BM06F800                                                        
                AT END MOVE 'Y'  TO  WS-EOF-F800                                
                       GO TO 320-EXIT.                                          
                                                                                
           ADD 1 TO WS-READ-CNT-F800.                                           
                                                                                
           PERFORM 350-DECODE-FLAT-TYPE                                         
              THRU 350-EXIT.                                                    
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       350-DECODE-FLAT-TYPE.                                                    
      *-------------------------------------------------------------            
           EVALUATE F800-CDE-TYP-ACTUSE                                         
           WHEN '01'                                                            
                 IF F800-CDE-MODL-DESG  = '80' OR '89' OR '92'                  
                    MOVE '1A'       TO WS-F800-NUM-FLAT-TYPE                    
                 ELSE                                                           
                    MOVE '1 '       TO WS-F800-NUM-FLAT-TYPE                    
                 END-IF                                                         
           WHEN '02'                                                            
                 IF F800-NUM-DESIGN-OFFER = 'F' OR                              
                    F800-CDE-MODL-DESG  = '97' OR '98'                          
                    MOVE '2 '       TO WS-F800-NUM-FLAT-TYPE                    
                    GO TO 350-EXIT                                              
                 END-IF                                                         
                 IF F800-CDE-MODL-DESG  = '80' OR '89' OR '92'                  
                    MOVE '2A'       TO WS-F800-NUM-FLAT-TYPE                    
                 ELSE                                                           
                    MOVE '2 '       TO WS-F800-NUM-FLAT-TYPE                    
                 END-IF                                                         
           WHEN '03'                                                            
                 MOVE '3 '       TO WS-F800-NUM-FLAT-TYPE                       
           WHEN '04'                                                            
                 MOVE '4 '       TO WS-F800-NUM-FLAT-TYPE                       
           WHEN '05'                                                            
                 MOVE '5 '       TO WS-F800-NUM-FLAT-TYPE                       
           WHEN '06'                                                            
           WHEN '08'                                                            
                 MOVE 'E '       TO WS-F800-NUM-FLAT-TYPE                       
           WHEN OTHER                                                           
                 MOVE '  '       TO WS-F800-NUM-FLAT-TYPE                       
           END-EVALUATE.                                                        
                                                                                
       350-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       400-CHECK-NT-ZONE-ALLOCDTE.                                              
      *-------------------------------------------------------------            
                                                                                
           IF ((F205-NUM-NT-ZONE        = F800-CDE-NT)           OR             
               (F205-NUM-NT-ZONE (1:2)  = F800-CDE-ZONE))        AND            
               (F205-DTE-ALLOCN         = F800-DTE-ALLOCN)       AND            
              ((F205-NUM-FLAT-TYPE      = WS-F800-NUM-FLAT-TYPE) OR             
              ((F205-NUM-FLAT-TYPE      = 'SE')                  AND            
               (WS-F800-NUM-FLAT-TYPE   = '3 ' OR '4 ' OR '5 ' OR               
                                          '2 ' OR '1A' OR '2A')) OR             
              ((F205-NUM-NT-ZONE (1:2)  = 'NN' OR 'EE' OR 'WW' OR 'NE')         
                AND                                                             
               (WS-F800-NUM-FLAT-TYPE   = '1A' OR '2A' OR '2 ' OR               
                                          '3 '))                 OR             
              ((F205-NUM-NT-ZONE (1:2)  = '3R')                  AND            
               (WS-F800-NUM-FLAT-TYPE   = '1A' OR '2A' OR '2 ' OR               
                                          '3 '))                 OR             
              ((F800-NUM-COMBINE-SA     = 'Y')                   AND            
               (WS-F800-NUM-FLAT-TYPE   = '1A' OR '2A')))                       
                PERFORM 410-WRITE-BM06F800 THRU 410-EXIT                        
           ELSE                                                                 
              IF (F205-NUM-NT-ZONE   = F800-NUM-BTO-OPTION)   AND               
                 (F205-DTE-ALLOCN    = F800-DTE-ALLOCN)                         
                  PERFORM 410-WRITE-BM06F800 THRU 410-EXIT                      
              ELSE                                                              
                 IF (F205-NUM-NT-ZONE   = F800-CDE-ZONE)   AND                  
                    (F205-DTE-ALLOCN    = F800-DTE-ALLOCN) AND                  
                    (F205-NUM-FLAT-TYPE = '00')                                 
                     PERFORM 410-WRITE-BM06F800 THRU 410-EXIT                   
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM    320-READ-BM06F800    THRU   320-EXIT.                     
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       410-WRITE-BM06F800.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE F800-REC TO BM06F800A-REC.                                      
           WRITE BM06F800A-REC.                                                 
           ADD 1 TO WS-WRITE-CNT-F800.                                          
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '  '.                                                        
           DISPLAY '*----------- BP13C27B CONTROL LIST ----------*'.            
           DISPLAY '  '.                                                        
           DISPLAY 'NO OF BP13F205 READ   = ' WS-READ-TOTAL-CNT.                
           DISPLAY 'NO OF BM06F800 READ   = ' WS-READ-CNT-F800.                 
           DISPLAY 'NO OF BM06F800 WRITE  = ' WS-WRITE-CNT-F800.                
                                                                                
           CLOSE BP13F205                                                       
                 M06F800A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
