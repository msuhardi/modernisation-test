       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CY55.                                                 
      *AUTHOR.        JIANG BO.                                                 
      *DATE-WRITTEN.  18/06/2009.                                               
      ******************************************************************        
      *                SYSTEM OF COMMITMENT (BP13)                     *        
      ******************************************************************        
      * OBJECTIVE :                                                    *        
      *     FOR NON-SELECTION > 0 , GET LASTEST APPT DATE (LIMIT = Y)  *        
      *     FOR NON-SELECTION = 0 , GET LASTEST APPT DATE (LIMIT = N)  *        
      *                                                                *        
      * INPUT  FILES : BP13FH10                                        *        
      * OUTPUT FILES : P13FH10A                                        *        
      *                BP13F555                                        *        
      ******************************************************************        
      * CHG REQ   DATE      BY   DETAIL                                *        
      * ========  ========  ==== ===================================== *        
      * BP133614  18/06/09  JB8  NEW PROGRAM.                          *        
      * BP133663  10/07/09  JB8  OUPUT BP13F555 & BP13F716             *        
      ******************************************************************        
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT   BP13FH10      ASSIGN TO BP13FH10.                           
           SELECT   P13FH10A      ASSIGN TO P13FH10A.                           
           SELECT   BP13F555      ASSIGN TO BP13F555.                           
           SELECT   BP13F716      ASSIGN TO BP13F716.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13FH10                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13FH10.                                                           
                                                                                
       FD   P13FH10A                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       01   P13FH10A-REC                 PIC X(2000).                           
                                                                                
       FD   BP13F555                                                            
            RECORD CONTAINS 600 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F555.                                                           
                                                                                
       FD   BP13F716                                                            
            RECORD CONTAINS 350 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F716.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  CNT-FH10-READ             PIC 9(7)  VALUE ZEROS.                 
           05  CNT-FH10A-WRITE           PIC 9(7)  VALUE ZEROS.                 
           05  CNT-F555-WRITE            PIC 9(7)  VALUE ZEROS.                 
           05  CNT-F716-WRITE            PIC 9(7)  VALUE ZEROS.                 
           05  CNT-CHANGED               PIC 9(7)  VALUE ZEROS.                 
           05  CNT-FH10-DUPREC           PIC 9(7)  VALUE ZEROS.                 
           05  WS-FH10-EOF               PIC X(01) VALUE SPACE.                 
               88  FH10-EOF              VALUE 'Y'.                             
           05  WS-CTR                    PIC 9(02) VALUE ZEROES.                
           05  WS-CTR2                   PIC 9(02) VALUE ZEROES.                
           05  WS-CNT                    PIC 9(02) VALUE ZEROES.                
           05  WS-F555-CTR               PIC 9(02) VALUE ZEROES.                
           05  WS-F555-CTR2              PIC 9(02) VALUE ZEROES.                
           05  WS-TMP-TAB                PIC X(101) VALUE SPACES.               
           05  WS-CHANGED                PIC X(01) VALUE SPACE.                 
               88  REC-CHG               VALUE 'Y'.                             
                                                                                
       01   BP13FH10-RED.                                                       
            05  FH10-UIN                 PIC X(9).                              
            05  FILLER                   PIC X(94).                             
            05  FH10-TAB OCCURS 15 TIMES.                                       
                10 FILLER                PIC X(35).                             
                10 FH10-NRIC1            PIC X(09).                             
                10 FILLER                PIC X(57).                             
            05  FILLER                   PIC X(382).                            
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
      *==========*                                                              
       0000-MAIN.                                                               
      *==========*                                                              
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13FH10       THRU 2000-EXIT.                     
           PERFORM 3000-PROCESS-RECORD      THRU 3000-EXIT                      
             UNTIL FH10-EOF.                                                    
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================*                                                        
       1000-OPEN-FILES.                                                         
      *================*                                                        
                                                                                
           OPEN INPUT BP13FH10                                                  
               OUTPUT P13FH10A                                                  
                      BP13F555                                                  
                      BP13F716.                                                 
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *===================*                                                     
       2000-READ-BP13FH10.                                                      
      *===================*                                                     
                                                                                
           READ BP13FH10                                                        
             AT END                                                             
                MOVE 'Y'                   TO WS-FH10-EOF                       
                GO                         TO 2000-EXIT                         
           END-READ.                                                            
                                                                                
           ADD  1                          TO CNT-FH10-READ.                    
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *====================*                                                    
       3000-PROCESS-RECORD.                                                     
      *====================*                                                    
                                                                                
           MOVE SPACES                     TO WS-CHANGED                        
                                              WS-TMP-TAB.                       
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 14                 
              COMPUTE WS-CTR2 =  WS-CTR + 1                                     
              PERFORM VARYING WS-CTR2 FROM WS-CTR2 BY 1                         
                      UNTIL WS-CTR2 > 15                                        
                 IF FH10-NUM-REGN(WS-CTR)  > SPACES                             
                AND FH10-NUM-REGN(WS-CTR2) > SPACES                             
                    IF FH10-NUM-WITHIN-LIMIT(WS-CTR2) = 'Y'                     
                   AND FH10-NUM-WITHIN-LIMIT(WS-CTR) = 'N'                      
                       PERFORM 3500-SWITCH-TABLE     THRU 3500-EXIT             
                    ELSE                                                        
                       IF FH10-DTE-APPT(WS-CTR2) > FH10-DTE-APPT(WS-CTR)        
                          PERFORM 3500-SWITCH-TABLE  THRU 3500-EXIT             
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
              END-PERFORM                                                       
           END-PERFORM.                                                         
      **                                                                        
      ** COUNT CHANGED CASES                                                    
      **                                                                        
           IF REC-CHG                                                           
              ADD 1                        TO CNT-CHANGED                       
           END-IF                                                               
      **                                                                        
      ** OMIT DUPLICAT RECORDS                                                  
      **                                                                        
           IF FH10-NUM-UIN = FH10-NUM-NRIC1(1)                                  
              IF FH10-NUM-NON-SELECTION > '00'                                  
                 PERFORM 4000-OUTPUT-BP13F555    THRU 4000-EXIT                 
              END-IF                                                            
                                                                                
              MOVE BP13FH10-REC            TO P13FH10A-REC                      
              WRITE P13FH10A-REC                                                
              ADD 1                        TO CNT-FH10A-WRITE                   
           ELSE                                                                 
              ADD 1                        TO CNT-FH10-DUPREC                   
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FH10    THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *==================*                                                      
       3500-SWITCH-TABLE.                                                       
      *==================*                                                      
                                                                                
           MOVE FH10-BOOKING-TABLE(WS-CTR2)                                     
             TO WS-TMP-TAB.                                                     
                                                                                
           MOVE FH10-BOOKING-TABLE(WS-CTR)                                      
             TO FH10-BOOKING-TABLE(WS-CTR2).                                    
                                                                                
           MOVE WS-TMP-TAB                                                      
             TO FH10-BOOKING-TABLE(WS-CTR).                                     
                                                                                
           MOVE 'Y'   TO WS-CHANGED.                                            
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *====================*                                                    
       4000-OUTPUT-BP13F555.                                                    
      *====================*                                                    
                                                                                
           MOVE SPACES                   TO BP13F555-REC.                       
           INITIALIZE                       BP13F555-REC.                       
                                                                                
           MOVE 00                       TO WS-F555-CTR                         
                                                                                
           MOVE FH10-NUM-UIN             TO F555-NUM-UIN.                       
           MOVE FH10-NUM-NON-SELECTION   TO F555-NUM-NON-SELECTION.             
           MOVE FH10-DTE-DEBAR           TO F555-DTE-DEBAR.                     
           MOVE FH10-DTE-DEBAR-EXPIRY    TO F555-DTE-DEBAR-EXPIRY.              
                                                                                
           PERFORM VARYING WS-CTR FROM 1 BY 1 UNTIL WS-CTR > 15                 
              IF FH10-NUM-WITHIN-LIMIT(WS-CTR) = 'Y'                            
                 ADD 1                   TO WS-F555-CTR                         
                 IF WS-F555-CTR > FH10-NUM-NON-SELECTION                        
                    DISPLAY '__NON-SELECTION < CASES WHICH WITHIN LIMIT         
      -                     '= "Y" , UIN : ' FH10-NUM-UIN                       
                 END-IF                                                         
                 IF WS-F555-CTR = 1                                             
                    PERFORM 4100-OUTPUT-LATEST-APPLN   THRU 4100-EXIT           
                    PERFORM 4200-OUTPUT-BP13F716       THRU 4200-EXIT           
                 ELSE                                                           
                    PERFORM 4300-OUTPUT-OTHER-APPLNS   THRU 4300-EXIT           
                 END-IF                                                         
              END-IF                                                            
           END-PERFORM.                                                         
                                                                                
           WRITE BP13F555-REC.                                                  
           ADD 1                         TO CNT-F555-WRITE.                     
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ***************************                                               
       4100-OUTPUT-LATEST-APPLN.                                                
      ***************************                                               
                                                                                
           MOVE FH10-NUM-REGN(WS-CTR)       TO F555-LATEST-NUM-REGN             
           MOVE FH10-NUM-ALLO-CAT(WS-CTR)   TO F555-LATEST-NUM-ALLO-CAT         
           MOVE FH10-DTE-BALLOT(WS-CTR)     TO F555-LATEST-DTE-BALLOT           
           MOVE FH10-NUM-CATEGORY(WS-CTR)   TO F555-LATEST-NUM-CAT              
           MOVE FH10-NUM-NT(WS-CTR)         TO F555-LATEST-NUM-NT               
           MOVE FH10-NUM-FT(WS-CTR)         TO F555-LATEST-NUM-FT               
           MOVE FH10-NUM-HOUSEHOLD(WS-CTR)  TO F555-LATEST-NUM-HH               
           MOVE FH10-NUM-QUEUE(WS-CTR)      TO F555-LATEST-NUM-QUEUE            
           MOVE FH10-NUM-RESULT(WS-CTR)     TO F555-LATEST-NUM-RESULT           
           MOVE FH10-DTE-APPT(WS-CTR)       TO F555-LATEST-DTE-APPT.            
           MOVE FH10-NUM-NRIC1(WS-CTR)      TO F555-LATEST-HA1-NRIC.            
           MOVE FH10-NUM-NRIC2(WS-CTR)      TO F555-LATEST-HA2-NRIC.            
           MOVE FH10-NUM-NRIC3(WS-CTR)      TO F555-LATEST-HA3-NRIC.            
           MOVE FH10-NUM-NRIC4(WS-CTR)      TO F555-LATEST-HA4-NRIC.            
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **********************                                                    
       4200-OUTPUT-BP13F716.                                                    
      **********************                                                    
                                                                                
           IF FH10-NUM-NRIC1(WS-CTR) NOT = SPACES AND LOW-VALUES                
              MOVE SPACES                   TO BP13F716-REC                     
              MOVE FH10-NUM-REGN(WS-CTR)    TO F716-NUM-REGN                    
              MOVE FH10-NUM-NRIC1(WS-CTR)   TO F716-NUM-NRIC                    
              WRITE BP13F716-REC                                                
              ADD 1                         TO CNT-F716-WRITE                   
           END-IF.                                                              
                                                                                
           IF FH10-NUM-NRIC2(WS-CTR) NOT = SPACES AND LOW-VALUES                
              MOVE SPACES                   TO BP13F716-REC                     
              MOVE FH10-NUM-REGN(WS-CTR)    TO F716-NUM-REGN                    
              MOVE FH10-NUM-NRIC2(WS-CTR)   TO F716-NUM-NRIC                    
              WRITE BP13F716-REC                                                
              ADD 1                         TO CNT-F716-WRITE                   
           END-IF.                                                              
                                                                                
           IF FH10-NUM-NRIC3(WS-CTR) NOT = SPACES AND LOW-VALUES                
              MOVE SPACES                   TO BP13F716-REC                     
              MOVE FH10-NUM-REGN(WS-CTR)    TO F716-NUM-REGN                    
              MOVE FH10-NUM-NRIC3(WS-CTR)   TO F716-NUM-NRIC                    
              WRITE BP13F716-REC                                                
              ADD 1                         TO CNT-F716-WRITE                   
           END-IF.                                                              
                                                                                
           IF FH10-NUM-NRIC4(WS-CTR) NOT = SPACES AND LOW-VALUES                
              MOVE SPACES                   TO BP13F716-REC                     
              MOVE FH10-NUM-REGN(WS-CTR)    TO F716-NUM-REGN                    
              MOVE FH10-NUM-NRIC4(WS-CTR)   TO F716-NUM-NRIC                    
              WRITE BP13F716-REC                                                
              ADD 1                         TO CNT-F716-WRITE                   
           END-IF.                                                              
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************                                                
       4300-OUTPUT-OTHER-APPLNS.                                                
      **************************                                                
                                                                                
           COMPUTE WS-F555-CTR2 = WS-F555-CTR - 1.                              
                                                                                
           IF WS-F555-CTR2 > 3                                                  
              GO TO 4300-EXIT                                                   
           END-IF.                                                              
                                                                                
           MOVE FH10-NUM-REGN(WS-CTR)                                           
             TO F555-NUM-REGN(WS-F555-CTR2)                                     
           MOVE FH10-NUM-ALLO-CAT(WS-CTR)                                       
             TO F555-NUM-ALLO-CAT(WS-F555-CTR2)                                 
           MOVE FH10-DTE-BALLOT(WS-CTR)                                         
             TO F555-DTE-BALLOT(WS-F555-CTR2)                                   
           MOVE FH10-NUM-CATEGORY(WS-CTR)                                       
             TO F555-NUM-CAT(WS-F555-CTR2)                                      
           MOVE FH10-NUM-NT(WS-CTR)                                             
             TO F555-NUM-NT(WS-F555-CTR2)                                       
           MOVE FH10-NUM-FT(WS-CTR)                                             
             TO F555-NUM-FT(WS-F555-CTR2)                                       
           MOVE FH10-NUM-HOUSEHOLD(WS-CTR)                                      
             TO F555-NUM-HH(WS-F555-CTR2)                                       
           MOVE FH10-NUM-QUEUE(WS-CTR)                                          
             TO F555-NUM-QUEUE(WS-F555-CTR2)                                    
           MOVE FH10-NUM-RESULT(WS-CTR)                                         
             TO F555-NUM-RESULT(WS-F555-CTR2)                                   
           MOVE FH10-DTE-APPT(WS-CTR)                                           
             TO F555-DTE-APPT(WS-F555-CTR2)                                     
           MOVE FH10-NUM-NRIC1(WS-CTR)                                          
             TO F555-NUM-NRIC1(WS-F555-CTR2)                                    
           MOVE FH10-NUM-NRIC2(WS-CTR)                                          
             TO F555-NUM-NRIC2(WS-F555-CTR2)                                    
           MOVE FH10-NUM-NRIC3(WS-CTR)                                          
             TO F555-NUM-NRIC3(WS-F555-CTR2)                                    
           MOVE FH10-NUM-NRIC4(WS-CTR)                                          
             TO F555-NUM-NRIC4(WS-F555-CTR2).                                   
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *=================*                                                       
       9000-CLOSE-FILES.                                                        
      *=================*                                                       
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '***************************************'.                   
           DISPLAY 'PROGRAM ID     :  BP13CY55.'                                
           DISPLAY '***************************************'.                   
           DISPLAY 'NO. OF BP13FH10 READ     : ' CNT-FH10-READ.                 
           DISPLAY 'NO. OF P13FH10A WRITTEN  : ' CNT-FH10A-WRITE.               
           DISPLAY 'NO. OF BP13F555 WRITTEN  : ' CNT-F555-WRITE.                
           DISPLAY 'NO. OF BP13F716 WRITTEN  : ' CNT-F716-WRITE.                
           DISPLAY 'NO. OF CASES MOVED       : ' CNT-CHANGED.                   
           DISPLAY 'NO. OF DUPLICATE REC     : ' CNT-FH10-DUPREC.               
                                                                                
           CLOSE BP13FH10                                                       
                 P13FH10A                                                       
                 BP13F555                                                       
                 BP13F716.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
