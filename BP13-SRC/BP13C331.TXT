       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C331.                                                 
      *AUTHOR.        YPH.                                                      
      *DATE-WRITTEN.  11/07/91.                                                 
      * =========================================================== *           
      *  SYSTEM OF COMMITMENT (BP13)                                *           
      * =========================================================== *           
      *                                                             *           
      *  OBJECTIVE :  AT TIME OF BOOKING AND IF KEYS AVAILABLE,     *           
      *               TO PRINT ACCEPTED CASES SEPARATELY FROM       *           
      *               KEYS NOT AVAILABLE IN LISTS. FOR ACCEPTED     *           
      *               CASES WHERE KEYS AVAILABLE, PRINT             *           
      *               EXPIRED-DATE-FOR-TAKE-POSSESSION.             *           
      *                                                             *           
      * CHG REF  BY    ON      DESCRIPTION                          *           
      * -------- ---  ------  ------------                          *           
      * C910481  YPH  240791  AS ABOVE                              *           
      * SOCPH8B  LITO 200993  CHG FROM P10K020 TO BP13K800 AND      *           
      *                       ACCESS FLAT ADDRESS VIA BP13C904.     *           
      * BP130418 SGK  040698  CATER FOR Y2K.                        *           
      * BP130883 RAS  070800  CHG BP13C904 TO BP13C913              *           
      * BP132371 SSS  111003  PRT LOAN TAG                          *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
      *-------------------------------------------------------------            
       FILE-CONTROL.                                                            
           SELECT BP13F300 ASSIGN TO BP13F300.                                  
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ACCESS MODE IS RANDOM                                         
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K110-KEY-FLD                                    
                  FILE STATUS IS BM06K110-STATUS.                               
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE IS RANDOM                                         
                  ORGANIZATION IS INDEXED                                       
                  RECORD KEY IS K800-NUM-REGN                                   
                  FILE STATUS IS K800-STATUS.                                   
           SELECT BP13L331 ASSIGN TO BP13L331.                                  
                                                                                
                                                                                
      *-------------------------------------------------------------            
       DATA DIVISION.                                                           
      *-------------------------------------------------------------            
       FILE SECTION.                                                            
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BP13F300                                                             
           BLOCK  CONTAINS 0 CHARACTERS                                         
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS IS STANDARD.                                           
       COPY BP13F300.                                                           
                                                                                
       FD  BP13K800                                                             
           BLOCK  CONTAINS 0 CHARACTERS                                         
           RECORD CONTAINS 2000 CHARACTERS                                      
           LABEL RECORDS IS STANDARD.                                           
       COPY BP13K800.                                                           
                                                                                
       FD  BP13L331                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01  BP13L331-REC                   PIC X(132).                           
                                                                                
                                                                                
      *-------------------------------------------------------------            
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-DATE                 PIC X(8)   VALUE SPACES.                     
                                                                                
       01  BM06K110-STATUS         PIC 99       VALUE ZEROS.                    
       01  K800-STATUS             PIC 99       VALUE ZEROS.                    
       01  WS-FND-K800             PIC X.                                       
       01  WS-EOF-F300             PIC X.                                       
                                                                                
       01  WS-CNT-READ-F300        PIC 9(5)     VALUE ZEROS.                    
       01  WS-TOT-ACCEPT           PIC 9(5)     VALUE ZEROS.                    
       01  WS-CNT-ACCEPT-0         PIC 9(5)     VALUE ZEROS.                    
       01  WS-CNT-REJECT           PIC 9(5)     VALUE ZEROS.                    
                                                                                
       01  WS-LISTING-CONTROLS.                                                 
           05  WS-SEQ           PIC 9(4)        VALUE 0.                        
           05  CNT-LINE-0       PIC 99          VALUE 99.                       
           05  CNT-PAGE-0       PIC 9(5)        VALUE ZEROS.                    
           05  CNT-NUM-LINE-0   PIC 9(5)        VALUE ZEROS.                    
                                                                                
       01  L331-PR-HEAD-01.                                                     
           05  FILLER              PIC X(8)      VALUE 'BP13L331'.              
           05  FILLER              PIC X(4)      VALUE SPACES .                 
           05  FILLER              PIC X(8)      VALUE 'HDBCAT 3'.              
           05  FILLER              PIC X(35)     VALUE SPACES.                  
           05  FILLER              PIC X(20)     VALUE                          
                                    'SYSTEM OF COMMITMENT'.                     
           05  FILLER              PIC X(20)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'DATE :'.                
           05  L331-DATE           PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'PAGE : '.               
           05  L331-PAGENO         PIC ZZZZ9 .                                  
           05  FILLER              PIC X(6)      VALUE SPACES.                  
                                                                                
       01  L331-PR-HEAD-02.                                                     
           05  FILLER              PIC X(50)     VALUE SPACES.                  
           05  FILLER              PIC X(32)     VALUE                          
           'BOOKING SYSTEM (AGREEMENT  TEAM)'.                                  
           05  FILLER              PIC X(68)     VALUE SPACES.                  
                                                                                
       01  L331-PR-HEAD-03.                                                     
           05  FILLER              PIC X(35)     VALUE SPACES.                  
           05  FILLER              PIC X(62)     VALUE                          
           'DETAIL LIST KEY AVAILABLE CASES (AGREEMENT ORDER RAISED)'.          
           05  FILLER              PIC X(35)     VALUE SPACES.                  
                                                                                
       01  L331-PR-HEAD-05.                                                     
           05  FILLER              PIC X(12)     VALUE                          
               ' NO. REGN-NO'.                                                  
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE                          
               'SCH-ACCT-NO'.                                                   
           05  FILLER              PIC X(6)      VALUE SPACES.                  
           05  FILLER              PIC X(5)      VALUE 'BLK'.                   
           05  FILLER              PIC X         VALUE SPACES.                  
           05  FILLER              PIC X(11)     VALUE                          
               'STREET NAME'.                                                   
           05  FILLER              PIC X(19)     VALUE SPACES.                  
           05  FILLER              PIC X(7)      VALUE 'UNIT-NO'.               
           05  FILLER              PIC X(5)      VALUE SPACES.                  
           05  FILLER              PIC X(9)      VALUE 'BK REFUND'.             
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(9)      VALUE 'KEY AVAIL'.             
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(9)      VALUE 'ALLOC SCH'.             
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(10)     VALUE 'ED FOR TP'.             
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X(4)      VALUE 'LOAN'.                  
                                                                                
       01  L331-PR-DETAIL.                                                      
           05  L331-NO             PIC ZZZ9      VALUE ZEROS.                   
           05  FILLER              PIC X         VALUE SPACES.                  
           05  L331-CUR-REGN-NO    PIC X(8)      VALUE SPACES.                  
           05  FILLER              PIC X         VALUE SPACES.                  
           05  L331-NUM-SCH-ACC    PIC X(14)     VALUE SPACES.                  
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  L331-BLK-NO         PIC X(5)      VALUE SPACES.                  
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  L331-STREET-NAME    PIC X(28)     VALUE SPACES.                  
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X         VALUE '#'.                     
           05  L331-LEVEL-NO       PIC X(2)      VALUE SPACES.                  
           05  FILLER              PIC X         VALUE '-'.                     
           05  L331-MAIN-UNIT      PIC X(4)      VALUE SPACES.                  
           05  L331-SUB-UNIT       PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X         VALUE SPACES.                  
           05  L331-AMT-BK         PIC $ZZZZ9.99.                               
           05  FILLER              PIC X(2)      VALUE SPACES.                  
           05  L331-DTE-KEY-AVAIL  PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(4)      VALUE SPACES.                  
           05  L331-ALLOC-SCH      PIC X(3)      VALUE SPACES.                  
           05  FILLER              PIC X(6)      VALUE SPACES.                  
           05  L331-DTE-EXP-TP     PIC X(10)     VALUE SPACES.                  
           05  FILLER              PIC X(3)      VALUE SPACES.                  
           05  L331-LOAN-TAG       PIC X         VALUE SPACES.                  
                                                                                
       01  L331-PR-LINE.                                                        
           05  FILLER              PIC X(28)     VALUE SPACES.                  
           05  FILLER              PIC X(41)     VALUE                          
               '---------------------------------------'.                       
                                                                                
      *---------------------------------------------------------------*         
      *        LINKAGE FOR BP13C913                                   *         
      *---------------------------------------------------------------*         
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH   PIC X(4).                                  
               10  WS-LINK-NUM-ACC   PIC X(5).                                  
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
      *-------------------------------------------------------------            
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       MAIN.                                                                    
      *-------------------------------------------------------------            
           PERFORM 000-OPENING-ROUTINE THRU 000-EXIT.                           
           PERFORM 100-READ-F300       THRU 100-EXIT.                           
           PERFORM 200-PROCESS-F300    THRU 200-EXIT                            
               UNTIL WS-EOF-F300 = 'Y'.                                         
           PERFORM 999-CLOSING-ROUTINE.                                         
                                                                                
                                                                                
      *-------------------------------------------------------------            
       000-OPENING-ROUTINE.                                                     
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F300                                                 
                       BM06K110                                                 
                       BP13K800                                                 
                OUTPUT BP13L331.                                                
                                                                                
           IF BM06K110-STATUS NOT = 0 AND 97                                    
              DISPLAY 'OPENING ERROR, K110-STATUS ' BM06K110-STATUS             
              MOVE  BM06K110-STATUS TO RETURN-CODE                              
              PERFORM 999-CLOSING-ROUTINE.                                      
                                                                                
           IF K800-STATUS NOT = 0 AND 97                                        
              DISPLAY 'OPENING ERROR, K800-STATUS ' K800-STATUS                 
              MOVE  K800-STATUS TO RETURN-CODE                                  
              PERFORM 999-CLOSING-ROUTINE.                                      
                                                                                
           PERFORM 750-PRINT-HEADER-0 THRU 750-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       100-READ-F300.                                                           
      *-------------------------------------------------------------            
            READ BP13F300 AT END                                                
                MOVE 'Y' TO WS-EOF-F300                                         
                GO TO 100-EXIT.                                                 
                                                                                
            ADD 1                     TO WS-CNT-READ-F300.                      
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       200-PROCESS-F300.                                                        
      *-------------------------------------------------------------            
      *      IF KEY AVAILABLE DATE NOT = '0000000'                 *            
      *         THEN PRINT REPORT                                  *            
      *      ELSE                                                  *            
      *          READ BP13F310                                     *            
      *-------------------------------------------------------------            
           PERFORM 300-READ-K110 THRU 300-EXIT.                                 
                                                                                
           IF K110-DTE-KEY-AVAIL  NOT = ZERO AND SPACES AND LOW-VALUES          
                 PERFORM 400-READ-K800 THRU 400-EXIT                            
                 PERFORM 600-PROCESS-LIST THRU 600-EXIT.                        
                                                                                
           PERFORM 100-READ-F300 THRU 100-EXIT.                                 
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       300-READ-K110.                                                           
      *-------------------------------------------------------------            
           MOVE F300-SCH-ACC    TO K110-KEY-FLD.                                
                                                                                
           READ BM06K110                                                        
           IF BM06K110-STATUS = 0                                               
              NEXT SENTENCE                                                     
           ELSE                                                                 
              DISPLAY 'READ ERROR, K110-STATUS = ' BM06K110-STATUS              
              DISPLAY 'K110-KEY-FLD = ' K110-KEY-FLD.                           
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       400-READ-K800.                                                           
      *-------------------------------------------------------------            
                                                                                
           MOVE F300-CUR-REG            TO K800-NUM-REGN.                       
           READ BP13K800.                                                       
                                                                                
           IF K800-STATUS = 0                                                   
              NEXT SENTENCE                                                     
           ELSE                                                                 
              DISPLAY 'READ ERROR, K800-STATUS = ' K800-STATUS                  
              DISPLAY 'K800-NUM-REGN = ' K800-NUM-REGN                          
              GO TO 400-EXIT.                                                   
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       600-PROCESS-LIST.                                                        
      *-------------------------------------------------------------            
      * PRINT DETAIL LIST OF PREPARATION LIST FOR ACCEPTED CASES *              
      * ONLY.                                                    *              
      *-------------------------------------------------------------            
           MOVE SPACES TO                L331-CUR-REGN-NO                       
                                         L331-NUM-SCH-ACC                       
                                         L331-BLK-NO                            
                                         L331-STREET-NAME                       
                                         L331-LEVEL-NO                          
                                         L331-MAIN-UNIT                         
                                         L331-SUB-UNIT                          
                                         L331-DTE-KEY-AVAIL                     
                                         L331-ALLOC-SCH                         
                                         L331-DTE-EXP-TP                        
                                         L331-LOAN-TAG.                         
                                                                                
            ADD 1                    TO   WS-TOT-ACCEPT.                        
            PERFORM 650-PREP-LIST    THRU 650-EXIT.                             
                                                                                
            ADD 1                    TO   WS-CNT-ACCEPT-0.                      
            PERFORM 700-PRINT-LIST-0 THRU 700-EXIT.                             
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       650-PREP-LIST.                                                           
      *-------------------------------------------------------------            
            MOVE F300-KEY-FLD         TO L331-CUR-REGN-NO.                      
            IF F300-AMT-BK-TRANSFER = ZEROS                                     
               MOVE F300-AMT-BOOKING-FEE TO L331-AMT-BK.                        
                                                                                
            STRING K110-SCHEME '-'                                              
                   K110-ACC '-'                                                 
                   K110-CHK '-'                                                 
                   K110-ACC-TYPE                                                
                   DELIMITED BY SIZE INTO L331-NUM-SCH-ACC.                     
                                                                                
            MOVE K110-SCH-ACC-NO      TO WS-LINK-REC.                           
                                                                                
            MOVE SPACES               TO BP13COMM8-REC.                         
            INITIALIZE                   BP13COMM8-REC.                         
                                                                                
            CALL 'BP13C913'  USING WS-LINK-REC,                                 
                                   BP13COMM8-REC.                               
                                                                                
            MOVE COMM8-NME-STREET     TO L331-STREET-NAME.                      
            MOVE COMM8-NUM-LEVEL      TO L331-LEVEL-NO.                         
            MOVE COMM8-NUM-UNIT-MAIN  TO L331-MAIN-UNIT.                        
            MOVE COMM8-NUM-BLK        TO L331-BLK-NO.                           
            MOVE COMM8-NUM-UNIT-SUB   TO L331-SUB-UNIT.                         
                                                                                
            STRING K110-DTE-KEY-AVAIL(7:2) '/'                                  
                   K110-DTE-KEY-AVAIL(5:2) '/'                                  
                   K110-DTE-KEY-AVAIL(1:4)                                      
                   DELIMITED BY SIZE INTO L331-DTE-KEY-AVAIL.                   
                                                                                
            MOVE K800-NUM-ALLO-SCHEME  TO L331-ALLOC-SCH.                       
            MOVE K800-NUM-LOAN-TAG     TO L331-LOAN-TAG.                        
                                                                                
            STRING K800-DTE-EXPIRED-TP(7:2) '/'                                 
                   K800-DTE-EXPIRED-TP(5:2) '/'                                 
                   K800-DTE-EXPIRED-TP(1:4)                                     
                   DELIMITED BY SIZE INTO L331-DTE-EXP-TP.                      
                                                                                
       650-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       700-PRINT-LIST-0.                                                        
      *-------------------------------------------------------------            
      * FOR ACCEPTED CASES WHERE KEYS ARE AVAILABLE                             
      *-------------------------------------------------------------            
            IF CNT-NUM-LINE-0 > 53                                              
                PERFORM 750-PRINT-HEADER-0 THRU 750-EXIT.                       
                                                                                
            MOVE SPACES           TO BP13L331-REC.                              
            ADD 1                 TO CNT-LINE-0 CNT-NUM-LINE-0.                 
            ADD 1                 TO WS-SEQ.                                    
            MOVE WS-SEQ           TO L331-NO.                                   
            WRITE BP13L331-REC FROM L331-PR-DETAIL AFTER 1.                     
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       750-PRINT-HEADER-0.                                                      
      *-------------------------------------------------------------            
            ADD 1                 TO CNT-PAGE-0.                                
            MOVE CNT-PAGE-0       TO L331-PAGENO.                               
            MOVE ZEROS            TO CNT-LINE-0.                                
            MOVE 8                TO CNT-NUM-LINE-0.                            
                                                                                
            MOVE FUNCTION CURRENT-DATE(1:8)  TO   WS-DATE.                      
            STRING WS-DATE(7:2) '/'                                             
                   WS-DATE(5:2) '/'                                             
                   WS-DATE(1:4)                                                 
                   DELIMITED BY SIZE INTO L331-DATE.                            
                                                                                
            WRITE BP13L331-REC FROM L331-PR-HEAD-01 AFTER PAGE.                 
            WRITE BP13L331-REC FROM L331-PR-HEAD-02 AFTER 1.                    
            WRITE BP13L331-REC FROM L331-PR-HEAD-03 AFTER 1.                    
            WRITE BP13L331-REC FROM L331-PR-HEAD-05 AFTER 2.                    
                                                                                
            MOVE SPACES           TO BP13L331-REC.                              
            WRITE BP13L331-REC.                                                 
                                                                                
       750-EXIT.                                                                
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       999-CLOSING-ROUTINE.                                                     
      *-------------------------------------------------------------            
           DISPLAY '  '.                                                        
           DISPLAY ' BP13C331 CONTROL TOTAL ---------------*'.                  
           DISPLAY 'TOTAL NO OF REC READ FROM F300  = '                         
                   WS-CNT-READ-F300.                                            
           DISPLAY 'TOTAL NO OF REC OUTPUT WITH KEY = '                         
                   WS-CNT-ACCEPT-0.                                             
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F300                                                       
                 BM06K110                                                       
                 BP13K800                                                       
                 BP13L331.                                                      
                                                                                
           IF BM06K110-STATUS NOT = 0                                           
               DISPLAY 'CLOSING ERROR, K110-STATUS ' BM06K110-STATUS            
               MOVE  BM06K110-STATUS TO RETURN-CODE.                            
                                                                                
           IF K800-STATUS NOT = 0                                               
               DISPLAY 'CLOSING ERROR, K800-STATUS ' K800-STATUS                
               MOVE  K800-STATUS TO RETURN-CODE.                                
                                                                                
           STOP RUN.                                                            
