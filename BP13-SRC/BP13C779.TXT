       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C779.                                                 
      *AUTHOR.        ED.                                                       
      *DATE-WRITTEN.  29JAN2002.                                                
      **********************************************************                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE :                                         *                
      *      1.  TO TRANSFER REMAINING SERVING QUOTA TO        *                
      *          NON-SERVING QUOTA BEFORE 2ND ROUND PIGEON-HOLD*                
      *                                                        *                
      *     I-O   FILE  :                                      *                
      *      1. BP13K765                                       *                
      *                                                        *                
      * CHG-NO   BY  ON        DESCRIPTION                     *                
      * ======== === ========= ===============================*                 
      * BP132089 ED  28JAN2002 NEW PGM.                        *                
      * BP132098 LSB 05FEB2002 CHECK FOR EMPTY REC.            *                
      **********************************************************                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K765 ASSIGN TO BP13K765                                   
                ACCESS MODE  IS SEQUENTIAL                                      
                ORGANIZATION IS INDEXED                                         
                RECORD KEY   IS K765-KEY-FLD                                    
                FILE STATUS  IS BP13K765-STATUS.                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13K765                                                            
            BLOCK CONTAINS 0 RECORDS                                            
            RECORD CONTAINS 200 CHARACTERS                                      
            LABEL RECORD IS STANDARD.                                           
       COPY BP13K765.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-VARIABLES.                                                        
           05  BP13K765-STATUS           PIC 9(02) VALUE ZEROS.                 
           05  WS-CNT-K765               PIC 9(05) VALUE 0.                     
           05  WS-CNT-K765-UPD           PIC 9(05) VALUE 0.                     
           05  WS-K765-EOF               PIC X     VALUE SPACES.                
               88  K765-EOF                        VALUE 'Y'.                   
                                                                                
      *==================================================                       
       PROCEDURE DIVISION.                                                      
      *==================================================                       
                                                                                
       0000-MAIN-ROUTINE.                                                       
                                                                                
           PERFORM 1000-OPEN-FILES                                              
              THRU 1000-EXIT.                                                   
                                                                                
           PERFORM 3000-READ-BP13K765                                           
              THRU 3000-EXIT.                                                   
                                                                                
           PERFORM 4000-MAIN-ROUTINE                                            
              THRU 4000-EXIT                                                    
             UNTIL K765-EOF.                                                    
                                                                                
           PERFORM 9000-CLOSE-ROUTINE                                           
              THRU 9000-EXIT.                                                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
                                                                                
           OPEN I-O    BP13K765.                                                
                                                                                
           IF BP13K765-STATUS NOT = 0 AND 97                                    
              DISPLAY 'ERROR OPENING BP13K765,STATUS : '                        
                        BP13K765-STATUS                                         
              MOVE BP13K765-STATUS       TO RETURN-CODE                         
              PERFORM 9000-CLOSE-ROUTINE                                        
                 THRU 9000-EXIT.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-READ-BP13K765.                                                      
                                                                                
            READ BP13K765 NEXT           AT END                                 
            MOVE 'Y'                     TO WS-K765-EOF                         
            GO TO 3000-EXIT.                                                    
                                                                                
            ADD 1                        TO WS-CNT-K765.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-MAIN-ROUTINE.                                                       
                                                                                
           IF K765-KEY-FLD NOT = SPACES                                         
                                                                                
             IF K765-NUM-FLAT-SERVE IS NOT NUMERIC                              
                MOVE ZEROS TO K765-NUM-FLAT-SERVE                               
             END-IF                                                             
                                                                                
             IF K765-NUM-REG-SERVE IS NOT NUMERIC                               
                MOVE ZEROS TO K765-NUM-REG-SERVE                                
             END-IF                                                             
                                                                                
             IF K765-NUM-FLAT-SERVE > K765-NUM-REG-SERVE                        
                                                                                
                 COMPUTE K765-NUM-FLAT-NONSERVE =                               
                 (K765-NUM-FLAT-NONSERVE + (K765-NUM-FLAT-SERVE -               
                                            K765-NUM-REG-SERVE))                
                                                                                
                 REWRITE BP13K765-REC                                           
                 EVALUATE BP13K765-STATUS                                       
                 WHEN 00                                                        
                      ADD 1 TO WS-CNT-K765-UPD                                  
                 WHEN OTHER                                                     
                      DISPLAY 'ERROR RE-WRITING BP13K765. STATUS IS '           
                               BP13K765-STATUS                                  
                 END-EVALUATE                                                   
                                                                                
             END-IF                                                             
           END-IF.                                                              
                                                                                
           PERFORM 3000-READ-BP13K765                                           
              THRU 3000-EXIT.                                                   
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-ROUTINE.                                                      
                                                                                
           CLOSE BP13K765.                                                      
                                                                                
           DISPLAY '*************** BP13C779 ***************'.                  
           DISPLAY ' '.                                                         
           DISPLAY 'NO. OF BP13K765 RECORDS READ    : ' WS-CNT-K765.            
           DISPLAY 'NO. OF BP13K765 RECORDS UPDATED : ' WS-CNT-K765-UPD.        
           DISPLAY ' '.                                                         
                                                                                
           IF BP13K765-STATUS NOT = 0                                           
              DISPLAY '***  CLOSING ERROR, BP13K765-STATUS '                    
              BP13K765-STATUS.                                                  
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
