       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C050.                                                 
      *AUTHOR.        NOR.                                                      
      *DATE-WRITTEN : 30/07/1999.                                               
      * =========================================================== *           
      *  SYSTEM OF COMMITMENT (BP13)                                *           
      * =========================================================== *           
      *                                                             *           
      *  OBJECTIVE : THIS PROGRAM TO SELECT THE EARLIEST RECORD IN  *           
      *              CDB MOBILITY FILE FOR SALES TYPE 2A FLAT       *           
      *              FOR THE SAME SCH-ACC-NO( 9 BYTES)              *           
      *                                                             *           
      *  SIMILAR PGM : BP13C626                                     *           
      * ----------------------------------------------------------- *           
      * CHGE ID  OIC  DATE     DESCRIPTION                          *           
      * -------- ---- -------- -----------                          *           
      * BP130715 NOR  30071999 NEW PGM                              *           
      * BP130897 AKT  31072000 F558 EXPAND TO X(600)                *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F716 ASSIGN TO BP13F716.                                  
           SELECT BP13F558 ASSIGN TO BP13F558.                                  
           SELECT P13F716A ASSIGN TO P13F716A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13F558                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 600 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F558.                                                           
                                                                                
       FD   BP13F716                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 350 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       COPY BP13F716.                                                           
                                                                                
       FD   P13F716A                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 350 CHARACTERS                                      
            RECORDING MODE IS F                                                 
            LABEL RECORDS ARE STANDARD.                                         
                                                                                
       01   P13F716A-REC           PIC X(350).                                  
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-F558-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-F716-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-MATCH                PIC 9(8)    VALUE ZERO.                      
       01  WS-F716-NOTMATCH        PIC 9(8)    VALUE ZERO.                      
       01  WS-F558-NOTMATCH        PIC 9(8)    VALUE ZERO.                      
       01  WS-F716-STORED          PIC 9(8)    VALUE ZERO.                      
                                                                                
       77  WS-EOF-F716             PIC X(3).                                    
           88  EOF-OF-F716                     VALUE 'YES'.                     
       77  WS-EOF-F558             PIC X(3).                                    
           88  EOF-OF-F558                     VALUE 'YES'.                     
                                                                                
       01  WS-F558-KEY-FLD.                                                     
           05  WS-F558-REGN        PIC X(8)    VALUE SPACES.                    
           05  WS-F558-UIN         PIC X(9)    VALUE SPACES.                    
                                                                                
       01  WS-F716-KEY-FLD.                                                     
           05  WS-F716-REGN        PIC X(8)    VALUE SPACES.                    
           05  WS-F716-UIN         PIC X(9)    VALUE SPACES.                    
                                                                                
       01  WS-DTE-CMOVEIN.                                                      
           05  WS-CMI-YY           PIC 9(04)   VALUE ZEROES.                    
           05  WS-CMI-MM           PIC 9(02)   VALUE ZEROES.                    
           05  WS-CMI-DD           PIC 9(02)   VALUE ZEROES.                    
                                                                                
       01  WS-DTE-CMOVEOUT.                                                     
           05  WS-CMO-YY           PIC 9(04)   VALUE ZEROES.                    
           05  WS-CMO-MM           PIC 9(02)   VALUE ZEROES.                    
           05  WS-CMO-DD           PIC 9(02)   VALUE ZEROES.                    
                                                                                
       01  WS-DTE-PMOVEIN.                                                      
           05  WS-PMI-YY           PIC 9(04)   VALUE ZEROES.                    
           05  WS-PMI-MM           PIC 9(02)   VALUE ZEROES.                    
           05  WS-PMI-DD           PIC 9(02)   VALUE ZEROES.                    
                                                                                
       01  WS-DTE-PMOVEOUT.                                                     
           05  WS-PMO-YY           PIC 9(04)   VALUE ZEROES.                    
           05  WS-PMO-MM           PIC 9(02)   VALUE ZEROES.                    
           05  WS-PMO-DD           PIC 9(02)   VALUE ZEROES.                    
                                                                                
       01  WS-LAPSE-DIFF           PIC 9(4)    VALUE ZEROS.                     
       01  WS-LAPSE-FLAG           PIC X(01).                                   
           88  WS-LAPSE            VALUE 'Y'.                                   
       01  WS-LAST-F716-REC        PIC X(350)  VALUE SPACES.                    
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       000-MAIN-LOGIC.                                                          
      *-------------------------------------------------------------            
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
                                                                                
           PERFORM 200-READ-F716      THRU 200-EXIT.                            
                                                                                
           PERFORM 300-READ-F558      THRU 300-EXIT.                            
                                                                                
           PERFORM 400-PROCESS        THRU 400-EXIT                             
                   UNTIL WS-F716-KEY-FLD = HIGH-VALUE                           
                    AND  WS-F558-KEY-FLD = HIGH-VALUE.                          
                                                                                
           PERFORM 900-FINALIZATION   THRU 900-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       100-INITIALIZATION.                                                      
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F716                                                 
                       BP13F558                                                 
                OUTPUT P13F716A.                                                
                                                                                
           MOVE 'NO' TO WS-EOF-F716.                                            
           MOVE 'NO' TO WS-EOF-F558.                                            
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-F716.                                                           
      *-------------------------------------------------------------            
           READ BP13F716 AT END                                                 
                MOVE 'YES'      TO WS-EOF-F716                                  
                MOVE HIGH-VALUES TO WS-F716-KEY-FLD                             
                GO TO 200-EXIT                                                  
           END-READ.                                                            
                                                                                
            ADD 1 TO WS-F716-READ.                                              
            MOVE F716-NUM-REGN  TO WS-F716-REGN.                                
            MOVE F716-NUM-NRIC  TO WS-F716-UIN.                                 
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       300-READ-F558.                                                           
      *-------------------------------------------------------------            
           READ BP13F558 AT END                                                 
                MOVE 'YES'      TO WS-EOF-F558                                  
                MOVE HIGH-VALUES TO WS-F558-KEY-FLD                             
                GO TO 300-EXIT                                                  
           END-READ.                                                            
                                                                                
           ADD 1 TO WS-F558-READ.                                               
            MOVE F558-NUM-REGN  TO WS-F558-REGN.                                
            MOVE F558-NUM-NRIC  TO WS-F558-UIN.                                 
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *****************************************************************         
      *  MATCH  BY REGNNO                                             *         
      *****************************************************************         
       400-PROCESS.                                                             
      *-------------------------------------------------------------            
           IF WS-F716-KEY-FLD = WS-F558-KEY-FLD                                 
              ADD 1                    TO WS-MATCH                              
              PERFORM 450-CHECK-FILE-STATUS THRU 450-EXIT                       
                UNTIL WS-F716-KEY-FLD NOT = WS-F558-KEY-FLD                     
                 OR  WS-F716-KEY-FLD = HIGH-VALUES                              
              PERFORM 300-READ-F558 THRU 300-EXIT                               
           ELSE                                                                 
               IF WS-F716-KEY-FLD  < WS-F558-KEY-FLD                            
                   ADD 1 TO WS-F716-NOTMATCH                                    
                   PERFORM 200-READ-F716 THRU 200-EXIT                          
               ELSE                                                             
                   ADD 1 TO WS-F558-NOTMATCH                                    
                   PERFORM 300-READ-F558 THRU 300-EXIT                          
               END-IF                                                           
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       450-CHECK-FILE-STATUS.                                                   
      *-------------------------------------------------------------            
           IF F558-NUM-HDB-REF-SMS (1:9) = F716-NUM-SCH-ACCNT                   
              IF F558-CDE-FILE-STATUS-SMS  NOT = 'OM'                           
                 MOVE SPACES              TO  P13F716A-REC                      
                                              WS-LAST-F716-REC                  
                 MOVE BP13F716-REC        TO  P13F716A-REC                      
                                              WS-LAST-F716-REC                  
                 MOVE ZEROS               TO  WS-DTE-PMOVEIN                    
                                              WS-DTE-PMOVEOUT                   
                                              WS-DTE-CMOVEIN                    
                                              WS-DTE-CMOVEOUT                   
                                                                                
                 MOVE 'N'                   TO  WS-LAPSE-FLAG                   
                 PERFORM 460-PROCESS-SAME-SCHEME     THRU 460-EXIT              
                   UNTIL WS-F716-KEY-FLD NOT = WS-F558-KEY-FLD                  
                      OR WS-F716-KEY-FLD = HIGH-VALUES                          
                      OR F558-NUM-HDB-REF-SMS(1:9) NOT =                        
                         F716-NUM-SCH-ACCNT                                     
                                                                                
                 WRITE P13F716A-REC                                             
                 ADD  1                   TO WS-F716-STORED                     
              ELSE                                                              
                 PERFORM 200-READ-F716 THRU 200-EXIT                            
              END-IF                                                            
           ELSE                                                                 
              PERFORM 200-READ-F716 THRU 200-EXIT                               
           END-IF.                                                              
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       460-PROCESS-SAME-SCHEME.                                                 
      *-------------------------------------------------------------            
           MOVE    BP13F716-REC    TO  P13F716A-REC                             
           PERFORM 200-READ-F716 THRU  200-EXIT.                                
                                                                                
       460-EXIT.                                                                
           EXIT.                                                                
                                                                                
       900-FINALIZATION.                                                        
      *-------------------------------------------------------------            
             COMPUTE WS-F716-NOTMATCH = WS-F716-READ - WS-MATCH.                
                                                                                
             DISPLAY '*--------  BP13C050  -----------*'.                       
             DISPLAY '                               '.                         
             DISPLAY '* MATCH BP13F716 WITH BP13F558,   '.                      
             DISPLAY '* TO GET EARLIST MOVE-IN-DATE   '.                        
             DISPLAY '                               '.                         
             DISPLAY ' BP13F716-READ      ' WS-F716-READ.                       
             DISPLAY ' BP13F558-READ      ' WS-F558-READ.                       
             DISPLAY ' NO OF F716 MATCHED ' WS-MATCH.                           
             DISPLAY ' F716-NOT-MATCH     ' WS-F716-NOTMATCH.                   
             DISPLAY ' F558-NOT-MATCH     ' WS-F558-NOTMATCH.                   
             DISPLAY ' RECORDS WRITTEN    ' WS-F716-STORED.                     
                                                                                
           CLOSE      BP13F716                                                  
                      BP13F558                                                  
                      P13F716A.                                                 
           STOP RUN.                                                            
                                                                                
       900-EXIT.                                                                
           EXIT.                                                                
