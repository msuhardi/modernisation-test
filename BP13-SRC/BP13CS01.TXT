       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CS01.                                                 
       AUTHOR.        CHAN KIN KEONG.                                           
       DATE-WRITTEN.  26/10/2006.                                               
      *========================================================*                
      *                                                        *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT                  *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO EXTRACT DATES FROM BP13K454 BASED  *                
      *                  ON THE CURRENT DATE.                  *                
      *    INPUT       : BP13.K800.APPLN                       *                
      *                  BP13.K820.OCCUP                       *                
      *                  BP13.K454.APPTABLE                    *                
      *                  BM06.K110.FLATINV                     *                
      *    OUTPUT      : BP13.F203.SMS                         *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO    BY    DATE      DETAILS                      *                
      * BP132943  CKK7  20061201  NEW PROGRAM                  *                
      * BP134209  JB8   20110523  TO READ BP13K421             *                
      * BP134252  JB8   20110524  TO CATER FOR K421 FND        *                
      *========================================================*                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F203 ASSIGN TO BP13F203.                                  
                                                                                
           SELECT BP13K454 ASSIGN TO BP13K454                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K454-KEY-FLD                               
                  FILE STATUS     IS K454-STATUS.                               
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS K800-STATUS.                               
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS K820-STATUS.                               
                                                                                
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K110-KEY-FLD                               
                  FILE STATUS     IS K110-STATUS.                               
                                                                                
           SELECT BP13K421 ASSIGN TO BP13K420                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K421-KEY-FLD                               
                  ALTERNATE KEY   IS K421-NUM-REGN                              
                  FILE STATUS     IS K421-STATUS.                               
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F203                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 200 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       COPY BP13F203.                                                           
                                                                                
       FD  BP13K454                                                             
           RECORD CONTAINS 100 CHARACTERS.                                      
       COPY BP13K454.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BP13K421                                                             
           RECORD CONTAINS 450 CHARACTERS.                                      
       COPY BP13K421.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-FILE-STATUS.                                                      
           05 K454-STATUS                 PIC 9(2)  VALUE ZEROS.                
           05 K800-STATUS                 PIC 9(2)  VALUE ZEROS.                
           05 K820-STATUS                 PIC 9(2)  VALUE ZEROS.                
           05 K110-STATUS                 PIC 9(2)  VALUE ZEROS.                
           05 K421-STATUS                 PIC 9(2)  VALUE ZEROS.                
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F203-WRITE               PIC 9(5)  VALUE ZEROS.                
           05 WS-F203-NOT-WRITE           PIC 9(5)  VALUE ZEROS.                
           05 WS-K454-READ                PIC 9(5)  VALUE ZEROS.                
                                                                                
       01  WS-EOF-K454                    PIC X     VALUE 'N'.                  
       01  WS-NO-APPT                     PIC X     VALUE 'Y'.                  
       01  WS-K421-FND                    PIC X     VALUE SPACES.               
           88  K421-FND                             VALUE 'Y'.                  
                                                                                
       01  WS-CUR-DATE1                   PIC 9(8)  VALUE ZEROES.               
       01  WS-DATE-VALUES.                                                      
           05  WS-CUR-DATE                PIC X(8).                             
           05  WS-CUR-DATE-NUM REDEFINES WS-CUR-DATE                            
                                          PIC 9(8).                             
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
                                                                                
       000-MAIN-ROUTINE.                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-STARTBR-K454   THRU 200-EXIT.                            
           PERFORM 300-PROCESS-RTN    THRU 300-EXIT                             
             UNTIL (WS-CUR-DATE NOT = K454-DTE-APPT  OR                         
                    WS-EOF-K454 = 'Y')                                          
           PERFORM 999-CLOSE-ROUTINE  THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       100-INITIALIZATION.                                                      
      ******************************************************************        
                                                                                
           OPEN INPUT  BP13K800                                                 
                       BP13K820                                                 
                       BM06K110                                                 
                       BP13K454                                                 
                       BP13K421                                                 
                OUTPUT BP13F203.                                                
                                                                                
           IF K800-STATUS NOT = ZEROS AND 97                                    
              DISPLAY ' ERROR OPENING BP13K800  FILE, STATUS '                  
                  K800-STATUS                                                   
              MOVE K800-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                          
                                                                                
           IF K454-STATUS NOT = ZEROS AND 97                                    
              DISPLAY ' ERROR OPENING BP13K454  FILE, STATUS '                  
                  K454-STATUS                                                   
              MOVE K454-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                          
                                                                                
           IF K820-STATUS NOT = ZEROS AND 97                                    
              DISPLAY ' ERROR OPENING BP13K820  FILE, STATUS '                  
                  K820-STATUS                                                   
              MOVE K820-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                          
                                                                                
           IF K110-STATUS NOT = ZEROS AND 97                                    
              DISPLAY ' ERROR OPENING BM06K110  FILE, STATUS '                  
                  K110-STATUS                                                   
              MOVE K110-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                          
                                                                                
           IF K421-STATUS NOT = ZEROS AND 97                                    
              DISPLAY ' ERROR OPENING BP13K421  FILE, STATUS '                  
                  K421-STATUS                                                   
              MOVE K421-STATUS TO RETURN-CODE                                   
              PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT.                          
                                                                                
           MOVE FUNCTION CURRENT-DATE    TO WS-CUR-DATE.                        
                                                                                
      * FIND NEXT DAY                                                           
                                                                                
           COMPUTE WS-CUR-DATE-NUM =                                            
                   FUNCTION DATE-OF-INTEGER(                                    
                   FUNCTION INTEGER-OF-DATE(WS-CUR-DATE-NUM) + 3).              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       200-STARTBR-K454.                                                        
      ******************************************************************        
                                                                                
           MOVE  SPACES                  TO K454-KEY-FLD.                       
           MOVE  WS-CUR-DATE             TO K454-DTE-APPT.                      
                                                                                
           START BP13K454 KEY NOT < K454-KEY-FLD.                               
                                                                                
           IF K454-STATUS = 00                                                  
              PERFORM 320-READNXT-K454   THRU 320-EXIT                          
      *       CONTINUE                                                          
           ELSE                                                                 
              IF K454-STATUS = 23                                               
                 DISPLAY 'NO APPT ON ' WS-CUR-DATE(7:2) '-'                     
                          WS-CUR-DATE(5:2) '-' WS-CUR-DATE(1:4)                 
                 PERFORM 999-CLOSE-ROUTINE THRU 999-EXIT                        
              ELSE                                                              
                 MOVE K454-STATUS          TO RETURN-CODE                       
                 DISPLAY 'ERROR READING BP13K454, ' K454-KEY-FLD                
                 GO TO 999-CLOSE-ROUTINE                                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       300-PROCESS-RTN.                                                         
      ******************************************************************        
                                                                                
           MOVE 'Y'                      TO WS-NO-APPT.                         
                                                                                
      *    PERFORM 320-READNXT-K454      THRU 320-EXIT.                         
                                                                                
           PERFORM 400-READ-K800         THRU 400-EXIT.                         
                                                                                
           IF (K454-CDE-APPT-TYPE = 'SA' AND                                    
              (K800-DTE-RAISE-AO NOT = '00000000' AND                           
               K800-DTE-RAISE-AO NOT = SPACES AND                               
               K800-DTE-RAISE-AO NOT = LOW-VALUES))                             
               DISPLAY 'BOOKED FLAT BEFORE APPT : ' K800-NUM-REGN               
               MOVE 'N' TO WS-NO-APPT                                           
           END-IF.                                                              
                                                                                
           IF (K454-CDE-APPT-TYPE = 'SP' OR 'TP' OR 'SR')                       
              PERFORM 500-READ-K110       THRU 500-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-NO-APPT = 'Y'                                                  
              PERFORM 600-READ-K820       THRU 600-EXIT                         
           END-IF.                                                              
                                                                                
           IF ((WS-NO-APPT = 'Y') AND                                           
              (K820-NUM-HP-PGR NOT = SPACES AND LOW-VALUES AND ZEROES))         
              PERFORM 330-WRITE-F203      THRU 330-EXIT                         
           ELSE                                                                 
              IF K820-NUM-HP-PGR = SPACES OR LOW-VALUES OR ZEROES               
                 DISPLAY 'NO HANDPHONE: ' K800-NUM-REGN                         
              END-IF                                                            
              ADD   1                     TO WS-F203-NOT-WRITE                  
              PERFORM 320-READNXT-K454    THRU 320-EXIT                         
           END-IF.                                                              
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       320-READNXT-K454.                                                        
      ******************************************************************        
                                                                                
           READ BP13K454 NEXT                                                   
             AT END                                                             
                MOVE 'Y' TO WS-EOF-K454.                                        
                                                                                
           ADD 1         TO WS-K454-READ.                                       
                                                                                
       320-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       330-WRITE-F203.                                                          
      ******************************************************************        
                                                                                
           MOVE SPACES                 TO BP13F203-REC.                         
           MOVE K820-NUM-NRIC          TO F203-NUM-NRIC.                        
           MOVE K820-NME-OCCP          TO F203-NME-NRIC.                        
           MOVE K820-NUM-HP-PGR        TO F203-NUM-HP.                          
           MOVE 'HA1'                  TO F203-NUM-APLCNT.                      
           MOVE K454-DTE-APPT          TO F203-DTE-APPMT.                       
           MOVE K454-TME-APPT          TO F203-TME-APPMT.                       
           MOVE K454-CDE-APPT-TYPE     TO F203-TYPE-APPMT.                      
           MOVE K800-NUM-ALLO-CAT      TO F203-NUM-ALLO-MODE.                   
      *    MOVE 'SMS'                  TO F203-NUM-ALLO-MODE.                   
           MOVE K800-NUM-NEW-TOWN      TO F203-NUM-NEW-TOWN.                    
           MOVE K800-NUM-FLAT-TYPE     TO F203-NUM-FLAT-TYPE.                   
           MOVE K800-NUM-ZONE          TO F203-NUM-ZONE.                        
           MOVE K800-NUM-CAT           TO F203-NUM-ETHNIC.                      
           MOVE K800-NUM-NT-FT-QUEUE   TO F203-NUM-QUEUE.                       
           MOVE K800-NUM-REGN          TO F203-NUM-REGN.                        
           MOVE K800-DTE-BALLOT        TO F203-DTE-BALLOT.                      
                                                                                
           PERFORM 700-READ-K421       THRU 700-EXIT.                           
           IF K421-FND                                                          
              MOVE 'CC'                TO F203-TYPE-APPMT                       
           END-IF.                                                              
                                                                                
           WRITE BP13F203-REC.                                                  
           ADD   1                     TO WS-F203-WRITE.                        
                                                                                
           PERFORM 320-READNXT-K454    THRU 320-EXIT.                           
                                                                                
       330-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       400-READ-K800.                                                           
      ******************************************************************        
                                                                                
           MOVE K454-NUM-REGN       TO K800-NUM-REGN.                           
           READ BP13K800 KEY IS K800-NUM-REGN.                                  
                                                                                
           IF K800-STATUS = 0                                                   
              IF K800-NUM-STATUS = 'C'                                          
                 DISPLAY 'REC CANCELLED IN SOC MASTER: ' K454-NUM-REGN          
                 MOVE 'N' TO WS-NO-APPT                                         
              ELSE                                                              
                 CONTINUE                                                       
              END-IF                                                            
           ELSE                                                                 
              DISPLAY 'REC NOT FOUND IN SOC MASTER: ' K454-NUM-REGN             
              MOVE 'N' TO WS-NO-APPT                                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *************************************************************             
       500-READ-K110.                                                           
      *************************************************************             
           MOVE K454-SCH-ACCT     TO K110-KEY-FLD                               
           READ BM06K110                                                        
           IF K110-STATUS = 00                                                  
              IF (K110-DTE-KEY-ISSUED NOT = '00000000' AND                      
                  K110-DTE-KEY-ISSUED NOT = SPACES AND                          
                  K110-DTE-KEY-ISSUED NOT = LOW-VALUES)                         
                  DISPLAY 'TPED FLAT BEFORE APPT : ' K110-REGN-NO               
                  MOVE 'N' TO WS-NO-APPT                                        
              END-IF                                                            
           ELSE                                                                 
              DISPLAY 'REC NOT FOUND IN BM06K110: ' K454-SCH-ACCT               
              MOVE 'N' TO WS-NO-APPT                                            
           END-IF.                                                              
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
      ******************************************************************        
       600-READ-K820.                                                           
      ******************************************************************        
                                                                                
           MOVE K800-NUM-REGN       TO K820-NUM-REGN.                           
           MOVE K800-NUM-NRIC1      TO K820-NUM-NRIC.                           
                                                                                
           READ BP13K820.                                                       
           IF K820-STATUS = 0                                                   
              CONTINUE                                                          
           ELSE                                                                 
             IF K820-STATUS = 23                                                
                DISPLAY 'MAIN APPLICANT NOT FOUND: ' K800-NUM-REGN              
             ELSE                                                               
                DISPLAY 'ERROR READING K820 : ' K820-STATUS                     
                MOVE 'N' TO WS-NO-APPT                                          
             END-IF                                                             
           END-IF.                                                              
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       700-READ-K421.                                                           
      ******************************************************************        
                                                                                
           MOVE K454-NUM-REGN       TO K421-NUM-REGN.                           
                                                                                
           READ BP13K421 KEY IS K421-NUM-REGN.                                  
           IF K421-STATUS = 00 OR 02                                            
              MOVE 'Y'              TO WS-K421-FND                              
           ELSE                                                                 
             IF K421-STATUS = 23                                                
                MOVE 'N'            TO WS-K421-FND                              
             ELSE                                                               
                DISPLAY 'ERROR READING K820 : ' K820-STATUS                     
                MOVE 'N'            TO WS-K421-FND                              
             END-IF                                                             
           END-IF.                                                              
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************************************************************        
       999-CLOSE-ROUTINE.                                                       
      ******************************************************************        
           CLOSE BP13K800                                                       
                 BP13K820                                                       
                 BM06K110                                                       
                 BP13F203                                                       
                 BP13K454                                                       
                 BP13K421.                                                      
                                                                                
           SUBTRACT 1 FROM WS-K454-READ.                                        
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CS01 ================*'.              
           DISPLAY '*======= RUN DATE : ' WS-CUR-DATE ' =======*'.              
           DISPLAY '    TOTAL NO OF REC READ    = ' WS-K454-READ.               
           DISPLAY '    NO. OF REC NOT SENT     = ' WS-F203-NOT-WRITE.          
           DISPLAY '    NO. OF SMS SENT         = ' WS-F203-WRITE.              
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
       999-EXIT.                                                                
           EXIT.                                                                
                                                                                
