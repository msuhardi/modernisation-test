       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C45H.                                                 
      *AUTHOR.        KSJ3.                                                     
      *DATE-WRITTEN.  06/01/2017.                                               
      * ========================================================== *            
      * OBJECTIVE    : DAILY PROGRAM TO EXTRACT UNUSED PAST KC30   *            
      *                BATCHES (SENT DATE < CURRENT DATE)          *            
      * ========================================================== *            
      * INPUT  FILES :  1.) BP13FC30                               *            
      *                 2.) BP13KC20                               *            
      * OUTPUT       :  1.) BP13FC3A                               *            
      * ========================================================== *            
      * CHG-NO   BY   DATE       DESCRIPTION                       *            
      * -------- ---  ------     -----------                       *            
      * BP136580 KSJ3 06/01/2017 NEW PROGRAM                       *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FC30  ASSIGN        TO BP13FC30.                          
                                                                                
           SELECT BP13KC20  ASSIGN        TO BP13KC20                           
                            ACCESS MODE   IS DYNAMIC                            
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS KC20-KEY-FLD                       
                            FILE STATUS   IS WS-KC20-STATUS.                    
                                                                                
           SELECT BP13FC3A  ASSIGN        TO BP13FC3A.                          
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13FC30                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 200 CHARACTERS.                                      
           COPY BP13FC30.                                                       
                                                                                
       FD  BP13KC20                                                             
           RECORD CONTAINS 150 CHARACTERS.                                      
           COPY BP13KC20.                                                       
                                                                                
       FD  BP13FC3A                                                             
           RECORDING MODE IS F                                                  
           RECORD CONTAINS 200 CHARACTERS.                                      
       01  BP13FC3A-REC.                                                        
           05  FILLER                    PIC X(28).                             
           05  WS-FC3A-CNT-REC           PIC 9(02).                             
           05  FILLER                    PIC X(170).                            
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS.                                                      
           05  WS-KC20-STATUS            PIC 9(2)   VALUE ZEROES.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-CNT-FC30-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-FC30-SKIP          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-KC20-READ          PIC 9(6)   VALUE ZEROES.               
           05  WS-CNT-FC3A-WRITE         PIC 9(6)   VALUE ZEROES.               
                                                                                
       01  WS-FLAGS.                                                            
           05  WS-FC30-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-KC20-FND               PIC X(1)   VALUE 'N'.                  
           05  WS-KC20-EOF               PIC X(1)   VALUE 'N'.                  
           05  WS-SWITCH-FL              PIC X(1)   VALUE 'N'.                  
                                                                                
       01  WS-DATE.                                                             
           05  WS-DTE-FMT8.                                                     
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-FMT10.                                                    
               10  WS-DTE-DD             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-MM             PIC 9(2)   VALUE ZEROES.               
               10  FILLER                PIC X      VALUE '/'.                  
               10  WS-DTE-CCYY.                                                 
                   15  WS-DTE-CC         PIC 9(2)   VALUE ZEROES.               
                   15  WS-DTE-YY         PIC 9(2)   VALUE ZEROES.               
           05  WS-DTE-CURR-8             PIC 9(8)   VALUE ZEROES.               
           05  WS-DTE-CURR-10            PIC X(10)  VALUE ZEROES.               
           05  WS-DTE-SENT-PREV          PIC X(8)   VALUE SPACES.               
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *=============================================================            
       0000-CONTROL.                                                            
      *=============================================================            
                                                                                
           PERFORM 1000-OPEN-ROUTINE       THRU 1000-EXIT.                      
           PERFORM 1100-GET-DATE           THRU 1100-EXIT.                      
           PERFORM 2000-BP13FC30-READ      THRU 2000-EXIT.                      
           PERFORM 3000-MAIN-ROUTINE       THRU 3000-EXIT                       
                   UNTIL WS-FC30-EOF = 'Y'.                                     
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1000-OPEN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           OPEN INPUT  BP13FC30                                                 
                       BP13KC20                                                 
                OUTPUT BP13FC3A.                                                
                                                                                
           IF WS-KC20-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPENING BP13KC20 ERROR ' WS-KC20-STATUS                  
              MOVE     WS-KC20-STATUS      TO   RETURN-CODE                     
              PERFORM  9000-CLOSE-ROUTINE  THRU 9000-EXIT                       
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       1100-GET-DATE.                                                           
      *=============================================================            
                                                                                
      *-------------------------------------------------------------            
      *    GET CURRENT DATE                                                     
      *-------------------------------------------------------------            
           MOVE FUNCTION CURRENT-DATE(1:8) TO   WS-DTE-CURR-8.                  
           MOVE WS-DTE-CURR-8              TO   WS-DTE-FMT8.                    
           MOVE CORR WS-DTE-FMT8           TO   WS-DTE-FMT10.                   
           MOVE WS-DTE-FMT10               TO   WS-DTE-CURR-10.                 
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       2000-BP13FC30-READ.                                                      
      *=============================================================            
                                                                                
           READ BP13FC30                                                        
              AT END MOVE 'Y'              TO   WS-FC30-EOF.                    
                                                                                
           IF WS-FC30-EOF NOT = 'Y'                                             
              IF FC30-DTE-SENT-SELF-BK >= WS-DTE-CURR-8                         
                 MOVE 'Y'                  TO   WS-FC30-EOF                     
              ELSE                                                              
                 ADD 1                     TO   WS-CNT-FC30-READ                
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3000-MAIN-ROUTINE.                                                       
      *=============================================================            
                                                                                
           IF WS-DTE-SENT-PREV NOT = FC30-DTE-SENT-SELF-BK                      
              MOVE 'N'                     TO   WS-KC20-FND                     
                                                WS-KC20-EOF                     
              MOVE SPACES                  TO   BP13KC20-REC                    
              MOVE FC30-DTE-SENT-SELF-BK   TO   KC20-DTE-SENT-SELF-BK           
                                                                                
              PERFORM 3100-BP13KC20-STARTBR     THRU 3100-EXIT                  
                                                                                
              IF WS-KC20-FND = 'Y'                                              
                 MOVE 'N'                  TO   WS-SWITCH-FL                    
              ELSE                                                              
                 MOVE 'Y'                  TO   WS-SWITCH-FL                    
              END-IF                                                            
                                                                                
              MOVE FC30-DTE-SENT-SELF-BK   TO   WS-DTE-SENT-PREV                
           END-IF.                                                              
                                                                                
           IF WS-SWITCH-FL = 'Y'                                                
              MOVE BP13FC30-REC            TO   BP13FC3A-REC                    
              MOVE 1                       TO   WS-FC3A-CNT-REC                 
              WRITE BP13FC3A-REC                                                
              ADD 1                        TO   WS-CNT-FC3A-WRITE               
           ELSE                                                                 
              ADD 1                        TO   WS-CNT-FC30-SKIP                
           END-IF.                                                              
                                                                                
           PERFORM 2000-BP13FC30-READ      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3100-BP13KC20-STARTBR.                                                   
      *=============================================================            
                                                                                
           START BP13KC20 KEY >= KC20-KEY-FLD.                                  
                                                                                
           EVALUATE WS-KC20-STATUS                                              
           WHEN 00                                                              
              PERFORM 3110-BP13KC20-READNEXT    THRU 3110-EXIT                  
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                     TO   WS-KC20-EOF                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'START BP13KC20 ERROR '   WS-KC20-STATUS                  
              MOVE    WS-KC20-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       3110-BP13KC20-READNEXT.                                                  
      *=============================================================            
                                                                                
           READ BP13KC20 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-KC20-STATUS                                              
           WHEN 00                                                              
           WHEN 02                                                              
              IF KC20-DTE-SENT-SELF-BK NOT = FC30-DTE-SENT-SELF-BK              
                 MOVE 'Y'                  TO   WS-KC20-EOF                     
              ELSE                                                              
                 MOVE 'Y'                  TO   WS-KC20-FND                     
              END-IF                                                            
                                                                                
           WHEN 10                                                              
           WHEN 23                                                              
              MOVE 'Y'                     TO   WS-KC20-EOF                     
                                                                                
           WHEN OTHER                                                           
              DISPLAY 'READNEXT BP13KC20 ERROR ' WS-KC20-STATUS                 
              MOVE    WS-KC20-STATUS       TO   RETURN-CODE                     
              PERFORM 9000-CLOSE-ROUTINE   THRU 9000-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *=============================================================            
       9000-CLOSE-ROUTINE.                                                      
      *=============================================================            
                                                                                
           CLOSE BP13FC30                                                       
                 BP13KC20                                                       
                 BP13FC3A.                                                      
                                                                                
           IF WS-KC20-STATUS NOT = 00 AND 97                                    
              DISPLAY 'CLOSING BP13KC20 ERROR ' WS-KC20-STATUS                  
              MOVE     WS-KC20-STATUS      TO   RETURN-CODE                     
           END-IF.                                                              
                                                                                
           DISPLAY '   '.                                                       
           DISPLAY '*------- BP13C45H CONTROL TOTAL -------*'.                  
           DISPLAY '   '.                                                       
           DISPLAY ' 1. NO OF BP13FC30 READ      : ' WS-CNT-FC30-READ.          
           DISPLAY ' 2. NO OF BP13FC30 SKIPPED   : ' WS-CNT-FC30-SKIP.          
           DISPLAY ' 3. NO OF BP13KC20 READ      : ' WS-CNT-KC20-READ.          
           DISPLAY ' 4. NO OF BP13FC3A WRITTEN   : ' WS-CNT-FC3A-WRITE.         
           DISPLAY '  '.                                                        
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
