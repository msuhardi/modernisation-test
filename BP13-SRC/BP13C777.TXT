       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C777.                                                 
       AUTHOR.        SHARON DUMDUM.                                            
      *DATE-WRITTEN.  07/09/2004.                                               
      * =========================================================== *           
      *              SYSTEM OF COMMITMENT (BP13)                    *           
      * =========================================================== *           
      *   OBJECTIVE  :  TO UPDATE MCPH AND MCPG IN BP13K012.        *           
      *                                                             *           
      *   INPUT      :  BP13F012                                    *           
      *                 BP13K010                                    *           
      *   I-O        :  BP13K012                                    *           
      *                                                             *           
      *   AMENDMENTS:                                               *           
      *    REF      DATE     BY          DESCRIPTIONS               *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP132596 07/09/2004 SD10 NEW PROGRAM                        *           
      * BP134861 22/04/2013 EAA1 EXPAND BP13K010 TO 600             *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F012 ASSIGN TO BP13F012.                                  
                                                                                
           SELECT BP13K012 ASSIGN TO        BP13K012                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K012-KEY-FLD                        
                           FILE   STATUS IS WS-K012-STATUS.                     
                                                                                
           SELECT BP13K010 ASSIGN TO        BP13K010                            
                           ACCESS MODE   IS DYNAMIC                             
                           ORGANIZATION  IS INDEXED                             
                           RECORD KEY    IS K010-KEY-FLD                        
                           FILE   STATUS IS WS-K010-STATUS.                     
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BP13F012                                                            
            BLOCK  CONTAINS 0 RECORDS                                           
            RECORD CONTAINS 400 CHARACTERS                                      
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
       COPY BP13F012.                                                           
                                                                                
       FD   BP13K012                                                            
            RECORD CONTAINS 400 CHARACTERS.                                     
       COPY BP13K012.                                                           
                                                                                
       FD   BP13K010                                                            
            RECORD CONTAINS 600 CHARACTERS.                                     
       COPY BP13K010.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-F012-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-K010-READ            PIC 9(8)    VALUE ZERO.                      
       01  WS-K012-REWRITE         PIC 9(8)    VALUE ZERO.                      
       01  WS-K012-FOUND           PIC 9(8)    VALUE ZERO.                      
       01  WS-K010-NOTFND          PIC 9(8)    VALUE ZERO.                      
                                                                                
       01  WS-EOF-F012             PIC X       VALUE SPACE.                     
       01  WS-K012-STATUS          PIC 9(2)    VALUE ZEROES.                    
       01  WS-K010-STATUS          PIC 9(2)    VALUE ZEROES.                    
       01  WS-K012-REC             PIC X(400)  VALUE SPACES.                    
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-NUM-MAX-MCP      PIC S9(5)V9(5)  VALUE ZEROES.                
           05  WS-NUM-TOT-PA1      PIC S9(5)V9(5)  VALUE ZEROES.                
           05  WS-NUM-TOT-PA2      PIC S9(5)V9(5)  VALUE ZEROES.                
           05  WS-NUM-TOT-PA3      PIC S9(5)V9(5)  VALUE ZEROES.                
                                                                                
       01  WS-TIME-DATES.                                                       
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-CCYY.                                                 
                   15  WS-SYS-CC   PIC 9(2).                                    
                   15  WS-SYS-YY   PIC 9(2).                                    
               10  WS-SYS-MM       PIC 9(2).                                    
               10  WS-SYS-DD       PIC 9(2).                                    
           05  WS-EDITED-DATE.                                                  
               10  WS-EDITED-DD    PIC X(2)    VALUE SPACES.                    
               10  FILLER          PIC X       VALUE '/'.                       
               10  WS-EDITED-MM    PIC X(2)    VALUE SPACES.                    
               10  FILLER          PIC X       VALUE '/'.                       
               10  WS-EDITED-YY    PIC X(4)    VALUE SPACES.                    
                                                                                
      ****************************************************************          
       PROCEDURE DIVISION.                                                      
      ****************************************************************          
       0000-MAIN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           PERFORM 1000-OPEN-ROUTINE  THRU 1000-EXIT.                           
           PERFORM 2000-PROCESS       THRU 2000-EXIT                            
              UNTIL WS-EOF-F012 = 'Y'.                                          
           PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************************************************          
       1000-OPEN-ROUTINE.                                                       
      ****************************************************************          
                                                                                
           OPEN INPUT  BP13F012                                                 
                       BP13K010                                                 
                I-O    BP13K012.                                                
           MOVE 'N' TO WS-EOF-F012.                                             
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)  TO  WS-SYSTEM-DATE.                 
                                                                                
           MOVE   WS-SYS-DD           TO WS-EDITED-DD.                          
           MOVE   WS-SYS-MM           TO WS-EDITED-MM.                          
           MOVE   WS-SYS-CCYY         TO WS-EDITED-YY.                          
                                                                                
           IF WS-K012-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K012 OPEN ERROR, STATUS IS ' WS-K012-STATUS          
              GO TO   9000-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           IF WS-K010-STATUS NOT = 00 AND 97                                    
              DISPLAY 'BP13K010 OPEN ERROR, STATUS IS ' WS-K010-STATUS          
              GO TO   9000-CLOSE-ROUTINE                                        
           END-IF.                                                              
                                                                                
           INITIALIZE    WS-F012-READ                                           
                         WS-K010-READ                                           
                         WS-K012-REWRITE                                        
                         WS-K012-FOUND                                          
                         WS-K010-NOTFND.                                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       2000-PROCESS.                                                            
      *****************************************************************         
                                                                                
           PERFORM 3000-READ-F012        THRU 3000-EXIT.                        
           IF WS-EOF-F012 NOT = 'Y'                                             
              PERFORM 4000-READ-K012     THRU 4000-EXIT                         
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       3000-READ-F012.                                                          
      *****************************************************************         
                                                                                
           READ BP13F012 AT END MOVE 'Y' TO WS-EOF-F012                         
                GO TO 3000-EXIT.                                                
            ADD 1 TO WS-F012-READ.                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4000-READ-K012.                                                          
      *****************************************************************         
                                                                                
           MOVE F012-KEY-FLD           TO K012-KEY-FLD.                         
                                                                                
           READ BP13K012.                                                       
           IF WS-K012-STATUS = 00                                               
                                                                                
              PERFORM 4100-READ-K010       THRU 4100-EXIT                       
                                                                                
              IF WS-K010-STATUS = 23                                            
                 GO TO 4000-EXIT                                                
              END-IF                                                            
                                                                                
              PERFORM 4200-COMPUTE-FIELDS  THRU 4200-EXIT                       
              PERFORM 5000-REWRITE-K012  THRU 5000-EXIT                         
           ELSE                                                                 
              IF WS-K012-STATUS = 00                                            
                 DISPLAY 'ERROR, RECORD NOT FOUND IN BP13K012. '                
                         'K012-CDE-NT-ZONE : ' K012-CDE-NT-ZONE '. '            
                         'K012-CDE-FLAT-TY : ' K012-CDE-FLAT-TY                 
                 ADD 1 TO WS-K012-FOUND                                         
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K012. STATUS IS '                   
                          WS-K012-STATUS                                        
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4100-READ-K010.                                                          
      *****************************************************************         
                                                                                
           MOVE K012-KEY-FLD           TO K010-KEY-FLD.                         
                                                                                
           READ BP13K010.                                                       
           IF WS-K010-STATUS = ZEROES                                           
                                                                                
              MOVE K010-NUM-TOT-PA     TO K012-NUM-TOT-PA                       
              MOVE K010-NUM-MAX-PA1    TO K012-NUM-MAX-PA1                      
              MOVE K010-NUM-MAX-PA2    TO K012-NUM-MAX-PA2                      
              MOVE K010-NUM-MAX-PA3    TO K012-NUM-MAX-PA3                      
              MOVE K010-REG-PA1        TO K012-REG-PA1                          
              MOVE K010-REG-PA2        TO K012-REG-PA2                          
              MOVE K010-REG-PA3        TO K012-REG-PA3                          
                                                                                
              ADD 1 TO WS-K010-READ                                             
                                                                                
           ELSE                                                                 
              IF WS-K010-STATUS = 23                                            
                 ADD 1 TO WS-K010-NOTFND                                        
                 DISPLAY 'RECORD NOT FOUND IN BP13K010. '                       
                         'K010-CDE-NT-ZONE : ' K010-CDE-NT-ZONE '. '            
                         'K010-CDE-FLAT-TY : ' K010-CDE-FLAT-TY '. '            
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K010. STATUS IS '                   
                          WS-K010-STATUS                                        
                 GO TO 9000-CLOSE-ROUTINE                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       4200-COMPUTE-FIELDS.                                                     
      *****************************************************************         
                                                                                
           COMPUTE  WS-NUM-TOT-PA1 =                                            
                   (K012-NUM-MAX-PA1 - K012-REG-PA1) /                          
                    K012-NUM-TOT-PA.                                            
           COMPUTE  WS-NUM-TOT-PA2 =                                            
                   (K012-NUM-MAX-PA2 - K012-REG-PA2) /                          
                    K012-NUM-TOT-PA.                                            
           COMPUTE  WS-NUM-TOT-PA3 =                                            
                   (K012-NUM-MAX-PA3 - K012-REG-PA3) /                          
                    K012-NUM-TOT-PA.                                            
                                                                                
           COMPUTE  WS-NUM-MAX-MCP ROUNDED =                                    
                    K012-NUM-TOT-MCPG * WS-NUM-TOT-PA1 + 0.49.                  
           MOVE WS-NUM-MAX-MCP      TO K012-NUM-MAX-MCPG1.                      
                                                                                
           COMPUTE  WS-NUM-MAX-MCP ROUNDED =                                    
                    K012-NUM-TOT-MCPG * WS-NUM-TOT-PA2 + 0.49.                  
           MOVE WS-NUM-MAX-MCP      TO K012-NUM-MAX-MCPG2.                      
                                                                                
           COMPUTE  WS-NUM-MAX-MCP ROUNDED =                                    
                    K012-NUM-TOT-MCPG * WS-NUM-TOT-PA3 + 0.49.                  
           MOVE WS-NUM-MAX-MCP           TO K012-NUM-MAX-MCPG3.                 
                                                                                
           COMPUTE  WS-NUM-MAX-MCP ROUNDED =                                    
                    K012-NUM-TOT-NOMCH * WS-NUM-TOT-PA1 + 0.49.                 
           MOVE WS-NUM-MAX-MCP           TO K012-NUM-MAX-NOMCH1.                
                                                                                
           COMPUTE  WS-NUM-MAX-MCP ROUNDED =                                    
                    K012-NUM-TOT-NOMCH * WS-NUM-TOT-PA2 + 0.49.                 
           MOVE WS-NUM-MAX-MCP           TO K012-NUM-MAX-NOMCH2.                
                                                                                
           COMPUTE  WS-NUM-MAX-MCP ROUNDED =                                    
                    K012-NUM-TOT-NOMCH * WS-NUM-TOT-PA3 + 0.49.                 
           MOVE WS-NUM-MAX-MCP           TO K012-NUM-MAX-NOMCH3.                
                                                                                
           COMPUTE  WS-NUM-MAX-MCP ROUNDED =                                    
                    K012-NUM-TOT-NOMCG * WS-NUM-TOT-PA1 + 0.49.                 
           MOVE WS-NUM-MAX-MCP           TO K012-NUM-MAX-NOMCG1.                
                                                                                
           COMPUTE  WS-NUM-MAX-MCP ROUNDED =                                    
                    K012-NUM-TOT-NOMCG * WS-NUM-TOT-PA2 + 0.49.                 
           MOVE WS-NUM-MAX-MCP           TO K012-NUM-MAX-NOMCG2.                
                                                                                
           COMPUTE  WS-NUM-MAX-MCP ROUNDED =                                    
                    K012-NUM-TOT-NOMCG * WS-NUM-TOT-PA3 + 0.49.                 
           MOVE WS-NUM-MAX-MCP           TO K012-NUM-MAX-NOMCG3.                
                                                                                
           COMPUTE  K012-NUM-MAX-MCPH1 =                                        
                   (K012-NUM-MAX-PA1 - K012-REG-PA1) -                          
                   (K012-NUM-MAX-MCPG1)  -                                      
                   (K012-NUM-MAX-NOMCH1) -                                      
                   (K012-NUM-MAX-NOMCG1).                                       
                                                                                
           COMPUTE  K012-NUM-MAX-MCPH2 =                                        
                   (K012-NUM-MAX-PA2 - K012-REG-PA2) -                          
                   (K012-NUM-MAX-MCPG2)  -                                      
                   (K012-NUM-MAX-NOMCH2) -                                      
                   (K012-NUM-MAX-NOMCG2).                                       
                                                                                
           COMPUTE  K012-NUM-MAX-MCPH3 =                                        
                   (K012-NUM-MAX-PA3 - K012-REG-PA3) -                          
                   (K012-NUM-MAX-MCPG3)  -                                      
                   (K012-NUM-MAX-NOMCH3) -                                      
                   (K012-NUM-MAX-NOMCG3).                                       
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       5000-REWRITE-K012.                                                       
      *****************************************************************         
                                                                                
           REWRITE BP13K012-MCPSUNIT.                                           
                                                                                
           EVALUATE WS-K012-STATUS                                              
               WHEN ZEROES                                                      
                    ADD 1         TO WS-K012-REWRITE                            
               WHEN 02                                                          
                    DISPLAY 'RECORD FOUND IN BP13K012. '                        
                            'K010-CDE-NT-ZONE : ' K010-CDE-NT-ZONE '. '         
                            'K010-CDE-FLAT-TY : ' K010-CDE-FLAT-TY              
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BP13K012. STATUS IS '                
                             WS-K012-STATUS                                     
                    GO TO 9000-CLOSE-ROUTINE                                    
           END-EVALUATE.                                                        
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *****************************************************************         
       9000-CLOSE-ROUTINE.                                                      
      *****************************************************************         
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'RUN DATE : ' WS-EDITED-DATE.                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========BP13C777 CONTROL TOTALS============*'.          
           DISPLAY SPACES.                                                      
           DISPLAY ' F012-READ    :  ' WS-F012-READ.                            
           DISPLAY ' K010-READ    :  ' WS-K010-READ.                            
           DISPLAY ' K010-NOTFND  :  ' WS-K010-NOTFND.                          
           DISPLAY ' K012-FOUND   :  ' WS-K012-FOUND.                           
           DISPLAY ' K012-REWRITE :  ' WS-K012-REWRITE.                         
           DISPLAY SPACES.                                                      
           DISPLAY '*==============================================*'.          
           CLOSE      BP13F012                                                  
                      BP13K012                                                  
                      BP13K010.                                                 
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
