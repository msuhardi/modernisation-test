       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C92W.                                                 
      *AUTHOR.        SMR2.                                                     
      *DATE-WRITTEN.  24/05/2017.                                               
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *                                                             *           
      *   OBJECTIVE   : UPDATE TOTAL FLAT & BOOK COUNT PER PROJECT  *           
      *                 PER FLAT TYPE                               *           
      *                                                             *           
      *   INPUT FILES : BP13FW3E - BOOKING SUMMARY PER PROJECT      *           
      *                 BP13F230 - FLAT LIST FILE                   *           
      *   OUTPUT FILE : P13FW3EA - UPDATED FILE                     *           
      *                                                             *           
      * =========================================================== *           
      * CHQ REQ  DATE       BY   DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP136299 24/05/2017 SMR2 NEW PROGRAM                        *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FW3E ASSIGN       TO BP13FW3E.                            
                                                                                
           SELECT BP13F230 ASSIGN       TO BP13F230.                            
                                                                                
           SELECT P13FW3EA ASSIGN       TO P13FW3EA.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD   BP13FW3E                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 250 CHARACTERS                                      
            LABEL RECORD     IS STANDARD                                        
            RECORDING MODE   IS F.                                              
       COPY BP13FW3E.                                                           
                                                                                
       FD   BP13F230                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 450 CHARACTERS                                      
            LABEL RECORD     IS STANDARD                                        
            RECORDING MODE   IS F.                                              
       COPY BP13K230.                                                           
                                                                                
       FD   P13FW3EA                                                            
            BLOCK CONTAINS    0 RECORDS                                         
            RECORD CONTAINS 250 CHARACTERS                                      
            LABEL RECORD     IS STANDARD                                        
            RECORDING MODE   IS F.                                              
       01   P13FW3EA-REC                    PIC X(250).                         
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-FW3E-READ                 PIC 9(06)   VALUE ZEROES.           
           05  WS-F230-READ                 PIC 9(06)   VALUE ZEROES.           
           05  WS-FW3EA-WRITE               PIC 9(06)   VALUE ZEROES.           
                                                                                
       01  WS-FW3E-COUNTERS.                                                    
           05  WS-FLAT-01                   PIC 9(05)   VALUE ZEROES.           
           05  WS-FLAT-2A                   PIC 9(05)   VALUE ZEROES.           
           05  WS-FLAT-2B                   PIC 9(05)   VALUE ZEROES.           
           05  WS-FLAT-03                   PIC 9(05)   VALUE ZEROES.           
           05  WS-FLAT-04                   PIC 9(05)   VALUE ZEROES.           
           05  WS-FLAT-05                   PIC 9(05)   VALUE ZEROES.           
           05  WS-FLAT-5G                   PIC 9(05)   VALUE ZEROES.           
           05  WS-BOOK-01                   PIC 9(05)   VALUE ZEROES.           
           05  WS-BOOK-2A                   PIC 9(05)   VALUE ZEROES.           
           05  WS-BOOK-2B                   PIC 9(05)   VALUE ZEROES.           
           05  WS-BOOK-03                   PIC 9(05)   VALUE ZEROES.           
           05  WS-BOOK-04                   PIC 9(05)   VALUE ZEROES.           
           05  WS-BOOK-05                   PIC 9(05)   VALUE ZEROES.           
           05  WS-BOOK-5G                   PIC 9(05)   VALUE ZEROES.           
                                                                                
       01  WS-DATE-VARIABLES.                                                   
           05  WS-CURR-DATE                 PIC X(08)   VALUE SPACES.           
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-FW3E-KEY.                                                     
               10  WS-FW3E-NEW-TOWN         PIC X(20)   VALUE SPACES.           
               10  WS-FW3E-NEIGH            PIC X(04)   VALUE SPACES.           
               10  WS-FW3E-CNTRCT           PIC X(04)   VALUE SPACES.           
           05  WS-F230-KEY.                                                     
               10  WS-F230-NEW-TOWN         PIC X(20)   VALUE SPACES.           
               10  WS-F230-NEIGH            PIC X(04)   VALUE SPACES.           
               10  WS-F230-CNTRCT           PIC X(04)   VALUE SPACES.           
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES          THRU 1000-EXIT.                     
           PERFORM 2000-READ-BP13FW3E       THRU 2000-EXIT.                     
           PERFORM 2500-READ-BP13F230       THRU 2500-EXIT.                     
           PERFORM 3000-PROCESS-RECS        THRU 3000-EXIT                      
             UNTIL WS-FW3E-KEY = HIGH-VALUES                                    
               AND WS-F230-KEY = HIGH-VALUES.                                   
           PERFORM 9000-CLOSE-FILES         THRU 9000-EXIT.                     
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13FW3E                                                 
                       BP13F230                                                 
                OUTPUT P13FW3EA.                                                
                                                                                
           MOVE FUNCTION CURRENT-DATE       TO WS-CURR-DATE.                    
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2000-READ-BP13FW3E.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13FW3E AT END                                                 
                MOVE HIGH-VALUES            TO WS-FW3E-KEY                      
                GO TO 2000-EXIT.                                                
                                                                                
           MOVE FW3E-NME-NEW-TOWN           TO WS-FW3E-NEW-TOWN.                
           MOVE FW3E-NEIGHBOURHOOD          TO WS-FW3E-NEIGH.                   
           MOVE FW3E-CONTRACT-NO            TO WS-FW3E-CNTRCT.                  
                                                                                
           ADD 1                            TO WS-FW3E-READ.                    
                                                                                
           MOVE ZEROES                      TO WS-FW3E-COUNTERS.                
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2500-READ-BP13F230.                                                      
      *-------------------*                                                     
                                                                                
           READ BP13F230 AT END                                                 
                MOVE HIGH-VALUES            TO WS-F230-KEY                      
                GO TO 2500-EXIT.                                                
                                                                                
           MOVE K230-NUM-NEW-TOWN4          TO WS-F230-NEW-TOWN.                
           MOVE K230-NUM-NEIGH              TO WS-F230-NEIGH.                   
           MOVE K230-NUM-CNTRCT             TO WS-F230-CNTRCT.                  
                                                                                
           ADD 1                            TO WS-F230-READ.                    
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-RECS.                                                       
      *-----------------*                                                       
                                                                                
           IF WS-FW3E-KEY = WS-F230-KEY                                         
              PERFORM 4000-MOVE-DETAILS        THRU 4000-EXIT                   
                UNTIL WS-F230-KEY > WS-FW3E-KEY                                 
                   OR WS-F230-KEY = HIGH-VALUES                                 
                                                                                
              PERFORM 7000-WRITE-P13FW3EA      THRU 7000-EXIT                   
                                                                                
              PERFORM 2000-READ-BP13FW3E       THRU 2000-EXIT                   
           ELSE                                                                 
              IF WS-FW3E-KEY < WS-F230-KEY                                      
                 PERFORM 2000-READ-BP13FW3E    THRU 2000-EXIT                   
              ELSE                                                              
                 IF WS-FW3E-KEY > WS-F230-KEY                                   
                    PERFORM 2500-READ-BP13F230 THRU 2500-EXIT                   
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *------------------*                                                      
       4000-MOVE-DETAILS.                                                       
      *------------------*                                                      
                                                                                
           EVALUATE K230-NUM-FLAT-TYPE4(1:2)                                    
           WHEN 'SA'                                                            
                ADD 1                       TO WS-FLAT-01                       
                IF K230-DTE-ACCEPT NOT = SPACES AND LOW-VALUES                  
                   ADD 1                    TO WS-BOOK-01                       
                END-IF                                                          
                                                                                
           WHEN '2-'                                                            
                IF K230-QTY-AREA-TOTAL <= 40                                    
                   ADD 1                    TO WS-FLAT-2A                       
                   IF K230-DTE-ACCEPT NOT = SPACES AND LOW-VALUES               
                      ADD 1                 TO WS-BOOK-2A                       
                   END-IF                                                       
                ELSE                                                            
                   ADD 1                    TO WS-FLAT-2B                       
                   IF K230-DTE-ACCEPT NOT = SPACES AND LOW-VALUES               
                      ADD 1                 TO WS-BOOK-2B                       
                   END-IF                                                       
                END-IF                                                          
                                                                                
           WHEN '3-'                                                            
           WHEN '3P'                                                            
                ADD 1                       TO WS-FLAT-03                       
                IF K230-DTE-ACCEPT NOT = SPACES AND LOW-VALUES                  
                   ADD 1                    TO WS-BOOK-03                       
                END-IF                                                          
                                                                                
           WHEN '4-'                                                            
                ADD 1                       TO WS-FLAT-04                       
                IF K230-DTE-ACCEPT NOT = SPACES AND LOW-VALUES                  
                   ADD 1                    TO WS-BOOK-04                       
                END-IF                                                          
                                                                                
           WHEN '5-'                                                            
                IF K230-CDE-MODL-DESG = '90' OR '91' OR '95'                    
                   ADD 1                    TO WS-FLAT-5G                       
                   IF K230-DTE-ACCEPT NOT = SPACES AND LOW-VALUES               
                      ADD 1                 TO WS-BOOK-5G                       
                   END-IF                                                       
                ELSE                                                            
                   ADD 1                    TO WS-FLAT-05                       
                   IF K230-DTE-ACCEPT NOT = SPACES AND LOW-VALUES               
                      ADD 1                 TO WS-BOOK-05                       
                   END-IF                                                       
                END-IF                                                          
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2500-READ-BP13F230       THRU 2500-EXIT.                     
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------*                                                    
       7000-WRITE-P13FW3EA.                                                     
      *--------------------*                                                    
                                                                                
           MOVE WS-FLAT-01                  TO FW3E-NUM-UNIT-01.                
           MOVE WS-FLAT-2A                  TO FW3E-NUM-UNIT-2A.                
           MOVE WS-FLAT-2B                  TO FW3E-NUM-UNIT-2B.                
           MOVE WS-FLAT-03                  TO FW3E-NUM-UNIT-03.                
           MOVE WS-FLAT-04                  TO FW3E-NUM-UNIT-04.                
           MOVE WS-FLAT-05                  TO FW3E-NUM-UNIT-05.                
           MOVE WS-FLAT-5G                  TO FW3E-NUM-UNIT-5G.                
           MOVE WS-BOOK-01                  TO FW3E-NUM-BOOK-01.                
           MOVE WS-BOOK-2A                  TO FW3E-NUM-BOOK-2A.                
           MOVE WS-BOOK-2B                  TO FW3E-NUM-BOOK-2B.                
           MOVE WS-BOOK-03                  TO FW3E-NUM-BOOK-03.                
           MOVE WS-BOOK-04                  TO FW3E-NUM-BOOK-04.                
           MOVE WS-BOOK-05                  TO FW3E-NUM-BOOK-05.                
           MOVE WS-BOOK-5G                  TO FW3E-NUM-BOOK-5G.                
                                                                                
           WRITE P13FW3EA-REC               FROM BP13FW3E-REC.                  
           ADD 1                            TO WS-FW3EA-WRITE.                  
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '*===========================================*'.             
           DISPLAY '  BP13C92E CONTROL TOTAL     : ' WS-CURR-DATE.              
           DISPLAY '*-------------------------------------------*'.             
           DISPLAY '  RECS READ FROM BP13FW3E    : ' WS-FW3E-READ.              
           DISPLAY '  RECS READ FROM BP13F230    : ' WS-F230-READ.              
           DISPLAY '  RECS WRITTEN IN P13FW3EA   : ' WS-FW3EA-WRITE.            
           DISPLAY '*===========================================*'.             
                                                                                
           CLOSE BP13FW3E                                                       
                 BP13F230                                                       
                 P13FW3EA.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
