       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC08.                                                 
       AUTHOR.        SHARON DUMDUM.                                            
       DATE-WRITTEN.  25 APRIL 2006.                                            
      ****************************************************************          
      * OBJECTIVE - TO UPDATE NEW AND TERMINATED GIRO CASES          *          
      *             TO DBSS MASTER.                                  *          
      *                                                              *          
      *  SUBROUTINE     - NIL                                        *          
      *                                                              *          
      *  IDENTICAL PGM  - AB03C228                                   *          
      *--------------------------------------------------------------*          
      *  REF NO    DATE    BY  AMENDMENTS/ENHANCEMENTS               *          
      * -------- -------- ---- --------------------------------------*          
      * BP132845 25042006 SD10 NEW PROGRAM                           *          
      * BP132845 29112006 BA3  INCLUDE ERROR REPORT                  *          
      *          03012007 BA3  INCLUDE REMARKS                       *          
      * BP133091 17102007 RB12 EXPAND KD00 LENGTH FROM 1200 TO 1600  *          
      ****************************************************************          
                                                                                
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT  SY02F001  ASSIGN  TO   SY02F001.                             
                                                                                
           SELECT  AC08F200  ASSIGN  TO   AC08F200.                             
                                                                                
           SELECT  BP13FD00  ASSIGN  TO   BP13FD00.                             
                                                                                
           SELECT  BP13KD00  ASSIGN  TO   BP13KD00                              
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS KD00-KEY-FLD                       
                             FILE STATUS  IS KD00-STATUS                        
                             ACCESS MODE  IS RANDOM.                            
                                                                                
           SELECT  BP13LC08  ASSIGN  TO   BP13LC08.                             
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  AC08F200                    BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 150 CHARACTERS           
                                       LABEL RECORDS ARE STANDARD               
                                       RECORDING MODE IS F.                     
                                                                                
       COPY AC08F200.                                                           
                                                                                
       FD  BP13FD00                    BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 1600 CHARACTERS          
                                       LABEL RECORDS ARE STANDARD.              
       COPY BP13FD00.                                                           
                                                                                
       FD   BP13KD00                   RECORD CONTAINS 1600 CHARACTERS.         
       COPY BP13KD00.                                                           
                                                                                
       FD  BP13LC08                    BLOCK CONTAINS 0 RECORDS                 
                                       RECORD CONTAINS 132 CHARACTERS           
                                       LABEL RECORDS ARE OMITTED                
                                       RECORDING MODE IS F.                     
       01  PRINT-REC                   PIC X(132).                              
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  KD00-STATUS                   PIC 9(2)  VALUE 0.                     
       01  WS-F200-EOF                   PIC X     VALUE 'N'.                   
       01  WS-FD00-EOF                   PIC X     VALUE 'N'.                   
       01  WS-COUNTERS.                                                         
           05  WS-F200-READ-CNT          PIC 9(7)  VALUE 0.                     
           05  WS-FD00-READ-CNT          PIC 9(7)  VALUE 0.                     
           05  WS-KD00-UPD-CNT           PIC 9(7)  VALUE 0.                     
           05  WS-KD00-ERR-CNT           PIC 9(7)  VALUE 0.                     
           05  WS-FD00-BYPASS-UPD-CNT    PIC 9(7)  VALUE 0.                     
           05  WS-CNT-REC-MATCH          PIC 9(7)  VALUE 0.                     
           05  WS-CNT-NOT-FND-FD00       PIC 9(7)  VALUE 0.                     
           05  WS-CNT-NOT-FND-F200       PIC 9(7)  VALUE 0.                     
           05  WS-LINE-CNT               PIC 9(5)  VALUE 60.                    
           05  WS-PAGE-CNT               PIC 9(5)  VALUE 0.                     
           05  WS-LC08-WRITTEN           PIC 9(5)  VALUE 0.                     
                                                                                
       01  LC08-HEAD-01.                                                        
           05  FILLER                  PIC X(8)  VALUE 'BP13LC08'.              
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(8)  VALUE 'HDBCAT 3'.              
           05  FILLER                  PIC X(24) VALUE SPACES.                  
           05  FILLER                  PIC X(20) VALUE                          
                                       'S Y S T E M   O F   '.                  
           05  FILLER                  PIC X(19) VALUE                          
                                       'C O M M I T M E N T'.                   
           05  FILLER                  PIC X(15) VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'DATE :'.                
           05  LC08-DATE               PIC X(10) VALUE SPACES.                  
           05  FILLER                  PIC X(4)  VALUE SPACES.                  
           05  FILLER                  PIC X(7)  VALUE 'PAGE : '.               
           05  LC08-PAGE-NO            PIC ZZZZ9 .                              
                                                                                
       01  LC08-HEAD-02.                                                        
           05  FILLER                  PIC X(36) VALUE SPACES.                  
           05  FILLER                  PIC X(56) VALUE                          
           'ERROR REPORT ON UPDATING NEW/TERMINATED GIRO INFORMATION'.          
                                                                                
       01  LC08-HEAD-03.                                                        
           05  FILLER                  PIC X(11) VALUE SPACES.                  
           05  FILLER                  PIC X(05) VALUE 'S/NO'.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(14) VALUE                          
                                                '  SCH-ACC-NO  '.               
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(09) VALUE                          
                                                'BANK-CODE'.                    
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(11) VALUE                          
                                                'BANK-BRANCH'.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(11) VALUE                          
                                                ' BANK-ACCT '.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  FILLER                  PIC X(10) VALUE                          
                                                'DTE-EFF-MO'.                   
           05  FILLER                  PIC X(14) VALUE SPACES.                  
           05  FILLER                  PIC X(07) VALUE                          
                                                'REMARKS'.                      
                                                                                
       01  LC08-DETAILS.                                                        
           05  FILLER                  PIC X(11) VALUE SPACES.                  
           05  LC08-SN                 PIC ZZZZ9 VALUE ZEROES.                  
           05  FILLER                  PIC X(02) VALUE SPACES.                  
           05  LC08-SCH-NO             PIC X(14) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  LC08-BANK-CODE          PIC X(04) VALUE SPACES.                  
           05  FILLER                  PIC X(09) VALUE SPACES.                  
           05  LC08-BANK-BRANCH        PIC X(03) VALUE SPACES.                  
           05  FILLER                  PIC X(06) VALUE SPACES.                  
           05  LC08-BANK-ACCT          PIC X(11) VALUE SPACES.                  
           05  FILLER                  PIC X(03) VALUE SPACES.                  
           05  LC08-DTE-EFF-MO         PIC X(07) VALUE SPACES.                  
           05  FILLER                  PIC X(04) VALUE SPACES.                  
           05  LC08-REMARKS            PIC X(30) VALUE SPACES.                  
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      ************************************************************              
       0000-MAIN-PARA.                                                          
      ************************************************************              
                                                                                
           PERFORM 1000-START-UP         THRU 1000-EXIT.                        
           PERFORM 1100-READ-AC08F200    THRU 1100-EXIT.                        
           PERFORM 1200-READ-BP13FD00    THRU 1200-EXIT.                        
           PERFORM 2000-MATCH-RECS       THRU 2000-EXIT                         
                   UNTIL WS-F200-EOF = 'Y'.                                     
           PERFORM 9999-WRAP-UP.                                                
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       1000-START-UP.                                                           
      ************************************************************              
                                                                                
           OPEN INPUT  SY02F001                                                 
                       AC08F200                                                 
                       BP13FD00                                                 
                I-O    BP13KD00                                                 
                OUTPUT BP13LC08.                                                
                                                                                
           IF  KD00-STATUS NOT = 0 AND 97                                       
               DISPLAY 'OPEN FAILED FOR BP13KD00 ' KD00-STATUS                  
               MOVE KD00-STATUS TO RETURN-CODE                                  
               GO TO 9999-WRAP-UP.                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
           STRING F001-DTE-CURRENT(7:2) '/'                                     
                  F001-DTE-CURRENT(5:2) '/'                                     
                  F001-DTE-CURRENT(1:4)                                         
                  DELIMITED BY SIZE INTO LC08-DATE.                             
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       1100-READ-AC08F200.                                                      
      ************************************************************              
                                                                                
           READ AC08F200 AT END                                                 
                MOVE 'Y' TO WS-F200-EOF                                         
                DISPLAY 'AC08F200 EOF '                                         
                GO TO 1100-EXIT.                                                
                                                                                
           ADD 1 TO WS-F200-READ-CNT.                                           
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       1200-READ-BP13FD00.                                                      
      ************************************************************              
                                                                                
           READ BP13FD00 AT END                                                 
                MOVE 'Y'         TO WS-FD00-EOF                                 
                MOVE HIGH-VALUES TO FD00-NUM-SCH-ACCNT                          
                DISPLAY 'BP13FD00 EOF '                                         
                GO TO 1200-EXIT.                                                
                                                                                
           ADD 1 TO WS-FD00-READ-CNT.                                           
                                                                                
       1200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       2000-MATCH-RECS.                                                         
      ************************************************************              
                                                                                
           IF FD00-NUM-SCH-ACCNT > F200-NUM-HDB-REF(3:11)                       
              PERFORM 2100-PRINT-ERROR-REPORT THRU 2100-EXIT                    
              ADD 1 TO WS-CNT-NOT-FND-FD00                                      
              PERFORM 1100-READ-AC08F200      THRU 1100-EXIT                    
           ELSE                                                                 
           IF FD00-NUM-SCH-ACCNT < F200-NUM-HDB-REF(3:11)                       
              ADD 1 TO WS-CNT-NOT-FND-F200                                      
              PERFORM 1200-READ-BP13FD00      THRU 1200-EXIT                    
           ELSE                                                                 
              ADD 1 TO WS-CNT-REC-MATCH                                         
              PERFORM 3000-PROCESS-REC        THRU 3000-EXIT                    
              PERFORM 1200-READ-BP13FD00      THRU 1200-EXIT                    
              PERFORM 1100-READ-AC08F200      THRU 1100-EXIT.                   
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************************************************              
       2100-PRINT-ERROR-REPORT.                                                 
      ************************************************************              
                                                                                
           IF WS-LINE-CNT > 50                                                  
              PERFORM 2200-PRINT-HEADING THRU 2200-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE SPACES                     TO LC08-DETAILS.                     
           INITIALIZE                         LC08-DETAILS.                     
                                                                                
           ADD 1                           TO WS-LC08-WRITTEN                   
                                              WS-LINE-CNT.                      
           MOVE WS-LC08-WRITTEN            TO LC08-SN.                          
           STRING F200-NUM-HDB-REF(3:4)  '-'                                    
                  F200-NUM-HDB-REF(7:4)  '-'                                    
                  F200-NUM-HDB-REF(11:2) '-'                                    
                  F200-NUM-HDB-REF(13:1)                                        
                  DELIMITED BY SIZE INTO LC08-SCH-NO.                           
           MOVE F200-NUM-BANK-CODE         TO LC08-BANK-CODE.                   
           MOVE F200-NUM-BANK-BRH          TO LC08-BANK-BRANCH.                 
           MOVE F200-NUM-BANK-ACCT         TO LC08-BANK-ACCT.                   
           STRING F200-DTE-EFF-MTH-MM '/'                                       
                  F200-DTE-EFF-MTH(1:4)                                         
                  DELIMITED BY SIZE INTO LC08-DTE-EFF-MO.                       
           MOVE 'HDB-REF NOT FOUND IN PP MASTER' TO LC08-REMARKS.               
                                                                                
           WRITE PRINT-REC               FROM LC08-DETAILS AFTER 1.             
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************************************************              
       2200-PRINT-HEADING.                                                      
      ************************************************************              
                                                                                
           ADD 1                   TO WS-PAGE-CNT.                              
           MOVE WS-PAGE-CNT        TO LC08-PAGE-NO.                             
                                                                                
           MOVE  SPACES            TO PRINT-REC.                                
           WRITE PRINT-REC AFTER PAGE.                                          
           WRITE PRINT-REC.                                                     
           WRITE PRINT-REC.                                                     
           WRITE PRINT-REC.                                                     
           WRITE PRINT-REC       FROM LC08-HEAD-01.                             
           MOVE  SPACES            TO PRINT-REC.                                
           WRITE PRINT-REC.                                                     
           WRITE PRINT-REC       FROM LC08-HEAD-02 AFTER 1.                     
           WRITE PRINT-REC       FROM LC08-HEAD-03 AFTER 2.                     
                                                                                
           MOVE 8                  TO WS-LINE-CNT.                              
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************************************************              
       3000-PROCESS-REC.                                                        
      ************************************************************              
                                                                                
           IF F200-NUM-STATUS NOT = '1' AND '2' AND 'A'                         
              ADD 1 TO WS-FD00-BYPASS-UPD-CNT                                   
              GO TO 3000-EXIT.                                                  
                                                                                
           PERFORM 3100-READ-BP13KD00        THRU 3100-EXIT.                    
                                                                                
           IF F200-NUM-STATUS = '1' OR '2'                                      
              IF FD00-CDE-ACCT-STAT = 'C'                                       
                 MOVE F200-NUM-BANK-REF   TO KD00-NUM-GIRO-INFO                 
                 MOVE F200-DTE-EFF-MTH    TO KD00-DTE-GIRO-EFF                  
                 MOVE 'Y'                 TO KD00-NUM-GIRO                      
                 PERFORM 3200-REWRITE-BP13KD00  THRU 3200-EXIT                  
              ELSE                                                              
                 ADD 1 TO WS-FD00-BYPASS-UPD-CNT                                
                 GO TO 3000-EXIT.                                               
                                                                                
           IF F200-NUM-STATUS = 'A'                                             
              IF FD00-CDE-ACCT-STAT = 'C'                                       
                 MOVE ZEROES              TO KD00-NUM-GIRO-INFO                 
                 MOVE F200-DTE-EFF-MTH    TO KD00-DTE-GIRO-EFF                  
                 MOVE 'N'                 TO KD00-NUM-GIRO                      
                 PERFORM 3200-REWRITE-BP13KD00  THRU 3200-EXIT                  
              ELSE                                                              
                 ADD 1 TO WS-FD00-BYPASS-UPD-CNT                                
                 GO TO 3000-EXIT.                                               
                                                                                
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       3100-READ-BP13KD00.                                                      
      ************************************************************              
                                                                                
           MOVE SPACES             TO KD00-KEY-FLD.                             
           MOVE FD00-NUM-REGN      TO KD00-NUM-REGN.                            
           READ BP13KD00 KEY IS KD00-KEY-FLD.                                   
                                                                                
           IF KD00-STATUS = 23                                                  
               DISPLAY 'RECORD NOT FOUND ' F200-NUM-HDB-REF                     
               ADD   1  TO WS-KD00-ERR-CNT                                      
           ELSE                                                                 
           IF KD00-STATUS NOT = 0                                               
              MOVE '4'  TO RETURN-CODE                                          
              DISPLAY  'RECORD NOT FOUND ' F200-NUM-HDB-REF                     
              ADD   1   TO WS-KD00-ERR-CNT                                      
              GO        TO 9999-WRAP-UP.                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       3200-REWRITE-BP13KD00.                                                   
      ************************************************************              
                                                                                
           REWRITE BP13KD00-REC.                                                
           IF KD00-STATUS NOT = 0 AND 97                                        
              DISPLAY 'REWRITE ERROR, STATUS : ' KD00-STATUS                    
                      ' KEY : ' FD00-NUM-SCH-ACCNT                              
              ADD 1 TO WS-KD00-ERR-CNT                                          
           ELSE                                                                 
              ADD 1 TO WS-KD00-UPD-CNT.                                         
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      ************************************************************              
       9999-WRAP-UP.                                                            
      ************************************************************              
                                                                                
           DISPLAY  SPACES.                                                     
           DISPLAY '******* CONTROL   TOTALS ********'.                         
           DISPLAY 'PROGRAM-ID : BP13CC08'.                                     
           DISPLAY  SPACES.                                                     
           DISPLAY 'NUMBER OF RECORDS MATCHED                  : '              
                    WS-CNT-REC-MATCH.                                           
           DISPLAY 'NUMBER OF AC08F200 RECORDS READ            : '              
                    WS-F200-READ-CNT.                                           
           DISPLAY 'NUMBER OF AC08F200 RECORDS NOT FOUND       : '              
                    WS-CNT-NOT-FND-F200.                                        
           DISPLAY 'NUMBER OF BP13FD00 RECORDS READ            : '              
                    WS-FD00-READ-CNT.                                           
           DISPLAY 'NUMBER OF BP13FD00 RECORDS NOT FOUND       : '              
                    WS-CNT-NOT-FND-FD00.                                        
           DISPLAY 'NUMBER OF BP13FD00 RECORDS BYPASS UPDATED  : '              
                    WS-FD00-BYPASS-UPD-CNT.                                     
           DISPLAY 'NUMBER OF BP13KD00 RECORDS UPDATED         : '              
                    WS-KD00-UPD-CNT.                                            
           DISPLAY 'NUMBER OF ERRONEOUS RECORDS                : '              
                    WS-KD00-ERR-CNT.                                            
           DISPLAY 'NUMBER OF RECORD WRITTEN INTO BP13LC08     : '              
                    WS-LC08-WRITTEN.                                            
                                                                                
           CLOSE SY02F001                                                       
                 AC08F200                                                       
                 BP13FD00                                                       
                 BP13KD00                                                       
                 BP13LC08.                                                      
                                                                                
           IF  KD00-STATUS NOT = 0                                              
               DISPLAY 'CLOSE FAILED FOR BP13KD00 ' KD00-STATUS.                
                                                                                
           STOP RUN.                                                            
                                                                                
