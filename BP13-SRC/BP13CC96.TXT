       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CC96.                                                 
       AUTHOR.        EDEN DINGAL.                                              
       DATE-WRITTEN.  18/04/2008.                                               
      * ====================================================== *                
      *    SYSTEM NAME : SOC                                   *                
      *    SYSTEM ID   : BP13                                  *                
      *    OBJECTIVE   : TO CREATE NEW CDE-PAYMENT TYPE 'JVA'  *                
      *                  & UPDATE DBS MASTER WHEN AMT-ARR-INTR *                
      *                  IS < 0                                *                
      *                  TO CREATE INTRADJ DEBIT FOR LAST DAY  *                
      *                  OF PREVIOUS MO                        *                
      *                                                        *                
      *    INPUT FILES : BP13FD00                              *                
      *                  SY02F001                              *                
      *    I-O FILE    : BP13KD00                              *                
      *    OUTPUT FILE : BP13F125                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO    BY     DATE      DESCRIPTION                 *                
      * ------    --     ----      -----------                 *                
      * BP133209  ED1   20080418   NEW PGM                     *                
      * BP133314  ED1   20080506   CHGE HEADER TO 'INTR PYMT'  *                
      * BP133489  JB8   20090313   TO CATER FOR FD00-AMT-ARR-INTR               
      *                            > 0                         *                
      * BP133389  JB8   20090320   TO OUTPUT THE RIGHT CRDR    *                
      * BP134154  JB8   20110415   TO CATER FOR RE-FINANCING   *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FD00 ASSIGN       TO BP13FD00.                            
                                                                                
           SELECT BP13F125 ASSIGN       TO BP13F125.                            
                                                                                
           SELECT BP13LC96 ASSIGN       TO BP13LC96.                            
                                                                                
           SELECT SY02F001 ASSIGN       TO SY02F001.                            
                                                                                
           SELECT BP13KD00 ASSIGN TO BP13KD00                                   
                  ACCESS MODE     IS DYNAMIC                                    
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS KD00-KEY-FLD                               
                  FILE STATUS     IS KD00-STATUS.                               
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F125                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F125.                                                           
                                                                                
       FD  BP13FD00                                                             
           RECORD CONTAINS 1600 CHARACTERS                                      
           RECORDING MODE IS F.                                                 
       COPY BP13FD00.                                                           
                                                                                
       FD  BP13KD00                                                             
           RECORD CONTAINS 1600 CHARACTERS.                                     
       COPY BP13KD00.                                                           
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD   BP13LC96                                                            
            RECORD CONTAINS   132 CHARACTERS                                    
            LABEL RECORDS ARE OMITTED                                           
            RECORDING MODE IS F.                                                
       01   PRINT-REC             PIC X(132).                                   
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  CNT-LINE-ON-PAGE       PIC 9(02) VALUE 66.                           
       01  CNT-F125-WRITE         PIC 9(05) VALUE ZEROES.                       
       01  CNT-PRT-CASES          PIC 9(06) VALUE ZEROES.                       
       01  CNT-PAGE               PIC 9(03) VALUE ZEROES.                       
                                                                                
       01 WS-CURR-DATE.                                                         
          05  WS-DATE                     PIC X(8)  VALUE SPACES.               
          05  WS-TIME                     PIC X(8)  VALUE SPACES.               
                                                                                
       01  F125-STATUS                    PIC 9(2)  VALUE ZERO.                 
       01  KD00-STATUS                    PIC 9(2)  VALUE 00.                   
       01  NO-KD00-REWRITE                PIC 9(5)  VALUE 0.                    
       01  NO-F125-WRITE                  PIC 9(5)  VALUE 0.                    
                                                                                
       01  WS-FD00-READ                   PIC 9(5)  VALUE ZERO.                 
       01  WS-F125-READ                   PIC 9(5)  VALUE ZERO.                 
       01  WS-F125-UPDATE                 PIC 9(5)  VALUE ZERO.                 
       01  WS-AMT-ARR-INTR-POS            PIC S9(6)V99  VALUE ZERO.             
       01  WS-FD00-SOC                    PIC S9(7)V99  VALUE ZERO.             
       01  WS-DIFF                        PIC S9(7)V99  VALUE ZERO.             
                                                                                
       01  WS-EOF-FD00                    PIC X     VALUE SPACE.                
           88 EOF-FD00                    VALUE 'Y'.                            
                                                                                
       01  LC96-PR-HEAD-01.                                                     
           05  FILLER             PIC X(08) VALUE 'BP13LC96'.                   
           05  FILLER             PIC X(05) VALUE SPACES.                       
           05  FILLER             PIC X(08) VALUE 'HDBCAT 3'.                   
           05  FILLER             PIC X(24) VALUE SPACES.                       
           05  FILLER             PIC X(39) VALUE                               
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER             PIC X(17) VALUE SPACES.                       
           05  FILLER             PIC X(06) VALUE 'DATE: '.                     
           05  LC96-DATE          PIC X(10).                                    
           05  FILLER             PIC X(03) VALUE SPACES.                       
           05  FILLER             PIC X(06) VALUE 'PAGE :'.                     
           05  LC96-PAGE          PIC ZZ9 .                                     
                                                                                
       01  LC96-PR-HEAD-02.                                                     
           05  FILLER             PIC X(47) VALUE SPACES.                       
           05  FILLER             PIC X(55) VALUE                               
               'REPORT ON ARREAR INTEREST ADJUSTMENT'.                          
                                                                                
       01  LC96-PR-HEAD-03.                                                     
           05  FILLER             PIC X(35)                                     
                  VALUE '  S/NO   REGN NO.   SCH ACCT       '.                  
           05  FILLER             PIC X(36)                                     
                  VALUE 'INTEREST($)    INTR PYMT TODTE($)'.                    
                                                                                
       01  LC96-PR-DETAILS.                                                     
           05  FILLER             PIC X(01) VALUE SPACES.                       
           05  LC96-SNO           PIC ZZZZZ9 .                                  
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LC96-REG-NO        PIC X(08).                                    
           05  FILLER             PIC X(02) VALUE SPACES.                       
           05  LC96-SCH-ACC       PIC X(11).                                    
           05  FILLER             PIC X(01) VALUE SPACES.                       
           05  LC96-AMOUNT        PIC    $$$$,$$9.99DB.                         
           05  FILLER             PIC X(07) VALUE SPACES.                       
           05  LC96-AMT-TODTE     PIC    $$$$,$$9.99DB.                         
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *---------------------------------------------------------------*         
       0000-CONTROL.                                                            
      *---------------------------------------------------------------*         
           PERFORM 1000-OPEN-FILES       THRU 1000-EXIT.                        
           MOVE FUNCTION CURRENT-DATE    TO  WS-CURR-DATE.                      
           PERFORM 2000-READ-BP13FD00    THRU 2000-EXIT.                        
           PERFORM 3000-PROCESS-BP13F125 THRU 3000-EXIT                         
             UNTIL EOF-FD00.                                                    
           PERFORM 9000-CLOSE-FILES      THRU 9000-EXIT.                        
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       1000-OPEN-FILES.                                                         
      *---------------------------------------------------------------*         
           OPEN INPUT  BP13FD00                                                 
                       SY02F001                                                 
                I-O    BP13KD00                                                 
               OUTPUT  BP13F125                                                 
                       BP13LC96.                                                
                                                                                
           IF KD00-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13KD00 - OPEN ERROR, STATUS: ' KD00-STATUS             
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
           READ SY02F001.                                                       
                                                                                
           STRING F001-CURR-DD '/'                                              
                  F001-CURR-MM '/'                                              
                  F001-CURR-CC F001-CURR-YY                                     
           DELIMITED BY SIZE INTO LC96-DATE.                                    
                                                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       2000-READ-BP13FD00.                                                      
      *---------------------------------------------------------------*         
           READ BP13FD00 AT END                                                 
                MOVE 'Y' TO WS-EOF-FD00                                         
                GO TO 2000-EXIT.                                                
           ADD 1 TO WS-FD00-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       3000-PROCESS-BP13F125.                                                   
      *---------------------------------------------------------------*         
           IF FD00-AMT-ARR-INTR IS NOT NUMERIC                                  
              MOVE ZEROS TO FD00-AMT-ARR-INTR                                   
           END-IF.                                                              
                                                                                
           IF (FD00-AMT-ARR-INTR < ZEROS) OR                                    
              (FD00-AMT-ARR-INTR > ZEROS AND                                    
               FD00-AMT-LOAN-BAL = 0 AND                                        
               ((FD00-DTE-EFF-TRANSFER NOT = SPACES AND                         
                                             LOW-VALUES AND ZEROES AND          
                 FD00-DTE-EFF-TRANSFER(1:6) < F001-DTE-CURRENT(1:6)) OR         
                (FD00-DTE-REPUDIATE NOT = SPACES AND                            
                                          LOW-VALUES AND ZEROES AND             
                 FD00-DTE-REPUDIATE(1:6) < F001-DTE-CURRENT(1:6)) OR            
                (FD00-DTE-REFINANCE NOT = SPACES AND                            
                                          LOW-VALUES AND ZEROES AND             
                 FD00-DTE-REFINANCE(1:6) < F001-DTE-CURRENT(1:6))))             
              PERFORM 4000-PROCESS-F125     THRU 4000-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13FD00    THRU 2000-EXIT.                        
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       4000-PROCESS-F125.                                                       
      *---------------------------------------------------------------*         
           MOVE SPACES TO  BP13F125-CASHTRAN.                                   
           INITIALIZE      BP13F125-CASHTRAN.                                   
           MOVE FD00-NUM-REGN      TO F125-NUM-REGN.                            
           MOVE FD00-NUM-SCH-ACCNT TO F125-NUM-SCH-ACCT.                        
           MOVE FD00-AMT-ARR-INTR  TO F125-AMT-INTEREST.                        
           IF FD00-AMT-ARR-INTR <= 0                                            
              MOVE 'D'             TO F125-CDE-CRDR                             
           ELSE                                                                 
              MOVE 'C'             TO F125-CDE-CRDR                             
           END-IF.                                                              
           MOVE '69'               TO F125-CDE-TRANS-TYPE.                      
           MOVE 'JVA'              TO F125-CDE-PAYMENT-TYPE.                    
           MOVE 'INTEREST'         TO F125-NUM-RECPT-JRNO.                      
           MOVE F001-DTE-LAST-DAY-PREV TO F125-DTE-TRANS.                       
           MOVE F001-DTE-LAST-DAY-PREV TO F125-DTE-POST.                        
                                                                                
           PERFORM 4100-READ-KD00     THRU 4100-EXIT.                           
           PERFORM 5000-PRINT-REPORT  THRU 5000-EXIT.                           
                                                                                
           WRITE BP13F125-CASHTRAN.                                             
                                                                                
           ADD 1   TO NO-F125-WRITE.                                            
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------------------------------------------------------*         
       4100-READ-KD00.                                                          
      *---------------------------------------------------------------*         
            MOVE SPACES        TO KD00-KEY-FLD.                                 
            MOVE FD00-KEY-FLD  TO KD00-KEY-FLD.                                 
                                                                                
            READ BP13KD00.                                                      
            EVALUATE KD00-STATUS                                                
            WHEN 00                                                             
                 IF KD00-AMT-TOT-INTR-TODTE IS NOT NUMERIC                      
                    MOVE ZEROS TO KD00-AMT-TOT-INTR-TODTE                       
                 END-IF                                                         
                                                                                
                 MOVE ZEROS    TO KD00-AMT-ARR-INTR                             
                                                                                
                 COMPUTE KD00-AMT-TOT-INTR-TODTE =                              
                         KD00-AMT-TOT-INTR-TODTE + FD00-AMT-ARR-INTR            
                                                                                
                 MOVE WS-DATE        TO KD00-DTE-UPDATE                         
                 MOVE WS-TIME        TO KD00-TME-UPDATE                         
                 MOVE 'BP13CC96'     TO KD00-NUM-USERID                         
                                                                                
                 PERFORM 4200-UPD-KD00  THRU 4200-EXIT                          
                                                                                
            WHEN 23                                                             
                 DISPLAY 'BP13KD00 - RECORD NOT FOUND, KEY: '                   
                          KD00-KEY-FLD                                          
            WHEN OTHER                                                          
                 DISPLAY 'BP13KD00 - ERROR IN READ, STATUS: '                   
                          KD00-STATUS                                           
            END-EVALUATE.                                                       
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       4200-UPD-KD00.                                                           
      *---------------------------------------------------------------*         
            REWRITE BP13KD00-REC.                                               
            EVALUATE KD00-STATUS                                                
            WHEN 00                                                             
              ADD 1 TO NO-KD00-REWRITE                                          
                                                                                
            WHEN OTHER                                                          
                 DISPLAY 'BP13KD00 - REWRITE ERROR, STATUS: '                   
                          KD00-STATUS                                           
                          ' KEY IS ' KD00-KEY-FLD                               
            END-EVALUATE.                                                       
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       5000-PRINT-REPORT.                                                       
      *---------------------------------------------------------------*         
           MOVE SPACES TO LC96-PR-DETAILS.                                      
           INITIALIZE     LC96-PR-DETAILS.                                      
                                                                                
           IF CNT-LINE-ON-PAGE > 46                                             
              MOVE 1 TO CNT-LINE-ON-PAGE                                        
              PERFORM 5100-REPORT-HEADING THRU 5100-EXIT                        
           ELSE                                                                 
              ADD 1 TO CNT-LINE-ON-PAGE                                         
           END-IF.                                                              
                                                                                
           ADD 1 TO CNT-PRT-CASES.                                              
                                                                                
           MOVE CNT-PRT-CASES       TO LC96-SNO.                                
           MOVE F125-NUM-REGN       TO LC96-REG-NO.                             
           MOVE F125-NUM-SCH-ACCT   TO LC96-SCH-ACC.                            
                                                                                
           IF F125-AMT-INTEREST NOT NUMERIC                                     
              MOVE ZEROES           TO F125-AMT-INTEREST                        
           END-IF.                                                              
           MOVE F125-AMT-INTEREST        TO LC96-AMOUNT.                        
           MOVE KD00-AMT-TOT-INTR-TODTE  TO LC96-AMT-TODTE.                     
                                                                                
           WRITE PRINT-REC          FROM LC96-PR-DETAILS.                       
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------*                                                    
       5100-REPORT-HEADING.                                                     
      *--------------------*                                                    
            ADD 1           TO CNT-PAGE.                                        
            MOVE CNT-PAGE   TO LC96-PAGE.                                       
            MOVE SPACES     TO PRINT-REC.                                       
            WRITE PRINT-REC AFTER PAGE.                                         
            WRITE PRINT-REC FROM LC96-PR-HEAD-01 AFTER 2.                       
            WRITE PRINT-REC FROM LC96-PR-HEAD-02 AFTER 3.                       
            WRITE PRINT-REC FROM LC96-PR-HEAD-03 AFTER 3.                       
            MOVE ALL '-'    TO PRINT-REC.                                       
            WRITE PRINT-REC.                                                    
            MOVE SPACES     TO PRINT-REC.                                       
            WRITE PRINT-REC AFTER 1.                                            
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
                                                                                
      *---------------------------------------------------------------*         
       9000-CLOSE-FILES.                                                        
      *---------------------------------------------------------------*         
           CLOSE BP13FD00                                                       
                 BP13KD00                                                       
                 BP13F125                                                       
                 SY02F001.                                                      
                                                                                
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY WS-DATE.                                                     
           DISPLAY '*================ BP13CC96 ================*'.              
           DISPLAY '   NO. OF FD00 RECORDS READ    = ' WS-FD00-READ.            
           DISPLAY '   NO. OF KD00 RECORDS UPDATED = ' NO-KD00-REWRITE.         
           DISPLAY '   NO. OF F125 RECORDS CREATED = ' NO-F125-WRITE.           
           DISPLAY '*==========================================*'.              
           STOP RUN.                                                            
                                                                                
           IF KD00-STATUS NOT = 00 AND 97                                       
              DISPLAY 'BP13KD00 - CLOSING ERROR, STATUS: ' KD00-STATUS          
           END-IF.                                                              
                                                                                
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
                                                                                
