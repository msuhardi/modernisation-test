      *--------------------------*                                              
       IDENTIFICATION DIVISION.                                                 
      *--------------------------*                                              
       PROGRAM-ID.    BP13C22U.                                                 
      *AUTHOR.        JIJO FRANCIS.                                             
      *DATE-WRITTEN.  07 JUN 2005.                                              
      * ============================================================ *          
      * SYSTEM OF COMMITMENT (BP13)                                  *          
      * ============================================================ *          
      *    OBJECTIVE  :                                              *          
      *      1.  PROGRAM TO WRITE LIST OF REJECTED UNITS             *          
      *                                                              *          
      *     INPUT FILES:                                             *          
      *           1. BP13F205                                        *          
      *           2. BP13K248                                        *          
      *           3. BP13K060                                        *          
      *           4. BM06K100                                        *          
      *           5. BM06K110                                        *          
      *           6. BB14K500                                        *          
      *           7. BB14K580                                        *          
      *                                                              *          
      *     OUTPUT FILES:                                            *          
      *           1. BP13F999                                        *          
      *                                                              *          
      * ------------------------------------------------------------ *          
      *  CHG NO     DATE     BY   DESCRIPTION                        *          
      * -------- ----------  ---  ---------------------------------- *          
      * BP132688 07/06/2005  JF4  NEW PROGRAM                        *          
      * BP132688 07/07/2005  LMS  TO CHECK ALLOCATION DATE           *          
      * BP132835 20/03/2006  LMS  TO CHANGE CURR SP TO OFFER SP      *          
      * BP132878 09/06/2006  GAN  REMOVAL OF F999-DTE-UPDATE         *          
      * BP132874 20/06/2006  LMS  TO CHANGE OFFER SP TO CURR SP      *          
      * BP133390 30/07/2008  LMS  TO EXPAND BP13F999 FROM 200 TO 300 *          
      * BP134731 09/11/2012  PCL4 TO CATER FOR MODL-DESIGN 89        *          
      * BP135838 09/11/2012  ESA1 TO CATER FOR MODL-DESIGN 92        *          
      * ============================================================ *          
                                                                                
      *--------------------------*                                              
       ENVIRONMENT DIVISION.                                                    
      *--------------------------*                                              
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13K060 ASSIGN       TO BP13K060                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K060-KEY-FLD                         
                           FILE STATUS  IS BP13K060-STATUS.                     
                                                                                
           SELECT BP13K260 ASSIGN       TO BP13K260                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K260-KEY-FLD                         
                           FILE STATUS  IS BP13K260-STATUS.                     
                                                                                
           SELECT BM06K100 ASSIGN       TO BM06K100                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K100-KEY-FLD                         
                           FILE STATUS  IS BM06K100-STATUS.                     
                                                                                
           SELECT BM06K110 ASSIGN       TO BM06K110                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K110-KEY-FLD                         
                           FILE STATUS  IS BM06K110-STATUS.                     
                                                                                
           SELECT BB14K500 ASSIGN       TO BB14K500                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K500-KEY-FLD                         
                           FILE STATUS  IS BB14K500-STATUS.                     
                                                                                
           SELECT BB14K580 ASSIGN       TO BB14K580                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K580-KEY-FLD                         
                           FILE STATUS  IS BB14K580-STATUS.                     
                                                                                
           SELECT BP13F248 ASSIGN       TO BP13F248.                            
                                                                                
           SELECT BP13F999 ASSIGN       TO BP13F999.                            
                                                                                
           SELECT BP13F205 ASSIGN       TO BP13F205.                            
                                                                                
      *--------------------------*                                              
       DATA DIVISION.                                                           
      *--------------------------*                                              
       FILE SECTION.                                                            
                                                                                
       FD   BP13K060                                                            
            RECORD CONTAINS   25 CHARACTERS.                                    
       COPY BP13K060.                                                           
                                                                                
       FD   BP13K260                                                            
            RECORD CONTAINS  100 CHARACTERS.                                    
       COPY BP13K260.                                                           
                                                                                
       FD   BB14K500                                                            
            RECORD CONTAINS  400 CHARACTERS.                                    
       COPY BB14K500.                                                           
                                                                                
       FD   BB14K580                                                            
            RECORD CONTAINS  300 CHARACTERS.                                    
       COPY BB14K580.                                                           
                                                                                
       FD   BM06K100                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BM06K100.                                                           
                                                                                
       FD   BM06K110                                                            
            RECORD CONTAINS  500 CHARACTERS.                                    
       COPY BM06K110.                                                           
                                                                                
       FD   BP13F248                                                            
            BLOCK  CONTAINS    0 RECORDS                                        
            LABEL  RECORDS   ARE STANDARD                                       
            RECORDING MODE    IS F                                              
            RECORD CONTAINS  150 CHARACTERS.                                    
       COPY BP13K248.                                                           
                                                                                
       FD   BP13F999                                                            
            BLOCK CONTAINS      0 RECORDS                                       
            LABEL RECORDS     ARE STANDARD                                      
            RECORDING MODE     IS F                                             
            RECORD CONTAINS   300 CHARACTERS.                                   
       COPY BP13F999.                                                           
                                                                                
       FD   BP13F205                                                            
            BLOCK  CONTAINS 0   RECORDS                                         
            LABEL  RECORDS  ARE STANDARD                                        
            RECORDING MODE IS F                                                 
            RECORD CONTAINS 80  CHARACTERS.                                     
       COPY BP13F205.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-STATUS.                                                      
           05  BP13K248-STATUS              PIC 9(02)  VALUE ZEROS.             
           05  BP13K060-STATUS              PIC 9(02)  VALUE ZEROS.             
           05  BP13K260-STATUS              PIC 9(02)  VALUE ZEROS.             
           05  BM06K100-STATUS              PIC 9(02)  VALUE ZEROS.             
           05  BM06K110-STATUS              PIC 9(02)  VALUE ZEROS.             
           05  BB14K500-STATUS              PIC 9(02)  VALUE ZEROS.             
           05  BB14K580-STATUS              PIC 9(02)  VALUE ZEROS.             
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F205-READ-CNT             PIC 9(08)  VALUE ZEROS.             
           05  WS-F248-READ-CNT             PIC 9(08)  VALUE ZEROS.             
           05  WS-F999-WRITE-CNT            PIC 9(08)  VALUE ZEROS.             
           05  WS-K110-NOTFND-CNT           PIC 9(08)  VALUE ZEROS.             
           05  WS-K100-NOTFND-CNT           PIC 9(08)  VALUE ZEROS.             
           05  WS-K500-NOTFND-CNT           PIC 9(08)  VALUE ZEROS.             
           05  WS-PIDB-NOTFND-CNT           PIC 9(08)  VALUE ZEROS.             
                                                                                
       01  WS-FILE-VARIABLES.                                                   
           05 WS-K500-DTE-EST-CMPL          PIC X(08)  VALUE SPACES.            
           05 WS-K500-DTE-LSE-COMM          PIC X(08)  VALUE SPACES.            
                                                                                
       01  WS-VARIABLES.                                                        
           05 WS-CURRENT-DTE                PIC 9(08)  VALUE ZEROES.            
           05 WS-DIFF-LSE-COMM              PIC S9(08) VALUE ZEROES.            
           05 WS-TMP-DTE-LSE-COMM           PIC 9(08)  VALUE ZEROES.            
           05 WS-NUM-UPG-STATUS             PIC X(03)  VALUE SPACES.            
           05 WS-TMP-DATE                   PIC 9(08)  VALUE ZEROES.            
           05 WS-CURR-DATE                  PIC 9(08)  VALUE ZEROES.            
           05 WS-F999-WRITE-TAG             PIC X(01)  VALUE SPACE.             
           05 WS-EDT-TMP                    PIC Z(7)9.                          
           05 WS-DEL-POSSN-DATE.                                                
              10  WS-DEL-POSSN-YYYY         PIC X(4).                           
              10  WS-DEL-POSSN-MM           PIC X(2).                           
              10  WS-DEL-POSSN-DD           PIC X(2).                           
           05  WS-8-DATE-CHAR               PIC X(8).                           
           05  WS-8-DATE  REDEFINES WS-8-DATE-CHAR.                             
               10  WS-8-DATE-YYYY           PIC 9(4).                           
               10  WS-8-DATE-MM             PIC 9(2).                           
               10  WS-8-DATE-DD             PIC 9(2).                           
           05  WS-DAY                       PIC X(2).                           
           05  WS-MONTH                     PIC X(2).                           
               88  MTH-END-WITH-30    VALUE '04' '06' '09' '11'.                
               88  MTH-END-WITH-28    VALUE '02'.                               
                                                                                
       01  WS-FILE-FLAGS.                                                       
           05  WS-BP13F205-END-TAG          PIC X(01)  VALUE SPACE.             
               88 BP13F205-EOF              VALUE 'Y'.                          
           05  WS-BP13F248-END-TAG          PIC X(01)  VALUE SPACE.             
               88 BP13F248-EOF              VALUE 'Y'.                          
           05  WS-BM06K110-FND-TAG          PIC X(01)  VALUE SPACE.             
               88 BM06K110-FOUND            VALUE 'Y'.                          
           05  WS-BM06K100-FND-TAG          PIC X(01)  VALUE SPACE.             
               88 BM06K100-FOUND            VALUE 'Y'.                          
           05  WS-BP13K060-FND-TAG          PIC X(01)  VALUE SPACE.             
               88 BP13K060-FOUND            VALUE 'Y'.                          
           05  WS-BP13K260-FND-TAG          PIC X(01)  VALUE SPACE.             
               88 BP13K260-FOUND            VALUE 'Y'.                          
           05  WS-BB14K500-FND-TAG          PIC X(01)  VALUE SPACE.             
               88 BB14K500-FOUND            VALUE 'Y'.                          
           05  WS-BB14K580-FND-TAG          PIC X(01)  VALUE SPACE.             
               88 BB14K580-FOUND            VALUE 'Y'.                          
           05  WS-PIDB-FND-TAG              PIC X(01)  VALUE SPACE.             
               88 PIDB-FOUND                VALUE 'Y'.                          
                                                                                
       01  WS-EDT-DATE.                                                         
           05  WS-EDT-DD                    PIC 9(02).                          
           05  FILLER                       PIC X      VALUE '/'.               
           05  WS-EDT-MM                    PIC 9(02).                          
           05  FILLER                       PIC X      VALUE '/'.               
           05  WS-EDT-CCYY                  PIC 9(04).                          
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CCYY                  PIC 9(04).                          
           05  WS-CUR-MM                    PIC 9(02).                          
           05  WS-CUR-DD                    PIC 9(02).                          
                                                                                
       01  WS-LCOMM-DTE.                                                        
           05  WS-LCOMM-YYYY                 PIC 9(4).                          
           05  WS-LCOMM-MM                   PIC 9(2).                          
           05  WS-LCOMM-DD                   PIC 9(2).                          
                                                                                
       01  WS-YEAR-COMM                      PIC 99.                            
       01  WS-MONTH-COMM                     PIC 99.                            
                                                                                
      *----------------------------------*                                      
      *    LINKAGE FOR BP13C913          *                                      
      *----------------------------------*                                      
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH   PIC X(4).                                  
               10  WS-LINK-NUM-ACC   PIC X(5).                                  
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      *------------------------------------------------*                        
      *    LINK-AREA TO CALL SUB-PROGRAM (DJ02C906)    *                        
      *------------------------------------------------*                        
       01  LINK-AREA.                                                           
           05  LINK-SQL-RETCODE             PIC S9(4).                          
           05  LINK-NUM-SCHM                PIC X(4).                           
           05  LINK-NUM-ACCNT               PIC X(7).                           
           05  LINK-NUM-BNDRY-STATUS        PIC X(3).                           
                                                                                
       01  WS-SQL-RETCODE                   PIC -9(3).                          
                                                                                
                                                                                
      *----------------------*                                                  
       PROCEDURE DIVISION.                                                      
      *----------------------*                                                  
                                                                                
      *--------------------------------------------------------------*          
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------------------------------------*          
           PERFORM 1000-OPEN-FILES         THRU 1000-EXIT.                      
           PERFORM 1500-READ-BP13F205      THRU 1500-EXIT.                      
           PERFORM 2000-READ-BP13F248      THRU 2000-EXIT.                      
           PERFORM 3000-PROCESS-REC        THRU 3000-EXIT                       
                                           UNTIL BP13F248-EOF.                  
           PERFORM 9000-CLOSE-ROUTINE      THRU 9000-EXIT.                      
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       1000-OPEN-FILES.                                                         
      *--------------------------------------------------------------*          
           OPEN INPUT  BP13F205                                                 
                       BP13F248                                                 
                       BP13K060                                                 
                       BP13K260                                                 
                       BM06K100                                                 
                       BM06K110                                                 
                       BB14K500                                                 
                       BB14K580                                                 
                OUTPUT BP13F999.                                                
                                                                                
           IF BP13K060-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BP13K060 STATUS ' BP13K060-STATUS            
              MOVE BP13K060-STATUS       TO   RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF BP13K260-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BP13K260 STATUS ' BP13K260-STATUS            
              MOVE BP13K260-STATUS       TO   RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF BM06K100-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BM06K100 STATUS ' BM06K100-STATUS            
              MOVE BM06K100-STATUS       TO   RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BM06K110 STATUS ' BM06K110-STATUS            
              MOVE BM06K110-STATUS       TO   RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF BB14K500-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BB14K500 STATUS ' BB14K500-STATUS            
              MOVE BB14K500-STATUS       TO   RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF BB14K580-STATUS NOT = ZEROS AND 97                                
              DISPLAY 'OPEN ERROR, BB14K580 STATUS ' BB14K580-STATUS            
              MOVE BB14K580-STATUS       TO   RETURN-CODE                       
              PERFORM 9000-CLOSE-ROUTINE THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CUR-DATE.                           
           MOVE WS-CUR-CCYY           TO WS-EDT-CCYY.                           
           MOVE WS-CUR-MM             TO WS-EDT-MM.                             
           MOVE WS-CUR-DD             TO WS-EDT-DD.                             
                                                                                
           MOVE WS-CUR-DATE           TO WS-CURRENT-DTE.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-----------------------------------------------------------              
       1500-READ-BP13F205.                                                      
      *-----------------------------------------------------------              
           READ BP13F205 AT END                                                 
                          MOVE 'Y'    TO WS-BP13F205-END-TAG                    
                         NOT AT END                                             
                          ADD 1       TO WS-F205-READ-CNT                       
           END-READ.                                                            
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       2000-READ-BP13F248.                                                      
      *--------------------------------------------------------------*          
           READ BP13F248 AT END                                                 
                          MOVE 'Y'    TO WS-BP13F248-END-TAG                    
                         NOT AT END                                             
                          ADD 1       TO WS-F248-READ-CNT                       
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       3000-PROCESS-REC.                                                        
      *--------------------------------------------------------------*          
           IF K248-DTE-ALLOC = F205-DTE-ALLOCN                                  
              PERFORM 3050-PROCESS-BP13F999 THRU 3050-EXIT                      
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BP13F248      THRU 2000-EXIT.                      
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       3050-PROCESS-BP13F999.                                                   
      *--------------------------------------------------------------*          
           MOVE 'Y'       TO  WS-F999-WRITE-TAG.                                
                                                                                
           MOVE SPACES    TO  BP13F999-RECORD.                                  
           INITIALIZE         BP13F999-RECORD.                                  
                                                                                
           MOVE K248-NUM-SCH-ACC           TO   K110-KEY-FLD.                   
           PERFORM 3100-READ-BM06K110      THRU 3100-EXIT.                      
           IF BM06K110-FOUND                                                    
              MOVE K110-AIX1               TO   K100-KEY-FLD                    
              PERFORM 3300-READ-BM06K100   THRU 3300-EXIT                       
              IF NOT BM06K110-FOUND                                             
                 ADD  1                    TO   WS-K100-NOTFND-CNT              
                 MOVE 'N'                  TO   WS-F999-WRITE-TAG               
              END-IF                                                            
           ELSE                                                                 
              ADD  1                       TO   WS-K110-NOTFND-CNT              
              MOVE 'N'                     TO   WS-F999-WRITE-TAG               
           END-IF.                                                              
                                                                                
           PERFORM 3500-CALL-BP13C913      THRU 3500-EXIT.                      
           IF  NOT PIDB-FOUND                                                   
              ADD  1                       TO   WS-PIDB-NOTFND-CNT              
              MOVE 'N'                     TO   WS-F999-WRITE-TAG               
           END-IF.                                                              
                                                                                
           PERFORM 3600-CALL-DJ02C906      THRU 3600-EXIT.                      
                                                                                
           MOVE K100-CDE-BLK               TO K500-CDE-BLK.                     
           PERFORM 3700-READ-BB14K500      THRU 3700-EXIT.                      
           IF BB14K500-FOUND                                                    
              MOVE K500-DTE-BLDG-COMPLETE   TO WS-K500-DTE-EST-CMPL             
              MOVE K500-DTE-LEASE-COMM-FLAT TO WS-K500-DTE-LSE-COMM             
           ELSE                                                                 
              MOVE ZEROES                   TO WS-K500-DTE-EST-CMPL             
                                               WS-K500-DTE-LSE-COMM             
              ADD  1                        TO WS-K500-NOTFND-CNT               
              MOVE 'N'                      TO WS-F999-WRITE-TAG                
           END-IF.                                                              
                                                                                
           IF WS-F999-WRITE-TAG = 'Y'                                           
              IF K110-DTE-KEY-AVAIL = ZEROES OR SPACES OR LOW-VALUES            
                 MOVE WS-K500-DTE-EST-CMPL TO F999-DTE-ECD                      
                 PERFORM 4000-GET-DLVRY-POSSN-DATE  THRU 4000-EXIT              
                 MOVE WS-DEL-POSSN-DATE    TO F999-DTE-DLVRY-POSSESSION         
              ELSE                                                              
                 IF WS-K500-DTE-LSE-COMM = ZEROES OR SPACES OR                  
                                           LOW-VALUES                           
                    PERFORM 4100-GET-COMM-DATE THRU 4100-EXIT                   
                    MOVE WS-K500-DTE-LSE-COMM TO WS-TMP-DTE-LSE-COMM            
                 ELSE                                                           
                    MOVE WS-K500-DTE-LSE-COMM TO WS-TMP-DTE-LSE-COMM            
                 END-IF                                                         
                 PERFORM 4300-CHK-COMM-DATE   THRU 4300-EXIT                    
                 MOVE SPACES                  TO K580-KEY-FLD                   
                 MOVE K248-NUM-SCH-ACC        TO K580-KEY-FLD                   
                 PERFORM 4500-READ-BB14K580   THRU 4500-EXIT                    
                 IF K580-DTE-UNIT-DELIVERY NOT = ZEROES AND SPACES AND          
                                                 LOW-VALUES                     
                    MOVE SPACES           TO F999-DTE-DLVRY-POSSESSION          
                 END-IF                                                         
              END-IF                                                            
                                                                                
              PERFORM 5000-WRITE-BP13F999     THRU 5000-EXIT                    
           END-IF.                                                              
                                                                                
       3050-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       3100-READ-BM06K110.                                                      
      *--------------------------------------------------------------*          
           READ BM06K110.                                                       
                                                                                
           EVALUATE BM06K110-STATUS                                             
             WHEN 00                                                            
               MOVE  'Y'               TO   WS-BM06K110-FND-TAG                 
             WHEN 23                                                            
               MOVE  SPACE             TO   WS-BM06K110-FND-TAG                 
               DISPLAY 'BM06K110 REC NOT FOUND, KEY : ' K110-KEY-FLD            
             WHEN OTHER                                                         
               DISPLAY ' BM06K110 READ ERROR, STATUS ' BM06K110-STATUS          
                       '  KEY : ' K110-KEY-FLD                                  
               MOVE BM06K110-STATUS    TO   RETURN-CODE                         
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       3300-READ-BM06K100.                                                      
      *--------------------------------------------------------------*          
           READ BM06K100.                                                       
                                                                                
           EVALUATE BM06K100-STATUS                                             
             WHEN 00                                                            
               MOVE  'Y'               TO   WS-BM06K100-FND-TAG                 
             WHEN 23                                                            
               MOVE  SPACE             TO   WS-BM06K100-FND-TAG                 
               DISPLAY 'BM06K100 REC NOT FOUND, KEY : ' K100-KEY-FLD            
             WHEN OTHER                                                         
               DISPLAY ' BM06K100 READ ERROR, STATUS ' BM06K100-STATUS          
                       '  KEY : ' K100-KEY-FLD                                  
               MOVE BM06K100-STATUS    TO   RETURN-CODE                         
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       3500-CALL-BP13C913.                                                      
      *--------------------------------------------------------------*          
           MOVE SPACES               TO BP13COMM8-REC.                          
           INITIALIZE                   BP13COMM8-REC.                          
           MOVE K248-NUM-SCH-ACC     TO WS-LINK-REC.                            
           CALL 'BP13C913'  USING WS-LINK-REC,                                  
                                  BP13COMM8-REC.                                
                                                                                
           IF COMM8-CDE-SYSERR NOT = ZEROES                                     
              IF COMM8-CDE-SYSERR = +100                                        
                 MOVE SPACE          TO WS-PIDB-FND-TAG                         
                 DISPLAY 'SCH-ACC NOT FOUND IN PIDB : '                         
                                                 K248-NUM-SCH-ACC               
              ELSE                                                              
                 DISPLAY 'ERROR CALLING BP13C913 : ' COMM8-CDE-SYSERR           
                 MOVE '99'                       TO  RETURN-CODE                
                 PERFORM 9000-CLOSE-ROUTINE                                     
              END-IF                                                            
           ELSE                                                                 
              MOVE 'Y'               TO WS-PIDB-FND-TAG                         
           END-IF.                                                              
                                                                                
       3500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       3600-CALL-DJ02C906.                                                      
      *--------------------------------------------------------------*          
      *-----------------------------------*                                     
      *   GET UPGRADING STATUS            *                                     
      *-----------------------------------*                                     
           MOVE SPACES      TO LINK-AREA.                                       
           INITIALIZE          LINK-AREA.                                       
           MOVE K248-NUM-SCH-ACC(1:4) TO LINK-NUM-SCHM.                         
           MOVE K248-NUM-SCH-ACC(5:7) TO LINK-NUM-ACCNT.                        
                                                                                
           MOVE SPACES        TO WS-NUM-UPG-STATUS.                             
           CALL 'DJ02C906' USING LINK-AREA.                                     
                                                                                
           IF LINK-SQL-RETCODE = 0                                              
              MOVE LINK-NUM-BNDRY-STATUS  TO WS-NUM-UPG-STATUS                  
           ELSE                                                                 
              IF LINK-SQL-RETCODE = 100                                         
                 MOVE SPACES              TO WS-NUM-UPG-STATUS                  
                 DISPLAY 'SCH-ACC NOT FOUND IN PIDB(DJ02C906) : '               
                                                K248-NUM-SCH-ACC                
              ELSE                                                              
                 MOVE SPACES              TO WS-NUM-UPG-STATUS                  
                 MOVE LINK-SQL-RETCODE    TO WS-SQL-RETCODE                     
                 DISPLAY 'ERROR CALLING DJ02C906 : ' WS-SQL-RETCODE             
              END-IF                                                            
           END-IF.                                                              
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       3700-READ-BB14K500.                                                      
      *--------------------------------------------------------------*          
           READ BB14K500.                                                       
                                                                                
           EVALUATE BB14K500-STATUS                                             
             WHEN 00                                                            
               MOVE  'Y'               TO   WS-BB14K500-FND-TAG                 
             WHEN 23                                                            
               MOVE  SPACE             TO   WS-BB14K500-FND-TAG                 
               DISPLAY 'BB14K500 REC NOT FOUND, KEY : ' K500-KEY-FLD            
             WHEN OTHER                                                         
               DISPLAY ' BB14K500 READ ERROR, STATUS ' BB14K500-STATUS          
                       '  KEY : ' K500-KEY-FLD                                  
               MOVE BB14K500-STATUS    TO   RETURN-CODE                         
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       3700-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       4000-GET-DLVRY-POSSN-DATE.                                               
      *--------------------------------------------------------------*          
      *---------------------------------------------------------------*         
      * COMPUTE DELIVERY POSSESSION DATE. IF COMPLETION DATE NO BLANK *         
      * ADD 12 MONTHS TO COMPLETION DATE.                             *         
      *---------------------------------------------------------------*         
           MOVE SPACES      TO WS-DEL-POSSN-DATE.                               
                                                                                
           IF K500-DTE-BLDG-COMPLETE = ZEROES OR SPACES OR LOW-VALUES           
              MOVE SPACES   TO F999-CDE-KEY-AVAIL                               
           ELSE                                                                 
              MOVE K500-DTE-BLDG-COMPLETE TO WS-8-DATE-CHAR                     
              ADD 12         TO WS-8-DATE-MM                                    
              IF WS-8-DATE-MM > 12                                              
                 ADD 1                    TO WS-8-DATE-YYYY                     
                 SUBTRACT 12            FROM WS-8-DATE-MM                       
                 MOVE WS-8-DATE-MM        TO WS-MONTH                           
                 MOVE WS-8-DATE-DD        TO WS-DAY                             
                 PERFORM 4010-CHECK-DAY THRU 4010-EXIT                          
                 MOVE WS-DAY              TO WS-DEL-POSSN-DD                    
                 MOVE WS-8-DATE-MM        TO WS-DEL-POSSN-MM                    
                 MOVE WS-8-DATE-YYYY      TO WS-DEL-POSSN-YYYY                  
              ELSE                                                              
                 MOVE WS-8-DATE-MM        TO WS-MONTH                           
                 MOVE WS-8-DATE-DD        TO WS-DAY                             
                 PERFORM 4010-CHECK-DAY THRU 4010-EXIT                          
                 MOVE WS-DAY              TO WS-DEL-POSSN-DD                    
                 MOVE WS-8-DATE-MM        TO WS-DEL-POSSN-MM                    
                 MOVE WS-8-DATE-YYYY      TO WS-DEL-POSSN-YYYY                  
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       4010-CHECK-DAY.                                                          
      *--------------------------------------------------------------*          
           IF MTH-END-WITH-30                                                   
              IF WS-DAY = '31'                                                  
                 MOVE '30'   TO WS-DAY                                          
              END-IF                                                            
           ELSE                                                                 
              IF MTH-END-WITH-28                                                
                 IF WS-DAY = '29' OR '30' OR '31'                               
                    MOVE '28'   TO WS-DAY                                       
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4010-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       4100-GET-COMM-DATE.                                                      
      *--------------------------------------------------------------*          
      *---------------------------------------------------------------*         
      * CALCULATE K500-DTE-LEASE-COMM-FLAT IF K500-DTE-BLDG-COMPLETE  *         
      * NOT EQUAL TO ZEROES.                                          *         
      *---------------------------------------------------------------*         
           IF K500-DTE-BLDG-COMPLETE = ZEROES OR SPACES OR LOW-VALUES           
              MOVE SPACES                 TO   F999-CDE-KEY-AVAIL               
           ELSE                                                                 
              MOVE K500-DTE-BLDG-COMPLETE TO   WS-LCOMM-DTE                     
                                                                                
              MOVE K100-KEY-FLD           TO   K260-KEY-FLD                     
                                                                                
              MOVE SPACES                 TO   WS-BP13K260-FND-TAG              
              PERFORM 4200-READ-BP13K260  THRU 4200-EXIT                        
              IF BP13K260-FOUND                                                 
                 ADD    13                TO   WS-LCOMM-MM                      
              ELSE                                                              
                 IF K100-DTE-1ST-SALE  > '19960610'                             
                    ADD    13             TO   WS-LCOMM-MM                      
                 ELSE                                                           
                    ADD    10             TO   WS-LCOMM-MM                      
                 END-IF                                                         
              END-IF                                                            
              DIVIDE WS-LCOMM-MM BY 12  GIVING  WS-YEAR-COMM                    
                                        REMAINDER WS-MONTH-COMM                 
                                                                                
              IF WS-MONTH-COMM = 0                                              
                 MOVE 12                  TO   WS-LCOMM-MM                      
                 ADD  1                   TO   WS-LCOMM-YYYY                    
              ELSE                                                              
                 ADD  WS-YEAR-COMM        TO   WS-LCOMM-YYYY                    
                 MOVE WS-MONTH-COMM       TO   WS-LCOMM-MM                      
              END-IF                                                            
                                                                                
              MOVE 01                     TO   WS-LCOMM-DD                      
              MOVE WS-LCOMM-DTE           TO   WS-K500-DTE-LSE-COMM             
                                                                                
              IF (COMM8-CDE-MODL-DESG   = '80' OR '89' OR '92') AND             
                 (COMM8-CDE-TYP-ACTLUSE = '01' OR '02')                         
                 MOVE SPACES              TO   WS-K500-DTE-LSE-COMM             
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       4200-READ-BP13K260.                                                      
      *--------------------------------------------------------------*          
           READ BP13K260.                                                       
                                                                                
           EVALUATE BP13K260-STATUS                                             
             WHEN 00                                                            
               MOVE  'Y'               TO   WS-BP13K260-FND-TAG                 
             WHEN 23                                                            
               MOVE  SPACE             TO   WS-BP13K260-FND-TAG                 
               DISPLAY 'BP13K260 REC NOT FOUND, KEY : ' K260-KEY-FLD            
             WHEN OTHER                                                         
               DISPLAY ' BP13K260 READ ERROR, STATUS ' BP13K260-STATUS          
                       '  KEY : ' K260-KEY-FLD                                  
               MOVE BP13K260-STATUS    TO   RETURN-CODE                         
               PERFORM 9000-CLOSE-ROUTINE                                       
           END-EVALUATE.                                                        
                                                                                
       4200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       4300-CHK-COMM-DATE.                                                      
      *--------------------------------------------------------------*          
           IF WS-TMP-DTE-LSE-COMM NOT = ZEROES AND SPACES AND                   
                                        LOW-VALUES                              
              COMPUTE WS-TMP-DATE =                                             
                   FUNCTION INTEGER-OF-DATE(WS-TMP-DTE-LSE-COMM)                
              COMPUTE WS-CURR-DATE =                                            
                   FUNCTION INTEGER-OF-DATE(WS-CURRENT-DTE)                     
              COMPUTE WS-DIFF-LSE-COMM = WS-TMP-DATE  -                         
                                         WS-CURR-DATE                           
                                                                                
              IF WS-DIFF-LSE-COMM < 31                                          
                 MOVE WS-K500-DTE-LSE-COMM TO F999-DTE-LEASE-COMM               
                 MOVE 'Y'                  TO F999-CDE-KEY-AVAIL                
              ELSE                                                              
                 MOVE SPACES               TO F999-DTE-LEASE-COMM               
                 MOVE 'Y'                  TO F999-CDE-KEY-AVAIL                
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       4500-READ-BB14K580.                                                      
      *--------------------------------------------------------------*          
           READ BB14K580.                                                       
           EVALUATE BB14K580-STATUS                                             
           WHEN 00                                                              
              MOVE 'Y'         TO WS-BB14K580-FND-TAG                           
           WHEN 23                                                              
              MOVE SPACE       TO WS-BB14K580-FND-TAG                           
           WHEN OTHER                                                           
              DISPLAY 'BB14K580 - READ ERROR : ' BB14K580-STATUS                
              MOVE BB14K580-STATUS   TO  RETURN-CODE                            
              PERFORM 9000-CLOSE-ROUTINE                                        
           END-EVALUATE.                                                        
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       5000-WRITE-BP13F999.                                                     
      *--------------------------------------------------------------*          
           IF K110-CURR-SELLING-PRICE IS NOT NUMERIC                            
              MOVE ZEROES    TO K110-CURR-SELLING-PRICE                         
           END-IF.                                                              
           IF COMM8-QTY-AREA-TOTAL    IS NOT NUMERIC                            
              MOVE ZEROES    TO COMM8-QTY-AREA-TOTAL                            
           END-IF.                                                              
           IF COMM8-QTY-AREA-ORGNL    IS NOT NUMERIC                            
              MOVE ZEROES    TO COMM8-QTY-AREA-ORGNL                            
           END-IF.                                                              
                                                                                
           MOVE K248-DTE-ALLOC            TO F999-DTE-ALLOC.                    
           MOVE K248-NUM-BLK              TO F999-BLOCK-NO.                     
           MOVE K248-NME-STREET           TO F999-NME-STREET.                   
           MOVE K248-NUM-LEVEL            TO F999-LEVEL-NO.                     
           MOVE K248-NUM-MAIN             TO F999-MAIN-UNIT.                    
           MOVE K248-NUM-SUB              TO F999-SUB-UNIT.                     
           MOVE K100-NT-CODE              TO F999-CDE-NT.                       
           MOVE K110-NEIGHBOURHOOD        TO F999-NEIGHBOURHOOD.                
           MOVE K110-CONTRACT-NO          TO F999-CONTRACT-NO.                  
           MOVE K110-CURR-SELLING-PRICE   TO F999-AMT-SELLING-PRICE.            
           MOVE COMM8-QTY-AREA-TOTAL      TO F999-QTY-AREA-TOTAL.               
           MOVE COMM8-QTY-AREA-ORGNL      TO F999-QTY-AREA-ORGNL.               
           MOVE COMM8-CDE-TYP-ACTLUSE     TO F999-NUM-FLAT-TYPE.                
           MOVE COMM8-CDE-MODL-DESG       TO F999-NUM-MODL-DESG.                
           MOVE WS-NUM-UPG-STATUS         TO F999-NUM-UPG-STATUS.               
                                                                                
           PERFORM 5100-GET-CNTRCT-TYPE   THRU 5100-EXIT.                       
           PERFORM 5200-GET-ESTATE-NAME   THRU 5200-EXIT.                       
           PERFORM 5400-DECODE-FLAT-TYPE  THRU 5400-EXIT.                       
                                                                                
           WRITE BP13F999-RECORD.                                               
           ADD 1    TO WS-F999-WRITE-CNT.                                       
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       5100-GET-CNTRCT-TYPE.                                                    
      *--------------------------------------------------------------*          
           IF (COMM8-CDE-TYP-ACTLUSE = '04') AND                                
              (COMM8-CDE-MODL-DESG = '36')                                      
              MOVE '3STB'            TO F999-NUM-CNTRCT-TYP                     
           ELSE                                                                 
              IF COMM8-CDE-MODL-DESG = '81' OR '82' OR '89'                     
                 MOVE '1PA '         TO F999-NUM-CNTRCT-TYP                     
              ELSE                                                              
                 IF COMM8-CDE-MODL-DESG = '34' OR '70' OR '71' OR               
                                          '72' OR '73'                          
                    MOVE '2DBP'      TO F999-NUM-CNTRCT-TYP                     
                 ELSE                                                           
                    MOVE '3STD'      TO F999-NUM-CNTRCT-TYP                     
                END-IF                                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       5100-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       5200-GET-ESTATE-NAME.                                                    
      *--------------------------------------------------------------*          
           MOVE SPACES                 TO   K060-KEY-FLD.                       
           MOVE '03'                   TO   K060-SERIAL-NO.                     
           MOVE K110-ESTATE            TO   K060-CODE.                          
           PERFORM 5300-READ-BP13K060  THRU 5300-EXIT.                          
                                                                                
           IF BP13K060-FOUND                                                    
              MOVE K060-DESC           TO   F999-NME-ESTATE                     
           ELSE                                                                 
              MOVE SPACES              TO   F999-NME-ESTATE                     
           END-IF.                                                              
                                                                                
       5200-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       5300-READ-BP13K060.                                                      
      *--------------------------------------------------------------*          
           READ BP13K060.                                                       
                                                                                
           EVALUATE BP13K060-STATUS                                             
           WHEN 00                                                              
              MOVE 'Y'         TO WS-BP13K060-FND-TAG                           
           WHEN 23                                                              
              MOVE SPACE       TO WS-BP13K060-FND-TAG                           
           WHEN OTHER                                                           
              DISPLAY 'BP13K060 - READ ERROR : ' BP13K060-STATUS                
              MOVE BP13K060-STATUS            TO  RETURN-CODE                   
              PERFORM 9000-CLOSE-ROUTINE                                        
           END-EVALUATE.                                                        
                                                                                
       5300-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       5400-DECODE-FLAT-TYPE.                                                   
      *--------------------------------------------------------------*          
           EVALUATE COMM8-CDE-TYP-ACTLUSE                                       
             WHEN '01'                                                          
                IF COMM8-CDE-MODL-DESG = '80' OR '89' OR '92'                   
                   MOVE '1A'     TO F999-NUM-FLAT-TYPE                          
                ELSE                                                            
                   MOVE '1 '     TO F999-NUM-FLAT-TYPE                          
                END-IF                                                          
             WHEN '02'                                                          
                IF COMM8-CDE-MODL-DESG = '80' OR '89' OR '92'                   
                   MOVE '2A'     TO F999-NUM-FLAT-TYPE                          
                ELSE                                                            
                   MOVE '2 '     TO F999-NUM-FLAT-TYPE                          
                END-IF                                                          
             WHEN '03'                                                          
                MOVE '3 '        TO F999-NUM-FLAT-TYPE                          
             WHEN '04'                                                          
                MOVE '4 '        TO F999-NUM-FLAT-TYPE                          
             WHEN '05'                                                          
                MOVE '5 '        TO F999-NUM-FLAT-TYPE                          
             WHEN '06'                                                          
                MOVE 'E '        TO F999-NUM-FLAT-TYPE                          
           END-EVALUATE.                                                        
                                                                                
      *** DECODE FLAT CODE                                                      
           IF K110-FLAT-STATUS = 'RP'                                           
              MOVE 'RP'          TO F999-NUM-FLAT-CODE                          
           ELSE                                                                 
              MOVE SPACES        TO F999-NUM-FLAT-CODE                          
           END-IF.                                                              
                                                                                
       5400-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *--------------------------------------------------------------*          
       9000-CLOSE-ROUTINE.                                                      
      *--------------------------------------------------------------*          
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY '      PROGRAM : BP13C22U       ' WS-EDT-DATE.               
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-F248-READ-CNT    TO WS-EDT-TMP.                              
           DISPLAY 'NO OF RECS READ FROM BP13F248    : ' WS-EDT-TMP.            
           MOVE WS-F999-WRITE-CNT   TO WS-EDT-TMP.                              
           DISPLAY 'NO OF RECS WRITE  TO BP13F999    : ' WS-EDT-TMP.            
           DISPLAY '*---------------------------------------------*'.           
           DISPLAY ' '.                                                         
           MOVE WS-K110-NOTFND-CNT  TO WS-EDT-TMP.                              
           DISPLAY 'NO OF RECS NOT FOUND IN BM06K110 : '  WS-EDT-TMP.           
           MOVE WS-K100-NOTFND-CNT  TO WS-EDT-TMP.                              
           DISPLAY 'NO OF RECS NOT FOUND IN BM06K100 : '  WS-EDT-TMP.           
           MOVE WS-PIDB-NOTFND-CNT      TO WS-EDT-TMP.                          
           DISPLAY 'NO OF RECS NOT FOUND IN PIDB     : '  WS-EDT-TMP.           
           MOVE WS-K500-NOTFND-CNT      TO WS-EDT-TMP.                          
           DISPLAY 'NO OF RECS NOT FOUND IN BB14K500 : '  WS-EDT-TMP.           
                                                                                
           CLOSE BP13F205                                                       
                 BP13F248                                                       
                 BP13K060                                                       
                 BP13K260                                                       
                 BM06K100                                                       
                 BM06K110                                                       
                 BB14K500                                                       
                 BB14K580                                                       
                 BP13F999.                                                      
                                                                                
           IF BP13K060-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BP13K060-STATUS ' BP13K060-STATUS           
              MOVE BP13K060-STATUS     TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF BP13K260-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BP13K260-STATUS ' BP13K260-STATUS           
              MOVE BP13K260-STATUS     TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF BM06K100-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BM06K100-STATUS ' BM06K100-STATUS           
              MOVE BM06K100-STATUS     TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF BM06K110-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BM06K110-STATUS ' BM06K110-STATUS           
              MOVE BM06K110-STATUS     TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF BB14K500-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BB14K500-STATUS ' BB14K500-STATUS           
              MOVE BB14K500-STATUS     TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           IF BB14K580-STATUS NOT = 00 AND 97                                   
              DISPLAY 'CLOSE ERROR, BB14K580-STATUS ' BB14K580-STATUS           
              MOVE BB14K580-STATUS     TO RETURN-CODE                           
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
