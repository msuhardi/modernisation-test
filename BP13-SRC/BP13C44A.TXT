       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.         BP13C44A.                                            
      *AUTHOR.             EDWIN ANGELES (EA1).                                 
      *DATE-WRITTEN.       26 MAR 2009.                                         
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (SOC) (BP13)                          *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :  GENERATE REPORT WITH LN/LNS CASES FOR THE   *            
      *                DAILY SALES ORDER RAISED (BP13F410)         *            
      *                                                            *            
      *  INPUT FILE :  1. BP13F410                                 *            
      *                2. BP13K800                                 *            
      *                3. BP13K500/BP13K510                        *            
      *  OUTPUT FILE:  1. BP13L44A                                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * BP133527 EA1  26032009 NEW PROGRAM                         *            
      * BP133606 EA1  26052009 ADDED AB03K030 IN THE REPORT FOR SMS*            
      *                        CASES, ADDED LEVY CODES.            *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
            SELECT BP13F410 ASSIGN       TO BP13F410.                           
                                                                                
            SELECT BP13K800 ASSIGN TO BP13K800                                  
                   ORGANIZATION IS INDEXED                                      
                   RECORD KEY IS K800-NUM-REGN                                  
                   FILE STATUS IS WS-BP13K800-STAT                              
                   ACCESS MODE IS RANDOM.                                       
                                                                                
            SELECT BB24K510 ASSIGN TO BB24K510                                  
                   ORGANIZATION IS INDEXED                                      
                   RECORD KEY IS K510-KEY-FLD                                   
                   FILE STATUS IS WS-BB24K510-STAT                              
                   ACCESS MODE IS DYNAMIC.                                      
                                                                                
            SELECT BB24K500 ASSIGN TO BB24K500                                  
                   ORGANIZATION IS INDEXED                                      
                   RECORD KEY IS K500-KEY-FLD                                   
                   FILE STATUS IS WS-BB24K500-STAT                              
                   ACCESS MODE IS RANDOM.                                       
                                                                                
            SELECT BP13K162 ASSIGN TO BP13K162                                  
                   ORGANIZATION IS INDEXED                                      
                   RECORD KEY IS K162-KEY-FLD                                   
                   FILE STATUS IS WS-BP13K162-STAT                              
                   ACCESS MODE IS RANDOM.                                       
                                                                                
            SELECT AB03K030 ASSIGN TO AB03K030                                  
                   ORGANIZATION IS INDEXED                                      
                   RECORD KEY IS K030-NUM-HDB-REF                               
                   FILE STATUS IS WS-AB03K030-STAT                              
                   ACCESS MODE IS RANDOM.                                       
                                                                                
            SELECT BP13L44A ASSIGN TO BP13L44A.                                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BP13F410                                                            
            RECORDING MODE  IS   F                                              
            BLOCK  CONTAINS 0    RECORDS                                        
            RECORD CONTAINS 2000 CHARACTERS                                     
            LABEL  RECORDS  ARE  STANDARD.                                      
       COPY BP13F410.                                                           
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS  2000 CHARACTERS.                                   
       COPY BP13K800.                                                           
                                                                                
       FD   BB24K510                                                            
            RECORD CONTAINS  50 CHARACTERS.                                     
       COPY BB24K510.                                                           
                                                                                
       FD   BB24K500                                                            
            RECORD CONTAINS  1700 CHARACTERS.                                   
       COPY BB24K500.                                                           
                                                                                
       FD   BP13K162                                                            
            RECORD CONTAINS   600 CHARACTERS.                                   
       COPY BP13K162.                                                           
                                                                                
       FD   AB03K030                                                            
            RECORD CONTAINS   800 CHARACTERS.                                   
       COPY AB03K030.                                                           
                                                                                
       FD   BP13L44A                                                            
            RECORD CONTAINS  132 CHARACTERS                                     
            LABEL RECORDS ARE STANDARD                                          
            RECORDING MODE IS F.                                                
                                                                                
       01   BP13L44A-REC              PIC X(132).                               
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01 WS-NRIC-KEY                PIC X(9).                                  
       01 WS-RESALE-STATUS           PIC X(3).                                  
       01 WS-K500-NUM-HDB-REF        PIC X(11).                                 
       01 WS-LNS-FLAG                PIC X(1).                                  
       01 WS-LNS-BYPASS-NRIC-FLAG    PIC X(1).                                  
       01 WS-LNS-BYPASS-F410-FLAG    PIC X(1).                                  
       01 WS-BYPASS-F410             PIC X(1).                                  
       01 WS-INITIAL-REGNO           PIC X(1).                                  
       01 WS-INITIAL-UIN             PIC X(1).                                  
                                                                                
       01 WS-VALID-RESALE-STATUS     PIC X(3).                                  
          88 WS-VLD-RSL-STAT         VALUE 'LN ', 'LNS', 'PN ', 'FN ',          
                                           'LNH', 'FNH', 'LO ', 'LI ',          
                                           'PO ', 'PI ', 'FO ', 'FI ',          
                                           'LOH', 'LIH', 'FOH', 'FIH'           
                                           'QN ', 'QNH', 'FNQ', 'PNQ',          
                                           'BQN', 'BHN', 'QO ', 'QOH',          
                                           'FOQ', 'POQ', 'BQO', 'BQP',          
                                           'BHO', 'BHP'.                        
                                                                                
       01 WS-CURRENT-DATE.                                                      
          05 WS-CCYY                 PIC X(4).                                  
          05 WS-MM                   PIC X(2).                                  
          05 WS-DD                   PIC X(2).                                  
                                                                                
       01 WS-FILE-STATUS.                                                       
          05 WS-BB24K510-STAT        PIC X(02) VALUE SPACES.                    
          05 WS-BB24K500-STAT        PIC X(02) VALUE SPACES.                    
          05 WS-BP13K800-STAT        PIC X(02) VALUE SPACES.                    
          05 WS-BP13K162-STAT        PIC X(02) VALUE SPACES.                    
          05 WS-AB03K030-STAT        PIC X(02) VALUE SPACES.                    
                                                                                
       01 EOF-FLAG-BP13F410          PIC X(01) VALUE 'N'.                       
       01 EOF-FLAG-BB24K510          PIC X(01) VALUE 'N'.                       
                                                                                
       01 WS-PAGE-CTR                PIC 9(04) VALUE ZEROES.                    
       01 WS-LINE-CTR                PIC 9(03) VALUE ZEROES.                    
                                                                                
       01 WS-BYPASS1-BP13F410        PIC 9(8)  VALUE ZEROES.                    
       01 WS-BYPASS2-BP13F410        PIC 9(8)  VALUE ZEROES.                    
       01 WS-READ-BP13F410           PIC 9(8)  VALUE ZEROES.                    
       01 WS-PROCESS-BP13F410        PIC 9(8)  VALUE ZEROES.                    
       01 WS-WRITTEN-BP13L44A        PIC 9(8)  VALUE ZEROES.                    
                                                                                
       01 WS-BLANK-LINE              PIC X(132) VALUE SPACES.                   
       01 WS-HEADER1.                                                           
          05 FILLER                  PIC X(08) VALUE 'BP13L44A'.                
          05 FILLER                  PIC X(04) VALUE SPACES.                    
          05 FILLER                  PIC X(04) VALUE 'HDB3'.                    
          05 FILLER                  PIC X(29) VALUE SPACES.                    
          05 FILLER                  PIC X(39) VALUE                            
               'S Y S T E M   OF   C O M M I T M E N T'.                        
          05 FILLER                  PIC X(27) VALUE SPACES.                    
          05 FILLER                  PIC X(10) VALUE 'RUN DATE: '.              
          05 WS-DATE-IN-CURR.                                                   
             10 WS-DAY-IN-CURR       PIC X(02) VALUE 'DD'.                      
             10 FILLER               PIC X(01) VALUE '/'.                       
             10 WS-MONTH-IN-CURR     PIC X(02) VALUE 'MM'.                      
             10 FILLER               PIC X(01) VALUE '/'.                       
             10 WS-YEAR-IN-CURR      PIC X(04) VALUE 'CCYY'.                    
          05 FILLER                  PIC X(01) VALUE SPACES.                    
                                                                                
       01 WS-HEADER2.                                                           
          05 FILLER                  PIC X(36) VALUE SPACES.                    
          05 FILLER                  PIC X(60) VALUE                            
             'DAILY SALES ORDER RAISED WITH "LN", "LNS", "QN" & "EC" CAS        
      -      'ES'.                                                              
          05 FILLER                  PIC X(19) VALUE SPACES.                    
          05 FILLER                  PIC X(06) VALUE 'PAGE: '.                  
          05 WS-PAGE-NUM             PIC 9(04) VALUE ZEROES.                    
          05 FILLER                  PIC X(07) VALUE SPACES.                    
                                                                                
       01 WS-HEADER3.                                                           
          05 FILLER                  PIC X(33) VALUE SPACES.                    
          05 FILLER                  PIC X(24) VALUE                            
             '<------ RSL LEVY ------>'.                                        
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(29) VALUE                            
             '<--------------------------- '.                                   
          05 FILLER                  PIC X(15) VALUE                            
             ' Q U A N T U M '.                                                 
          05 FILLER                  PIC X(29) VALUE                            
             ' --------------------------->'.                                   
                                                                                
       01 WS-HEADER4.                                                           
          05 FILLER                  PIC X(08) VALUE ' REGNO  '.                
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(09) VALUE ' HA UIN  '.               
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(10) VALUE ' SO DATE  '.              
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(03) VALUE 'SOC'.                     
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(03) VALUE 'RSL'.                     
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(14) VALUE                            
               'RESALE HDBREF '.                                                
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(04) VALUE 'TYPE'.                    
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(16) VALUE 'HDBREF / ADDRESS'.        
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(08) VALUE 'SALE TYP'.                
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(08) VALUE 'RSL STAT'.                
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(14) VALUE 'CURRENT LESSEE'.          
          05 FILLER                  PIC X(15) VALUE SPACES.                    
                                                                                
       01 WS-HEADER5.                                                           
          05 FILLER                  PIC X(08) VALUE ALL '-'.                   
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(09) VALUE ALL '-'.                   
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(10) VALUE ALL '-'.                   
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(03) VALUE ALL '-'.                   
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(03) VALUE ALL '-'.                   
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(14) VALUE ALL '-'.                   
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(04) VALUE ALL '-'.                   
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(16) VALUE ALL '-'.                   
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(08) VALUE ALL '-'.                   
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(08) VALUE ALL '-'.                   
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(14) VALUE ALL '-'.                   
          05 FILLER                  PIC X(15) VALUE SPACES.                    
                                                                                
       01 WS-DETAIL-RPT.                                                        
          05 WS-RPT-REGNO            PIC X(08) VALUE SPACES.                    
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 WS-RPT-UIN              PIC X(09) VALUE SPACES.                    
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 WS-RPT-SODATE           PIC X(10) VALUE SPACES.                    
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 WS-RPT-SOC-STAT         PIC X(03) VALUE SPACES.                    
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 WS-RPT-RSL-STAT         PIC X(03) VALUE SPACES.                    
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 WS-RPT-RESALE-SCH       PIC X(14) VALUE SPACES.                    
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 WS-RPT-QNTM-TYPE        PIC X(03) VALUE SPACES.                    
          05 FILLER                  PIC X(03) VALUE SPACES.                    
          05 WS-RPT-QNTM-HDBREF      PIC X(14) VALUE SPACES.                    
          05 FILLER                  PIC X(07) VALUE SPACES.                    
          05 WS-RPT-QNTM-SLS-TYPE    PIC X(02) VALUE SPACES.                    
          05 FILLER                  PIC X(08) VALUE SPACES.                    
          05 WS-RPT-QNTM-RSL-STAT    PIC X(03) VALUE SPACES.                    
          05 FILLER                  PIC X(11) VALUE SPACES.                    
          05 WS-RPT-QNTM-CURR-LESS   PIC X(01) VALUE SPACES.                    
          05 FILLER                  PIC X(21) VALUE SPACES.                    
                                                                                
       01 WS-DETAIL-RPT-EC.                                                     
          05 WS-RPT-REGNO-EC         PIC X(08) VALUE SPACES.                    
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 WS-RPT-UIN-EC           PIC X(09) VALUE SPACES.                    
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 WS-RPT-SODATE-EC        PIC X(10) VALUE SPACES.                    
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 WS-RPT-SOC-STAT-EC      PIC X(03) VALUE SPACES.                    
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 WS-RPT-RSL-STAT-EC      PIC X(03) VALUE SPACES.                    
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 WS-RPT-RESALE-SCH-EC    PIC X(14) VALUE SPACES.                    
          05 FILLER                  PIC X(02) VALUE SPACES.                    
          05 WS-RPT-QNTM-TYPE-EC     PIC X(03) VALUE SPACES.                    
          05 FILLER                  PIC X(03) VALUE SPACES.                    
          05 WS-RPT-QNTM-ADDRESS.                                               
             10 FILLER               PIC X(04) VALUE 'BLK '.                    
             10 WS-RPT-QNTM-BLK      PIC X(05) VALUE SPACES.                    
             10 FILLER               PIC X(01) VALUE SPACE.                     
             10 WS-RPT-QNTM-UNIT     PIC X(11) VALUE SPACES.                    
             10 FILLER               PIC X(01) VALUE SPACE.                     
             10 WS-RPT-QNTM-SNAME    PIC X(45) VALUE SPACES.                    
                                                                                
       01 WS-NIL-RPT1.                                                          
          05 FILLER                  PIC X(21) VALUE SPACES.                    
          05 FILLER                  PIC X(10) VALUE '**********'.              
          05 FILLER                  PIC X(53) VALUE                            
             '   N O   R E C O R D   T O   R E P O R T   **********'.           
          05 FILLER                  PIC X(48) VALUE SPACES.                    
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      ****************************                                              
       0000-MAIN-CONTROL.                                                       
      ****************************                                              
                                                                                
           PERFORM 1000-OPEN-FILES        THRU 1000-EXIT.                       
           PERFORM 2000-READ-BP13F410     THRU 2000-EXIT                        
                   UNTIL EOF-FLAG-BP13F410 = 'Y'.                               
                                                                                
           PERFORM 9999-CLOSE-FILES       THRU 9999-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       1000-OPEN-FILES.                                                         
      ****************************                                              
                                                                                
           OPEN INPUT  BP13F410                                                 
                       BP13K800                                                 
                       BB24K510                                                 
                       BB24K500                                                 
                       BP13K162                                                 
                       AB03K030                                                 
                OUTPUT BP13L44A.                                                
                                                                                
           IF WS-BP13K800-STAT  NOT = '00' AND '97'                             
              DISPLAY 'ERROR IN OPENING BP13K800   STATUS - '                   
              WS-BP13K800-STAT                                                  
              MOVE WS-BP13K800-STAT             TO RETURN-CODE                  
              PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT                  
           END-IF.                                                              
                                                                                
           IF WS-BB24K510-STAT  NOT = '00' AND '97'                             
              DISPLAY 'ERROR IN OPENING BB24K510   STATUS - '                   
              WS-BB24K510-STAT                                                  
              MOVE WS-BB24K510-STAT             TO RETURN-CODE                  
              PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT                  
           END-IF.                                                              
                                                                                
           IF WS-BB24K500-STAT  NOT = '00' AND '97'                             
              DISPLAY 'ERROR IN OPENING BB24K500   STATUS - '                   
              WS-BB24K500-STAT                                                  
              MOVE WS-BB24K500-STAT             TO RETURN-CODE                  
              PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT                  
           END-IF.                                                              
                                                                                
           IF WS-BP13K162-STAT  NOT = '00' AND '97'                             
              DISPLAY 'ERROR IN OPENING BP13K162   STATUS - '                   
              WS-BP13K162-STAT                                                  
              MOVE WS-BP13K162-STAT             TO RETURN-CODE                  
              PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT                  
           END-IF.                                                              
                                                                                
           IF WS-AB03K030-STAT  NOT = '00' AND '97'                             
              DISPLAY 'ERROR IN OPENING AB03K030   STATUS - '                   
              WS-AB03K030-STAT                                                  
              MOVE WS-AB03K030-STAT             TO RETURN-CODE                  
              PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT                  
           END-IF.                                                              
                                                                                
           ADD 1                         TO WS-PAGE-CTR.                        
           PERFORM 1100-WRITE-HEADERS    THRU 1100-EXIT.                        
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       1100-WRITE-HEADERS.                                                      
      ****************************                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
           MOVE WS-CCYY               TO WS-YEAR-IN-CURR.                       
           MOVE WS-MM                 TO WS-MONTH-IN-CURR.                      
           MOVE WS-DD                 TO WS-DAY-IN-CURR.                        
           MOVE WS-PAGE-CTR           TO WS-PAGE-NUM.                           
           WRITE BP13L44A-REC       FROM WS-HEADER1 AFTER PAGE.                 
           WRITE BP13L44A-REC       FROM WS-HEADER2 AFTER 1.                    
           WRITE BP13L44A-REC       FROM WS-HEADER3 AFTER 2.                    
           WRITE BP13L44A-REC       FROM WS-HEADER4 AFTER 1.                    
           WRITE BP13L44A-REC       FROM WS-HEADER5 AFTER 1.                    
           ADD 6                      TO WS-LINE-CTR.                           
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       2000-READ-BP13F410.                                                      
      ****************************                                              
                                                                                
           READ BP13F410                                                        
              AT END MOVE 'Y'      TO    EOF-FLAG-BP13F410                      
              GO TO 2000-EXIT.                                                  
                                                                                
           ADD 1                   TO    WS-READ-BP13F410.                      
           MOVE 'Y'                TO    WS-INITIAL-REGNO.                      
           MOVE 'N'                TO    WS-BYPASS-F410.                        
                                                                                
           PERFORM 2100-READ-BP13K800  THRU 2100-EXIT.                          
           IF WS-BYPASS-F410 = 'N'                                              
              PERFORM 2200-MAIN-ROUTINE   THRU 2200-EXIT                        
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       2100-READ-BP13K800.                                                      
      ************************                                                  
                                                                                
           MOVE SPACES                      TO BP13K800-MASTER.                 
           INITIALIZE                          BP13K800-MASTER.                 
                                                                                
           MOVE F410-NUM-REGN               TO K800-NUM-REGN.                   
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-BP13K800-STAT = 00                                             
              IF (K800-NUM-IDP-SCHEME = 'SA ' OR                                
                 (K800-NUM-FLAT-TYPE = '1A' OR '2A'))                           
                 ADD 1                          TO WS-BYPASS1-BP13F410          
                 MOVE 'Y'                       TO WS-BYPASS-F410               
              END-IF                                                            
           ELSE                                                                 
              IF WS-BP13K800-STAT = 23                                          
                 ADD 1                          TO WS-BYPASS1-BP13F410          
                 MOVE 'Y'                       TO WS-BYPASS-F410               
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K800 , '                            
                      'STATUS = ' WS-BP13K800-STAT                              
                                                                                
                 MOVE WS-BP13K800-STAT          TO RETURN-CODE                  
                 MOVE 'Y'                       TO EOF-FLAG-BP13F410            
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       2200-MAIN-ROUTINE.                                                       
      ****************************                                              
                                                                                
           MOVE 'Y'  TO  WS-LNS-BYPASS-F410-FLAG                                
                         WS-LNS-BYPASS-NRIC-FLAG.                               
                                                                                
           IF K800-NUM-NRIC1 NOT = SPACES AND LOW-VALUES                        
              MOVE K800-NUM-NRIC1                TO WS-NRIC-KEY                 
              PERFORM 2300-READ-BP13K162       THRU 2300-EXIT                   
              PERFORM 2400-READ-BB24K510       THRU 2400-EXIT                   
              IF WS-LNS-BYPASS-NRIC-FLAG = 'Y' AND                              
                 WS-RPT-QNTM-TYPE NOT = SPACES                                  
                 MOVE SPACES TO WS-K500-NUM-HDB-REF                             
                                WS-RESALE-STATUS                                
                 PERFORM 3600-MOVE-DATA-FOR-REPORT THRU 3600-EXIT               
                 PERFORM 4000-PRINT-REPORT         THRU 4000-EXIT               
                 WRITE BP13L44A-REC FROM WS-BLANK-LINE AFTER 1                  
                 ADD 1                               TO WS-LINE-CTR             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE 'Y'  TO  WS-LNS-BYPASS-NRIC-FLAG.                               
           IF K800-NUM-NRIC2 NOT = SPACES AND LOW-VALUES                        
              MOVE K800-NUM-NRIC2                TO WS-NRIC-KEY                 
              PERFORM 2300-READ-BP13K162       THRU 2300-EXIT                   
              PERFORM 2400-READ-BB24K510       THRU 2400-EXIT                   
              IF WS-LNS-BYPASS-NRIC-FLAG = 'Y' AND                              
                 WS-RPT-QNTM-TYPE NOT = SPACES                                  
                 MOVE SPACES TO WS-K500-NUM-HDB-REF                             
                                WS-RESALE-STATUS                                
                 PERFORM 3600-MOVE-DATA-FOR-REPORT THRU 3600-EXIT               
                 PERFORM 4000-PRINT-REPORT         THRU 4000-EXIT               
                 WRITE BP13L44A-REC FROM WS-BLANK-LINE AFTER 1                  
                 ADD 1                               TO WS-LINE-CTR             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE 'Y'  TO  WS-LNS-BYPASS-NRIC-FLAG.                               
           IF K800-NUM-NRIC3 NOT = SPACES AND LOW-VALUES                        
              MOVE K800-NUM-NRIC3                TO WS-NRIC-KEY                 
              PERFORM 2300-READ-BP13K162       THRU 2300-EXIT                   
              PERFORM 2400-READ-BB24K510       THRU 2400-EXIT                   
              IF WS-LNS-BYPASS-NRIC-FLAG = 'Y' AND                              
                 WS-RPT-QNTM-TYPE NOT = SPACES                                  
                 MOVE SPACES TO WS-K500-NUM-HDB-REF                             
                                WS-RESALE-STATUS                                
                 PERFORM 3600-MOVE-DATA-FOR-REPORT THRU 3600-EXIT               
                 PERFORM 4000-PRINT-REPORT         THRU 4000-EXIT               
                 WRITE BP13L44A-REC FROM WS-BLANK-LINE AFTER 1                  
                 ADD 1                               TO WS-LINE-CTR             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE 'Y'  TO  WS-LNS-BYPASS-NRIC-FLAG.                               
           IF K800-NUM-NRIC4 NOT = SPACES AND  LOW-VALUES                       
              MOVE K800-NUM-NRIC4                TO WS-NRIC-KEY                 
              PERFORM 2300-READ-BP13K162       THRU 2300-EXIT                   
              PERFORM 2400-READ-BB24K510       THRU 2400-EXIT                   
              IF WS-LNS-BYPASS-NRIC-FLAG = 'Y' AND                              
                 WS-RPT-QNTM-TYPE NOT = SPACES                                  
                 MOVE SPACES TO WS-K500-NUM-HDB-REF                             
                                WS-RESALE-STATUS                                
                 PERFORM 3600-MOVE-DATA-FOR-REPORT THRU 3600-EXIT               
                 PERFORM 4000-PRINT-REPORT         THRU 4000-EXIT               
                 WRITE BP13L44A-REC FROM WS-BLANK-LINE AFTER 1                  
                 ADD 1                               TO WS-LINE-CTR             
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF WS-LNS-BYPASS-F410-FLAG = 'Y'                                     
              ADD 1                              TO WS-BYPASS2-BP13F410         
           ELSE                                                                 
              ADD 1                              TO WS-PROCESS-BP13F410         
           END-IF.                                                              
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       2300-READ-BP13K162.                                                      
      ************************                                                  
                                                                                
           MOVE SPACES                      TO WS-RPT-QNTM-TYPE                 
                                               WS-RPT-QNTM-TYPE-EC              
                                               WS-RPT-QNTM-HDBREF               
                                               WS-RPT-QNTM-SLS-TYPE             
                                               WS-RPT-QNTM-RSL-STAT             
                                               WS-RPT-QNTM-CURR-LESS            
                                               WS-RPT-QNTM-BLK                  
                                               WS-RPT-QNTM-UNIT                 
                                               WS-RPT-QNTM-SNAME.               
                                                                                
           MOVE SPACES                      TO BP13K162-REC.                    
           INITIALIZE                          BP13K162-REC.                    
                                                                                
           MOVE F410-NUM-REGN               TO K162-NUM-REGNO.                  
           MOVE WS-NRIC-KEY                 TO K162-NUM-NRIC.                   
                                                                                
           READ BP13K162.                                                       
                                                                                
           IF WS-BP13K162-STAT = 00                                             
                                                                                
              EVALUATE K162-NUM-TYPE                                            
                 WHEN 'R'                                                       
                     MOVE 'RSL'                 TO WS-RPT-QNTM-TYPE             
                                                   WS-RPT-QNTM-TYPE-EC          
                                                                                
                     STRING K162-NUM-EXIST-HDBREF(1:4) '-'                      
                            K162-NUM-EXIST-HDBREF(5:4) '-'                      
                            K162-NUM-EXIST-HDBREF(9:1) '-'                      
                            K162-NUM-EXIST-HDBREF(10:2)                         
                            DELIMITED BY SIZE                                   
                            INTO WS-RPT-QNTM-HDBREF                             
                                                                                
                     MOVE K162-CDE-RESALE-STATUS TO WS-RPT-QNTM-RSL-STAT        
                                                                                
                 WHEN 'S'                                                       
                     MOVE 'SMS'                 TO WS-RPT-QNTM-TYPE             
                                                   WS-RPT-QNTM-TYPE-EC          
                                                                                
                     STRING K162-NUM-EXIST-HDBREF(1:4) '-'                      
                            K162-NUM-EXIST-HDBREF(5:4) '-'                      
                            K162-NUM-EXIST-HDBREF(9:1) '-'                      
                            K162-NUM-EXIST-HDBREF(10:2)                         
                            DELIMITED BY SIZE                                   
                            INTO WS-RPT-QNTM-HDBREF                             
                                                                                
                     MOVE K162-NUM-SALES-TYPE   TO WS-RPT-QNTM-SLS-TYPE         
                     PERFORM 2350-READ-AB03K030 THRU 2350-EXIT                  
                                                                                
                 WHEN 'D'                                                       
                     MOVE 'EC'            TO WS-RPT-QNTM-TYPE                   
                                             WS-RPT-QNTM-TYPE-EC                
                     MOVE K162-BLOCK      TO WS-RPT-QNTM-BLK                    
                     MOVE K162-HOUSE-NO   TO WS-RPT-QNTM-UNIT                   
                     MOVE K162-STREETNAME TO WS-RPT-QNTM-SNAME                  
                                                                                
                 WHEN OTHER                                                     
                     MOVE K162-NUM-TYPE   TO WS-RPT-QNTM-TYPE                   
                                             WS-RPT-QNTM-TYPE-EC                
                                                                                
                     STRING K162-NUM-EXIST-HDBREF(1:4) '-'                      
                            K162-NUM-EXIST-HDBREF(5:4) '-'                      
                            K162-NUM-EXIST-HDBREF(9:1) '-'                      
                            K162-NUM-EXIST-HDBREF(10:2)                         
                            DELIMITED BY SIZE                                   
                            INTO WS-RPT-QNTM-HDBREF                             
                                                                                
                     MOVE K162-NUM-SALES-TYPE    TO WS-RPT-QNTM-SLS-TYPE        
                     MOVE K162-CDE-RESALE-STATUS TO WS-RPT-QNTM-RSL-STAT        
                                                                                
              END-EVALUATE                                                      
                                                                                
           ELSE                                                                 
              IF WS-BP13K162-STAT = 23                                          
                 CONTINUE                                                       
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K162 , '                            
                      'STATUS = ' WS-BP13K162-STAT                              
                                                                                
                 MOVE WS-BP13K162-STAT          TO RETURN-CODE                  
                 PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ************************                                                  
       2350-READ-AB03K030.                                                      
      ************************                                                  
                                                                                
           MOVE SPACES                      TO WS-RPT-QNTM-CURR-LESS.           
                                                                                
           MOVE SPACES                      TO AB03K030-REC.                    
           INITIALIZE                          AB03K030-REC.                    
                                                                                
           MOVE K162-NUM-EXIST-HDBREF       TO K030-NUM-HDB-REF.                
                                                                                
           READ AB03K030.                                                       
                                                                                
           IF WS-AB03K030-STAT = 00                                             
                                                                                
              IF (WS-NRIC-KEY NOT = K030-NUM-UIN1) AND                          
                 (WS-NRIC-KEY NOT = K030-NUM-UIN2) AND                          
                 (WS-NRIC-KEY NOT = K030-NUM-UIN3) AND                          
                 (WS-NRIC-KEY NOT = K030-NUM-UIN4)                              
                                                                                
                 MOVE 'N'   TO    WS-RPT-QNTM-CURR-LESS                         
                                                                                
              END-IF                                                            
                                                                                
           ELSE                                                                 
              IF WS-AB03K030-STAT = 23                                          
                 MOVE 'Z'   TO    WS-RPT-QNTM-CURR-LESS                         
              ELSE                                                              
                 DISPLAY 'ERROR READING AB03K030 , '                            
                      'STATUS = ' WS-AB03K030-STAT                              
                                                                                
                 MOVE WS-AB03K030-STAT          TO RETURN-CODE                  
                 PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2350-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       2400-READ-BB24K510.                                                      
      ****************************                                              
                                                                                
           MOVE 'Y'                       TO   WS-INITIAL-UIN.                  
           MOVE 'N'                       TO   EOF-FLAG-BB24K510.               
           MOVE SPACES                    TO   K510-KEY-FLD.                    
           MOVE WS-NRIC-KEY               TO   K510-NUM-UIN.                    
                                                                                
           START BB24K510 KEY IS >= K510-KEY-FLD.                               
           EVALUATE WS-BB24K510-STAT                                            
               WHEN 00                                                          
               WHEN 02                                                          
                    MOVE 'N'      TO  WS-LNS-FLAG                               
                                                                                
                    PERFORM 3100-READ-NEXT        THRU 3100-EXIT                
                        UNTIL EOF-FLAG-BB24K510 = 'Y'                           
                         OR   K510-NUM-UIN NOT  = WS-NRIC-KEY                   
                                                                                
                    IF WS-LNS-FLAG = 'Y'                                        
                       WRITE BP13L44A-REC FROM WS-BLANK-LINE AFTER 1            
                       ADD 1                   TO WS-LINE-CTR                   
                    END-IF                                                      
                                                                                
               WHEN 23                                                          
                    CONTINUE                                                    
                                                                                
               WHEN OTHER                                                       
                    DISPLAY 'ERROR READING BB24K510..' WS-BB24K510-STAT         
                    MOVE WS-BB24K510-STAT             TO RETURN-CODE            
                    PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT            
           END-EVALUATE.                                                        
                                                                                
                                                                                
       2400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3100-READ-NEXT.                                                          
      ****************************                                              
                                                                                
           READ BB24K510 NEXT RECORD.                                           
                                                                                
           EVALUATE WS-BB24K510-STAT                                            
               WHEN 00                                                          
               WHEN 22                                                          
                  IF (WS-NRIC-KEY = K510-NUM-UIN) AND                           
                     (K510-CDE-STATUS = 'S' OR 'P')                             
                      PERFORM 3200-READ-BB24K500  THRU 3200-EXIT                
                  END-IF                                                        
               WHEN 23                                                          
               WHEN 10                                                          
                  MOVE 'Y' TO EOF-FLAG-BB24K510                                 
               WHEN OTHER                                                       
                 DISPLAY 'ERROR OPENING BB24K510..' WS-BB24K510-STAT            
                 MOVE WS-BB24K510-STAT             TO RETURN-CODE               
                 PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT               
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3200-READ-BB24K500.                                                      
      ****************************                                              
                                                                                
           MOVE SPACES              TO   K500-KEY-FLD                           
                                         WS-RPT-RSL-STAT                        
                                         WS-RESALE-STATUS                       
                                         WS-K500-NUM-HDB-REF.                   
                                                                                
           MOVE K510-NUM-HDB-REF    TO   K500-NUM-HDB-REF.                      
           MOVE K510-DTE-APPROVAL   TO   K500-DTE-APPROVAL.                     
                                                                                
           READ BB24K500.                                                       
                                                                                
           EVALUATE WS-BB24K500-STAT                                            
             WHEN 00                                                            
             WHEN 02                                                            
                  IF K500-CDE-RESALE-STATUS2 = SPACES OR LOW-VALUES             
                     MOVE K500-CDE-RESALE-STATUS    TO WS-RESALE-STATUS         
                  ELSE                                                          
                     IF (K500-NUM-UIN-SELLER1 = WS-NRIC-KEY)                    
                        MOVE K500-CDE-RESALE-STATUS TO WS-RESALE-STATUS         
                     ELSE                                                       
                        IF (K500-NUM-UIN-SELLER2 = WS-NRIC-KEY)                 
                           MOVE K500-CDE-RESALE-STATUS2                         
                                                    TO WS-RESALE-STATUS         
                        END-IF                                                  
                     END-IF                                                     
                  END-IF                                                        
                                                                                
                  MOVE WS-RESALE-STATUS  TO WS-VALID-RESALE-STATUS              
                                                                                
                  IF WS-VLD-RSL-STAT                                            
                     MOVE 'Y' TO WS-LNS-FLAG                                    
                     MOVE 'N' TO WS-LNS-BYPASS-NRIC-FLAG                        
                                 WS-LNS-BYPASS-F410-FLAG                        
                     MOVE K500-NUM-HDB-REF TO WS-K500-NUM-HDB-REF               
                     PERFORM 3600-MOVE-DATA-FOR-REPORT THRU 3600-EXIT           
                     PERFORM 4000-PRINT-REPORT         THRU 4000-EXIT           
                  END-IF                                                        
             WHEN 23                                                            
                  CONTINUE                                                      
             WHEN OTHER                                                         
               DISPLAY 'ERROR READING BB24K500..' WS-BB24K500-STAT              
               MOVE WS-BB24K500-STAT             TO RETURN-CODE                 
               PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT                 
           END-EVALUATE.                                                        
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       3600-MOVE-DATA-FOR-REPORT.                                               
      ****************************                                              
                                                                                
           MOVE F410-NUM-REGN                 TO  WS-RPT-REGNO                  
                                                  WS-RPT-REGNO-EC.              
           MOVE WS-NRIC-KEY                   TO  WS-RPT-UIN                    
                                                  WS-RPT-UIN-EC.                
                                                                                
           STRING F410-DTE-KEY-ISSUE(7:2) '/'                                   
                  F410-DTE-KEY-ISSUE(5:2) '/'                                   
                  F410-DTE-KEY-ISSUE(1:4)                                       
                  DELIMITED BY SIZE         INTO  WS-RPT-SODATE.                
                                                                                
           MOVE WS-RPT-SODATE                 TO  WS-RPT-SODATE-EC.             
                                                                                
           MOVE K800-NUM-LVY-RSL-STATUS       TO  WS-RPT-SOC-STAT               
                                                  WS-RPT-SOC-STAT-EC.           
                                                                                
           IF WS-K500-NUM-HDB-REF = SPACES OR LOW-VALUES                        
              MOVE SPACES                     TO  WS-RPT-RESALE-SCH             
                                                  WS-RPT-RESALE-SCH-EC          
           ELSE                                                                 
              STRING WS-K500-NUM-HDB-REF(1:4) '-'                               
                     WS-K500-NUM-HDB-REF(5:4) '-'                               
                     WS-K500-NUM-HDB-REF(9:1) '-'                               
                     WS-K500-NUM-HDB-REF(10:2)                                  
                     DELIMITED BY SIZE      INTO  WS-RPT-RESALE-SCH             
                                                                                
              MOVE WS-RPT-RESALE-SCH          TO  WS-RPT-RESALE-SCH-EC          
           END-IF.                                                              
                                                                                
           MOVE WS-RESALE-STATUS              TO  WS-RPT-RSL-STAT               
                                                  WS-RPT-RSL-STAT-EC.           
                                                                                
       3600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       4000-PRINT-REPORT.                                                       
      ****************************                                              
                                                                                
           IF WS-INITIAL-REGNO = 'Y'                                            
              IF WS-RPT-QNTM-TYPE = 'EC'                                        
                 WRITE BP13L44A-REC  FROM WS-DETAIL-RPT-EC AFTER 1              
              ELSE                                                              
                 WRITE BP13L44A-REC  FROM WS-DETAIL-RPT AFTER 1                 
              END-IF                                                            
              MOVE 'N'  TO WS-INITIAL-REGNO                                     
              IF WS-INITIAL-UIN = 'Y'                                           
                 MOVE 'N'  TO WS-INITIAL-UIN                                    
              END-IF                                                            
           ELSE                                                                 
              IF WS-INITIAL-UIN = 'Y'                                           
                 MOVE SPACES           TO WS-RPT-REGNO                          
                                          WS-RPT-REGNO-EC                       
                 IF WS-RPT-QNTM-TYPE = 'EC'                                     
                    WRITE BP13L44A-REC  FROM WS-DETAIL-RPT-EC AFTER 1           
                 ELSE                                                           
                    WRITE BP13L44A-REC  FROM WS-DETAIL-RPT AFTER 1              
                 END-IF                                                         
                 MOVE 'N'  TO WS-INITIAL-UIN                                    
              ELSE                                                              
                 MOVE SPACES           TO WS-RPT-REGNO                          
                                          WS-RPT-REGNO-EC                       
                                          WS-RPT-UIN                            
                                          WS-RPT-UIN-EC                         
                                          WS-RPT-SODATE                         
                                          WS-RPT-SODATE-EC                      
                                          WS-RPT-SOC-STAT                       
                                          WS-RPT-SOC-STAT-EC                    
                                          WS-RPT-QNTM-TYPE                      
                                          WS-RPT-QNTM-TYPE-EC                   
                                          WS-RPT-QNTM-HDBREF                    
                                          WS-RPT-QNTM-SLS-TYPE                  
                                          WS-RPT-QNTM-RSL-STAT                  
                                          WS-RPT-QNTM-CURR-LESS                 
                                          WS-RPT-QNTM-BLK                       
                                          WS-RPT-QNTM-UNIT                      
                                          WS-RPT-QNTM-SNAME                     
                 IF WS-RPT-QNTM-TYPE = 'EC'                                     
                    WRITE BP13L44A-REC  FROM WS-DETAIL-RPT-EC AFTER 1           
                 ELSE                                                           
                    WRITE BP13L44A-REC  FROM WS-DETAIL-RPT AFTER 1              
                 END-IF                                                         
              END-IF                                                            
           END-IF.                                                              
                                                                                
           ADD 1               TO   WS-LINE-CTR                                 
                                    WS-WRITTEN-BP13L44A.                        
                                                                                
           PERFORM 4100-LINE-CTR   THRU 4100-EXIT.                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       4100-LINE-CTR.                                                           
      ****************************                                              
                                                                                
           IF WS-LINE-CTR > 58                                                  
              MOVE 0                     TO   WS-LINE-CTR                       
              ADD 1                      TO   WS-PAGE-CTR                       
              PERFORM 1100-WRITE-HEADERS THRU 1100-EXIT                         
              MOVE 'Y'                   TO   WS-INITIAL-REGNO                  
           END-IF.                                                              
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ****************************                                              
       9999-CLOSE-FILES.                                                        
      ****************************                                              
                                                                                
           IF (EOF-FLAG-BP13F410 = 'Y') AND                                     
              (WS-WRITTEN-BP13L44A = 0)                                         
              WRITE BP13L44A-REC FROM WS-BLANK-LINE AFTER 1                     
              WRITE BP13L44A-REC FROM WS-NIL-RPT1 AFTER 1                       
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'REPORT DESCRIPTION'.                                        
           DISPLAY '------------------'.                                        
           DISPLAY SPACES.                                                      
           DISPLAY 'NO BP13F410 READ                : '                         
                                          WS-READ-BP13F410.                     
           DISPLAY 'NO BP13F410 BYPASS (NOT IN K800): '                         
                                          WS-BYPASS1-BP13F410.                  
           DISPLAY 'NO BP13F410 BYPASS (NON LN/LNS) : '                         
                                          WS-BYPASS2-BP13F410.                  
           DISPLAY 'NO BP13F410 PROCESS (LN/LNS)    : '                         
                                          WS-PROCESS-BP13F410                   
           DISPLAY 'NO OF BP13L44A RECORDS WRITTEN  : '                         
                                          WS-WRITTEN-BP13L44A.                  
           DISPLAY ' '.                                                         
                                                                                
           CLOSE BP13F410                                                       
                 BP13K800                                                       
                 BB24K510                                                       
                 BB24K500                                                       
                 BP13K162                                                       
                 AB03K030                                                       
                 BP13L44A.                                                      
                                                                                
           IF WS-BP13K800-STAT  NOT = '00'                                      
              DISPLAY 'ERROR IN CLOSING BP13K800   STATUS - '                   
              WS-BP13K800-STAT                                                  
              MOVE WS-BP13K800-STAT             TO RETURN-CODE                  
           END-IF.                                                              
                                                                                
           IF WS-BB24K510-STAT  NOT = '00'                                      
              DISPLAY 'ERROR IN CLOSING BB24K510   STATUS - '                   
              WS-BB24K510-STAT                                                  
              MOVE WS-BB24K510-STAT             TO RETURN-CODE                  
           END-IF.                                                              
                                                                                
           IF WS-BB24K500-STAT  NOT = '00'                                      
              DISPLAY 'ERROR IN CLOSING BB24K500   STATUS - '                   
              WS-BB24K500-STAT                                                  
              MOVE WS-BB24K500-STAT             TO RETURN-CODE                  
           END-IF.                                                              
                                                                                
           IF WS-BP13K162-STAT  NOT = '00'                                      
              DISPLAY 'ERROR IN CLOSING BP13K162   STATUS - '                   
              WS-BP13K162-STAT                                                  
              MOVE WS-BP13K162-STAT             TO RETURN-CODE                  
           END-IF.                                                              
                                                                                
           IF WS-AB03K030-STAT  NOT = '00'                                      
              DISPLAY 'ERROR IN CLOSING AB03K030   STATUS - '                   
              WS-AB03K030-STAT                                                  
              MOVE WS-AB03K030-STAT             TO RETURN-CODE                  
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
       9999-EXIT.                                                               
           EXIT.                                                                
