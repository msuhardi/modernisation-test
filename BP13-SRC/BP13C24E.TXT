       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C24E.                                                 
       AUTHOR.        ELAINE S ARGA.                                            
      *DATE-WRITTEN.  01 APR 2010.                                              
      ***************************************************************           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      ***************************************************************           
      * OBJECTIVE: PRINT REPORT FOR FIS RECORDS WITH DIFFERENT      *           
      *            OFFER AND CURRENT SELLING PRICE                  *           
      *-------------------------------------------------------------*           
      * INPUT FILES: BM06F190                                       *           
      *              BM06K110                                       *           
      *              BM06K510                                       *           
      *              BP13K276                                       *           
      *                                                             *           
      * OUTPUT FILE: BP13L24E (REPORT)                              *           
      *-------------------------------------------------------------*           
      * CHG REF  OIC  DATE    DESCRIPTION                           *           
      *--------- ---  ------  --------------------------------------*           
      * BP133854 ESA1 010410  NEW PROGRAM(MODELLED AFTER BP13C22P)  *           
      * BP134411 AB9  211211  INCREASE LENGTH OF BP13K276 TO 400.   *           
      ***************************************************************           
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.  IBM-3090.                                              
       OBJECT-COMPUTER.  IBM-3090.                                              
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BM06K110  ASSIGN        TO BM06K110                           
                            ORGANIZATION  IS INDEXED                            
                            ACCESS MODE   IS DYNAMIC                            
                            RECORD KEY    IS K110-KEY-FLD                       
                            ALTERNATE KEY IS K110-AIX1                          
                            FILE STATUS   IS WS-K110-STATUS.                    
                                                                                
           SELECT BM06K510  ASSIGN       TO BM06K510                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K510-ESTCDE                         
                            FILE STATUS  IS WS-K510-STATUS.                     
                                                                                
           SELECT BP13K276  ASSIGN       TO BP13K276                            
                            ORGANIZATION IS INDEXED                             
                            ACCESS MODE  IS RANDOM                              
                            RECORD KEY   IS K276-KEY-FLD                        
                            FILE STATUS  IS WS-K276-STATUS.                     
                                                                                
           SELECT BM06F190  ASSIGN TO BM06F190.                                 
                                                                                
           SELECT BP13L24E  ASSIGN TO BP13L24E.                                 
                                                                                
       DATA DIVISION.                                                           
      *--------------------------------------------------------------           
       FILE SECTION.                                                            
      *--------------------------------------------------------------           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BM06K510                                                             
           RECORD CONTAINS 60  CHARACTERS.                                      
       COPY BM06K510.                                                           
                                                                                
       FD  BP13K276                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K276.                                                           
                                                                                
       FD  BM06F190                                                             
           RECORD CONTAINS 650 CHARACTERS                                       
           RECORDING MODE IS F.                                                 
       COPY BM06F190.                                                           
                                                                                
       FD  BP13L24E                                                             
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS OMITTED                                                
           RECORDING MODE IS F.                                                 
       01  BP13L24E-REC                   PIC X(132).                           
                                                                                
      *--------------------------------------------------------------           
       WORKING-STORAGE SECTION.                                                 
      *--------------------------------------------------------------           
       01  WS-WORK-VAR.                                                         
           05  WS-SYSTEM-DATE.                                                  
               10  WS-SYS-DATE            PIC X(08) VALUE SPACES.               
               10  FILLER              PIC X(13) VALUE SPACES.                  
           05  WS-DISP-DATE.                                                    
               10  WS-DISP-DD          PIC X(02) VALUE SPACES.                  
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-DISP-MM          PIC X(02) VALUE SPACES.                  
               10  FILLER              PIC X(01) VALUE '/'.                     
               10  WS-DISP-CCYY        PIC X(04) VALUE SPACES.                  
           05  WS-RETCODE              PIC -ZZZZ.                               
           05  WS-F190-ALT-KEY         PIC X(160).                              
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K110-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K510-STATUS          PIC 9(02) VALUE ZEROES.                  
           05  WS-K276-STATUS          PIC 9(02) VALUE ZEROES.                  
                                                                                
       01  WS-SWITCHES.                                                         
           05  WS-END-OF-F190-SW       PIC X(01) VALUE SPACES.                  
               88  C-END-OF-F190       VALUE 'Y'.                               
           05  WS-END-OF-K110-SW       PIC X(01) VALUE SPACES.                  
               88  C-END-OF-K110       VALUE 'Y'.                               
           05  WS-FIRST-K110-REC-SW    PIC X(01) VALUE SPACES.                  
               88  C-FIRST-K110-REC    VALUE 'Y'.                               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F190-READ-CTR        PIC 9(06) VALUE ZEROES.                  
           05  WS-LINE-CTR             PIC 9(02) VALUE ZEROES.                  
           05  WS-PAGE-CTR             PIC 9(04) VALUE ZEROES.                  
           05  WS-PRT-CTR              PIC 9(04) VALUE ZEROES.                  
           05  WS-SNO-CTR              PIC 9(04) VALUE ZEROES.                  
           05  WS-ESTATE-CTR           PIC 9(04) VALUE ZEROES.                  
                                                                                
      *-------------------------------------------------------------            
      *    BP13C913 PARAMETERS                                                  
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH-ACC.                                              
              10 WS-LINK-NUM-SCH       PIC X(4).                                
              10 WS-LINK-NUM-ACC       PIC X(5).                                
       COPY P13COMM8.                                                           
                                                                                
      *--------------------------------------------------------------           
      *   REPORT LAYOUT                                                         
      *--------------------------------------------------------------           
       01 L24E-HEADER-1.                                                        
          05 FILLER              PIC X(08)  VALUE 'BP13L24E'.                   
          05 FILLER              PIC X(05)  VALUE SPACES .                      
          05 FILLER              PIC X(08)  VALUE 'HDBCAT3'.                    
          05 FILLER              PIC X(22)  VALUE SPACES.                       
          05 FILLER              PIC X(40)  VALUE                               
             'S Y S T E M   O F   C O M M I T M E N T'.                         
          05 FILLER              PIC X(28)  VALUE SPACES.                       
          05 FILLER              PIC X(07)  VALUE 'DATE :'.                     
          05 L24E-DATE           PIC X(10)  VALUE SPACES.                       
                                                                                
       01 L24E-HEADER-2.                                                        
          05 FILLER              PIC X(30)  VALUE SPACES .                      
          05 FILLER              PIC X(46)  VALUE                               
             'REPORT ON FIS RECORDS WITH DIFFERENT OFFER AND'.                  
          05 FILLER              PIC X(26)  VALUE                               
             ' CURRENT SELLING PRICE'.                                          
          05 FILLER              PIC X(09)  VALUE SPACES.                       
          05 FILLER              PIC X(07)  VALUE 'PAGE : '.                    
          05 L24E-PAGE           PIC ZZZ9 .                                     
                                                                                
       01 L24E-HEADER-3.                                                        
          05 FILLER              PIC X(55)  VALUE                               
             'S/NO  ESTATE                NEIGH/CONT  BLK    SCHEME A'.         
          05 FILLER              PIC X(55)  VALUE                               
             'CCT NO  POD  POF  COST OF  COST OF  FLEXI  FLEXI       '.         
          05 FILLER              PIC X(20)  VALUE                               
             '  SELLING PRICE'.                                                 
                                                                                
       01 L24E-HEADER-4.                                                        
          05 FILLER              PIC X(55)  VALUE SPACES.                       
          05 FILLER              PIC X(55)  VALUE                               
             '        TAG  TAG  DOORS    FLOOR    CODE   COST      OF'.         
          05 FILLER              PIC X(25)  VALUE                               
             'FER       CURRENT'.                                               
                                                                                
       01 L24E-DETAIL.                                                          
          05 L24E-SNO                PIC ZZZZ   VALUE ZEROES.                   
          05 FILLER                  PIC X(02)  VALUE SPACES.                   
          05 L24E-NME-ESTATE         PIC X(20)  VALUE SPACES.                   
          05 FILLER                  PIC X(02)  VALUE SPACES.                   
          05 L24E-NEIGH-CONT         PIC X(09)  VALUE SPACES.                   
          05 FILLER                  PIC X(03)  VALUE SPACES.                   
          05 L24E-NUM-BLK            PIC X(05)  VALUE SPACES.                   
          05 FILLER                  PIC X(02)  VALUE SPACES.                   
          05 L24E-SCH-ACCT           PIC X(14)  VALUE SPACES.                   
          05 FILLER                  PIC X(03)  VALUE SPACES.                   
          05 L24E-POD-TAG            PIC X(01)  VALUE SPACES.                   
          05 FILLER                  PIC X(04)  VALUE SPACES.                   
          05 L24E-POF-TAG            PIC X(01)  VALUE SPACES.                   
          05 FILLER                  PIC X(03)  VALUE SPACES.                   
          05 L24E-AMT-POD            PIC ZZZ,ZZZ VALUE ZEROES.                  
          05 FILLER                  PIC X(02)  VALUE SPACES.                   
          05 L24E-AMT-POF            PIC ZZZ,ZZZ VALUE ZEROES.                  
          05 FILLER                  PIC X(03)  VALUE SPACES.                   
          05 L24E-CDE-FLEXI          PIC X(01)  VALUE SPACES.                   
          05 FILLER                  PIC X(05)  VALUE SPACES.                   
          05 L24E-AMT-FLEXI          PIC ----,--9 VALUE ZEROES.                 
          05 FILLER                  PIC X(02)  VALUE SPACES.                   
          05 L24E-OFFER-SELL-PRICE   PIC ZZ,ZZZ,ZZ9 VALUE ZEROES.               
          05 FILLER                  PIC X(02)  VALUE SPACES.                   
          05 L24E-CURR-SELL-PRICE    PIC ZZ,ZZZ,ZZ9 VALUE ZEROES.               
                                                                                
       01  L24E-LINE                 PIC X(132)  VALUE ALL '-'.                 
                                                                                
      *--------------------------------------------------------------           
       PROCEDURE DIVISION.                                                      
      *--------------------------------------------------------------           
                                                                                
      *--------------------------------*                                        
       0000-MAIN-ROUTINE.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 1000-INITIALIZATION    THRU 1000-EXIT.                       
           PERFORM 2000-PROCESS-RECORDS   THRU 2000-EXIT                        
             UNTIL C-END-OF-F190.                                               
           PERFORM 9000-CLOSE-FILES       THRU 9000-EXIT.                       
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       1000-INITIALIZATION.                                                     
      *--------------------------------*                                        
                                                                                
           OPEN INPUT    BM06F190                                               
                         BM06K110                                               
                         BM06K510                                               
                         BP13K276                                               
                OUTPUT   BP13L24E.                                              
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BM06K110-STATUS ' WS-K110-STATUS              
              MOVE WS-K110-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K510-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BM06K510-STATUS ' WS-K510-STATUS              
              MOVE WS-K510-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           IF WS-K276-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR-BP13K276-STATUS ' WS-K276-STATUS              
              MOVE WS-K276-STATUS      TO RETURN-CODE                           
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE  TO WS-SYSTEM-DATE.                       
           MOVE WS-SYS-DATE(1:4)       TO WS-DISP-CCYY.                         
           MOVE WS-SYS-DATE(5:2)       TO WS-DISP-MM.                           
           MOVE WS-SYS-DATE(7:2)       TO WS-DISP-DD.                           
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2000-PROCESS-RECORDS.                                                    
      *--------------------------------*                                        
                                                                                
           READ BM06F190 AT END                                                 
                MOVE 'Y'     TO WS-END-OF-F190-SW                               
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1      TO WS-F190-READ-CTR.                                      
           MOVE 'Y'   TO WS-FIRST-K110-REC-SW.                                  
                                                                                
           PERFORM 2100-START-BM06K110           THRU 2100-EXIT.                
           PERFORM 2200-PROCESS-BM06K110         THRU 2200-EXIT                 
             UNTIL WS-F190-ALT-KEY NOT = K110-AIX1                              
                OR C-END-OF-K110.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2100-START-BM06K110.                                                     
      *--------------------------------*                                        
                                                                                
           MOVE F190-2-ESTATE           TO K110-ESTATE.                         
           MOVE F190-2-NEIGHBOURHOOD    TO K110-NEIGHBOURHOOD.                  
           MOVE F190-2-CONTRACT-NO      TO K110-CONTRACT-NO.                    
           MOVE F190-2-BLK-NO           TO K110-BLK-NO.                         
           MOVE K110-AIX1               TO WS-F190-ALT-KEY.                     
                                                                                
           START BM06K110 KEY NOT LESS THAN K110-AIX1.                          
           EVALUATE WS-K110-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BM06K110: ' K110-AIX1           
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BM06K110,STATUS ' WS-K110-STATUS         
                   MOVE WS-K110-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       2200-PROCESS-BM06K110.                                                   
      *--------------------------------*                                        
                                                                                
           READ BM06K110 NEXT RECORD AT END                                     
                MOVE 'Y'      TO WS-END-OF-K110-SW                              
                GO TO 2200-EXIT.                                                
                                                                                
           IF WS-F190-ALT-KEY NOT = K110-AIX1                                   
              GO TO 2200-EXIT                                                   
           END-IF.                                                              
                                                                                
           IF (K110-CDE-POD-TAG = 'Y') OR                                       
              (K110-CDE-POF-TAG = 'Y') OR                                       
              (K110-1ST-OFFER-SELLING-PRICE NOT =                               
               K110-CURR-SELLING-PRICE)                                         
              PERFORM 3000-PRINT-REPORT           THRU 3000-EXIT                
           END-IF.                                                              
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3000-PRINT-REPORT.                                                       
      *--------------------------------*                                        
                                                                                
           PERFORM 3300-CHECK-LINE-CTR            THRU 3300-EXIT.               
                                                                                
           IF C-FIRST-K110-REC                                                  
              MOVE 'N'          TO WS-FIRST-K110-REC-SW                         
              PERFORM 3100-MOVE-ESTATE-DETAILS    THRU 3100-EXIT                
              MOVE SPACES       TO BP13L24E-REC                                 
              WRITE BP13L24E-REC                                                
              ADD   1           TO WS-LINE-CTR                                  
                                   WS-ESTATE-CTR                                
           END-IF.                                                              
                                                                                
           PERFORM 3200-MOVE-POD-POF-DETAILS      THRU 3200-EXIT.               
                                                                                
           PERFORM 3300-CHECK-LINE-CTR            THRU 3300-EXIT.               
                                                                                
           WRITE BP13L24E-REC    FROM L24E-DETAIL.                              
           ADD   1                 TO WS-LINE-CTR.                              
           MOVE SPACES             TO L24E-DETAIL.                              
           ADD   1                 TO WS-PRT-CTR.                               
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3100-MOVE-ESTATE-DETAILS.                                                
      *--------------------------------*                                        
                                                                                
           PERFORM 3110-READ-BM06K510          THRU 3110-EXIT.                  
                                                                                
           ADD  1   TO WS-SNO-CTR.                                              
           MOVE WS-SNO-CTR              TO L24E-SNO.                            
           MOVE K510-ESTATE-NAME        TO L24E-NME-ESTATE.                     
           STRING F190-2-NEIGHBOURHOOD '/' F190-2-CONTRACT-NO                   
                  DELIMITED BY SIZE   INTO L24E-NEIGH-CONT.                     
           MOVE F190-2-BLK-NO           TO L24E-NUM-BLK.                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3110-READ-BM06K510.                                                      
      *--------------------------------*                                        
                                                                                
           MOVE K110-ESTATE             TO K510-ESTCDE.                         
           READ BM06K510.                                                       
                                                                                
           EVALUATE WS-K510-STATUS                                              
              WHEN 00                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BM06K510: ' K510-ESTCDE         
                   MOVE SPACES     TO K510-REC                                  
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BM06K510,STATUS ' WS-K510-STATUS         
                   MOVE WS-K510-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3110-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3200-MOVE-POD-POF-DETAILS.                                               
      *--------------------------------*                                        
                                                                                
           PERFORM 3210-READ-BP13K276            THRU 3210-EXIT.                
           PERFORM 3220-READ-PIDB                THRU 3220-EXIT.                
                                                                                
           PERFORM 3230-MOVE-POD-POF-AMT         THRU 3230-EXIT.                
                                                                                
           STRING K110-SCH-ACC-NO (1:4) '-'                                     
                  K110-SCH-ACC-NO (5:4) '-'                                     
                  K110-SCH-ACC-NO (9:1) '-'                                     
                  K110-SCH-ACC-NO (10:2)                                        
                  DELIMITED BY SIZE  INTO L24E-SCH-ACCT.                        
           MOVE K110-CDE-POD-TAG       TO L24E-POD-TAG.                         
           MOVE K110-CDE-POF-TAG       TO L24E-POF-TAG.                         
           MOVE K110-CDE-FLEXI         TO L24E-CDE-FLEXI.                       
           MOVE K110-1ST-OFFER-SELLING-PRICE  TO L24E-OFFER-SELL-PRICE.         
           MOVE K110-CURR-SELLING-PRICE       TO L24E-CURR-SELL-PRICE.          
                                                                                
       3200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3210-READ-BP13K276.                                                      
      *--------------------------------*                                        
                                                                                
           MOVE F190-2-ESTATE           TO K276-NUM-ESTATE.                     
           MOVE F190-2-NEIGHBOURHOOD    TO K276-NUM-NEIGHBOUR.                  
           MOVE F190-2-CONTRACT-NO      TO K276-NUM-CONTR.                      
           MOVE F190-2-BLK-NO           TO K276-NUM-BLK.                        
                                                                                
           READ BP13K276.                                                       
                                                                                
           EVALUATE WS-K276-STATUS                                              
              WHEN 00                                                           
                   CONTINUE                                                     
              WHEN 23                                                           
                   DISPLAY 'RECORD NOT FOUND IN BP13K276, KEY: '                
                            K276-KEY-FLD                                        
                   MOVE SPACES     TO BP13K276-REC                              
                   INITIALIZE         BP13K276-REC                              
              WHEN OTHER                                                        
                   DISPLAY 'READ ERROR BP13K276,STATUS ' WS-K276-STATUS         
                   MOVE WS-K276-STATUS        TO RETURN-CODE                    
                   PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                    
           END-EVALUATE.                                                        
                                                                                
       3210-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3220-READ-PIDB.                                                          
      *--------------------------------*                                        
                                                                                
           MOVE SPACES           TO BP13COMM8-REC.                              
           INITIALIZE               BP13COMM8-REC.                              
           MOVE K110-SCH-ACC-NO  TO WS-LINK-REC.                                
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC                                    
                                 BP13COMM8-REC.                                 
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
              WHEN +000                                                         
                 CONTINUE                                                       
              WHEN +100                                                         
                 MOVE SPACES   TO BP13COMM8-REC                                 
                 INITIALIZE       BP13COMM8-REC                                 
                 DISPLAY 'REC NOT FOUND IN PIDB: ' K110-SCH-ACC-NO              
              WHEN OTHER                                                        
                 MOVE COMM8-CDE-SYSERR      TO WS-RETCODE                       
                 DISPLAY 'ERROR IN CALLING BP13C913, RETCODE: '                 
                         WS-RETCODE                                             
                 MOVE 99                    TO RETURN-CODE                      
                 PERFORM 9000-CLOSE-FILES        THRU 9000-EXIT                 
           END-EVALUATE.                                                        
                                                                                
                                                                                
       3220-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3230-MOVE-POD-POF-AMT.                                                   
      *--------------------------------*                                        
                                                                                
           EVALUATE COMM8-CDE-TYP-ACTLUSE                                       
              WHEN '03'                                                         
                 IF K110-CDE-POD-TAG = 'Y'                                      
                    IF K110-CDE-FLEXI = 'A'                                     
                       MOVE K276-AMT-POD-3RM(1)    TO L24E-AMT-POD              
                    ELSE                                                        
                       IF K110-CDE-FLEXI = 'B'                                  
                          MOVE K276-AMT-POD-3RM(2)    TO L24E-AMT-POD           
                       ELSE                                                     
                          IF K110-CDE-FLEXI = 'C'                               
                             MOVE K276-AMT-POD-3RM(3) TO L24E-AMT-POD           
                          ELSE                                                  
                             MOVE ZEROES              TO L24E-AMT-POD           
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                 ELSE                                                           
                    MOVE ZEROES              TO L24E-AMT-POD                    
                 END-IF                                                         
                 IF K110-CDE-POF-TAG = 'Y'                                      
                    IF K110-CDE-FLEXI = 'A'                                     
                       MOVE K276-AMT-POF-3RM(1)    TO L24E-AMT-POF              
                    ELSE                                                        
                       IF K110-CDE-FLEXI = 'B'                                  
                          MOVE K276-AMT-POF-3RM(2)    TO L24E-AMT-POF           
                       ELSE                                                     
                          IF K110-CDE-FLEXI = 'C'                               
                             MOVE K276-AMT-POF-3RM(3) TO L24E-AMT-POF           
                          ELSE                                                  
                             MOVE ZEROES              TO L24E-AMT-POF           
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                 ELSE                                                           
                    MOVE ZEROES              TO L24E-AMT-POF                    
                 END-IF                                                         
                 IF K110-CDE-FLEXI = 'A'                                        
                    MOVE K276-AMT-OPTION-3RM(1) TO L24E-AMT-FLEXI               
                 ELSE                                                           
                    IF K110-CDE-FLEXI = 'B'                                     
                       MOVE K276-AMT-OPTION-3RM(2) TO L24E-AMT-FLEXI            
                    ELSE                                                        
                       IF K110-CDE-FLEXI = 'C'                                  
                          MOVE K276-AMT-OPTION-3RM(3) TO L24E-AMT-FLEXI         
                       ELSE                                                     
                          MOVE ZEROES                 TO L24E-AMT-FLEXI         
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
                                                                                
              WHEN '04'                                                         
                 IF K110-CDE-POD-TAG = 'Y'                                      
                    IF K110-CDE-FLEXI = 'A'                                     
                       MOVE K276-AMT-POD-4RM(1)    TO L24E-AMT-POD              
                    ELSE                                                        
                       IF K110-CDE-FLEXI = 'B'                                  
                          MOVE K276-AMT-POD-4RM(2)    TO L24E-AMT-POD           
                       ELSE                                                     
                          IF K110-CDE-FLEXI = 'C'                               
                             MOVE K276-AMT-POD-4RM(3) TO L24E-AMT-POD           
                          ELSE                                                  
                             MOVE ZEROES              TO L24E-AMT-POD           
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                 ELSE                                                           
                    MOVE ZEROES              TO L24E-AMT-POD                    
                 END-IF                                                         
                 IF K110-CDE-POF-TAG = 'Y'                                      
                    IF K110-CDE-FLEXI = 'A'                                     
                       MOVE K276-AMT-POF-4RM(1)    TO L24E-AMT-POF              
                    ELSE                                                        
                       IF K110-CDE-FLEXI = 'B'                                  
                          MOVE K276-AMT-POF-4RM(2)    TO L24E-AMT-POF           
                       ELSE                                                     
                          IF K110-CDE-FLEXI = 'C'                               
                             MOVE K276-AMT-POF-4RM(3) TO L24E-AMT-POF           
                          ELSE                                                  
                             MOVE ZEROES              TO L24E-AMT-POF           
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                 ELSE                                                           
                    MOVE ZEROES              TO L24E-AMT-POF                    
                 END-IF                                                         
                 IF K110-CDE-FLEXI = 'A'                                        
                    MOVE K276-AMT-OPTION-4RM(1) TO L24E-AMT-FLEXI               
                 ELSE                                                           
                    IF K110-CDE-FLEXI = 'B'                                     
                       MOVE K276-AMT-OPTION-4RM(2) TO L24E-AMT-FLEXI            
                    ELSE                                                        
                       IF K110-CDE-FLEXI = 'C'                                  
                          MOVE K276-AMT-OPTION-4RM(3) TO L24E-AMT-FLEXI         
                       ELSE                                                     
                          MOVE ZEROES                 TO L24E-AMT-FLEXI         
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
                                                                                
              WHEN '05'                                                         
                 IF K110-CDE-POD-TAG = 'Y'                                      
                    IF K110-CDE-FLEXI = 'A'                                     
                       MOVE K276-AMT-POD-5RM(1)    TO L24E-AMT-POD              
                    ELSE                                                        
                       IF K110-CDE-FLEXI = 'B'                                  
                          MOVE K276-AMT-POD-5RM(2)    TO L24E-AMT-POD           
                       ELSE                                                     
                          IF K110-CDE-FLEXI = 'C'                               
                             MOVE K276-AMT-POD-5RM(3) TO L24E-AMT-POD           
                          ELSE                                                  
                             MOVE ZEROES              TO L24E-AMT-POD           
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                 ELSE                                                           
                    MOVE ZEROES              TO L24E-AMT-POD                    
                 END-IF                                                         
                 IF K110-CDE-POF-TAG = 'Y'                                      
                    IF K110-CDE-FLEXI = 'A'                                     
                       MOVE K276-AMT-POF-5RM(1)    TO L24E-AMT-POF              
                    ELSE                                                        
                       IF K110-CDE-FLEXI = 'B'                                  
                          MOVE K276-AMT-POF-5RM(2)    TO L24E-AMT-POF           
                       ELSE                                                     
                          IF K110-CDE-FLEXI = 'C'                               
                             MOVE K276-AMT-POF-5RM(3) TO L24E-AMT-POF           
                          ELSE                                                  
                             MOVE ZEROES              TO L24E-AMT-POF           
                          END-IF                                                
                       END-IF                                                   
                    END-IF                                                      
                 ELSE                                                           
                    MOVE ZEROES              TO L24E-AMT-POF                    
                 END-IF                                                         
                 IF K110-CDE-FLEXI = 'A'                                        
                    MOVE K276-AMT-OPTION-5RM(1) TO L24E-AMT-FLEXI               
                 ELSE                                                           
                    IF K110-CDE-FLEXI = 'B'                                     
                       MOVE K276-AMT-OPTION-5RM(2) TO L24E-AMT-FLEXI            
                    ELSE                                                        
                       IF K110-CDE-FLEXI = 'C'                                  
                          MOVE K276-AMT-OPTION-5RM(3) TO L24E-AMT-FLEXI         
                       ELSE                                                     
                          MOVE ZEROES                 TO L24E-AMT-FLEXI         
                       END-IF                                                   
                    END-IF                                                      
                 END-IF                                                         
           END-EVALUATE.                                                        
                                                                                
       3230-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3300-CHECK-LINE-CTR.                                                     
      *--------------------------------*                                        
                                                                                
           IF WS-LINE-CTR > 56  OR                                              
              WS-LINE-CTR = 0                                                   
              PERFORM 3310-PRINT-PAGE-BREAK      THRU 3310-EXIT                 
           END-IF.                                                              
                                                                                
       3300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       3310-PRINT-PAGE-BREAK.                                                   
      *--------------------------------*                                        
                                                                                
           ADD   1   TO WS-PAGE-CTR.                                            
           MOVE WS-PAGE-CTR      TO L24E-PAGE.                                  
           MOVE WS-DISP-DATE     TO L24E-DATE.                                  
           WRITE BP13L24E-REC  FROM L24E-HEADER-1 AFTER PAGE.                   
           WRITE BP13L24E-REC  FROM L24E-HEADER-2.                              
           WRITE BP13L24E-REC  FROM L24E-LINE     AFTER 2.                      
           WRITE BP13L24E-REC  FROM L24E-HEADER-3.                              
           WRITE BP13L24E-REC  FROM L24E-HEADER-4.                              
           WRITE BP13L24E-REC  FROM L24E-LINE.                                  
           MOVE 7                TO WS-LINE-CTR.                                
                                                                                
       3310-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *--------------------------------*                                        
       9000-CLOSE-FILES.                                                        
      *--------------------------------*                                        
                                                                                
           DISPLAY 'PROGRAM ID   : BP13C24E'.                                   
           DISPLAY 'CURRENT DATE : ' WS-DISP-DATE.                              
           DISPLAY SPACES.                                                      
           DISPLAY '*=============== COUNTERS ===============*'.                
           DISPLAY 'BM06F190 RECORDS READ       : ' WS-F190-READ-CTR.           
           DISPLAY 'BM06F190 RECORDS PRINTED    : ' WS-ESTATE-CTR.              
           DISPLAY 'NO. OF RECORDS PRINTED      : ' WS-PRT-CTR.                 
                                                                                
           CLOSE BM06K110                                                       
                 BM06K510                                                       
                 BP13K276                                                       
                 BM06F190                                                       
                 BP13L24E.                                                      
                                                                                
            IF WS-K110-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BM06K110-STATUS ' WS-K110-STATUS            
            END-IF.                                                             
                                                                                
            IF WS-K510-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BM06K510-STATUS ' WS-K510-STATUS            
            END-IF.                                                             
                                                                                
            IF WS-K276-STATUS NOT = 00                                          
               DISPLAY 'CLOSE ERROR-BP13K276-STATUS ' WS-K276-STATUS            
            END-IF.                                                             
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
