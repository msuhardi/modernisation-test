      ***************************                                               
       IDENTIFICATION DIVISION.                                                 
      ***************************                                               
                                                                                
       PROGRAM-ID.    BP13C601.                                                 
       AUTHOR.        LJL.                                                      
       DATE-WRITTEN.  19/3/92.                                                  
      * ======================================================== *              
      *                SYSTEM OF COMMITMENT (BP13)               *              
      * ======================================================== *              
      *                                                          *              
      *    OBJECTIVE   :                                         *              
      *      1.  THIS IS A PROGRAM TO UPDATE SOC MASTER          *              
      *          FROM SALES ORDER FILE ON SA, AO, DELIVERY       *              
      *          POSSESSION DATE AND STAMP FEES (CF & LSF)       *              
      *          AND ZEROLISED BK FEE IN RFF FOR TRANSFER CASE.  *              
      *                                                          *              
      *     INPUT FILES:                                         *              
      *      1.  SORTED SALES ORDER  (BP13F410)                  *              
      *      2.  SOC MASTER FILE     (BP13K800)                  *              
      *                                                          *              
      *     OUTPUT FILES:                                        *              
      *      1.  SOC MASTER FILE     (BP13K800)                  *              
      *                                                          *              
      * ======================================================== *              
      *  CHG-NO  BY  ON      DESCRIPTION                         *              
      *  ------  --- ------  -----------                         *              
      * I920718  JEP 930716  CONVERT TO COBOL II.                *              
      * SOCPH8B  JEP 930716  USE BP13K800 FILE INSTEAD OF        *              
      *                          P10K020  FILE                   *              
      * C930993  LJL 940118  TO CATER FOR GST FOR CONVEYANCING   *              
      *                           FEES.                          *              
      * BP130020 SSS 951017  RENAME BP13-L601 TO BP13L601        *              
      *                      RENAME BP13L601 TO P13L601A         *              
      * BP130302 RHB 980406  TO CATER FOR REGN DEPOSIT.          *              
      * BP130418 SIA 980326  CATER FOR YEAR 2000                 *              
      * BP130418 RHB 980609  Y2K                                 *              
      * BP135041 ESD1 051214 CATER FOR MGL CASES.                *              
      * BP137484 DN8  021018 CATER FOR DDS TP CASES.             *              
      * BP138321 ZDD1 261120 CATER FOR MGL REVISED POLICY        *              
      * BP138665 KR13 281021 REMOVED REPORTS L601 & L601A        *              
      * ======================================================== *              
                                                                                
      ************************                                                  
       ENVIRONMENT DIVISION.                                                    
      ************************                                                  
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13K800 ASSIGN       TO BP13K800                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K800-NUM-REGN                        
                           FILE STATUS  IS WS-K800-FILE-STATUS.                 
                                                                                
           SELECT BP13K480 ASSIGN       TO BP13K480                             
                           ORGANIZATION IS INDEXED                              
                           ACCESS MODE  IS RANDOM                               
                           RECORD KEY   IS K480-NUM-REGN                        
                           FILE STATUS  IS WS-K480-FILE-STATUS.                 
                                                                                
           SELECT BP13F410 ASSIGN       TO BP13F410                             
                           ACCESS MODE  IS SEQUENTIAL.                          
                                                                                
                                                                                
      *****************                                                         
       DATA DIVISION.                                                           
      *****************                                                         
       FILE SECTION.                                                            
                                                                                
       FD   BP13K800                                                            
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13K800.                                                           
                                                                                
       FD   BP13K480                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K480.                                                           
                                                                                
       FD   BP13F410                                                            
            RECORDING MODE  IS   F                                              
            RECORD CONTAINS 2000 CHARACTERS.                                    
       COPY BP13F410.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-INDICATORS.                                                       
           05  WS-K800-FILE-STATUS      PIC 99       VALUE ZEROES.              
           05  WS-K480-FILE-STATUS      PIC 99       VALUE ZEROES.              
           05  WS-EOF-F410              PIC X(3)     VALUE 'NO '.               
               88  EOF-OF-F410                       VALUE 'YES'.               
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-K800-OUTPUT           PIC 9(7)     VALUE ZEROES.              
           05  WS-K480-OUTPUT           PIC 9(7)     VALUE ZEROES.              
           05  WS-PAGE                  PIC 9(3)     VALUE ZEROES.              
           05  WS-PAGE1                 PIC 9(3)     VALUE ZEROES.              
           05  WS-LINE                  PIC 9(2)     VALUE 60.                  
           05  WS-LINE1                 PIC 9(2)     VALUE 60.                  
                                                                                
       01  WS-STORE-FIELDS.                                                     
           05  WS-BK                    PIC S9(4)V99 VALUE +0.                  
           05  WS-CD                    PIC S9(7)V99 VALUE +0.                  
           05  WS-CD-CASH               PIC S9(7)V99 VALUE +0.                  
           05  WS-CD-CASH-RDE           PIC S9(7)V99 VALUE +0.                  
           05  WS-CD-CPF1               PIC S9(7)V99 VALUE +0.                  
           05  WS-CD-CPF2               PIC S9(7)V99 VALUE +0.                  
           05  WS-HIST                  PIC 9(3)     VALUE 0.                   
                                                                                
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURR-CENT             PIC X(2).                               
           05  WS-CURR-DTE.                                                     
               10  WS-CURR-YY           PIC 9(2).                               
               10  WS-CURR-MM           PIC 9(2).                               
               10  WS-CURR-DD           PIC 9(2).                               
                                                                                
       01  WS-EDITED-DATE.                                                      
           05  WS-EDITED-DD             PIC X(2)     VALUE SPACES.              
           05  FILLER                   PIC X        VALUE '/'.                 
           05  WS-EDITED-MM             PIC X(2)     VALUE SPACES.              
           05  FILLER                   PIC X        VALUE '/'.                 
           05  WS-EDITED-CC             PIC X(2)     VALUE SPACES.              
           05  WS-EDITED-YY             PIC X(2)     VALUE SPACES.              
                                                                                
       01  WS-ARGORDER-DATE.                                                    
           05  WS-ARGORDER-YY           PIC X(4)     VALUE SPACES.              
           05  WS-ARGORDER-MM           PIC X(2)     VALUE SPACES.              
           05  WS-ARGORDER-DD           PIC X(2)     VALUE SPACES.              
                                                                                
       01  WS-KIV-OUTPUT                PIC 999    VALUE ZEROS.                 
       01  WS-F410-INPUT                PIC 999    VALUE ZEROS.                 
                                                                                
                                                                                
      **********************                                                    
       PROCEDURE DIVISION.                                                      
      **********************                                                    
      *==========================================*                              
      * PROGRAM-LOGIC.                           *                              
      *------------------------------------------*                              
      *    10-INITIALIZATION.                    *                              
      *    20-MAIN-PROCESS.                      *                              
      *    30-FINALIZATION.                      *                              
      *==========================================*                              
                                                                                
      *********************                                                     
       10-INITIALIZATION.                                                       
      *********************                                                     
                                                                                
           OPEN INPUT  BP13F410                                                 
                I-O    BP13K800                                                 
                       BP13K480.                                                
                                                                                
           IF WS-K800-FILE-STATUS NOT = 00 AND 97                               
              DISPLAY 'BP13K800 - OPEN ERROR ' WS-K800-FILE-STATUS              
              MOVE WS-K800-FILE-STATUS TO RETURN-CODE                           
              PERFORM 30-FINALIZATION.                                          
                                                                                
           IF WS-K480-FILE-STATUS NOT = 00 AND 97                               
              DISPLAY 'BP13K480 - OPEN ERROR ' WS-K480-FILE-STATUS              
              MOVE WS-K480-FILE-STATUS TO RETURN-CODE                           
              PERFORM 30-FINALIZATION.                                          
                                                                                
           MOVE    FUNCTION CURRENT-DATE TO  WS-CURRENT-DATE.                   
      *    ACCEPT  WS-CURR-DTE       FROM  DATE.                                
           MOVE    WS-CURR-CENT      TO    WS-EDITED-CC.                        
           MOVE    WS-CURR-YY        TO    WS-EDITED-YY.                        
           MOVE    WS-CURR-MM        TO    WS-EDITED-MM.                        
           MOVE    WS-CURR-DD        TO    WS-EDITED-DD.                        
                                                                                
                                                                                
      *******************                                                       
       20-MAIN-PROCESS.                                                         
      *******************                                                       
                                                                                
           PERFORM 100-READ-BP13F410 THRU  100-EXIT.                            
           PERFORM 200-UPDATE-SOC    THRU  200-EXIT                             
                       UNTIL  EOF-OF-F410.                                      
                                                                                
      *******************                                                       
       30-FINALIZATION.                                                         
      *******************                                                       
                                                                                
           CLOSE   BP13F410                                                     
                   BP13K800                                                     
                   BP13K480.                                                    
                                                                                
           IF WS-K800-FILE-STATUS NOT = 00                                      
              DISPLAY 'BP13K800 - CLOSE ERROR ' WS-K800-FILE-STATUS             
              MOVE WS-K800-FILE-STATUS  TO RETURN-CODE.                         
                                                                                
           IF WS-K480-FILE-STATUS NOT = 00                                      
              DISPLAY 'BP13K480 - CLOSE ERROR ' WS-K480-FILE-STATUS             
              MOVE WS-K480-FILE-STATUS  TO RETURN-CODE.                         
                                                                                
           STOP RUN.                                                            
                                                                                
      *********************                                                     
       100-READ-BP13F410.                                                       
      *********************                                                     
                                                                                
           READ BP13F410 AT END                                                 
                         MOVE 'YES'  TO WS-EOF-F410                             
                         GO TO 100-EXIT.                                        
           ADD 1 TO WS-F410-INPUT.                                              
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************                                                        
       200-UPDATE-SOC.                                                          
      ******************                                                        
      *=================================================================        
      * 200-UPDATE-SOC.               |                                *        
      *----------------------------------------------------------------*        
      *   2000-CHECK-SOC.             | CHECK AMT-BK-PAID              *        
      *   2110-UPD-AMOUNT-SOC-INFO.   | MOVE UPDATED AMOUNT-SOC-INFO   *        
      *                               |      TO BP13K800 RECORD        *        
      *   2400-WRITE-TO-FILE.         | REWRITE BP13K800 RECORD        *        
      *================================================================*        
                                                                                
           MOVE F410-NUM-REGN TO K800-NUM-REGN.                                 
                                                                                
           READ BP13K800.                                                       
                                                                                
           IF WS-K800-FILE-STATUS  = 00                                         
              ADD  1                 TO   WS-HIST                               
              PERFORM 2000-CHECK-SOC THRU 2000-EXIT                             
              PERFORM 2400-WRITE-TO-FILE THRU 2400-EXIT                         
           END-IF.                                                              
                                                                                
           IF K800-NUM-ALLO-SCHEME = 'MGL'                                      
              PERFORM 400-UPDATE-K480 THRU 400-EXIT                             
           END-IF.                                                              
                                                                                
           PERFORM 100-READ-BP13F410 THRU 100-EXIT.                             
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************                                                        
       400-UPDATE-K480.                                                         
      ******************                                                        
                                                                                
           MOVE F410-NUM-REGN TO K480-NUM-REGN.                                 
                                                                                
           READ BP13K480.                                                       
                                                                                
           IF WS-K480-FILE-STATUS  = 00                                         
              MOVE F410-AMT-LEASE-STAMP TO K480-AMT-LRA-CONTRA-SF-REQ           
              MOVE F410-AMT-CONVEY TO K480-AMT-INT-BILLING-CF-REQ               
              PERFORM 450-REWRITE-K480 THRU 450-EXIT                            
           ELSE                                                                 
              IF WS-K480-FILE-STATUS  = 23                                      
                 PERFORM 460-CREATE-BP13K480 THRU 460-EXIT                      
              ELSE                                                              
                 DISPLAY 'ERROR READING BP13K480 = ' F410-NUM-REGN              
                 DISPLAY 'FILE STATUS = ' WS-K480-FILE-STATUS                   
              END-IF                                                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       450-REWRITE-K480.                                                        
      *-------------------*                                                     
                                                                                
           REWRITE BP13K480-REC.                                                
                                                                                
           IF WS-K480-FILE-STATUS = ZEROES                                      
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING BP13K480, STATUS : '                     
                       WS-K480-FILE-STATUS                                      
              PERFORM 30-FINALIZATION.                                          
                                                                                
       450-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       460-CREATE-BP13K480.                                                     
      *-------------------*                                                     
                                                                                
           MOVE SPACES TO BP13K480-REC                                          
           INITIALIZE BP13K480-REC                                              
           MOVE  F410-NUM-REGN     TO  K480-NUM-REGN                            
           MOVE  F410-NUM-SCH-ACC  TO  K480-SCH-ACC                             
           MOVE  F410-NRIC1        TO  K480-NUM-NRIC1                           
           MOVE  F410-NRIC2        TO  K480-NUM-NRIC2                           
           MOVE  F410-NRIC3        TO  K480-NUM-NRIC3                           
           MOVE  F410-NRIC4        TO  K480-NUM-NRIC4                           
           MOVE  F410-CPF1         TO  K480-NUM-CPF1                            
           MOVE  F410-CPF2         TO  K480-NUM-CPF2                            
           MOVE  F410-CPF3         TO  K480-NUM-CPF3                            
           MOVE  F410-CPF4         TO  K480-NUM-CPF4                            
           MOVE  WS-CURRENT-DATE   TO  K480-DTE-UPDATE                          
           MOVE  'BP13C601'        TO  K480-USERID                              
      *    MOVE F410-AMT-LEASE-STAMP TO K480-AMT-LRA-CONTRA-SF-REQ              
           MOVE F410-AMT-SER-CONTRA-STAMP-FEES TO                               
                K480-AMT-LRA-CONTRA-SF-REQ                                      
      *    MOVE F410-AMT-CONVEY    TO K480-AMT-INT-BILLING-CF-REQ.              
           MOVE F410-AMT-SER-CONTRA-CONVY-FEES TO                               
                K480-AMT-INT-BILLING-CF-REQ.                                    
           WRITE BP13K480-REC.                                                  
                                                                                
       460-EXIT.                                                                
           EXIT.                                                                
                                                                                
      ******************                                                        
       2000-CHECK-SOC.                                                          
      ******************                                                        
                                                                                
           IF K800-AMT-BK-PAID NOT NUMERIC                                      
              MOVE ZEROES      TO  K800-AMT-BK-PAID.                            
                                                                                
           IF K800-AMT-BK-PAID = 0                                              
              CONTINUE                                                          
           ELSE                                                                 
              DISPLAY ' REGN '  F410-NUM-REGN                                   
              PERFORM 2110-UPD-AMOUNT-SOC-INFO THRU 2110-EXIT.                  
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ***************************                                               
       2110-UPD-AMOUNT-SOC-INFO.                                                
      ***************************                                               
                                                                                
           IF (K800-NUM-HALF-IDP = 'D'  OR 'F')                                 
              CONTINUE                                                          
           ELSE                                                                 
           IF F410-DTE-ARGORDER NOT NUMERIC                                     
              MOVE ZEROES                TO   K800-DTE-SALE-AGMT                
              MOVE ZEROES                TO   K800-DTE-RAISE-AO                 
           ELSE                                                                 
             MOVE F410-DTE-ARGORDER      TO   K800-DTE-SALE-AGMT                
             MOVE F410-DTE-ARGORDER      TO   K800-DTE-RAISE-AO.                
                                                                                
      *-------------------------------------------------*                       
      *  SINCE APPLICANT ALREADY COLLECTED KEYS         *                       
      *  DELIVERY POSSESSION DATE SHOULD BE SO-DATE.    *                       
      *-------------------------------------------------*                       
           IF F410-DTE-DELIVERY-POSSN  NOT NUMERIC                              
              MOVE ZEROES                  TO K800-DTE-DELIVERY-POSSN           
           ELSE                                                                 
              MOVE F410-DTE-DELIVERY-POSSN TO K800-DTE-DELIVERY-POSSN.          
                                                                                
           IF F410-CDE-ASPF = 'Y'                                               
              MOVE F410-CDE-ASPF            TO   K800-NUM-ASPF.                 
                                                                                
           IF (K800-NUM-HALF-IDP = 'D'  OR 'F')                                 
              CONTINUE                                                          
           ELSE                                                                 
           IF F410-AMT-LEASE-STAMP NOT NUMERIC                                  
              MOVE ZEROES                   TO  K800-AMT-LEASE-FEE              
           ELSE                                                                 
              MOVE F410-AMT-LEASE-STAMP     TO  K800-AMT-LEASE-FEE.             
                                                                                
           IF F410-AMT-CONVEY      NOT NUMERIC                                  
              MOVE ZEROES                   TO  F410-AMT-CONVEY                 
           ELSE                                                                 
              MOVE F410-AMT-CONVEY          TO  K800-AMT-CONVEY-FEE             
              MOVE F410-AMT-CONVEY-GST      TO  K800-AMT-CONVEY-FEE-GST.        
                                                                                
              DISPLAY ' REGN '   F410-NUM-REGN.                                 
              DISPLAY 'BK FEE TR '    F410-AMT-BK-TRANSFER.                     
                                                                                
           IF F410-AMT-BK-TRANSFER NOT NUMERIC                                  
              MOVE ZEROES                   TO  F410-AMT-BK-TRANSFER            
              MOVE ZEROES                   TO  K800-AMT-BK-TR                  
           ELSE                                                                 
              MOVE F410-AMT-BK-TRANSFER     TO   K800-AMT-BK-TR.                
                                                                                
           IF F410-AMT-RDE-TRANSFER NOT NUMERIC                                 
              MOVE ZEROES                   TO  F410-AMT-RDE-TRANSFER.          
                                                                                
      *-------------------------------------------------*                       
      *  ZEROLISE BOOKING FEE FOR BOOKING FEE USED AS   *                       
      *  BOOKING FEE IS TRANSFER TO CD TO BE USED AS ICP*                       
      *-------------------------------------------------*                       
           IF F410-AMT-BK-TRANSFER = ZEROES                                     
              NEXT SENTENCE                                                     
           ELSE                                                                 
           IF F410-AMT-BK-TRANSFER > K800-AMT-BK-PAID                           
              CONTINUE                                                          
           ELSE                                                                 
              ADD  F410-AMT-BK-TRANSFER TO   K800-AMT-SOC-CASH                  
              COMPUTE WS-BK =  K800-AMT-BK-PAID  - F410-AMT-BK-TRANSFER         
              MOVE WS-BK                TO   K800-AMT-BK-PAID                   
              MOVE ZEROES               TO   WS-BK.                             
                                                                                
      *--------------------------------------------------*                      
      *  ZEROLISE REGN DEPOSIT FOR REGN DEPOSIT USED AS  *                      
      *  REGN DEPOSIT IS TRANSFER TO CD TO BE USED AS ICP*                      
      *------------------------------------------------- *                      
           IF K800-AMT-REGN-DEPOSIT-CASH NOT NUMERIC                            
              MOVE ZEROS TO K800-AMT-REGN-DEPOSIT-CASH.                         
                                                                                
           IF K800-AMT-REGN-DEPOSIT-CPF1 NOT NUMERIC                            
              MOVE ZEROS TO K800-AMT-REGN-DEPOSIT-CPF1.                         
                                                                                
           IF K800-AMT-REGN-DEPOSIT-CPF2 NOT NUMERIC                            
              MOVE ZEROS TO K800-AMT-REGN-DEPOSIT-CPF2.                         
                                                                                
           IF K800-AMT-REGN-DEPOSIT-CPF3 NOT NUMERIC                            
              MOVE ZEROS TO K800-AMT-REGN-DEPOSIT-CPF3.                         
                                                                                
           IF K800-AMT-REGN-DEPOSIT-CPF4 NOT NUMERIC                            
              MOVE ZEROS TO K800-AMT-REGN-DEPOSIT-CPF4.                         
                                                                                
                                                                                
           MOVE ZEROES          TO   K800-AMT-CD-CPF1                           
                                     K800-AMT-CD-CPF2                           
                                     K800-AMT-CD-CPF3                           
                                     K800-AMT-CD-CPF4.                          
                                                                                
           IF F410-AMT-RDE-TRANSFER = ZEROES                                    
              NEXT SENTENCE                                                     
           ELSE                                                                 
           IF F410-AMT-RDE-TRANSFER > (K800-AMT-REGN-DEPOSIT-CASH +             
                                       K800-AMT-REGN-DEPOSIT-CPF1 +             
                                       K800-AMT-REGN-DEPOSIT-CPF2 +             
                                       K800-AMT-REGN-DEPOSIT-CPF3 +             
                                       K800-AMT-REGN-DEPOSIT-CPF4)              
              CONTINUE                                                          
           ELSE                                                                 
           IF F410-AMT-RDE-TRANSFER NOT = 0 AND 5000                            
              CONTINUE                                                          
           ELSE                                                                 
              ADD  K800-AMT-REGN-DEPOSIT-CASH TO K800-AMT-SOC-CASH              
                                                 WS-CD-CASH-RDE                 
              INITIALIZE    K800-AMT-REGN-DEPOSIT-CASH                          
              ADD  K800-AMT-REGN-DEPOSIT-CPF1 TO K800-AMT-CPF1-PAID             
                                                 K800-AMT-CD-CPF1               
              INITIALIZE    K800-AMT-REGN-DEPOSIT-CPF1                          
              ADD  K800-AMT-REGN-DEPOSIT-CPF2 TO K800-AMT-CPF2-PAID             
                                                 K800-AMT-CD-CPF2               
              INITIALIZE    K800-AMT-REGN-DEPOSIT-CPF2                          
              ADD  K800-AMT-REGN-DEPOSIT-CPF3 TO K800-AMT-CPF3-PAID             
                                                 K800-AMT-CD-CPF3               
              INITIALIZE    K800-AMT-REGN-DEPOSIT-CPF3                          
              ADD  K800-AMT-REGN-DEPOSIT-CPF4 TO K800-AMT-CPF4-PAID             
                                                 K800-AMT-CD-CPF4               
              INITIALIZE    K800-AMT-REGN-DEPOSIT-CPF4.                         
                                                                                
           COMPUTE WS-CD-CASH = F410-AMT-BK-TRANSFER +                          
                                WS-CD-CASH-RDE.                                 
                                                                                
                                                                                
           MOVE WS-CD-CASH      TO   K800-AMT-CD-CASH.                          
                                                                                
           COMPUTE WS-CD = K800-AMT-CD-CASH +                                   
                           K800-AMT-CD-CPF1 +                                   
                           K800-AMT-CD-CPF2 +                                   
                           K800-AMT-CD-CPF3 +                                   
                           K800-AMT-CD-CPF4.                                    
                                                                                
           MOVE WS-CD           TO   K800-AMT-CD.                               
                                                                                
      *------------------------------*                                          
      *  INITIALIZE WORK AREA        *                                          
      *------------------------------*                                          
           MOVE ZEROES          TO   WS-CD-CASH.                                
           MOVE ZEROES          TO   WS-CD-CASH-RDE.                            
           MOVE ZEROES          TO   WS-CD-CPF1.                                
           MOVE ZEROES          TO   WS-CD-CPF2.                                
                                                                                
       2110-EXIT.                                                               
            EXIT.                                                               
                                                                                
      **********************                                                    
       2400-WRITE-TO-FILE.                                                      
      **********************                                                    
                                                                                
           MOVE WS-CURRENT-DATE TO K800-DTE-UPDATE.                             
           REWRITE BP13K800-MASTER.                                             
           ADD  1  TO  WS-K800-OUTPUT.                                          
                                                                                
       2400-EXIT.                                                               
            EXIT.                                                               
