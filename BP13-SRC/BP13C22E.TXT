       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C22E.                                                 
      *AUTHOR.        LMS.                                                      
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE  :                                              *            
      *                                                            *            
      *    1. TO PASS ACCEPTED/REJECTED CASES TO HOMMAS.           *            
      *                                                            *            
      *  INPUT FILE :                                              *            
      *      1. BP13F310 (TRANS FILE)                              *            
      *                                                            *            
      *  OUTPUT FILE :                                             *            
      *      1. BP13F050 (BP13.F050.HOMMAS.ACCREJ)                 *            
      *                                                            *            
      * ---------------------------------------------------------- *            
      * CHG REF  BY  DATE      DESCRIPTION                         *            
      * -------- --- --------  -----------                         *            
      * BP132794 LMS 17/02/06  NEW PROGRAM.                        *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K110-SCH-ACC-NO                            
                  FILE STATUS     IS BM06K110-STATUS.                           
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ACCESS MODE     IS RANDOM                                     
                  ORGANIZATION    IS INDEXED                                    
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS BP13K800-STATUS.                           
                                                                                
           SELECT BP13F310 ASSIGN TO BP13F310.                                  
                                                                                
           SELECT BP13F050 ASSIGN TO BP13F050.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
       FD  BP13F050                                                             
           RECORDING MODE F                                                     
           BLOCK  CONTAINS 0 CHARACTERS                                         
           RECORD CONTAINS 80 CHARACTERS                                        
           LABEL RECORDS IS STANDARD.                                           
       COPY BP13F050.                                                           
                                                                                
       FD  BP13F310                                                             
           RECORDING MODE F                                                     
           BLOCK  CONTAINS 0 CHARACTERS                                         
           RECORD CONTAINS 150 CHARACTERS                                       
           LABEL RECORDS IS STANDARD.                                           
       COPY BP13F310.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FILE-STATUS-VARS.                                                 
           05  BM06K110-STATUS     PIC 9(02)  VALUE ZEROES.                     
           05  BP13K800-STATUS     PIC 9(02)  VALUE ZEROES.                     
                                                                                
       01  WS-EOF-F310             PIC X      VALUE 'N'.                        
       01  WS-FND-K110             PIC X      VALUE 'N'.                        
       01  WS-FND-K800             PIC X      VALUE 'N'.                        
       01  WS-READ-F310            PIC 9(4)   VALUE ZEROES.                     
       01  WS-READ-K110            PIC 9(4)   VALUE ZEROES.                     
       01  WS-READ-K800            PIC 9(4)   VALUE ZEROES.                     
       01  WS-DATE                 PIC X(8)   VALUE SPACES.                     
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN.                                                               
      *-------------------------------------------------------------            
           PERFORM 100-OPENING-ROUTINE       THRU 100-EXIT.                     
           PERFORM 150-READ-DATA             THRU 150-EXIT                      
             UNTIL WS-EOF-F310 = 'Y'.                                           
           PERFORM 1100-CLOSING-ROUTINE      THRU 1100-EXIT.                    
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       100-OPENING-ROUTINE.                                                     
      *-------------------------------------------------------------            
           OPEN INPUT  BP13F310                                                 
                       BM06K110                                                 
                       BP13K800                                                 
                OUTPUT BP13F050.                                                
                                                                                
           IF BM06K110-STATUS  NOT =  ZEROES AND 97                             
              DISPLAY 'BM06K110 OPEN ERROR, STATUS ' BM06K110-STATUS            
              MOVE BM06K110-STATUS   TO RETURN-CODE                             
              PERFORM 1100-CLOSING-ROUTINE THRU 1100-EXIT                       
           END-IF.                                                              
                                                                                
           IF BP13K800-STATUS  NOT =  ZEROES AND 97                             
              DISPLAY 'BP13K800 OPEN ERROR, STATUS ' BP13K800-STATUS            
              MOVE BP13K800-STATUS   TO RETURN-CODE                             
              PERFORM 1100-CLOSING-ROUTINE THRU 1100-EXIT                       
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-DATE.                               
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       150-READ-DATA.                                                           
      *-------------------------------------------------------------            
           READ BP13F310 AT END                                                 
                MOVE 'Y' TO WS-EOF-F310                                         
                GO TO 150-EXIT.                                                 
                                                                                
           ADD 1 TO WS-READ-F310.                                               
           PERFORM 200-READ-BM06K110 THRU 200-EXIT.                             
           PERFORM 250-READ-BP13K800 THRU 250-EXIT.                             
                                                                                
           IF (WS-FND-K110 = 'Y') AND (WS-FND-K800 = 'Y')                       
              MOVE SPACES TO BP13F050-REC                                       
              MOVE F310-SCH-ACC      TO F050-NUM-SCH-ACC                        
              MOVE K110-DTE-HANDOVER TO F050-DTE-HANDOVER                       
                                                                                
              IF F310-CDE-TRANS = 'AI' OR 'AS'                                  
                 MOVE 'A'          TO F050-NUM-STATUS                           
              ELSE                                                              
                 IF F310-CDE-TRANS = 'RI' OR 'RS' OR                            
                                     'RK' OR 'RL' OR 'RM' OR                    
                                     'R1' OR 'R2' OR 'R3' OR                    
                                     'RX' OR 'RY' OR 'RZ' OR                    
                                     'RG' OR 'R4' OR 'R5'                       
                    MOVE 'R'          TO F050-NUM-STATUS                        
                 END-IF                                                         
              END-IF                                                            
                                                                                
              MOVE K800-NUM-ALLO-CAT      TO F050-NUM-SELTY                     
              MOVE F310-DTE-UPDATE        TO F050-DTE-UPDATE                    
              MOVE F310-TME-ACCEPT-REJECT TO F050-TME-UPDATE                    
              MOVE WS-DATE                TO F050-DTE-RUN                       
                                                                                
              WRITE BP13F050-REC                                                
           END-IF.                                                              
                                                                                
       150-EXIT.                                                                
           EXIT.                                                                
                                                                                
                                                                                
       200-READ-BM06K110.                                                       
      ******************************************************************        
            MOVE SPACES        TO K110-SCH-ACC-NO.                              
            MOVE 'N'           TO WS-FND-K110.                                  
            MOVE F310-SCH-ACC  TO K110-SCH-ACC-NO.                              
            READ BM06K110                                                       
            EVALUATE BM06K110-STATUS                                            
               WHEN 00                                                          
                    MOVE 'Y' TO WS-FND-K110                                     
                    ADD 1 TO WS-READ-K110                                       
               WHEN 23                                                          
                    MOVE 'N' TO WS-FND-K110                                     
               WHEN OTHER                                                       
                    DISPLAY 'BM06K110 READ ERROR : ' BM06K110-STATUS            
                    DISPLAY 'BM06K110 KEY-FLD    : ' K110-SCH-ACC-NO            
                    MOVE BM06K110-STATUS         TO   RETURN-CODE               
                    PERFORM 1100-CLOSING-ROUTINE THRU 1100-EXIT                 
            END-EVALUATE.                                                       
                                                                                
       200-EXIT.                                                                
            EXIT.                                                               
                                                                                
       250-READ-BP13K800.                                                       
      ******************************************************************        
            MOVE SPACES        TO K800-NUM-REGN.                                
            MOVE 'N'           TO WS-FND-K800.                                  
            MOVE F310-REGN-NO  TO K800-NUM-REGN.                                
            READ BP13K800                                                       
            EVALUATE BP13K800-STATUS                                            
               WHEN 00                                                          
                    MOVE 'Y' TO WS-FND-K800                                     
                    ADD 1 TO WS-READ-K800                                       
               WHEN 23                                                          
                    MOVE 'N' TO WS-FND-K800                                     
               WHEN OTHER                                                       
                    DISPLAY 'BP13K800 READ ERROR : ' BP13K800-STATUS            
                    DISPLAY 'BP13K800 KEY-FLD    : ' K800-NUM-REGN              
                    MOVE BP13K800-STATUS         TO   RETURN-CODE               
                    PERFORM 1100-CLOSING-ROUTINE THRU 1100-EXIT                 
            END-EVALUATE.                                                       
                                                                                
       250-EXIT.                                                                
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1100-CLOSING-ROUTINE.                                                    
      *-------------------------------------------------------------            
           DISPLAY ' '.                                                         
           DISPLAY '-----------------------------------'.                       
           DISPLAY ' CONTROL LIST : BP13C22E '.                                 
           DISPLAY '-----------------------------------'.                       
           DISPLAY ' 1) TOT BP13F310 READ      : ' WS-READ-F310.                
           DISPLAY ' 2) TOT BM06K110 READ      : ' WS-READ-K110.                
           DISPLAY ' 3) TOT BP13K800 READ      : ' WS-READ-K800.                
                                                                                
           CLOSE BM06K110                                                       
                 BP13F310                                                       
                 BP13K800                                                       
                 BP13F050.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
