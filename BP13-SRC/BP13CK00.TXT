       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CK00.                                                 
       AUTHOR.        YCH2.                                                     
       DATE-WRITTEN.  03/03/2006.                                               
      * ====================================================== *                
      * SYSTEM OF COMMITMENT (BP13)                            *                
      * ====================================================== *                
      *                                                        *                
      *    OBJECTIVE   : CHECK PARENT/CHILD RELATIONSHIP IN    *                
      *                  REQUEST FILE                          *                
      *                                                        *                
      *    INPUT FILE  : BP13F820                              *                
      *                                                        *                
      *--------------------------------------------------------*                
      * CHG-NO  BY   DATE     DETAILS                          *                
      * ------  ---  ----     -------                          *                
      *BP132833 YCH2 03032006 NEW PROGRAM                      *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-4341.                                               
       OBJECT-COMPUTER. IBM-4341.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F820   ASSIGN TO BP13F820.                                
                                                                                
           SELECT BP13K800   ASSIGN       TO BP13K800                           
                             ACCESS       IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K800-NUM-REGN                      
                             FILE STATUS  IS WS-K800-STATUS.                    
                                                                                
           SELECT BM06K110   ASSIGN       TO BM06K110                           
                             ACCESS       IS RANDOM                             
                             ORGANIZATION IS INDEXED                            
                             RECORD KEY   IS K110-KEY-FLD                       
                             FILE STATUS  IS WS-K110-STATUS.                    
                                                                                
           SELECT P13F820A ASSIGN TO P13F820A.                                  
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD  BP13F820                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 400 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       COPY BP13F820.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  P13F820A                                                             
           BLOCK CONTAINS 0 RECORDS                                             
           RECORD CONTAINS 400 CHARACTERS                                       
           LABEL RECORDS ARE STANDARD                                           
           RECORDING MODE IS F.                                                 
       01  P13F820A-REC                   PIC X(400).                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-COUNTERS.                                                         
           05 WS-F820-READ                PIC 9(8)    VALUE ZEROS.              
           05 WS-K800-READ                PIC 9(8)    VALUE ZEROS.              
           05 WS-K800-STATUS              PIC 9(2)    VALUE ZEROS.              
           05 WS-K110-STATUS              PIC 9(2)    VALUE ZEROS.              
           05 WS-OUTPUT                   PIC 9(8)    VALUE ZEROS.              
           05 WS-OUTPUT-BYPASS            PIC 9(8)    VALUE ZEROS.              
           05 WS-OUTPUT-PARENT            PIC 9(8)    VALUE ZEROS.              
           05 WS-INPUT-REGN               PIC 9(8)    VALUE ZEROS.              
           05 WS-OUTPUT-REGN              PIC 9(8)    VALUE ZEROS.              
           05 WS-SINGLE-CHILD             PIC 9(2)    VALUE ZEROS.              
           05 WS-CHILD                    PIC 9(2)    VALUE ZEROS.              
           05 WS-BYPASS-REGN              PIC 9(8)    VALUE ZEROS.              
           05 WS-REGN-BYPASS              PIC 9(8)    VALUE ZEROES.             
           05 WS-PROCESS-REGN             PIC 9(8)    VALUE ZEROES.             
                                                                                
       01  WS-PREV-REGN                   PIC X(8)    VALUE SPACES.             
       01  WS-AGE21                       PIC X(1)    VALUE 'N'.                
       01  WS-HA-CHILD                    PIC X(1)    VALUE 'N'.                
       01  WS-HA-PARENT                   PIC X(1)    VALUE 'N'.                
       01  WS-K800-FND                    PIC X(1)    VALUE 'N'.                
       01  WS-APPL-MS                     PIC X(1)    VALUE SPACES.             
                                                                                
       01  WS-FILE-POINTER.                                                     
           05 WS-F820-SUB                 PIC 9(2)    VALUE 0.                  
           05 WS-HA-STORED                PIC 9(2)    VALUE 0.                  
                                                                                
       01  WS-CURR-DATE                   PIC X(08) VALUE ZEROES.               
       01  WS-NUM-CURR-DATE  REDEFINES WS-CURR-DATE.                            
           05 WS-DATE                     PIC 9(08).                            
       01  WS-BIRTH-DATE                  PIC 9(08) VALUE ZEROES.               
                                                                                
       01  WS-AGE                  PIC 9(08) VALUE ZEROES.                      
       01  WS-NUM-AGE  REDEFINES WS-AGE.                                        
           05 WS-AGE-CMPLTD        PIC 9(04).                                   
           05 WS-NUM-REMNG         PIC 9(04).                                   
                                                                                
       01  WS-EOF-F820                    PIC X.                                
           88 EOF-F820                    VALUE 'Y'.                            
                                                                                
       01  WS-F820-RECORD.                                                      
           03 WS-F820-REC OCCURS 20 TIMES           PIC X(400).                 
                                                                                
      ******************************************************************        
       PROCEDURE DIVISION.                                                      
      ******************************************************************        
       000-MAIN-ROUTINE.                                                        
           PERFORM 100-INITIALIZATION THRU 100-EXIT.                            
           PERFORM 200-READ-F820      THRU 200-EXIT.                            
           PERFORM 400-PROCESS-RTN    THRU 400-EXIT                             
             UNTIL EOF-F820.                                                    
           PERFORM 999-CLOSE-FILES    THRU 999-EXIT.                            
                                                                                
       000-EXIT.                                                                
           EXIT.                                                                
                                                                                
       100-INITIALIZATION.                                                      
                                                                                
           OPEN INPUT  BP13F820                                                 
                       BP13K800                                                 
                       BM06K110                                                 
                OUTPUT P13F820A.                                                
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'K800 OPEN ERROR '                                        
              DISPLAY 'FILE STATUS IS :' WS-K800-STATUS                         
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'K110 OPEN ERROR '                                        
              DISPLAY 'FILE STATUS IS :' WS-K110-STATUS                         
              PERFORM 999-CLOSE-FILES THRU 999-EXIT                             
           END-IF.                                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)    TO WS-CURR-DATE.                  
                                                                                
       100-EXIT.                                                                
           EXIT.                                                                
                                                                                
       200-READ-F820.                                                           
                                                                                
           READ BP13F820 AT END                                                 
                MOVE HIGH-VALUE TO F820-KEY-FLD                                 
                MOVE 'Y'        TO WS-EOF-F820                                  
                GO TO 200-EXIT.                                                 
                                                                                
           ADD  1 TO WS-F820-READ.                                              
                                                                                
       200-EXIT.                                                                
           EXIT.                                                                
                                                                                
       300-INITIALIZE-TABLE.                                                    
                                                                                
           MOVE SPACES TO WS-F820-REC (WS-F820-SUB).                            
                                                                                
       300-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *                                                                         
       400-PROCESS-RTN.                                                         
                                                                                
           PERFORM 300-INITIALIZE-TABLE THRU 300-EXIT                           
           VARYING WS-F820-SUB FROM 1 BY 1                                      
             UNTIL WS-F820-SUB > 20.                                            
                                                                                
           MOVE  SPACES       TO  WS-F820-RECORD.                               
           MOVE  SPACES       TO  WS-APPL-MS.                                   
           MOVE  'N'          TO  WS-HA-PARENT.                                 
           MOVE  'N'          TO  WS-HA-CHILD.                                  
           MOVE  'N'          TO  WS-AGE21.                                     
           MOVE  'N'          TO  WS-K800-FND.                                  
           MOVE  1            TO  WS-F820-SUB.                                  
           MOVE  ZEROES       TO  WS-SINGLE-CHILD.                              
           MOVE  ZEROES       TO  WS-CHILD.                                     
           MOVE  ZEROES       TO  WS-HA-STORED.                                 
           MOVE  F820-NUM-REGN TO WS-PREV-REGN.                                 
           ADD   1            TO  WS-INPUT-REGN.                                
                                                                                
           PERFORM 600-READ-K800 THRU 600-EXIT                                  
           IF WS-K800-FND = 'N'                                                 
              ADD 1            TO WS-BYPASS-REGN                                
              PERFORM 410-READ-BP13F820 THRU 410-EXIT                           
               UNTIL F820-NUM-REGN NOT = WS-PREV-REGN                           
                 OR  EOF-F820                                                   
           ELSE                                                                 
              ADD 1            TO WS-PROCESS-REGN                               
              PERFORM 500-STORE-F820 THRU 500-EXIT                              
                UNTIL F820-NUM-REGN NOT = WS-PREV-REGN                          
                OR    EOF-F820                                                  
                                                                                
      * --------------------------------------------------------                
      *  1)  IF APPLICANT IS A CHILD MUST BE SINGLE                             
      *         AND PARENT MUST TAG AS ESSENTIAL FAMILY                         
      *  2)  IF APPLICANT IS PARENT CHILD MUST BE >= 20 AS AT                   
      *         PROCESS DATE AND NUMBER OF SINGLE CHILD MUST TALLY              
      *         TO NUMBER OF CHILD IN THE HOUSEHOLD                             
      * --------------------------------------------------------                
                                                                                
              IF (WS-HA-CHILD = 'Y' AND WS-APPL-MS = '1') OR                    
                 (WS-HA-PARENT = 'Y' AND                                        
                  WS-SINGLE-CHILD = WS-CHILD AND                                
                  WS-AGE21 = 'Y')                                               
                 ADD 1            TO WS-OUTPUT-REGN                             
                 PERFORM 800-WRITE-OUTPUT THRU 800-EXIT                         
                  VARYING WS-F820-SUB FROM 1 BY 1                               
                    UNTIL WS-F820-SUB > WS-HA-STORED                            
              ELSE                                                              
                 ADD 1             TO WS-REGN-BYPASS                            
                 MOVE WS-HA-STORED TO WS-OUTPUT-PARENT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       400-EXIT.                                                                
           EXIT.                                                                
                                                                                
       410-READ-BP13F820.                                                       
           ADD 1 TO WS-OUTPUT-BYPASS.                                           
           PERFORM 200-READ-F820      THRU 200-EXIT.                            
                                                                                
       410-EXIT.                                                                
           EXIT.                                                                
                                                                                
      * NUM-RELATIONSHIP ='12' MOTHER                                           
      * NUM-RELATIONSHIP ='02' FATHER                                           
      * NUM-RELATIONSHIP ='41' CHILD                                            
      * NUM-RELATIONSHIP ='16' PARENT - NA                                      
      * NUM-RELATIONSHIP ='38' ADOPTIVE PARENTS                                 
      * NUM-RELATIONSHIP ='39' ADOPTIVE CHILD                                   
      * F820-NUM-MARITAL-STATUS = '1'  - SINGLE                                 
                                                                                
       500-STORE-F820.                                                          
           IF F820-NUM-RELATIONSHIP = '00'                                      
              MOVE F820-NUM-MARITAL-STATUS TO WS-APPL-MS                        
           END-IF.                                                              
                                                                                
           IF F820-NUM-RELATIONSHIP = '02' OR '12' OR '19' OR '16' OR           
                                      '38' OR '39'                              
              IF  F820-NUM-LESSEE-CHECK = 'Y'                                   
                  MOVE 'Y' TO WS-HA-CHILD                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
           IF F820-NUM-RELATIONSHIP = '41'                                      
              ADD 1 TO WS-CHILD                                                 
              IF F820-NUM-MARITAL-STATUS = '1'                                  
                 MOVE 'Y' TO WS-HA-PARENT                                       
                 ADD 1 TO WS-SINGLE-CHILD                                       
                 PERFORM 510-CHECK-AGE THRU 510-EXIT                            
              END-IF                                                            
           END-IF.                                                              
                                                                                
           MOVE BP13F820-REC TO WS-F820-REC (WS-F820-SUB).                      
           ADD 1 TO WS-F820-SUB.                                                
           ADD 1 TO WS-HA-STORED.                                               
           PERFORM 200-READ-F820 THRU 200-EXIT.                                 
                                                                                
       500-EXIT.                                                                
           EXIT.                                                                
                                                                                
       510-CHECK-AGE.                                                           
           IF F820-DTE-BIRTH IS NUMERIC                                         
              MOVE F820-DTE-BIRTH        TO WS-BIRTH-DATE                       
              COMPUTE WS-AGE  =  WS-DATE  -  WS-BIRTH-DATE                      
              IF  WS-AGE-CMPLTD >= 20                                           
                  MOVE 'Y' TO WS-AGE21                                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       510-EXIT.                                                                
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       600-READ-K800.                                                           
      *-----------------*                                                       
                                                                                
           MOVE  F820-NUM-REGN TO K800-NUM-REGN.                                
                                                                                
           READ  BP13K800.                                                      
                                                                                
           EVALUATE WS-K800-STATUS                                              
           WHEN 00                                                              
                ADD 1 TO WS-K800-READ                                           
                IF (K800-NUM-STATUS = 'C') OR                                   
                   (K800-NUM-FLAT-TYPE = '1A' OR '2A')                          
                   MOVE 'N' TO WS-K800-FND                                      
                ELSE                                                            
                   MOVE 'Y' TO WS-K800-FND                                      
                   IF K800-NUM-SCH-ACC NOT = SPACES AND LOW-VALUES              
                      PERFORM 700-READ-BM06K110 THRU 700-EXIT                   
                   END-IF                                                       
                END-IF                                                          
           WHEN 23                                                              
                MOVE 'N' TO WS-K800-FND                                         
           WHEN OTHER                                                           
                DISPLAY 'K800-FILE STATUS IS  : ' WS-K800-STATUS                
                        ' K800-REGN  : ' K800-NUM-REGN                          
                PERFORM 999-CLOSE-FILES THRU 999-EXIT                           
           END-EVALUATE.                                                        
                                                                                
       600-EXIT.                                                                
           EXIT.                                                                
                                                                                
       700-READ-BM06K110.                                                       
           MOVE K800-NUM-SCH-ACC TO K110-KEY-FLD.                               
           READ BM06K110.                                                       
           EVALUATE WS-K110-STATUS                                              
              WHEN  00                                                          
                    IF K110-REGN-NO = K800-NUM-REGN AND                         
                       K110-DTE-KEY-ISSUED > '00000000'                         
                       MOVE 'N' TO WS-K800-FND                                  
                    END-IF                                                      
              WHEN  23                                                          
                    CONTINUE                                                    
              WHEN  OTHER                                                       
                    DISPLAY 'ERROR READ BM06K110 ' WS-K110-STATUS               
                    MOVE WS-K110-STATUS TO RETURN-CODE                          
                    PERFORM 999-CLOSE-FILES THRU 999-EXIT                       
           END-EVALUATE.                                                        
                                                                                
       700-EXIT.                                                                
           EXIT.                                                                
                                                                                
       800-WRITE-OUTPUT.                                                        
           MOVE WS-F820-REC (WS-F820-SUB) TO P13F820A-REC.                      
           WRITE P13F820A-REC.                                                  
           ADD 1 TO WS-OUTPUT.                                                  
                                                                                
       800-EXIT.                                                                
           EXIT.                                                                
                                                                                
       999-CLOSE-FILES.                                                         
           CLOSE BP13F820                                                       
                 BM06K110                                                       
                 P13F820A                                                       
                 BP13K800.                                                      
                                                                                
           IF WS-K800-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'K800 CLOSE ERROR '                                       
              DISPLAY 'FILE STATUS IS : ' WS-K800-STATUS                        
           END-IF.                                                              
                                                                                
           IF WS-K110-STATUS NOT = ZEROES AND 97                                
              DISPLAY 'K110 CLOSE ERROR '                                       
              DISPLAY 'FILE STATUS IS : ' WS-K110-STATUS                        
           END-IF.                                                              
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*================ BP13CK00 ================*'.              
           DISPLAY '1.  NO. OF F820 RECORDS READ    = ' WS-F820-READ.           
           DISPLAY '2.  NO. OF F820 REGNO READ      = ' WS-INPUT-REGN           
           DISPLAY '3.  NO. OF F820 REGN PROCESS    = ' WS-PROCESS-REGN         
           DISPLAY ' '.                                                         
           DISPLAY '4.  NO. OF F820 REGNO OUTPUT    = ' WS-OUTPUT-REGN          
           DISPLAY '4.  NO. OF F820 REGN BYPASS     = ' WS-BYPASS-REGN.         
           DISPLAY '5.  NO. OF F820 REGN NOT IN SOC = ' WS-REGN-BYPASS.         
           DISPLAY ' '.                                                         
           DISPLAY '6.  NO. OF F820 REC BYPASS      = ' WS-OUTPUT-BYPASS        
           DISPLAY '7.  NO. OF F820 RECORDS OUTPUT  = ' WS-OUTPUT.              
           DISPLAY '8.  NO. OF F820 REC NOT P/S C   = '                         
                                              WS-OUTPUT-PARENT.                 
           DISPLAY ' '.                                                         
           DISPLAY '10. NO. OF F800 RECORD READ     = ' WS-K800-READ.           
           DISPLAY ' '.                                                         
           DISPLAY '*==========================================*'.              
                                                                                
           STOP RUN.                                                            
                                                                                
        999-EXIT.                                                               
            EXIT.                                                               
