       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CE17.                                                 
      *AUTHOR.        KATHRINA VALENZUELA.                                      
      *DATE-WRITTEN.  02/07/2019.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *   OBJECTIVE   :  GENERATE REPORT OF RESERVED FLATS WHERE   *            
      *                  DATE RESERVE IS: A.) GREATER THAN OR EQUAL*            
      *                  1 MONTH BUT LESS THAN OR EQUAL 2 MONTHS   *            
      *                  B.) EQUAL 2.5 MONTHS                      *            
      *                  C.) GREATER THAN OR EQUAL 3 MONTHS        *            
      *                                                            *            
      *   INPUT  FILES:  1.BM06F110 - FLAT INV FILE                *            
      *   OUTPUT FILES:  1.BP13FE17 - OUTPUT DETAILS FOR REPORT    *            
      *                                                            *            
      * CHG REF  DATE      OIC   REMARKS                           *            
      * -------- --------  ----  --------------------------------- *            
      * BP137719 02072019  KV6   NEW PROGRAM                       *            
      * BP137866 30072019  KV6   INITIALIZE K200 REC FOR EVERY READ*            
      * BP138067 08112019  KV6   PASS DTE-REPRICE FOR ALL ALERTTYPE*            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           SELECT BM06F110  ASSIGN       TO BM06F110.                           
                                                                                
           SELECT BM06K200  ASSIGN        TO BM06K200                           
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K200-KEY-FLD                       
                            ACCESS MODE   IS RANDOM                             
                            FILE STATUS   IS WS-K200-STATUS.                    
                                                                                
           SELECT BP13FE17  ASSIGN       TO BP13FE17.                           
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *-------------------------------------------------------------            
                                                                                
       FD   BM06F110           BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 500 CHARACTERS                   
                               LABEL RECORD IS STANDARD                         
                               RECORDING MODE  IS  F.                           
       COPY BM06F110.                                                           
                                                                                
       FD   BM06K200           RECORD CONTAINS 100 CHARACTERS.                  
       COPY BM06K200.                                                           
                                                                                
       FD   BP13FE17           BLOCK CONTAINS 0 RECORDS                         
                               RECORD CONTAINS 150 CHARACTERS                   
                               LABEL RECORD IS STANDARD                         
                               RECORDING MODE  IS  F.                           
       COPY BP13FE17.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
                                                                                
       01  WS-STATUS.                                                           
           05 WS-K200-STATUS           PIC 9(2)  VALUE ZEROES.                  
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F110-READ            PIC 9(4)  VALUE ZEROES.                  
           05  WS-NO-WRT-FE17-REC      PIC 9(4)  VALUE ZEROES.                  
                                                                                
       01  WS-F110-EOF                 PIC X(01).                               
           88 F110-EOF                           VALUE 'Y'.                     
                                                                                
       01  WS-DTE-RUN                  PIC 9(08) VALUE ZEROES.                  
       01  WS-DTE-RESERVE              PIC 9(08) VALUE ZEROES.                  
       01  WS-NUM-RESERVE              PIC 9(07) VALUE ZEROES.                  
       01  WS-DTE-RESERVE-1MTH         PIC 9(08) VALUE ZEROES.                  
       01  WS-NUM-RESERVE-1MTH         PIC 9(07) VALUE ZEROES.                  
       01  WS-DTE-RESERVE-2MTH         PIC 9(08) VALUE ZEROES.                  
       01  WS-NUM-RESERVE-2MTH         PIC 9(07) VALUE ZEROES.                  
       01  WS-DTE-RESERVE-2MTH5        PIC 9(08) VALUE ZEROES.                  
       01  WS-NUM-RESERVE-2MTH5        PIC 9(07) VALUE ZEROES.                  
       01  WS-DTE-RESERVE-2MTH6        PIC 9(08) VALUE ZEROES.                  
       01  WS-NUM-RESERVE-2MTH6        PIC 9(07) VALUE ZEROES.                  
       01  WS-DTE-RESERVE-3MTH         PIC 9(08) VALUE ZEROES.                  
       01  WS-NUM-RESERVE-3MTH         PIC 9(07) VALUE ZEROES.                  
                                                                                
      *-------------------------------------------------------------            
      *          LINKAGE FOR SUB-PROGRAM BP13C913                               
      *-------------------------------------------------------------            
       01  WS-LINK-REC.                                                         
           05  WS-LINK-NUM-SCH-ACC.                                             
               10  WS-LINK-NUM-SCH     PIC X(4).                                
               10  WS-LINK-NUM-ACC     PIC X(5).                                
                                                                                
       COPY P13COMM8.                                                           
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
                                                                                
      *-------------------------------------------------------------            
       0000-CONTROL.                                                            
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES    THRU 1000-EXIT.                           
           PERFORM 2000-READ-BM06F110 THRU 2000-EXIT.                           
           PERFORM 3000-MAIN-PROCESS  THRU 3000-EXIT                            
                       UNTIL F110-EOF                                           
           PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT.                           
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN INPUT  BM06F110                                                 
                       BM06K200                                                 
                OUTPUT BP13FE17.                                                
                                                                                
           IF WS-K200-STATUS NOT = 00 AND 97                                    
              DISPLAY "WS-K200-STATUS    : " WS-K200-STATUS                     
              MOVE     WS-K200-STATUS    TO  RETURN-CODE                        
              PERFORM 9000-CLOSE-FILES   THRU 9000-EXIT                         
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       2000-READ-BM06F110.                                                      
      *-------------------------------------------------------------            
                                                                                
           READ BM06F110 AT END MOVE 'Y'         TO WS-F110-EOF                 
                            GO TO 2000-EXIT.                                    
                                                                                
           ADD  1                                TO WS-F110-READ.               
                                                                                
       2000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3000-MAIN-PROCESS.                                                       
      *-------------------------------------------------------------            
                                                                                
           IF (F110-DTE-ACCEPTANCE = SPACES OR LOW-VALUES OR ZEROES) AND        
              (F110-ALLOCN-TAG NOT = SPACES AND LOW-VALUES) AND                 
              (F110-DTE-RESERVE-RELEASE NOT = SPACES AND LOW-VALUES)            
              PERFORM 3100-PROCESS-DATE     THRU 3100-EXIT                      
              IF FE17-ALERT-TYPE = 'A' OR 'B' OR 'C'                            
                 MOVE F110-ALLOCN-TAG            TO FE17-ALLOCATION-TAG         
                 MOVE F110-DTE-RESERVE-RELEASE   TO FE17-DTE-RESERVE            
                 MOVE F110-DTE-REPRICE           TO FE17-DTE-REPRICE            
                 MOVE F110-SCH-ACC-NO            TO FE17-SCH-ACC                
                                                                                
                 PERFORM 4000-READ-BM06K200      THRU 4000-EXIT                 
                 IF K200-NUM-RESERVE-BY = SPACES OR LOW-VALUES                  
                    MOVE F110-NUM-RSRVD-OIC      TO FE17-OIC                    
                 ELSE                                                           
                    MOVE K200-NUM-RESERVE-BY     TO FE17-OIC                    
                 END-IF                                                         
                                                                                
                 PERFORM 4100-CALL-BP13C913      THRU 4100-EXIT                 
                                                                                
                 WRITE BP13FE17-REC                                             
                 ADD 1                           TO WS-NO-WRT-FE17-REC          
              ELSE                                                              
                 CONTINUE                                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2000-READ-BM06F110            THRU 2000-EXIT.                
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       3100-PROCESS-DATE.                                                       
      *-------------------------------------------------------------            
                                                                                
           MOVE F110-DTE-RESERVE-RELEASE         TO WS-DTE-RESERVE              
                                                                                
           COMPUTE WS-NUM-RESERVE =                                             
                   FUNCTION INTEGER-OF-DATE (WS-DTE-RESERVE)                    
                                                                                
           COMPUTE WS-NUM-RESERVE-1MTH  = WS-NUM-RESERVE + 30                   
           COMPUTE WS-NUM-RESERVE-2MTH  = WS-NUM-RESERVE + 60                   
           COMPUTE WS-NUM-RESERVE-3MTH  = WS-NUM-RESERVE + 90                   
           COMPUTE WS-NUM-RESERVE-2MTH5 = WS-NUM-RESERVE + 75                   
           COMPUTE WS-NUM-RESERVE-2MTH6 = WS-NUM-RESERVE + 81                   
                                                                                
           COMPUTE WS-DTE-RESERVE-1MTH =                                        
                   FUNCTION DATE-OF-INTEGER (WS-NUM-RESERVE-1MTH)               
                                                                                
           COMPUTE WS-DTE-RESERVE-2MTH =                                        
                   FUNCTION DATE-OF-INTEGER (WS-NUM-RESERVE-2MTH)               
                                                                                
           COMPUTE WS-DTE-RESERVE-3MTH =                                        
                   FUNCTION DATE-OF-INTEGER (WS-NUM-RESERVE-3MTH)               
                                                                                
           COMPUTE WS-DTE-RESERVE-2MTH5 =                                       
                   FUNCTION DATE-OF-INTEGER (WS-NUM-RESERVE-2MTH5)              
                                                                                
           COMPUTE WS-DTE-RESERVE-2MTH6 =                                       
                   FUNCTION DATE-OF-INTEGER (WS-NUM-RESERVE-2MTH6)              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8)       TO WS-DTE-RUN                  
                                                                                
           IF WS-DTE-RUN >= WS-DTE-RESERVE-3MTH                                 
              MOVE 'C'                           TO FE17-ALERT-TYPE             
              GO TO 3100-EXIT                                                   
           END-IF.                                                              
           IF (WS-DTE-RUN >= WS-DTE-RESERVE-2MTH5) AND                          
              (WS-DTE-RUN <= WS-DTE-RESERVE-2MTH6)                              
              MOVE 'B'                           TO FE17-ALERT-TYPE             
              GO TO 3100-EXIT                                                   
           END-IF.                                                              
           IF (WS-DTE-RUN >= WS-DTE-RESERVE-1MTH) AND                           
              (WS-DTE-RUN <= WS-DTE-RESERVE-2MTH)                               
              MOVE 'A'                           TO FE17-ALERT-TYPE             
           END-IF.                                                              
                                                                                
       3100-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4000-READ-BM06K200.                                                      
      *-------------------------------------------------------------            
                                                                                
            MOVE SPACES                          TO BM06K200-REC                
            INITIALIZE BM06K200-REC                                             
            MOVE F110-SCH-ACC-NO                 TO K200-NUM-SCH-ACC            
                                                                                
            READ BM06K200.                                                      
                                                                                
            EVALUATE WS-K200-STATUS                                             
                WHEN 00                                                         
                     MOVE K200-NUM-NRIC1         TO FE17-HA1-NRIC               
                WHEN 23                                                         
                     MOVE SPACES                 TO FE17-HA1-NRIC               
                                                    K200-NUM-RESERVE-BY         
             DISPLAY 'SCHACC IN K200 NOT FND :' F110-SCH-ACC-NO                 
                WHEN OTHER                                                      
                     DISPLAY 'ERROR IN BM06K200 : ' WS-K200-STATUS              
                             '  K200-NUM-SCH-ACC ' K200-NUM-SCH-ACC             
                     MOVE     WS-K200-STATUS     TO  RETURN-CODE                
                     PERFORM 9000-CLOSE-FILES    THRU 9000-EXIT                 
            END-EVALUATE.                                                       
                                                                                
       4000-EXIT.                                                               
            EXIT.                                                               
                                                                                
      *-------------------------------------------------------------            
       4100-CALL-BP13C913.                                                      
      *-------------------------------------------------------------            
                                                                                
           MOVE SPACES                     TO WS-LINK-REC.                      
           MOVE F110-SCH-ACC-NO            TO WS-LINK-REC.                      
                                                                                
           CALL 'BP13C913' USING                                                
                 WS-LINK-REC, BP13COMM8-REC                                     
           END-CALL.                                                            
                                                                                
           EVALUATE COMM8-CDE-SYSERR                                            
             WHEN 000                                                           
                MOVE COMM8-RM-TYPE               TO FE17-FLAT-TYPE              
                STRING 'BLK'  COMM8-NUM-BLK  ' '                                
                       '#'  COMM8-NUM-LEVEL   '-'                               
                       COMM8-NUM-UNIT-MAIN         DELIMITED BY SIZE            
                       COMM8-NUM-UNIT-SUB     ' '  DELIMITED BY SIZE            
                       COMM8-NME-STREET            DELIMITED BY SIZE            
                                                 INTO FE17-FLAT-ADDRESS         
             WHEN 100                                                           
                DISPLAY 'SCH-ACC NOT FOUND IN PBF TABLE - '                     
                         F110-SCH-ACC-NO                                        
                MOVE  SPACES                     TO FE17-FLAT-ADDRESS           
             WHEN OTHER                                                         
                DISPLAY 'ERROR READING PBF TABLE' COMM8-CDE-SYSERR              
                MOVE  SPACES                     TO FE17-FLAT-ADDRESS           
           END-EVALUATE.                                                        
                                                                                
       4100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       9000-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY 'BP13CE17 CONTROL '.                                         
           DISPLAY '-----------------------------------'.                       
           DISPLAY 'TOTAL F110  REC READ      = ' WS-F110-READ.                 
           DISPLAY 'TOTAL FE17  REC WRITTEN   = ' WS-NO-WRT-FE17-REC            
                                                                                
           CLOSE      BM06F110                                                  
                      BM06K200                                                  
                      BP13FE17.                                                 
           IF WS-K200-STATUS NOT = 00 AND 97                                    
              DISPLAY "WS-K200-STATUS    : " WS-K200-STATUS                     
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
                                                                                
