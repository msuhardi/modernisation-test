      **************************                                                
       IDENTIFICATION DIVISION.                                                 
      **************************                                                
       PROGRAM-ID.    BP13C95J.                                                 
      *AUTHOR.        GURU..                                                    
      *DATE-WRITTEN.  22 FEB 2022.                                              
                                                                                
      * =========================================================== *           
      * SYSTEM OF COMMITMENT (BP13)                                 *           
      * =========================================================== *           
      *   OBJECTIVE   : TO UPDATE PROJECT NAME IN BP13FW3C.         *           
      *                                                             *           
      *   INPUT FILES :                                             *           
      *   1.  BP13FW3C                                              *           
      *                                                             *           
      *   OUTPUT FILES:                                             *           
      *   1.  P13FW3CA                                              *           
      *                                                             *           
      * CHQ REQ     DATE     BY  DESCRIPTIONS                       *           
      * -------- ---------- ---- ---------------------------------- *           
      * BP139063 22/02/2022 KR13 NEW PROGRAM                        *           
      * =========================================================== *           
                                                                                
       ENVIRONMENT DIVISION.                                                    
                                                                                
       CONFIGURATION SECTION.                                                   
                                                                                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
                                                                                
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13FW3C ASSIGN       TO BP13FW3C.                            
                                                                                
           SELECT BP13K813 ASSIGN       TO BP13K813                             
                           ORGANIZATION IS INDEXED                              
                           RECORD KEY   IS K813-KEY-FLD                         
                           FILE STATUS  IS WS-K813-STATUS                       
                           ACCESS MODE  IS DYNAMIC.                             
                                                                                
           SELECT P13FW3CA ASSIGN       TO P13FW3CA.                            
                                                                                
                                                                                
       DATA DIVISION.                                                           
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   BP13FW3C                                                            
            BLOCK CONTAINS     0 RECORDS                                        
            RECORD CONTAINS  500 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       COPY BP13FW3C.                                                           
                                                                                
       FD   BP13K813                                                            
            RECORD CONTAINS 1000 CHARACTERS.                                    
       COPY BP13K813.                                                           
                                                                                
       FD   P13FW3CA                                                            
            RECORD CONTAINS  500 CHARACTERS                                     
            LABEL RECORD IS STANDARD                                            
            RECORDING MODE IS F.                                                
       01   P13FW3CA-REC PIC X(500).                                            
                                                                                
                                                                                
      *************************                                                 
       WORKING-STORAGE SECTION.                                                 
      *************************                                                 
                                                                                
       01  WS-CURR-DATE           PIC X(08) VALUE ZEROES.                       
       01  WS-FW3C-READ           PIC 9(08) VALUE ZEROES.                       
       01  WS-K813-READ           PIC 9(08) VALUE ZEROES.                       
       01  WS-FW3C-EOF            PIC X     VALUE SPACES.                       
       01  WS-BLANK-PROJ          PIC 9(08) VALUE ZEROES.                       
       01  WS-FW3CA-WRITE         PIC 9(08) VALUE ZEROES.                       
       01  WS-K813-STATUS         PIC 9(02) VALUE ZEROES.                       
                                                                                
      ********************                                                      
       PROCEDURE DIVISION.                                                      
      ********************                                                      
                                                                                
      *----------*                                                              
       0000-MAIN.                                                               
      *----------*                                                              
                                                                                
           PERFORM 1000-OPEN-FILES   THRU 1000-EXIT.                            
                                                                                
           PERFORM 2000-READ-FW3C    THRU 2000-EXIT.                            
                                                                                
           PERFORM 3000-PROCESS-REC  THRU 3000-EXIT                             
                   UNTIL WS-FW3C-EOF = 'Y'.                                     
                                                                                
           PERFORM 9000-CLOSE-FILES  THRU 9000-EXIT.                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------*                                                        
       1000-OPEN-FILES.                                                         
      *----------------*                                                        
                                                                                
           OPEN INPUT  BP13FW3C                                                 
                       BP13K813                                                 
                OUTPUT P13FW3CA.                                                
                                                                                
           IF WS-K813-STATUS NOT EQUAL ZEROES AND 97                            
              DISPLAY 'BP13K813 - OPEN ERROR: ' WS-K813-STATUS                  
              MOVE WS-K813-STATUS     TO   RETURN-CODE                          
              PERFORM 9000-CLOSE-FILES THRU 9000-EXIT                           
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2000-READ-FW3C.                                                          
      *---------------*                                                         
                                                                                
           READ BP13FW3C AT END                                                 
                MOVE HIGH-VALUES TO BP13FW3C-REC                                
                MOVE 'Y' TO WS-FW3C-EOF                                         
                GO TO 2000-EXIT.                                                
                                                                                
           ADD 1 TO WS-FW3C-READ.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       3000-PROCESS-REC.                                                        
      *-----------------*                                                       
                                                                                
           IF FW3C-NME-PROJECT = SPACES OR LOW-VALUES                           
              PERFORM 3100-READ-BP13K813  THRU 3100-EXIT                        
              ADD 1 TO WS-BLANK-PROJ                                            
           END-IF                                                               
                                                                                
           WRITE P13FW3CA-REC FROM BP13FW3C-REC.                                
                                                                                
           ADD   1  TO WS-FW3CA-WRITE.                                          
                                                                                
           PERFORM 2000-READ-FW3C         THRU 2000-EXIT.                       
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       3100-READ-BP13K813.                                                      
      *-------------------*                                                     
           IF FW3C-NUM-BTO-ZONE = SPACES                                        
              GO TO 3100-EXIT.                                                  
                                                                                
           MOVE SPACES                      TO BP13K813-REC.                    
           INITIALIZE                          BP13K813-REC.                    
                                                                                
           MOVE FW3C-NUM-BTO-ZONE           TO K813-NUM-ZONE.                   
                                                                                
           START BP13K813 KEY >= K813-KEY-FLD                                   
                                                                                
           EVALUATE WS-K813-STATUS                                              
               WHEN 00                                                          
                    READ BP13K813 NEXT RECORD                                   
                    IF K813-NUM-ZONE = FW3C-NUM-BTO-ZONE                        
                       MOVE K813-NME-PROJECT-FLAT-AVAI                          
                                            TO FW3C-NME-PROJECT                 
                       ADD 1 TO WS-K813-READ                                    
                    ELSE                                                        
                       DISPLAY 'BP13K813 - NOT FND: ' K813-KEY-FLD              
                    END-IF                                                      
               WHEN 23                                                          
                    CONTINUE                                                    
               WHEN OTHER                                                       
                    DISPLAY 'BP13K813 - READ ERROR: ' WS-K813-STATUS            
           END-EVALUATE.                                                        
                                                                                
       3100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       9000-CLOSE-FILES.                                                        
      *-----------------*                                                       
                                                                                
           DISPLAY '******  BP13C95J  *************'.                           
           DISPLAY 'RECS READ FROM  BP13FW3C   : ' WS-FW3C-READ.                
           DISPLAY 'RECS WITH BLANK PROJ NAME  : ' WS-BLANK-PROJ.               
           DISPLAY 'RECS READ FROM  BP13K813   : ' WS-K813-READ.                
           DISPLAY 'RECS WRITTEN IN P13FW3CA   : ' WS-FW3CA-WRITE.              
                                                                                
           CLOSE BP13FW3C                                                       
                 BP13K813                                                       
                 P13FW3CA.                                                      
                                                                                
           IF WS-K813-STATUS NOT EQUAL 00                                       
              DISPLAY 'BP13K813 - CLOSE ERROR: ' WS-K813-STATUS                 
              MOVE WS-K813-STATUS        TO RETURN-CODE                         
           END-IF.                                                              
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
