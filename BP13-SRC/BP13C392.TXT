       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C392.                                                 
      *AUTHOR.        SELVY.                                                    
      *DATE-WRITTEN.  22/01/99.                                                 
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      * OBJECTIVE : TO MATCH SOC MASTER  TO BYPASS NON-FAMILY      *            
      *             NUCLEUS CASES BEFORE PASSING DATA TO MOE.      *            
      *                                                            *            
      *    INPUT  : BP13F216                                       *            
      *             BP13F800                                       *            
      *    OUTPUT : P13F216A                                       *            
      * ---------------------------------------------------------- *            
      * CHG REF  DATE      AUTHOR  REMARKS                         *            
      * -------- --------  ------  -------                         *            
      * BP130604 19990122   SRA    NEW PROGRAM                     *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F216 ASSIGN TO BP13F216.                                  
           SELECT BP13F800 ASSIGN TO BP13F800.                                  
           SELECT P13F216A ASSIGN TO P13F216A.                                  
                                                                                
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       FD  BP13F216     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 100 CHARACTERS                          
                        LABEL RECORD IS STANDARD                                
                        RECORDING MODE IS F.                                    
       COPY BP13F216.                                                           
                                                                                
       FD  BP13F800     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 2000 CHARACTERS                         
                        LABEL RECORD IS STANDARD                                
                        RECORDING MODE IS F.                                    
       COPY BP13F800.                                                           
                                                                                
       FD  P13F216A     BLOCK  CONTAINS 0 RECORDS                               
                        RECORD CONTAINS 100 CHARACTERS                          
                        LABEL RECORD IS OMITTED                                 
                        RECORDING MODE IS F.                                    
       01  P13F216A-REC            PIC X(100).                                  
                                                                                
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-FILE-FLAGS.                                                       
           05  WS-F216-EOF         PIC X         VALUE 'N'.                     
           05  WS-F800-EOF         PIC X         VALUE 'N'.                     
                                                                                
       01  WS-COUNTERS.                                                         
           05  WS-F216-CNT-READ    PIC 9(8)      VALUE ZEROS.                   
           05  WS-F800-CNT-READ    PIC 9(8)      VALUE ZEROS.                   
           05  WS-F216A-CNT-WRT    PIC 9(8)      VALUE ZEROS.                   
           05  WS-MATCH-REC        PIC 9(8)      VALUE ZEROS.                   
           05  WS-UNMTACH-F216     PIC 9(8)      VALUE ZEROS.                   
           05  WS-UNMTACH-F800     PIC 9(8)      VALUE ZEROS.                   
           05  WS-MATCH-ELIG-SCH   PIC 9(8)      VALUE ZEROS.                   
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
       0000-CONTROL.                                                            
      *****************************************************************         
                                                                                
           PERFORM 1000-OPEN-FILES      THRU 1000-EXIT.                         
           PERFORM 1200-F216-READ       THRU 1200-EXIT.                         
           PERFORM 1500-F800-READ       THRU 1500-EXIT.                         
           PERFORM 2000-MAIN-ROUTINE    THRU 2000-EXIT                          
                   UNTIL WS-F216-EOF = 'Y' AND                                  
                         WS-F800-EOF = 'Y'                                      
           PERFORM 9000-CLOSE-FILES     THRU 9000-EXIT.                         
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
       1000-OPEN-FILES.                                                         
      *****************************************************************         
                                                                                
           OPEN INPUT  BP13F216                                                 
                       BP13F800                                                 
               OUTPUT  P13F216A.                                                
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *****************************************************************         
       1200-F216-READ.                                                          
      *****************************************************************         
                                                                                
           READ BP13F216 AT END                                                 
                        MOVE HIGH-VALUES TO F216-NUM-REGN                       
                        MOVE 'Y' TO WS-F216-EOF                                 
                        GO TO 1200-EXIT.                                        
                                                                                
           ADD 1 TO WS-F216-CNT-READ.                                           
                                                                                
       1200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       1500-F800-READ.                                                          
      *****************************************************************         
                                                                                
           READ BP13F800 AT END                                                 
                        MOVE HIGH-VALUES TO F800-NUM-REGN                       
                        MOVE 'Y' TO WS-F800-EOF                                 
                        GO TO 1500-EXIT.                                        
                                                                                
           ADD 1 TO WS-F800-CNT-READ.                                           
                                                                                
       1500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *****************************************************************         
       2000-MAIN-ROUTINE.                                                       
      *****************************************************************         
                                                                                
           IF F216-NUM-REGN > F800-NUM-REGN                                     
              ADD 1 TO WS-UNMTACH-F800                                          
              PERFORM 1500-F800-READ THRU 1500-EXIT                             
           ELSE                                                                 
              IF F216-NUM-REGN < F800-NUM-REGN                                  
                 PERFORM 2200-UNMATCH-F216  THRU 2200-EXIT                      
                 DISPLAY 'REC NOT FOUND IN SOC,REG= ' F216-NUM-REGN             
                         ' SCH= ' F216-NUM-SCH-ACC                              
                 PERFORM 1200-F216-READ THRU 1200-EXIT                          
              ELSE                                                              
                 PERFORM 2100-CHECK-ELIGSCH THRU 2100-EXIT                      
                 PERFORM 1200-F216-READ THRU 1200-EXIT                          
                 PERFORM 1500-F800-READ THRU 1500-EXIT                          
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      ******************************************************************        
       2100-CHECK-ELIGSCH.                                                      
      ******************************************************************        
                                                                                
            ADD 1 TO WS-MATCH-REC.                                              
            IF F800-NUM-ELIG-SCHEME  = 'ASC' OR 'FS ' OR 'JSS'                  
                                             OR 'OR ' OR 'SS '                  
               ADD 1 TO WS-MATCH-ELIG-SCH                                       
               DISPLAY 'REG= ' F800-NUM-REGN                                    
                       ' ELIG= ' F800-NUM-ELIG-SCHEME                           
                       ' UIN1= ' F800-NUM-NRIC1                                 
                       ' SCH= ' F216-NUM-SCH-ACC                                
            ELSE                                                                
               MOVE BP13F216-REC TO P13F216A-REC                                
               WRITE P13F216A-REC                                               
               ADD 1 TO WS-F216A-CNT-WRT                                        
            END-IF.                                                             
                                                                                
       2100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       2200-UNMATCH-F216.                                                       
      ******************************************************************        
                                                                                
           ADD 1 TO WS-UNMTACH-F216.                                            
           MOVE BP13F216-REC TO P13F216A-REC.                                   
           WRITE P13F216A-REC.                                                  
           ADD 1 TO  WS-F216A-CNT-WRT.                                          
                                                                                
       2200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       9000-CLOSE-FILES.                                                        
      ******************************************************************        
           DISPLAY 'TOTAL F216 REC READ       :'  WS-F216-CNT-READ.             
           DISPLAY 'TOTAL F800 REC READ       :'  WS-F800-CNT-READ.             
           DISPLAY 'TOTAL MATCH F216 * F800   :'  WS-MATCH-REC.                 
           DISPLAY 'TOTAL F216A REC WRITTEN   :'  WS-F216A-CNT-WRT.             
           DISPLAY 'TOTAL MATCH ELIG SCHEME   :'  WS-MATCH-ELIG-SCH.            
           DISPLAY 'TOTAL UNMATCH F216 REC    :'  WS-UNMTACH-F216.              
           DISPLAY 'TOTAL UNMATCH F800 REC    :'  WS-UNMTACH-F800.              
                                                                                
           CLOSE BP13F216                                                       
                 BP13F800                                                       
                 P13F216A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
            EXIT.                                                               
