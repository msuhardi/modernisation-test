      *------------------------*                                                
       IDENTIFICATION DIVISION.                                                 
      *------------------------*                                                
       PROGRAM-ID.    BP13C25G.                                                 
       AUTHOR.        BALA AMUTHAN.                                             
       DATE-WRITTEN.  15 SEP 2006.                                              
                                                                                
      *============================================================*            
      * BP13 - LOAN ELIGIBILITY AND ASSESSMENT SYSTEM              *            
      *============================================================*            
      * OBJECTIVES       : PROGRAM TO CREATE THE XML FORMAT RECORDS*            
      *                    FOR FTP                                 *            
      *============================================================*            
      * REVISION HISTORY :                                         *            
      * CHG-REQ#  BY    DATE        DESCRIPTION                    *            
      * --------  ----  ----------  ------------------------------ *            
      * BP132925  BA2   15/09/2006  NEW PROGRAM.                   *            
      * BP133086  BA2   11/10/2007  INCLUDE STATUSC,STATUSM AND    *            
      *                             STATUSIO TAG.                  *            
      *============================================================*            
      *------------------------------------------------------------*            
       ENVIRONMENT DIVISION.                                                    
      *-------------------------------------------------------------            
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13F299 ASSIGN TO BP13F299.                                  
                                                                                
           SELECT BP13FXML ASSIGN TO BP13FXML.                                  
                                                                                
      *-------------------------------------------------------------            
       DATA DIVISION.                                                           
      *-------------------------------------------------------------            
       FILE SECTION.                                                            
       FD  BP13F299                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 80  CHARACTERS                                       
           LABEL RECORDS ARE STANDARD.                                          
       01 WS-BP13F299-REC PIC X(80).                                            
                                                                                
       FD  BP13FXML                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 132 CHARACTERS                                       
           LABEL RECORDS ARE OMITTED.                                           
       01 XML-REC PIC X(132).                                                   
                                                                                
      *------------------------------------------------------------*            
       WORKING-STORAGE SECTION.                                                 
      *------------------------------------------------------------*            
      *-------------------------*                                               
      *    copy cob for f299    *                                               
      *-------------------------*                                               
       COPY BP13F299.                                                           
       01 WS-REC-BP13F299 REDEFINES BP13F299-REC.                               
           05  FILLER                PIC X(42).                                 
           05  F299-REMARKM          PIC X(01).                                 
           05  F299-REMARKC          PIC X(01).                                 
           05  F299-REMARKO          PIC X(01).                                 
           05  FILLER                PIC X(35).                                 
                                                                                
      *-------------------------*                                               
      *    DATE - VARIABLES     *                                               
      *-------------------------*                                               
       01  WS-CURRENT-DATE.                                                     
           05  WS-CURR-DATE.                                                    
               10 WS-CURR-CCYY           PIC X(04)  VALUE SPACES.               
               10 WS-CURR-MM             PIC X(02)  VALUE SPACES.               
               10 WS-CURR-DD             PIC X(02)  VALUE SPACES.               
           05  WS-CURRENT-TIME           PIC X(09)  VALUE SPACES.               
               05  WS-TIME REDEFINES WS-CURRENT-TIME.                           
                   10  WS-CURR-TIME.                                            
                       15 WS-CURR-HH     PIC X(02).                             
                       15 WS-CURR-MN     PIC X(02).                             
                       15 WS-CURR-SS     PIC X(02).                             
                   10  FILLER            PIC X(02).                             
                   10  WS-GM             PIC X(01).                             
           05  FILLER                    PIC X(04)  VALUE SPACES.               
                                                                                
      *-------------------------*                                               
      *  COUNT - VARIABLES.     *                                               
      *-------------------------*                                               
       01  WS-COUNT.                                                            
           05  WS-READ-F299              PIC 9(05) COMP VALUE ZEROES.           
           05  WS-WRIT-XML               PIC 9(05) COMP VALUE ZEROES.           
                                                                                
      *-------------------------*                                               
      *  SWITCH - FLAG          *                                               
      *-------------------------*                                               
       01  WS-SWITCH.                                                           
           05  WS-F299-END               PIC X(01)   VALUE 'N'.                 
               88  CS-F299-END                       VALUE 'Y'.                 
                                                                                
      *-------------------------*                                               
      *  JUSTIFY-VARIABLES      *                                               
      *-------------------------*                                               
       01  WS-VAR.                                                              
           05  WS-LEFT-JUST-TXT     PIC X(04) VALUE SPACES.                     
           05  WS-RIGHT-JUST-TXT    PIC X(04) VALUE SPACES.                     
                                                                                
           05  WS-LEN               PIC 9(02) VALUE ZEROES.                     
           05  WS-START             PIC 9(02) VALUE ZEROES.                     
           05  WS-CNT               PIC 9(02) VALUE ZEROES.                     
                                                                                
      *-------------------------*                                               
      *  TAB-VARIABLES          *                                               
      *-------------------------*                                               
       01  WS-TAB.                                                              
           05  WS-CHAR           PIC X(20) VALUE SPACES.                        
           05  WS-TAB-CHR   REDEFINES WS-CHAR OCCURS 20 TIMES.                  
               10  WS-CHAR-TAB   PIC X(1).                                      
                                                                                
      *------------------------------------------------------------*            
       PROCEDURE DIVISION.                                                      
      *------------------------------------------------------------*            
                                                                                
      *------------------------------------------------------------*            
       0000-MAIN.                                                               
      *------------------------------------------------------------*            
           PERFORM 1000-INIT-ROUTINE  THRU 1000-EXIT.                           
           PERFORM 2000-READ-F299     THRU 2000-EXIT.                           
           PERFORM 3000-XML-PROCESS   THRU 3000-EXIT.                           
           PERFORM 9999-CLOSE-ROUTINE THRU 9999-EXIT.                           
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-INIT-ROUTINE.                                                       
      *-------------------------------------------------------------            
           OPEN INPUT BP13F299                                                  
               OUTPUT BP13FXML.                                                 
                                                                                
           MOVE FUNCTION CURRENT-DATE TO WS-CURRENT-DATE.                       
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       2000-READ-F299.                                                          
      *-------------------------------------------------------------            
           READ BP13F299 AT END MOVE 'Y' TO WS-F299-END                         
                                GO TO 2000-EXIT.                                
           MOVE WS-BP13F299-REC       TO BP13F299-REC.                          
           ADD 1 TO WS-READ-F299.                                               
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *------------------------------------------------------------*            
       3000-XML-PROCESS.                                                        
      *------------------------------------------------------------*            
                                                                                
           IF WS-READ-F299 > 0                                                  
              PERFORM 4000-OPEN-TAG       THRU 4000-EXIT                        
              PERFORM 5000-DETAIL-REC-XML THRU 5000-EXIT                        
                      UNTIL CS-F299-END                                         
              PERFORM 6000-CLOSE-TAG      THRU 6000-EXIT                        
           END-IF.                                                              
                                                                                
       3000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      *-------------------------------------------------------------            
       4000-OPEN-TAG.                                                           
      *-------------------------------------------------------------            
           MOVE    SPACES              TO  XML-REC.                             
           STRING '<?xml version="1.0" ?>'                                      
               DELIMITED BY SIZE INTO  XML-REC                                  
           END-STRING.                                                          
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           STRING '  <MYDATA>'                                                  
               DELIMITED BY SIZE INTO  XML-REC                                  
           END-STRING.                                                          
           WRITE   XML-REC.                                                     
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-DETAIL-REC-XML.                                                     
      *-------------------------------------------------------------            
           MOVE    SPACES              TO  XML-REC.                             
           STRING '    <CONTRACT>'                                              
               DELIMITED BY SIZE INTO  XML-REC                                  
           END-STRING.                                                          
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           IF F299-NUM-SELECTION = SPACES OR LOW-VALUES                         
              STRING '      <SELECTION></SELECTION>'                            
                     DELIMITED BY SIZE INTO  XML-REC                            
              END-STRING                                                        
           ELSE                                                                 
              STRING '      <SELECTION>' DELIMITED BY SIZE                      
                       F299-NUM-SELECTION DELIMITED BY SPACES                   
                     '</SELECTION>'                                             
                     DELIMITED BY SIZE INTO  XML-REC                            
              END-STRING                                                        
           END-IF.                                                              
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           IF F299-NUM-NEW-TOWN = SPACES OR LOW-VALUES                          
             STRING '      <NEWTOWN></NEWTOWN>'                                 
                 DELIMITED BY SIZE INTO XML-REC                                 
             END-STRING                                                         
           ELSE                                                                 
             STRING '      <NEWTOWN>'      DELIMITED BY SIZE                    
                    F299-NUM-NEW-TOWN DELIMITED BY '  '                         
                 '</NEWTOWN>' DELIMITED BY SIZE INTO XML-REC                    
             END-STRING                                                         
           END-IF.                                                              
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           IF F299-NUM-NEIGH  = SPACES OR LOW-VALUES                            
              STRING '      <NEIGH></NEIGH>'                                    
                  DELIMITED BY SIZE INTO XML-REC                                
              END-STRING                                                        
           ELSE                                                                 
              MOVE SPACES                   TO  WS-RIGHT-JUST-TXT               
                                                WS-LEFT-JUST-TXT                
              MOVE F299-NUM-NEIGH           TO  WS-RIGHT-JUST-TXT               
              PERFORM 7000-REMOVE-RIGHT-SPACES THRU 7000-EXIT                   
              STRING '      <NEIGH>' DELIMITED BY SIZE                          
                 WS-LEFT-JUST-TXT DELIMITED BY SPACE                            
                 '</NEIGH>' DELIMITED BY SIZE INTO XML-REC                      
              END-STRING                                                        
           END-IF.                                                              
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           IF F299-NUM-CNTRCT = SPACES OR LOW-VALUES                            
              STRING '      <CNTRCT></CNTRCT>'                                  
                     DELIMITED BY SIZE INTO  XML-REC                            
              END-STRING                                                        
           ELSE                                                                 
              MOVE SPACES                   TO  WS-RIGHT-JUST-TXT               
                                                WS-LEFT-JUST-TXT                
              MOVE F299-NUM-CNTRCT          TO  WS-RIGHT-JUST-TXT               
              PERFORM 7000-REMOVE-RIGHT-SPACES THRU 7000-EXIT                   
              STRING '      <CNTRCT>' DELIMITED BY SIZE                         
                     WS-LEFT-JUST-TXT DELIMITED BY ' '                          
                     '</CNTRCT>' DELIMITED BY SIZE INTO XML-REC                 
              END-STRING                                                        
           END-IF.                                                              
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           IF F299-NUM-FLAT-TYPE = SPACES OR LOW-VALUES                         
              STRING '      <FLATTY></FLATTY>'                                  
                      DELIMITED BY SIZE INTO  XML-REC                           
              END-STRING                                                        
           ELSE                                                                 
              STRING '      <FLATTY>'                                           
                     F299-NUM-FLAT-TYPE                                         
                     '</FLATTY>'                                                
                  DELIMITED BY SIZE INTO  XML-REC                               
              END-STRING                                                        
           END-IF.                                                              
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           IF F299-NUM-UNITS NOT NUMERIC                                        
              MOVE    ZEROES           TO  F299-NUM-UNITS                       
           END-IF.                                                              
           STRING '      <UNIT>'                                                
               F299-NUM-UNITS                                                   
                 '</UNIT>'                                                      
             DELIMITED BY SIZE INTO XML-REC                                     
           END-STRING                                                           
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           IF F299-NUM-REMARKS = SPACES OR LOW-VALUES                           
              STRING '      <STATUS></STATUS>'                                  
                  DELIMITED BY SIZE INTO XML-REC                                
              END-STRING                                                        
           ELSE                                                                 
              STRING '      <STATUS>'                                           
                     F299-NUM-REMARKS                                           
                     '</STATUS>'                                                
                  DELIMITED BY SIZE INTO XML-REC                                
              END-STRING                                                        
           END-IF.                                                              
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           IF F299-REMARKC  = SPACES OR LOW-VALUES                              
              STRING '      <STATUSC></STATUSC>'                                
                  DELIMITED BY SIZE INTO XML-REC                                
              END-STRING                                                        
           ELSE                                                                 
              STRING '      <STATUSC>'                                          
                     F299-REMARKC                                               
                     '</STATUSC>'                                               
                  DELIMITED BY SIZE INTO XML-REC                                
              END-STRING                                                        
           END-IF.                                                              
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           IF F299-REMARKM  = SPACES OR LOW-VALUES                              
              STRING '      <STATUSM></STATUSM>'                                
                  DELIMITED BY SIZE INTO XML-REC                                
              END-STRING                                                        
           ELSE                                                                 
              STRING '      <STATUSM>'                                          
                     F299-REMARKM                                               
                     '</STATUSM>'                                               
                  DELIMITED BY SIZE INTO XML-REC                                
              END-STRING                                                        
           END-IF.                                                              
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           IF F299-REMARKO  = SPACES OR LOW-VALUES                              
              STRING '      <STATUSIO></STATUSIO>'                              
                  DELIMITED BY SIZE INTO XML-REC                                
              END-STRING                                                        
           ELSE                                                                 
              STRING '      <STATUSIO>'                                         
                     F299-REMARKO                                               
                     '</STATUSIO>'                                              
                  DELIMITED BY SIZE INTO XML-REC                                
              END-STRING                                                        
           END-IF.                                                              
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           STRING '    </CONTRACT>'                                             
               DELIMITED BY SIZE INTO  XML-REC                                  
           END-STRING.                                                          
           WRITE   XML-REC.                                                     
                                                                                
           ADD +1                  TO WS-WRIT-XML.                              
           PERFORM 2000-READ-F299         THRU 2000-EXIT.                       
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       6000-CLOSE-TAG.                                                          
      *-------------------------------------------------------------            
      *DATE/TIME TAG                                                            
           MOVE    SPACES              TO  XML-REC.                             
           STRING '    <CONTRACT>'                                              
               DELIMITED BY SIZE INTO  XML-REC                                  
           END-STRING.                                                          
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           STRING '      <SELECTION>' DELIMITED BY SIZE                         
                  WS-CURR-DD          DELIMITED BY SIZE                         
                  '/'                 DELIMITED BY SIZE                         
                  WS-CURR-MM          DELIMITED BY SIZE                         
                  '/'                 DELIMITED BY SIZE                         
                  WS-CURR-CCYY        DELIMITED BY SIZE                         
                  '</SELECTION>'                                                
                   DELIMITED BY SIZE INTO  XML-REC                              
           END-STRING.                                                          
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           STRING '      <NEWTOWN>'  DELIMITED BY SIZE                          
                 WS-CURR-HH          DELIMITED BY SIZE                          
                 ':'                 DELIMITED BY SIZE                          
                 WS-CURR-MN          DELIMITED BY SIZE                          
                 ':'                 DELIMITED BY SIZE                          
                 WS-CURR-SS          DELIMITED BY SIZE                          
                 '</NEWTOWN>' DELIMITED BY SIZE INTO XML-REC                    
           END-STRING.                                                          
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           MOVE   '      <NEIGH>X</NEIGH>'                                      
                                       TO  XML-REC.                             
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           MOVE   '      <CNTRCT>X</CNTRCT>'                                    
                                       TO  XML-REC.                             
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           MOVE   '      <FLATTY>X</FLATTY>'                                    
                                       TO  XML-REC.                             
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           MOVE   '      <UNIT>X</UNIT>'                                        
                                       TO  XML-REC.                             
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           MOVE  '      <STATUS>X</STATUS>'                                     
                                       TO XML-REC.                              
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           MOVE  '      <STATUSC>X</STATUSC>'                                   
                                       TO XML-REC.                              
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           MOVE  '      <STATUSM>X</STATUSM>'                                   
                                       TO XML-REC.                              
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           MOVE  '      <STATUSIO>X</STATUSIO>'                                 
                                       TO XML-REC.                              
           WRITE   XML-REC.                                                     
                                                                                
           MOVE    SPACES              TO  XML-REC.                             
           STRING '    </CONTRACT>'                                             
               DELIMITED BY SIZE INTO  XML-REC                                  
           END-STRING.                                                          
           WRITE   XML-REC.                                                     
                                                                                
      *CLOSING TAG                                                              
           MOVE    SPACES              TO  XML-REC.                             
           STRING '  </MYDATA>'                                                 
               DELIMITED BY SIZE INTO  XML-REC                                  
           END-STRING.                                                          
           WRITE   XML-REC.                                                     
                                                                                
           ADD +1                  TO WS-WRIT-XML.                              
                                                                                
       6000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       7000-REMOVE-RIGHT-SPACES.                                                
      *-------------------------------------------------------------            
           MOVE ZEROES     TO  WS-LEN                                           
                               WS-START.                                        
           INSPECT WS-RIGHT-JUST-TXT TALLYING WS-START                          
                      FOR ALL SPACES.                                           
           COMPUTE WS-LEN = 4 - WS-START.                                       
           ADD +1          TO WS-START.                                         
           MOVE WS-RIGHT-JUST-TXT(WS-START:WS-LEN)      TO                      
                                  WS-LEFT-JUST-TXT.                             
                                                                                
       7000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9999-CLOSE-ROUTINE.                                                      
      *-------------------------------------------------------------            
           DISPLAY '------- BP13C25G -------------'.                            
           DISPLAY 'NO OF RECORDS READ    (BP13F299) : '                        
                                               WS-READ-F299.                    
           DISPLAY 'NO OF RECORDS WRITTEN (BP13FXML) : '                        
                                               WS-WRIT-XML.                     
                                                                                
           CLOSE BP13F299                                                       
                 BP13FXML.                                                      
                                                                                
       9999-EXIT.                                                               
           STOP RUN.                                                            
                                                                                
      *-------------------------------------------------------------            
      *             END-PROGRAM.                                   *            
      *-------------------------------------------------------------            
