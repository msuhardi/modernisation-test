      *-------------------------*                                               
       IDENTIFICATION DIVISION.                                                 
      *-------------------------*                                               
       PROGRAM-ID.    BP13C408.                                                 
      *AUTHOR.        J MOHD SHAMSUDEEN                                         
      *DATE-WRITTEN.  23/02/99.                                                 
                                                                                
      * ========================================================= *             
      *                SYSTEM OF COMMITMENT  (BP13)               *             
      * ========================================================= *             
      *    OBJECTIVE :                                            *             
      *      1. TO PRINT THE OUTSTANDING CASES FOR STAMPING       *             
      *         > 1 MTH  (TP CASES)                               *             
      *                                                           *             
      *    I/O FILES :                                            *             
      *       INPUT  :  BP13F410 -                                *             
      *                 BP13K022 - OIC    FILE                    *             
      *                 B05K006  - SFMAST FILE                    *             
      *      OUTPUT  :  BP13L408 - PRINT  FILE                    *             
      *                                                           *             
      *    FUNCTION :                                             *             
      *      1. BATCH PROGRAMMING IN COBOL                        *             
      *      2. PRINT LISTINGS OF OUTSTANDING CASES FOR STAMPING  *             
      *                                                           *             
      * ========================================================= *             
      * CHGE-NO  DATE     BY   DESCRIPTIONS                       *             
      * -------- -------- ---  ------------                       *             
      * BP130565 19/02/99 JMS  NEW PROGRAM                        *             
      * BP130883 08/08/00 RAS  TO CHANGE BP13C904 TO BP13C913     *             
      * BP132504 26/03/04 JF4  INCLUDE OIC IN THE REPORT          *             
      * BP132561 20/07/04 SSD  REMOVE THE CHECKING FOR            *             
      *                        DTE-FILE-SENT & DTE-LEGAL-DOC-SENT *             
      *                        WHEN DTE-STAMP-DOA IS BLANK        *             
      * BP132734 12/09/05 JF4  MONITOR 1 MONTH                    *             
      * BP134779 13/06/13 PCL4 TO CONVERT THE REPORT TO SENDMAIL  *             
      * BP135901 26/08/15 KSJ3 -TO CHANGE HEADER TO DYNAMIC TO    *             
      *                         CLARIFY DIFF REPORT CRITERIA      *             
      *                        -TO BYPASS CASES WITH NO STAMP FEE *             
      * BP136625 01/02/17 RJB1 REMOVE NORMALA BUANG, INCLUDE CHANDRA            
      *                        ARUMUGAM AND MEENACHI              *             
      * BP137087 17/11/17 PCL4 TO UPDATE EMAIL RECIPIENTS         *             
      * BP137133 13/12/17 PCL4 TO UPDATE EMAIL RECIPIENTS         *             
      * BP137155 08/01/18 PCL4 TO UPDATE EMAIL RECIPIENTS         *             
      * BP138202 20/02/20 KV5  ADDED EMAIL RECIPIENTS             *             
      * BP138757 23/07/21 KV8  TO REPLACE KATHIJAH TO SUZANNE     *             
      * BP139030 07/01/22 ZAR7 REPLACE CHUA YEE MIEN TO LIM KOK CHUN            
      * BP139159 13/06/22 KAC1 ADDED zoe_sm_aw@hdb.gov.sg         *             
      *                              jocelyn_wt_seah@hdb.gov.sg   *             
      * BP139177 17/08/22 KAC1 TO CATER FOR PREMIUM STAMP FEE     *             
      *                        INSTEAD OF PREMIUM RATE  > ZEROES  *             
      * BP139276 05/04/24 KAC1 REPLACE LIM KOK CHUN BY            *             
      *                                KATHIJAH_BEE_ALI_MOHAMED   *             
      * ========================================================= *             
                                                                                
      *----------------------*                                                  
       ENVIRONMENT DIVISION.                                                    
      *----------------------*                                                  
                                                                                
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT B05K006   ASSIGN        TO B05K006                            
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K006-HDB-REF-SF                    
                            FILE STATUS   IS WS-K006-STATUS.                    
                                                                                
           SELECT BP13K022  ASSIGN        TO BP13K022                           
                            ACCESS MODE   IS RANDOM                             
                            ORGANIZATION  IS INDEXED                            
                            RECORD KEY    IS K022-KEY-FLD                       
                            ALTERNATE KEY IS K022-NUM-REGN                      
                            FILE STATUS   IS WS-K022-STATUS.                    
                                                                                
           SELECT BP13F410  ASSIGN        TO BP13F410.                          
                                                                                
           SELECT BP13L408  ASSIGN        TO BP13L408.                          
                                                                                
                                                                                
      *---------------*                                                         
       DATA DIVISION.                                                           
      *---------------*                                                         
                                                                                
       FILE SECTION.                                                            
                                                                                
       FD   B05K006.                                                            
       COPY B05K006.                                                            
                                                                                
       FD   BP13K022.                                                           
       COPY BP13K022.                                                           
                                                                                
       FD   BP13F410                                                            
            RECORD CONTAINS 2000 CHARACTERS                                     
            RECORDING MODE IS F.                                                
       COPY BP13F410.                                                           
                                                                                
       FD   BP13L408                                                            
            RECORDING MODE  IS  F                                               
            RECORD CONTAINS 132 CHARACTERS.                                     
       01   BP13L408-REC         PIC X(132).                                    
                                                                                
                                                                                
      *-----------------------*                                                 
       WORKING-STORAGE SECTION.                                                 
      *-----------------------*                                                 
                                                                                
       01  WS-EMAIL-RPT-VARIABLES.                                              
           05  WS-DATE-RPT-DTL.                                                 
               10 FILLER                PIC X(6)  VALUE 'DATE: '.               
               10 WS-DATE-RPT           PIC X(10) VALUE SPACES.                 
           05  WS-MAIL-RPT-HELO         PIC X(12) VALUE 'HELO SGPHDB1'.         
           05  WS-MAIL-RPT-MAIL-FROM    PIC X(27)                               
               VALUE 'MAIL FROM:<OPCP@SGPHDB1>'.                                
           05  WS-MAIL-RPT-DATA         PIC X(4)                                
               VALUE 'DATA'.                                                    
           05  WS-MAIL-RPT-FROM         PIC X(40)                               
               VALUE 'FROM:Soc System - Email Alert'.                           
           05  WS-MAIL-RPT-SUBJECT.                                             
               10  FILLER               PIC X(36)  VALUE                        
               'SUBJECT : LIST OF OUTSTANDING CASES '.                          
               10  WS-MAIL-RPT-SUBJ     PIC X(64).                              
                                                                                
           05  WS-MAIL-RCPT-TO-BALA     PIC X(80)  VALUE                        
               'RCPT TO:<BALASUBRAMANIAM_ARUNASALAM@hdb.gov.sg>'.               
           05  WS-MAIL-TO-BALA          PIC X(80)  VALUE                        
               'TO:<BALASUBRAMANIAM_ARUNASALAM@hdb.gov.sg>'.                    
                                                                                
           05  WS-MAIL-RCPT-TO-NOOR9    PIC X(80)  VALUE                        
               'RCPT TO:<NOORJAHAN_SHAIK_MOHAMED_GULEMKADER@hdb.gov.sg>'        
                .                                                               
           05  WS-MAIL-TO-NOOR9         PIC X(80)  VALUE                        
               'TO:<NOORJAHAN_SHAIK_MOHAMED_GULEMKADER@hdb.gov.sg>'.            
                                                                                
           05  WS-MAIL-RCPT-TO-SB1      PIC X(80)  VALUE                        
               'RCPT TO:<MUHAMMED_SHARIFF_SAHROM@hdb.gov.sg>'.                  
           05  WS-MAIL-TO-SB1           PIC X(80)  VALUE                        
               'TO:<MUHAMMED_SHARIFF_SAHROM@hdb.gov.sg>'.                       
                                                                                
           05  WS-MAIL-RCPT-TO-CL14     PIC X(80)  VALUE                        
               'RCPT TO:<CHANDRA_LAGHA_ARUMUGAM@hdb.gov.sg>'.                   
           05  WS-MAIL-TO-CL14          PIC X(80)  VALUE                        
               'TO:<CHANDRA_LAGHA_ARUMUGAM@hdb.gov.sg>'.                        
                                                                                
           05  WS-MAIL-RCPT-TO-SS98     PIC X(80)  VALUE                        
      *        'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                 
               'RCPT TO:<SUZANNE_SC_HENG@HDB.GOV.SG>'.                          
           05  WS-MAIL-TO-SS98          PIC X(80)  VALUE                        
      *        'TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                      
               'TO:<SUZANNE_SC_HENG@HDB.GOV.SG>'.                               
                                                                                
           05  WS-MAIL-RCPT-TO-CYM3     PIC X(80)  VALUE                        
               'RCPT TO:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                 
           05  WS-MAIL-TO-CYM3          PIC X(80)  VALUE                        
               'CC:<KATHIJAH_BEE_ALI_MOHAMED@hdb.gov.sg>'.                      
                                                                                
           05  WS-MAIL-RCPT-TO-OMH      PIC X(80)  VALUE                        
               'RCPT TO:<OH_Meng_Huat@hdb.gov.sg>'.                             
           05  WS-MAIL-TO-OMH           PIC X(80)  VALUE                        
               'TO:<OH_Meng_Huat@hdb.gov.sg>'.                                  
                                                                                
           05  WS-MAIL-RCPT-TO-NCJ      PIC X(80)  VALUE                        
               'RCPT TO:<NGAI_Chyau_Jiann@hdb.gov.sg>'.                         
           05  WS-MAIL-TO-NCJ           PIC X(80)  VALUE                        
               'TO:<NGAI_Chyau_Jiann@hdb.gov.sg>'.                              
                                                                                
           05  WS-MAIL-RCPT-TO-ZSA      PIC X(80)  VALUE                        
               'RCPT TO:<zoe_sm_aw@hdb.gov.sg>'.                                
           05  WS-MAIL-TO-ZSA           PIC X(80)  VALUE                        
               'TO:<zoe_sm_aw@hdb.gov.sg>'.                                     
                                                                                
           05  WS-MAIL-RCPT-TO-JWS      PIC X(80)  VALUE                        
               'RCPT TO:<jocelyn_wt_seah@hdb.gov.sg>'.                          
           05  WS-MAIL-TO-JWS           PIC X(80)  VALUE                        
               'TO:<jocelyn_wt_seah@hdb.gov.sg>'.                               
                                                                                
      *-----------------------------------------------------------*             
      *    DEFINE BP13L408               HEADING LINES            *             
      *-----------------------------------------------------------*             
       01  PRT-L408-HDG-1.                                                      
           05  L408-REPORT-ID       PIC X(10) VALUE 'BP13L408'.                 
           05  FILLER               PIC X(8)  VALUE '    HDB3'.                 
           05  FILLER               PIC X(27) VALUE SPACES.                     
           05  FILLER               PIC X(39) VALUE                             
               'S Y S T E M   O F   C O M M I T M E N T'.                       
           05  FILLER               PIC X(14) VALUE SPACES.                     
           05  FILLER               PIC X(7)  VALUE 'DATE : '.                  
           05  L408-CURRENT-DATE    PIC X(10) VALUE SPACES.                     
           05  FILLER               PIC X(4)  VALUE SPACES.                     
           05  FILLER               PIC X(7)  VALUE 'PAGE : '.                  
           05  L408-PAGE            PIC ZZZZ9.                                  
                                                                                
       01  PRT-L408-HDG-2.                                                      
           05  FILLER               PIC X(27) VALUE SPACES.                     
           05  FILLER               PIC X(49) VALUE                             
               'SUMMARY LIST OF OUTSTANDING CASES FOR STAMPING ('.              
           05  PRT-L408-HDG-TITLE   PIC X(40) VALUE SPACES.                     
                                                                                
       01  PRT-L408-HDG-4.                                                      
           05  FILLER               PIC X(04) VALUE 'S/NO'.                     
           05  FILLER               PIC X(02) VALUE SPACES.                     
           05  FILLER               PIC X(05) VALUE 'SALES'.                    
           05  FILLER               PIC X(02) VALUE SPACES.                     
           05  FILLER               PIC X(08) VALUE 'REGN  NO'.                 
           05  FILLER               PIC X(02) VALUE SPACES.                     
           05  FILLER               PIC X(11) VALUE 'SCH ACCT NO'.              
           05  FILLER               PIC X(02) VALUE SPACES.                     
           05  FILLER               PIC X(35) VALUE 'FLAT ADDRESS'.             
           05  FILLER               PIC X(02) VALUE SPACES.                     
           05  FILLER               PIC X(16) VALUE 'SALES AGRMT DATE'.         
           05  FILLER               PIC X(04) VALUE SPACES.                     
           05  FILLER               PIC X(14) VALUE 'STAMP FEE PAID'.           
           05  FILLER               PIC X(04) VALUE SPACES.                     
           05  FILLER               PIC X(03) VALUE 'OIC'.                      
                                                                                
       01  PRT-L408-HDG-5.                                                      
           05  FILLER               PIC X(06) VALUE SPACES.                     
           05  FILLER               PIC X(05) VALUE 'TYPE '.                    
           05  FILLER               PIC X(62) VALUE SPACES.                     
           05  FILLER               PIC X(16) VALUE 'SALES ORDER DATE'.         
                                                                                
       01  L408-BLANK-LINE          PIC X(132) VALUE SPACES.                    
                                                                                
       01  L408-UNDER-LINE          PIC X(132) VALUE ALL '-'.                   
                                                                                
       01  PRT-L408-NO-CASE.                                                    
           05  FILLER               PIC X(50)  VALUE SPACES.                    
           05  FILLER               PIC X(31)  VALUE                            
               '*******  N O   C A S E  *******'.                               
                                                                                
       01  PRT-L408-DTL-1.                                                      
           05  L408-SEQ-NO          PIC ZZZ9  VALUE ZEROES.                     
           05  FILLER               PIC X(02) VALUE SPACES.                     
           05  L408-SALE-TYPE       PIC X(02) VALUE SPACES.                     
           05  FILLER               PIC X(05) VALUE SPACES.                     
           05  L408-REGN-NO         PIC X(08) VALUE SPACES.                     
           05  FILLER               PIC X(02) VALUE SPACES.                     
           05  L408-SCH-ACC         PIC X(11) VALUE SPACES.                     
           05  FILLER               PIC X(02) VALUE SPACES.                     
           05  L408-FLAT-ADD1       PIC X(35) VALUE SPACES.                     
           05  FILLER               PIC X(05) VALUE SPACES.                     
           05  L408-SA-DTE          PIC X(10) VALUE SPACES.                     
           05  FILLER               PIC X(05) VALUE SPACES.                     
           05  L408-STAMP-PAID      PIC Z,ZZZ,ZZZ,ZZ9.99 VALUE ZEROS.           
           05  FILLER               PIC X(04) VALUE SPACES.                     
           05  L408-NUM-OIC         PIC X(05) VALUE SPACES.                     
                                                                                
       01  PRT-L408-DTL-2.                                                      
           05  FILLER               PIC X(36) VALUE SPACES.                     
           05  L408-FLAT-ADD2       PIC X(35) VALUE SPACES.                     
           05  FILLER               PIC X(05) VALUE SPACES.                     
           05  L408-SO-DTE          PIC X(10) VALUE SPACES.                     
                                                                                
      *-----------------------------------------------------------*             
      *    OTHER VARIABLES                                        *             
      *-----------------------------------------------------------*             
       01  WS-COUNTERS.                                                         
           05  WS-BP13F410-READ-CNT         PIC 9(6)       VALUE ZEROS.         
           05  WS-BP13F410-PROC-CNT         PIC 9(6)       VALUE ZEROS.         
           05  WS-BP13K006-READ-CNT         PIC 9(6)       VALUE ZEROS.         
           05  WS-BP13K022-READ-CNT         PIC 9(6)       VALUE ZEROS.         
           05  WS-F410-READ-CNT             PIC Z,ZZ,ZZ9   VALUE ZEROS.         
           05  WS-K006-READ-CNT             PIC Z,ZZ,ZZ9   VALUE ZEROS.         
           05  WS-K022-READ-CNT             PIC Z,ZZ,ZZ9   VALUE ZEROS.         
           05  WS-PRINT-CNT                 PIC Z,ZZ,ZZ9   VALUE ZEROS.         
           05  WS-LINE-CNT                  PIC 9(2)       VALUE 99.            
           05  WS-PAGE-CNT                  PIC 9(5)       VALUE ZEROS.         
           05  WS-SEQ-NO                    PIC 9(5)       VALUE ZEROS.         
                                                                                
       01  WS-FILE-STATUS.                                                      
           05  WS-K006-STATUS               PIC 9(2)   VALUE ZEROES.            
           05  WS-K022-STATUS               PIC 9(2)   VALUE ZEROES.            
           05  EOF-F410                     PIC X      VALUE 'N'.               
                                                                                
       01  WS-FUNC-CUR-DTE                  PIC 9(9).                           
       01  WS-FUNC-SRD-DTE                  PIC 9(9).                           
       01  WS-DATE-DIFF                     PIC 9(9).                           
                                                                                
       01  WS-EDIT-DTE.                                                         
           05  WS-EDIT-DD                   PIC X(2).                           
           05  FILLER                       PIC X(1) VALUE '/'.                 
           05  WS-EDIT-MM                   PIC X(2).                           
           05  FILLER                       PIC X(1) VALUE '/'.                 
           05  WS-EDIT-CC                   PIC X(2).                           
           05  WS-EDIT-YY                   PIC X(2).                           
                                                                                
       01  WS-CUR-DATE.                                                         
           05  WS-CUR-CY                    PIC 9(4).                           
           05  WS-CUR-MM                    PIC 9(2).                           
           05  WS-CUR-DD                    PIC 9(2).                           
       01  WS-CUR-DATE-9 REDEFINES                                              
               WS-CUR-DATE                  PIC 9(8).                           
                                                                                
       01  WS-SORD-DATE.                                                        
           05  WS-SORD-CY                   PIC 9(4).                           
           05  WS-SORD-MM                   PIC 9(2).                           
           05  WS-SORD-DD                   PIC 9(2).                           
       01  WS-SORD-DATE-9 REDEFINES                                             
               WS-SORD-DATE                 PIC 9(8).                           
                                                                                
       01  WS-ADDRESS1.                                                         
           05  FILLER                       PIC X(05) VALUE 'BLOCK'.            
           05  WS-BLK-NO                    PIC X(05) VALUE SPACES.             
           05  FILLER                       PIC X(02) VALUE ' ,'.               
           05  WS-ST-NAME                   PIC X(20) VALUE SPACES.             
                                                                                
       01  WS-ADDRESS2.                                                         
           05  FILLER                       PIC X(02) VALUE '# '.               
           05  WS-LEVEL-NO                  PIC X(02) VALUE SPACES.             
           05  FILLER                       PIC X(03) VALUE ' - '.              
           05  WS-MAIN-UNIT                 PIC X(04) VALUE SPACES.             
           05  FILLER                       PIC X(03) VALUE ' , '.              
           05  FILLER                       PIC X(09) VALUE 'SINGAPORE'.        
           05  FILLER                       PIC X(02) VALUE ' ('.               
           05  WS-COUNTRY-POSTCDE           PIC X(06) VALUE SPACES.             
           05  FILLER                       PIC X(01) VALUE ')'.                
                                                                                
       01  WS-STAMP-PAID                    PIC 9(10)V99.                       
       01  WS-DTE-STAMP-AGMT                PIC X(8)  VALUE SPACES.             
                                                                                
       COPY P13COMM8.                                                           
                                                                                
      ***LINKAGE FOR BP13C913*                                                  
       01  WS-LINK-REC.                                                         
           05 WS-LINK-NUM-SCH-ACC.                                              
              10 WS-LINK-NUM-SCH        PIC X(4).                               
              10 WS-LINK-NUM-ACC        PIC X(5).                               
                                                                                
       LINKAGE SECTION.                                                         
                                                                                
       01  PARM-FIELD.                                                          
           05  PARM-LENGTH             PIC S9(04) COMP.                         
           05  PARM-DATA               PIC X(05).                               
                                                                                
                                                                                
      *------------------------------------*                                    
       PROCEDURE DIVISION USING PARM-FIELD.                                     
      *------------------------------------*                                    
      ******************************************************************        
       0000-CONTROL.                                                            
      ******************************************************************        
            PERFORM  0100-SET-DATE               THRU  0100-EXIT.               
            PERFORM  0200-OPEN-FILES             THRU  0200-EXIT.               
            PERFORM  0300-READ-BP13F410          THRU  0300-EXIT                
                          UNTIL EOF-F410 = 'Y'.                                 
            PERFORM  0890-CHK-REPORT-STATUS      THRU  0890-EXIT.               
            PERFORM  900-DISPLAY-STATISTICS      THRU  900-EXIT.                
            PERFORM  999-CLOSE-FILES             THRU  999-EXIT.                
                                                                                
       0000-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0100-SET-DATE.                                                           
      ******************************************************************        
            STRING  FUNCTION CURRENT-DATE(7:2) '/'                              
                    FUNCTION CURRENT-DATE(5:2) '/'                              
                    FUNCTION CURRENT-DATE(1:4)                                  
            DELIMITED BY SIZE INTO L408-CURRENT-DATE.                           
                                                                                
       0100-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0200-OPEN-FILES.                                                         
      ******************************************************************        
            OPEN INPUT  BP13F410                                                
                        BP13K022                                                
                        B05K006                                                 
                 OUTPUT BP13L408.                                               
                                                                                
            IF WS-K022-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING ERROR BP13K022 ' WS-K022-STATUS                 
               PERFORM 900-DISPLAY-STATISTICS    THRU  900-EXIT                 
               PERFORM 999-CLOSE-FILES           THRU  999-EXIT                 
            END-IF.                                                             
                                                                                
            IF WS-K006-STATUS NOT = 00 AND 97                                   
               DISPLAY 'OPENING ERROR B05K006  ' WS-K006-STATUS                 
               PERFORM 900-DISPLAY-STATISTICS    THRU  900-EXIT                 
               PERFORM 999-CLOSE-FILES           THRU  999-EXIT                 
            END-IF.                                                             
                                                                                
            PERFORM 0201-CHECK-PARMS             THRU 0201-EXIT.                
            PERFORM 0891-HEADER-EMAIL            THRU 0891-EXIT.                
                                                                                
       0200-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0201-CHECK-PARMS.                                                        
      ******************************************************************        
                                                                                
            EVALUATE PARM-DATA                                                  
            WHEN 'TP'                                                           
               MOVE 'BP13L408A' TO   L408-REPORT-ID                             
            WHEN 'SA-TP'                                                        
               MOVE 'BP13L408B' TO   L408-REPORT-ID                             
            WHEN 'DBSS'                                                         
               MOVE 'BP13L408C' TO   L408-REPORT-ID                             
            END-EVALUATE.                                                       
                                                                                
            MOVE SPACES         TO   WS-MAIL-RPT-SUBJ.                          
            STRING '(' PARM-DATA ')' DELIMITED BY SPACE                         
              INTO WS-MAIL-RPT-SUBJ.                                            
                                                                                
            STRING PARM-DATA DELIMITED BY SPACE                                 
                   ' ) > 1 MONTH (NON-BANK CASES)' DELIMITED BY SIZE            
              INTO PRT-L408-HDG-TITLE.                                          
                                                                                
       0201-EXIT.                                                               
            EXIT.                                                               
                                                                                
      ******************************************************************        
       0300-READ-BP13F410.                                                      
      ******************************************************************        
            READ BP13F410                                                       
              AT END                                                            
                 MOVE    'Y'  TO EOF-F410                                       
                 GO TO   0300-EXIT.                                             
                                                                                
            ADD 1 TO WS-BP13F410-READ-CNT.                                      
                                                                                
      *------------------------------------------------------------*            
      * FOR DTE-STAMP-DOA CASES WHICH IS SPACES, THERE IS A                     
      * POSSIBILITY THAT THE STAMPING HAS BEEN DONE MANUALLY.                   
      * TO FIND OUT WHETHER THE STAMPING IS MANUAL, EITHER THE                  
      * DTE-FILE-SENT OR DTE-LEGAL-DOC-SENT WILL HAVE VALUES TO                 
      * DENOTE THAT THE CASE HAS BEEN SENT TO BO.                               
      *------------------------------------------------------------*            
SSD   * TO SURFACE IN TP REPORT WHEN DTE-STAMP-DOA IS BLANK AND                 
SSD   * EXCLUDE CHECKING FOR DTE-FILE-SENT AND DTE-LEGAL-DOC-SENT.              
      *------------------------------------------------------------*            
                                                                                
            IF F410-DTE-STAMP-DOA  = SPACES OR LOW-VALUES OR '00000000'         
               PERFORM 0500-CHECK-DATE  THRU 0500-EXIT                          
            END-IF.                                                             
                                                                                
            ADD 1 TO WS-BP13F410-PROC-CNT.                                      
                                                                                
       0300-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0500-CHECK-DATE.                                                         
      ******************************************************************        
            MOVE   FUNCTION CURRENT-DATE   TO   WS-CUR-DATE.                    
            MOVE   F410-DTE-KEY-ISSUE      TO   WS-SORD-DATE.                   
                                                                                
            COMPUTE  WS-FUNC-CUR-DTE =                                          
                     FUNCTION INTEGER-OF-DATE(WS-CUR-DATE-9).                   
            COMPUTE  WS-FUNC-SRD-DTE =                                          
                     FUNCTION INTEGER-OF-DATE(WS-SORD-DATE-9).                  
                                                                                
            COMPUTE WS-DATE-DIFF = WS-FUNC-CUR-DTE - WS-FUNC-SRD-DTE.           
                                                                                
      ******  THE DIFFERENCE OF 30 DAYS IS TAKEN TO SORT CASES WHOSE            
      *****   KEY ISSUE DATE IS 1 MTH PRIOR TO CURRENT DATE.                    
                                                                                
      *------------------------------------------------------------*            
      * USE 30 DAYS TO DENOTE > 1 MTH.                                          
      *                                                                         
      * FOR OUTRIGHT PURCHASE CASES, IF THERE IS NO PREMIUM REQUIRED,           
      * THERE IS NO NEED TO STAMP. THUS ONLY PROCESS PREMIUM CASE               
      * FOR OUTRIGHT PURCHASE.                                                  
      *------------------------------------------------------------*            
                                                                                
            IF F410-AMT-PREMIUM-STAMP-FEE IS NOT NUMERIC                        
               MOVE ZEROES TO  F410-AMT-PREMIUM-STAMP-FEE                       
            END-IF.                                                             
                                                                                
            IF WS-DATE-DIFF >= 30                                               
               PERFORM 0850-READ-AB05K006 THRU 0850-EXIT                        
               IF WS-STAMP-PAID > 0                                             
                  IF F410-CDE-SA-TP = 'Y'                                       
                     PERFORM 0600-PRINT-LISTING  THRU   0600-EXIT               
                  ELSE                                                          
                     IF F410-AMT-LOAN = ZERO                                    
      *                 IF F410-NUM-PREMIUM-RATE > ZEROES                       
                        IF F410-AMT-PREMIUM-STAMP-FEE > ZEROES                  
                           PERFORM 0600-PRINT-LISTING  THRU   0600-EXIT         
                        ELSE                                                    
                           CONTINUE                                             
                        END-IF                                                  
                     ELSE                                                       
                        PERFORM 0600-PRINT-LISTING  THRU   0600-EXIT            
                     END-IF                                                     
                  END-IF                                                        
               END-IF                                                           
            END-IF.                                                             
                                                                                
       0500-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0600-PRINT-LISTING.                                                      
      ******************************************************************        
            IF WS-LINE-CNT > 50                                                 
               PERFORM  0650-PRINT-HEADING     THRU   0650-EXIT                 
            END-IF.                                                             
                                                                                
            ADD         1                      TO     WS-SEQ-NO.                
            ADD         3                      TO     WS-LINE-CNT.              
            MOVE        WS-SEQ-NO              TO     L408-SEQ-NO.              
            PERFORM     0700-MOVE-DATA         THRU   0700-EXIT.                
            WRITE       BP13L408-REC           FROM   L408-BLANK-LINE.          
            WRITE       BP13L408-REC           FROM   PRT-L408-DTL-1.           
            WRITE       BP13L408-REC           FROM   PRT-L408-DTL-2.           
                                                                                
       0600-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0650-PRINT-HEADING.                                                      
      ******************************************************************        
            ADD    1             TO    WS-PAGE-CNT.                             
            MOVE   WS-PAGE-CNT   TO    L408-PAGE.                               
            MOVE   7             TO    WS-LINE-CNT.                             
            WRITE  BP13L408-REC  FROM  PRT-L408-HDG-1 AFTER PAGE.               
            WRITE  BP13L408-REC  FROM  PRT-L408-HDG-2.                          
            WRITE  BP13L408-REC  FROM  L408-BLANK-LINE.                         
            WRITE  BP13L408-REC  FROM  PRT-L408-HDG-4.                          
            WRITE  BP13L408-REC  FROM  PRT-L408-HDG-5.                          
            WRITE  BP13L408-REC  FROM  L408-UNDER-LINE.                         
                                                                                
       0650-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0700-MOVE-DATA.                                                          
      ******************************************************************        
            PERFORM  0800-GET-ADDRESS        THRU  0800-EXIT.                   
            MOVE     WS-STAMP-PAID           TO    L408-STAMP-PAID.             
            PERFORM  0870-READ-BP13K022      THRU  0870-EXIT.                   
            MOVE     F410-NUM-REGN           TO    L408-REGN-NO.                
            MOVE     F410-NUM-SCH-ACC        TO    L408-SCH-ACC.                
            MOVE     F410-CDE-SALES-TYPE     TO    L408-SALE-TYPE.              
            MOVE     F410-DTE-ARGORDER(1:2)  TO    WS-EDIT-CC.                  
            MOVE     F410-DTE-ARGORDER(3:2)  TO    WS-EDIT-YY.                  
            MOVE     F410-DTE-ARGORDER(5:2)  TO    WS-EDIT-MM.                  
            MOVE     F410-DTE-ARGORDER(7:2)  TO    WS-EDIT-DD.                  
            MOVE     WS-EDIT-DTE             TO    L408-SA-DTE.                 
            MOVE     F410-DTE-KEY-ISSUE(1:2) TO    WS-EDIT-CC.                  
            MOVE     F410-DTE-KEY-ISSUE(3:2) TO    WS-EDIT-YY.                  
            MOVE     F410-DTE-KEY-ISSUE(5:2) TO    WS-EDIT-MM.                  
            MOVE     F410-DTE-KEY-ISSUE(7:2) TO    WS-EDIT-DD.                  
            MOVE     WS-EDIT-DTE             TO    L408-SO-DTE.                 
            MOVE     K022-NUM-OIC            TO    L408-NUM-OIC.                
                                                                                
       0700-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0800-GET-ADDRESS.                                                        
      ******************************************************************        
           MOVE SPACES                 TO BP13COMM8-REC.                        
           INITIALIZE                     BP13COMM8-REC.                        
                                                                                
           MOVE F410-NUM-SCH-ACC       TO WS-LINK-REC.                          
                                                                                
           CALL 'BP13C913' USING WS-LINK-REC,                                   
                                BP13COMM8-REC.                                  
                                                                                
           IF (COMM8-CDE-SYSERR = 0) AND                                        
              (COMM8-NUM-MGMT   NOT = SPACES)                                   
              MOVE COMM8-NUM-BLK         TO WS-BLK-NO                           
              MOVE COMM8-NME-STREET      TO WS-ST-NAME                          
              MOVE COMM8-NUM-UNIT-MAIN   TO WS-MAIN-UNIT                        
              MOVE COMM8-NUM-LEVEL       TO WS-LEVEL-NO                         
              MOVE COMM8-NUM-POSTAL-CODE TO WS-COUNTRY-POSTCDE                  
              MOVE WS-ADDRESS1           TO L408-FLAT-ADD1                      
              MOVE WS-ADDRESS2           TO L408-FLAT-ADD2                      
           ELSE                                                                 
              DISPLAY 'ERROR CALLING BP13C913 '                                 
              DISPLAY 'SQL CODE = ' COMM8-CDE-SYSERR                            
              MOVE SPACES                TO L408-FLAT-ADD1                      
              MOVE SPACES                TO L408-FLAT-ADD2                      
           END-IF.                                                              
                                                                                
       0800-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0850-READ-AB05K006.                                                      
      ******************************************************************        
            MOVE  F410-NUM-SCH-ACC  TO  K006-HDB-REF-SF.                        
                                                                                
            READ B05K006.                                                       
                                                                                
            ADD  1  TO  WS-BP13K006-READ-CNT.                                   
                                                                                
            IF WS-K006-STATUS = 00                                              
               IF K006-BALANCES NOT NUMERIC                                     
                  MOVE ZEROES        TO WS-STAMP-PAID                           
               ELSE                                                             
                  MOVE K006-BALANCES TO WS-STAMP-PAID                           
               END-IF                                                           
            ELSE                                                                
               IF WS-K006-STATUS = 23                                           
                  DISPLAY 'B05K006  REC NOT FOUND : ' K006-HDB-REF-SF           
                  MOVE ZEROES        TO WS-STAMP-PAID                           
               ELSE                                                             
                  DISPLAY 'READ ERROR IN B05K006  '  WS-K006-STATUS             
                  MOVE ZEROES        TO WS-STAMP-PAID                           
               END-IF                                                           
            END-IF.                                                             
                                                                                
       0850-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0870-READ-BP13K022.                                                      
      ******************************************************************        
            MOVE   SPACES          TO   K022-KEY-FLD.                           
            MOVE   F410-NUM-REGN   TO   K022-NUM-REGN.                          
                                                                                
            READ BP13K022 KEY IS K022-NUM-REGN.                                 
                                                                                
            EVALUATE WS-K022-STATUS                                             
               WHEN 00                                                          
               WHEN 02                                                          
                  ADD    1         TO   WS-BP13K022-READ-CNT                    
               WHEN 23                                                          
                  DISPLAY 'BP13K022 REC NOT FOUND : ' K022-NUM-REGN             
               WHEN OTHER                                                       
                  DISPLAY 'BP13K022 - READ ERROR, STATUS '                      
                                                     WS-K022-STATUS             
                  MOVE     WS-K022-STATUS      TO   RETURN-CODE                 
                  PERFORM 999-CLOSE-FILES      THRU 999-EXIT                    
            END-EVALUATE.                                                       
                                                                                
       0870-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       0890-CHK-REPORT-STATUS.                                                  
      ******************************************************************        
           IF WS-SEQ-NO = ZERO                                                  
              PERFORM 0650-PRINT-HEADING THRU 0650-EXIT                         
              WRITE   BP13L408-REC       FROM PRT-L408-NO-CASE AFTER 25         
           END-IF.                                                              
                                                                                
       0890-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *================================================================*        
       0891-HEADER-EMAIL.                                                       
      *================================================================*        
                                                                                
            WRITE BP13L408-REC FROM WS-MAIL-RPT-HELO.                           
            WRITE BP13L408-REC FROM WS-MAIL-RPT-MAIL-FROM.                      
                                                                                
            EVALUATE PARM-DATA                                                  
            WHEN 'TP'                                                           
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-BALA                     
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-NOOR9                    
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-OMH                      
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-SB1                      
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-CL14                     
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-NCJ                      
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-ZSA                      
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-JWS                      
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-CYM3                     
            WHEN 'SA-TP'                                                        
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-BALA                     
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-NOOR9                    
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-OMH                      
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-CL14                     
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-NCJ                      
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-ZSA                      
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-JWS                      
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-SB1                      
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-CYM3                     
            WHEN 'DBSS'                                                         
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-BALA                     
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-NOOR9                    
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-SB1                      
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-CL14                     
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-SS98                     
               WRITE BP13L408-REC FROM WS-MAIL-RCPT-TO-CYM3                     
            END-EVALUATE.                                                       
                                                                                
            WRITE BP13L408-REC FROM WS-MAIL-RPT-DATA.                           
            WRITE BP13L408-REC FROM WS-MAIL-RPT-FROM.                           
                                                                                
            EVALUATE PARM-DATA                                                  
            WHEN 'TP'                                                           
               WRITE BP13L408-REC FROM WS-MAIL-TO-BALA                          
               WRITE BP13L408-REC FROM WS-MAIL-TO-NOOR9                         
               WRITE BP13L408-REC FROM WS-MAIL-TO-OMH                           
               WRITE BP13L408-REC FROM WS-MAIL-TO-SB1                           
               WRITE BP13L408-REC FROM WS-MAIL-TO-CL14                          
               WRITE BP13L408-REC FROM WS-MAIL-TO-NCJ                           
               WRITE BP13L408-REC FROM WS-MAIL-TO-ZSA                           
               WRITE BP13L408-REC FROM WS-MAIL-TO-JWS                           
               WRITE BP13L408-REC FROM WS-MAIL-TO-CYM3                          
            WHEN 'SA-TP'                                                        
               WRITE BP13L408-REC FROM WS-MAIL-TO-BALA                          
               WRITE BP13L408-REC FROM WS-MAIL-TO-NOOR9                         
               WRITE BP13L408-REC FROM WS-MAIL-TO-OMH                           
               WRITE BP13L408-REC FROM WS-MAIL-TO-CL14                          
               WRITE BP13L408-REC FROM WS-MAIL-TO-NCJ                           
               WRITE BP13L408-REC FROM WS-MAIL-TO-ZSA                           
               WRITE BP13L408-REC FROM WS-MAIL-TO-JWS                           
               WRITE BP13L408-REC FROM WS-MAIL-TO-SB1                           
               WRITE BP13L408-REC FROM WS-MAIL-TO-CYM3                          
            WHEN 'DBSS'                                                         
               WRITE BP13L408-REC FROM WS-MAIL-TO-BALA                          
               WRITE BP13L408-REC FROM WS-MAIL-TO-NOOR9                         
               WRITE BP13L408-REC FROM WS-MAIL-TO-SB1                           
               WRITE BP13L408-REC FROM WS-MAIL-TO-CL14                          
               WRITE BP13L408-REC FROM WS-MAIL-TO-SS98                          
               WRITE BP13L408-REC FROM WS-MAIL-TO-CYM3                          
            END-EVALUATE.                                                       
                                                                                
            WRITE BP13L408-REC FROM WS-MAIL-RPT-SUBJECT.                        
            MOVE L408-CURRENT-DATE TO WS-DATE-RPT.                              
            WRITE BP13L408-REC FROM WS-DATE-RPT-DTL.                            
                                                                                
            MOVE SPACES TO  BP13L408-REC.                                       
            WRITE   BP13L408-REC.                                               
                                                                                
       0891-EXIT.                                                               
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       900-DISPLAY-STATISTICS.                                                  
      ******************************************************************        
            MOVE    WS-BP13F410-READ-CNT  TO  WS-F410-READ-CNT.                 
            MOVE    WS-BP13K006-READ-CNT  TO  WS-K006-READ-CNT.                 
            MOVE    WS-BP13K022-READ-CNT  TO  WS-K022-READ-CNT.                 
            MOVE    WS-SEQ-NO             TO  WS-PRINT-CNT.                     
                                                                                
            DISPLAY '************* BP13C408 ******************'.                
            DISPLAY ' RECORDS READ FROM BP13F410  : ' WS-F410-READ-CNT.         
            MOVE    WS-BP13F410-PROC-CNT  TO  WS-F410-READ-CNT.                 
            DISPLAY ' RECORDS PROCESSED FROM F410 : ' WS-F410-READ-CNT.         
            DISPLAY ' RECORDS READ FROM B05K006   : ' WS-K006-READ-CNT.         
            DISPLAY ' RECORDS READ FROM BP13K022  : ' WS-K022-READ-CNT.         
            DISPLAY ' NO OF RECORDS PRINTED       : ' WS-PRINT-CNT.             
                                                                                
       900-EXIT.                                                                
            EXIT.                                                               
                                                                                
                                                                                
      ******************************************************************        
       999-CLOSE-FILES.                                                         
      ******************************************************************        
            CLOSE  BP13F410                                                     
                   BP13K022                                                     
                   B05K006                                                      
                   BP13L408.                                                    
                                                                                
           IF WS-K006-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING B05K006, STAT = ' WS-K006-STATUS           
              MOVE     WS-K006-STATUS      TO  RETURN-CODE                      
           END-IF.                                                              
                                                                                
           IF WS-K022-STATUS NOT = 00 AND 97                                    
              DISPLAY 'ERROR CLOSING BP13K022, STAT = ' WS-K022-STATUS          
              MOVE     WS-K022-STATUS      TO  RETURN-CODE                      
           END-IF.                                                              
                                                                                
            STOP RUN.                                                           
                                                                                
       999-EXIT.                                                                
            EXIT.                                                               
