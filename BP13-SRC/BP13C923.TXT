       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C923.                                                 
      *AUTHOR.        JULITO A. MACAPAGAL.                                      
      *DATE-CREATED.  12/03/2002.                                               
      * ========================================================== *            
      * SYSTEM OF COMMITMENT (BP13)                                *            
      * ========================================================== *            
      *                                                            *            
      *  OBJECTIVE   :                                             *            
      *    EXTRACT RECS WITH DTE RECEIVED <= 3 MTH FROM SYSTEM DTE *            
      *                                                            *            
      *  INPUT FILE(S):                                            *            
      *  1. BP13K517   - VSAM                                      *            
      *  2. SY02F001   - SYSTEM DATE                               *            
      *                                                            *            
      *  OUTPUT FILE(S):                                           *            
      *  1. BP13F517   - SEQUENTIAL FILE                           *            
      *                                                            *            
      * ========================================================== *            
      * CHG REF  BY    ON       DESCRIPTION                        *            
      * -------- ----- -------- -----------                        *            
      * BP132025 JAM1  20020312 NEW PGM                            *            
      * BP132412 SSS2  20031006 CHK K517-DTE-SENT                  *            
      * BP136646 SMR2  20170911 CHANGE INPUT FILE FROM BP13K517 TO *            
      *                         BP13F517 AND OUTPUT FILE FROM      *            
      *                         BP13F517 TO P13F517A               *            
      * ========================================================== *            
                                                                                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
                                                                                
           SELECT BP13F517      ASSIGN        TO BP13F517.                      
                                                                                
           SELECT P13F517A      ASSIGN        TO P13F517A.                      
                                                                                
           SELECT SY02F001      ASSIGN        TO SY02F001.                      
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
                                                                                
       COPY SY02F001.                                                           
                                                                                
       FD  P13F517A                                                             
           RECORD CONTAINS 250 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS STANDARD.                                              
       01  P13F517A-REC                PIC X(250).                              
                                                                                
       FD  BP13F517                                                             
           RECORD CONTAINS 250 CHARACTERS                                       
           BLOCK CONTAINS 0 RECORDS                                             
           RECORDING MODE IS F                                                  
           LABEL RECORDS STANDARD.                                              
       COPY BP13F517.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
      *-------------------------------------------------------------            
       01  WS-FLAGS.                                                            
           05  WS-F517-EOF             PIC X     VALUE 'N'.                     
                                                                                
       01  WS-FILE-COUNTERS.                                                    
           05  WS-F517-READ            PIC 9(6)  VALUE ZEROES.                  
           05  WS-F517-BYPASS          PIC 9(6)  VALUE ZEROES.                  
           05  WS-F517A-WRITE          PIC 9(6)  VALUE ZEROES.                  
           05  WS-F517A-WRIT-ERR       PIC 9(6)  VALUE ZEROES.                  
                                                                                
       01  WS-DATE                     PIC X(8)  VALUE SPACES.                  
                                                                                
       01  WS-VARIABLES.                                                        
           05  WS-BACKDATE-3M.                                                  
               10  WS-BACKDATE-3M-CCYY PIC 9(4).                                
               10  WS-BACKDATE-3M-MM   PIC 9(2).                                
               10  WS-BACKDATE-3M-DD   PIC 9(2).                                
                                                                                
           05  WS-BACKDATE-2M.                                                  
               10  WS-BACKDATE-2M-CCYY PIC 9(4).                                
               10  WS-BACKDATE-2M-MM   PIC 9(2).                                
               10  WS-BACKDATE-2M-DD   PIC 9(2).                                
                                                                                
           05  WS-F001-DTE-CURRENT.                                             
               10  WS-F001-CURR-CCYY   PIC 9(4).                                
               10  WS-F001-CURR-MM     PIC 9(2).                                
               10  WS-F001-CURR-DD     PIC 9(2).                                
                                                                                
                                                                                
       PROCEDURE DIVISION.                                                      
      *-------------------------------------------------------------            
       0000-MAIN-ROUTINE.                                                       
      *-------------------------------------------------------------            
                                                                                
           PERFORM 1000-OPEN-FILES           THRU 1000-EXIT.                    
           PERFORM 1500-READ-SY02F001        THRU 1500-EXIT.                    
           PERFORM 4500-CALC-DATE-3MO-BACK   THRU 4500-EXIT.                    
           PERFORM 4700-CALC-DATE-2MO-BACK   THRU 4700-EXIT.                    
           PERFORM 2500-READ-BP13F517        THRU 2500-EXIT.                    
           PERFORM 3000-PROCESS-RECORD       THRU 3000-EXIT                     
                   UNTIL WS-F517-EOF = 'Y'.                                     
           PERFORM 9999-CLOSE-FILES          THRU 9999-EXIT.                    
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1000-OPEN-FILES.                                                         
      *-------------------------------------------------------------            
                                                                                
           OPEN   INPUT  BP13F517                                               
                         SY02F001                                               
                  OUTPUT P13F517A.                                              
                                                                                
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-DATE.                          
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       1500-READ-SY02F001.                                                      
      *-------------------------------------------------------------            
           READ SY02F001 AT END                                                 
              DISPLAY 'SY02F001 - NO RECORD FOUND'                              
              MOVE 99                    TO   RETURN-CODE                       
              PERFORM 9999-CLOSE-FILES   THRU 9999-EXIT.                        
                                                                                
       1500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2500-READ-BP13F517.                                                      
      *-------------------------------------------------------------            
           READ BP13F517 AT END                                                 
              MOVE 'Y'                   TO WS-F517-EOF                         
              GO TO 2500-EXIT.                                                  
                                                                                
           ADD 1                       TO WS-F517-READ.                         
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       3000-PROCESS-RECORD.                                                     
      *-------------------------------------------------------------            
           IF F517-DTE-RECEIVED IS NOT NUMERIC                                  
              MOVE ZEROES TO F517-DTE-RECEIVED                                  
           END-IF.                                                              
                                                                                
           IF F517-DTE-SENT IS NOT NUMERIC                                      
              MOVE ZEROES TO F517-DTE-SENT                                      
           END-IF.                                                              
                                                                                
           IF F517-DTE-RECEIVED NOT = ZEROES                                    
              IF F517-DTE-RECEIVED(1:6) >= WS-BACKDATE-3M(1:6)                  
                 MOVE BP13F517-REC TO P13F517A-REC                              
                 PERFORM 5000-WRITE-P13F517A THRU 5000-EXIT                     
              ELSE                                                              
                 ADD 1 TO WS-F517-BYPASS                                        
              END-IF                                                            
           ELSE                                                                 
              IF F517-DTE-SENT(1:6) >= WS-BACKDATE-2M(1:6)                      
                 MOVE BP13F517-REC TO P13F517A-REC                              
                 PERFORM 5000-WRITE-P13F517A THRU 5000-EXIT                     
              ELSE                                                              
                 ADD 1 TO WS-F517-BYPASS                                        
              END-IF                                                            
           END-IF.                                                              
                                                                                
           PERFORM 2500-READ-BP13F517   THRU 2500-EXIT.                         
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
      * LOGIC USED TO CALCULATE 3 MONTHS BACK FROM SYSTEM DATE                  
      **************************************************************            
       4500-CALC-DATE-3MO-BACK.                                                 
      *-------------------------------------------------------------            
           MOVE F001-DTE-CURRENT  TO WS-F001-DTE-CURRENT.                       
           MOVE WS-F001-CURR-DD TO WS-BACKDATE-3M-DD.                           
           IF WS-F001-CURR-MM > 03                                              
              COMPUTE WS-BACKDATE-3M-MM = WS-F001-CURR-MM - 3                   
              MOVE WS-F001-CURR-CCYY TO WS-BACKDATE-3M-CCYY                     
           ELSE                                                                 
              COMPUTE WS-BACKDATE-3M-MM = WS-F001-CURR-MM + 9                   
              SUBTRACT 1  FROM WS-F001-CURR-CCYY                                
                        GIVING WS-BACKDATE-3M-CCYY                              
           END-IF.                                                              
                                                                                
       4500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      **************************************************************            
      * LOGIC USED TO CALCULATE 2 MONTHS BACK FROM SYSTEM DATE                  
      **************************************************************            
       4700-CALC-DATE-2MO-BACK.                                                 
      *-------------------------------------------------------------            
           MOVE F001-DTE-CURRENT  TO WS-F001-DTE-CURRENT.                       
           MOVE WS-F001-CURR-DD TO WS-BACKDATE-2M-DD.                           
           IF WS-F001-CURR-MM > 02                                              
              COMPUTE WS-BACKDATE-2M-MM = WS-F001-CURR-MM - 2                   
              MOVE WS-F001-CURR-CCYY TO WS-BACKDATE-2M-CCYY                     
           ELSE                                                                 
              COMPUTE WS-BACKDATE-2M-MM = WS-F001-CURR-MM + 10                  
              SUBTRACT 1  FROM WS-F001-CURR-CCYY                                
                        GIVING WS-BACKDATE-2M-CCYY                              
           END-IF.                                                              
                                                                                
       4700-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       5000-WRITE-P13F517A.                                                     
      *-------------------------------------------------------------            
           WRITE P13F517A-REC.                                                  
           ADD 1 TO WS-F517A-WRITE.                                             
                                                                                
       5000-EXIT.                                                               
           EXIT.                                                                
                                                                                
                                                                                
      *-------------------------------------------------------------            
       9999-CLOSE-FILES.                                                        
      *-------------------------------------------------------------            
                                                                                
           DISPLAY 'BP13C923 CONTROL TOTAL       DTE: ' WS-DATE.                
           DISPLAY '--------------------------------------------'.              
           DISPLAY 'NO OF RECS READ FROM    BP13F517:' WS-F517-READ.            
           DISPLAY 'NO OF RECS WRITTEN  TO  P13F517A:' WS-F517A-WRITE.          
           DISPLAY 'NO OF RECS BYPASSED IN  BP13F517:' WS-F517-BYPASS.          
           DISPLAY 'NO OF RECS W/ WRITE ERR P13F517A:' WS-F517A-WRIT-ERR        
                                                                                
           CLOSE BP13F517                                                       
                 SY02F001                                                       
                 P13F517A.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9999-EXIT.                                                               
           EXIT.                                                                
                                                                                
