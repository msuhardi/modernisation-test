       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13C543.                                                 
       AUTHOR.        NOR.                                                      
       DATE-WRITTEN.  25/061999.                                                
      *****************************************************************         
      *               SYSTEM OF COMMITMENT (BP13)                     *         
      *****************************************************************         
      * OBJECTIVE : TRANSFER BALANCE FLAT SET ASIDE FOR NHH TO UPG    *         
      *             BUT SUBJECT TO UPG QUOTA SET ASIDE 3RD ROUND      *         
      *             BP13APA3                                          *         
      *****************************************************************         
      * CHGE REQ    DATE   BY  DESCRIPTION                            *         
      * -------- -------- ---- -------------------------------------- *         
      * BP130695 19990625 NOR  NEW PGM.                               *         
      * BP130695 19990703 LSB  ONLY IF FLAT-PA3>0 , THEN UPDATE       *         
      * BP135458 20140804 SMR2 EXPAND BP13K712 TO 350                 *         
      *****************************************************************         
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
       SOURCE-COMPUTER.   IBM-3090.                                             
       OBJECT-COMPUTER.   IBM-3090.                                             
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
           SELECT BP13K712       ASSIGN TO BP13K712                             
                                 ORGANIZATION IS INDEXED                        
                                 ACCESS MODE IS SEQUENTIAL                      
                                 RECORD KEY IS K712-KEY-FLD                     
                                 FILE STATUS IS WS-K712-STATUS.                 
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
       FD   BP13K712             RECORD CONTAINS 350 CHARACTERS.                
       COPY BP13K712.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
       01  WS-COUNTERS.                                                         
           05  WS-K712-READ                  PIC 9(05)  VALUE ZEROS.            
           05  WS-K712-BYPASS                PIC 9(05)  VALUE ZEROS.            
           05  WS-K712-UPD                   PIC 9(05)  VALUE ZEROS.            
                                                                                
       01  WS-NH-DIFF                        PIC S9(05) VALUE ZEROS.            
       01  WS-UPG-APA3                       PIC S9(05) VALUE ZEROS.            
       01  WS-K712-STATUS                    PIC 9(02)  VALUE ZEROS.            
                                                                                
       01  WS-DATE-TIME-STAMPED.                                                
           05  WS-DATE-STAMPED.                                                 
               10  WS-DTE-YY                     PIC X(04).                     
               10  WS-DTE-MM                     PIC X(02).                     
               10  WS-DTE-DD                     PIC X(02).                     
           05  WS-TIME-STAMPED                   PIC X(07).                     
                                                                                
       01  WS-RUN-DATE.                                                         
           05  WS-RUN-YY                     PIC X(04).                         
           05  WS-RUN-MM                     PIC X(02).                         
           05  WS-RUN-DD                     PIC X(02).                         
                                                                                
       PROCEDURE DIVISION.                                                      
       0000-MAIN.                                                               
      *----------                                                               
           PERFORM 1000-OPEN-FILES           THRU  1000-EXIT.                   
           PERFORM 2000-READ-BP13K712        THRU  2000-EXIT.                   
           PERFORM 3000-PROCESS              THRU  3000-EXIT                    
             UNTIL K712-KEY-FLD = HIGH-VALUES.                                  
           PERFORM 9000-CLOSE-FILES          THRU  9000-EXIT.                   
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       1000-OPEN-FILES.                                                         
      *----------------                                                         
           OPEN I-O BP13K712.                                                   
                                                                                
           IF WS-K712-STATUS NOT = ZEROS AND 97                                 
              DISPLAY 'ERROR OPENING BP13K712, FILE STATUS('                    
                       WS-K712-STATUS ')'                                       
              MOVE WS-K712-STATUS              TO  RETURN-CODE                  
              PERFORM 9000-CLOSE-FILES       THRU  9000-CLOSE-FILES             
           END-IF.                                                              
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       2000-READ-BP13K712.                                                      
      *-------------------                                                      
           READ BP13K712                                                        
             AT END                                                             
                MOVE HIGH-VALUES               TO  K712-KEY-FLD                 
            NOT AT END                                                          
                ADD  1                         TO  WS-K712-READ                 
           END-READ.                                                            
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       3000-PROCESS.                                                            
      *-------------                                                            
           IF K712-CDE-COHORT = 'PA' AND                                        
              K712-CDE-ALLOC-SCH = 'PA'                                         
              PERFORM 4000-COMPUTATIONS      THRU  4000-EXIT                    
           ELSE                                                                 
              ADD  1                           TO  WS-K712-BYPASS               
           END-IF.                                                              
           PERFORM 2000-READ-BP13K712        THRU  2000-EXIT.                   
                                                                                
       3000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4000-COMPUTATIONS.                                                       
      *------------------                                                       
           IF K712-NUM-FLAT-NH NOT NUMERIC                                      
              MOVE ZEROS                     TO  K712-NUM-FLAT-NH               
           END-IF.                                                              
           IF K712-NUM-REG-NH NOT NUMERIC                                       
              MOVE ZEROS                     TO  K712-NUM-REG-NH                
           END-IF.                                                              
                                                                                
           IF K712-NUM-FLAT-UG-APA3 NOT NUMERIC                                 
              MOVE ZEROS                  TO  K712-NUM-FLAT-UG-APA3             
           END-IF.                                                              
           IF K712-NUM-FLAT-UG NOT NUMERIC                                      
              MOVE ZEROS                  TO  K712-NUM-FLAT-UG                  
           END-IF.                                                              
                                                                                
           IF K712-NUM-FLAT-UG-APA3 > 0                                         
                                                                                
              COMPUTE WS-NH-DIFF = K712-NUM-FLAT-NH - K712-NUM-REG-NH           
              IF WS-NH-DIFF > 0                                                 
                                                                                
                 COMPUTE WS-UPG-APA3 = K712-NUM-FLAT-UG-APA3 -                  
                                    K712-NUM-FLAT-UG                            
                 IF WS-UPG-APA3 > WS-NH-DIFF                                    
                    ADD WS-NH-DIFF         TO  K712-NUM-FLAT-UG                 
                    MOVE K712-NUM-REG-NH   TO  K712-NUM-FLAT-NH                 
                 ELSE                                                           
                    ADD WS-UPG-APA3        TO  K712-NUM-FLAT-UG                 
                    SUBTRACT WS-UPG-APA3 FROM  K712-NUM-FLAT-NH                 
                 END-IF                                                         
                 PERFORM 4010-UPDATE-BP13K712  THRU  4010-EXIT                  
              ELSE                                                              
                 ADD  1                           TO  WS-K712-BYPASS            
              END-IF                                                            
           END-IF.                                                              
                                                                                
       4000-EXIT.                                                               
           EXIT.                                                                
                                                                                
       4010-UPDATE-BP13K712.                                                    
      *---------------------                                                    
           MOVE FUNCTION CURRENT-DATE(1:15) TO  WS-DATE-TIME-STAMPED.           
           MOVE WS-DATE-STAMPED             TO  K712-DTE-UPDATE.                
           MOVE WS-TIME-STAMPED             TO  K712-TME-SYSTEM.                
           MOVE 'P13C543'                   TO  K712-USERID.                    
                                                                                
           REWRITE BP13K712-REC.                                                
           IF WS-K712-STATUS = 00                                               
              ADD 1                     TO  WS-K712-UPD                         
           ELSE                                                                 
              DISPLAY 'ERROR REWRITING BP13K712, FILE STATUS('                  
                      WS-K712-STATUS ')'                                        
           END-IF.                                                              
                                                                                
       4010-EXIT.                                                               
           EXIT.                                                                
                                                                                
       9000-CLOSE-FILES.                                                        
      *-----------------                                                        
           DISPLAY '***************** BP13C543 ***********************'.        
           ACCEPT  WS-RUN-DATE             FROM DATE YYYYMMDD.                  
           DISPLAY ' RUN DATE : ' WS-RUN-DD '/' WS-RUN-MM '/' WS-RUN-YY.        
           DISPLAY SPACES.                                                      
           DISPLAY ' TOTAL NO. OF BP13K712 READ     : ' WS-K712-READ.           
           DISPLAY ' TOTAL NO. OF BP13K712 BYPASS   : ' WS-K712-BYPASS.         
           DISPLAY ' TOTAL NO. OF BP13K712 UPDATED  : ' WS-K712-UPD.            
           DISPLAY '***************** -------- ***********************'.        
                                                                                
           CLOSE BP13K712.                                                      
                                                                                
           STOP RUN.                                                            
                                                                                
       9000-EXIT.                                                               
           EXIT.                                                                
      *                                                                         
