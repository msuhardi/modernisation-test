       IDENTIFICATION DIVISION.                                                 
       PROGRAM-ID.    BP13CRG4.                                                 
       AUTHOR.        CHETANA.                                                  
       DATE-WRITTEN.  18/12/18.                                                 
      * ====================================================== *                
      *    SYSTEM NAME : SYSTEM OF COMMITMENT.                 *                
      *                                                        *                
      *    SYSTEM ID : BP13                                    *                
      *                                                        *                
      *    OBJECTIVE  :                                        *                
      *                                                        *                
      *        THIS IS THE E-REGISTRATION FOR CRG EVENTS       *                
      *                                                        *                
      *    INPUT FILES : BP13K800                              *                
      *                  BP13K820                              *                
      *                  BP13K857                              *                
      *                  BP13F205                              *                
      *                  BM06K110                              *                
      *--------------------------------------------------------*                
      * CHG-NO  BY   DATE     DETAILS                          *                
      * ------  --   ----     -------------------------------- *                
      *BP13XXXX CC37 191218   NEW PROGRAM                      *                
      *BP137495 CC37 270919   ADDED NEW COLUMNS BLDNG_GL & LEVEL                
      *BP137495 SMP3 261119   PASS EVENT REF EVEN NO RECORD IS *                
      *                       FOUND DURING DELETION            *                
      *BP137495 CC37 271119   PASS RESEDENTIAL ADDRESS IF      *                
      *                       MAIL ADDRESS IS NOT FOUND        *                
      *BP137583 KR13 130120   READ BM06K120 INSTEAD OF BM06F120*                
      *BP13XXXX EL27 030420   ADD SQL COND FOR ACTIVE BLKS ONLY*                
      *BP138473 MRR5 231020   ADD NUM-AGE-RANGE                *                
      *BP138473 MRR5 291020   MODIFIED LOGIC FOR NUM-AGE-RANGE *                
      *BP139089 MRR5 291020   CHECKING OF K830-NUM-MAIL-POSTAL *                
      *BP139847 MRR5 080124   NEW COLUMN NAMES FOR EVENT_PTCPNT*                
      *BP139847 MRR5 200224   ADD NUM_FIRST_TIMER_SECOND_TIMER *                
      * ====================================================== *                
       ENVIRONMENT DIVISION.                                                    
       CONFIGURATION SECTION.                                                   
      *--------------------------------------------------------*                
       SOURCE-COMPUTER. IBM-3090.                                               
       OBJECT-COMPUTER. IBM-3090.                                               
                                                                                
       INPUT-OUTPUT SECTION.                                                    
       FILE-CONTROL.                                                            
      *--------------------------------------------------------*                
           SELECT BP13F205 ASSIGN TO BP13F205.                                  
           SELECT BM06K110 ASSIGN TO BM06K110                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K110-KEY-FLD                               
                  ALTERNATE RECORD KEY IS K110-REGN-NO                          
                  ALTERNATE RECORD KEY IS K110-AIX1                             
                  FILE STATUS     IS WS-K110-STATUS.                            
                                                                                
           SELECT BP13K800 ASSIGN TO BP13K800                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K800-NUM-REGN                              
                  FILE STATUS     IS WS-K800-STATUS.                            
                                                                                
           SELECT BP13K857 ASSIGN TO BP13K857                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K857-KEY-FLD                               
                  FILE STATUS     IS WS-K857-STATUS.                            
                                                                                
           SELECT BP13K830 ASSIGN TO BP13K830                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K830-KEY-FLD                               
                  FILE STATUS     IS WS-K830-STATUS.                            
                                                                                
           SELECT BP13K060 ASSIGN TO BP13K060                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS RANDOM                                     
                  RECORD KEY      IS K060-KEY-FLD                               
                  FILE STATUS     IS WS-K060-STATUS.                            
                                                                                
           SELECT BP13K820 ASSIGN TO BP13K820                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K820-KEY-FLD                               
                  FILE STATUS     IS WS-K820-STATUS.                            
                                                                                
           SELECT BM06K120 ASSIGN TO BM06K120                                   
                  ORGANIZATION    IS INDEXED                                    
                  ACCESS MODE     IS DYNAMIC                                    
                  RECORD KEY      IS K120-KEY-FLD                               
                  FILE STATUS     IS WS-K120-STATUS.                            
                                                                                
       DATA DIVISION.                                                           
       FILE SECTION.                                                            
      *--------------------------------------------------------*                
       FD  BP13F205                                                             
           RECORDING MODE  IS  F                                                
           RECORD CONTAINS 80 CHARACTERS.                                       
       COPY BP13F205.                                                           
                                                                                
       FD  BM06K110                                                             
           RECORD CONTAINS 500 CHARACTERS.                                      
       COPY BM06K110.                                                           
                                                                                
       FD  BP13K800                                                             
           RECORD CONTAINS 2000 CHARACTERS.                                     
       COPY BP13K800.                                                           
                                                                                
       FD  BP13K857                                                             
           RECORD CONTAINS 200 CHARACTERS.                                      
       COPY BP13K857.                                                           
                                                                                
       FD  BP13K830                                                             
           RECORD CONTAINS 250 CHARACTERS.                                      
       COPY BP13K830.                                                           
                                                                                
       FD  BP13K060                                                             
           RECORD CONTAINS 25  CHARACTERS.                                      
       COPY BP13K060.                                                           
                                                                                
       FD  BP13K820                                                             
           RECORD CONTAINS 400 CHARACTERS.                                      
       COPY BP13K820.                                                           
                                                                                
       FD  BM06K120                                                             
           RECORD CONTAINS 700 CHARACTERS.                                      
       COPY BM06K120.                                                           
                                                                                
       WORKING-STORAGE SECTION.                                                 
                                                                                
       01  WS-SWITCHES.                                                         
           05 WS-BP13F205-EOF-SW          PIC X(01)  VALUE SPACES.              
              88 C-BP13F205-EOF           VALUE 'Y'.                            
           05 WS-BM06K110-EOF-SW          PIC X(01)  VALUE SPACES.              
              88 C-BM06K110-EOF           VALUE 'Y'.                            
           05 WS-BP13K820-EOF-SW          PIC X(01)  VALUE SPACES.              
              88 C-BP13K820-EOF           VALUE 'Y'.                            
           05 WS-K110-REC-FND-SW          PIC X(01)  VALUE SPACES.              
              88 C-K110-REC-FND           VALUE 'Y'.                            
           05 WS-K820-REC-FND-SW          PIC X(01)  VALUE SPACES.              
              88 C-K820-REC-FND           VALUE 'Y'.                            
           05 WS-K110-FIRST-READ-SW       PIC X(01)  VALUE SPACES.              
              88 C-K110-FIRST-READ        VALUE 'Y'.                            
                                                                                
       01  WS-K800-STATUS                 PIC 99     VALUE ZEROS.               
       01  WS-K110-STATUS                 PIC 99     VALUE ZEROS.               
       01  WS-K857-STATUS                 PIC 99     VALUE ZEROS.               
       01  WS-K830-STATUS                 PIC 99     VALUE ZEROS.               
       01  WS-K820-STATUS                 PIC 99     VALUE ZEROS.               
       01  WS-K060-STATUS                 PIC 99     VALUE ZEROS.               
       01  WS-K120-STATUS                 PIC 99     VALUE ZEROS.               
       01  WS-K800-FND                    PIC X      VALUE SPACES.              
       01  WS-K820-FND                    PIC X      VALUE SPACES.              
       01  WS-K857-FND                    PIC X      VALUE SPACES.              
       01  WS-K830-FND                    PIC X      VALUE SPACES.              
       01  WS-K110-FND                    PIC X      VALUE SPACES.              
       01  WS-F205-READ-CTR               PIC 9(03)  VALUE ZEROS.               
       01  WS-K110-READ-CNT               PIC 9(04)  VALUE ZEROS.               
       01  WS-INSERT-CNT                  PIC 9(04)  VALUE ZEROS.               
       01  WS-DELETE-CNT                  PIC 9(04)  VALUE ZEROS.               
       01  WS-POSTAL-CODE                 PIC X(06)  VALUE SPACES.              
       01  WS-EVENT-REF                   PIC X(12)  VALUE SPACES.              
       01  WS-CNT                         PIC 9(02)  VALUE ZEROES.              
       01  WS-TIMESTAMP                   PIC X(26)  VALUE SPACES.              
       01  WS-EVENT-DELETED               PIC X(12)  VALUE SPACES.              
       01  WS-ETHNIC                      PIC X(01)  VALUE SPACES.              
       01  WS-DTE-BIRTH                   PIC 9(08) VALUE ZEROES.               
       01  WS-DTE-EVENT-START             PIC 9(08) VALUE ZEROES.               
       01  WS-CURR-DATE                   PIC 9(08) VALUE ZEROES.               
       01  WS-COMPUTE-AGE.                                                      
           05  WS-NUM-AGE                   PIC 9(08) VALUE ZEROES.             
           05  WS-AGE REDEFINES WS-NUM-AGE.                                     
               10  WS-AGE-CY                PIC 9(04).                          
               10  WS-AGE-MM                PIC 9(02).                          
               10  WS-AGE-DD                PIC 9(02).                          
                                                                                
       01  WS-AGE-RANGE                   PIC X(02)  VALUE SPACES.              
                                                                                
           EXEC SQL INCLUDE P13VPRFL END-EXEC.                                  
           EXEC SQL INCLUDE P13VPTCN END-EXEC.                                  
           EXEC SQL INCLUDE P13VINTF END-EXEC.                                  
           EXEC SQL INCLUDE SQLCA    END-EXEC.                                  
                                                                                
      *****************************************************************         
       PROCEDURE DIVISION.                                                      
      *****************************************************************         
      *------------------*                                                      
       0000-MAIN-ROUTINE.                                                       
      *------------------*                                                      
           PERFORM 1000-INITIALIZATION THRU 1000-EXIT.                          
           PERFORM 2000-PROCESS-PARA   THRU 2000-EXIT                           
             UNTIL C-BP13F205-EOF.                                              
           PERFORM 3000-CLOSE-FILES.                                            
                                                                                
       0000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       1000-INITIALIZATION.                                                     
      *-------------------*                                                     
           OPEN INPUT  BP13F205                                                 
                       BM06K110                                                 
                       BP13K800                                                 
                       BP13K857                                                 
                       BP13K830                                                 
                       BP13K820                                                 
                       BP13K060                                                 
                       BM06K120                                                 
                                                                                
           IF WS-K110-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BM06K110 ' WS-K110-STATUS                
              MOVE WS-K110-STATUS TO RETURN-CODE                                
              PERFORM 3000-CLOSE-FILES.                                         
                                                                                
           IF WS-K120-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BM06K120 ' WS-K120-STATUS                
              MOVE WS-K120-STATUS TO RETURN-CODE                                
              PERFORM 3000-CLOSE-FILES.                                         
                                                                                
           IF WS-K857-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K857 ' WS-K857-STATUS                
              MOVE WS-K857-STATUS TO RETURN-CODE                                
              PERFORM 3000-CLOSE-FILES.                                         
                                                                                
           IF WS-K830-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K830 ' WS-K830-STATUS                
              MOVE WS-K830-STATUS TO RETURN-CODE                                
              PERFORM 3000-CLOSE-FILES.                                         
                                                                                
           IF WS-K800-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K800 ' WS-K800-STATUS                
              MOVE WS-K800-STATUS TO RETURN-CODE                                
              PERFORM 3000-CLOSE-FILES.                                         
                                                                                
           IF WS-K060-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K060 ' WS-K060-STATUS                
              MOVE WS-K060-STATUS TO RETURN-CODE                                
              PERFORM 3000-CLOSE-FILES.                                         
                                                                                
           IF WS-K820-STATUS NOT = 00 AND 97                                    
              DISPLAY 'OPEN ERROR FILE BP13K820 ' WS-K820-STATUS                
              MOVE WS-K820-STATUS TO RETURN-CODE                                
              PERFORM 3000-CLOSE-FILES.                                         
                                                                                
           PERFORM 1100-SET-DB2-TIMESTAMP THRU 1100-EXIT.                       
           MOVE FUNCTION CURRENT-DATE(1:8) TO WS-CURR-DATE.                     
                                                                                
       1000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *----------------------------------------------------------------*        
       1100-SET-DB2-TIMESTAMP.                                                  
      *----------------------------------------------------------------*        
            EXEC SQL                                                            
                 SET :WS-TIMESTAMP = CURRENT TIMESTAMP                          
            END-EXEC.                                                           
            IF SQLCODE NOT = 000                                                
               DISPLAY 'SQL ERROR SET DB2 TIMESTAMP'                            
               DISPLAY 'SQL ERROR CODE  : '  SQLCODE                            
               PERFORM 3000-CLOSE-FILES                                         
            END-IF.                                                             
                                                                                
       1100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       2000-PROCESS-PARA.                                                       
      *-----------------*                                                       
           MOVE ZEROES                         TO    WS-CNT                     
           MOVE 'N'                            TO    WS-BM06K110-EOF-SW         
           MOVE 'N'                            TO    WS-K110-FND                
           PERFORM 2100-READ-F205              THRU  2100-EXIT.                 
           IF WS-BP13F205-EOF-SW  NOT = 'Y'                                     
              IF F205-EVENT-REF NOT = WS-EVENT-DELETED                          
                 PERFORM 2600-DELETE-EVENT-PTCPNT THRU  2600-EXIT               
              END-IF                                                            
              PERFORM 2200-GET-ENC             THRU  2200-EXIT                  
              PERFORM 2300-START-K110          THRU  2300-EXIT                  
              PERFORM 2400-PROCESS-OTHER-FILES THRU  2400-EXIT                  
                      UNTIL C-BM06K110-EOF                                      
              IF C-BM06K110-EOF AND WS-CNT NOT = ZERO                           
                 PERFORM 2500-UPDATE-PRFL-TABLE   THRU  2500-EXIT               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2000-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *---------------*                                                         
       2100-READ-F205.                                                          
      *---------------*                                                         
           READ BP13F205                                                        
                AT END MOVE 'Y' TO WS-BP13F205-EOF-SW                           
                       GO  TO 2100-EXIT.                                        
                                                                                
           ADD  1                  TO WS-F205-READ-CTR.                         
                                                                                
       2100-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------*                                                           
       2200-GET-ENC.                                                            
      *-------------*                                                           
           MOVE F205-POSTCODE     TO WS-POSTAL-CODE.                            
                                                                                
           EXEC SQL SELECT NUM_SCHM_ACNT,                                       
                           NUM_POSTAL_CODE,                                     
                           NUM_ESTATE_CIM,                                      
                           NUM_NGHBHD_CIM,                                      
                           NUM_CNTRCT_CIM,                                      
                           NUM_BLK,                                             
                           NUM_BLDNG_GL,                                        
                           NUM_LEVEL                                            
                      INTO :DCLPIDB-INTERFACE.NUM-SCHM-ACNT,                    
                           :DCLPIDB-INTERFACE.NUM-POSTAL-CODE,                  
                           :DCLPIDB-INTERFACE.NUM-ESTATE-CIM,                   
                           :DCLPIDB-INTERFACE.NUM-NGHBHD-CIM,                   
                           :DCLPIDB-INTERFACE.NUM-CNTRCT-CIM,                   
                           :DCLPIDB-INTERFACE.NUM-BLK,                          
                           :DCLPIDB-INTERFACE.NUM-BLDNG-GL,                     
                           :DCLPIDB-INTERFACE.NUM-LEVEL                         
                      FROM PIDB_INTERFACE                                       
                     WHERE NUM_POSTAL_CODE = :WS-POSTAL-CODE                    
                     AND BLDG_CDE_STAT IN ('A','B')                             
                     FETCH FIRST ROW ONLY                                       
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = +100                                                    
              DISPLAY 'POSTAL CODE NOT FOUND. ERROR:' SQLCODE                   
              PERFORM 3000-CLOSE-FILES                                          
           ELSE                                                                 
           IF SQLCODE NOT = 00                                                  
                 DISPLAY 'ERROR WHILE READING PIDB. ERROR:' SQLCODE             
                 PERFORM 3000-CLOSE-FILES.                                      
                                                                                
       2200-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       2300-START-K110.                                                         
      *-----------------*                                                       
           MOVE NUM-ESTATE-CIM   TO K110-ESTATE                                 
           MOVE NUM-NGHBHD-CIM   TO K110-NEIGHBOURHOOD                          
           MOVE NUM-CNTRCT-CIM   TO K110-CONTRACT-NO                            
           MOVE NUM-BLK          TO K110-BLK-NO                                 
                                                                                
           START BM06K110 KEY IS >= K110-AIX1.                                  
           EVALUATE WS-K110-STATUS                                              
              WHEN 00                                                           
                 MOVE 'Y'        TO WS-K110-REC-FND-SW                          
                                                                                
              WHEN 23                                                           
                 MOVE 'N'        TO WS-K110-REC-FND-SW                          
                 DISPLAY 'RECORD NOT FOUND IN BM06K110 '                        
                                                                                
              WHEN OTHER                                                        
                 DISPLAY 'START READ ERROR BM06K110,STATUS '                    
                          WS-K110-STATUS                                        
                 MOVE WS-K110-STATUS        TO RETURN-CODE                      
                 PERFORM 3000-CLOSE-FILES                                       
           END-EVALUATE.                                                        
                                                                                
       2300-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------*                                               
       2400-PROCESS-OTHER-FILES.                                                
      *-------------------------*                                               
           PERFORM 2410-READNEXT-K110 THRU  2410-EXIT                           
           IF WS-K110-FND      = 'Y' AND                                        
              K110-REGN-NO NOT = SPACES AND LOW-VALUES                          
              PERFORM 2420-READ-K800 THRU  2420-EXIT                            
              IF WS-K800-FND = 'Y'                                              
                 PERFORM 2425-DECODE-ETHNIC THRU 2425-EXIT                      
                 PERFORM 2427-READ-K830 THRU  2427-EXIT                         
                 PERFORM 2430-READ-K857 THRU  2430-EXIT                         
                 PERFORM 2432-READ-K120 THRU  2432-EXIT                         
                 PERFORM 2435-START-K820 THRU 2435-EXIT                         
                 PERFORM 2440-READ-K820 THRU  2440-EXIT UNTIL                   
                         WS-BP13K820-EOF-SW = 'Y'                               
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2400-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------*                                                     
       2410-READNEXT-K110.                                                      
      *-------------------*                                                     
           READ BM06K110 NEXT RECORD AT END                                     
                MOVE 'Y'             TO WS-BM06K110-EOF-SW                      
                GO TO 2410-EXIT.                                                
                                                                                
           EVALUATE WS-K110-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                   IF K110-ESTATE        = NUM-ESTATE-CIM AND                   
                      K110-NEIGHBOURHOOD = NUM-NGHBHD-CIM AND                   
                      K110-CONTRACT-NO   = NUM-CNTRCT-CIM AND                   
                      K110-BLK-NO        = NUM-BLK                              
                      MOVE 'Y'       TO WS-K110-FND                             
                      ADD 1          TO WS-K110-READ-CNT                        
                   ELSE                                                         
                      MOVE 'N'       TO WS-K110-FND                             
                      MOVE 'Y'       TO WS-BM06K110-EOF-SW                      
                   END-IF                                                       
                                                                                
              WHEN 10                                                           
                 MOVE 'Y'            TO WS-BM06K110-EOF-SW                      
                                                                                
              WHEN OTHER                                                        
                 DISPLAY 'READNEXT ERROR:BM06K110 STAT ' WS-K110-STATUS         
                 MOVE WS-K110-STATUS        TO RETURN-CODE                      
                 PERFORM 3000-CLOSE-FILES                                       
           END-EVALUATE.                                                        
                                                                                
       2410-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2420-READ-K800.                                                          
      *-------------------------------------------------------------            
           INITIALIZE                            BP13K800-MASTER.               
           MOVE K110-REGN-NO                  TO K800-NUM-REGN.                 
           READ BP13K800.                                                       
           IF WS-K800-STATUS = ZEROES                                           
              MOVE 'Y'                        TO WS-K800-FND                    
           ELSE                                                                 
             IF WS-K800-STATUS = 23                                             
                MOVE 'N'                      TO WS-K800-FND                    
             ELSE                                                               
                DISPLAY 'BP13K800 - READ FILE ERROR'                            
                DISPLAY 'KEY: ' K800-NUM-REGN                                   
                DISPLAY 'ERROR : ' WS-K800-STATUS                               
                PERFORM 3000-CLOSE-FILES                                        
             END-IF                                                             
           END-IF.                                                              
                                                                                
       2420-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2425-DECODE-ETHNIC.                                                      
      *-------------------------------------------------------------            
           INITIALIZE                            BP13K060-REC.                  
           MOVE 09                            TO K060-SERIAL-NO.                
           MOVE K800-NUM-CAT                  TO K060-CODE.                     
                                                                                
           READ BP13K060.                                                       
                                                                                
           IF WS-K060-STATUS = 00                                               
              MOVE K060-DESC(1:1)             TO WS-ETHNIC                      
           ELSE                                                                 
              MOVE   SPACES                   TO WS-ETHNIC                      
           END-IF.                                                              
                                                                                
       2425-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2427-READ-K830.                                                          
      *-------------------------------------------------------------            
           INITIALIZE                            BP13K830-REC.                  
           MOVE K800-NUM-REGN                 TO K830-KEY-FLD.                  
           READ BP13K830.                                                       
           IF WS-K830-STATUS = ZEROES                                           
              MOVE 'Y'                        TO WS-K830-FND                    
           ELSE                                                                 
             IF WS-K830-STATUS = 23                                             
                MOVE 'N'                      TO WS-K830-FND                    
             ELSE                                                               
                DISPLAY 'BP13K830 - READ FILE ERROR'                            
                DISPLAY 'KEY: ' K830-KEY-FLD                                    
                DISPLAY 'ERROR : ' WS-K830-STATUS                               
                PERFORM 3000-CLOSE-FILES                                        
             END-IF                                                             
           END-IF.                                                              
                                                                                
       2427-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2430-READ-K857.                                                          
      *-------------------------------------------------------------            
           INITIALIZE                            BP13K857-REC.                  
           MOVE K110-REGN-NO                  TO K857-NUM-REGN.                 
           READ BP13K857.                                                       
           IF WS-K857-STATUS = ZEROES                                           
              MOVE 'Y'                        TO WS-K857-FND                    
           ELSE                                                                 
             IF WS-K857-STATUS = 23                                             
                MOVE 'N'                      TO WS-K857-FND                    
             ELSE                                                               
                DISPLAY 'BP13K857 - READ FILE ERROR'                            
                DISPLAY 'KEY: ' K857-NUM-REGN                                   
                DISPLAY 'ERROR : ' WS-K857-STATUS                               
                PERFORM 3000-CLOSE-FILES                                        
             END-IF                                                             
           END-IF.                                                              
                                                                                
       2430-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       2432-READ-K120.                                                          
      *-----------------*                                                       
           MOVE SPACES                      TO K120-REC.                        
           INITIALIZE K120-REC.                                                 
                                                                                
           MOVE K110-SCH-ACC-NO             TO K120-SCH-ACC-NO.                 
                                                                                
           READ BM06K120.                                                       
                                                                                
           IF WS-K120-STATUS NOT = ZEROES                                       
              IF WS-K120-STATUS = 23                                            
                 MOVE SPACES                TO K120-LEVEL-NO                    
              ELSE                                                              
                DISPLAY 'BM06K120 - READ FILE ERROR'                            
                DISPLAY 'KEY: ' K110-SCH-ACC-NO                                 
                DISPLAY 'ERROR : ' WS-K120-STATUS                               
                PERFORM 3000-CLOSE-FILES                                        
             END-IF                                                             
           END-IF.                                                              
                                                                                
       2432-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------*                                                       
       2435-START-K820.                                                         
      *-----------------*                                                       
           MOVE SPACES           TO K820-KEY-FLD                                
                                    WS-BP13K820-EOF-SW                          
           MOVE K800-NUM-REGN    TO K820-NUM-REGN                               
                                                                                
           START BP13K820 KEY IS >= K820-KEY-FLD.                               
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
                 MOVE 'Y'        TO WS-K820-REC-FND-SW                          
                                                                                
              WHEN 23                                                           
                 MOVE 'N'        TO WS-K820-REC-FND-SW                          
                 DISPLAY 'RECORD NOT FOUND IN BP13K820 '                        
                                                                                
              WHEN OTHER                                                        
                 DISPLAY 'START READ ERROR BP13K820,STATUS '                    
                          WS-K820-STATUS                                        
                 MOVE WS-K820-STATUS        TO RETURN-CODE                      
                 PERFORM 3000-CLOSE-FILES                                       
           END-EVALUATE.                                                        
                                                                                
       2435-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------            
       2440-READ-K820.                                                          
      *-------------------------------------------------------------            
           READ BP13K820 NEXT RECORD AT END                                     
                MOVE 'Y'             TO WS-BP13K820-EOF-SW                      
                GO TO 2440-EXIT.                                                
                                                                                
           EVALUATE WS-K820-STATUS                                              
              WHEN 00                                                           
              WHEN 02                                                           
                   IF K820-NUM-REGN      = K800-NUM-REGN                        
                      MOVE 'Y'       TO WS-K820-FND                             
                      PERFORM 2450-INSERT-EVENT-PTCPNT THRU 2450-EXIT           
                   ELSE                                                         
                      MOVE 'N'       TO WS-K820-FND                             
                      MOVE 'Y'       TO WS-BP13K820-EOF-SW                      
                   END-IF                                                       
                                                                                
              WHEN 10                                                           
                 MOVE 'Y'            TO WS-BP13K820-EOF-SW                      
                                                                                
              WHEN OTHER                                                        
                 DISPLAY 'READNEXT ERROR:BP13K820 STAT ' WS-K820-STATUS         
                 MOVE WS-K820-STATUS        TO RETURN-CODE                      
                 PERFORM 3000-CLOSE-FILES                                       
           END-EVALUATE.                                                        
                                                                                
                                                                                
       2440-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------*                                               
       2450-INSERT-EVENT-PTCPNT.                                                
      *-------------------------*                                               
           INITIALIZE DCLEVENT-PTCPNT                                           
           MOVE F205-EVENT-REF               TO NUM-EVENT-REF                   
                                             IN DCLEVENT-PTCPNT                 
           MOVE K820-NUM-CSTMR-SOURCE        TO NUM-CSTMR-SOURCE                
           MOVE K110-SCHEME                  TO NUM-SCHEME                      
           MOVE K110-SCH-ACC-NO(5:5)         TO NUM-ACNT                        
           MOVE K110-ACC-TYPE                TO NUM-TNCY                        
           MOVE K820-NUM-HP-PGR              TO NUM-CNTCT                       
           MOVE K820-NME-OCCP                TO NME-APLCNT                      
           IF K857-NUM-EMAIL NOT = SPACES AND LOW-VALUES                        
      *       AND K820-NUM-NRIC = K800-NUM-NRIC1                                
              MOVE K857-NUM-EMAIL            TO TXT-EMAIL-TEXT                  
              MOVE LENGTH OF TXT-EMAIL-TEXT  TO TXT-EMAIL-LEN                   
           END-IF                                                               
           MOVE SPACES                       TO NUM-ATNDC                       
           MOVE SPACES                       TO NUM-DOOR-GIFT                   
           MOVE WS-TIMESTAMP                 TO TME-CREATE                      
                                                                                
           MOVE WS-ETHNIC                    TO NUM-ETHNIC-GROUP                
           EVALUATE K820-NUM-NRIC                                               
               WHEN K800-NUM-NRIC1                                              
                    MOVE 'H1'                TO NUM-APLCNT-TYPE                 
               WHEN K800-NUM-NRIC2                                              
                    MOVE 'H2'                TO NUM-APLCNT-TYPE                 
               WHEN K800-NUM-NRIC3                                              
                    MOVE 'H3'                TO NUM-APLCNT-TYPE                 
               WHEN K800-NUM-NRIC4                                              
                    MOVE 'H4'                TO NUM-APLCNT-TYPE                 
               WHEN OTHER                                                       
                    MOVE 'O '                TO NUM-APLCNT-TYPE                 
           END-EVALUATE.                                                        
                                                                                
           PERFORM 2650-COMPUTE-AGE-RANGE THRU 2650-EXIT.                       
           MOVE WS-AGE-RANGE                 TO NUM-AGE-RANGE.                  
                                                                                
           IF WS-K830-FND = 'Y'                                                 
              IF (K830-NUM-MAIL-ADDR  = SPACES OR LOW-VALUES) OR                
                 ((K830-NUM-MAIL-LVL-CHAR = '#') AND                            
                 (K830-NUM-MAIL-LVL-NUM = SPACES OR LOW-VALUES)) OR             
                 (K830-NUM-MAIL-POSTAL = SPACES OR LOW-VALUES)                  
                 MOVE K830-NUM-BLK(1:10)        TO NUM-CRSPND-BLDNG             
                 MOVE K830-NUM-LVL-NUM          TO NUM-CRSPND-LEVEL             
                 MOVE K830-NUM-HSEMAIN          TO NUM-CRSPND-UNIT              
                 MOVE K830-NME-STREET           TO TXT-CRSPND-ADRS1             
                 MOVE K830-NME-BLDG             TO TXT-CRSPND-ADRS2             
                 MOVE K830-NUM-POSTAL           TO NUM-CRSPND-POSTAL            
              ELSE                                                              
                 MOVE K830-NUM-MAIL-BLK(1:10)   TO NUM-CRSPND-BLDNG             
                 MOVE K830-NUM-MAIL-LVL-NUM     TO NUM-CRSPND-LEVEL             
                 MOVE K830-NUM-MAIL-HSEMAIN     TO NUM-CRSPND-UNIT              
                 MOVE K830-NME-MAIL-STREET      TO TXT-CRSPND-ADRS1             
                 MOVE K830-NME-MAIL-BLDG        TO TXT-CRSPND-ADRS2             
                 MOVE K830-NUM-MAIL-POSTAL      TO NUM-CRSPND-POSTAL            
              END-IF                                                            
           END-IF.                                                              
           MOVE NUM-BLDNG-GL                 IN DCLPIDB-INTERFACE               
             TO NUM-BLDNG-GL                 IN DCLEVENT-PTCPNT                 
           MOVE K120-LEVEL-NO                                                   
             TO NUM-LEVEL                    IN DCLEVENT-PTCPNT                 
                                                                                
           MOVE K800-NUM-HOUSEHOLD           TO NUM-HSHLD-TYPE.                 
           MOVE K800-NUM-NTIMER              TO NUM-TIMER.                      
           MOVE K800-NUM-FT2T         TO NUM-FIRST-TIMER-SECOND-TIMER.          
                                                                                
           EXEC SQL                                                             
                INSERT INTO EVENT_PTCPNT                                        
                (NUM_EVENT_REF                                                  
                ,NUM_CSTMR_SOURCE                                               
                ,NUM_SCHEME                                                     
                ,NUM_ACNT                                                       
                ,NUM_TNCY                                                       
                ,NUM_CNTCT                                                      
                ,TME_CREATE                                                     
                ,TXT_EMAIL                                                      
                ,NUM_APLCNT_TYPE                                                
                ,NUM_ETHNIC_GROUP                                               
                ,NUM_CRSPND_BLDNG                                               
                ,NUM_CRSPND_LEVEL                                               
                ,NUM_CRSPND_UNIT                                                
                ,TXT_CRSPND_ADRS1                                               
                ,TXT_CRSPND_ADRS2                                               
                ,NUM_CRSPND_POSTAL                                              
                ,NME_APLCNT                                                     
                ,NUM_BLDNG_GL                                                   
                ,NUM_LEVEL                                                      
                ,NUM_AGE_RANGE                                                  
                ,NUM_HSHLD_TYPE                                                 
                ,NUM_TIMER                                                      
                ,NUM_FIRST_TIMER_SECOND_TIMER)                                  
                VALUES                                                          
                (:DCLEVENT-PTCPNT.NUM-EVENT-REF                                 
                ,:DCLEVENT-PTCPNT.NUM-CSTMR-SOURCE                              
                ,:DCLEVENT-PTCPNT.NUM-SCHEME                                    
                ,:DCLEVENT-PTCPNT.NUM-ACNT                                      
                ,:DCLEVENT-PTCPNT.NUM-TNCY                                      
                ,:DCLEVENT-PTCPNT.NUM-CNTCT                                     
                ,:DCLEVENT-PTCPNT.TME-CREATE                                    
                ,:DCLEVENT-PTCPNT.TXT-EMAIL                                     
                ,:DCLEVENT-PTCPNT.NUM-APLCNT-TYPE                               
                ,:DCLEVENT-PTCPNT.NUM-ETHNIC-GROUP                              
                ,:DCLEVENT-PTCPNT.NUM-CRSPND-BLDNG                              
                ,:DCLEVENT-PTCPNT.NUM-CRSPND-LEVEL                              
                ,:DCLEVENT-PTCPNT.NUM-CRSPND-UNIT                               
                ,:DCLEVENT-PTCPNT.TXT-CRSPND-ADRS1                              
                ,:DCLEVENT-PTCPNT.TXT-CRSPND-ADRS2                              
                ,:DCLEVENT-PTCPNT.NUM-CRSPND-POSTAL                             
                ,:DCLEVENT-PTCPNT.NME-APLCNT                                    
                ,:DCLEVENT-PTCPNT.NUM-BLDNG-GL                                  
                ,:DCLEVENT-PTCPNT.NUM-LEVEL                                     
                ,:DCLEVENT-PTCPNT.NUM-AGE-RANGE                                 
                ,:DCLEVENT-PTCPNT.NUM-HSHLD-TYPE                                
                ,:DCLEVENT-PTCPNT.NUM-TIMER                                     
                ,:DCLEVENT-PTCPNT.NUM-FIRST-TIMER-SECOND-TIMER)                 
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = 00                                                      
              ADD 1 TO WS-INSERT-CNT                                            
              ADD 1 TO WS-CNT                                                   
           ELSE                                                                 
              IF SQLCODE = -803                                                 
                 DISPLAY 'INSERT NOT ALLOWED FOR ' K110-SCH-ACC-NO              
                         '.SQLCODE:' SQLCODE ' CUST:' NUM-CSTMR-SOURCE          
              ELSE                                                              
                 DISPLAY 'ERROR WHILE INSERTING. SQLCODE:' SQLCODE              
                 PERFORM 3000-CLOSE-FILES                                       
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2450-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-----------------------*                                                 
       2500-UPDATE-PRFL-TABLE.                                                  
      *-----------------------*                                                 
           MOVE F205-EVENT-REF   TO WS-EVENT-REF                                
           EXEC SQL                                                             
                UPDATE EVENT_PRFL                                               
                SET    NUM_INTRFC_STATUS = '1'                                  
                WHERE  NUM_EVENT_REF = :WS-EVENT-REF                            
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = ZERO                                                    
              CONTINUE                                                          
           ELSE                                                                 
              IF SQLCODE = +100                                                 
                 DISPLAY 'EVENT:' WS-EVENT-REF 'IS NOT PRESENT IN TABLE'        
                         ' EVENT_PRFL'                                          
              ELSE                                                              
                 DISPLAY 'EVENT_PRFL TABLE NOT UPDATED.SQLCODE:' SQLCODE        
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2500-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------*                                               
       2600-DELETE-EVENT-PTCPNT.                                                
      *-------------------------*                                               
           MOVE F205-EVENT-REF   TO WS-EVENT-REF                                
                                    WS-EVENT-DELETED                            
                                                                                
           EXEC SQL                                                             
                DELETE FROM EVENT_PTCPNT                                        
                WHERE  NUM_EVENT_REF = :WS-EVENT-REF                            
           END-EXEC.                                                            
                                                                                
           IF SQLCODE = ZERO                                                    
              ADD  1              TO WS-DELETE-CNT                              
           ELSE                                                                 
              IF SQLCODE = +100                                                 
                 CONTINUE                                                       
      *          DISPLAY 'EVENT:' WS-EVENT-REF 'IS NOT PRESENT IN TABLE'        
      *                  ' EVENT_PTCPNT'                                        
              ELSE                                                              
                 DISPLAY 'EVENT_PTCPNT TABLE NOT DELETED.CODE:' SQLCODE         
              END-IF                                                            
           END-IF.                                                              
                                                                                
       2600-EXIT.                                                               
           EXIT.                                                                
                                                                                
      *-------------------------------------------------------------*           
       2650-COMPUTE-AGE-RANGE.                                                  
      *-------------------------------------------------------------*           
           MOVE ZEROES TO WS-NUM-AGE, WS-DTE-BIRTH.                             
           MOVE SPACES TO WS-AGE-RANGE.                                         
                                                                                
      *** F205-DTE-RESELECTION IS USED TO STORE EVENT-START-DATE                
                                                                                
           IF F205-DTE-RESELECTION NOT NUMERIC                                  
              MOVE WS-CURR-DATE                   TO WS-DTE-EVENT-START         
           ELSE                                                                 
              MOVE F205-DTE-RESELECTION           TO WS-DTE-EVENT-START         
           END-IF.                                                              
                                                                                
           IF K820-DTE-BIRTH NOT NUMERIC                                        
              MOVE ZEROES            TO WS-DTE-BIRTH                            
           ELSE                                                                 
              MOVE K820-DTE-BIRTH    TO WS-DTE-BIRTH                            
           END-IF.                                                              
                                                                                
           IF WS-DTE-EVENT-START > WS-DTE-BIRTH                                 
              COMPUTE WS-NUM-AGE = WS-DTE-EVENT-START - WS-DTE-BIRTH            
           ELSE                                                                 
              COMPUTE WS-NUM-AGE = 0                                            
           END-IF.                                                              
                                                                                
           IF WS-AGE-MM > 0                                                     
              COMPUTE WS-AGE-CY = WS-AGE-CY + 1                                 
           END-IF.                                                              
                                                                                
           IF WS-AGE-CY <= 19                                                   
              MOVE '1' TO WS-AGE-RANGE                                          
           END-IF.                                                              
                                                                                
           IF WS-AGE-CY >= 20 AND WS-AGE-CY <= 24                               
              MOVE '2' TO WS-AGE-RANGE                                          
           END-IF.                                                              
                                                                                
           IF WS-AGE-CY >= 25 AND WS-AGE-CY <= 29                               
              MOVE '3' TO WS-AGE-RANGE                                          
           END-IF.                                                              
                                                                                
           IF WS-AGE-CY >= 30 AND WS-AGE-CY <= 34                               
              MOVE '4' TO WS-AGE-RANGE                                          
           END-IF.                                                              
                                                                                
           IF WS-AGE-CY >= 35 AND WS-AGE-CY <= 39                               
              MOVE '5' TO WS-AGE-RANGE                                          
           END-IF.                                                              
                                                                                
           IF WS-AGE-CY >= 40 AND WS-AGE-CY <= 44                               
              MOVE '6' TO WS-AGE-RANGE                                          
           END-IF.                                                              
                                                                                
           IF WS-AGE-CY >= 45 AND WS-AGE-CY <= 49                               
              MOVE '7' TO WS-AGE-RANGE                                          
           END-IF.                                                              
                                                                                
           IF WS-AGE-CY >= 50 AND WS-AGE-CY <= 54                               
              MOVE '8' TO WS-AGE-RANGE                                          
           END-IF.                                                              
                                                                                
           IF WS-AGE-CY >= 55 AND WS-AGE-CY <= 59                               
              MOVE '9' TO WS-AGE-RANGE                                          
           END-IF.                                                              
                                                                                
           IF WS-AGE-CY >= 60 AND WS-AGE-CY <= 64                               
              MOVE '10' TO WS-AGE-RANGE                                         
           END-IF.                                                              
                                                                                
           IF WS-AGE-CY >= 65 AND WS-AGE-CY <= 69                               
              MOVE '11' TO WS-AGE-RANGE                                         
           END-IF.                                                              
                                                                                
           IF WS-AGE-CY >= 70 AND WS-AGE-CY <= 74                               
              MOVE '12' TO WS-AGE-RANGE                                         
           END-IF.                                                              
                                                                                
           IF WS-AGE-CY >= 75 AND WS-AGE-CY <= 79                               
              MOVE '13' TO WS-AGE-RANGE                                         
           END-IF.                                                              
                                                                                
           IF WS-AGE-CY >= 80                                                   
              MOVE '14' TO WS-AGE-RANGE                                         
           END-IF.                                                              
                                                                                
       2650-EXIT.                                                               
           EXIT.                                                                
      *-----------------*                                                       
       3000-CLOSE-FILES.                                                        
      *-----------------*                                                       
           CLOSE BP13F205                                                       
                 BM06K110                                                       
                 BP13K820                                                       
                 BP13K800                                                       
                 BP13K857                                                       
                 BP13K060                                                       
                 BP13K830                                                       
                 BM06K120.                                                      
                                                                                
           DISPLAY SPACES.                                                      
           DISPLAY '*===========END OF BP13CRG4============*'.                  
           DISPLAY SPACES.                                                      
           DISPLAY '   NO. OF F205 RECORDS READ = ' WS-F205-READ-CTR.           
           DISPLAY '   NO. OF K110 RECORDS READ = ' WS-K110-READ-CNT.           
           DISPLAY '   NO. OF INSERT INTO TABLE = ' WS-INSERT-CNT.              
           DISPLAY '   NO. OF EVENTS DELETED    = ' WS-DELETE-CNT.              
           DISPLAY SPACES.                                                      
           DISPLAY '*======================================*'.                  
                                                                                
           IF WS-K110-STATUS NOT = 00                                           
              DISPLAY 'FILE BM06K110  CLOSE ERROR ' WS-K110-STATUS.             
                                                                                
           IF WS-K120-STATUS NOT = 00                                           
              DISPLAY 'FILE BM06K120  CLOSE ERROR ' WS-K120-STATUS.             
                                                                                
           IF WS-K857-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K857  CLOSE ERROR ' WS-K857-STATUS.             
                                                                                
           IF WS-K830-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K830  CLOSE ERROR ' WS-K830-STATUS.             
                                                                                
           IF WS-K060-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K060  CLOSE ERROR ' WS-K060-STATUS.             
                                                                                
           IF WS-K800-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K800  CLOSE ERROR ' WS-K800-STATUS.             
                                                                                
           IF WS-K820-STATUS NOT = 00                                           
              DISPLAY 'FILE BP13K820  CLOSE ERROR ' WS-K820-STATUS.             
                                                                                
           STOP RUN.                                                            
                                                                                
